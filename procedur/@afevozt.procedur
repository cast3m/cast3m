$$$$ @AFEVOZT
* @AFEVOZT  PROCEDUR  GF238795  15/07/02    21:15:00     8565
'DEBPROC' @AFEVOZT;
  'ARGUMENT' MOSENS / 'MOT'; 'ARGUMENT' RAPDEM / 'FLOTTANT';
  'ARGUMENT' TABEVO * 'TABLE';
  'SI' ('EGA' MOSENS 'HORIZONTAL');
    VASENS = 'HORIZONTAL';
  'SINON';
    VASENS = 'VERTICAL';
  'FINSI';
  'SI' ('EGA' ('TYPE' RAPDEM) 'FLOTTANT');
    RAPFO = RAPDEM;
  'SINON';
    RAPFO = 1.0;
  'FINSI';
  AVALEM = 'VALE' 'ELEM'; 'OPTI' 'ELEM' 'SEG2';
  LITP = TABEVO . 'TEMPS';
  LISPOI = 'VIDE' 'MAILLAGE'; PPCHPO = 'VIDE' 'CHPOINT';
  MINT = 'MINI' LITP; MAXT = 'MAXI' LITP; MAXZ = -1E31; MINZ = 1E31;
  'REPETER' ITPS ('DIME' LITP);
    LIZ = 'EXTR' ('EXTR' TABEVO . &ITPS 'COUR' 1 ) 'ABSC';
    MAXZ = 'MAXI' (LIZ 'ET' MAXZ); MINZ = 'MINI' (LIZ 'ET' MINZ);
  'FIN' ITPS;
  'REPETER' ITPS ('DIME' LITP);
    TPCOUR = 'EXTR' LITP &ITPS;
    TPCOUR = MINT '+' (TPCOUR '-' MINT) '*' (RAPFO '*' (MAXZ '-' MINZ)
    '/' (MAXT '-' MINT));
    EVOTZ = 'EXTR' TABEVO . &ITPS 'COUR' 1;
    'SI' ('EGA' VASENS 'VERTICAL');
      LIGCOUR = 'QUEL' 'SEG2' ('PROG' ('DIME' LIZ) * TPCOUR ) LIZ;
      PPCHPO = PPCHPO '+' ('IPOL' ('COOR' 2 LIGCOUR ) EVOTZ );
    'SINON';
      LIGCOUR = 'QUEL' 'SEG2' LIZ ('PROG' ('DIME' LIZ) * TPCOUR );
      PPCHPO = PPCHPO '+' ('IPOL' ('COOR' 1 LIGCOUR ) EVOTZ );
    'FINSI';
    'SI'( 'NON' ( 'EGA' &ITPS 1 ));
      LISPOI = LISPOI 'ET' ('COUT' LIGCOUR LIGANC);
    'FINSI';
    LISPOI = LISPOI 'ET' LIGCOUR; LIGANC = LIGCOUR;
  'FIN' ITPS;
  'MENAGE'; 'OPTION' 'ELEM' AVALEM; 'RESP' LISPOI PPCHPO;
'FINPROC';
$$$$

