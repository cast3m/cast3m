$$$$ AFT2
* AFT2      PROCEDUR  BP208322  16/09/01    21:15:02     9010

************************************************************************
*
* OBJET : Calcul de la force non lineaire frequentiel
*         a partir de son expression temporelle
*
* /!\  TOUS LES ARGUMENTS (SAUF FNL1) SONT PASSES IMPLICITEMENT  /!\
*
************************************************************************

DEBPROC AFT2 ;


*=== CALCUL DE FNL PAR LES LIST_CHPO et TFR ============================

SI IFCHPO2;

  ARGU FNL1*'LISTCHPO';

  FNLFR FNLFI fprog = TFR OTFR FNL1 LISTDT  'REIM' 'ROUG' ;
*
  FNLFR2 = VIDE 'LISTCHPO';
  FNLFI2 = VIDE 'LISTCHPO';
  in = 1;
  FNLFR2= FNLFR2 et (EXTR FNLFR 1);
  FNLFI2= FNLFI2 et (EXTR FNLFI 1);
  REPE Bcoef (nhbm); in = in + 1;
    FNLFR2 = FNLFR2 et ( 2. * (EXTR FNLFR in));
    FNLFI2 = FNLFI2 et (-2. * (EXTR FNLFI in));
  FIN Bcoef;
  ik = 0;
* harmonique 0
  FNLFRE = EXCO (EXTR FNLFR2 1) NOMCOM NOMF0 'NOID' 'NATURE' 'DISCRET';
* harmonique ik
  REPE Btfr nhbm ; ik = ik + 1;
    NOMFc = NHBMF . ik . 'COS';
    NOMFs = NHBMF . ik . 'SIN';
    FNLFRE = FNLFRE +
     (EXCO (EXTR FNLFR2 (ik+1)) NOMCOM NOMFc 'NOID' 'NATURE' 'DISCRET')
   + (EXCO (EXTR FNLFI2 (ik+1)) NOMCOM NOMFs 'NOID' 'NATURE' 'DISCRET');
  FIN Btfr;

SINON;

*=== CALCUL DE FNL PAR LES LISTREELS ===================================
*
  ARGU FNL1*'TABLE';

  FNLFRE = VIDE 'CHPOINT';

*-boucle sur les ddls physique NL (UX, UY, ...) ---------
  REPE Bnl1 (DIME FNL1); ia = &Bnl1;
*   TFR de l'evolution temporelle de cette composante
    EVFNLT = EVOL MANU LISTDT FNL1 . ia;
    EVFNLF = TFR OTFR EVFNLT 'REIM' 'ROUG' ;
    FNLFR = EXTR EVFNLF 'ORDO' 1;
    FNLFI = EXTR EVFNLF 'ORDO' 2;
*   recup du nom de composante, du point support
    icnl  = EXTR COMP_FNL ia;
    icFnl = CHAI 'F' (EXTR icnl 2);
    SI (EGA (TYPE Pnl) 'POINT'); Ps = Pnl;
    SINON;                       Ps = EXTR Pnl ia;
    FINSI;
*---recombinaison pour chaque harmonique ---
    FNLFRE = FNLFRE + ( MANU 'CHPO' Ps 1 ((EXTR FNLFR 1)  )
                   (EXTR COMPF . icFnl 1) 'NATURE' 'DISCRET');
    REPE Bcoef nhbm; ik = &Bcoef;
     FNLFRE = FNLFRE + (MANU CHPO Ps 1 (2. * (EXTR FNLFR (ik + 1)))
                 (EXTR COMPF . icFnl (2*ik)) 'NATURE' 'DISCRET');
     FNLFRE = FNLFRE + (MANU CHPO Ps 1 (-2. * (EXTR FNLFI (ik + 1)))
                 (EXTR COMPF . icFnl (2*ik + 1)) 'NATURE' 'DISCRET');
    FIN Bcoef;
  FIN Bnl1;

FINSI;

FINP FNLFRE;

