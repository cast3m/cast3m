$$$$ @PASHIST
* @PASHIST  PROCEDUR  BP208322  16/05/24    21:15:00     8932
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
'DEBP' @PASHIST ;
*                                                                     *
*------------------------ Lecture des entrees ------------------------*
*                                                                     *
* Modele / Champ a analyser :                                         *
'ARGU' MOD1*'MMODEL' CHAM1*'MCHAML' ;
*                                                                     *
* Nom de la composante :                                              *
'ARGU' LCOMP1/'LISTMOTS' ;
ICOMP1   = 'EXIS' LCOMP1 ;
'SI' ('NON' ICOMP1) ;
  'ARGU' COMP1/'MOT' ;
  ICOMP1   = 'EXIS' COMP1 ;
  'SI' ICOMP1 ;
    LCOMP1   = 'MOTS' COMP1 ;
  'FINS' ;
'FINS' ;
'SI' ICOMP1 ;
  CHAM2    = 'EXCO' CHAM1 LCOMP1 LCOMP1 ;
'SINO' ;
  CHAM2    = CHAM1 ;
'FINS' ;
*                                                                     *
* Visualisation (entree cachee) :                                     *
'ARGU' IVISU1/'LOGIQUE' ;
'SI' ('NON' ('EXIS' IVISU1)) ;
  IVISU1   = FAUX ;
'FINS' ;
*                                                                     *
*------------------- Intervalles d'Echantillonnage -------------------*
*                                                                     *
XMINI1   = 'MINI' CHAM2 ;
XMAXI1   = 'MAXI' CHAM2 ;
XPLAG1   = XMAXI1 - XMINI1 ;
EPLAG1   = 10. ** ('ENTI' (('LOG' XPLAG1) / ('LOG' 10.))) ;
XMININ1  = XMINI1 / EPLAG1 ;
XPLAGN1  = XPLAG1 / EPLAG1 ;
'SI' (XPLAGN1 '<' 2.) ;
  ICOEF1   = 2 ;
  XPASI1   = 0.2 ;
  XPLAGN2  = 1.
    + (('FLOT' ((('ENTI' ((XPLAGN1 - 1.) * 10.)) / 2) + 1)) * XPASI1) ;
  'SI' (XMINI1 '<' 0.) ;
    XMININ1  = XMININ1 - 0.1 ;
  'FINS' ;
  XMININ2  = ('FLOT' ('ENTI' (XMININ1 * 10.))) * 0.1 ;
'SINO' ;
  'SI' (XPLAGN1 '<' 5.0) ;
    ICOEF1   = 1 ;
    XPASI1   = 0.5 ;
    XPLAGN2  = 1.
    + (('FLOT' ((('ENTI' ((XPLAGN1 - 1.) * 10.)) / 5) + 1)) * XPASI1) ;
    'SI' (XMINI1 '<' 0.) ;
      XMININ1  = XMININ1 - 0.1 ;
    'FINS' ;
    XMININ2  = ('FLOT' ('ENTI' (XMININ1 / (0.5 * XPASI1))))
      * (0.5 * XPASI1) ;
  'SINO' ;
    ICOEF1   = 0 ;
    XPASI1   = 1.0 ;
    XPLAGN2  = 1.
    + (('FLOT' ((('ENTI' ((XPLAGN1 - 1.) * 10.)) / 10) + 1)) * XPASI1) ;
    'SI' (XMINI1 '<' 0.) ;
      XMININ1  = XMININ1 - 1.0 ;
    'FINS' ;
    XMININ2  = 'FLOT' ('ENTI' XMININ1) ;
  'FINS' ;
'FINS' ;
XPASI1   =  XPASI1 * EPLAG1 ;
XMINI2   = XMININ2 * EPLAG1 ;
XPLAG2   = XPLAGN2 * EPLAG1 ;
XMAXI2   =  XMINI2 + XPLAG2 ;
LRE2     = 'PROG' XMINI2 PAS XPASI1 XMAXI2 ;
*                                                                     *
*--------------- Analyse par raffinements successifs  ----------------*
*                                                                     *
LCOEF1   = 'PROG' 0.5 0.4 0.5 ;
*                                                                     *
NB1      = 20 ;
LREI1    = LRE2 ;
DDISTI1  = 'HIST' MOD1 CHAM2 LRE2 ;
LMES1    = 'PROG' ;
LPAS1    = 'PROG' ;
NBOF1    = 0 ;
'REPE' B1 NB1 ;
  ICOEF1   = ICOEF1 + 1 ;
  'SI' (ICOEF1 'EGA' 4) ;
    ICOEF1   = 1 ;
  'FINS' ;
* Raffinement plage d'echantillonnage :                               *
  XCOEF1   = LCOEF1 'EXTR' ICOEF1 ;
  XPASI1   =  XPASI1 * XCOEF1 ;
  LREI1    = 'PROG' XMINI2 PAS XPASI1 XMAXI2 ;
  LPAS1    = LPAS1 'ET' ('PROG' XPASI1) ;
* Distribution sur plage d'echantillonnage raffinee :                 *
  DDISTI2  = 'HIST' MOD1 CHAM2 LREI1 ;
* Interpolation distribution grossiere sur plage d'echantillonnage    *
* raffinee :                                                          *
  'REPE' B11 ('DIME' DDISTI1) ;
    COMP11   = 'EXTR' DDISTI1 'LEGX' &B11 ;
    LFVI11   = 'IPOL' LREI1 ('EXTR' DDISTI1 'COUR' &B11) ;
    DDISTI11 = 'EVOL' 'MANU' 'TYPE' 'HIST' COMP11 LREI1 LFVI11 ;
*   Ieme courbe de DDISTI2 :                                          *
    DDISTI21 = 'EXTR' DDISTI2 'COUR' &B11 ;
*   Mesure Ecarts :                                                   *
    MESI11   = 'MAXI' ('INTG' ((DDISTI21 - DDISTI11) ** 2)) ;
    'SI' (&B11 'EGA' 1) ;
      DDISTI0  = DDISTI11 ;
      MESI1    = MESI11 ;
    'SINO' ;
      DDISTI0  = DDISTI0 'ET' DDISTI11 ;
      'SI' (MESI11 '<' MESI1) ;
        MESI1    = MESI11 ;
      'FINS' ;
    'FINS' ;
  'FIN' B11 ;
  DDISTI1  = DDISTI0 ;
  LMES1    = LMES1 'ET' ('PROG' MESI1) ;
* Visualisation :                                                     *
  'SI' IVISU1 ;
    MOTMESI1 = 'CHAI' 'Mesure ecart L2 = ' MESI1 ;
    'DESS' (DDISTI1 'ET' (DDISTI2 'COUL' 'ROUG')) 'TITR' MOTMESI1 ;
  'FINS' ;
*  Sauvegarde Plage d'echantillonnage :                               *
   'SI' (&B1 'EGA' 1) ;
     MESREF1   = MESI1 ;
   'SINO' ;
     'SI' (MESI1 '<' MESREF1) ;
       MESREF1   = MESI1 ;
       LRE2      = LREI1 ;
     'SINO' ;
       NBOF1     = NBOF1 + 1 ;
     'FINS' ;
   'FINS' ;
* Test sortie boucle :                                                *
  'SI' (NBOF1 '>' 2) ;
   'QUIT' B1 ;
  'SINO' ;
    DDISTI1  = DDISTI2 ;
  'FINS' ;
'FIN' B1 ;
*                                                                     *
*--------------------------- Sortie Et Fin ---------------------------*
*                                                                     *
* Visualisation evolution ecart :                                     *
'SI' IVISU1 ;
  'DESS' ('EVOL' 'MANU' LPAS1 LMES1) 'LOGX' 'TITR' 'Evolution ecart' ;
  'DESS' ('HIST' MOD1 CHAM2 LRE2) 'TITR' 'Echantillonnage propose' ;
'FINS' ;
*                                                                     *
'RESP' LRE2 ;
*                                                                     *
'FINP' ;
*---------------------------------------------------------------------*
* FIN PROCEDURE @PASHIST
$$$$


