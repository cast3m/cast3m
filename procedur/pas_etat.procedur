$$$$ PAS_ETAT
* PAS_ETAT  PROCEDUR  CB215821  18/09/18    21:15:03     9925
'DEBPROC' PAS_ETAT PRECED*'TABLE' TI*'FLOTTANT'   ;
************************************************************************
* Cette procedure cree un MCHAML qui contient l'ETAT au sens
* THERMODYNAMIQUE à un instant donne (TI)
*
* Partie 1 : Interpolation entre le debut du PAS (CONTINUATION)
*                             et la fin   du PAS (ESTIMATION  )
* Partie 2 : Tiree dans le CHARGEMENT
* on  interpole entre le dernier pas (dans la table estimation)
* et les dernieres valeurs convergees (dans continuation).
*
* CB215821 : Pour simplifier PASAPAS, on calcule
*    - ETAT_REF : Etat a T=TEMPERATURE_REFERENCE (WTAB.'TEMPERATURE_REFERENCE')
*    - ETAT1    : Etat a T=Tmoyen (combinaison linaire (A*Tdebut_pas) + ((1-A)*T
*
************************************************************************
WTAB = PRECED. 'WTABLE';

CONTI = PRECED. 'CONTINUATION';
ESTIM = PRECED. 'ESTIMATION'  ;
NCONT = 'DIME' CONTI          ;

IND   = 'INDEX' CONTI         ;
TDEB  =  CONTI.'TEMPS'        ;
TFIN  =  ESTIM.'TEMPS'        ;
IAD   =  FAUX                 ;
IAC   =  FAUX                 ;

'SI' ( 'NEG' TFIN TDEB)                       ;
   XPRO  = (TI  '-' TDEB) '/' (TFIN '-' TDEB) ;
   UNXPRO= 1.D0 '-' XPRO                      ;
   IAD   = ('ABS' UNXPRO) '>' 1.D-15          ;
   IAC   = ('ABS' XPRO  ) '>' 1.D-15          ;
'FINS'                                        ;

* Construction du LISTENTI des ETATS a traiter
NBETAT = 0;
LISTETAT = LECT;
'SI' WTAB.'FOR_MECA' ;
  'SI' (WTAB.'CHAR_THE' 'OU'  WTAB.'FOR_THER' );
    'SI' (('EGA' WTAB.'ETAT_REF' ('MOT' 'INCONNU'))    'OU'
          ('NEG' WTAB.'MOD_REF' WTAB.'MO_TOT'   ));
       LISTETAT = LISTETAT 'ET' -1      ;
       NBETAT   = NBETAT '+' 1            ;
*      Tout les indices de REFERENCE sont remis a 'INCONNU'
       WTAB.'ETAT_REF' ='MOT' 'INCONNU' ;
       WTAB.'MAT_REF'  ='MOT' 'INCONNU' ;
       WTAB.'ETREF'    ='MOT' 'INCONNU' ;
       WTAB.'MOD_REF'  ='MOT' 'INCONNU' ;
    'FINS';
  'FINS';
'FINS';

* Evaluation de l'ETAT courant
LISTETAT = LISTETAT 'ET' 1 ;
NBETAT   = NBETAT '+' 1;

'REPE' IETAT NBETAT;
   JETAT = 'EXTR' LISTETAT &IETAT;
*  CB215821: 21/07/2015
*            Utilisation d'un VIDE 'MCHAML' pour MCH au lieu de séparer
*            le cas du premier appel des autres.
   MCH   = 'VIDE'  'MCHAML'      ;

  'REPE' BOU NCONT;
     BOOL1 = FAUX;
     MO    ='MOT' IND. &BOU ;
*     pas decide si grave d'avoir les reactions !!!! ni les temperatures ni les
     'SI' ( 'EGA' MO 'GEOFIN')                   ; 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'GEODEB')                   ; 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'REACTIONS')                ; 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'DEFORMATIONS_INELASTIQUES'); 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'DEFORMATIONS')             ; 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'VITESSES')                 ; 'ITER' BOU;'FINSI';
*    'SI' ( 'EGA' MO 'CARA_ACTU')                ; 'ITER' BOU;'FINSI';
     'SI' ( 'EGA' MO 'PROPORTIONS_PHASES')       ; 'ITER' BOU;'FINSI';

      TYPMO ='TYPE' CONTI.MO ;

     'SI' (('NEG' TYPMO 'ENTIER') 'ET' ('NEG' TYPMO 'FLOTTANT') 'ET' ('NEG' TYPMO 'MCHAML')  'ET' ('NEG' TYPMO 'CHPOINT'));
*       On ne traite que les types qui nous plaisent :)
        'ITER' BOU ;
     'FINS';

     'SI' (('EGA' MO 'TEMPERATURES') 'ET' (JETAT 'EGA' -1));
*       On utilise TEMPERATURE_REFERENCE au lieu de la TEMPERATURE
        VAD = WTAB.'TEMPERATURE_REFERENCE';
        MO   = 'T' ;

     'SINO';
       'SI' IAD;
          'SI' IAC ;
             VAD =(CONTI.MO '*' UNXPRO) '+' (ESTIM.MO '*' XPRO) ;
          'SINO';
             VAD = CONTI.MO;
          'FINS';
       'SINON' ;
          VAD = ESTIM. MO;
       'FINSI';
     'FINS';

     'SI' (( 'EGA' ( 'TYPE' VAD) ('MOT' 'FLOTTANT')) 'OU'
           ( 'EGA' ( 'TYPE' VAD) ('MOT' 'ENTIER'  )));
        BOOL1 = VRAI;
     'FINSI';

     'SI' (BOOL1);
        MCH = MCH 'ADET' WTAB. 'MO_TOTAL'  MO VAD ;
     'SINON';
       'SI' ( 'EGA' ( 'TYPE' VAD) ('MOT' 'MCHAML'));
          MCH= 'ADET' MCH WTAB.'MO_TOTAL' (REDU VAD WTAB.'MO_TOTAL');
       'SINON';
          MCH= 'ADET' MCH WTAB.'MO_TOTAL'       VAD ;
       'FINSI';
     'FINS';
  'FIN' BOU;
* --------------------------------------------------------------
* Debut Calcul de l etat pour le Tracking dans le cas du modele PBFEM
* --------------------------------------------------------------

* Switch selon la methode de tracking si existence de PBFEM avec option
* Tracking active
'SI' ('EXIS' PRECED.'MODELE' 'MATE' 'EFEM');
'SI' ('EXIS' PRECED 'TRACKING');

    'SI' (PRECED.'TRACKING');

* Cas du tracking global (Oliver, 2002) - determination de l etat
    'SI' ('EGA' PRECED.'TYP_TRAC' 'fixed_HC');
      MCH_TRA = FIXHC PRECED;
    'SINON';

* Cas du tracking global - diffusion-convection isotrope - determination de l et
      'SI' ('EGA' PRECED.'TYP_TRAC' 'variable_HC');
         MCH_TRA = VARIHC PRECED;
      'SINON';

* Cas du tracking global - diffusion-convection anisotrope - determination de l
         'SI' ('EGA' PRECED.'TYP_TRAC' 'variable_HCD_SU');
            MCH_TRA = VARIHCSU PRECED;
         'FINSI';

      'FINSI';
      'FINSI';

* On rempli MCH pour ensuite realiser l instanciation dans PAS_MATE
       MCH= 'ADET' MCH WTAB.'MO_TOTAL' MCH_TRA;
    'FINSI';

'FINSI';
'FINSI';

* --------------------------------------------------------------
* Fin Calcul de l etat pour le Tracking dans le cas du modele PBFEM
* --------------------------------------------------------------

* Ajout du CHARGEMENT a la fin seulement sinon l'ETAT de REFERENCE est
* calcule avec T issu du CHARGEMENT au lieu de T issu de TEMPERATURE_REFERENCE
  'SI' ('EXIS' WTAB 'CHARGEMENT')                              ;
     MCH = 'ADET' MCH WTAB.'MO_TOTAL' WTAB.'CHARGEMENT' TI     ;
  'FINS'                                                       ;

 PAS_VERM PRECED MCH                     ;

  'SI'   (JETAT EGA  1)                    ;
     ETAT1 = MCH                           ;
  'SINO'                                   ;
     WTAB.'MOD_REF'  = WTAB.'MO_TOT'       ;
     WTAB.'ETAT_REF' = MCH                 ;
     WTAB.'MAT_REF'  = PAS_MATE PRECED MCH ;
     WTAB.'ETAT_REF' ='MOT' 'SUPPRIME'     ;
  'FINS';

'FIN' IETAT;

'FINP' ETAT1;
$$$$


