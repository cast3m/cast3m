$$$$ OUVFISS
* OUVFISS   PROCEDUR  AM        16/04/04    21:15:01     8883

DEBP OUVFISS TAB1*TABLE IPAS/ENTIER OPT1/MOT;
*
* Procédure OUVFISS Christian La Borderie & M. Matallah
* Cette procédure permet de calculer l'ouverture de fissure à
* Partir de la table de calcul PASAPAS si le modèle utilisé est
* Régularisé par la méthode de Hillerborg
* Les résultats sont dans tab1.ouv
*
* Améliorations à apporter :
*     utilisation de l'opérateur taille (à valider sur l'ensemble des élémen
*     identification de l'ouverture normale et du glissement
*
*
* Si vous utilisez cette procédure, vous êtes priés de citer :
* M. Matallah, C La Borderie and O. Maurel
*  'A practical method to estimate crack openings in concrete structures'
*  International Journal for Numerical and Analytical Methods in Geomechanics, 2
*  doi:10.1002/nag.876

*
*
*
  SI ('EGA' OPT1  'TR');
    LTRA=VRAI;
    MESSAGE ' Extraction de la trace de l ouverture de fissure';
  SINON;
    LTRA=FAUX;
    LPRIN=FAUX;
    MESSAGE ' Extraction du tenseur d ouverture de fissure';
    SI ('EGA' OPT1 'PRIN');
      LPRIN=VRAI;
      MESSAGE 'Calcul des valeurs principales';
    FINSI;
  FINSI;
   IDIM=VALE DIME;
   SI ('EGA' IDIM 2);
        MOD1='EXTR' TAB1.MODELE 'ELEM' 'TRI3' 'QUA4' 'TRI6' 'QUA8' ;
   SINON; SI ('EGA' IDIM 3);
        MOD1='EXTR' TAB1.MODELE 'ELEM'
             'CUB8' 'CU20' 'PRI6' 'PR15' 'PYR5' 'PY13' 'TET4' 'TE10';
        SINON;
            ERREUR 'La dimension doit être égale à 2 ou 3';
            QUITTER OUVFISS;
        FINSI;
   FINSI;
   CHAM1='MANU' 'CHML' MOD1 'SCAL' 1. 'TYPE' 'DEFORMATIONS' 'STRESSES';
   CHAM2='INTG' CHAM1 MOD1 'ELEM' 'SCAL';
   CHAM3=('CHAN' 'STRESSES' MOD1 CHAM2) 'CHAN' 'TYPE' 'DEFORMATIONS';
   CHH=CHAM3**(1./(VALE DIME)) ;
   MAT1='REDU' TAB1 . 'CARACTERISTIQUES' MOD1;
   MAILL1='EXTR' MOD1 'MAILL';
   SI (EXISTE IPAS);
     N1=1;
     MESSAGE 'Pour le pas numéro ' IPAS;
   SINON;
      LECT1='INDEX' (TAB1.TEMPS);
      N1='DIME' LECT1;
      MESSAGE 'Pour tous les pas de temps de la table';
   FINSI;
   TAB1.OUV='TABLE';
   REPETER BOU1 N1;
     SI (EXISTE IPAS);
       I=IPAS;
     SINON;
        I=LECT1 . &BOU1;
     FINSI;
     DEP1='REDU' TAB1.DEPLACEMENTS.I MAILL1;
     EPS1='EPSI' TAB1.DEPLACEMENTS.I MOD1;
     SIG1='REDU' TAB1.CONTRAINTES.I MOD1;
     SIG0I='ELAS' EPS1 MOD1 MAT1;
     SIGAN=SIG0I - SIG1;
     OUF0='ELAS' SIGAN MOD1 MAT1;
     LM1=EXTR OUF0 COMP;
     NCOMP=DIME LM1;
     LM2=MOTS;
     REPETER BOUCOMP NCOMP;
       LM2=INSE LM2  &BOUCOMP 'SCAL';
     FIN BOUCOMP;
     OUF1=OUF0 * CHH LM1 LM2 LM1;
     SI LTRA;
       SI (IDIM EGA 2);
          TROUF1=(('EXCO' OUF1 'EPXX' 'SCAL')
             +('EXCO' OUF1 'EPYY' 'SCAL')) ;
       FINSI;
       SI (IDIM EGA 3);
          TROUF1=(('EXCO' OUF1 'EPXX' 'SCAL')
            +('EXCO' OUF1 'EPYY' 'SCAL') +
             ('EXCO' OUF1 'EPZZ' 'SCAL'));
       FINSI;
       TAB1.OUV . I=TROUF1;
     SINON;
      SI LPRIN;
        OUF2=PRIN OUF1 MOD1;
        TAB1.OUV . I=OUF2;
      SINON;
        TAB1.OUV . I=OUF1;
      FINSI;
     FINSI;
     I=I+1;
  FIN BOU1;
FINP;
$$$$


