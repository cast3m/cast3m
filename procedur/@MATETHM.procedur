$$$$ @MATETHM
* @MATETHM  PROCEDUR  FANDEUR   10/12/17    21:18:00     6427
* PROCEDURE @MATETHM
*---------------------------------------------------------------------*
* NOM         : @MATETHM                                              *
*                                                                     *
* DESCRIPTION :                                                       *
*                                                                     *
* SYNTAXE     :                                                       *
*                                                                     *
*                                                                     *
* LANGAGE     : GIBIANE-CAST3M                                        *
* AUTEUR      : S. PASCAL (CEA/DEN/DM2S/SEMT/LM2S)                    *
* COURRIEL    : serge.pascal@cea.fr                                   *
*---------------------------------------------------------------------*
* VERSION    : v1, xx/xx/xx08, version initiale                       *
* HISTORIQUE : v1, xx/xx/xx08, creation                               *
* HISTORIQUE :                                                        *
* HISTORIQUE :                                                        *
*---------------------------------------------------------------------*
* Priere de PRENDRE LE TEMPS de completer les commentaires            *
* en cas de modification de ce sous-programme afin de faciliter       *
* la maintenance !                                                    *
*---------------------------------------------------------------------*
'DEBP' @MATETHM ;
*                                                                     *
'ARGU' MOD1*'MMODEL' CH0*'CHPOINT' ;
*                                                                     *
CH0      = 'REDU' ('EXTR' MOD1 'MAIL') CH0 ;
*                                                                     *
CHPG0    = 'EXCO' CH0 'PG' ;
CHPC0    = 'EXCO' CH0 'PC' ;
CHTK0    = 'EXCO' CH0 'T ' ;
GRCH0    = 'GRAD' MOD1 CH0 ;
*                                                                     *
* LISTMOTS des composantes du gradient :                              *
VDIM1    = 'VALE' 'DIME' ;
'SI' ('EGA' VDIM1 3) ;
  LMGPG1   = 'MOTS' 'PG,X' 'PG,Y' 'PG,Z' ;
  LMGPC1   = 'MOTS' 'PC,X' 'PC,Y' 'PC,Z' ;
  LMGTK1   = 'MOTS' 'T,X' 'T,Y' 'T,Z' ;
  LMSCA1   = 'MOTS' 'SCAL' 'SCAL' 'SCAL' ;
'SINO' ;
  VMOD1    = 'VALE' 'MODE' ;
  'SI' ('EGA' VMOD1 'AXIS') ;
    LMGPG1   = 'MOTS' 'PG,R' 'PG,Z' ;
    LMGPC1   = 'MOTS' 'PC,R' 'PC,Z' ;
    LMGTK1   = 'MOTS' 'T,R' 'T,Z' ;
  'SINO' ;
    LMGPG1   = 'MOTS' 'PG,X' 'PG,Y' ;
    LMGPC1   = 'MOTS' 'PC,X' 'PC,Y' ;
    LMGTK1   = 'MOTS' 'T,X' 'T,Y' ;
  'FINS' ;
  LMSCA1   = 'MOTS' 'SCAL' 'SCAL' ;
'FINS' ;
*                                                                     *
* D'ou les gradients de chaque composante :                           *
GRPG0    = 'EXCO' GRCH0 LMGPG1 ;
GRPC0    = 'EXCO' GRCH0 LMGPC1 ;
GRTK0    = 'EXCO' GRCH0 LMGTK1 ;
*
TC0      = 273.15 ;
CHTC0    = CHTK0 - TC0 ;
CHPGSC0  = CHPG0 'NOMC' 'SCAL' ;
CHPCSC0  = CHPC0 'NOMC' 'SCAL' ;
CHTKSC0  = CHTK0 'NOMC' 'SCAL' ;
CHTCSC0  = CHTC0 'NOMC' 'SCAL' ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                     CARACTERISTIQUES MATERIELLES                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*-------------------- CARACTERISTIQUES CONSTANTES --------------------*
*                                                                     *
* RGP1     : Constante des gaz parfaits (J/mol/K)                     *
* MMGA1    : Masse molaire de l'air (kg/m3)                           *
* MMGW1    : Masse molaire de l'eau (kg/m3)                           *
* RHOS1    : Masse volumique du solide (kg/m3)                        *
* CPW1     : Capacite calorifique a P constante de l'eau (J/kg/K)     *
* CPGW1    : Capacite calorifique a P constante de la vapeur (J/kg/K) *
* CPGA1    : Capacite calorifique a P constante de l'air (J/kg/K)     *
* DHHD1    : Enthalpie de deshydratation (J/kg)                       *
RGP1     = 8.3145 ;
MMGA1    = 28.9645E-3 ;
MMGW1    = 18.0153E-3 ;
RHOS1    = 2625.8 ;
CPW1     = 4181.0 ;
CPGW1    = 1805.0 ;
CPGA1    = 1005.7 ;
DHHD1    = 0.5E6 / 0.24 ;
*                                                                     *
* Variables de travail :
CHTKSCM1 = CHTKSC0 ** -1 ;
FGPGA0   = MMGA1 / RGP1 * CHTKSCM1 ;
FGPGW0   = MMGW1 / RGP1 * CHTKSCM1 ;
CHPGSCM1 = CHPGSC0 ** -1 ;
CHPGSCM2 = CHPGSC0 ** -2 ;
*                                                                     *
*------------------ CAPACITE CALORIFIQUE DU SOLIDE -------------------*
* Ok !                                                                *
* ENTREE                                                              *
*  - Temperature (K)                                                  *
* SORTIE                                                              *
*  - Capacite calorifique a P constante du solide (J/kg/K) : CPS0     *
* PARAMETRE                                                           *
* CPS1     : Capacite calorifique a P constante du solide a 0Â°C       *
* ACHI1    : Coef. de variation lineaire en temperature               *
CPS1     = 940. ;
ACHI1    = 0.35 ;
CPS0     = CPS1 + (ACHI1 * CHTCSC0) ;
*                                                                     *
*---------------------------- SATURATION -----------------------------*
*                                                                     *
* ENTREES                                                             *
* - Temperature (K)                                                   *
* - Pression capillaire (Pa)                                          *
* SORTIES                                                             *
* - Satiration en eau (-) : SW0                                       *
* - Derivee -/- Pc : DSWDPC0                                          *
* - Derivee -/- T  : DSWDT0                                           *
* - Satiration en gaz () : SG0                                        *
* PARAMETRES                                                          *
* @SATURAT : Procedure                                                *
SW0      = @SATURAT CHTKSC0 CHPCSC0 ;
*'MESS' 'SW0 =' ('MINI' SW0) ;
*                                                                     *
* Derivee de la staturation par rapport a Pc : Ok !
DSWDPC0  = (@SATURAT CHTKSC0 (CHPCSC0 + 50.))
         - (@SATURAT CHTKSC0 (CHPCSC0 - 50.)) ;
DSWDPC0  = 0.01 * DSWDPC0 ;
*                                                                     *
* Derivee de la staturation par rapport a T :
DSWDT0   = (@SATURAT (CHTKSC0 + 0.05) CHPCSC0)
         - (@SATURAT (CHTKSC0 - 0.05) CHPCSC0) ;
DSWDT0   = 10. * DSWDT0 ;
*                                                                     *
* Saturation en phase gazeuse : SG0                                   *
SG0      = 1. - SW0 ;
*                                                                     *
*----------------------------- POROSITE ------------------------------*
*                                                                     *
* ENTREE                                                              *
*  - Temperature (Celsius)                                            *
* SORTIES                                                             *
*  - Porosite (-) : N0                                                *
* PARAMETRES                                                          *
* N1       : porosite initiale (a TREF1)                              *
* APN      : coef. de variation avec la temperature                   *
N1       = 0.1368 ;
APN1     = 7.8E-5 ;
*                                                                     *
* Porosite :
N0       = N1 + (APN1 * CHTCSC0) ;
*                                                                     *
* Derivee -/- T :
DNDT1    = APN1 ;
*                                                                     *
*----------------- MASSE VOLUMIQUE DE L'EAU LIQUIDE ------------------*
*                                                                     *
* ENTREES                                                             *
*  - Temperature (Celsius)                                            *
* SORTIES                                                             *
*  - Masse volumique de l'eau (kg/m3) : RHOW0                         *
*  - Derivee -/- T (kg/m3/K) : DRHOWDT0                               *
*  - Derivee -/- Pg ou Pc (kg/m3/Pa) : DRHOWDP1                       *
* PARAMETRES                                                          *
* PAi ,PBi : facteurs polynomiaux :                                   *
*                                                                     *
PA0      =  4.8863E-7 ;
PA1      = -1.6528E-9 ;
PA2      =  1.8621E-12 ;
PA3      =  2.4266E-13 ;
PA4      = -1.5996E-15 ;
PA5      =  3.3703E-18 ;
PB0      =  1.0213E+3 ;
PB1      = -7.7377E-1 ;
PB2      =  8.7696E-3 ;
PB3      = -9.2118E-5 ;
PB4      =  3.3534E-7 ;
PB5      = -4.4034E-10 ;
PAX      = (PA5 * CHTCSC0) + PA4 ;
PAX      = (PAX * CHTCSC0) + PA3 ;
PAX      = (PAX * CHTCSC0) + PA2 ;
PAX      = (PAX * CHTCSC0) + PA1 ;
PAX      = (PAX * CHTCSC0) + PA0 ;
PBX      = (PB5 * CHTCSC0) + PB4 ;
PBX      = (PBX * CHTCSC0) + PB3 ;
PBX      = (PBX * CHTCSC0) + PB2 ;
PBX      = (PBX * CHTCSC0) + PB1 ;
PBX      = (PBX * CHTCSC0) + PB0 ;
*                                                                     *
* La masse volumique de l'eau liquide : Ok !                          *
RHOW0    = (PAX * -1.E7) + PBX ;
*                                                                     *
* Sa derivee -/- T : Ok !
DAX      = (4. * PA4) + (5. * PA5 * CHTCSC0) ;
DAX      = (3. * PA3) + (DAX * CHTCSC0) ;
DAX      = (2. * PA2) + (DAX * CHTCSC0) ;
DAX      =       PA1  + (DAX * CHTCSC0) ;
DBX      = (4. * PB4) + (5. * PB5 * CHTCSC0) ;
DBX      = (3. * PB3) + (DBX * CHTCSC0) ;
DBX      = (2. * PB2) + (DBX * CHTCSC0) ;
DBX      =       PB1  + (DBX * CHTCSC0) ;
DRHOWDT0 = (DAX * -1.E7) + DBX ;
*                                                                     *
* Sa derivee -/- Pg et Pc :                                           *
DRHOWDP1 = 0. ;
*                                                                     *
*------------------- PRESSION DE VAPEUR SATURANTE --------------------*
*                                                                     *
* ENTREE                                                              *
*  - Temperature (K)                                                  *
* SORTIES                                                             *
*  - Pression de vapeur saturante (Pa) : CHPVSAT0                     *
*  - Derivee -/- T (Pa/K) : DPSATDT0                                  *
* PARAMETRES                                                          *
* GCi      : facteurs polynomiaux                                     *
GC1      = -5.8002206E+03 ;
GC2      =  1.3914993E+00 ;
GC3      = -4.8640239E-02 ;
GC4      =  4.1764768E-05 ;
GC5      = -1.4452093E-08 ;
GC6      =  6.5459673E+00 ;
*                                                                     *
* Pression de vapeur saturante :                                      *
CHPVSAT0 = (GC1 * CHTKSCM1) + GC2 + (GC3 * CHTKSC0)
         + (GC4 * (CHTKSC0 ** 2)) + (GC5 * (CHTKSC0 ** 3))
         + (GC6 * ('LOG' CHTKSC0)) ;
CHPVSAT0 = 'EXP' CHPVSAT0 ;
*                                                                     *
* Derivee -/- T :                                                     *
DPSATDT0 = (-1. * GC1 * (CHTKSCM1 ** 2)) + GC3 + (2. * GC4 * CHTKSC0)
         + (3. * GC5 * (CHTKSC0 ** 2)) + (GC6 * CHTKSCM1) ;
DPSATDT0 = DPSATDT0 * CHPVSAT0 ;
*                                                                     *
*---------------------- TAUX DE DESHYDRATATION -----------------------*
*                                                                     *
* ENTREE                                                              *
*  - Temperature (Celcius)                                            *
* SORTIES                                                             *
*  - Taux de deshydratation (kg/s) : MDH0                             *
*  - Derivee -/- T (kg/s/K) : DMDHDT0                                 *
* PARAMETRES                                                          *
* FS1      : facteur stoechiometrique                                 *
* PM1      : parametre prenanyt en compte l'age du beton              *
* GC1      : une constante                                            *
FS1      = 0.4 ;
PM1      = 0.24 ;
GC1      = 200. ;
FTC1     = 'EXP' (-0.004 * (CHTCSC0 - 105.)) ;
DFTC1    = -0.004 * FTC1 ;
FTC1     = 0.5 * PI  * (1. - (2. * FTC1)) ;
DFTC1    = -1. * PI * DFTC1 ;
DFTC1    = 0.5 * ('COS' (180. / PI * FTC1)) * DFTC1 ;
FTC1     = 0.5 * (1. + ('SIN' (180. / PI * FTC1))) ;
*                                                                     *
* Taux de deshydratation : Ok !                                       *
MAQST1   = CHTCSC0 'MASQ' 'SUPE' 105. ;
MDH0     = FS1 * PM1 * GC1 * FTC1 * MAQST1 ;
*                                                                     *
* Derivee -/- T : Ok !                                                *
DMDHDT0  = FS1 * PM1 * GC1 * DFTC1 * MAQST1 ;
*                                                                     *
*--------------------- ENTHALPIE DE VAPORISATION ---------------------*
*                                                                     *
* ENTREE                                                              *
*  - Temperature (K)                                                  *
* SORTIES                                                             *
*  - Enthalpie de vaporisation (J) : HVAP0                            *
*  - Derivee -/- T (J/K) : DHVAPDT1                                   *
* PARAMETRES                                                          *
* TCRI1    : Temperature critique (idem que saturation)               *
* PN1      : exposant                                                 *
* KHVAPT1  : coeff. de proportionalite en temperature                 *
TCRI1    = 647.15 ;
PN1      = 0.38 ;
KHVAPT1  = 2.672E5 ;
DCHTK1   = TCRI1 - CHTKSC0 ;
HVAP0    = KHVAPT1 * (DCHTK1 ** PN1) ;
DHVAPDT1 = -1. * PN1 * HVAP0 * (DCHTK1 ** -1) ;
*                                                                     *
*--------------------- PERMEABILITE INTRINSEQUE ----------------------*
*                                                                     *
* ENTREE                                                              *
*  - Temperature (K)                                                  *
*  - Pression de gaz (Pa)                                             *
* SORTIES                                                             *
*  - Permeabilite intrinseque (---) : KINT0                           *
* PARAMETRES                                                          *
* T0       : Temperature de reference (25 degres Celcius)             *
* P0       : Pression de reference (1 atm.)                           *
* KK0      : Permeabilite a (T0,P0)                                   *
* AT0      : coef. sur temperature                                    *
* AP0      : coef. sur pression                                       *
T0       = 298.15 ;
P0       = 101325. ;
KK0      = 3.2E-18 ;
AT0      = 0.005 ;
AP0      = ('LOG' (5. / 3.)) / ('LOG' 4.) ;
KINT0    = KK0 * ('EXP' (('LOG' 10.) * AT0 * (CHTKSC0 - T0)))
         * ((CHPGSC0 / P0) ** AP0) ;
*                                                                     *
*------------------- PERMEABILITE RELATIVE AU GAZ --------------------*
*                                                                     *
* ENTREE                                                              *
* - Saturation (-) : SW0                                              *
* SORTIE                                                              *
* - Permeabilite relative au gaz (---) : KRG0                         *
* PARAMETRES                                                          *
* SCR1     : Saturation critique                                      *
* AG1      : exposant                                                 *
SCR1     = 1. ;
AG1      = 1. ;
KRG0     = 1. - ((SW0 / SCR1) ** AG1) ;
*                                                                     *
*------------------- PERMEABILITE RELATIVE A L'EAU -------------------*
*                                                                     *
* ENTREE                                                              *
* - Saturation (-) : SW0                                              *
* SORTIE                                                              *
* - Permeabilite relative a l'eau (---) : KRW0                        *
* PARAMETRES                                                          *
* AW1      : exposant                                                 *
AW1      = 6 ;
KRW0     = SW0 ** AW1 ;
*                                                                     *
*------------------------- VISCOSITE DE L'EAU ------------------------*
*                                                                     *
* ENTREE                                                              *
* - Temperature (K)                                                   *
* SORTIE                                                              *
* - Viscosite de l'eau (---) : MUW0                                   *
* PARAMETRES                                                          *
* T0       : Temperature de reference (K)                             *
* ALPHW1   : coef. sur temperature                                    *
* EXPOW1   : exposant
T0       = 229. ;
ALPHGW1  = 0.6612 ;
EXPOW1   = -1.562 ;
MUW0     = ALPHGW1 * ((CHTKSC0 - T0) ** EXPOW1) ;
*                                                                     *
*------------------- VISCOSITE DE LA VAPEUR D'EAU --------------------*
*                                                                     *
* ENTREE                                                              *
* - Temperature (C)                                                   *
* SORTIE                                                              *
* - Viscosite de la vapeur d'eau (---) : MUGW0                        *
* PARAMETRES                                                          *
* MUGW1    : Viscosite a 0 degre Celcius                              *
* ALPHGW1  : coef. sur temperature                                    *
MUGW1    = 8.85E-6 ;
ALPHGW1  = 3.633E-8 ;
MUGW0    = MUGW1 + (ALPHGW1 * CHTCSC0) ;
*                                                                     *
*---------------------- VISCOSITE DE L'AIR SEC -----------------------*
*                                                                     *
* ENTREE                                                              *
* - Temperature (C)                                                   *
* SORTIE                                                              *
* - Viscosite de l'air sec (---) : MUGA0                              *
* PARAMETRES                                                          *
* MUGA1    : Viscosite a 0 degre Celcius                              *
* ALPHGA1  : coef. sur temperature                                    *
* ALPHGA2  : coef. sur temperature au carre                           *
MUGA1    = 17.17E-6 ;
ALPHGA1  = 4.733E-8 ;
ALPHGA2  = -2.222E-11 ;
MUGA0    = MUGA1 + (ALPHGA1 * CHTCSC0) + (ALPHGA2 * (CHTCSC0 ** 2)) ;
*                                                                     *
*-------------- DIFFUSITE DE LA VAPEUR D'EAU DANS l'AIR --------------*
*                                                                     *
* ENTREES                                                             *
* - Temperature (K)                                                   *
* - Pression gazeuse  (Pa)                                            *
* SORTIE                                                              *
* - Diffusivite de la vapeur d'eau dans l'air (---) : DGWGA0          *
* PARAMETRES                                                          *
* T0       : Temperature de reference (0 degre C)                     *
* P0       : Pression e reference (1 atm.)                            *
* DGWGA1   : Diffusite a (T0,P0)                                      *
* ET1      : Exposant sur la temperature                              *
T0       = 273.15 ;
P0       = 101325. ;
DGWGA1   = 2.17E-5 ;
ET1      = 1.88 ;
DGWGA0     = DGWGA1 * (CHPGSCM1 * P0) * ((CHTKSC0 / T0) ** ET1) ;

*------------------------ DIFFUSITE EFFECTIVE ------------------------*
*                                                                     *
* ENTREES                                                             *
* - Saturation (-) : SW0                                              *
* - Porosite (-) : N0                                                 *
* - Diffusivite de la vapeur d'eau dans l'air (---) : DGWGA0          *
* SORTIE                                                              *
* - Diffusivite effective (---) : DEFF0                               *
UNTIERS1 = 1. / 3. ;
TORT0    = (N0 ** UNTIERS1) * (SG0 ** (7. * UNTIERS1)) ;
DEFF0    = SG0 * N0 * TORT0 * DGWGA0 ;
*                                                                     *
*----------------- CONDUCTIVITE THERMIQUE DU SOLIDE ------------------*
*                                                                     *
* ENTREES                                                             *
* - Temperature (K)                                                   *
* SORTIE                                                              *
* - Conductivite thermique du solide (W/m/K) : KS0                    *
* PARAMETRES                                                          *
* T0       : Temperature de reference (25 degres C)                   *
* KS1      : Conductivite thermique a T0                              *
* AKS1     : Coeff. de proportionalite en temperature                 *
T0       = 298. ;
KS1      = 2.1 ;
AKS1     = -1.017E-3 ;
KS0      = KS1 + (AKS1 * (CHTKSC0 - T0)) ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              CALCUL DES TERMES DE LA MATRICE DU SYSTEME             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*---------------------- Quantites intermediaires ---------------------*
*                                                                     *
* CHPGWSC0 : Pression de vapeur d'eau (Pa, issue formule de Kelvin) : *
CHXX     = -1. * FGPGW0 * (RHOW0 ** -1) ;
CHYY     = 'EXP' (CHXX * CHPCSC0) ;
CHPGWSC0 = CHPVSAT0 * CHYY ;
* DPGWDPC0 :  Derivee -/- Pc (-) :                                    *
DPGWDPC0 = CHXX * CHPGWSC0 ;
* DPGWDT0  : Derivee -/- T (Pa/K) :                                   *
DPGWDT0  = (DPSATDT0 * CHYY) + (CHPVSAT0 * CHYY * CHPCSC0
         * ( (FGPGW0 * (RHOW0 ** -1) * CHTKSCM1)
           + (FGPGW0 * (RHOW0 ** -2) * DRHOWDT0))) ;
*                                                                     *
* MUG0     : Viscosite de la phase gazeuse :                          *
MASQ1    = (CHPGSC0 - CHPGWSC0) 'MASQ' 'SUPE' 0. ;
CHXX     = (1. - (CHPGWSC0 * CHPGSCM1)) * MASQ1 ;
MUG0     = MUGW0 + ((MUGA0 - MUGW0) * (CHXX ** 0.6083)) ;
*                                                                     *
* RHOGA0   : Masse volumique de l'air sec (kg/m3) : Ok !              *
RHOGA0   = (CHPGSC0 - CHPGWSC0) * FGPGA0 ;
* DRHOGADT : Derivee -/- T (kg/m3/K) : Ok !                           *
DRHOGADT = -1. * ((RHOGA0 * CHTKSCM1) + (FGPGA0 * DPGWDT0)) ;
*                                                                     *
* RHOGW0   : Masse volumique de la vapeur (kg/m3) :                   *
RHOGW0   = FGPGW0 * CHPGWSC0 ;
* DRHOGWDT : Derivee -/- T (kg/m3/K) : Ok !                           *
DRHOGWDT = (FGPGW0 * DPGWDT0) - (RHOGW0 * CHTKSCM1) ;
*                                                                     *
* RHOG0    : Masse volumique de la phase gazeuse (kg/m3) : Ok !       *
RHOG0    = (FGPGA0 * CHPGSC0) + ((FGPGW0 - FGPGA0) * CHPGWSC0) ;
*                                                                     *
* CPG0     : Capacite calorifique a P constante du gaz (J/kg/K) Ok !  *
CPG0     = (RHOG0 * CPGA1) + (RHOGW0 * (CPGW1 - CPGA1)) ;
*                                                                     *
* Masse Molaire du gaz (kg/mol) :                                     *
MMG0     = MMGA1 + ((MMGW1 - MMGA1) * (CHPGWSC0 * CHPGSCM1)) ;
*                                                                     *
*------------------------ Matrice de Capacite ------------------------*
*                                                                     *
* Pression de Gaz (PG) :                                              *
CGG0     = N0 * SG0 * FGPGA0 ;
*                                                                     *
CGC0     = (RHOGA0 * DSWDPC0) + (SG0 * FGPGA0 * DPGWDPC0) ;
CGC0     = -1. * N0 * CGC0 ;
*                                                                     *

CGT0     = (N0 * ((SG0 * DRHOGADT) - (RHOGA0 * DSWDT0)))
         + (DMDHDT0 * RHOGA0 / RHOS1 * SG0) ;
*                                                                     *
CGU0     = SG0 * RHOGA0 ;
*                                                                     *
* Pression Capillaire (PC) :                                          *
CCG0     = 0. ;
*                                                                     *
DRHOW0   = RHOW0 - RHOGW0 ;
CCC0     = N0 * ((SG0 * FGPGW0 * DPGWDPC0) + (DRHOW0 * DSWDPC0)) ;
*                                                                     *
CCU0     = (RHOGW0 * SG0) + (RHOW0 * SW0) ;
*                                                                     *
* Remarque : signe termes avec DMDHDT0 opposes dans eq. 37 du rapport *
* SdP et premiere integrale de l'eq. ne se retrouve pas ci-dessous.   *
CCT0     = (SG0 * DRHOGWDT) + (SW0 * DRHOWDT0) + (DRHOW0 * DSWDT0) ;
CCT0     = (N0 * CCT0) - DMDHDT0 + (DMDHDT0 * CCU0 / RHOS1) ;
*                                                                     *
* Temperature :                                                       *
CTG0     = 0. ;
*                                                                     *
CTC0     = -1. * HVAP0 * N0 * RHOW0 * DSWDPC0 ;
*                                                                     *
* Remarque : corrections dans rapport SdP.                            *
CTT0     = ((1. - N0) * RHOS1 * CPS0)
         + (N0 * ((SW0 * RHOW0 * CPW1) + (SG0 * CPG0)))
         + (DMDHDT0 * DHHD1) ;
CTT0     = CTT0 - (HVAP0 * (N0 * ((RHOW0 * DSWDT0) + (SW0 * DRHOWDT0))
         + (RHOW0 / RHOS1 * SW0 * DMDHDT0) - DMDHDT0)) ;
*                                                                     *
CTU0     = -1. * HVAP0 * RHOW0 * SW0 ;
*                                                                     *
*'MESS' 'CTC0 =' ('MINI' CTC0) ;
*'MESS' 'CTT0 =' ('MINI' CTT0) ;
*'MESS' 'CTU0 =' ('MINI' CTU0) ;
*'MESS' 'CGG0 =' ('MINI' CGG0) ;
*'MESS' 'CGC0 =' ('MINI' CGC0) ;
*'MESS' 'CGT0 =' ('MINI' CGT0) ;
*'MESS' 'CGU0 =' ('MINI' CGU0) ;
*'MESS' 'CCC0 =' ('MINI' CCC0) ;
*'MESS' 'CCT0 =' ('MINI' CCT0) ;
*'MESS' 'CCU0 =' ('MINI' CCU0) ;
*                                                                     *
CGG0 = 'CHAN' 'CHAM' CGG0 MOD1 ;
CGC0 = 'CHAN' 'CHAM' CGC0 MOD1 ;
CGT0 = 'CHAN' 'CHAM' CGT0 MOD1 ;
CCC0 = 'CHAN' 'CHAM' CCC0 MOD1 ;
CCT0 = 'CHAN' 'CHAM' CCT0 MOD1 ;
CTC0 = 'CHAN' 'CHAM' CTC0 MOD1 ;
CTT0 = 'CHAN' 'CHAM' CTT0 MOD1 ;
*                                                                     *
*---------------------- Matrice de conductivite ----------------------*
*                                                                     *
* Pression de Gaz (PG) :                                              *
RPERMGA0 = RHOGA0 * KINT0 * KRG0 * (MUG0 ** -1) ;
CHFDIFF0 = RHOG0 * MMGA1 * MMGW1 * (MMG0 ** -2) * DEFF0 ;
KGG0     = RPERMGA0 + (CHFDIFF0 * CHPGWSC0 * CHPGSCM2) ;
*                                                                     *
SGN1     = 1. ;
* Remarque : signe KGC0 oppose dans eq. 31 rapport SdP.               *
KGC0     = SGN1 * CHFDIFF0 * DPGWDPC0 * CHPGSCM1 ;
*                                                                     *
* Remarque : signe KGT0 oppose dans eq. 32 rapport SdP.               *
KGT0     = SGN1 * CHFDIFF0 * DPGWDT0 * CHPGSCM1 ;
*                                                                     *
KGU0      = 0. ;
*                                                                     *
* Pression Capillaire (PC) :                                          *
RPERMG0  = KINT0 * KRG0 * (MUG0 ** -1) ;
RPERMGW0 = RHOGW0 * RPERMG0 ;
RPERMG0  = RHOG0 * RPERMG0 ;
RPERMW0  = RHOW0 * KINT0 * KRW0 * (MUW0 ** -1) ;
*                                                                     *
* Remarque : 1er terme de signe oppose dans eq. 39 rapport SdP, ainsi *
* que terme RPERMGW0.
KCG0     = (CHFDIFF0 * CHPGWSC0 * CHPGSCM2) - RPERMGW0 + RPERMW0 ;
*                                                                     *
* Remarque : Dans rapport SdP, KCC0 de signe oppose.                  *
KCC0      = (-1. * SGN1 * KGC0) - RPERMW0 ;
*                                                                     *
* Remarque : idem KCC0 pour KCT0.                                     *
KCT0      = -1. * SGN1 * KGT0 ;
*                                                                     *
KCU0      = 0. ;
*                                                                     *
* Temperature :                                                       *
* ATTENTION : termes en GRAD PG/PC/T !
KTG0     = -1. * HVAP0 * RPERMW0 ;
KTGGT0   = -1. * ((CPW1 * RPERMW0) + (CPG0 * RPERMG0)) ;
*KG21     = KTGGT0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'T,X' 'SCAL')) ;
*KG22     = KTGGT0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'T,Y' 'SCAL')) ;
*                                                                     *
KTC0     = -1. * KTG0 ;
KTCGT0   = CPW1 * RPERMW0 ;
*KC21     = KTCGT0
*  * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'T,X' 'SCAL')) ;
*KC22     = KTCGT0
*  * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'T,Y' 'SCAL')) ;

*                                                                     *
* Remarque : gravite negligee.                                        *
KTT0     = KS0
  * ((4. * N0 * SW0 * RHOW0 * ((1. - N0) ** -1) / RHOS1) + 1.) ;
KTTGPG0  = KTGGT0 ;
KTTGPC0  = KTCGT0 ;
*KT21     = (KTTGPG0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'PG,X' 'SCAL')))
* + (KTTGPC0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'PC,X' 'SCAL'))) ;
*KT22     = (KTTGPG0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'PG,Y' 'SCAL')))
* + (KTTGPC0 * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'PC,Y' 'SCAL'))) ;
*                                                                     *
KTU0     = 0. ;
*                                                                     *
*'MESS' 'KGG0 =' ('MINI' KGG0) ;
*'MESS' 'KGC0 =' ('MINI' KGC0) ;
*'MESS' 'KGT0 =' ('MINI' KGT0) ;
*'MESS' 'KCG0 =' ('MINI' KCG0) ;
*'MESS' 'KCC0 =' ('MINI' KCC0) ;
*'MESS' 'KCT0 =' ('MINI' KCT0) ;
*'MESS' 'KTG0 =' ('MINI' KTG0) ;
*'MESS' 'KTC0 =' ('MINI' KTC0) ;
*'MESS' 'KTT0 =' ('MINI' KTT0) ;
* ATTENTION : valeurs des termes gradient differentes de ZH car
*             ces dernieres sont fausses (capacite cal. pas en USI)   *
*'MESS' 'KTGGT0 =' ('MINI' KTGGT0) ;
*'MESS' 'KTCGT0 =' ('MINI' KTCGT0) ;
*'MESS' 'KTTGPG0 =' ('MINI' KTTGPG0) ;
*'MESS' 'KTTGPC0 =' ('MINI' KTTGPC0) ;
*                                                                     *
* Sauvegarde CHPO pour 2nd membre :                                   *
KGC1 = KGC0 ;
KGT1 = KGT0 ;
KCG1 = KCG0 ;
KCT1 = KCT0 ;
KTC1 = KTC0 ;
*                                                                     *
KGG0 = 'CHAN' 'CHAM' KGG0 MOD1 ;
KGC0 = 'CHAN' 'CHAM' KGC0 MOD1 ;
KGT0 = 'CHAN' 'CHAM' KGT0 MOD1 ;
KCG0 = 'CHAN' 'CHAM' KCG0 MOD1 ;
KCC0 = 'CHAN' 'CHAM' KCC0 MOD1 ;
KCT0 = 'CHAN' 'CHAM' KCT0 MOD1 ;
KTG0 = 'CHAN' 'CHAM' KTG0 MOD1 ;
KTC0 = 'CHAN' 'CHAM' KTC0 MOD1 ;
KTT0 = 'CHAN' 'CHAM' KTT0 MOD1 ;
*KG21 = 'CHAN' 'CHAM' KG21 MOD1 ;
*KG22 = 'CHAN' 'CHAM' KG22 MOD1 ;
*KC21 = 'CHAN' 'CHAM' KC21 MOD1 ;
*KC22 = 'CHAN' 'CHAM' KC22 MOD1 ;
*KT21 = 'CHAN' 'CHAM' KT21 MOD1 ;
*KT22 = 'CHAN' 'CHAM' KT22 MOD1 ;
KTGGT0 = 'CHAN' 'CHAM' KTGGT0 MOD1 ;
KTCGT0 = 'CHAN' 'CHAM' KTCGT0 MOD1 ;
*KTTGPG0 = 'CHAN' 'CHAM' KTTGPG0 MOD1 ;
*KTTGPC0 = 'CHAN' 'CHAM' KTTGPC0 MOD1 ;
*                                                                     *
*----- Construction du MCHAML de Caracteristiques : appel a MATE -----*
*                                                                     *
MAT1     = 'MATE' MOD1
  'CGG' CGG0 'CGC' CGC0 'CGT' CGT0
  'CCG' CCG0 'CCC' CCC0 'CCT' CCT0
  'CTG' CTG0 'CTC' CTC0 'CTT' CTT0
  'KGG' KGG0 'KGC' KGC0 'KGT' KGT0
  'KCG' KCG0 'KCC' KCC0 'KCT' KCT0
  'KTG' KTG0 'KTC' KTC0 'KTT' KTT0 'KTGG' KTGGT0 'KTCG' KTCGT0 ;
*                                                                     *
'RESP' MAT1 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                 CALCUL DES TERMES DU SECOND MEMBRE                  *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* Acceleration de la gravite :                                        *
AGRAV1   = 9.81 ;
*                                                                     *
* Composante "gravite" a preciser car depend du produit scalaire avec *
* gradient de la fonction d'interpolation support inconnue Pg.        *
*FG0      = RHOG0 * RPERMGA0 * AGRAV1 ;
*FG0      = FG0 'NOMC' 'Q' ;
*                                                                     *
* Composante "gravite" a preciser car depend du produit scalaire avec *
* gradient de la fonction d'interpolation support inconnue Pc.        *
*FC0      = ((RPERMGW0 * RHOG0) + (RPERMW0 * RHOW0)) * AGRAV1 ;
*FC0      = FC0 'NOMC' 'Q' ;
*                                                                     *
* Composante "gravite" a preciser car depend du produit scalaire avec *
* gradient de la fonction d'interpolation support inconnue T.         *
*FT0      = ((CPW1 * RPERMW0 * RHOW0) + (CPG0 * RPERMG0 * RHOG0))
*  * ('CHAN' 'CHPO' MOD1 ('EXCO' GRCH0 'T,Y' 'SCAL')) * AGRAV1 ;
*FT0      = (HVAP0 * RPERMW0 * RHOW0 * AGRAV1) + FT0 ;
*FT0      = FT0 'NOMC' 'Q' ;
*                                                                     *
* Laplaciens de Pg, Pc, T :                                           *
GRCH1    = 'CHAN' 'CHPO' MOD1 GRCH0 ;
GRXPG0   = 'EXCO' GRCH1 ('EXTR' LMGPG1 1) 'PG' ;
GRXPC0   = 'EXCO' GRCH1 ('EXTR' LMGPC1 1) 'PC' ;
GRXTK0   = 'EXCO' GRCH1 ('EXTR' LMGTK1 1) 'T' ;
GRYPG0   = 'EXCO' GRCH1 ('EXTR' LMGPG1 2) 'PG' ;
GRYPC0   = 'EXCO' GRCH1 ('EXTR' LMGPC1 2) 'PC' ;
GRYTK0   = 'EXCO' GRCH1 ('EXTR' LMGTK1 2) 'T' ;
IDIM3    = 'EGA' VDIM1 3 ;
'SI' IDIM3 ;
  GRZPG0   = 'EXCO' GRCH1 ('EXTR' LMGPG1 3) 'PG' ;
  GRZPC0   = 'EXCO' GRCH1 ('EXTR' LMGPC1 3) 'PC' ;
  GRZTK0   = 'EXCO' GRCH1 ('EXTR' LMGTK1 3) 'T' ;
'FINS' ;
*                                                                     *
FG0X     = GRXPG0 + (KGC1 * GRXPC0) + (KGT1 * GRXTK0) ;
FG0Y     = GRYPG0 + (KGC1 * GRYPC0) + (KGT1 * GRYTK0) ;
FG0X     = 'GRAD' MOD1 FG0X ;
FG0Y     = 'GRAD' MOD1 FG0Y ;
CPGX1    = 'EXTR' LMGPG1 1 ;
CPGY1    = 'EXTR' LMGPG1 2 ;
CPCX1    = 'EXTR' LMGPC1 1 ;
CPCY1    = 'EXTR' LMGPC1 2 ;
CTKX1    = 'EXTR' LMGTK1 1 ;
CTKY1    = 'EXTR' LMGTK1 2 ;
FG0G     = ('EXCO' FG0X CPCX1 'SCAL') + ('EXCO' FG0X CTKX1 'SCAL')
  + ('EXCO' FG0Y CPCY1 'SCAL') + ('EXCO' FG0Y CTKY1 'SCAL') ;
'SI' IDIM3 ;
  FG0Z     = GRZPG0 + (KGC1 * GRZPC0) + (KGT1 * GRZTK0) ;
  FG0Z     = 'GRAD' MOD1 FG0Z ;
  FG0G   = ('EXCO' FG0Z 'PC,Z' 'SCAL') + ('EXCO' FG0Z 'T,Z' 'SCAL')
    + FG0G ;
'FINS' ;
FG0      = 'SOUR' MOD1 FG0G 'QG' ;
*                                                                     *
FC0X     = (KCG1 * GRXPG0) + GRXPC0 + (KCT1 * GRXTK0) ;
FC0Y     = (KCG1 * GRYPG0) + GRYPC0 + (KCT1 * GRYTK0) ;
FC0X     = 'GRAD' MOD1 FC0X ;
FC0Y     = 'GRAD' MOD1 FC0Y ;
FC0G     = ('EXCO' FC0X CPGX1 'SCAL') + ('EXCO' FC0X CTKX1 'SCAL')
  + ('EXCO' FC0Y CPGY1 'SCAL') + ('EXCO' FC0Y CTKY1 'SCAL') ;
'SI' IDIM3 ;
  FC0Z     = (KCG1 * GRZPG0) + GRZPC0 + (KCT1 * GRZTK0) ;
  FC0Z     = 'GRAD' MOD1 FC0Z ;
  FC0G   = ('EXCO' FC0Z 'PG,Z' 'SCAL') + ('EXCO' FC0Z 'T,Z' 'SCAL')
    + FC0G ;
'FINS' ;
FC0      = 'SOUR' MOD1 FC0G 'QC' ;
*                                                                     *
FT0X     = (KTC1 * (GRXPG0 + GRXPC0)) + GRXTK0 ;
FT0Y     = (KTC1 * (GRYPG0 + GRYPC0)) + GRYTK0 ;
FT0X     = 'GRAD' MOD1 FT0X ;
FT0Y     = 'GRAD' MOD1 FT0Y ;
FT0G     = ('EXCO' FT0X CPGX1 'SCAL') + ('EXCO' FT0X CPCX1 'SCAL')
  + ('EXCO' FT0Y CPGY1 'SCAL') + ('EXCO' FT0Y CPCY1 'SCAL') ;
'SI' IDIM3 ;
  FT0Z     = (KTC1 * (GRZPG0 + GRZPC0)) + GRZTK0 ;
  FT0Z     = 'GRAD' MOD1 FT0Z ;
  FT0G   = ('EXCO' FT0Z 'PG,Z' 'SCAL') + ('EXCO' FT0Z 'PC,Z' 'SCAL')
    + FT0G ;
'FINS' ;
FT0      = 'SOUR' MOD1 FT0G 'Q' ;
*                                                                     *
F0       = FG0 + FC0 + FT0 ;
*                                                                     *
*'MESS' 'FG0 =' ('MINI' FG0) ;
*'MESS' 'FC0 =' ('MINI' FC0) ;
*'MESS' 'FT0 =' ('MINI' FT0) ;
*                                                                     *
'RESP' F0 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
'FINP' ;
*---------------------------------------------------------------------*
* FIN PROCEDURE @MATETHM


