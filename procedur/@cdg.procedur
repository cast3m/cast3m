$$$$ @CDG
* @CDG      PROCEDUR  AM        93/11/02    21:15:17     956
*-----------------------------------------------------------
******              PROCEDURE @CDG                    ******
*-----------------------------------------------------------
*
*      CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*     A DISPOSITION DE LA COMMUNAUTE  CASTEM2000
*  PAR  DELERUYELLE Fr.  (SOCOTEC-INDUSTRIE à L IPSN/DES)
*
*-----------------------------------------------------------
*          CALCUL DU CENTRE DE GRAVITE D UNE GEOMETRIE
*                CONTENUE DANS UN MODELE
*-----------------------------------------------------------
'DEBP' @CDG MO1*'MMODEL' CA1/'MCHAML' ;
GEO1 = 'EXTR' MO1 'MAIL' ;
CHP1 = 'COOR' 1 GEO1 ;
CHE1 = 'CHAN' 'CHAM' CHP1 MO1 'NOEUD' 'SCALAIRE' ;
CHP2 = 'COOR' 2 GEO1 ;
CHE2 = 'CHAN' 'CHAM' CHP2 MO1 'NOEUD' 'SCALAIRE' ;
UN = 'MANU' 'CHML' MO1 'SCAL' 1. 'TYPE' 'SCALAIRE' 'NOEUD' ;
'SI' ('EXISTE' CA1) ;
 LCOM = 'EXTR' CA1 'COMP' ;
 'SI' ('EXISTE' LCOM 'RHO') ;
  RH = 'CHAN' 'NOEUD' MO1 ('EXCO' 'RHO' CA1 'SCAL') ;
  XSECT = 'INTG' MO1 RH CA1 ;
  XCHE1 = 'INTG' MO1 (MO1 RH * CHE1) CA1 ;
  XCHE2 = 'INTG' MO1 (MO1 RH * CHE2) CA1 ;
 'SINON' ;
  XSECT = 'INTG' MO1 UN CA1 ;
  XCHE1 = 'INTG' MO1 CHE1 CA1 ;
  XCHE2 = 'INTG' MO1 CHE2 CA1 ;
 'FINSI' ;
'SINON' ;
 XSECT = 'INTG' MO1 UN ;
 XCHE1 = 'INTG' MO1 CHE1 ;
 XCHE2 = 'INTG' MO1 CHE2 ;
'FINSI' ;
XG1 = XCHE1 / XSECT ;
XG2 = XCHE2 / XSECT ;
'RESP' XG1 XG2 ;
'SI' ('EGA' ('VALE' 'DIME') 3) ;
 CHP3 = 'COOR' 3 GEO1 ;
 CHE3 = 'CHAN' 'CHAM' CHP3 MO1 'NOEUD' 'SCALAIRE' ;
 'SI' ('EXISTE' CA1) ;
  'SI' ('EXISTE' LCOM 'RHO') ;
   XCHE3 = 'INTG' MO1 (MO1 RH * CHE3) CA1 ;
  'SINON' ;
   XCHE3 = 'INTG' MO1 CHE3 CA1 ;
  'FINSI' ;
 'SINON' ;
  XCHE3 = 'INTG' MO1 CHE3 ;
 'FINSI' ;
 XG3 = XCHE3 / XSECT ;
 'RESP' XG3 ;
'FINSI' ;
'FINP' ;
