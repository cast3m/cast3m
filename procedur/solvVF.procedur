$$$$ SOLVVF
* SOLVVF    PROCEDUR  GOUNAND   10/10/13    21:15:43     6779
**********************************************************************
'DEBP'  SOLVVF MoDARCY*'MMODEL' ChPSour*'CHPOINT'
                 matk/'MATRIK' SMTR*'CHPOINT' cini*'CHPOINT'
                 Mctot*'MCHAML' Mdiff*'MCHAML' Difftot*'CHPOINT'
                 RESID*'CHPOINT' TetaDiff*'FLOTTANT'
                 QFACE/'CHPOINT' nomespec*'LISTMOTS'
                 nbespece*'ENTIER' nbsource*'ENTIER' TABRES*'TABLE'
                 CHCLIM*'TABLE' NOUVMAT*'LOGIQUE' LCONV*'LOGIQUE'
                 CHTR*'LOGIQUE' ;
*  |-----------------------------------------------------------------|
*  | Phrase d'appel (en GIBIANE)                                     |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*                matsor TABRES cfin cflu cfluco = SOLVVF MoDARCY
*                ChPSour Mattt Smtr Cini Mctot Mdiff Difftot
*                Qface nomespec nbespece
*                nbsource OPTRES CHCLIM  Nouvmat
*  |                                                                 |
*  |                                                                 |
*  |-----------------------------------------------------------------|
*  | Généralités : MATTVF construit la matrice de discrétisation   |
*  |               du problème de transport convection-diffusion pour|
*  |               le premier pas de tps d'un algorithme transitoire.|
*  |               Le second membre et les Conditions limites de flux|
*  |               sont pris en compte.                              |
*  |            RESTE TCINI, DECENTR et TERME LIN                    |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*  |-----------------------------------------------------------------|
*  |                         ENTREES                                 |
*  |-----------------------------------------------------------------|
*  | MoDARCY  : modele Darcy.                                        |
*  |                                                                 |
*  | ChPSour  : Champ par points des sources volumiques par unité de |
*  |            temps (support maillage centre). Composante associées|
*  |            aux especes                                          |
*  |                                                                 |
*  | Mattt : matrice discretisation  VF                              |
*  |                                                                 |
*  | SMTr     : second membre sur les traces                         |
*  |                                                                 |
*  | nomespec : liste des noms de composante des espèces dans Cini   |
*  |                                                                 |
*  | nbespece : nombre de composante de Cini, soit nombre d'especes  |
*  |                                                                 |
*  | nbsource : nombre de composantes du terme source qd X especes   |
*  |                                                                 |
*  | TABRES   : Table complète définissant les options de résolution |
*  |            pour 'KRES'.                                         |
*  |                                                                 |
*  |                                                                 |
*  | CHCLIM   : table d'indice 'NEUMANN' et 'DIRICHLET' contenant les|
*  |            Chpoint à n composantes contenant les conditions aux |
*  |            limites de Neumann et Dirichlet par espece.          |
*
*  | NOUVMAT   : Logique affecté à VRAI lorsque que Matot vient
*  |             d'etre calculée
*  |                                                                 |
*  |-----------------------------------------------------------------|
*  |                         SORTIES                                 |
*  |-----------------------------------------------------------------|

*  | Matk     : matrice globale VF
*  |                                                                 |
*  | cfin     : concentration apres calcul pour toutes les especes   |
*  |                                                                 |
*  | TABSORT  : Table complète définissant les options de résolution |
*  |            pour 'KRES'.                                         |
*  |                                                                 |
*  |-----------------------------------------------------------------|
*  |                  VARIABLES INTERNES                             |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*  |                                                                 |
*  | CChpsour : Source aux centre (une composante)                   |
*  |                                                                 |
*  | CCfin    : concentration aux centres (une composante)           |
*  |                                                                 |
*  | SSMTr    : second membre sur les traces pour une espèce         |
*  |                                                                 |
*  | Nouvmatr : Logique, si VRAI on transforme la matrice rigidité   |
*  |            en matrik                                            |
*  |                                                                 |
*  | DIRCLI   : logique valant VRAI si conditions aux                |
*  |            limites de Dirichlet                                 |
*  |                                                                 |
*  | CLDIRI   : Chpoint à n composantes contenant les conditions aux |
*  |            limites de Dirichlet par espece.                     |
*  |            il faudra en faire un nuage si supports géométriques |
*  |            différents par espece. OPTIONNEL                     |
*  |                                                                 |
**********************************************************************



*---------------------------------------------------------------------
*---------- On récupere les conditions limites      ------------------
*---------------------------------------------------------------------



*
* On recopie la table de résolution TABRES dans TABSORT
* Attention si une valeur contenue dans la table a le
* debut d'un nom d'opérateur de castem, il y a probleme
* d'ou démarrage apres l'indice soustype de valeur
* METHINV, identique à opérateur METHode.
*

FLUNEU = FAUX;
DIRCLI = FAUX;
FLUTOT = FAUX;
FLUMIX = FAUX;
CLFLUX MA = 'KOPS' 'MATRIK';
CLDIRI MA = 'KOPS' 'MATRIK';
CLFLUT MA = 'KOPS' 'MATRIK';
EXFLU = CLFLUX;
EXDIR = CLDIRI;
EXFLUT = CLFLUT;

* Neumann
'SI' ('EXISTE' CHCLIM 'NEUMANN') ;
   CLFLUX = CHCLIM . 'NEUMANN';
   FLUNEU = VRAI;
'FINSI';

'SI' ('EXISTE' CHCLIM 'DIRICHLET') ;
   CLDIRI = CHCLIM . 'DIRICHLET';
   DIRCLI = VRAI;
'FINSI';

'SI' ('EXISTE' CHCLIM 'FLUTOTAL') ;
   CLFLT = CHCLIM . 'FLUTOTAL';
   FLUTOT = VRAI;
'FINSI';

'SI' ('EXISTE' CHCLIM 'FLUMIXTE')                                     ;
*  comme on impose A Dgrad C + B C = flumix, on le traite sous
* la forme D grad C + (B/A) C = flumix/A plus naturelle en EFMH car
* D grad C est le flux diffusif
   COFA   = -1.D0 * CHCLIM . 'FLUMIXTE' . 'COEFA'                     ;
   CLFLUX3 = CHCLIM . 'FLUMIXTE' . 'VAL' '/' COFA                     ;
   CLFLUX3 = CHAN 'ATTRIBUT' CLFLUX3 NATURE DISCRET                   ;
   FLUMIX = VRAI                                                      ;
   mayage = 'EXTRAIRE' CHCLIM . 'FLUMIXTE' . 'VAL' maillage           ;
   cofb   = (doma modarcy SURFACE) * CHCLIM . 'FLUMIXTE' . 'COEFB'    ;
*   cofb   =  CHCLIM . 'FLUMIXTE' . 'COEFB'    ;
   cofa   = 'REDU' (CHCLIM . 'FLUMIXTE' . 'COEFA')  mayage            ;
   cofb   = 'REDU' cofb  mayage                                       ;
   coefm  = (1.D0 * cofb) '/' cofa                                   ;
   CLFLUX3   = 'REDU' CLFLUX3  mayage                                 ;
   'OUBLIER' cofa                                                     ;
   'OUBLIER' cofb                                                     ;
'FINSI'                                                               ;

dumm = 'INDEX' TABRES                                                  ;
TABSORT = 'TABLE' METHINV                                              ;
'REPETER' bou1 (('DIME' dumm) '-' 1)                                   ;
   TABSORT . (mot dumm . (&bou1 '+' 1)) = TABRES . (mot dumm . (&bou1
   '+' 1))                                                             ;
'FIN' bou1                                                             ;


'SI' (NOUVMAT)                                                        ;
   TABSORT . 'MATASS' = Matk                                          ;
   TABSORT . 'MAPREC' = Matk                                          ;
'FINSI'                                                               ;



*---------------------------------------------------------------------
*-------------- RESOLUTION  -----------------
*---------------------------------------------------------------------
* boucle sur les espèces.
  SURF = 'DOMA' MoDarcy 'SURFACE'                                     ;
'REPETER' bloc1 nbespece                                              ;

   espc =  ('EXTRAIRE' &bloc1 nomespec)                               ;


*  préparation solution initiale - ie trace initiale
   TABSORT . 'XINIT' = 'NOMC' 'RETN'
             ('EXCO' espc Cini)                                       ;
   hh = TABSORT . 'XINIT'                                             ;



*  préparation du terme source
   'SI' (nbsource 'EGA' 1)                                            ;
      CChpsour = 'NOMC' 'RETN' Chpsour                                ;
   'SINON'                                                            ;
      CChpsour = 'NOMC' 'RETN' ('EXCO' espc
                               Chpsour)                               ;
   'FINSI'                                                            ;

*  préparation second membre
   'SI' ('NON' CHTR) ;
* VERRUE
      Smaux = ((1.D0 - Tetadiff)*RESID) + (Tetadiff*Smtr) ;
      Smaux = Smtr;
      SSmtr = 'NOMC' 'RETN' ('EXCO' espc Smaux)
              '+'   CChpsour ;
    'SINON' ;
      SSmtr = 'NOMC' 'RETN' ('EXCO' espc Smtr)
              '+'   CChpsour ;
    'FINSI' ;


*LIST CCHPsour;
*   Calcul du résidu

   res = KRES matk 'TYPI' TABSORT
            'SMBR' ('NOMC' 'RETN' SSMTr)
            'IMPR' 0                                                  ;


   TABSORT . 'MATASS' = matk                                          ;
   TABSORT . 'MAPREC' = matk                                          ;

   CCfin = 'CHAN' 'ATTRIBUT' ( hh '+' ('EXCO' res 'RETN' 'RETN') )
            'NATU' 'DISCRET' ;

   HHS = 'NOMC' CCfin 'SCAL' ;

     'SI' (FLUNEU)                                                   ;
             EXFLU =
              ('NOMC' 'FLUX' ('EXCO' espc
              CLFLUX))                                                ;
     'FINSI'                                                          ;
     'SI' (DIRCLI) ;
             EXDIR =
             ('NOMC' 'SCAL' ('EXCO' espc
             CLDIRI))                                                 ;
     'FINSI'                                                          ;

     'SI' (FLUTOT) ;
               EXFLUT =
               ('NOMC' 'FLUX' ('EXCO' espc
               CLFLT ))                                               ;
               QLIM = NOMC(EXFLUT) 'FLUX'                             ;
               SUP = 'EXTR' EXFLUT  'MAIL'                            ;
               XPAR1 = 1.D0 + (0.0*CLFLT)                             ;
               XPAR1  = 'NOMC' XPAR1 'PAR1'                           ;
               'SI' LCONV                                             ;
               USCNR = 'REDU' QFACE SUP                               ;
               MUSCN = USCNR*(-1.D0)                                  ;
               'SINON'                                                ;
                MUSCN =  (0.D0*CLFLT)                                 ;
               'FINSI'                                                ;
               XPAR2  = 'NOMC' MUSCN 'PAR2'                           ;
               EXFLUT = XPAR1 + XPAR2 + QLIM                          ;
              EXFLUT = CHAN 'ATTRIBUT' EXFLUT NATURE DISCRET         ;
      'FINSI'                                                         ;

       'SI' (FLUMIX) ;
               EXFLUM =
               ('NOMC' 'FLUX' ('EXCO' espc
               CLFLUX3 ))                                             ;
               QLIM = NOMC(EXFLUM) 'FLUX'                             ;
               SUP = 'EXTR' EXFLUM  'MAIL'                            ;
               XPAR1 = 1.D0 + (0.0*CLFLUX3)                           ;
               XPAR1  = 'NOMC' XPAR1 'PAR1'                           ;
               MUSCN = coefm                                          ;
               XPAR2  = 'NOMC' MUSCN 'PAR2'                           ;
               EXFLUM = XPAR1 + XPAR2 + QLIM                          ;
              EXFLUM = CHAN 'ATTRIBUT' EXFLUM NATURE DISCRET         ;
               EXFLUT = EXFLUT 'ET' EXFLUM                            ;
       'FINSI'                                                        ;


      'SI' ('NON' CHTR) ;
* On reconstruit le flux diffusif aux faces
         CCFLU  = 'PENT'  MoDARCY 'FACE' 'MPFA'
             HHS 'DISPDIF' difftot
            'TIMP' EXDIR 'QIMP' EXFLU  'MIXT' EXFLUT
            'UPWICENT' QFace 'GRADGEO' Mdiff      ;
         CCFLU = (-1.D0)*CCFLU*SURF;
         CCFLU = 'CHAN' 'ATTRIBUT' CCFLU 'NATU' 'DISCRET' ;

* On reconstruit le flux convectif aux faces  A PATIR DU FLUX TOTAL
         CCFLUTO  = 'PENT'  MoDARCY 'FACE' 'MPFA'
              HHS 'DISPDIF' difftot
            'TIMP' EXDIR 'QIMP' EXFLU  'MIXT' EXFLUT 'UPWICENT' QFace
             'GRADGEO' Mctot      ;

* CALCUL DU RESIDU POUR LA PAS DE TEMPS SUIVANT
          JACO CHPRES DT = 'LAPN' 'VF' 'CLAUDEIS'  'EXPL'
                   MoDARCY HHS CCFLUTO ;

         'SI' ((&bloc1) 'EGA' 1) ;
            SSMTr = 'NOMC' espc CHPRES ;
            RESI =  SSMTr ;
         'SINON' ;
            CHPRES = 'NOMC' espc ('COPIER' CHPRES) ;
            RESI = CHPRES 'ET'  RESI;
         'FINSI' ;

         CCFLUTO = (-1.D0)*CCFLUTO*SURF;
         CCFLUTO = 'CHAN' 'ATTRIBUT' CCFLUTO 'NATU' 'DISCRET' ;
         CCFLUCO = CCFLUTO - CCFLU;
      'SINON' ;
         CCFLU = 0.*SURF ;
         CCFLUCO = 0.*SURF ;
* PHM : ATTENTION : est ce ok de mettre cela ?
         RESI = RESID  ;
      'FINSI'   ;


* On reconstitue les champoints à plusieurs composante

  'SI' (&bloc1 'EGA' 1)                                               ;
     CCfin  = 'NOMC' espc  CCfin              ;
     CCflu  = 'NOMC' espc CCFLU               ;
     CCFLUCO = 'NOMC' espc CCFLUCO
               NATU DISCRET           ;
     cfin   = ccfin                                                   ;
     cflu   = ccflu                                                   ;
     cfluco  = ccfluco                                                ;
   'SINON'                                                            ;
     CCfin  = 'NOMC' espc  ('COPIER' CCfin)
               NATU DISCRET                                           ;
     ccflu  = 'NOMC' espc ('COPIER' CCflu)
               NATU DISCRET                                           ;
     CCFLUCO = 'NOMC' espc ('COPIER' CCFLUCO)
               NATU DISCRET                                           ;
     cfin   = cfin 'ET' ccfin                                         ;
     cflu   = cflu 'ET' ccflu                                         ;
     cfluco = cfluco 'ET' ccfluco                                     ;
   'FINSI'                                                            ;

'FIN' bloc1                                                           ;

'FINP' matk TABSORT cfin cflu cfluco RESI                             ;
$$$$







