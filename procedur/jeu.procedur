$$$$ JEU
* JEU       PROCEDUR  NOBODY    92/01/01    00:00:00     001
*---------------------------------------------------------------------
*             PROCEDURE JEU   VERSION DU 1/08/91
*---------------------------------------------------------------------
*   CETTE PROCEDURE PERMET DE DETERMINER LE SECOND MEMBRE ASSOCIE
*                  A UN JEU ENTRE DEUX SOLIDES
*
*
*                    CHP1 = JEU RIG1 ;
*
*       RIG1    =   LA MATRICE DES BLOCAGES ( TYPE RIGIDITE)
*                   OBTENUE PAR EXEMPLE PAR RELA
*       CHP1    =   LE CHAMP SECOND MEMBRE
*
*---------------------------------------------------------------------
*
   'DEBPROC' JEU RIG1*'RIGIDITE' ;
*
   IDIM  = 'VALE' 'DIME' ;
   LEMOD = 'VALE' 'MODE' ;
   GEO1  = 'EXTR' RIG1 'MAIL' 'NOMU' ;
   'SI' ( 'EGA' IDIM 2 ) ;
   XX YY = 'COOR' GEO1 ;
   'SI' ( 'EGA' LEMOD 'AXIS' ) ;
   DDL1 = 'UR' ;
   DDL2 = 'UZ' ;
   'SINON' ;
   DDL1 = 'UX' ;
   DDL2 = 'UY' ;
   'FINSI' ;
   XX = 'NOMC' DDL1 XX ;
   YY = 'NOMC' DDL2 YY ;
   CHP2 = XX 'ET' YY ;
   'SINON' ;
   XX YY ZZ = 'COOR' GEO1 ;
   XX = 'NOMC' 'UX' XX ;
   YY = 'NOMC' 'UY' YY ;
   ZZ = 'NOMC' 'UZ' ZZ ;
   CHP2 = XX ET YY ET ZZ ;
   'FINSI';
    CHP2 = -1. * ( RIG1 * CHP2 ) ;
    CHP1 = 'EXCO' 'FLX' CHP2 'FLX'  ;
   'FINPROC' CHP1 ;
*---------------------------------------------------------------------
