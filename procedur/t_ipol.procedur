$$$$ T_IPOL
* T_IPOL    PROCEDUR  MAGN      00/10/12    21:17:53     3973
DEBPROC T_IPOL t*FLOTTANT rxt*TABLE ;

*------------------------------------------------------------
* Procedure interpolation des CLIM imposee en temperature
* Fonction du temps
*
* ENTREE : t : FLOTTANT : Temps auquel on veut faire interpolation
*          rxt : TABLE contenant les informations necessaires
* SORTIE : TBT.'cTp0' CHAMPOINT SOMMET actualise avec interpolation
*------------------------------------------------------------
  GEO=rxt.'GEO' ;
  TBT=rxt.'TBT' ;
  $mtpi = GEO.'$mtpi' ;
  NCLIM = TBT.'NCLIM' ;
  T_MCLIM = TBT.'T_MCLIM' ;
  T_CTP0 = TBT.'T_CTP0' ;
*--- Creation des champs points sur MTPI : cTp0
  cTp0    = 'KCHT' $mtpi SCAL SOMMET 0.0 ;

 'REPETER' BOUC3 NCLIM ;
  maili = T_MCLIM . &BOUC3 ;
*--- Calcul de la temperature a partir du nom de la table
  ts       = rxt.'scenario'.'t'                        ;
  ctp1     = 'IPOL' t ts  (rxt.'scenario'. (T_CTP0 . &BOUC3) ) ;
  tempo = 'MANU' 'CHPO' maili 1 'SCAL' ctp1
                      'NATURE' 'DISCRET' ;
  cTp0    = 'KCHT' $mtpi SCAL SOMMET cTp0 tempo ;
 'FIN' BOUC3 ;
  ctp0c   = NOEL $mtpi cTp0    ;
  cTp0    = ELNO $mtpi ctp0c   ;
  'SI' (('EGA' TBT.'discr' 'LINE' ) 'OU' ('EGA' ('VALE' 'DIME') 2)) ;
       FACT = 1.0 ;
  'SINON' ;
       FACT = 4.0/ 3.0 ;
  'FINSI' ;
  cTp0    = 'KCHT' $mtpi SCAL SOMMET (cTp0 * FACT ) ;
  TBT.'cTp0' = cTp0 ;
FINPROC                ;

