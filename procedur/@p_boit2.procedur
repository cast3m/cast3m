$$$$ @P_BOIT2
* @P_BOIT2  PROCEDUR  PASCAL    12/10/18    21:15:02     7532
*---------------------------------------------------------------------*
*                                                                     *
* NOM         : @P_BOIT2                                              *
*                                                                     *
* DESCRIPTION : Procedure determinant l'intersection d'un maillage de *
*               polyedres de Voronoi construit par la procedure       *
*               @P_VORO avec le cube de cote 1. @P_BOIT2 utilise la   *
*               PROCEDURE @COUPLAN pour realiser les coupes avec les  *
*               faces du CUBE, contairement a @P_BOIT.                *
*                                                                     *
* SYNTAXE     : TAB2  = @P_BOIT TAB1 (IVISU1) ;                       *
*                                                                     *
*    - TAB1   = TABLE, resultat de la procedure @P_VORO ;             *
*    - TAB2   = TABLE, sa structure est similaire a celle de TAB1     *
*               (voir notice @P_VORO) ;                               *
*    - IVISU1 = LOGIQUE, active des traces (pour DeBogage).           *
*                                                                     *
* REMARQUE    : Fait appel a la procedure @COUPLAN.                   *
*                                                                     *
* LANGAGE     : GIBIANE-CAST3M                                        *
* AUTEUR      : S. PASCAL (CEA/DEN/DM2S/SEMT/LM2S)                    *
* COURRIEL    : serge.pascal@cea.fr                                   *
*---------------------------------------------------------------------*
'DEBP' @P_BOIT2 ;
*                                                                     *
'ARGU' THO7U*'TABLE' ;
*                                                                     *
'ARGU' ITRAC1/'LOGIQUE' ;
'SI' ('NON' ('EXIS' ITRAC1)) ;
  ITRAC1   = FAUX ;
'FINS' ;
*                                                                     *
* Decoupage :                                                         *
TG1 TD1  = @COUPLAN THO7U (0. 0. 0.) (0. 1. 0.) (0. 0. 1.) ;
TG1 TD1  = @COUPLAN TD1   (1. 0. 0.) (1. 1. 0.) (1. 0. 1.) ;
TG1 TD1  = @COUPLAN TG1   (0. 0. 0.) (0. 0. 1.) (1. 0. 0.) ;
TG1 TD1  = @COUPLAN TD1   (0. 1. 0.) (0. 1. 1.) (1. 1. 0.) ;
TG1 TD1  = @COUPLAN TG1   (0. 0. 0.) (1. 0. 0.) (0. 1. 0.) ;
TG1 TD1  = @COUPLAN TD1   (0. 0. 1.) (1. 0. 1.) (0. 1. 1.) ;
*                                                                     *
* Recuperation des faces :                                            *
MAIL1    = TG1 . 'MAV' ;
TOL1     = 1.E-6 ;
TG1.FACE1 = MAIL1 'POIN' 'PLAN' (0. 0. 0.) (1. 0. 0.) (0. 1. 0.) TOL1 ;
TG1.FACE2 = MAIL1 'POIN' 'PLAN' (0. 0. 0.) (0. 0. 1.) (1. 0. 0.) TOL1 ;
TG1.FACE3 = MAIL1 'POIN' 'PLAN' (0. 0. 0.) (0. 1. 0.) (0. 0. 1.) TOL1 ;
TG1.FACE4 = MAIL1 'POIN' 'PLAN' (1. 0. 0.) (1. 1. 0.) (1. 0. 1.) TOL1 ;
TG1.FACE5 = MAIL1 'POIN' 'PLAN' (0. 1. 0.) (0. 1. 1.) (1. 1. 0.) TOL1 ;
TG1.FACE6 = MAIL1 'POIN' 'PLAN' (0. 0. 1.) (1. 0. 1.) (0. 1. 1.) TOL1 ;
*                                                                     *
* Trace resultat :                                                    *
'SI' ITRAC1 ;
  NBC1     = 'NBNO' TG1 . 'MPT' ;
  'TITR' ('CHAI' ' Polyedre a ' NBC1 ' cellules') ;
  'TRAC' TG1 . 'MAV' ;
'FINS' ;
*                                                                     *
* Sortie :                                                            *
'RESP' TG1 ;
*                                                                     *
'FINP' ;
*---------------------------------------------------------------------*
* FIN DE LA PROCEDURE @P_BOIT2                                        *
$$$$

