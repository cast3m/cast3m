$$$$ @LAKAPPA
* @LAKAPPA  PROCEDUR  KK2000    99/11/30    21:15:11     3720
*
************************************************************************
*                       PROCEDURA @LAKAPPA
************************************************************************
*
* Descrizione : trova i coefficenti correttivi del taglio K13 e K23,
*               lamina per lamina, e fornisce in uscita una tabella
*               identica a quella di INPUT, ma con i moduli G13 e G23
*               di ogni lamina opportunamente corretti.
*               I valori sono moltiplicati per 6/5 in modo da annullare
*               la correzione nel caso di materiali isotropi, gi…
*               apportata internamente a procedure di calcolo
*
* Sintassi    : TG = @LAKAPPA TAB_COMP
*
* Autore/Data : Miliozzi A. 10/04/1997
*
* Revisione   :
*
***********************************************************************
*
*--- INPUT
*=========
*
* TAB_COMP  tavola caratteristica dei laminati compositi
*
*--- OUTPUT
*==========
*
* TG   tavola caratteristica dei laminati compositi con i valori
*       G13 e G23 corretti
*
*
**********************************************************************
*
DEBPROC @LAKAPPA TCOMP*TABLE   ;
*
NZON = (DIME TCOMP) - 2  ;
N1 = 1 ;
REPETER LOOP1 NZON  ;
*
 TG = TCOMP ;
 N2 = 1 ;
 PD1 = PROG ; PD2 = PROG ;
 PZS = PROG ; PZI = PROG ;
 NZN1 = 0; DZN1 = 0 ;
 NZN2 = 0; DZN2 = 0 ;
 NLAM = (DIME TCOMP.N1) - 6 ;
*
 REPETER LOOP2 NLAM ;
*
   EX1 = TCOMP.N1.N2.EXC ;
   EP1 = TCOMP.N1.N2.EPA ;
   AN1 = TCOMP.N1.N2.ANG ;
   ID1 = TCOMP.N1.N2.IDM ;
*
   TCM  = TCOMP.MCOL ;
   Y1   = TCM.ID1.YG1  ; Y2 = TCM.ID1.YG2 ;
   N12  = TCM.ID1.NU12 ; G1 = TCM.ID1.G12 ;
*
   BETA = AN1  ;
*
*******************************************************
*   Ricavo le coordinate estreme di ogni lamina
*******************************************************
*
   ZS = EX1+(EP1 / 2) ;
   ZI = EX1-(EP1 / 2) ;
   POLD=PZS;
   PZS = INSERE POLD N2 ZS ; DETR POLD ;
   POLD=PZI;
   PZI = INSERE POLD N2 ZI ; DETR POLD ;
*
********************************************************
*    Calcolo i valori delle costanti ingegneristiche
*        nel sistema di riferimento globale
********************************************************
*
   CB = COS BETA;   SB = SIN BETA;
   YX = 1/(((CB**4)/Y1)+((1/G1)-((2*N12/Y1))*(SB**2)*(CB**2))
   +((SB**4)/Y2));
   YY = 1/(((SB**4)/Y1)+((1/G1)-((2*N12/Y1))*(SB**2)*(CB**2))
   +((CB**4)/Y2));
   NXY = (((N12/Y1)*((SB**4)+(CB**4)))-
   (((1/Y1)+(1/Y2)-(1/G1))*(SB**2)*(CB**2)))*YX ;
   GXY = 1/((2*((2/Y1)+(2/Y2)+(4*N12/Y1)-(1/G1))*(SB**2)*(CB**2))
   +(((SB**4)+(CB**4))/G1)) ;
   NYX = (YY/YX)*NXY ;
*
*******************************************************
*   Ricavo i valori D1 e D2 per ogni lamina
*******************************************************
*
   D1 = YX/(1-(NXY*NYX)) ;
   D2 = YY/(1-(NXY*NYX)) ;
   POLD=PD1;
   PD1 = INSERE POLD N2 D1 ; DETR POLD ;
   POLD=PD2;
   PD2 = INSERE POLD N2 D2 ; DETR POLD ;
*
*  Ricavo num e denom per il calcolo di z neutra
*
   NZN1 = (D1*((ZS**2)-(ZI**2)))+NZN1 ;
   DZN1 = (D1*EP1)+DZN1   ;
   NZN2 = (D2*((ZS**2)-(ZI**2)))+NZN2 ;
   DZN2 = (D2*EP1)+DZN2   ;
*
   N2 = N2 + 1;
 FIN LOOP2 ;
*
******************************************************
*       Calcolo la quota delle fibre medie
******************************************************
*
 ZN1 = 0.5*NZN1/DZN1 ;
 ZN2 = 0.5*NZN2/DZN2 ;
*
******************************************************
*      Calcolo SUM1 e SUM2 per ogni lamina
******************************************************
*
 PSUM1 = PROG ; PSUM2 = PROG ;
 POLD = PSUM1;
 PSUM1 = INSERE POLD 1 0. ; DETR POLD ;
 POLD=PSUM2;
 PSUM2 = INSERE POLD 1 0. ; DETR POLD ;
 N2 = NLAM ;
 SUM1 = 0.  ; SUM2 = 0. ;
 SI (NLAM > 1 ) ;
  REPETER LOOP3 (NLAM-1) ;
   ZS = EXTR PZS N2 ;
   ZI = EXTR PZI N2 ;
   D1 = EXTR PD1 N2 ;
   D2 = EXTR PD2 N2 ;
*
   SUM1 = (D1*((((ZS**2)-(ZI**2))/2)-(EP1*ZN1)))
   + SUM1                                                    ;
   SUM2 = (D2*((((ZS**2)-(ZI**2))/2)-(EP1*ZN2)))
   + SUM2                                                    ;
*
   N2 = N2-1 ;
   POLD=PSUM1;
   PSUM1 = INSERE POLD (NLAM+1-N2) SUM1 ; DETR POLD ;
   POLD=PSUM2;
   PSUM2 = INSERE POLD (NLAM+1-N2) SUM2 ; DETR POLD ;
  FIN LOOP3 ;
 FINSI;
 N2 = 1 ;
 R1 = 0 ; R2 = 0 ;
 I1TOT = 0 ; I2TOT = 0 ;
 PI1 = PROG ; PI2 = PROG ;
 REPETER LOOP4 NLAM ;
   D1 = EXTR PD1 N2 ;
   D2 = EXTR PD2 N2 ;
   ZS = EXTR PZS N2 ;
   ZI = EXTR PZI N2 ;
   SUM1 = EXTR PSUM1 (NLAM+1-N2) ;
   SUM2 = EXTR PSUM2 (NLAM+1-N2) ;
*
********************************************************
*           Calcolo R1 e R2
********************************************************
*
   R1 = (D1*((((ZS**3)-(ZI**3))/3)-(((ZS**2)-(ZI**2))*ZN1)+
   (EP1*(ZN1**2))))+R1                           ;

   R2 = (D2*((((ZS**3)-(ZI**3))/3)-(((ZS**2)-(ZI**2))*ZN2)+
   (EP1*(ZN2**2))))+R2                           ;
*
*
********************************************************
*      Calcolo Gxz eGyz rispetto al riferimento
*                 globale
********************************************************
*
   EP1 = TCOMP.N1.N2.EPA ;
   AN1 = TCOMP.N1.N2.ANG ;
   ID1 = TCOMP.N1.N2.IDM ;
*
   TCM  = TCOMP.MCOL ;
   G2   = TCM.ID1.G23  ; G3 = TCM.ID1.G13 ;
*
   BETA = AN1  ;
*
   CB = COS BETA;   SB = SIN BETA;
   GXZ = (G2*G3)/(((SB**2)*G3)+((CB**2)*G2)) ;
   GYZ = (G2*G3)/(((CB**2)*G3)+((SB**2)*G2)) ;
*
********************************************************
*           Calcolo I1 per ogni lamina
********************************************************
*
   A11 = (-1)*(D1/2) ;
   A21 = D1*ZN1 ;
   A31 = ((-1)*A11*(ZI**2))-(D1*ZI*ZN1)-SUM1 ;
   I1 = (((((ZS**5)-(ZI**5))*(A11**2))/5)+((((ZS**4)-(ZI**4))
   *A11*A21)/2)+((((A21**2)+(2*A11*A31))/3)*((ZS**3)-(ZI**3)))
   +(A21*A31*((ZS**2)-(ZI**2)))+(EP1*(A31**2)))
   /GXZ ;
*
   POLD=PI1;
   PI1 = INSERE POLD N2 I1 ; DETR POLD ;
*
********************************************************
*           Calcolo I2 per ogni lamina
********************************************************
*
   A12 = (-1)*(D2/2) ;
   A22 = D2*ZN2  ;
   A32 = ((-1)*A12*(ZI**2))-(D2*ZI*ZN2)-SUM2 ;
   I2 = (((((ZS**5)-(ZI**5))*(A12**2))/5)+((((ZS**4)-(ZI**4))
   *A12*A22)/2)+((((A22**2)+(2*A12*A32))/3)*((ZS**3)-(ZI**3)))
   +(A22*A32*((ZS**2)-(ZI**2)))+(EP1*(A32**2)))
   /GYZ                                          ;
*
   POLD=PI2;
   PI2 = INSERE POLD N2 I2 ; DETR POLD ;
*
*    Calcolo I1TOT e I2TOT
*
   I1TOT = I1 + I1TOT  ;
   I2TOT = I2 + I2TOT  ;
*
   N2 = N2+1 ;
 FIN LOOP4 ;
*
 N2 = 1 ;
 REPETER LOOP5 NLAM;
   I1 = EXTR PI1 N2 ;
   I2 = EXTR PI2 N2 ;
*
   EP1 = TCOMP.N1.N2.EPA ;
   AN1 = TCOMP.N1.N2.ANG ;
*
   BETA = AN1  ;
   CB = COS BETA;   SB = SIN BETA;
*
********************************************************
*       Calcolo i valori corretti per GXY e GYZ
********************************************************
*
   GXZ = ((R1**2)*I1)/(EP1*(I1TOT**2)) ;
   GYZ = ((R2**2)*I2)/(EP1*(I2TOT**2)) ;
*
*
*       e li ruoto per ciascuna lamina nel sistema
*             di riferimento ortotropo
*
   SI (ABS((CB**4)-(SB**4)) <EG 0.01) ;
    G1 = (GXZ + GYZ)*3/5 ;
    G2 = G1 ;
   SINON ;
    G1 = ((((CB**2)*GXZ)-((SB**2)*GYZ))/((CB**4)-(SB**4)))*6/5 ;
    G2 = ((((SB**2)*GXZ)-((CB**2)*GYZ))/((SB**4)-(CB**4)))*6/5 ;
   FINSI ;
*
   TG.N1.N2.G13 = G1 ;
   TG.N1.N2.G23 = G2 ;
   N2 = N2 + 1 ;
 FIN LOOP5 ;
*
N1 = N1 + 1 ;
FIN LOOP1 ;
*
************************************************************************
*                    FINE PROCEDURA @LAKAPPA
************************************************************************
*
FINPROC ;


