$$$$ PAS_RESU
* PAS_RESU  PROCEDUR  CB215821  18/08/31    21:15:03     9902
'DEBP' PAS_RESU TEMP0*'FLOTTANT' IAUTO*'ENTIER' PRECED*'TABLE' ;

  WTAB  = PRECED.'WTABLE';

* Recuperation de la table 'ESTIMATION' des champs en fin de pas :
  estim = PRECED.'ESTIMATION' ;

* remplissage de la table de 'CONTINUATION' a partir de 'ESTIMATION'
  'SI' ( 'EXIS' PRECED 'CONTINUATION' );
     conti= PRECED.'CONTINUATION';
  'SINON';
     conti='TABL' ;
     PRECED.'CONTINUATION' = conti;
  'FINSI';

  'SI' ('NEG' estim.'TEMPS' TEMP0 ('VALE' 'PREC'));
    'MESS' 'Temps differents dans PAS_RESU ?' ;
     estim.'TEMPS' = TEMP0 ;
  'FINS';

* Transfert des indices de la table "estim" dans "conti"
  DIM1  ='DIME' estim ;
  INDEX1='INDE' estim ;

  'REPE' SURi DIM1 ;
     ii     = &SURi;
     TYP1   ='TYPE' INDEX1.ii;
    'SI' ('NEG' TYP1 'MOT');
       MOT_tmp = INDEX1.ii ;
    'SINO';
       MOT_tmp ='MOT' INDEX1.ii;
    'FINS';
     conti.MOT_tmp = estim.MOT_tmp ;
  'FIN' SURi;

************************************************************************
*************** Sauvegarde eventuelle des resultats ********************
************************************************************************
*La sauvegarde n'est faite que si on a eu convergence des calculs :
   ISAUV = FAUX;
  'SI' WTAB.'CONV' ;
     ISAUV ='DANS' WTAB.'IPRESU' TEMP0 ;

    'SI' ('MULT' IAUTO WTAB.'NAURES' ) ;
       ISAUV = VRAI ;
    'FINSI' ;
  'FINSI' ;

  'SI' ISAUV ;
     NSOR        = WTAB.'NSOR' + 1 ;
     WTAB.'NSOR' = NSOR            ;

*    Cas particuliers de 'DEFIN' et 'DEFTO'
     TABSAUV = WTAB.'TABSAUV' ;
    'SI' ('NON' TABSAUV.'DEFTO');
      'SI' ('EXIS' estim 'DEFORMATIONS');
         estim ='ENLE' estim 'DEFORMATIONS';
      'FINS';
    'FINSI' ;

    'SI' ('NON' TABSAUV.'DEFIN') ;
      'SI' ('EXIS' estim 'DEFORMATIONS_INELASTIQUES');
         estim ='ENLE' estim 'DEFORMATIONS_INELASTIQUES';
      'FINS';
    'FINSI' ;
    'SI' ('EXIS' estim 'DFGRAD') ;
       estim ='ENLE' estim 'DFGRAD';
    'FINSI' ;

*    Transfert des indices de la table "estim" dans PRECED
     DIM1  ='DIME' estim;
     INDEX1='INDE' estim;

    'REPE' SURi DIM1 ;
       ii     = &SURi;
       TYP1   ='TYPE' INDEX1.ii;
      'SI' ('NEG' TYP1 'MOT');
         MOT_tmp = INDEX1.ii     ;
         OBJi    = estim.MOT_tmp ;

      'SINO';
         MOT_tmp ='MOT' INDEX1.ii;
         OBJi    = estim.MOT_tmp ;
        'SI' ('EGA' TYP1 'TABL');
          'SI' ('EXIS' estim.MOT_tmp 'SOUSTYPE');
            'SI' ('EGA' estim.MOT_tmp.'SOUSTYPE' 'ESCLAVE');
               ERRE 21;
               OBJi ='ETG' estim.MOT_tmp ;
            'FINS';
          'FINS';
        'FINS';
      'FINS';
      'SI'('EXIS' PRECED MOT_tmp);
         PRECED.MOT_tmp.NSOR = estim.MOT_tmp ;
      'SINO';
         CHAI1 ='CHAI' 'Indice "' MOT_tmp '" non present dans la table PASAPAS' ;
        'ERRE' CHAI1 ;
      'FINS';
    'FIN' SURi;

*-----------------------------------------------------------------------
*---  1- Sauvegarde incrementale sous certaines conditions
*---  2- Fantomisation (option 'ECONOMIQUE'=VRAI de PASAPAS) pour liberer la memoire
*-----------------------------------------------------------------------
    'SI' WTAB.'ISAUVEGL';
       ltpssauv = WTAB.'ISAUVEGA' ;
      'SI' ('DANS' ltpssauv TEMP0) ;
        'SI' ('NON' ('EXIS' WTAB ('MOT' 'EPOQUE'))) ;
           WTAB.'EPOQUE' = DATE 'EPOCH' ;
        'FINS' ;
         EPOQ1 ='DATE' 'EPOCH' ;

*        Sauvegarde : Dernier pas OU toutes les 300 secondes OU si DATE ne marche pas
*        Fantomisation si on sauvegarde effectivement !
        'SI' (     ('EGA' TEMP0 ('EXTR' ltpssauv ('DIME' ltpssauv)))
              'OU' ((EPOQ1 '-' WTAB.'EPOQUE') '>EG' 300.D0)
              'OU' ((WTAB.'EPOQUE' 'EGA' 0.D0) 'ET' (EPOQ1 'EGA' 0.D0))) ;

          'SAUV' 'MUET' PRECED ;
           WTAB.'EPOQUE' ='DATE' 'EPOCH' ;

          'SI' WTAB.'LECONOMI';
             NFFSOR = NSOR '-' WTAB.'NFSOR' '-' 1 ;
            'SI' ( NFFSOR '>' 0) ;

*              Boucle sur les instants SAUVES depuis la derniere fantomisation
              'REPE' DETRES  NFFSOR ;
                 NSORM2 = WTAB.'NFSOR' - 1 + &DETRES ;
                'SI' (NSORM2 '<' 0) ;
                  'ITER' DETRES ;
                'FINSI' ;

*                Boucle sur les indices a fantomiser
                'REPE' SURi DIM1 ;
                   ii     = &SURi;
                   TYP1   ='TYPE' INDEX1.ii;
                  'SI' ('NEG' TYP1 'MOT');
                     MOT_tmp = INDEX1.ii     ;
                  'SINO';
                     MOT_tmp ='MOT' INDEX1.ii;
                  'FINS';
                  'FANTOM' PRECED.MOT_tmp NSORM2 ;
                'FIN' SURi;
              'FIN' DETRES ;
               WTAB.'NFSOR'= WTAB.'NFSOR' + NFFSOR ;
            'FINSI' ;
          'FINS' ;
        'FINSI';
      'FINSI';
    'FINSI';
  'FINSI' ;
'FINP' ISAUV ;
$$$$

