$$$$ FOR_CONT
* FOR_CONT  PROCEDUR  FANDEUR   14/10/10    21:15:14     8178
 'DEBPROC' FOR_CONT GEO*'MAILLAGE' SOL1*'CHPOINT' COURI*'FLOTTANT' ;
************************************************************************
*      MAGNETOSTATIQUE  2D POTENTIEL VECTEUR
*   GEO   maillage
*   SOL1   SOLTUTION EN POTENTIEL  DEFINIE AU MOINS SUR BLOC
*      calcul des forces par integrale de contour
*   COURI   densite de courant sur geo  ( constante )
*   LOG1  logique valant vrai si pb axisym  ( defaut  pb plan
*      SORTIE
*             RESULTANTE DE LA FORCE AU CDG DE GEO
*             MOMENT PAR RAPPORT A L ORIGINE
*     EN  PLAN   FORCES PAR UNITE DE LONGUEUR
*     EN AXISYMETRIQUE  FORCES / RADIAN (attention signification )
************************************************************************
 CCONT = 'CONTOUR' ('ORIENTER' GEO ) ;
 OBSEG = CCONT 'MODE'  'THERMIQUE' 'ISOTROPE' ;
 AA = 'CHAN' 'CHAM' SOL1  OBSEG  'GRAVITE';
 NBSEG = CCONT 'NBEL' ;
 IEL = 0 ;SOMX = 0. ; SOMY = 0. ;SMM = 0. ;
*
 'REPETER' BOUC NBSEG ;
 IEL = IEL + 1 ; SEGC= CCONT 'ELEM' IEL ;
 I1 = SEGC 'POINT' 'INITIAL' ; I2 = SEGC 'POINT' 'FINAL' ;
 X1 = 'COOR' 1 I1 ; X2 = 'COOR' 1 I2 ; RX = ( X1 + X2) / 2. ;
 Y1 = 'COOR' 2 I1 ; Y2 = 'COOR' 2 I2 ; RY = ( Y1 + Y2) / 2. ;
 DX = X2 - X1 ; DY = Y2 - Y1 ;
 AME = 'EXTR' AA 'T'  1 IEL 1 ;
 SOMX = SOMX + ( AME * DX) ;
 SOMY = SOMY + ( AME * DY) ;
 MOMM = (RX * DY) + (RY * DX ) ;
 SMM= SMM + ( AME * MOMM ) ;
 'FIN' BOUC ;
*
 FXX = COURI * SOMY ; FYY = -1. * COURI * SOMX ;
 MOMT = -1. * COURI * SMM ;
 CDG1 = 'BARY' CCONT ;
 RFORC = 'MANU' 'CHPO' CDG1 2 'FX' FXX 'FY' FYY ;
 'FINPROC' RFORC SMM ;


