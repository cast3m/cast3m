$$$$ TOPOCRIT
* TOPOCRIT  PROCEDUR  CB215821  18/03/22    21:15:02     9786

************************************************************************
** Procedure called by TOPOPTIM for updating the topology using the
** Optimality Criterion method
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** 2.1 2018/01/29 Fixing an issue with quadratic elements
** 2.0 2017/11/11
************************************************************************

DEBP TOPOCRIT tab0*'TABLE' ;
*   Input data
    un1 = tab0.'WTABLE'.'UN'.(1) ;
    d0 = tab0.'WTABLE'.'FACTEUR_D' ;
    move0 = un1 * tab0.'TOPOLOGIE_MAX_INC' ;
    void0 = tab0.'TOPOLOGIE_MIN' ;
    xOld0 = tab0.'WTABLE'.'TOPOLOGIE' ;
    xOldP0 = xOld0 + move0 ;
    xOldM0 = xOld0 - move0 ;
    dc0 = tab0.'WTABLE'.'SENSIBILITE' ;
    q0 = tab0.'WTABLE'.'FACTEUR_Q' ;
    mcn0 = EXIS tab0 'MECANISME' ;
    mecha0 = EXIS tab0.'WTABLE' 'MECANIQUE' ;
    SI mecha0 ;
        mod1 = tab0.'WTABLE'.'MECANIQUE'.'MODELE'.(1) ;
        mat1 = tab0.'WTABLE'.'MECANIQUE'.'CARACTERISTIQUES'.(1) ;
    SINO ;
        mod1 = tab0.'WTABLE'.'THERMIQUE'.'MODELE'.(1) ;
        mat1 = tab0.'WTABLE'.'THERMIQUE'.'CARACTERISTIQUES'.(1) ;
    FINS ;
    v1 = tab0.'WTABLE'.'VOLUME'.(1) * tab0.'FRACTION_VOLUME' ;
    l1 = tab0.'OC_L1' ;
    l2 = tab0.'OC_L2' ;
    l2min = tab0.'OC_L2_MIN' ;
    crit0 = tab0.'OC_CRITERE' ;
    maxit0 = tab0.'OC_MAX_IT' ;
****    SI mcn0 ;
        bmin0 = tab0.'OC_B_MIN' ;
****    FINS ;
*    x0 = tab0.'WTABLE'.'UN'.(1) * 0.0 ;
*   Update the topology
    REPE loop1 maxit0 ;
        SI ((((l2 - l1) / (l1 + l2)) > crit0) ET (l2 > l2min)) ;
            lmid0 = 0.5 * (l2 + l1) ;
            tmp1 = dc0 * (-1.0 / lmid0) ;
*           Negative values does not only happen for compliant
*           mechanism synthesis but also for elastic strain energy
*           minimization with quadratic elements
****            SI mcn0 ;
                tmp1 = BORN tmp1 'SCAL' 'MINI' bmin0 ;
****            FINS ;
            tmp1 = xOld0 * (tmp1**d0) ;
            SI (q0 > 1) ;
                tmp1 = tmp1**q0 ;
            FINS ;
            ma1 = tmp1 MASQ 'INFE' xOldP0 ;
            tmp1 = (tmp1 * ma1) - (xOldP0 * (ma1 - un1)) ;
            tmp1 = BORN tmp1 'SCAL' 'MAXI' 1.0 ;
            ma1 = tmp1 MASQ 'SUPE' xOldM0 ;
            tmp1 = (tmp1 * ma1) - (xOldM0 * (ma1 - un1)) ;
            x0 = BORN tmp1 'SCAL' 'MINI' void0 ;
            tmp1 = INTG x0 mod1 mat1 ;
            SI (tmp1 > v1) ;
                l1 = lmid0 ;
            SINO ;
                l2 = lmid0 ;
            FINS ;
        SINO ;
            QUIT loop1 ;
        FINS ;
    FIN loop1 ;
*   Save the new topology
    tab0.'WTABLE'.'TOPOLOGIE' = x0 ;
FINP ;

$$$$

