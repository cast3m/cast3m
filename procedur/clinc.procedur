$$$$ CLINC
* CLINC     PROCEDUR  MAGN      18/06/08    21:15:05     9840
'DEBPROC' CLINC rxt*'TABLE' rtf*'TABLE' TKAS*'LOGIQUE' NIC*MOT Nj*MOT
 Nj1*'FLOTTANT' Nj2*'FLOTTANT' Nj3*'FLOTTANT'                          ;
*======================================================================;
* RESOLUTION DE LA TEMPERATURE DU FLUIDE TF
* Definition des températures TBPW et TBP pour ECHI
*                             TBPW température paroi (THERMP)
* Definition des CLIM pour l'injection et ajout
* des CLIM pour température paroi imposée
* Le terme source STF est calcule avant (fin pas precedent)

TBT = rxt.'TBT';
TIC = rxt.'TIC';
GEO = rxt.'GEO';
mess ' CLINC                            NIC=' NIC Nj ;

'SI' ((TBT.'TPAROIF') et ('EGA' NIC 'TF'))                     ;
     TBPW       = 'REDU' (tic.'TP') GEO.'paroif'               ;
     tic.'TBPW' = 'KCHT' GEO.'$paroif' 'SCAL' 'SOMMET' TBPW    ;
     TBFW       = 'REDU' (tic.NIC) GEO.'paroif'                ;
     tic.'TBFW' = 'KCHT' GEO.'$paroif' 'SCAL' 'SOMMET' TBFW    ;
'FINSI'                                                        ;

*-- On traite l'injection comme une C.L. en température
cpv  matxx= 'KOPS' 'MATRIK' ;
  Si (TBT.'TTsortie')                                      ;
     NBR=dime rxt.'Sorties'                                ;
     ibr=index (rxt.'Sorties')                             ;
    Repeter BBRS NBR                                       ;
     Briches=rxt.'Sorties'.(ibr.&BBRS)                     ;
*    mess ' Sorties CL pour T '   (ibr.&BBRS)              ;
     brecheis = Briches.'srechei'                          ;
Si (NON Briches.'TTOUT')                                   ;
 cpv = cpv et ('MANU' 'CHPO' brecheis 1 NIC
                 (Briches.Nj) 'NATURE' 'DISCRET')        ;
Finsi                                                      ;
    FIN BBRS                                               ;
  FINSI                                                    ;
  Si (TBT.'TTbreche')                                      ;
     NBR=dime rxt.'Breches'                                ;
     ibr=index (rxt.'Breches')                             ;
    Repeter BBRS NBR                                       ;
     Briches=rxt.'Breches'.(ibr.&BBRS)                     ;
*    mess ' Breches CL pour T '   (ibr.&BBRS)              ;
     brecheis = Briches.'brechei'                          ;
 cpv = cpv et ('MANU' 'CHPO' brecheis 1 NIC
                 (Briches.Nj) 'NATURE' 'DISCRET')        ;
    FIN BBRS                                               ;
  FINSI                                                    ;
Si (TBT.'Tbreche') ;
 cpv = cpv et ('MANU' 'CHPO' brechei 1 NIC
                            Nj1 'NATURE' 'DISCRET')       ;
Finsi ;
Si (TBT.'Tbreche2') ;
 cpv = cpv et ('MANU' 'CHPO' brech2i 1 NIC
                            Nj2 'NATURE' 'DISCRET')       ;
Finsi ;
Si (TBT.'Tbreche3') ;
 cpv = cpv et ('MANU' 'CHPO' brech3i 1 NIC
                            Nj3 'NATURE' 'DISCRET')       ;
Finsi ;

 Si ((TBT.'Tbreche') ou (TBT.'Tbreche2') ou (TBT.'Tbreche3')
       ou TBT.'TTbreche' ou TBT.'TTsortie');
   Si (TKAS et ('EXIST' rtf 'CLIM'))                      ;
     rtf.'CLIM'    = rtf.'CLIM' '+' cpv                   ;
   Sinon                                                  ;
     rtf.'CLIM' = cpv                                     ;
   Finsi                                                  ;
 Finsi ;

 'SI' (TBT.'TRECOMB' et ('EGA' NIC 'TF'))                 ;
   cpvrec  matxx= 'KOPS' 'MATRIK' ;

   NBREC = 'DIME' rxt.'RECOMB' ;
   'REPETER' BCLREC NBREC ;
      NAME = 'CHAIN' 'REC' 'sorI' &BCLREC ;
      SORTIEi = GEO.NAME ;
      NRECT = 'DIME' RXT.'TIC'.'RECOMB'.&BCLREC.'Tout' ;
      RTS0  = 'EXTR' RXT.'TIC'.'RECOMB'.&BCLREC.'Tout' NRECT ;

     cpvrec = cpvrec 'ET' ('MANU' 'CHPO' SORTIEi 1 NIC
                         RTS0 'NATURE' 'DISCRET')        ;
   'FIN' BCLREC ;

  'SI' ((TBT.'Tbreche') 'OU' (TBT.'Tbreche2') 'OU' (TBT.'Tbreche3')
          ou TBT.'TTbreche' ou TBT.'TTsortie') ;
     rtf.'CLIM'    = rtf.'CLIM' '+' cpvrec ;
  'SINON' ;
     rtf.'CLIM'    = cpvrec ;
  'FINSI' ;
 'FINSI' ;


FINPROC;

