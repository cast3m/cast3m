$$$$ @TOLE3
* @TOLE3    PROCEDUR  FANDEUR   14/10/10    21:15:01     8178
'DEBP'  @TOLE3   A_1*'MAILLAGE' A_2*'MAILLAGE' RA_1*'FLOTTANT'
N_1*'ENTIER' ;
ME = 'TEXT' 'MECANIQUE ELASTIQUE' ;
'OPTI' 'ELEM' 'CUB8' ;
S_1 = A_1 'REGL' 1 A_2 ;
    'OPTI' 'ELEM' 'PRI6' 'MODE' 'TRIDIM' ;
    AF_1 = 'MODE' S_1 ME 'COQ4' ;
    VE_1 = 'PRES' 'COQU' AF_1 1. 'NORM' ;
    VE_1 = 'REDU' VE_1 S_1 ;
    V1NX = 'EXCO' 'FX' VE_1 'SCAL' ;
    V1NY = 'EXCO' 'FY' VE_1 'SCAL' ;
    V1NZ = 'EXCO' 'FZ' VE_1 'SCAL' ;
    N1VN = ((V1NX ** 2) + (V1NY ** 2) + (V1NZ ** 2)) ** .5 ;
    V1UNX = V1NX / N1VN ;
    V1UNY = V1NY / N1VN ;
    V1UNZ = V1NZ / N1VN ;
    R1X = V1UNX * RA_1 ;
    R1Y = V1UNY * RA_1 ;
    R1Z = V1UNZ * RA_1 ;
    TR_1 = ('NOMC' UX R1X) + ('NOMC' UY R1Y) + ('NOMC' UZ R1Z) ;
    L_1 = (A_1 'PLUS' TR_1) ;
    L_2 = (A_2 'PLUS' TR_1) ;
NN1 =  'NBNO' A_1 ;
NN2 = NN1 / 2 ;
PA_11 = A_1 'POIN' 1 ;
PA_12 = A_1 'POIN' NN2 ;
PA_13 = A_1 'POIN' NN1 ;
PA_21 = A_2 'POIN' 1 ;
PA_22 = A_2 'POIN' NN2 ;
PA_23 = A_2 'POIN' NN1 ;
PA_11X = PA_11 COOR 1 ;
PA_11Y = PA_11 COOR 2 ;
PA_31 =  PA_11X PA_11Y 1. ;
P_1 = 'TABL' ;
P_2 = 'TABL' ;
*
I = 1 ;
  'REPE' BOU100 NN1 ;
  PP1 = L_1 'POIN' I ;
  PP2 = L_2 'POIN' I ;
  DP1 = PP2 'MOIN' PP1 ;
  P_1.I = PP1 'PROJ' 'CYLI' DP1 'PLAN' PA_11 PA_12 PA_13 ;
  P_2.I = PP2 'PROJ' 'CYLI' DP1 'PLAN' PA_21 PA_22 PA_23 ;
  I = I + 1 ;
  'FIN' BOU100 ;
*
I = 1 ;
  'REPE' BOU101 NN1 ;
  P_1IY = P_1.I 'COOR' 2 ;
    'SI' (P_1IY < PA_11Y) ;
    DP_1 = P_1.I 'MOIN' P_1.(I + 1) ;
    P_1.I = P_1.I 'PROJ' 'CYLI' DP_1 'PLAN' PA_31 PA_11 PA_21 ;
    P_1.I = P_1.I 'PLUS' ((0. 0. 1.E-5) * (I - 1)) ;
    'SINON' ;
    'QUIT' BOU101 ;
    'FINSI';
  I = I + 1 ;
  'FIN' BOU101 ;
*
I = 1 ;
  'REPE' BOU103 NN1 ;
  P_2IY = P_2.I 'COOR' 2 ;
    'SI' (P_2IY < PA_11Y) ;
    DP_1 = P_2.I 'MOIN' P_2.(I + 1) ;
    P_2.I = P_2.I 'PROJ' 'CYLI' DP_1 'PLAN' PA_31 PA_11 PA_21 ;
    P_2.I = P_2.I 'PLUS' ((0. 0. 1.E-5) * (I - 1)) ;
    'SINON' ;
    'QUIT' BOU103 ;
    'FINSI' ;
  I = I + 1 ;
  'FIN' BOU103 ;
*
I = 2 ;
  'REPE' BOU102 (NN1 - 1) ;
  'SI' ('EGA' I 2) ;
  C_1 = P_1.(I - 1) 'DROI' 1 P_1.I ;
  C_2 = P_2.(I - 1) 'DROI' 1 P_2.I ;
  'FINSI' ;
  'SI' (I > 2) ;
  C_1 = C_1 'ET' (P_1.(I - 1) 'DROI' 1 P_1.I) ;
  C_2 = C_2 'ET' (P_2.(I - 1) 'DROI' 1 P_2.I) ;
  'FINSI' ;
  I = I + 1 ;
  'FIN' BOU102 ;
A_1 = A_1 'COUL' 'ROUG' ;
A_2 = A_2 'COUL' 'ROUG' ;
C_1 = C_1 'COUL' 'ROUG' ;
C_2 = C_2 'COUL' 'ROUG' ;
S_2 = C_1 'REGL' 1 C_2 ;
V_1 = S_1 'VOLU' N_1 S_2 ;
V_1 = V_1 'COUL' 'ROUG' ;
 'DETR' VE_1 ; 'DETR' V1NX ; 'DETR' V1NY ; 'DETR' V1NZ ;
 'DETR' V1UNX ; 'DETR' V1UNY ; 'DETR' V1UNZ ;
 'DETR' R1X ; 'DETR' R1Y ; 'DETR' R1Z ; 'DETR' TR_1 ;
'FINP'  C_1 C_2 S_1 S_2 V_1 ;
$$$$


