$$$$ HTC_WWW
* HTC_WWW   PROCEDUR  AM        11/09/13    21:15:04     7126
*-----------------------------------------------------------------------
*23456789012345678901234567890123456789012345678901234567890123456789012
*-----------------------------------------------------------------------
*-------------------------------------------------
* HTC_WWW Specific water content of concrete
* Sorption isotherms (after BAZANT and THONGUTHAI)
* Date: 16-03-98
*--------------------------------------------------
'DEBP' HTC_WWW T0*CHPOINT P01*CHPOINT TAV1*'TABLE';
*--------------------------------------------------
*
 FLG1  = TAV1.'FLG1';
'SI' FLG1; 'MESS' 'PROCEDURE-HTC_WWW'; 'FINSI';
 EPS1 = TAV1.'EPSILON';
*Temperature °C
 TREF=25.;
*Pressure Mpa
 PREF=0.10135;
* Water specific volume [mc/kg] at 25 C and atmospheric pressure
 SV0=0.0010029;
* Water density [kg/mc] at 25 C and atmospheric pressure
 RHOW0=1./SV0;
* Concrete properties
*Kg/mc
 W1=TAV1. 'W1';
 CC=TAV1. 'C';
* Porosity at 25 °C
 N0=W1*SV0;
* Coefficient of linear thermal dilatation[ /°C]
 ALFA=TAV1.'ALFA';
* Bulk modulus
 EBM =TAV1.'EBM';
 BM  ='VARI' T0 EBM 'SCAL';
*
 PSAT=HTC_WTR TAV1 'SATU' T0;
*
 CCC=P01 'MASQUE' 'EGINFE' EPS1;
'SI' (('MAXI' CCC) > .9);
 'MESS' 'LOW OR NEGATIVE PRESSURE';
*  MAGLIA = 'EXTR' MODL1 'MAIL';
*  titr 'P01' ISTANT ITER ;
*  trac MAGLIA p01;
  P0=(EPS1*CCC)+(P01*(1.-CCC));
'SINON';
  P0=P01;
'FINSI';
'DETR' CCC;
*
 HR=P0/PSAT;
 RESO1   = HR 'MASQUE' 'EGINFE' 0.93 ;
 RESO2   = HR 'MASQUE' 'EGSUPE' 1.06 ;
 RESO31  = 1. - ( RESO1 + RESO2 );
 RESO3   = RESO31  'MASQUE' 'EGSUPE' 0.1 ;
 H94   = 'MAXI' RESO1;
 H15   = 'MAXI' RESO2;
 H9415 = 'MAXI' RESO3;
*
 WN=0.;WS=0.;WT=0.;
*
*  Water content Wn at HR  .9) 'OU' (H9415 > .9));
  TP  = (( T0 + 10.) / ( TREF + 10.))**2.;
  MT  = 1.04 - ( TP / ( 22.34 + TP ));
'FINSI';
*
'SI'(H94 > .9);
  EWC = 'LOG' ((W1/CC) * HR );
  WN1 = CC*('EXP'(EWC/MT));
  WN  = RESO1 * WN1;
'FINSI';
*
*  Water content Ws at HR >eg 1.06
*
'SI'((H15 > .9) 'OU' (H9415 > .9));
  WD AAA=HTC_CHBW T0 HR TAV1  ;DETR AAA;
  RHOW=HTC_WTR TAV1 'RHOW' T0 P0;
* N = N0;
* N = N0 + A (T0 -TREF);
  N =(N0 + ( WD / RHOW0));
  ALFT1 = 3. * ALFA * (T0-TREF);
'FINSI';
'SI'( H15 > .9);
  PH  = 1. + ( 0.12 * ( HR - 1.04 ));
  NA  = N * PH ;
  P0MR = P0-PREF;
  RESO4= P0MR 'MASQUE' 'EGSUPE'  0.;
  WS1 = (1. + (((RESO4 * NA * P0MR / BM) +
        ALFT1))) * NA * RHOW;
  WS = RESO2 * WS1;
'FINSI';
*-----------------------------------------------
* Water content Wt for  0.93 < HR > 1.06
* Transition range by bezier spline interpolation
*-----------------------------------------------
*
* Water content for H = 0.93 by first correlation
*
'SI'(H9415  > .9);
  EWC93 = 'LOG' (W1 * 0.93 / CC) ;
  WN931 = CC*('EXP'(EWC93*(MT**-1.))) ;
  WN93 = RESO3 * WN931;
*
* Water content for H = 1 by first correlation
*
  EWC100= 'LOG' (W1 * 1.0 / CC) ;
  WN1001 = CC*('EXP'(EWC100*(MT**-1.))) ;
  WN100 = RESO3 * WN1001;
*
* Water content for H = 1 by second correlation
*
  P1  = 1.0 * PSAT;
  RHOW1= HTC_WTR TAV1 'RHOW' T0 P1;
  P1MR = P1-PREF ;
  RESO5 = P1MR  'MASQUE' 'EGSUPE'  0.;
  WS1001 = (1. + (((RESO5 * N * P1MR / BM) +
          ALFT1)))*N*RHOW1;
  WS100 = RESO3 * WS1001;
*
* Water content for H = 1.06 by second correlation
*
  P16  = 1.06 * PSAT;
  RHOW16= HTC_WTR TAV1 'RHOW' T0 P16;
  P16MR = P16-PREF ;
  RESO5 = P16MR  'MASQUE' 'EGSUPE'  0.;
  WS161 = (1. + (((RESO5 * N * P16MR / BM) +
          ALFT1)))*N*RHOW16;
  WS16 = RESO3 * WS161;
*-----------------------------------------------
*  EQUAZIONI DELL' INTERP. DI BEZIER CON 4 PUNTI
*-----------------------------------------------
'SI' ('NON'('EXIST' TAV1 'HINT'));
*
  HHP  = 'PROG' ;
  UUP  = 'PROG' 0. PAS 0.001 1.;
 'REPE' CIC1 ('DIME' UUP) ;
   U   = 'EXTR' UUP &CIC1;
*
* EQUAZIONE GENERALE PER L'ASCISSA H :
* h(t)=(h4 - 3h3 + 3h2 - h1)t**3 + (3h3 - 6h2 + 3h1)t**2
* + (3h2 - 3h1)t + h1
*
   HU  = (0.13*(U**3)) - (0.21*(U**2)) + (0.21*U) + 0.93 ;
   HHP = 'INSE' HHP HU &CIC1 ;
 'FIN' CIC1;
  HHP=('PROG' 0.) 'ET' HHP 'ET' ('PROG' 20.);
  UUP=('PROG' 0.) 'ET' UUP 'ET' ('PROG'  0.);
  TAV1 . 'HINT' = 'EVOL' 'MANU' HHP 'SCAL' UUP 'SCAL';
'FINSI';
*
 U = 'VARI'  (TAV1 .'HINT') HR 'SCAL';
 WT1 =  U*(U*(U*(WS16-(3*WS100)+(3*WN100)-WN93)+
               ((3*WS100)-(6*WN100)+(3*WN93)))+
               ((3*WN100)-(3*WN93)))+WN93;
 WT = RESO3 * WT1 ;
'FINSI';
*
*  Water content WW
*
 WW =WN+WS+WT;
*
'FINPROC' WW  HR;
$$$$


