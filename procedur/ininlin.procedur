$$$$ ININLIN
* ININLIN   PROCEDUR  GOUNAND   05/12/21    21:33:04     5281
************************************************************************
* NOM         : ININLIN
* DESCRIPTION : Initialise les tables arguments pour ininlin
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : gounand@semt2.smts.cea.fr
**********************************************************************
* VERSION    : v1, 10/05/2004, version initiale
* HISTORIQUE : v1, 10/05/2004, création
* HISTORIQUE : v2, 12/12/2005, version pour nlin final
* HISTORIQUE :
************************************************************************
*
*
'DEBPROC' ININLIN nop*'ENTIER'
                 nvar*'ENTIER'
                 ndat*'ENTIER'
                 ncof*'ENTIER'
                 vdim*'ENTIER' ;
   A = 'TABLE' ;
   A . 'NUMOP'  = nop  ;
   A . 'NUMVAR' = nvar ;
   A . 'NUMDAT' = ndat ;
   A . 'NUMCOF' = ncof ;
   A . 'NUMDER' = vdim ;
*
   'SI' ('<EG' nvar 0) ;
      cherr = 'nvar =' nvar ' (should be > 0)' ;
      'ERREUR' cherr ;
   'FINSI' ;
   A . 'VAR' = 'TABLE' ;
   'REPETER' ivar nvar ;
      A . 'VAR' . &ivar = 'TABLE' ;
   'FIN' ivar ;
   'SI' ('<' ndat 0) ;
      cherr = 'ndat =' ndat ' (should be >= 0)' ;
      'ERREUR' cherr ;
   'FINSI' ;
   A . 'DAT' = 'TABLE' ;
   'SI' ('>' ndat 0) ;
      'REPETER' idat ndat ;
         A . 'DAT' . &idat = 'TABLE' ;
      'FIN' idat ;
   'FINSI' ;
*
   'SI' ('<' ncof 0) ;
      cherr = 'ncof =' ncof ' (should be >= 0)' ;
      'ERREUR' cherr ;
   'FINSI' ;
   A . 'COF' = 'TABLE' ;
   'SI' ('>' ncof 0) ;
      'REPETER' icof ncof ;
         A . 'COF' . &icof = 'TABLE' ;
      'FIN' icof ;
   'FINSI' ;
*
   'REPETER' iop nop ;
      A . &iop = 'TABLE' ;
      'REPETER' ivar nvar ;
         A . &iop . &ivar = 'TABLE' ;
*         'REPETER' idim ('+' vdim 1) ;
*            A . &iop . &ivar . ('-' &idim 1) = 'TABLE' ;
*         'FIN' idim ;
      'FIN' ivar ;
   'FIN' iop ;
*
'FINPROC' A ;
*
* End of procedure file ININLIN
*

