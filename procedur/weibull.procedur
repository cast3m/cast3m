$$$$ WEIBULL
* WEIBULL   PROCEDUR  CHAT      96/06/07    21:15:07     2167
* WEIBULL
*--------------------------------------------------------------
*
*  PROBABILITY OF FAILURE FOR BRITTLE MATERIALS
*  WITH THE FOLLOWING EXPRESSION
*
*  SEGUENTE :  PR= 1 - EXP( -V0 * < ((SIG - SIGU)/SIG0)**N > )
*
*  Weibull Weakest Link Theory
*  Principle of Indipendent Action (PIA)
*--------------------------------------------------------------
*    ENTREE  DANS L'ORDRE
*
*      ZSIG     CHAMP PAR ELEMENT DE CONTRAINTES
*      ZOBAFF   OBJET MODELE ASSOCIE A ZSIG
*      ZV0      FATTORE CHE TIENE CONTO DEL
*               VOLUME D'INTEGRAZIONE EFFETTIVO
*      ZSIGU    TENSIONE LIMITE
*      ZSIG0    TENSIONE DI NORMALIZZAZIONE
*      ZXN      Modulo di WEIBULL
*
*    SORTIE DANS L'ORDRE
*
*      ZXP     PROBABILITE
*      ZSIGF   CHAMP PAR ELEMENT  QU ON INTEGRE
*
*--------------------------------------------------------------
DEBPROC WEIBULL
  ZSIG*MCHAML     ZOBMO*MMODEL
  ZV0*FLOTTANT    ZSIGU*FLOTTANT
  ZSIG0*FLOTTANT  ZXN*FLOTTANT ;
*--------------------------------------------------------------
imode1 = ega (vale mode) 'AXIS' ;
*
* Principal stresses
tpr = prin zsig zobmo ;
t1  = exco si11 tpr   ;
t2  = exco si22 tpr   ;
t3  = exco si33 tpr   ;
*
* Tensile stresses only
COEF11 = T1 MASQUE SUPERIEUR ZSIGU ;
COEF22 = T2 MASQUE SUPERIEUR ZSIGU ;
COEF33 = T3 MASQUE SUPERIEUR ZSIGU ;
*
SI1   = COEF11 * T1 * (1. / ZSIG0) ;
SII1  = COEF22 * T2 * (1. / ZSIG0) ;
SIII1 = COEF33 * T3 * (1. / ZSIG0) ;
*
SITR   = REDUIRE SI1   ZOBMO ;
SIITR  = REDUIRE SII1  ZOBMO ;
SIIITR = REDUIRE SIII1 ZOBMO ;
*
* Calculation of the integral
SIG2 = ( SITR ** ZXN ) + (SIITR ** ZXN ) + ( SIIITR ** ZXN ) ;
SI IMODE1 ;
  zr   = coor 1 zsig ;
  sig2 = sig2 * zr   ;
FINSI;
ZSIGF= SIG2 * ZV0 ;

XPP  = INTG ZOBMO ZSIGF si11;
xpp= xpp + (INTG ZOBMO ZSIGF si22);
xpp = xpp + (INTG ZOBMO ZSIGF si33);
*
* On volume integral value, probability of survival and probability
* of failure.
XPM = -1. * XPP ;
XPQ = EXP  XPM  ;
ZXP = 1. - XPQ  ;
*
FINPROC  ZXP ZSIGF ;
*

