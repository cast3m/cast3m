$$$$ @DEDUIRE
* @DEDUIRE  PROCEDUR  GOUNAND   05/10/10    21:15:01     5199
debproc @deduire geo1*'MAILLAGE'   maianc*'MAILLAGE'
mainouv*'MAILLAGE';
iidim= 'VALE' dime;
'SI' ( 'EGA' iidim 3) ;
xs ys zs = 'COOR' mainouv;
xv yv zv = 'COOR' maianc;
'SINO';
xs ys  = 'COOR' mainouv;
xv yv  = 'COOR' maianc;
'FINS';
mais = 'EXTR' xs 'MAILLAGE';
maiv = 'EXTR' xv 'MAILLAGE';
nbp = 'NBEL' mais;
'SI' ( 'EGA' iidim 3);
 'REPETER' bouc3 nbp;i = 'POIN' &bouc3 mais;j = 'POIN' &bouc3 maiv;
 xi yi zi = 'COOR' i;xj yj zj = 'COOR' j;tx = xi - xj;
 ty = yi - yj;tz = zi -zj;
 mx = 'MANU' 'CHPO' j 1 'T' tx 'NATURE' 'DIFFUS';
 my = 'MANU' 'CHPO' j 1 'T' ty 'NATURE' 'DIFFUS';
 mz = 'MANU' 'CHPO' j 1 'T' tz  'NATURE' 'DIFFUS';
 'SI' ( &bouc3 'EGA' 1);
  mtx = mx; mty = my ;mtz= mz; 'SINO';
  mtx = mtx 'ET' mx; mty = mty 'ET' my; mtz = mtz 'ET' mz;
 'FINS';
 'FIN' bouc3;
'SINO';
 'REPETER' bouc2 nbp;i = 'POIN' &bouc2 mais;j ='POIN' &bouc2 maiv;
 xi yi = 'COOR' i;xj yj  = 'COOR' j;tx = xi - xj;
 ty = yi - yj;mx = 'MANU' 'CHPO' j 1 'T' tx 'NATURE' 'DIFFUS';
 my = 'MANU' 'CHPO' j 1 'T' ty 'NATURE' 'DIFFUS';
 'SI' ( &bouc2 'EGA' 1);
  mtx = mx; mty = my ; 'SINO' ;
  mtx = mtx 'ET' mx; mty = mty 'ET' my;
 'FINS';
 'FIN' bouc2;
'FINS';

* il n'y a plus qu'a faire la resolution thermique
mot = 'MODE' geo1 'THERMIQUE' ;
mat = mot 'MATE' 'K' 2;
mth = 'CONDU' mot mat;
bl = 'BLOQ' 'T' maianc;
mthb = mth 'ET' bl;

* resolution pour les deplacements en X
ft = 'DEPI' bl mtx;
dx = 'RESO' mthb ft;
* resolution en Y
fty = 'DEPI' bl mty;
dy = 'RESO' mthb fty;
* on additionne les deux chpoints
ddx = ( 'EXCO' dx 'T') 'NOMC' 'UX';
ddy = ( 'EXCO' dy 'T') 'NOMC' 'UY';
dep = ddx 'ET' ddy  ;

'SI' ( iidim 'EGA' 3);
ftz = 'DEPI' bl mtz;
dz = 'RESO' mthb ftz;
ddz = ( 'EXCO' dz 'T') 'NOMC' 'UZ';
dep = dep 'ET' ddz;
'FINS';
res = geo1 'PLUS' dep;
'FINP' res;




