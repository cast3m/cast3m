$$$$ @ENCA
* @ENCA     PROCEDUR  AM        95/05/02    21:15:33     1649
DEBPROC @ENCA TAIL1*'FLOTTANT' MAIL1*'MAILLAGE' VEC1*'POINT';
* [][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][]
*     D. DUREISSEIX       L.M.T. STRUCTURES & C.M.A.O.       le 23/03/93
*
* Procedure pour construire un vrai ENCAstrement en 2D
*
* on envoie
*         TAIL1  FLOTTANT pour definir la taille
*         MAIL1  MAILLAGE ligne sur laquelle il y a encastrement
*         VEC1   POINT    vecteur pour l'orientation des hachures
*                         (normale interieure)
* on recupere
*         PTF1   MAILLAGE definissant l'encastrement
* ======================================================================
  VEC1 = VEC1 / ('NORME' VEC1);
  'OUBLIE' PTF1;
  NMAIL1 = 'CHAN' 'POI1' MAIL1;
  NNO1   = 'NBNO' NMAIL1;
  N1 = 1;
  'REPETE' BLO1 (NNO1 - 1);
    N1 = N1 + 1;
    P1 = NMAIL1 'POINT' (N1 - 1);
    P2 = NMAIL1 'POINT' N1;
    VEC2 = P2 'MOINS' P1;
    VECT2 = VEC2 / ('NORME' VEC2);
    DIST1 = 'NORME' VEC2;
    D1 = 0.;
    'REPETE' BLO2 (('ENTIER' (DIST1 / TAIL1)) + 1);
      PT1 = P1 'PLUS' (D1 * VECT2);
*      'SI' (('NORME' (P2 'MOINS' PT1)) < (TAIL1 / 3.));
*        'QUITTER' BLO2;
*      'FINSI';
      PT2 = (PT1 'PLUS'  ((TAIL1 * 1.5) * VECT2))
                 'MOINS' ((TAIL1 * 1.5) * VEC1);
      'SI' ('EXISTE' PTF1);
        PTF1 = PTF1 'ET' ('MANU' 'SEG2' PT1 PT2);
      'SINON';
        PTF1 = ('MANU' 'SEG2' PT1 PT2);
      'FINSI';
      D1 = D1 + TAIL1;
    'FIN' BLO2;
  'FIN' BLO1;
*
*  'MENAGE';
'FINPROC' PTF1;
