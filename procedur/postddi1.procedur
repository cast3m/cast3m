$$$$ POSTDDI1
* POSTDDI1  PROCEDUR  CHAT      96/08/05    21:25:21     2238
'DEBPROC' POSTDDI1 var1*MCHAML alpha*FLOTTANT mod0*MMODEL;
*
****************************************************************
*     Calcul de CHI(Xv) = 1.5*ALPHA*XV1 + (1-ALPHA)*J2(Xv)     *
*       Avec XV1 = Contrainte principale maximale de Xv        *
****************************************************************
*
mot1 = 'VALE' 'MODE';
'SI' ('EGA' mot1 'AXIS');
xvrr = ('EXCO' var1 'Y1RR' 'SMRR') + ('EXCO' var1 'Y2RR' 'SMRR');
xvzz = ('EXCO' var1 'Y1ZZ' 'SMZZ') + ('EXCO' var1 'Y2ZZ' 'SMZZ');
xvtt = ('EXCO' var1 'Y1TT' 'SMTT') + ('EXCO' var1 'Y2TT' 'SMTT');
xvrz = ('EXCO' var1 'Y1RZ' 'SMRZ') + ('EXCO' var1 'Y2RZ' 'SMRZ');
xv = xvrr 'ET' xvzz 'ET' xvtt 'ET' xvrz ;
'SINON';
'SI' ('EGA' mot1 'TRID');
xvxx = ('EXCO' var1 'Y1XX' 'SMXX') + ('EXCO' var1 'Y2XX' 'SMXX');
xvyy = ('EXCO' var1 'Y1YY' 'SMYY') + ('EXCO' var1 'Y2YY' 'SMYY');
xvzz = ('EXCO' var1 'Y1ZZ' 'SMZZ') + ('EXCO' var1 'Y2ZZ' 'SMZZ');
xvxy = ('EXCO' var1 'Y1XY' 'SMXY') + ('EXCO' var1 'Y2XY' 'SMXY');
xvyz = ('EXCO' var1 'Y1YZ' 'SMYZ') + ('EXCO' var1 'Y2YZ' 'SMYZ');
xvxz = ('EXCO' var1 'Y1ZX' 'SMXZ') + ('EXCO' var1 'Y2ZX' 'SMXZ');
xv = xvxx 'ET' xvyy 'ET' xvzz 'ET' xvxy 'ET' xvyz 'ET' xvxz;
'SINON';
xvxx = ('EXCO' var1 'Y1XX' 'SMXX') + ('EXCO' var1 'Y2XX' 'SMXX');
xvyy = ('EXCO' var1 'Y1YY' 'SMYY') + ('EXCO' var1 'Y2YY' 'SMYY');
xvzz = ('EXCO' var1 'Y1ZZ' 'SMZZ') + ('EXCO' var1 'Y2ZZ' 'SMZZ');
xvxy = ('EXCO' var1 'Y1XY' 'SMXY') + ('EXCO' var1 'Y2XY' 'SMXY');
xv = 'REDU' (xvxx 'ET' xvyy 'ET' xvzz 'ET' xvxy) mod0;
'FINSI';
'FINSI';
xv = 'CHAN' xv 'TYPE' 'CONTRAINTES';
j2 = 'VMIS' mod0 xv;
princxv = 'PRIN' mod0 xv;
xv1 = 'EXCO' princxv 'SI11' 'SCAL';
xv2 = 'EXCO' princxv 'SI22' 'SCAL';
xv3 = 'EXCO' princxv 'SI33' 'SCAL';
max1 = ( (xv1+xv2) + ('ABS' (xv1-xv2)) ) / 2.;
max2 = ( (max1+xv3) + ('ABS' (max1-xv3)) ) / 2.;
chi = (alpha*max2*1.5) + ((1.-alpha)*j2) ;
'FINPROC' chi;
$$$$


