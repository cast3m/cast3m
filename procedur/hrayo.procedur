$$$$ HRAYO
* HRAYO     PROCEDUR  AM        14/04/17    21:15:03     8000
************************************************************************
 'DEBPROC' HRAYO mcv*'MMODEL'
           mor1*'MMODEL' mar1*'MCHAML' tt1*'CHPOINT'
           mor2/'MMODEL' mar2/'MCHAML' tt2*'CHPOINT'
           rel12/'MAILLAGE' stefan/'FLOTTANT' ;

************************************************************************
* Calcul d'un coefficient d'échange linéarisé pour le traitement
* du rayonnement infini et face à face :
*
* Données:
*   mcv     : modele de convection (sur maillage standard ou raccord)
*   mor1    : modele de rayonnement defini sur le maillage l1
*   mat1    : champ d'émissivités associé au modèle mor1
*   tt1     : champoint défini sur l1
*   indice 2: idem maillage l2
*             mais mor2 et mar2 inutilisés en cas de rayonnement infini
*             car on va chercher E_IN dans mat1
*   rel12   : géométrie définissant les relations entre le support
*             de l1 et le support de l2
*            (nécessaire pour le rayonnement face à face)
* Resultat
*   h       : coefficient d'échange linéarisé défini sur le modèle mcv
*
************************************************************************
* constante de Stefan-Boltzmann
'SI' ('NON' ('EXIS' stefan)) ;
 stefan = 5.673e-8 ;
'FINSI' ;

* extraction des emissivites et transformation en champoint
* (de type SCAL pour les operations arithmétiques)

'SI' ( 'EXIS' rel12 ) ;
* cas du rayonnement face a face ( rel12 existe)

* extraction des emissivites et transformation en champoint
* (de type SCAL pour les operations arithmétiques)
ee1 = 'EXCO' mar1 'EMIS' 'SCAL';
e1  = 'CHAN' 'CHPO' mor1 ee1 ;

ee2 = 'EXCO' mar2 'EMIS' 'SCAL';
ex2 = 'CHAN' 'CHPO' mor2 ee2 ;

t1 = 'NOMC' tt1 'SCAL' ;
tx2= 'NOMC' tt2 'SCAL' ;

* projection des champs sur le maillage l1

rel21 = 'INVE' rel12 ;

 t2 = 'KPRO' tx2 rel21 ;
 e2 = 'KPRO' ex2 rel21 ;

'SINON' ;
* cas du rayonnement infini      ( rel12 n'existe pas)

ee1 = 'EXCO' mar1 'EMIS' 'SCAL';
e1  = 'CHAN' 'CHPO' mor1 ee1 ;


'SI' ('EXIS' MAR1 'E_IN' ) ;
  ee2 = 'EXCO' mar1 'E_IN' 'SCAL';
  ex2 = 'CHAN' 'CHPO' mor1 ee2 ;
'SINON' ;
  ex2 = 'MANU' 'CHPO' ('EXTR' mor1 'MAIL')
          1 'SCAL' 1. 'NATURE' 'DIFFUS' ;
'FINSI' ;

t1 = 'NOMC' tt1 'SCAL' ;
tx2= 'NOMC' tt2 'SCAL' ;

t2 = tx2 ;
e2 = ex2 ;
'FINSI' ;

* calcul sur le maillage l1
r1 = 1. '-' e1 ;
r2 = 1. '-' e2 ;
dd = r1 '*' r2 ;
d = 1. '-' dd ;
ee=(e1 '*' e2) '/' d ;
sie = stefan '*'ee ;
2t1 = t1 '*' t1; 2t2 = t2 '*' t2;
hh1 = (2t1 '+' 2t2) '*' (t1 '+' t2) ;
hh  = sie '*' hh1 ;
h1 = 'NOMC' hh 'H' 'NATU' DIFFUS  ;

* projection sur le mailllage l2
'SI' ( 'EXIS' rel12 ) ;
* cas du rayonnement face a face
h2 = 'KPRO' h1 rel12 ;
h12 = h1 'ET' h2 ;
'SINON' ;
* cas du rayonnement infini
h12 = h1 ;
'FINSI' ;

* definition du champ h sur le modele de convection
*he  = 'CHAN' 'CHAM' h12 mcv 'NOEU' ;
he  = 'CHAN' 'CHAM' h12 mcv 'RIGIDITE' ;
h   = 'CHAN' 'TYPE' he  'CARACTERISTIQUE' ;
'FINPROC' h ;


