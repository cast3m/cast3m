$$$$ PROJGRIL
* PROJGRIL  PROCEDUR  BP208322  17/10/25    21:15:01     9596
DEBP PROJGRIL nuag1*'NUAGE' ;
  idim = VALE 'DIME' ;
  iele = VALE 'ELEM' ;
  lmt = EXTR nuag1 'COMP' ;
  nmt = DIME lmt ;
  SI (nmt <EG 2) ;
    ERREUR 'Le NUAGE doit etre de dimension superieure a 2' ;
  FINSI ;
  motf = EXTR lmt nmt ;
  nmt = nmt - 1 ;
* Listes de composantes et de leurs valeurs
  nm1 = 0 ;
  ARGU lm1/'LISTMOTS' ;
  SI (EGA (TYPE lm1) 'LISTMOTS') ;
    ARGU lr1*'LISTREEL' ;
    nm1 = DIME lm1 ;
    nr1 = DIME lr1 ;
    SI (NEG nm1 nr1) ;
      ERREUR (CHAI 'Le LISTMOTS et le LISTREEL ne sont pas de la meme '
                   'dimension') ;
    FINSI ;
  FINSI ;
  SI (NEG nm1 (nmt - 2)) ;
    ERREUR (CHAI 'Le LISTMOTS doit etre de dimension ' (nmt - 2)) ;
  FINSI ;
* Recherche des 2 composantes non fournies pour la fabrication du
* maillage
  n2 = 0 ;
  n3 = 0 ;
  SI (EGA nm1 0) ;
    motx = EXTR lmt 1 ;
    moty = EXTR lmt 2 ;
  SINON ;
    nnc = 0 ;
    REPE b1 nmt ;
      mot1 = EXTR lmt &b1 ;
      SI (NON (EXIS lm1 mot1)) ;
        nnc = nnc + 1 ;
        SI (nnc EGA 1) ;
          imx = &b1 ;
          motx = MOT mot1 ;
        FINSI ;
        SI (nnc EGA 2) ;
          imy = &b1 ;
          moty = MOT mot1 ;
        FINSI ;
        SI (nnc >EG 3) ;
          ERREUR (CHAI 'Les composantes du LISTMOTS ne correspondent '
                       'pas a celles du NUAGE') ;
        FINSI ;
      FINSI ;
    FIN b1 ;
  FINSI ;
* Construction du maillage pour le trace
  lx = EXTR nuag1 motx ;
  ly = EXTR nuag1 moty ;
  nx = DIME lx ;
  ny = DIME ly ;
  OPTI 'ELEM' 'QUA4' ;
  lx0 = 0. * lx ;
  ly0 = 0. * ly ;
  ly1 = PROG nx * (EXTR 1 ly) ;
  SI (EGA idim 2) ;
    lig1 = QUEL 'SEG2' lx ly1 ;
    lig2 = QUEL 'SEG2' ly0 ly ;
  SINON ;
    lig1 = QUEL 'SEG2' lx ly1 lx0 ;
    lig2 = QUEL 'SEG2' ly0 ly ly0 ;
  FINSI ;
  mail1 = lig1 GENE lig2 ;
  mp1 = CHAN 'POI1' mail1 ;
* Construction du CHPOINT de la position des noeuds de mp1 dans la
* grille, on commence par les composantes fixees (facile !)
  n1 = NBEL mp1 ;
  SI (nm1 > 0) ;
    chp1 = MANU 'CHPO' mp1 lm1 lr1 'NATURE' 'DIFFUS' ;
  SINON ;
    chp1 = VIDE 'CHPOINT'/'DIFFUS' ;
  FINSI ;
* Puis les composantes motx et moty
  x = COOR 1 mp1 ;
  x = NOMC x motx ;
  y = COOR 2 mp1 ;
  y = NOMC y moty ;
  chp1 = chp1 ET x ET y ;
* Interpolation de la fonction a partir de ce CHPOINT
  chp2 = IPOL nuag1 chp1 'GRILL' ;
* Remise a l'etat initial de 'ELEM'
  si (neg iele ' '); OPTI 'ELEM' iele ; finsi;
FINP mail1 chp2 ;
$$$$



