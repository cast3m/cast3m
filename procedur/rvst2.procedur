$$$$ RVST2
* RVST2     PROCEDUR  BLUMENFE  05/04/04    21:15:02     5052
* RVST2     PROCEDUR        04/07/15
*                                                                    ;
* Calcolo rho vapore alla saturazione per:  0 < T < 20
*                                           ------------
*                                                                    ;
 'DEBPROC' rvst2                                                     ;
   argu tn*chpoint                                                   ;
   si (((mini tn) < 0.0) ou ((maxi tn) > 20.0))                    ;
    mess '=========================================================' ;
    mess '===RVSAT: Interpolazione fuori limiti: 0 < T[C] < 20===' ;
    mess '=========================================================' ;
   finsi                                                             ;
   a0   =  4.86256716e-03                                            ;
   a1   =  2.98409029e-04                                            ;
   a2   =  2.10179146e-05                                            ;
   a3   = -7.96684413e-07                                            ;
   a4   =  2.77512067e-08                                            ;
   t1   =  kops       tn           '*' a1                            ;
   t2   =  kops (kops tn '**' 2.0) '*' a2                            ;
   t3   =  kops (kops tn '**' 3.0) '*' a3                            ;
   t4   =  kops (kops tn '**' 4.0) '*' a4                            ;
   rvs  =  kops a0 '+' (kops t1 '+' (kops t2 '+' (kops t3 '+' t4)))  ;
   rvs  =  kops rvs '|<' 0.0                                         ;
*
   s0   =  a1                                                        ;
   s1   =  kops       tn           '*' (a2 * 2.0)                    ;
   s2   =  kops (kops tn '**' 2.0) '*' (a3 * 3.0)                    ;
   s3   =  kops (kops tn '**' 3.0) '*' (a4 * 4.0)                    ;
   rvp  =  kops s0 '+' (kops s1 '+' (kops s2 '+' s3))                ;
   rvp  =  kops rvp '|<' 0.0                                         ;
*
*finproc rvs                                                         ;
 'FINPROC' rvs rvp                                                   ;




