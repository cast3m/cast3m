$$$$ PAS_VERM
* PAS_VERM  PROCEDUR  MB234859  16/09/16    21:15:44     9091
'DEBPROC' PAS_VERM PRECED*'TABLE' chetat1*'MCHAML';
*---------- verifications qu'il existe bien les donnees necessaires
*---------- au matériaux
conti=PRECED.'CONTINUATION';
WTAB=PRECED.'WTABLE';
LDEVA = 'EXTR' WTAB.'MA_TOTAL' 'DEVA';
LPAR0 = 'EXTR' WTAB.'MO_TOTAL' 'PARA';
LDEVA_DI = 'DIME' LDEVA;
nocom='EXTR'  CHETAT1 'COMP';
'SI' ('NEG' LDEVA_DI 0);

   'REPETER' BOU_VA1 LDEVA_DI;
        ITE1 = &BOU_VA1;
        NOM1 = 'EXTR' LDEVA ITE1;
        'SI' (WTAB.'FREQUENTIEL' 'ET' ('EGA' NOM1 'FREQ')) ;
        'SINON';
         'SI' ('NON' ('EXISTE'   nocom NOM1));
            'MESS' 'Il manque la variable ' NOM1 ;
            'ERRE'  ' ';
         'FINSI' ;
        'FINSI';
   'FIN' BOU_VA1;
   'SI' WTAB.'FOR_MECA' ;
     MA = 'VIDE' 'MCHAML' ;
     'SI' WTAB.'MEC1' ;
       MA = MA 'ET' (WTAB.'MAT_MEC') ;
     'FINSI' ;
     'SI' WTAB.'POR1' ;
       MA = MA 'ET' WTAB.'MAT_POR' ;
     'FINSI' ;
     'SI' ('EXIS' WTAB 'MAT_CHA') ;
       MA = MA 'ET' WTAB.'MAT_CHA' ;
     'FINSI' ;
     LDEVAM='EXTR' MA 'DEVA';
     LDEVA_DM = 'DIME' LDEVAM;
     'SI' ('NEG' LDEVA_DM 0);
        'SI' ('NON' (('EGA' LDEVA_DM 1) 'ET'
             ( 'EGA' (LDEVAM 'EXTR' 1) 'ALPH' ) ));
            WTAB.'RECALCUL'= VRAI;
        'FINSI';
        WTAB.'MATVAR'=VRAI;WTAB.'LDEVA'=LDEVAM;
        'SI' WTAB.'FREQUENTIEL' ; WTAB.'MATVAR'= FAUX; 'FINSI';
     'FINSI';
   'FINSI';
'FINSI';
*------- le modèle depend-t-il de parametres externes? Si oui verification
*------- de leur presence.
'SI' WTAB.'PARA_EXTE';
    TEMP0=conti.'TEMPS';
    LPAR0_DI = 'DIME' LPAR0;
   'REPETER' BOU_PA1 LPAR0_DI;
       NOM1 = 'EXTR' LPAR0 &BOU_PA1;
       'SI' ('NON' ('EXISTE'   nocom NOM1));
            'MESS' 'Il manque la variable ' NOM1  ;
            'ERRE'  ' ';
       'FINSI';
   'FIN' BOU_PA1;
   'SI' WTAB.'FOR_MECA' ;
     WTAB.'PARA_EXTE'=FAUX;
     'SI'  WTAB.'MEC1' ;
        MO=WTAB.'MOD_MEC';
        'SI' WTAB.'POR1' ;
          MO= MO 'ET' WTAB.'MOD_POR';
        'FINSI';
     'SINON';
        MO=WTAB.'MOD_POR';
     'FINSI';
     LPAR0 = 'EXTR' MO 'PARA';
     LPAR0_DI = 'DIME' LPAR0;
     'SI' ( LPAR0_DI  'NEG' 0) ;
       WTAB.'PARA_EXTE'=VRAI;
       WTAB.'LPAR0'= LPAR0 ;
     'FINSI';
   'FINSI';
'FINSI';
'FINPROC';






