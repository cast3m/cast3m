$$$$ TRANSFER
* TRANSFER  PROCEDUR  DC        06/02/06    21:16:07     5304
* TRANSFER  PROCEDUR  JEANV     97/10/27    21:16:57     2887
'DEBPROC' TRANSFER MOD/SOLUTION TA/TABLE LAMOR*LISTREEL MOT1*MOT;
*
SEIS=FAUX;
'SI' ('EGA' MOT1 'SEISME');
     'ARGU' DIRECT*MOT;
     SEIS=VRAI;
     'SI' ('EGA' DIRECT 'UX');QDIR='QX';EDIR = 1;'FINSI';
     'SI' ('EGA' DIRECT 'UY');QDIR='QY';EDIR = 2;'FINSI';
     'SI' ('EGA' DIRECT 'UZ');QDIR='QZ';EDIR = 3;'FINSI';
'SINON';
     'ARGU' POINT1*POINT;
'FINSI';
*
'ARGU' MOT2*MOT POINT2*POINT MOSOR*MOT LFREQ*LISTREEL MOCHO*MOT ;
***********************************************************************
*       CALCUL D UNE FONCTION DE TRANSFERT                            *
*                                                                     *
*       MOD     : LES MODES DE LA STRUCTURE                           *
*       LAMOR   : LES AMORTISSEMENTS MODAUX (ENTRE 0 ET 1)            *
*       MOT1    : DIRECTION DE LA SOLLICITATION (UX, UY, UZ ...)      *
*                 OU 'SEISME' POUR ACCELERATION D'ENSEMBLE            *
*       POINT1  : POINT D APPLICATION DE LA SOLLICITATION             *
*                 OU DIRECTION DU SEISME                              *
*       MOT2    : DIRECTION DE LA REPONSE                             *
*       POINT2  : POINT OU ON CHERCHE LA REPONSE                      *
*       MOSOR   : NATURE DES SORTIES (DEPL, VITE OU ACCE)             *
*       LFREQ   : LES FREQUENCES DE CALCUL                            *
*       MOCHO   : TYPE DES SORTIES (MOPH OU REIM)                     *
*                                                                     *
*       EVENTUELLEMENT                                                *
*       MOCOU   : COULEUR DES COURBES                                 *
*                                                                     *
*       EN SORTIE                                                     *
*       TRA     : OBJET EVOLUTION COMPLEXE                            *
***********************************************************************
'REPETER' BEX 1;
*
'SI' ('EXIS' TA) ;
   TAB_LOG = VRAI;
'SINON';
   TAB_LOG = FAUX;
   'SI' ('NON' ('EXIS' MOD)) ;
      'MESS' '***** ERREUR *****';
      'MESS' 'IL MANQUE LA DONNEE DES MODES PROPRES';
      'QUITTER' BEX;
   'FINSI';
'FINSI';
*
ISOR=0;ISO2=0;
'SI' SEIS;
     'SI' ('EGA' MOSOR 'ACCE');ISO2=1;'FINSI';
     'SI' ('EGA' MOSOR 'VITE');ISO2=2;'FINSI';
     'SI' ('EGA' MOSOR 'DEPL');ISO2=3;'FINSI';
'SINON';
     'SI' ('EGA' MOSOR 'ACCE');ISOR=1;'FINSI';
     'SI' ('EGA' MOSOR 'VITE');ISOR=2;'FINSI';
     'SI' ('EGA' MOSOR 'DEPL');ISOR=3;'FINSI';
'FINSI';
'SI' ((ISOR 'EGA' 0) 'ET' (ISO2 'EGA' 0)) ;
'MESS' '***** ERREUR *****';
'MESS' 'IL FAUT DONNER DEPL, VITE OU ACCE';
'QUITTER' BEX;
'FINSI';
*
ICHO=0;
'SI' ('EGA' MOCHO 'MOPH');ICHO=1;'FINSI';
'SI' ('EGA' MOCHO 'REIM');ICHO=2;'FINSI';
'SI' (ICHO 'EGA' 0);
'MESS' '***** ERREUR *****';
'MESS' 'IL FAUT DONNER MOPH OU REIM';
'QUITTER' BEX;
'FINSI';
*
'SI' TAB_LOG;
   NMOD = ('DIME' (TA . 'MODES')) - 2;
'SINON';
   NMOD='DIME' MOD;
'FINSI';
NAMO='DIME' LAMOR;
'SI' (NMOD '>' NAMO);
'MESS' '***** ERREUR *****';
'MESS' 'IL MANQUE UNE OU PLUSIEURS VALEURS D AMORTISSEMENT MODAL';
'QUITTER' BEX;
'FINSI';
*
DEUPI=6.28318530718;
Q4PI2=39.4784176044;
FRE2=LFREQ**2;
I=0;
*
'REPETER' BMOD NMOD;
I=I+1;
'SI' TAB_LOG;
   XI = TA . 'MODES' . I . 'DEFORMEE_MODALE';
   FI = TA . 'MODES' . I . 'FREQUENCE';
   MI = TA . 'MODES' . I . 'MASSE_GENERALISEE';
'SINON';
   XI='TIRE' MOD 'DEPL' 'RANG' I;
   FI='TIRE' MOD 'FREQ' 'RANG' I;
   MI='TIRE' MOD 'MGEN' 'RANG' I;
'FINSI';
EI='EXTR' LAMOR I;
FI2=FI*FI;
Q4E2F2=4*(FI2*(EI**2));
M2EIFI=-2*(EI*FI);
'SI' SEIS;
  'SI' TAB_LOG;
     X1=-1. * (TA.'MODES'. I . 'DEPLACEMENTS_GENERALISES' . EDIR);
  'SINON';
     X1=-1. * ('TIRE' MOD QDIR 'RANG' I);
  'FINSI';
'SINON';
     X1='EXTR' XI MOT1 POINT1;
'FINSI';
X2='EXTR' XI MOT2 POINT2;
C2=X1*X2;
COEF=C2/MI;
NUMRE='PROG' 'LINE' 'A' -1 'B' FI2 FRE2;
NUMIM='PROG' 'LINE' 'A' M2EIFI LFREQ;
DEN1='PROG' 'LINE' 'A' Q4E2F2 FRE2;
DEN2=NUMRE**2;
DENOM=DEN1+DEN2;'DETR' DEN1;'DETR' DEN2;
UNSDE=DENOM**-1;'DETR' DENOM;
EV1=NUMRE*UNSDE;EV2=NUMIM*UNSDE;
'DETR' NUMRE;'DETR' NUMIM;'DETR' UNSDE;
RES1=COEF*EV1;'DETR' EV1;
IMS1=COEF*EV2;'DETR' EV2;
*
'SI' (I 'EGA' 1);
     RES='COPI' RES1;'DETR' RES1;
     IMS='COPI' IMS1;'DETR' IMS1;
'SINON';
     RES2=RES+RES1;'DETR' RES;'DETR' RES1;
     RES='COPI' RES2;'DETR' RES2;
     IMS2=IMS+IMS1;'DETR' IMS;'DETR' IMS1;
     IMS='COPI' IMS2;'DETR' IMS2;
'FINSI';
*
'FIN' BMOD;
*
'SI' (ISOR 'EGA' 1);
     PRES=RES*FRE2;'DETR' RES;
     PIMS=IMS*FRE2;'DETR' IMS;
     RES=-1*PRES;'DETR' PRES;
     IMS=-1*PIMS;'DETR' PIMS;
'FINSI';
'SI' (ISOR 'EGA' 2);
     PRES=IMS*LFREQ;'DETR' IMS;
     PIMS=RES*LFREQ;'DETR' RES;
     RES=PRES/(-1*DEUPI);'DETR' PRES;
     IMS=PIMS/DEUPI;'DETR' PIMS;
'FINSI';
'SI' (ISOR 'EGA' 3);
     RES=RES/Q4PI2;
     IMS=IMS/Q4PI2;
'FINSI';
*
'SI' SEIS;
     'SI' (ISO2 'EGA' 1);
          PRES=RES*FRE2;'DETR' RES;
          PIMS=IMS*FRE2;'DETR' IMS;
          RES=-1*PRES;'DETR' PRES;
          IMS=-1*PIMS;'DETR' PIMS;
          SI (EGA DIRECT MOT2);
           RES=RES+('PROG' ('DIME' RES) * 1.);
          FINSI;
     'FINSI';
     'SI' (ISO2 'EGA' 2);
          PRES=(IMS*LFREQ)/DEUPI;'DETR' IMS;
          PIMS=(RES*LFREQ)/DEUPI;'DETR' RES;
          RES=-1*PRES;'DETR' PRES;
          IMS= 1*PIMS;'DETR' PIMS;
     'FINSI';
     'SI' (ISO2 'EGA' 3);
          PRES=RES/Q4PI2;'DETR' RES;
          PIMS=IMS/Q4PI2;'DETR' IMS;
          RES=1*PRES;'DETR' PRES;
          IMS=1*PIMS;'DETR' PIMS;
     'FINSI';
'FINSI';
*
'DETR' FRE2;
*
'ARGUMENT' MOCOU/MOT;
'SI' ('NON' ('EXISTE' MOCOU));
     MOCOU= VALEUR 'COUL';
'FINSI';
*
'SI' (ICHO 'EGA' 1);
TRA='EVOL' MOCOU 'COMP' 'FREQUENCE' LFREQ 'MODULE' RES 'PHASE' IMS;
'RIMP' TRA;
'SINON';
TRA='EVOL' MOCOU 'COMP' 'FREQUENCE' LFREQ 'P REELLE' RES
'P IMAGINAIRE' IMS;
'FINSI';
*
'FIN' BEX;
*
'FINPROC' TRA;



