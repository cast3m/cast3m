$$$$ ASPARAM
* ASPARAM   PROCEDUR  MAGN      08/12/09    21:15:00     6189
'DEBPROC' asparam rxt*TABLE                                      ;

 TBT    = rxt.'TBT'                                              ;
 GEO    = rxt.'GEO'                                              ;
*
  idim = vale 'DIME';
  DIM3D=FAUX        ;
  Si(EGA idim 3)    ;
  DIM3D=VRAI        ;
  Finsi             ;
*  MESS 'si VRAI dimension 3D' ;
*  LIST DIM3D   ;
*
 un     = rxt.'TIC'.'UN'                                         ;
 vn     = rxt.'TIC'.'VN'                                         ;
 xd     = rxt.'TIC'.'XD'                                         ;
 xdinj  = 'IPOL' 0.0 t (rxt.'scenasp'.'xdinj')                   ;
 xdmin  = xdinj * 0.01                                           ;
 ixd=(masq xd 'SUPERIEUR' xdmin )                                ;
 tf     = rxt.'TIC'.'TF'                                         ;
 td     = rxt.'TIC'.'TD'                                         ;
 dd     = rxt.'TIC'.'DD'                                         ;
 t      = rxt.'scenasp'.'t'                                      ;
 ddinj  = 'IPOL' 0.0 t (rxt.'scenasp'.'ddinj')                   ;
 ddmin  = ddinj * 0.01                                           ;
 idd=(masq dd 'SUPERIEUR' ddmin )                                ;
 dd     = rxt.'TIC'.'DD' *idd                                    ;
 rhogas = rxt.'TIC'.'RHO'                                        ;
 rhovap = rxt.'TIC'.'RVP'                                        ;
 mugas  = rxt.'TIC'.'Mu'                                         ;
 Cpgas  = extr (rxt.'TIC'.'Cpm') (dime (rxt.'TIC'.'Cpm'))        ;
 lamgas = TBT.'lamair'                                           ;
 rhodis = rxt.'rod'                                              ;
 Cpdis  = rxt.'Cpd'                                              ;
 $vtf   = rxt.'GEO'.'$vtf'                                       ;
 Lv     = TBT.'Lv'                                               ;
 dt     = rxt.'DT0'                                              ;
*
*corrections appel procedur rvst2.procedur
* rhosat rhosp  = rvsat td                                       ;
 rgva   = TBT.'Rgvap'                                            ;
 Pvap   = rgva * rhovap * (tf+273.0)   + 0.000005                ;
ind200= td masque 'EGINF' 200.                                   ;
ind20sup = td masque 'EGSUP' 20.                                 ;
indch= ind20sup * ind200                                         ;
*
**
td1 = td * indch                                                 ;
rhosat1 rhosp1  = rvsat td1                                      ;
rhosp1 = rhosp1 * indch                                          ;
rhosat1 = rhosat1 * indch                                        ;
ind20inf = td masque 'EGINF' 19.999999999999999                  ;
ind0sup = td masque 'EGSUP' 0.                                   ;
indfr = ind20inf * ind0sup                                       ;
td2 = td * indfr                                                 ;
rhosat2 rhosp2  = rvst2 td2                                      ;
rhosat2 = rhosat2 * indfr                                        ;
rhosp2 = rhosp2 * indfr                                          ;
rhosat = rhosat1 + rhosat2                                       ;
rhosp = rhosp1 + rhosp2                                          ;
*
*fin correction appel procedur
*
 Pt     = extr (dime rxt.'TIC'.'PT') rxt.'TIC'.'PT'              ;
 Dvap   = (((tf + 273.0)/273.0) ** 1.75) * 2.2 / Pt              ;
*
 drho   = noel $vtf  ((rhodis - rhogas)/rhodis)                  ;
 'SI' DIM3D  ;
 gdrx   = nomc 'UX' (drho * 0.00)                                ;
 gdry   = nomc 'UY' (drho * 0.00)                                ;
 gdrz   = nomc 'UZ' (drho * 9.81)                                ;
 rxt.'TIC'.'GRD'  = kcht $vtf vect centre (gdrx et gdry et gdrz) ;
 'SINON' ;
 gdrx   = nomc 'UX' (drho * 0.00) ;
 gdrz   = nomc 'UY' (drho * 9.81) ;
 rxt.'TIC'.'GRD' = kcht $vtf vect centre (gdrx et gdrz) ;
 FINSI;
*
 cmps   = extr un comp                                           ;
 ur     = (psca (vn - un) (vn - un) cmps cmps) ** 0.5            ;
 Re     = dd * ur * rhogas * (mugas** -1.)                       ;
 Pr     = mugas * Cpgas * (lamgas** -1.)                         ;
 Sc     = mugas * (rhogas ** -1.0) * (Dvap ** -1.0)              ;
* modif 1 2004
 Nu     = 2.00 + (0.599 * ((Pr ** 0.33333) * (Re ** 0.5)))       ;
*  Nu     = (2.00 + 0.599 * ((Pr ** 0.33333) * (Re ** 0.5)))     ;
* modif 2 2004
 Sh     = 2.00 + (0.599 * ((Sc ** 0.33333) * (Re ** 0.5)))       ;
*  Sh     = (2.00 + 0.599 * ((Sc ** 0.33333) * (Re ** 0.5)))     ;
*
 F_D    = (1.00 + (0.15 * (Re ** 0.687)))                        ;
*
 fk     = 18.0 * mugas  * F_D * (dd ** -2.0)                     ;
 fh     =  6.0 * lamgas * Nu  * (dd ** -2.0)                     ;
 fm     =  6.0 * Dvap   * Sh  * (dd ** -2.0) * (rhovap - rhosat) ;



*fmax   = (tf - td) * ((rhodis * Cpdis / dt) - (fh / Cpgas)) / Lv;
*fmin   = (-1.0*td) * ((rhodis * Cpdis / dt) - (fh / Cpgas)) / Lv;
*
 num    = ((rhovap - rhosat)/dt) * ((xd + 1.0e-10) ** -1.0)      ;
 den    = (rhosp  * ((xd + 1.0e-10) ** -1.0))                    ;
 den    = (den * Lv / (Cpdis * rhodis)) + 1.0                    ;
 fmax   = abs (num * (den ** -1.0))                              ;
 fmin   =  -1.0 * fmax                                           ;
*
 f1     =      fm  masque superieur      fmax                    ;
 f2     =      fm  masque inferieur      fmin                    ;
 f3     = (1.0 - f1) * (1.0 - f2)                                ;
 fm     = (fmax * f1) + (fmin * f2) + (fm * f3)                  ;
* fm     =  fm * 0.0                                             ;

 kgas   = fk * xd          * (rhogas ** -1.0)         * ixd      ;
 kdis   = fk               /  rhodis                             ;
 hgas   = fh * xd  / Cpgas * (rhogas ** -1.0) * ixd     ;
 hdis   = fh       / Cpdis /  rhodis                    ;
 sdia   = fm * dd  / 3.0   /  rhodis                             ;
 sxd    = fm * xd          /  rhodis                             ;
 srvp   = fm * xd  * (-1.0)                                      ;
*srho   = fm * xd  * (-1.0)                                      ;
 std    = fm * (Lv)  / Cpdis /  rhodis                           ;


* rxt.'TIC'.'GRD'  = kcht $vtf vect centre (gdrx et gdry et gdrz) ;
 rxt.'TIC'.'KGAS' = kcht $vtf scal sommet kgas                   ;
 rxt.'TIC'.'KDIS' = kcht $vtf scal sommet kdis                   ;
 rxt.'TIC'.'HGAS' = kcht $vtf scal sommet hgas                   ;
 rxt.'TIC'.'HDIS' = kcht $vtf scal sommet hdis                   ;
 rxt.'TIC'.'SDIA' = noel $vtf (kcht $vtf scal sommet sdia)       ;
 rxt.'TIC'.'SXD'  = noel $vtf (kcht $vtf scal sommet sxd )       ;
*rxt.'TIC'.'SRHO' = noel $vtf (kcht $vtf scal sommet srho)       ;
 rxt.'TIC'.'SRVP' = noel $vtf (kcht $vtf scal sommet srvp)       ;
 rxt.'TIC'.'STD'  = noel $vtf (kcht $vtf scal sommet std )       ;
 rxt.'TIC'.'fm'   = kcht $vtf scal sommet fm                     ;
 rxt.'TIC'.'fmax' = kcht $vtf scal sommet fmax                   ;



* mess 'Mini/Max kdis ' (mini kdis) (maxi kdis)                   ;
* mess 'Mini/Max hdis ' (mini hdis) (maxi hdis)                   ;
* mess 'Mini/Max kgas ' (mini kgas) (maxi kgas)                   ;
* mess 'Mini/Max hgas ' (mini hgas) (maxi hgas)                   ;
* mess 'Mini/Max fk   ' (mini fk  ) (maxi fk  )                   ;
* mess 'Mini/Max fh   ' (mini fh  ) (maxi fh  )                   ;
* mess 'Mini/Max fm   ' (mini fm  ) (maxi fm  )                   ;
* mess 'Mini/Max srvp ' (mini srvp) (maxi srvp)                   ;
* mess 'Mini/Max xd   ' (mini xd  ) (maxi xd  )                   ;
* mess 'Mini/Max  rhogas' (mini  rhogas) (maxi  rhogas)           ;
* mess 'Cpgas' Cpgas 'Lv=' lv               ;
* mess 'Mini/Max  mugas' (mini  mugas) (maxi  mugas)           ;
* mess 'Mini/Max  lamgas'        lamgas                           ;
* mess 'Mini/Max  Dvap  ' (mini  Dvap  ) (maxi  Dvap  )           ;


'FINPROC'                                                        ;

