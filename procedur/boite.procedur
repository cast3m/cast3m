$$$$ BOITE
* BOITE     PROCEDUR  GOUNAND   16/12/01    21:15:02     9218
************************************************************************
* NOM         : BOITE
* DESCRIPTION : maille une boite englobante d'un maillage
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 29/11/2016, version initiale
* HISTORIQUE : v1, 29/11/2016, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
'DEBPROC' BOITE ;
*
'ARGU' monmai*'MAILLAGE' ;
*
vdim = 'VALEUR' 'DIME' ;
lmima = 'PROG' ;
*
'REPETER' iidim vdim ;
   idim = &iidim ;
   cooi = 'COORDONNEE' idim monmai ;
   mii = 'MINIMUM' cooi ;
   mai = 'MAXIMUM' cooi ;
   lmima = 'ET' lmima ('PROG' mii mai) ;
'FIN' iidim ;
*
'SI' ('EGA' vdim 1) ;
   xmin = 'EXTR' lmima 1 ;
   xmax = 'EXTR' lmima 2 ;
   p1 = 'POIN' xmin ; p2 = 'POIN' xmax ;
   mb = 'MANU' 'SEG2' p1 p2 ;
'FINS' ;
*
'SI' ('EGA' vdim 2) ;
   xmin = 'EXTR' lmima 1 ; xmax = 'EXTR' lmima 2 ;
   ymin = 'EXTR' lmima 3 ; ymax = 'EXTR' lmima 4 ;
   p1 = xmin ymin ; p2 = xmax ymin ;
   p3 = xmax ymax ; p4 = xmin ymax ;
   mb = 'MANU' 'QUA4' p1 p2 p3 p4 ;
'FINS' ;
*
'SI' ('EGA' vdim 3) ;
   xmin = 'EXTR' lmima 1 ; xmax = 'EXTR' lmima 2 ;
   ymin = 'EXTR' lmima 3 ; ymax = 'EXTR' lmima 4 ;
   zmin = 'EXTR' lmima 5 ; zmax = 'EXTR' lmima 6 ;
   p1 = xmin ymin zmin ; p2 = xmax ymin zmin ;
   p3 = xmax ymax zmin ; p4 = xmin ymax zmin ;
   p5 p6 p7 p8 = 'PLUS' p1 p2 p3 p4 (0. 0. ('-' zmax zmin)) ;
   mb = 'MANU' 'CUB8' p1 p2 p3 p4 p5 p6 p7 p8 ;
'FINS' ;
*
'RESPRO' mb ;
*
* End of procedure file BOITE
*
'FINPROC' ;


