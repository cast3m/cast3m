$$$$ MULTIDEC
* MULTIDEC  PROCEDUR  PICA      92/07/03    21:27:22     619
*----------------------------------------------------------------------*
*                                                                      *
* PROCEDURE MULTIDEC                                                   *
*                                                                      *
* CALCUL DE LA DECOMPOSITION D'UN SIGNAL SUR UNE SEQUENCE DE FILTRE    *
*        ONDELETTE ORTHOGONALE                                         *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* ENTREE                                                               *
*                                                                      *
* SIGNAL X/Y CONTENANT LE SIGNAL. DX DOIT ETRE CONSTANT                *
*        DANS LE PROCESSUS DE DECOMPOSITION DIADIC, ON STOPPE          *
*        DES QUE LE NB DE POINT N'EST PLUS PAIR                        *
*                                                                      *
* HTILDE Y CONTENANT LE FILTRE (NON FORCEMENT SYMETRIQUE), DANS LA     *
*        DECOMPOSITION DIADIQUE ON STOPPE DES QUE CETTE  CORRELATION   *
*        DEVIENT IMPOSSIBLE. LE STOCKAGE PEUT COMPRENDRE               *
* MMHTI  POINTS NEGATIFS (MMHTI<0--->SYMETRIQUE)                       *
*                                                                      *
* GTILDE Y CONTENANT LE FILTRE ( NON SYMETRIQUE). LE STOCKAGE COMPREND *
* MMGTI  POINTS NEGATIFS (MMGTI<0--->SYMETRIQUE)                       *
*                                                                      *
* OPTIO  MOT CONTENANT LE MODE DE CORRELATION (DEFAUT='SYME')          *
*                                                                      *
* N      NOMBRE DE NIVEAU ESPERE (DEFAUT=NB MAX)                       *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* SORTIE                                                               *
*                                                                      *
* M      NOMBRE DE NIVEAU EFFECTIVEMENT ATTEINT                        *
*                                                                      *
* ONDELL M COURBES DE DECOMPOSITION PAR ORDRE DE FREQUENCE CROISSANT   *
*                                                                      *
* COARSE RESIDU DU SIGNAL APRES M NIVEAU                               *
*----------------------------------------------------------------------*
'DEBPROC' MULTIDEC SIGNAL*'EVOLUTION' HTILDE*'LISTREEL'
          MMHTI *'ENTIER' GTILDE*'LISTREEL' MMGTI *'ENTIER'
                    OPTIO/'MOT' N/'ENTIER';
*----------------------------------------------------------------------*
'SI' ('NON' ('EXISTE' OPTIO));OPTIO='TEXT' SYME;'FINSI';
'SI' ('NON' ('EXISTE' N))    ;N=1000;        'FINSI';
*
'SI' (MMHTI '<' 0); MMHO='TEXT' ' ';
                    MMHTI=('DIMENSION' HTILDE) - 1;
                    MPHTI= MMHTI;
'SINON';            MMHO='TEXT' 'NPNE' MMHTI;
                    MPHTI=('DIMENSION' HTILDE) - (MMHTI+1);
'FINSI';
*
'SI' (MMGTI '<' 0); MMGO='TEXT' ' ';
                    MMGTI=('DIMENSION' GTILDE) - 1;
                    MPGTI=MMGTI;
'SINON';            MMGO='TEXT' 'NPNE' MMGTI;
                    MPGTI=('DIMENSION' GTILDE) - (MMGTI+1);
'FINSI';
*
XXXX='EXTRAIRE' SIGNAL 'ABSC' 1;
YPHI='COPIER' ('EXTRAIRE' SIGNAL 'ORDO' 1);
NPT ='DIMENSION' XXXX;
*
Q=NPT/2;REF=NPT-(2*Q);
*
I=0;
'REPETER' LAB1 N;
*
  NNPT=NPT;NPT=(NPT+1)/2;
  Q=NPT/2;R=NPT-(2*Q);
  'SI' (R 'NEG' REF);
    'MESSAGE' '*** changement de parite du nb de point au niveau' (I+1);
    'MESSAGE' '*** reconstruction correcte impossible'
    'MESSAGE' '--> fin calcul';
    'QUITTER' LAB1;
  'FINSI';
  'SI' ((MMGTI '>' NNPT) 'OU' (MPGTI '>' NNPT));
    'MESSAGE' '*** nb de point insuffisant pour Phi au niveau' (I+1);
    'MESSAGE' '--> fin calcul';
    'QUITTER' LAB1;
  'FINSI';
  'SI' ((MMHTI '>' NNPT) 'OU' (MPHTI '>' NNPT));
    'MESSAGE' '*** nb de point insuffisant pour Psi au niveau' (I+1);
    'MESSAGE' '--> fin calcul';
    'QUITTER' LAB1;
  'FINSI';
*
  I=I+1;
  YYPHI='CVOLUTION' YPHI HTILDE MMHO OPTIO;
  YYPSI='CVOLUTION' YPHI GTILDE MMGO OPTIO;
  XX   ='DIADIQUE'  XXXX 'DIRE'; XXXX=XX;
  YPHI ='DIADIQUE'  YYPHI 'DIRE';
  YPSI ='DIADIQUE'  YYPSI 'DIRE';
  'TITRE' 'Decomposition en ondelette : Niveau i=' I;
  PSI='EVOLUTION' 'MANUEL' 'temps' XXXX 'Y(niveau i)' YPSI;
*
  'SI' (I 'EGA' 1);
    ONDELL=PSI;
  'SINON';
    ONDELL=PSI 'ET' ONDELL;
  'FINSI';
*
'FIN' LAB1;
*
M=I;
'TITRE' 'Signal de faible resolution : niveau m=' M;
COARSE='EVOLUTION' 'MANUEL' 'temps' XXXX 'Y(niveau i)' YPHI;
'FINPROC' M ONDELL COARSE;
