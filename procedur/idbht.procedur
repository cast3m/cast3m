$$$$ IDBHT
* IDBHT     PROCEDUR  FANDEUR   14/10/10    21:15:15     8178
'DEBPROC' IDBHT  M1*'MCHAML'  MO1*'MMODEL'  BHTMX*'CHPOINT'
                 T2*'CHPOINT' ;

* Identificazione dei modelli di cemento ad alta temperatura
IFER= FAUX;

*------------------- modèle drucker-parfait ------------------------

'SI' ('EXISTE' MO1 'MATE' 'DRUCKER_PARFAIT');
   LCO1 = 'EXTR' MO1 'COMP';
   DIM1 = 'DIME' LCO1;
   ISU1 = FAUX;
   ISU2 = FAUX;
   'REPE' B2BHT DIM1;
      ICOMP=&B2BHT;
      CC=LCO1.ICOMP;
     'SI' ('OU'('OU'('OU'('EGA' CC 'LTR'  )
                         ('EGA' CC 'LCS' ))
                         ('EGA' CC 'NU'  ))
                         ('EGA' CC 'YOUN'));
        'SI' ISU1;
           MA1 = MA1 'ET' ('EXCO' M1 CC CC);
        'SINON';
           MA1 = 'EXCO' M1 CC CC;
           ISU1 = VRAI;
        'FINSI';
     'SINON';
        'SI' ISU2;
           MA2 = MA2 'ET' ('EXCO' M1 CC CC);
        'SINON';
           MA2 = 'EXCO' M1 CC CC;
           ISU2 = VRAI;
        'FINSI';
     'FINSI';
   'FIN' B2BHT;
   'SI' ISU1;
      CCC1 = 'VARI' MO1 MA1 BHTMX;
   'FINSI';
   'SI' ISU2;
      CCC2 = 'VARI' MO1 MA2 T2;
   'FINSI';
   'SI' (ISU1 'ET' ISU2);
      CCC = CCC1 'ET' CCC2;
   'SINON';
      'SI' ISU1;
         CCC = CCC1;
      'FINSI';
      'SI' ISU2;
         CCC = CCC2;
      'FINSI';
   'FINSI';
   MI  = 'MATE' MO1 CC CCC ;

*------------------- modele drucker_prager ---------------------

'SINON';
   'SI'( 'EXISTE' MO1 'MATE' 'DRUCKER_PRAGER');
     'ERREUR' 'Le modele DRUCKER_PRAGER H. T. est indisponible.';
      IFER =  VRAI;
     'QUITTER' IDBHT;
   'SINON';

*------------------------ modèle mazars -------------------------

     'SI'('EXISTE' MO1 'MATE' 'MAZARS');
        LCO1 = 'EXTR' MO1 'COMP';
        DIM1 = 'DIME' LCO1;
        ISU1 = FAUX;
        ISU2 = FAUX;
        TMAZ=TABLE;
       'REPE' B3BHT DIM1;
          ICOMP = &B3BHT;
          CC=LCO1.ICOMP;
          'SI' ('OU'('OU'('OU'('OU'('OU'('OU'('EGA' CC 'LTR'  )
                                            ('EGA' CC 'LCS' ))
                                            ('EGA' CC 'NU'  ))
                                            ('EGA' CC 'YOUN'))
                                            ('EGA' CC 'EMAX'))
                                            ('EGA' CC 'ATRA'))
                                            ('EGA' CC 'BTRA'));

             MA1 = 'EXCO' M1 CC CC;
             TMAZ.CC = 'VARI' MO1 MA1 BHTMX;
           'SINON';
             MA1 = 'EXCO' M1 CC CC;
             TMAZ.CC = 'VARI' MO1 MA1   T2;
           'FINSI';
       'FIN' B3BHT;
*
        ED0MAZ = TMAZ.'LTR' * ( TMAZ.'YOUN' **(-1.) ) ;
        BCMAZ=(-1./(2.**.5))*((TMAZ.'NU' * TMAZ.'EMAX')**(-1.));
        BCED0MAZ= BCMAZ * ED0MAZ;
        ACMAZ1=((2.**.5)* TMAZ.'LCS' * TMAZ.'NU' *
               (TMAZ.'YOUN' **(-1.)  )) + ED0MAZ;
        ACMAZ2= BCMAZ * ( BCED0MAZ - ('EXP'(
          ('MANU' 'CHML' MO1 'SCAL' -1. 'TYPE' 'SCALAIRE' 'RIGIDITE')
          +  BCED0MAZ)));
        ACMAZ= ACMAZ1 * ACMAZ2;
        MI1 = 'MATE' MO1
              'YOUN' ('EXCO' TMAZ.'YOUN' 'SCAL' 'NOID' 'YOUN')
              'NU'   ('EXCO' TMAZ.'NU'   'SCAL' 'NOID' 'NU'  )
              'ALPH' ('EXCO' TMAZ.'ALPH' 'SCAL' 'NOID' 'ALPH');
        MI2 = 'MATE' MO1
              'ATRA' ('EXCO' TMAZ.'ATRA'  'SCAL' 'NOID' 'ATRA' )
              'BTRA' ('EXCO' TMAZ.'BTRA'  'SCAL' 'NOID' 'BTRA' )
              'BCOM' ('EXCO' BCMAZ        'SCAL' 'NOID' 'BCOM' )
              'ACOM' ('EXCO' ACMAZ        'SCAL' 'NOID' 'ACOM' )
              'KTR0' ('EXCO' ED0MAZ       'SCAL' 'NOID' 'KTR0' )
              'BETA' ('EXCO' TMAZ.'BETA'  'SCAL' 'NOID' 'BETA' );
*
        MI = MI1 + MI2 ;
        'SI' ('EXIST' TMAZ 'RHO' );
           MI= MI + ('MATE' MO1  'RHO'
                   ('EXCO' TMAZ.'RHO'   'SCAL' 'NOID' 'RHO'  ));
        'FINSI';
*
     'SINON';
        IFER=VRAI;
     'ERREUR' 'Le modele donne ne correspond pas a ceux attendus';
     'FINSI';
   'FINSI';
'FINSI';
'FINPROC' MI IFER;

