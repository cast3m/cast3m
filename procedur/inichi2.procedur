$$$$ INICHI2
* INICHI2   PROCEDUR  AURI      98/12/29    21:18:11     3419
DEBPROC INICHI2 TB3*TABLE TBC2*OBJET G1/MAILLAGE ;
*********************************************************************
*   modification d'un objet DONCHI2
*      TB3 table issue de chi1
*      TBC2 objet DONCHI2
*********************************************************************
CTOT= TBC2.TOT ;
CLGC= TBC2.LOGC ;
LICOMP= EXTR  CTOT 'COMP';
LILGC= EXTR  CLGC 'COMP';
LIIDX= TB3.DESCHI.IDX ;
NBIDX= DIME LIIDX ;
'SI' ('NON' ( 'EXIS' G1)) ;
      G1= EXTR  CTOT 'MAIL' ;
'FINS' ;
IMODT= FAUX ;
IMODL= FAUX ;
MOCOM1= MOTS ;
REPE BOUIDX NBIDX ;
NUNU= EXTR LIIDX &BOUIDX ;
 MO4 MO3 NU2 = NOCOMCHI TB3 'NUMCOMP' NUNU ;
MOCOM1= MOCOM1 ET (MOTS MO3) ;
SI ( NON (EXIS LICOMP MO3)) ;
CTOT= CTOT+ ( MANU CHPO G1 1 MO3 0.) ;
IMODT= VRAI ;
FINSI ;
SI ( NON (EXIS LILGC MO3)) ;
CLGC= CLGC+ ( MANU CHPO G1 1 MO3 0.) ;
IMODL= VRAI ;
FINSI ;
FIN BOUIDX ;
NBCOMP= DIME LIIDX ;
LICOMP= EXTR  CTOT 'COMP';
NBCOMT= DIME LICOMP ;
SI (NBCOMT NEG NBCOMP) ;
CTOT = EXCO MOCOM1 CTOT MOCOM1 ;
LICOMP= MOCOM1
  TBC2%GTOT CTOT ;
FINSI ;
LILGC= EXTR  CLGC 'COMP';
NBCOML= DIME LILGC ;
SI (NBCOML NEG NBCOMP) ;
CLGC = EXCO MOCOM1 CLGC MOCOM1 ;
LILGC= MOCOM1
  TBC2%GLOGC CLGC ;
FINSI ;
SI IMODT ;
IMODII='OUI' ;
 'MESS''Vous devez modifier la concentration totale ' ;
SINON ;
 'MESS''Voulez vous modifier les concentrations totales ?' ;
'OBTE' IMODII ;
FINSI ;
SI (EGA IMODII 'OUI' ) ;
*
 REPE BCTOT ;
  NTAB CTOT TITR 'CONCENTRATION TOTALE' ;
  LIMOT= EXTR CTOT 'COMP'  ;
 MOTOT= 'MENU' 'POUR MODIFIER UNE VALEUR CLIQUER SUR SON NOM ' LIMOT ;
 'SI' ('EGA' MOTOT 'Quitter' ) ;
 'QUIT' BCTOT ;
 'FINS' ;
  MO4 MO3 NU2 = NOCOMCHI TB3 'NOMINT' MOTOT ;
 'MESS' ' Entrez la concentration totale de ' NU2   MO4 ;
  'OBTE' VTOT*FLOTTANT ;
  'SI' ('NON' ('EGA' VTOT ('MOT' 'NON')))  ;
  CTOT1=  MANU CHPO G1 1 MO3 VTOT ;
  CTOT2= EXCO MO3 CTOT MO3 ;
  CTOT= CTOT+CTOT1 -CTOT2 ;
  'FINS' ;
 FIN BCTOT ;
*
* LIST CTOT   ;
  TBC2%GTOT CTOT ;
FINSI ;
SI IMODL ;
IMODII='OUI' ;
 'MESS''Vous devez modifier LOGC ' ;
SINON ;
 'MESS''Voulez vous modifier LOGC ?' ;
'OBTE' IMODII ;
FINSI ;
SI (EGA IMODII 'OUI' ) ;
 REPE BLGC1 ;
  NTAB CLGC 'TITR' ' VALEURS DE LOGC' ;
  LIMOT= EXTR CLGC 'COMP'  ;
 MOLGC= 'MENU' 'POUR MODIFIER UNE VALEUR CLIQUER SUR SON NOM ' LIMOT ;
 'SI' ('EGA' MOLGC 'Quitter' ) ;
 'QUIT' BLGC1 ;
 'FINS' ;
  MO4 MO3 NU2 = NOCOMCHI TB3 'NOMINT' MOLGC ;
 'MESS' ' Entrez LOGC de ' NU2   MO4 ;
  'OBTE' VLGC*FLOTTANT ;
  'SI' ('NON' ('EGA' VLGC ('MOT' 'NON')) ) ;
  CLGC1=  MANU CHPO G1 1 MO3 VLGC ;
  CLGC2=  EXCO MO3 CLGC MO3 ;
  CLGC= CLGC+CLGC1- CLGC2 ;
  'FINS' ;
 FIN BLGC1 ;
* LIST CLGC   ;
  TBC2%GLOGC CLGC ;
FINSI ;
 TIDEN=TB3.IDEN ;
 SI (EXIS TIDEN IMP3) ;
 NBIMP3= DIME TIDEN.IMP3 ;
SI (EXIS TBC2 'CLIM') ;
 IMOD3= FAUX ;
 CCLIM= TBC2.CLIM ;
LICLIM= EXTR CCLIM 'COMP';
 MOCOM1= MOTS ;
 REPE BOUIMP3 NBIMP3 ;
 NUNU=EXTR (  TIDEN.IMP3 ) &BOUIMP3 ;
   MO3 NU2 = NOESPCHI TB3  NUNU ;
 MOCOM1= MOCOM1 ET (MOTS MO3) ;
 SI (NON (EXIS LICLIM MO3) ) ;
 CCLIM= CCLIM+ (  MANU CHPO G1 1 MO3 0.) ;
 IMOD3= VRAI ;
 FINSI ;
FIN BOUIMP3 ;
LICLIM= EXTR CCLIM 'COMP';
NBCLIM= DIME LICLIM ;
SI( NBCLIM NEG NBIMP3 ) ;
CCLIM= EXCO MOCOM1 CCLIM MOCOM1 ;
 TBC2%GCLIM CCLIM;
FINSI ;
FINSI ;
SI IMOD3 ;
 'MESS''Vous devez modifier l  activité  imposée ' ;
  IMODII= MOT 'OUI' ;
SINON ;
 'MESS''Voulez vous modifier l  activité  imposée ?' ;
'OBTE' IMODII ;
FINSI ;
SI (EGA IMODII 'OUI' ) ;
SI (EXIS TBC2 'CLIM') ;
 CCLIM= TBC2.CLIM ;
LICLIM= EXTR CCLIM 'COMP';
NBCLIM= DIME TIDEN.IMP3 ;
     'REPE' BOUCLIM NBCLIM ;
     NUCLIM= 'EXTR' LICLIM &BOUCLIM ;
   MO3 NU2 = NOESPCHI TB3  NUCLIM ;
 VAL0= EXTR CCLIM MO3 ( G1 POIN 1) ;
 'MESS' ' Entrez l activité de ' NU2   ' Valeur actuelle ' VAL0 ;
  'OBTE' VLIM      ;
 'SI' (EGA ( TYPE VLIM)  'FLOTTANT') ;
  CCLIM0= EXCO MO3 CCLIM MO3 ;
  CCLIM1=  MANU CHPO G1 1 MO3 VLIM ;
  CCLIM= CCLIM+CCLIM1-CCLIM0 ;
  FINSI ;
     'FIN' BOUCLIM ;
* LIST CCLIM  ;
  TBC2%GCLIM CCLIM;
SINON  ;
 MESS ' Il n y a pas d activité imposée ' ;
FINSI ;
FINSI ;
SINON ;
SI (EXIS TBC2 'CLIM') ;
TBC2%OUBLIER CLIM ;
FINSI ;
FINSI ;
FINP ;

