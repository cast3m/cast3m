$$$$ @RAYO
* @RAYO     PROCEDUR  CB215821  15/12/02    21:15:02     8719

'DEBPROC' @RAYO PF*POINT P1*POINT NBC*ENTIER ;
* Procedure pour generer un maillage rayonnant autour du
* fond de fissure
&DIME = 'VALEUR' DIME ;
'SI' (&DIME 'NEG' 2) ;
   MESS 'PROCEDURE @RAYO : DIMENSION 2 OBLIGATOIRE' ;
   SAUT LIGNE;
   'QUITTER' @RAYO;
'FINSI';
OPTI ELEM QUA8 COUL ROUGE;
PFP1 = P1 'MOINS' PF ;
'SI' ((NBC * PI / 8.) > 4);
   MESS 'PROCEDURE @RAYO : ELEMENTS TROP DISTORDUS : DIMINUEZ LE '
        'NOMBRE DE COURONNES !' ;
   SAUT LIGNE;
   'QUITTER' @RAYO;
'FINSI';
*
FRAC = 1./NBC ;
EFRAC = FRAC / 100. ;
DENS  FRAC ;
PFP01 = FRAC * PFP1 ;
P01 = PF 'PLUS' PFP01 ;
L = 'DROIT' (NBC-1) P01 P1 ;
S1 = L 'ROTA' 5 90.  PF ;
L11 = S1 'COTE' 1 ;
L1EXT = S1 'COTE' 2 ;
L13 = S1 'COTE' 3 ;
L1INT = S1 'COTE' 4 ;
PEXTF = L1EXT 'POINT' 'FINAL' ;
LR = 'DROIT' 1 PF P01 ;
LLG = L11 'ET' LR ;
LRG = LR 'TOUR' 90. PF ;
LT = LRG 'ET' L1INT 'ET' LR ; ELIM LT EFRAC;
OPTI ELEM TRI6 ;
VEC1 = PF 'MOIN' (LT 'POIN' 3);
PT1 = PF 'MOIN' (VEC1 / 2.);
VEC2 = PF 'MOIN' (LT 'POIN' 4);
PT2 = PF 'MOIN' (VEC2 / 2.);
VEC3 = PF 'MOIN' (LT 'POIN' 5);
PT3 = PF 'MOIN' (VEC3 / 2.);
VEC4 = PF 'MOIN' (LT 'POIN' 6);
PT4 = PF 'MOIN' (VEC4 / 2.);
*
LT2 = 'CHANGER' 'SEG2' LT;
EL1 = 'MANU' 'TRI6' PF (LT2 'POIN' 2) (LT2 'POIN' 3) (LT2 'POIN' 4)
 (LT2 'POIN' 5) PT1 ;
EL2 = MANU TRI6 PF PT1 (LT2 'POIN' 5) (LT2 'POIN' 6) (LT2 'POIN' 7) PT2;
EL3 = MANU TRI6 PF PT2 (LT2 'POIN' 7) (LT2 'POIN' 8) (LT2 'POIN' 9) PT3;
EL4 = MANU TRI6 PF PT3 (LT2 'POIN' 9) (LT2 'POIN' 10) (LT2 'POIN' 11)
  PT4 ;
EL5 = MANU TRI6 PF PT4 (LT2 'POIN' 11) (LT2 'POIN' 12)
 ((LT2 'ELEM' 12) POINT 'FINAL') (LT2 'POIN' 14) ;
S0 = EL1 'ET' EL2 'ET' EL3 'ET' EL4 'ET' EL5;
S = S1 'ET' S0 ;
S2 L2EXT SI1 LL2 = S L1EXT S0 LLG SYME 'DROIT' PF PEXTF ;
P2EXTF = L2EXT POINT 'INITIAL' ;
'ELIM'  S  S2 EFRAC ;
S3 L3EXT SI2 LL3 = S2 L2EXT SI1 LL2 SYME 'DROIT' PF P2EXTF ;
S3 = S3 COUL JAUNE;
P3EXTF = L3EXT POINT 'FINAL' ;
'ELIM' S2  S3 EFRAC ;
S4 L4EXT SI3 LLD = S3 L3EXT SI2 LL3 SYME 'DROIT' PF P3EXTF ;
S4 = S4 COUL JAUNE;
'ELIM' S3 S4 EFRAC ;
SINT = S0 'ET' SI1 'ET' SI2 'ET' SI3;
ST =  S 'ET' S2 'ET' S3 'ET' S4 ;
'FINPROC' ST SINT L1EXT L2EXT L3EXT L4EXT LLD LLG;




