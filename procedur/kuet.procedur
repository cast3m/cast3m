$$$$ KUET
* KUET      PROCEDUR  MAGN      98/09/26    21:16:48     3310
*X KUET (Procedure)
*
*    procedure  KUET
*    ---------------
*
*    calcule la vitesse de frottement a la paroi U* pour un ecoulement
*  laminaire
*             U* = SQRT (TAU/RO) = SQRT (NU*dV/dY)
*
*      TAU  cisaillement
*      RO   masse volumique
*      NU   viscosite cinematique
*      V    vitesse parallele a la paroi
*      Y    distance a la paroi
*
*
*    SYNTAXE :  UET = KUET  NU UN NOR $DOMTOT $PAROI ;
*    --------
*       UET   CHPOINT SCAL CENTRE PAROI  vit. de frottement
*       NU    FLOTTANT                   viscosite cinematique
*       UN    CHPOINT VECT SOMMET        champ de vitesse
*       NOR   CHPOINT VECT FACE          champ des normales aux faces
*    $DOMTOT  MAILLAGE                   table domaine total
*    $PAROI   MAILLAGE              table domaine paroi support de UET
*
*-----------------------------------------------------------------------

DEBPROC KUET ;
ARGU NU*FLOTTANT UN*CHPOINT NOR*CHPOINT $DOMTOT*MMODEL $PAROI*MMODEL ;

VXM = MAXI  VX  'ABS' ;
VYM = MAXI  VY  'ABS' ;

* cas particulier où V // (OY) : VX = 0
SI ( VXM EGA 0. 1.E-6 ) ;

  GVY = KOPS  VY 'GRAD' $DOMTOT ;
  GXV = EXCO  UX GVY  ;
  GVY = KCHT  $DOMTOT SCAL CENTRE GXV ;
  GVS = ELNO  $DOMTOT GVY ;
  GVP = KCHT  $PAROI  SCAL SOMMET GVS ;

SINON ;
  SI ( VYM EGA 0. 1.E-6 ) ;
* cas particulier où V // (OX) : VY = 0

    GVX = KOPS  VX 'GRAD' $DOMTOT ;
    GYV = EXCO  UY GVX  ;
    GVY = KCHT  $DOMTOT SCAL CENTRE GYV ;
    GVS = ELNO  $DOMTOT GVY ;
    GVP = KCHT  $PAROI  SCAL SOMMET GVS ;

  SINON ;
* cas general où  NON (VX=0 OU VY=0 )

    NV = PSCA  VN VN (MOTS UX UY) (MOTS UX UY) ;
    NV = NV**0.5 ;
    NV = KCHT  $DOMTOT SCAL SOMMET NV ;
    GVC = KOPS NV 'GRAD' $DOMTOT ;
    GVS = ELNO  $DOMTOT GVC ;
    GVVP = KCHT  $PAROI VECT SOMMET GVS ;
    NORP = KCHT  $PAROI VECT CENTRE NOR ;
    NORS = ELNO  $PAROI NORP ;
    GVP  = PSCA  NORS GVVP (MOTS UX UY) (MOTS UX UY) ;

  FINSI ;
FINSI ;

DVDY = ABS  GVP ;
UET0 = (NU*DVDY)**0.5 ;
UET1 = KCHT $PAROI SCAL SOMMET UET0 ;
UET  = NOEL $PAROI UET1 ;

FINPROC UET ;



