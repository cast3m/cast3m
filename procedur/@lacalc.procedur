$$$$ @LACALC
* @LACALC   PROCEDUR  KK2000    99/11/30    21:15:02     3720
*
************************************************************************
*                        PROCEDURA @LACALC
************************************************************************
*
* Descrizione : effettua un calcolo elasto-statico su una geometria
*               costituita di piu' zone di materiale composito
*               multistrato.
*
* Sintassi    : DEP1 = @LACALC TAB_LAM CLIM FF (RIG2) ;
*
* Autore/Data : Miliozzi A.  10/04/1997
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_LAM  Tavola caratteristica del laminato composito
*
* CLIM     Condizioni al contorno per la struttura
*
* FF       Carichi da applicare alla struttura
*
* RIG2     Rigidezze addizionali relative a parti della
*          struttura non composte da laminati (Opzionale)
*
*--- OUTPUT
*==========
*
* DEP1     Campo di spostamenti
*
************************************************************************
*
DEBPROC @LACALC TCOMP*TABLE CLIM*RIGIDITE FF*CHPOINT
                RIG_AGG/RIGIDITE                   ;
* --- CALCOLO RIGIDEZZE ZONE COMPOSTE DA LAMINATI
*
 NZON = (DIME TCOMP) - 2                           ;
 NN    = 1                                         ;
 REPETER LOOP1 NZON                                ;
 SI (NN EGA 1)                                     ;
  TAB1   = @LAMAT TCOMP NN                         ;
  RIGITO = @LARIG TAB1                             ;
 SINON                                             ;
  TAB1   = @LAMAT TCOMP NN                         ;
  RIGITO = RIGITO ET ( @LARIG TAB1)                ;
 FINSI                                             ;
 NN = NN + 1                                       ;
FIN LOOP1                                          ;
*
* --- AGGIUNTA DI RIGIDEZZE ZONE NON IN LAMINATO
*
SI (EXIST RIG_AGG)                 ;
  RIGITO  = RIGITO ET RIG_AGG      ;
FINSI                              ;
*
* --- RIGIDEZZA TOTALE E RISOLUZIONE
*
RIGSTR = RIGITO   ET   CLIM ;
DEPSTR = RESOUDRE RIGSTR FF ;
*
FINPROC DEPSTR  ;
*
************************************************************************
*                       FINE PROCEDURA @LACALC
************************************************************************
*

