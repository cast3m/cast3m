$$$$ EXAC
* EXAC      PROCEDUR  CHAT      05/07/27    21:15:07     5137
*   Procedure EXAC  Ancienne procedure EXEC
*
*   Objet   :
*
*   Algorithme semi implicite pour les equations N.S. incompressible
*   Syntaxe :  EXAC RV ;
*   RV : Table EQEX contenant une entree PRESSION (table EQPR)
*
***********************************************************************
* VERSION    : ????
* HISTORIQUE : 20/12/99: gounand
* Le second membre et les conditions aux limites sont désormais tt le
* temps des chpoints (et pas des MOT lorsqu'ils sont vides).
* HISTORIQUE : 07/01/99: gounand
* Rajout de la gestion de la matrice servant à l'assemblage
* (rv . 'METHINV' . 'MATASS')
*
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
*
'DEBPROC'  EXAC ;
'ARGUMENT' rv*'TABLE   ' ;

tps=0.D0 ;
'SI'  ('EGA' ('VALEUR' 'DIME') 2) ;
  vnul=0.D0 0.D0 ;
'SINON' ;
  vnul=0.D0 0.D0 0.D0 ;
'FINSI' ;
'SI' ('NON' ('EXISTE' rv 'OMEGA')) ;
  omeg=1.D0 ;
'SINON' ;
  omeg=rv . 'OMEGA' ;
'FINSI' ;

testpr ='EXISTE' rv 'PRESSION' ;
testran=testpr 'ET' ('EXISTE' rv 'CO') ;

'SI' ('NON' ('EXISTE' rv 'HIST')) ;
  rv . 'HIST' = 'TABLE' ;
'FINSI' ;
'SI' ('NON' ('EXISTE' rv 'KIZG')) ;
  rv . 'KIZG' = 'TABLE' 'KIZG' ;
'FINSI' ;
'SI' ('NON' ('EXISTE' rv 'KIZG1')) ;
  rv . 'KIZG1' = 'TABLE' 'KIZG1' ;
'FINSI' ;
'SI' ('NON' ('EXISTE' rv 'KIZD')) ;
  'KDIA' rv ;
'FINSI'  ;

'SI' testpr ;
  rvp  =rv . 'PRESSION' ;
  nomvi=rv . 'NOMVI'  ;
  'SI' ('EXISTE' rv 'CLIM') ;
    rvp . 'CLIM' = rv . 'CLIM' ;
    rv . 'KIZD' . nomvi = 'KOPS' (rv . 'KIZD'. nomvi)
                          'CLIM' (rv . 'CLIM') 1;
  'FINSI' ;
  'SI' ('NON' ('EXISTE' rvp 'DIAGV')) ;
    rvp . 'DIAGV' = rv . 'KIZD' . nomvi ;
  'FINSI' ;
  rvp . 'MATC' = 'KMAB' rvp ;
  rvp . 'PRESSION' = 'KCHT' (rvp . 'DOMAINE') 'SCAL'
                     'CENTRE' 0.D0 ;
  rvp . 'GRADP'    = 'KCHT' (rvp . 'DOMAINE') 'VECT'
                     'SOMMET' vnul ;
'FINSI' ;

*mess 'EXAC ITMA=' (rv . 'ITMA') 'NITER=' (rv . 'NITER') ;
ITMA = (rv . 'ITMA') ;
'SI' ('EGA' ITMA 0) ;
  ITMA = 1 ;
'FINSI' ;
* Gestion de la matrice de préconditionnement
*
* calass  : doit-on recalculer l'assemblage
calass  = VRAI ;
'SI'(exist rv 'calass') ;
  calass=rv.'calass' ;
  matass=rv.'matass';
'FINSI' ;

'REPETER' bloc1 ITMA ;
  'REPETER' bloci (rv . 'NITER') ;
    ma1  = 'KOPS' 'MATRAK' ;
    kma1 = 0 ;
    'REPETER' bloc2 ('DIME' (rv . 'LISTOPER')) ;
      nomper = 'EXTRAIRE' &bloc2 (rv . 'LISTOPER') ;
      notable= 'MOT' ('TEXTE' ('CHAINE' &bloc2 nomper)) ;
*   mess ' EXAC OPERATEUR ' nomper ;
      ('TEXTE' nomper) (rv . notable) ;
      'SI' ('EXISTE' (rv . notable) 'MATELM' ) ;
        'SI' ('EGA' ('TYPE' (rv . notable . 'MATELM'))
                            'MATRIK  ') ;
          kma1= 1 ;
          ma1 = ma1 'ET' (rv . notable . 'MATELM') ;
        'FINSI' ;
      'FINSI' ;
    'FIN' bloc2 ;
    dt = (rv . 'PASDETPS' . 'DELTAT') '*' (rv . 'ALFA') ;
    tps= tps '+' dt ;

    'SI' testpr ;
      rvp . 'DELTAT' = dt ;
      f=rv . 'KIZG' . nomvi ;
      u=rv . 'INCO' . nomvi ;
      'SI' ('EXISTE'  (RV . 'KIZG1') nomvi) ;
        f1 = 'KOPS' f
              '+'   ('KOPS' (rv . 'KIZG1' . nomvi) '*' u) ;
      'SINON' ;
        f1=f ;
      'FINSI'  ;
      dm1f='KOPS' ('KOPS' f1 '/' (rv . 'KIZD' . nomvi))
            '-'   ('KOPS'  U '/' dt) ;

      rvp . 'PRESSION' = 'KMF' (rvp . 'MATC') dm1f ;
      rvp . 'FORCE'    = 'KCHT' (rvp . 'DOMAINE') 'SCAL' 'CENTRE'
                             0.D0 ;
      'KRES' rvp (rvp . 'PRESSION')
             'BETA' (rvp . 'KBETA') (rvp . 'BETA')
             'PIMP' (rvp . 'KPIMP') (rvp . 'PIMP') ;
      rvp . 'GRADP' = 'KMTP' 0 (rvp . 'MATC')
                               (rvp . 'PRESSION') ;
      rv . 'KIZG' . nomvi = 'KOPS' f
                              '-'  (rvp . 'GRADP') ;
    'FINSI' ;

    'SI' ('EGA' kma1 1) ;
      'SI' ('EXISTE' rv 'CLIM') ;
        s1 = rv . 'CLIM' ;
      'SINON' ;
        s1 matnul = 'KOPS' 'MATRIK' ;
        'OUBLIER' matnul ;
      'FINSI'  ;
      'SI' calass ;
        matass=ma1 ;
        rv.'matass'=matass ;
      'FINSI' ;
      rv . 'METHINV' . 'MATASS' = matass ;
      dt1 = 1. ;
      s2 = 'KOPS' 'MTABX' rv 1.D0 ;
      rv . 'S2' = s2 ;
      'SI'  ('NEG' ('TYPE' s2) 'CHPOINT ') ;
        s2 matnul = 'KOPS' 'MATRIK' ;
        'OUBLIER' matnul ;
      'FINSI' ;
      res = kres ma1 'TYPI' (rv . 'METHINV')
                     'CLIM' s1
                     'SMBR' s2
                     'IMPR' 0 ;
      eps = 'TCRR' res omeg (rv . 'INCO') 'IMPR' 1 ;
      'OUBLIER' res ;
    'FINSI'  ;
    'SI' ('NON' calass) ;
          'OUBLIER' ma1 ;
    'FINSI' ;
    'SI' ('EGA' kma1 1) ;
      calass = FAUX ;
      rv.'calass'=calass ;
    'FINSI' ;
    'MENAGE' ;
  'FIN' bloci ;

  irt=0 ;
  'SI' ('EGA' kma1 1) ;
    'SI' (dt '>' 0.D0) ;
      'TCNM' rv ;
    'FINSI' ;
  'SINON' ;
    irt = 'AVCT' rv (rv . 'ALFA') 'IMPR' (rv . 'FIDT') ;
  'FINSI' ;

  'SI' testran ;
    rv . 'CO' . 'VITESSE' ='KOPS' (rv . 'INCO' . nomvi)
                             '-'  (rv . 'SEDIM') ;
    k = 'ABS' (rv . 'INCO' . 'KN') ;
    e = 'ABS' (rv . 'INCO' . 'EN') ;
    k = 'KOPS' ('KOPS' k '*' k) '/' e ;
    DIF = 'KOPS' ('KOPS' k '*' 0.09) '+' (rv . 'COEF') ;
    rv . 'CO' . 'DIFFU'   = 'NOEL' (rv . 'DOMAINE') dif ;
    rv . 'CO' . 'TEMPERA' = rv . 'INCO' . 'CN' ;
  'FINSI' ;
  'MENAGE' ;

  'SI' ('EGA' irt 1) ;
    'MESSAGE' ' Temps final atteint : '
              (rv . 'PASDETPS' . 'TPS') ;
    'QUITTER' bloc1 ;
  'FINSI' ;
'FIN' bloc1 ;

'SI' testpr ;
  rvp . 'PRESSION' = 'KCHT' (rvp . 'DOMAINE') 'SCAL' 'CENTRE'
                           (rvp . 'PRESSION') ;
  rvp . 'PN'       = 'ELNO' (rvp . 'DOMAINE')
                           (rvp . 'PRESSION') ;
'FINSI' ;
************************    E X A C ************************************
'FINPROC' ;




