$$$$ @PRCA5
* @PRCA5    PROCEDUR  MB234859  18/11/21    21:15:02     10007
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*                    PROCEDURE DE CALCUL @PRCA5
*
*  Calcul de l'energie dissipee durant le cycle numerique (integration
*  du cycle de fretting), du volume use durant beta cycles numeriques
*  (integration du profil d'usure) et du nombre de cycles reels
*  equivalent (variable scalaire incrementee).
*
* Entrees :
*   T1    : Table de PASAPAS
*   ID_BU : Entier donnant le numero de la BOITE D'USURE a considerer
*
* Sortie :
*   T1    : Table de PASAPAS avec les booleens mis a jour
*
* Appelee par @EVN_MNG
*
***********************************************************************
DEBP @PRCA5 T1*'TABLE' ID_BU*'ENTIER';

  BOITE_US = T1.'BOITES_USURES'. ID_BU ;
  NCNECC   = BOITE_US.'NCNECC'   ;
  BETA     = BOITE_US.'FACTEUR_ACCELERATION' ;

  SI ('EXIS' BOITE_US 'LISTE_NUMERO_CYCLE_NUMERIQUE') ;
    LstNCN   = BOITE_US.'LISTE_NUMERO_CYCLE_NUMERIQUE'         ;
    LstNCRE  = BOITE_US.'LISTE_NOMBRE_CYCLES_REELS_EQUIVALENT' ;
    LstVolUs = BOITE_US.'LISTE_VOLUME_USE_CUMULE'              ;
    LstVUsPC = BOITE_US.'LISTE_VOLUME_USE_PAR_CYCLE'           ;
    LstEdPC  = BOITE_US.'LISTE_ENERGIE_DISSIPEE_PAR_CYCLE_NUM' ;
    LstEdTot = BOITE_US.'LISTE_ENERGIE_DISSIPEE_TOTALE'        ;
    NCRE     = BOITE_US.'NOMBRE_CYCLES_REELS_EQUIVALENT'       ;
    LstDltaG = BOITE_US.'LISTE_DELTA_G_ESTIMES'                ;
    LstDltaT = BOITE_US.'LISTE_DELTA_T_ESTIMES'                ;
    LstPrMax = BOITE_US.'LISTE_PRESSION_MAX'     ;
    LstCiMax = BOITE_US.'LISTE_CISAILLEMENT_MAX' ;
  SINON ;
    LstNCN   = PROG    ;
    LstNCRE  = PROG    ;
    Lst0NCN  = PROG 0 .;
    Lst0NCRE = PROG 0. ;
    LstVolUs = PROG 0. ;
    LstVUsPC = PROG    ;
    LstEdPC  = PROG    ;
    LstEdTot = PROG 0. ;
    NCRE     = 0.      ;
    LstDltaG = PROG    ;
    LstDltaT = PROG    ;
    LstPrMax = PROG    ;
    LstCiMax = PROG    ;
  FINSI ;

  EvNouvUs = BOITE_US.'EVO_USURE_APPLIQUEE'        . NCNECC ;
  EvCumuUs = BOITE_US.'EVO_USURE_CUMULEE'          . NCNECC ;
  CyclFret = BOITE_US.'CYCLE_DE_FRETTING'          . NCNECC ;
  EvEnDCyc = BOITE_US.'EVO_ENERGIE_DISSIPEE_CYCLE' . NCNECC ;
  EvEnDTot = BOITE_US.'EVO_ENERGIE_DISSIPEE_TOTALE'      ;
  LstIncCC = BOITE_US.'LISTE_INCREMENTS_DU_CYCLE'.NCNECC ;
  LstTpsCC = BOITE_US.'LISTE_TEMPS_DU_CYCLE'.     NCNECC ;
  NbrIncCC = DIME LstIncCC ;
  PremIncr = MINI LstIncCC ;
  LstMPres = PROG          ; LstMCisa = PROG ; LstMGlis = PROG ;

* Pression de contact moyenne et cisaillement moyen
  REPE BSICC NbrIncCC ;
    Inc_ii   = PremIncr + &BSICC - 1 ;
    PrfPress = BOITE_US.'EVO_PRESSION_CONTACT'     . Inc_ii ;
    PrfCisai = BOITE_US.'EVO_CISAILLEMENT_CONTACT' . Inc_ii ;
    PrfGliss = BOITE_US.'EVO_GLISSEMENT_CONTACT'   . Inc_ii ;

    OrdPress = EXTR PrfPress 'ORDO' ;
    OrdCisai = EXTR PrfCisai 'ORDO' ;
    OrdDCSli = EXTR PrfGliss 'ORDO' ;
    MaxPr_ii = MAXI OrdPress ; MaxCi_ii = MAXI (ABS OrdCisai) ;
    MaxDCSii = MAXI (ABS OrdDCSli) ;
    LstMPres = ET LstMPres MaxPr_ii ; LstMCisa = ET LstMCisa MaxCi_ii ;
    LstMGlis = ET LstMGlis MaxDCSii ;
    SI (&BSICC EGA 1);
      PressCum = EXTR PrfPress 'ORDO' ;
      CisaiCum = EXTR PrfCisai 'ORDO' ;
    SINON ;
      PressCum = PressCum + (EXTR PrfPress 'ORDO') ;
      CisaiCum = CisaiCum + (EXTR PrfCisai 'ORDO') ;
    FINSI ;
  FIN BSICC ;

  MaxPress = MAXI LstMPres ; MaxCisai = MAXI LstMCisa ;
  yPresMoy = PressCum / NbrIncCC ;
  yCisaMoy = CisaiCum / NbrIncCC ;
  xAbsc    = EXTR PrfPress 'ABSC' ;
  PressMoy = EVOL 'MANU' 'Absc' xAbsc 'Press. Contact' yPresMoy ;
  CisaiMoy = EVOL 'MANU' 'Absc' xAbsc 'Cisai. Contact' yCisaMoy ;
  BOITE_US.'EVO_PRESSION_MOYENNE_SUR_CYCLE'   . NCNECC = PressMoy ;
  BOITE_US.'EVO_CISAILLEMENT_MOYEN_SUR_CYCLE' . NCNECC = CisaiMoy ;

* Energie dissipee sur le cycle
  ECycFret = INTG CyclFret ;
  F_N_CyFr = EXTR CyclFret 'ORDO';
  Ampl_F_N = (MAXI F_N_CyFr) - (MINI F_N_CyFr) ;
  DltG_Est = ECycFret / 2. / Ampl_F_N ;
  LstDltaG = ET LstDltaG DltG_Est ;
*
  Dlt_CyFr = EXTR CyclFret 'ABSC';
  Ampl_Dlt = (MAXI Dlt_CyFr) - (MINI Dlt_CyFr) ;
  DltT_Est = (Ampl_Dlt / 2.) - DltG_Est ;
  LstDltaT = ET LstDltaT DltT_Est ;

* Numero du cycle calcule
  LstNCN   = ET LstNCN NCNECC ;
  Lst0NCN  = 0 ET LstNCN      ;
*
* Nombre de Cycle Reel Equivalent
  NCRE     = NCRE + BETA      ;
  LstNCRE  = ET LstNCRE NCRE  ;
*
  Lst0NCRE = 0 ET LstNCRE     ;
*
* Energie dissipee sur le cycle
  EdSurCyc = INTG EvEnDCyc ;
  LstEdPC  = ET LstEdPC  EdSurCyc ;
*
* Energie dissipee totale
  EdTotale = INTG EvEnDTot ;
  LstEdTot = ET LstEdTot EdTotale ;
*
* Volume use total
  VolUsTot = INTG EvCumuUs ;
  LstVolUs = ET LstVolUs VolUsTot ;
*
* Volume use pendant le cycle
  VolUsCyc = INTG EvNouvUs ;
  LstVUsPC = ET LstVUsPC VolUsCyc ;
*
  LstPrMax = ET LstPrMax MaxPress ;
  LstCiMax = ET LstCiMax MaxCisai ;

* Creation d'Evolution
  TxtNCRE  = 'Nb. Cyc. Reels' ;  TxtNCN   = 'Cyc. Num.'  ;
* Energie dissipee par cycle numerique
  EvEdByCN = EVOL 'MANU' 'LEGE' 'En Dis Par Cyc. Num.' TxtNCN LstNCN
                                'Ener. Diss. /Cycl.' LstEdPC ;
* Energie equivalente dissipee totale
  EvEnDisT = EVOL 'MANU' TxtNCRE Lst0NCRE 'Ener. Diss. Totale' LstEdTot;
  EvEnDisC = EVOL 'MANU' TxtNCRE LstNCRE 'Ener. Diss. / Cyc'   LstEdPC ;
  EvVUsCRE = EVOL 'MANU' TxtNCRE Lst0NCRE 'Vol. Use Cumule'    LstVolUs;
  EvVUsEdT = EVOL 'MANU' 'En. Diss. Totale' LstEdTot
                         'Vol. Use Cumule'  LstVolUs  ;
  TxtDG    = 'Delta G (mm)'   ;  TxtDT    = 'Delta T (mm)'   ;
  EvDtGCRE = EVOL 'BLEU' 'MANU' TxtNCRE LstNCRE  TxtDG LstDltaG ;
  EvDltGCN = EVOL 'VERT' 'MANU' TxtNCN  LstNCN   TxtDG LstDltaG ;
  EvDtTCRE = EVOL 'ROUG' 'MANU' TxtNCRE LstNCRE  TxtDT LstDltaT ;
  EvDltTCN = EVOL 'ORAN' 'MANU' TxtNCN  LstNCN   TxtDT LstDltaT ;
  EvMaxPre = EVOL 'VERT' 'MANU' TxtNCN  LstNCN 'Pression Max' LstPrMax ;
  EvMaxCis = EVOL 'ROUG' 'MANU' TxtNCN  LstNCN 'Cisaill. Max' LstCiMax ;
  EvMaxDCS = EVOL 'BLEU' 'MANU' 'Tps cycle'  LstTpsCC
                                'Max Gliss.' LstMGlis ;

* Stockage des resultats
  BOITE_US.'LISTE_NUMERO_CYCLE_NUMERIQUE'                = LstNCN   ;
  BOITE_US.'LISTE_NOMBRE_CYCLES_REELS_EQUIVALENT'        = LstNCRE  ;
  BOITE_US.'LISTE_VOLUME_USE_CUMULE'                     = LstVolUs ;
  BOITE_US.'LISTE_VOLUME_USE_PAR_CYCLE'                  = LstVUsPC ;
  BOITE_US.'LISTE_ENERGIE_DISSIPEE_PAR_CYCLE_NUM'        = LstEdPC  ;
  BOITE_US.'LISTE_ENERGIE_DISSIPEE_TOTALE'               = LstEdTot ;
  BOITE_US.'NOMBRE_CYCLES_REELS_EQUIVALENT'              = NCRE     ;
  BOITE_US.'LISTE_DELTA_G_ESTIMES'                       = LstDltaG ;
  BOITE_US.'LISTE_DELTA_T_ESTIMES'                       = LstDltaT ;
  BOITE_US.'LISTE_PRESSION_MAX'                          = LstPrMax ;
  BOITE_US.'LISTE_CISAILLEMENT_MAX'                      = LstCiMax ;

  BOITE_US.'EVO_ENERGIE_DISS_PAR_CYCLE_NUM'              = EvEdByCN ;
  BOITE_US.'EVO_ENERGIE_DISS_TOTALE'                     = EvEnDisT ;
  BOITE_US.'EVO_ENERGIE_DISS_PAR_CYCLE'                  = EvEnDisC ;
  BOITE_US.'EVO_VOLUME_USE_TOTAL_VS_CYCLES_REELS_EQUIV'  = EvVUsCRE ;
  BOITE_US.'EVO_VOLUME_USE_TOTAL_VS_ENERGIE_DISS_TOTALE' = EvVUsEdT ;
  BOITE_US.'EVO_DELTA_G_ESTIMES_VS_CYCLES_REELS_EQUIV'   = EvDtGCRE ;
  BOITE_US.'EVO_DELTA_G_ESTIMES_VS_CYCLES_NUMS'          = EvDltGCN ;
  BOITE_US.'EVO_DELTA_T_ESTIMES_VS_CYCLES_REELS_EQUIV'   = EvDtTCRE ;
  BOITE_US.'EVO_DELTA_T_ESTIMES_VS_CYCLES_NUMS'          = EvDltTCN ;
  BOITE_US.'EVO_PRESSION_MAX_VS_CYCLES_NUMS'             = EvMaxPre ;
  BOITE_US.'EVO_CISAILLEMENT_MAX_VS_CYCLES_NUMS'         = EvMaxCis ;
  BOITE_US.'EVO_AIRE_DE_CONTAC_VS_CYCLES_NUMS'           = EvCoArea ;

* Enregistrement Donnees cycliques
  BOITE_US.'EVO_MAX_DCSLIP_VS_TEMPS_DU_CYCLE'. NCNECC    = EvMaxDCS;

FINP T1 ;
$$$$


