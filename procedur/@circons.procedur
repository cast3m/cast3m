$$$$ @CIRCONS
* @CIRCONS  PROCEDUR  PASCAL    12/10/18    21:15:01     7532
*---------------------------------------------------------------------*
* NOM         : @CIRCONS                                              *
*                                                                     *
* DESCRIPTION : Procedure calculant le centre et le rayon du cercle   *
*               (sphere) circonscrit(e) a un element de type TRI3     *
*               (TET4) en 2D (3D).                                    *
*                                                                     *
* SYNTAXE     : PT1 R1 = @CIRCONS ELT1 ;                              *
*                                                                     *
*    - ELT1   = MAILLAGE, 1 element de type TRI3 ou TET4  ;           *
*    - PT1    = POINT, centre du cercle (sphere) circonscrit(e) ;     *
*    - R1     = FLOTTANT, rayon du cercle (sphere) circonscrit(e) ;   *
*                                                                     *
* LANGAGE     : GIBIANE-CAST3M                                        *
* AUTEUR      : S. PASCAL (CEA/DEN/DM2S/SEMT/LM2S)                    *
* COURRIEL    : serge.pascal@cea.fr                                   *
*---------------------------------------------------------------------*
* VERSION    : v1, 22/09/2006, version initiale                       *
* HISTORIQUE : v1, 22/09/2006, creation                               *
* HISTORIQUE :                                                        *
* HISTORIQUE :                                                        *
*---------------------------------------------------------------------*
* Priere de PRENDRE LE TEMPS de completer les commentaires            *
* en cas de modification de ce sous-programme afin de faciliter       *
* la maintenance !                                                    *
*---------------------------------------------------------------------*
'DEBP' @CIRCONS ;
'ARGU' ELT1*'MAILLAGE' ;
ELT1     = 'CHAN' 'POI1' ELT1 ;
DIM1     = 'VALE' 'DIME' ;
'SI' (DIM1 'EGA' 2) ;
  PT1      = ELT1 'POIN' 1 ;
  PT2      = ELT1 'POIN' 2 ;
  PT3      = ELT1 'POIN' 3 ;
  VP1      = PT2 'MOIN' PT3 ;
  VP2      = PT1 'MOIN' PT3 ;
  DET1     = 'PMIX' VP1 VP2 ;
  'SI' (('ABS' DET1) '<' 1.E-8) ;
    VP3      = PT2 'MOIN' PT1 ;
    DET1     = 'PMIX' VP2 VP3 ;
    'SI' (('ABS' DET1) '<' 1.E-8) ;
      DET1     = 'PMIX' VP3 VP1 ;
      PTX    = PT1 ;
      PT1    = PT3 ;
      PT3    = PT2 ;
      PT2    = PTX ;
      VP2    = VP1 ;
      VP1    = VP3 ;
    'SINO' ;
      PTX    = PT1 ;
      PT1    = PT2 ;
      PT2    = PT3 ;
      PT3    = PTX ;
      VP1    = VP2 ;
      VP2    = VP3 ;
    'FINS' ;
  'FINS' ;
  PM1      = 0.5 * (PT3 'PLUS' PT2) ;
  PM2      = 0.5 * (PT1 'PLUS' PT3) ;
  XN1M1    = 'PSCA' VP1 PM1 ;
  YN2M2    = 'PSCA' VP2 PM2 ;
  XCELT1   = ((VP2 'COOR' 2) * XN1M1) - ((VP1 'COOR' 2) * YN2M2) ;
  YCELT1   = ((VP1 'COOR' 1) * YN2M2) - ((VP2 'COOR' 1) * XN1M1) ;
  CELT1    = XCELT1 YCELT1 ;
  CELT1    = CELT1 / DET1 ;
  RELT1    = 'NORM' (PT1 'MOIN' CELT1) ;
'SINO' ;
  'SI' (DIM1 'EGA' 3) ;
    PT1      = ELT1 'POIN' 1 ;
    PT2      = ELT1 'POIN' 2 ;
    PT3      = ELT1 'POIN' 3 ;
    PT4      = ELT1 'POIN' 4 ;
    P4MP1    = PT4 'MOIN' PT1 ;
    P4MP2    = PT4 'MOIN' PT2 ;
    P4MP3    = PT4 'MOIN' PT3 ;
    A11      = P4MP1 'COOR' 1 ;
    A21      = P4MP2 'COOR' 1 ;
    A31      = P4MP3 'COOR' 1 ;
    A12      = P4MP1 'COOR' 2 ;
    A22      = P4MP2 'COOR' 2 ;
    A32      = P4MP3 'COOR' 2 ;
    A13      = P4MP1 'COOR' 3 ;
    A23      = P4MP2 'COOR' 3 ;
    A33      = P4MP3 'COOR' 3 ;
    W1       = (A22 * A33) - (A32 * A23) ;
    W2       = (A31 * A23) - (A21 * A33) ;
    W3       = (A21 * A32) - (A31 * A22) ;
    FF1      = 0.5 / ((A11 * W1) + (A12 * W2) + (A13 * W3)) ;
    B11      = W1 ;
    B21      = W2 ;
    B31      = W3 ;
    B12      = (A32 * A13) - (A12 * A33) ;
    B22      = (A11 * A33) - (A31 * A13) ;
    B32      = (A31 * A12) - (A11 * A32) ;
    B13      = (A12 * A23) - (A22 * A13) ;
    B23      = (A21 * A13) - (A11 * A23) ;
    B33      = (A11 * A22) - (A21 * A12) ;
    N2PT1    = ('NORM' PT1) ** 2 ;
    N2PT2    = ('NORM' PT2) ** 2 ;
    N2PT3    = ('NORM' PT3) ** 2 ;
    N2PT4    = ('NORM' PT4) ** 2 ;
    FX1      = N2PT4 - N2PT1 ;
    FY1      = N2PT4 - N2PT2 ;
    FZ1      = N2PT4 - N2PT3 ;
    CELT1    = ((B11 * FX1) + (B12 * FY1) + (B13 * FZ1))
               ((B21 * FX1) + (B22 * FY1) + (B23 * FZ1))
               ((B31 * FX1) + (B32 * FY1) + (B33 * FZ1)) ;
    CELT1    = FF1 * CELT1 ;
    RELT1    = 'NORM' (PT1 'MOIN' CELT1) ;
  'FINS' ;
'FINS' ;
'RESP' CELT1 RELT1 ;
'FINP' ;
*---------------------------------------------------------------------*
* FIN PROCEDURE @CIRCONS
$$$$

