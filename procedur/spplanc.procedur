$$$$ SPPLANC
* SPPLANC   PROCEDUR  BP208322  16/05/24    21:15:18     8932
**********************************************************************
*                                                                    *
*          UNE PROCEDURE LE CALCUL DES SPECTRES DE PLANCHER          *
*                                                                    *
**********************************************************************
*
'DEBPROC' SPPLANC ZTAB*'TABLE' ;
**********************************************************************
**********************************************************************
*                                                                    *
*                     PROCEDURE   S P P L A N C                     *
*                     ---------------------------                    *
*                                                                    *
*                 AUTEUR F. BEN JEMAA (DMT/SEMT/EMSI)                *
*                                                                    *
*     CALCUL DES SPECTRES DE PLANCHER PAR UNE APPROCHE ANALYTIQUE    *
*       THESE DE DOCTORAT DE 3 EME CYCLE 01/10/1990 - 30/03/1994     *
*                                                                    *
**********************************************************************
*                                                                    *
*     SPPLANC   TAB1                                                *
*                                                                    *
*                TAB1     :      OBJET DE TYPE TABLE                 *
*                                                                    *
**********************************************************************
*                                                                    *
* TAB1   :   objet de type table contenant                           *
*                                                                    *
*                                                                    *
*     Indice   Type                         Commentaires             *
*                                                                    *
*     STRUC    TABLE                        Caractéristiques modales *
*                                           de la structure support  *
*                                                                    *
*                     Indice     Type                                *
*                                                                    *
*                     NMODE      ENTIER     Nombre de modes          *
*                     FREQU      TABLE      Les frequences modales   *
*                     AMORT      TABLE      Les amortissements modaux*
*                                                                    *
*     PLANCH   TABLE                        Caractéristiques du      *
*                                           Plancher étudié (P)      *
*                                                                    *
*                     Indice     Type                                *
*                                                                    *
*                     LISFREQ    LISTREEL   Axe fréquentiel du       *
*                                           spectre de plancher      *
*                                              PAR DEFAUT :          *
*                                           discrétisation par la    *
*                                           procédure "DISCRFR"      *
*                                                                    *
*                     COEFFPL    TABLE      Les coefficients de      *
*                                           participation modale     *
*                                           en P : ( Qn * PHIn ) / Mn*
*                                                                    *
*                     AMORTPL    FLOTTANT   Amortissement du spectre *
*                                           de plancher à calculer   *
*                                                                    *
*                     TYPSPPL    MOT        Type du spectre de       *
*                                           plancher                 *
*                                           'DEPL' : déplacement     *
*                                           'VITE' : pseudo-vitesse  *
*                                           'ACCE' : pseudo-accél.   *
*                                                                    *
*     EXCIT    TABLE                        Données de l'excitation  *
*                                           ( processus séparable )  *
*                                                                    *
*                     Indice     Type                                *
*                                                                    *
*                     ENVE       MOT        Type de l'enveloppe      *
*                                           'PLATLIN' :              *
*                                           montée-plat-descente     *
*                                           ( par défaut : plateau ) *
*                                                                    *
*                     DUREE      FLOTTANT   Durée du signal          *
*                                                                    *
*                     TDEB       FLOTTANT   Temps où commence        *
*                                           le plateau ( montée )    *
*                                                                    *
*                     TFIN       FLOTTANT   Temps où se termine      *
*                                           le plateau ( début desc.)*
*                                                                    *
*                     DSP        EVOLUTION  D.S.P. de la fonction    *
*                                           aléatoire stationnaire   *
**********************************************************************
**********************************************************************
*
*=====================================================================
* LES DONNEES DU PROBLEME
*=====================================================================
*
ZPI           = PI ; Z2PI = 2.0 * ZPI ;
*
STRUC         = 'MOT' 'STRUC' ;
NMODE         = 'MOT' 'NMODE' ;
FREQU         = 'MOT' 'FREQU' ;
AMORT         = 'MOT' 'AMORT' ;
PLANCH        = 'MOT' 'PLANCH' ;
LISFREQ       = 'MOT' 'LISFREQ' ;
COEFFPL       = 'MOT' 'COEFFPL' ;
AMORTPL       = 'MOT' 'AMORTPL' ;
TYPSPPL       = 'MOT' 'TYPSPPL' ;
EXCIT         = 'MOT' 'EXCIT' ;
DUREE         = 'MOT' 'DUREE' ;
ENVE          = 'MOT' 'ENVE' ;
TDEB          = 'MOT' 'TDEB' ;
TFIN          = 'MOT' 'TFIN' ;
DSP           = 'MOT' 'DSP' ;
*
ZNMOD         = ZTAB. STRUC . NMODE ;
TABF          = ZTAB. STRUC . FREQU ;
TEPS          = ZTAB. STRUC . AMORT ;
*
TX            = ZTAB. PLANCH . COEFFPL ;
EPSPL         = ZTAB. PLANCH . AMORTPL ;
ZTYPSP        = ZTAB. PLANCH . TYPSPPL ;
*
ZPTLINE       = 'MOT' 'PLATLIN' ;
ZPTPLAT       = 'MOT' 'PLATEAU' ;
ZT            = ZTAB. EXCIT . DUREE ;
ZENVE         = ZTAB. EXCIT . ENVE ;
'SI' ( 'EGA' ZENVE ZPTLINE ) ;
    ZTDEB     = ZTAB. EXCIT . TDEB ;
    ZTFIN     = ZTAB. EXCIT . TFIN ;
'SINON' ;
    'MESS' 'L ENVELOPPE EST CONSIDERE PAR DEFAUT PLATEAU' ;
    ZENVE     = ZPTPLAT ;
'FINSI' ;
DSPE          = ZTAB. EXCIT . DSP ;
*
*=====================================================================
* CARACTERISATION DU MOUVEMENT DU PLANCHER
* PAR UN PROCESSUS SEPARABLE
*=====================================================================
*
'MESS' ' ' ;
'MESS' '==============================================================';
'MESS' 'CARACTERISATION DU MOUVEMENT DU PLANCHER' ;
'MESS' '==============================================================';
'MESS' ' ' ;
'MESS' 'STRUCTURE SUPPORT : ' ;
'MESS' '-----------------' ;
*
* DEFINITION DE L'AXE DES TEMPS :
*
IMOD          = 0 ;
ZZ1           = 0.0 ;
'REPETER' BOUCM ZNMOD ;
IMOD          = IMOD '+' 1 ;
ZZ0           = ( TABF.IMOD ) '*' ( TEPS.IMOD ) ;
'SI' ( ZZ0 '>' ZZ1 ) ;
   ZI0           = IMOD ;
   ZZ1           = ZZ0 ;
'FINSI' ;
'FIN' BOUCM ;
*
LTEV          = DISCRTM  TABF.ZI0   TEPS.ZI0  ZT ;
NTV           = 'DIME'  LTEV ;
LTEV0         = 'ENLEVER'  LTEV  1 ;
NTV1          = NTV '-' 1 ;
*
* CREATION DE L'ENVELOPPE D'EXCITATION
* DANS LE CAS OU L'ENVELOPPE EST LINEAAIRE-PLAT-LINEAIRE
*
TABENV        = 'TABLE' ;
TABENV.1      = LTEV ;
'SI' ( 'EGA' ZENVE ZPTLINE ) ;
    ZDT1      = ZTDEB '/' 10.0 ;
    LT1       = 'PROG' 0.0 'PAS' ZDT1 ZTDEB ;
    LENV1     = LT1 '*' ( 1.0 / ZTDEB ) ;
    ZDT2      = ( ZTFIN '-' ZTDEB ) '/' 10.0 ;
    LT2       = 'PROG' ( ZTDEB + ZDT2 ) 'PAS' ZDT2 ZTFIN ;
    LENV2     = 'PROG' ( 'DIME' LT2 ) '*' 1.0 ;
    ZDT3      = ( ZT '-' ZTFIN ) '/' 10.0 ;
    LT3       = 'PROG' ( ZTFIN + ZDT3 ) 'PAS' ZDT3 ZT ;
    LENV3     = ( 'PROG' ( 'DIME' LT3 ) '*' ZT ) '-' LT3 ;
    LENV3     = LENV3 '*' ( 1.0 '/' ( ZT '-' ZTFIN ) ) ;
    LT0       = LT1 'ET' LT2 'ET' LT3 ;
    LENV0     = LENV1 'ET' LENV2 'ET' LENV3 ;
    LENV      = 'IPOL' LTEV LT0 LENV0 ;
    TITRE 'ENVELOPPE : TDEBUT =' ZTDEB 'TFIN =' ZTFIN ;
    ENV       = 'EVOL' 'MANU' 'TEMPS' LTEV 'ENVELOP' LENV ;
    DESSIN ENV 'CARR' 'MIMA' ;
    TABENV.2  = LENV ;
'FINSI' ;
*
* CALCUL DES COEFFICIENTS D'AJUSTEMENT
* DE LA FONCTION DE TRANSFERT DE LA STRUCTURE SUPPORT
*
* APPEL DE LA PROCEDURE AJUSTE
*
IMOD          = 0 ;
LCT0          = 'TABLE' ;
LEPST0        = 'TABLE' ;
'REPETER' BOUCM ZNMOD ;
IMOD          = IMOD '+' 1 ;
*
EPSI          = TEPS.IMOD ;
OMGI          = TABF.IMOD * Z2PI ;
COEFAJ        = AJUSTE  TABF.IMOD  EPSI TABENV ;
LCT0.IMOD     = COEFAJ.1 ;
LEPST0.IMOD   = COEFAJ.2 ;
*
TITRE 'C(T0) : F0 =' TABF.IMOD 'EPS0 =' EPSI ;
CT0           = 'EVOL' 'MANU' 'TEMPS' LTEV0 'C(T0)' LCT0.IMOD ;
DESSIN CT0 'CARR' 'MIMA' ;
*
TITRE 'C**2(T0) : F0 =' TABF.IMOD 'EPS0 =' EPSI ;
LCT02         = ( LCT0.IMOD ) * ( LCT0.IMOD ) ;
CT02          = 'EVOL' 'MANU' 'TEMPS' LTEV0 'C**2(T0)' LCT02 ;
DESSIN CT02 'CARR' 'MIMA' ;
*
TITRE 'EPS(T0) : F0 =' TABF.IMOD 'EPS0 =' EPSI ;
EPST0         = 'EVOL' 'MANU' 'TEMPS' LTEV0 'EPS(T0)' LEPST0.IMOD ;
DESSIN EPST0 'CARR' 'MIMA' ;
'MESS' ' ' ;
'MESS' 'MODE :' IMOD 'FREQ. :' TABF.IMOD 'AMORT. :' TEPS.IMOD ;
*
FIN BOUCM ;
*
*
***** CALCUL DES FONCTIONS DE TRANSFERT MODALES APPROCHEES *****
*
*
* DISCRETISATION DE L'AXE DES FREQUENCE
*
FMIN          = 0.0 ;
FMAX          = 100.0 ;
LFREQ         = DISCRFR TABF TEPS FMIN FMAX ;
NFREQ         = 'DIME' LFREQ ;
LFREQ2        = LFREQ * LFREQ ;
*
* INTERPOLATION DE LA DSP STATIONNAIRE
* SUR LE NOUVEAU AXE DES FREQUENCES
*
XDSPE         = 'EXTRAIRE' DSPE 'ABSC' ;
YDSPE         = 'EXTRAIRE' DSPE 'ORDO' ;
YDSPEC        = 'IPOL' LFREQ XDSPE YDSPE ;
DSPEC         = 'EVOL' 'MANU' 'FREQUENCE' LFREQ 'D.S.P.' YDSPEC ;
*
LZEROF        = 'PROG' NFREQ * 0. ;
EVINIT        = 'EVOL' 'MANU' LFREQ LZEROF ;
LUNF          = 'PROG' NFREQ * 1. ;
ECUN0         = 'EVOL' 'COMP' 'FREQUENCE' LFREQ LZEROF LZEROF ;
ECUN1         = 'EVOL' 'COMP' 'FREQUENCE' LFREQ LUNF LZEROF ;
*
ITEM          = 0 ;
TEM1          = 0.0 ;
LVARAPP       = 'PROG' ;
DSPAPP        = EVINIT ;
DSPAPP1       = EVINIT ;
RVARMAXA      = 0.0 ;
REPETER BOUC2 NTV1 ;
ITEM          = ITEM + 1 ;
*
TEM2          = 'EXTRAIRE' LTEV0 ITEM ;
DTEM          = ( TEM2 - TEM1 ) / ( 2.0 * ZT ) ;
*
IMOD          = 0 ;
HA            = ECUN0 ;
REPETER BOUC3 ZNMOD ;
IMOD          = IMOD + 1 ;
*
LEPSITEM      = LEPST0.IMOD ;
EPSITEM       = 'EXTRAIRE' LEPSITEM ITEM ;
LCITEM        = LCT0.IMOD ;
CITEM         = 'EXTRAIRE' LCITEM ITEM ;
*
FREQI         = TABF.IMOD ;
FREQI2        = FREQI * FREQI ;
LRE           = ( LUNF * FREQI2 ) - LFREQ2 ;
*
RIM           = 2.0 * EPSITEM * FREQI ;
LIM           = LFREQ * RIM ;
EVCOMP        = 'EVOL' 'COMP' 'REIM' 'FREQUENCE' LFREQ
                'REEL' LRE 'IMAGIN' LIM ;
HID           = ( ECUN1 / EVCOMP ) * CITEM ;
HIA           = HID * FREQI2 * ( -1.0 ) ;
HA            = HA + ( HIA * ( TX.IMOD ) ) ;
*
FIN BOUC3 ;
*
********* MODULE DE LA FONCTION DE TRANSFERT A L'INSTANT T0 **********
*
RIMP HA ;
MODHA         = 'EXTRAIRE' HA 'COUR' 1 ;
*
******** D.S.P. DE LA REPONSE DE LA STRUCTURE A L'INSTANT T0 *********
*
DSPAPP2       = MODHA * MODHA * DSPEC ;
*
******* VARTIANCE DE LA REPONSE DE LA STRUCTURE A L'INSTANT T0 *******
*
LSOM0         = 'INTG' DSPAPP2 ;
RVAR0         = 'EXTRAIRE' LSOM0 1 ;
LVARAPP       = LVARAPP 'ET' LSOM0 ;
*
* LA D.S.P. MOYENNE DE LA REPONSE DE LA STRUCTURE
*
DSPAPP        = DSPAPP '+' ( ( DSPAPP1 '+' DSPAPP2 ) '*' DTEM ) ;
*
* MAXIMUM DE LA VARIANCE
* D.S.P. A L'INSTANT OU LA VARIANCE EST MAXIMUM
*
LOGIC1        = ( RVAR0 > RVARMAXA ) ;
LOGIC2        = (( 'ABS' (( RVAR0 - RVARMAXA ) / RVAR0 )) < 1.0E-2 ) ;
SI ( OU LOGIC1 LOGIC2 ) ;
TMAX          = TEM2 ;
RVARMAXA      = RVAR0 ;
DSPMAX        = DSPAPP2 ;
FINSI ;
*
TEM1          = TEM2 ;
DSPAPP1       = DSPAPP2 ;
*
MENAGE ;
FIN BOUC2 ;
*
TITRE 'VARIANCE : NMODE =' ZNMOD ;
LVARAPP       = 'INSERER' LVARAPP 1 0. ;
VARAPP        = 'EVOL' 'MANU' 'TEMPS' LTEV 'VARIANCE' LVARAPP ;
DESSIN VARAPP 'CARR' 'MIMA' ;
*
*
TITRE 'D.S.P. : NMODE =' ZNMOD ;
LABSC         = 'EXTRAIRE' DSPAPP 'ABSC' ; LABSC = 'ENLEVER' LABSC 1 ;
LORDO         = 'EXTRAIRE' DSPAPP 'ORDO' ; LORDO = 'ENLEVER' LORDO 1 ;
DSPAPP0       = 'EVOL' 'MANU' 'FREQUENCE' LABSC 'D.S.P.' LORDO ;
DESSIN DSPAPP0 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
VARMOY        = 'EXTRAIRE' ( 'INTG' VARAPP ) 1 ;
VARMOY        = VARMOY '/' ZT ;
LENVPL        = ( LVARAPP '/' VARMOY ) '**' 0.50 ;
ENVPL         = 'EVOL' 'MANU' 'TEMPS' LTEV 'ENVE PL' LENVPL ;
TABENVPL      = 'TABLE' ;
TABENVPL.1    = LTEV ;
TABENVPL.2    = LENVPL ;
*
DSPPL         = DSPAPP ;
*
*=====================================================================
* CALCUL DU SPECTRE DE PLANCHER
* AMORTISSEMENT : ZTAB. PLANCH . AMORTPL
*=====================================================================
*
'MESS' ' ' ;
'MESS' '==============================================================';
'MESS' 'CALCUL DU SPECTRE DE PLANCHER ( TYPE : ' ZTYPSP ' )' ;
'MESS' '==============================================================';
'MESS' ' ' ;
'MESS' 'AMORTISSEMENT DU SPECTRE : ' EPSPL ;
'MESS' '------------------------' ;
*
* AXE DES FREQUENCES DU SPECTRE
*
'SI' ( 'EXIST' ( ZTAB. PLANCH ) LISFREQ ) ;
'MESS' 'L AXE FREQUENTIEL DU SPECTRE EST DONNE PAR L UTILISATEUR' ;
LFOSC         = ZTAB. PLANCH . LISFREQ ;
'SINON' ;
'MESS' ' ' ;
'MESS' 'DISCRETISATION FREQUENTIELLE PAR LA PROCEDURE "DISCRFR" :
[0.5HZ 30.0HZ]' ;
'MESS' '-------------------------------------------------------' ;
FMINPL        = 0.50 ;
FMAXPL        = 30.0 ;
LFOSC         = DISCRFR TABF TEPS FMINPL FMAXPL ;
'FINSI' ;
*
NOSC          = 'DIME' LFOSC ;
*
IOSC          = 0 ;
TABFPL        = TABF ;
TEPSPL        = TEPS ;
ZNMOD1        = ZNMOD + 1 ;
TEPSPL.ZNMOD1 = EPSPL ;
LNBREAPP      = 'PROG' ;
LDELTAPP      = 'PROG' ;
LSIGMAPP      = 'PROG' ;
LFPICAPP      = 'PROG' ;
'REPETER' BOUC6 NOSC ;
IOSC          = IOSC + 1 ;
*
FREQI         = 'EXTRAIRE' LFOSC IOSC ;
TABFPL.ZNMOD1 = FREQI ;
OMGI          = Z2PI * FREQI ;
*
'MESS' ' ' ;
'MESS' '*** OSCILLATEUR AU PLANCHER *** :' FREQI EPSPL ;
*
* CALCUL DES COEFFICIENTS D'AJUSTEMENT
* DE LA FONCTION DE TRANSFERT DE L'OSCILLATEUR AU PLANCHER
*
* APPEL DE LA PROCEDURE AJUSTE
*
COEFAJ        = AJUSTE  FREQI  EPSPL TABENVPL ;
LCI           = COEFAJ.1 ;
LEPSI         = COEFAJ.2 ;
*
* DISCRETISATION DU DOMAINE DES FREQUENCES POUR L'INTEGRATION
*
LFREQ         = DISCRFR TABFPL TEPSPL FMIN FMAX ;
NFREQ         = 'DIME' LFREQ ;
LFREQ2        = LFREQ '*' LFREQ ;
*
* INTERPOLATION DE DSPPL SUR LE NOUVEAU AXE FREQUENTIEL
*
XDSPPL        = 'EXTRAIRE' DSPPL 'ABSC' ;
YDSPPL        = 'EXTRAIRE' DSPPL 'ORDO' ;
YDSPPL1       = 'IPOL' LFREQ XDSPPL YDSPPL ;
DSPPL1        = 'EVOL' 'MANU' 'FREQUENCE' LFREQ 'DSPPL' YDSPPL1 ;
*
FREQI2        = FREQI '*' FREQI ;
LZEROF        = 'PROG' NFREQ '*' 0.0 ;
EVINIT        = 'EVOL' 'MANU' LFREQ LZEROF ;
LUNF          = 'PROG' NFREQ '*' 1.0 ;
LFREQI2       = LUNF '*' FREQI2 ;
LRE           = LFREQI2 '-' LFREQ2 ;
ECUN1         = 'EVOL' 'COMP' 'FREQUENCE' LFREQ LUNF LZEROF ;
*
ITEM          = 0 ;
TEM1          = 0.0 ;
LVAROSC       = 'PROG' ;
DSPAPP1       = EVINIT ;
RVARMAXA      = 0.0 ;
'REPETER' BOUC7 NTV1 ;
ITEM          = ITEM '+' 1 ;
*
TEM2          = 'EXTRAIRE' LTEV0 ITEM ;
DTEM          = ( TEM2 '-' TEM1 ) '/' ( 2.0 '*' ZT ) ;
CI            = 'EXTRAIRE' LCI ITEM ;
EPSI          = 'EXTRAIRE' LEPSI ITEM ;
*
RIM           = 2.0 '*' EPSI '*' FREQI ;
LIM           = LFREQ '*' RIM ;
EVCOMP        = 'EVOL' 'COMP' 'REIM' 'FREQUENCE' LFREQ
                'REEL' LRE 'IMAGIN' LIM ;
*
HDEPL         = ( ECUN1 / EVCOMP ) * ( CI / ( Z2PI * Z2PI ) ) ;
*
* MODULE DE LA FONCTION DE TRANSFERT A L'INSTANT T0
*
RIMP HDEPL ;
MODHDEPL      = 'EXTRAIRE' HDEPL 'COUR' 1 ;
*
******** D.S.P. DE LA REPONSE DE LA STRUCTURE A L'INSTANT T0 *********
*
DSPAPP2       = MODHDEPL * MODHDEPL * DSPPL1 ;
*
******* VARTIANCE DE LA REPONSE DE LA STRUCTURE A L'INSTANT T0 *******
*
LSOM0         = 'INTG' DSPAPP2 ;
RVAR0         = 'EXTRAIRE' LSOM0 1 ;
LVAROSC       = LVAROSC 'ET' LSOM0 ;
*
* LA D.S.P. MOYENNE DE LA REPONSE DE LA STRUCTURE
*
DSPOSC        = ( DSPAPP1 + DSPAPP2 ) * DTEM ;
*
* MAXIMUM DE LA VARIANCE
* D.S.P. A L'INSTANT OU LA VARIANCE EST MAXIMUM
*
LOGIC1        = ( RVAR0 > RVARMAXA ) ;
LOGIC2        = (( 'ABS' (( RVAR0 - RVARMAXA ) / RVAR0 )) < 1.0E-2 ) ;
SI ( OU LOGIC1 LOGIC2 ) ;
TMAX          = TEM2 ;
RVARMAXA      = RVAR0 ;
DSPMAX        = DSPAPP2 ;
FINSI ;
*
TEM1          = TEM2 ;
DSPAPP1       = DSPAPP2 ;
*
MENAGE ;
FIN BOUC7 ;
*
******************** APPEL DE LA PROCEDURE LIVO **********************
****** CALCUL DE N0, DELTA, TEMPS MODIFIE TMOD ET FACTEUR DE PIC *****
*
LVAROSC       = 'INSERER' LVAROSC 1 0. ;
VAROSC        = 'EVOL' 'MANU' 'TEMPS' LTEV 'VAR OSCI' LVAROSC ;
*
* APPEL DE LA PROCEDURE LIVO
*
TPIC          = LIVO VAROSC DSPOSC ;
*
LNBREAPP      = LNBREAPP 'ET' ( 'PROG' TPIC.1 ) ;
*
LDELTAPP      = LDELTAPP 'ET' ( 'PROG' TPIC.2 ) ;
*
LSIGMAPP      = LSIGMAPP 'ET' ( 'PROG' TPIC.3 ) ;
*
LFPICAPP      = LFPICAPP 'ET' ( 'PROG' TPIC.4 ) ;
*
FIN BOUC6 ;
*
TITRE 'SPECTRE N0 : NMODE =' ZNMOD 'AMORT. PL. =' EPSPL ;
ENBREAPP      = 'EVOL' 'MANU' 'FREQUENCE' LFOSC 'N0' LNBREAPP ;
DESSIN ENBREAPP 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
TITRE 'SPECTRE DELTA : NMODE =' ZNMOD 'AMORT. PL. =' EPSPL ;
EDELTAPP      = 'EVOL' 'MANU' 'FREQUENCE' LFOSC 'DELTA' LDELTAPP ;
DESSIN EDELTAPP 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
TITRE 'SPECTRE SIGMA : NMODE =' ZNMOD 'AMORT. PL. =' EPSPL ;
ESIGMAPP      = 'EVOL' 'MANU' 'FREQUENCE' LFOSC 'SIGMA' LSIGMAPP ;
DESSIN ESIGMAPP 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
TITRE 'SPECTRE FACTPIC : NMODE =' ZNMOD 'AMORT. PL. =' EPSPL ;
EFPICAPP      = 'EVOL' 'MANU' 'FREQUENCE' LFOSC 'FACTPIC' LFPICAPP ;
DESSIN EFPICAPP 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
LSPPLDEP      = LFPICAPP * LSIGMAPP ;
ESPPLDEP      = 'EVOL' 'MANU' 'FREQUENCE' LFOSC 'SPEC PL' LSPPLDEP ;
*
* TRANSFORMATION DU SRO DE PLANCHER CALCULE EN TYPE VOULU
*
TITRE 'SPECTRE PLANCHER : TYPE_SP =' ZTYPSP 'NMODE =' ZNMOD
                          'AMORT. PL. =' EPSPL ;
ZDEPL         = 'MOT' 'DEPL' ;
'SI' ( 'EGA' ZTYPSP ZDEPL ) ;
    ESPPLAPP      = ESPPLDEP ;
'SINON' ;
    ESPPLAPP  = 'CHSP' ESPPLDEP 'ENTR' ZDEPL 'SORT' ZTYPSP ;
'FINSI' ;
DESSIN ESPPLAPP 'LOGX' 'LOGY' 'CARR' 'MIMA' ;
*
VARP          = 'MOT' 'VARP' ;
DSPP          = 'MOT' 'DSPP' ;
NBR0          = 'MOT' 'NBR0' ;
DELT          = 'MOT' 'DELT' ;
SIGM          = 'MOT' 'SIGM' ;
FPIC          = 'MOT' 'FPIC' ;
SPPL          = 'MOT' 'SPPL' ;
ZTABAPP       = 'TABLE' ;
ZTABAPP. VARP = VARAPP   ;
ZTABAPP. DSPP = DSPAPP   ;
ZTABAPP. NBR0 = ENBREAPP ;
ZTABAPP. DELT = EDELTAPP ;
ZTABAPP. SIGM = ESIGMAPP ;
ZTABAPP. FPIC = EFPICAPP ;
ZTABAPP. SPPL = ESPPLAPP ;
'FINPROC' ZTABAPP ;
**********************************************************************


