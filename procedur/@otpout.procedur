$$$$ @OTPOUT
* @OTPOUT   PROCEDUR  AM1       92/10/29    21:15:34     734
*---------------------------------------------------------------
* OTTIMIZZAZIONE TRAVI  A SEZIONE RETTANGOLARE
* A PESO MINIMO , TENSIONE LIMITE IMPOSTA
*
* REVISIONE 21-10-92 AUTORI : CHIRICOZZI - DONARELLI - GIANNUZZI
* E N E A - VEL MEP TERMO
*---------------------------------------------------------------
DEBPROC @OTPOUT
        CRIT*FLOTTANT NMIT*ENTIER
        SEZY*FLOTTANT SEZZ*FLOTTANT  SLIM*FLOTTANT  STEC*FLOTTANT
        TMOD*TABLE    TMAT*TABLE
        FORZ*CHPOINT  VINCO*RIGIDITE TVECT*TABLE;
*---------------------------------------------------------------
* CRIT  ERRORE SU SIGMA LIMITE ---------------------------------
* NMIT  NUMERO MASSIMO ITERAZIONI ------------------------------
* SEZY  LARGHEZZA INIZIALE TRAVI -------------------------------
* SEZZ  ALTEZZA INIZIALE TRAVI ---------------------------------
* SLIM  SIGMA LIMITE -------------------------------------------
* STEC  DIMENSIONE MINIMA --------------------------------------
* TMOD  TAVOLA CONTENENTE GLI OGG. MODELE DELLE ZONE -----------
* TMAT  TAVOLA CONTENENTE GLI OGG. MATERIAU DELLE ZONE ---------
* FORZ  CAMPO DI FORZE -----------------------------------------
* VINCO RIGIDEZZA VINCOLI + ZONE NON OTTIMIZZATE ---------------
* TVECT TAVOLA CONTENENTE I VETTORI ORIETAZIONE DELLE ZONE -----
* --------------------------------------------------------------
TTRA = TABLE ;
TMAI = TABLE ;
TCAR = TABLE ;
TRIG = TABLE ;
TRHO = TABLE ;NZ= DIME TMOD ;
TCOLD= LECT NZ*0;
SCNEW= LECT NZ*0;
SAUTER 2  LIGNE ;
MESS ' ---------------------------------------------------- ' ;
MESS ' ---------------------------------------------------- ' ;
MESS '                                                      ' ;
MESS '         OTTIMIZZAZIONE TRAVI A PESO MINIMO           ' ;
MESS '                                                      ' ;
MESS ' ---------------------------------------------------- ' ;
MESS ' ---------------------------------------------------- ' ;
SAUTER 1 LIGNE;
MESS ' ---------------------------------------------------- ' ;
MESS ' OGNI QUANTE ITERAZIONI VUOI UNA STAMPA COMPLETA ?    ' ;
MESS ' ---------------------------------------------------- ' ;
OBTENIR FSTAMP*ENTIER ;
MESS ' ----------------------------------------------------------'  ;
MESS ' VALORE USUALE DEL COEFFICIENTE DI RILASSAMENTO  -1.-      '  ;
MESS ' CONFERMARE O CAMBIARE VALORE                              '  ;
MESS ' ----------------------------------------------------------'  ;
A=1. ;
OBTENIR A1*FLOTTANT ;
SI('NEG' A1 'NON' );
A=A1;
FINSI;
*
* MOMEMTI E SFORZI NORMALI SIGNIFICATIVI
ALF=0.001               ;
DMFS = @CTRAVE STEC STEC ;
SEZM = EXTR DMFS 3      ;
IM   = EXTR DMFS 4      ;
MFS  = SLIM * ALF * IM / STEC ;
FNS  = SLIM * ALF * SEZM ;
DETR DMFS                ;
*
* CARICAMENTO DIMENSIONI E MOMENTI INIZIALI DELLE ZONE
N=0                              ;
REPETER CTRA NZ                  ;
 N=N+1                           ;
 TMAI.N= EXTR TMOD.N 'MAIL'      ;
 TTRA.N=@CTRAVE SEZY SEZZ        ;
 TCAR.N=CARA TMOD.N SECT  (EXTR TTRA.N 3)
                    INRY  (EXTR TTRA.N 4)
                    INRZ  (EXTR TTRA.N 5)
                    TORS  (EXTR TTRA.N 6)
                    VECT   TVECT.N        ;
*CALCOLO PESO
 TRHO.N=EXCO  TMAT.N  RHO        ;
 PESZ  =INTG  TMOD.N TRHO.N TCAR.N;
 SI ( N EGA 1 )                  ;
   TEL = TMAI.N                  ;
   PESO=PESZ                     ;
 SINON                           ;
   PESO = PESO + PESZ            ;
   TEL = TEL ET ( TMAI.N )       ;
 FINSI                           ;
FIN CTRA                         ;
PPESO= PROG PESO                 ;
PITER= PROG 0.                   ;
REPE LL66 ;
 MESS 'FORNIRE PUNTO DI VISTA'    ;
 OBTENIR PVX*FLOTTANT PVY*FLOTTANT PVZ*FLOTTANT;
 PV=PVX PVY PVZ                   ;
 TRAC PV TEL                      ;
 MESS 'VUOI CAMBIARE IL PUNTO VISTA (SI-NO)' ;
 OBTENIER FF66*MOT;
 SI ( 'NEG' FF66 'SI' ); QUITTER LL66 ; FINSI;
FIN LL66;
*
* CICLO PRINCIPALE
*
NIT=0;
REPETER BOUC1 NMIT;
MERS=0. ;
NOCONV= 1;
NIT = NIT+1;
SAUTER 2  LIGNE ;
MESS ' =============================='  ;
MESS '    ITERAZIONE NUMERO ' NIT       ;
MESS ' =============================='  ;
SAUTER 1 LIGNE             ;
* RIGIDEZZA TOTALE
N=0;
REPETER LOP11 NZ ;
 N=N+1;
 SI((EXTR SCNEW N) EGA 0) ;
  SI( NIT > 1 ) ;
  DETR ( TRIG.N) ;
  FINSI          ;
  TRIG.N= RIGI TMOD.N (TMAT.N ET TCAR.N) ;
 FINSI;
 SI ( N EGA 1 ) ;
  RIGT = VINCO ET (TRIG.N ) ;
 SINON ;
 RIGT  = RIGT ET (TRIG.N)   ;
 FINSI ;
FIN LOP11 ;
*
*RISOLUZIONE ALL ITERAZIONE -NIT-
*
SPOT=RESO RIGT FORZ ;
*
N=0 ;
REPETER BOUC3 NZ;
N=N+1;
SPOZ   = REDU  SPOT  TMAI.N          ;
SFOZ   = SIGMA SPOZ  TMOD.N (TMAT.N ET TCAR.N) ;
DETR SPOZ ;
*
DY =EXTRAI TTRA.N 1;
DZ =EXTRAI TTRA.N 2;
SEZ=EXTRAI TTRA.N 3;
IY =EXTRAI TTRA.N 4;
IZ =EXTRAI TTRA.N 5;
*
* CALCOLO TENSIONI DOVUTE A: TZX SFORZO NORMALE
*                            TZY MOM. FLETTENTE INTORNO A Y
*                            TZZ MOM. FLETTENTE INTORNO A Z
KK1 = EXCO EFFX SFOZ      ;
CX  = ABS KK1             ; DETR KK1 ;
MCX = MAXI CX             ;
TZX = CX / SEZ ; DETR CX  ;
MTZX= MAXI TZX            ;
KK1 = EXCO MOMY SFOZ      ;
CMY = ABS KK1             ; DETR KK1 ;
MCMY= MAXI CMY            ;
TZY = CMY * (DZ / 2. / IY); DETR CMY ;
MTZY= MAXI TZY            ;
KK1 = EXCO MOMZ SFOZ      ;
CMZ = ABS KK1             ; DETR KK1 ;
MCMZ= MAXI CMZ            ;
TZZ = CMZ * (DY / 2. / IZ); DETR CMZ ;
MTZZ= MAXI TZZ            ;
DETR SFOZ                 ;
*
* RAPPORTO MOMENTI MASSIMI  E DIMENSIONI
SI ( MCMZ < MCMY )          ;
  SI ( MCMY < MFS  )        ;
    RM=1.                   ;
  SINON                     ;
    SI ( MCMZ < MFS )       ;
      RM = MFS / MCMY       ;
    SINON                   ;
      RM = MCMZ / MCMY      ;
    FINSI                   ;
  FINSI                     ;
SINON                       ;
  SI ( MCMZ < MFS )         ;
    RM=1.                   ;
  SINON                     ;
    SI ( MCMY < MFS )       ;
      RM = MFS / MCMZ       ;
    SINON                   ;
      RM = MCMY / MCMZ      ;
    FINSI                   ;
  FINSI                     ;
FINSI                       ;
RMZY=MCMZ / (MCMY + 1.E-6)  ;
SF  = (MCX + ( 6. * ( MCMY + MCMZ ) / STEC)) / SEZM;
RO  = 1. - ( MCX / SEZM  / SF )    ;
RD  = 1. - ( RO * ( 1. - RM ))     ;
SI ( MCMY < MCMZ )                 ;
 RD = 1. / RD                      ;
FINSI                              ;
*
* SIGMA TOTALE MASSIMA CONSERVATIVA
MSIGT= MTZX  + MTZY + MTZZ  ;
* SIGMA TOTALE MASSIMA REALE
KK1   = TZX   + TZY  + TZZ  ;
SIGTM= MAXI KK1 ; DETR KK1  ;
* RAPPORTO UNIFORMITA SFORZI
RUNIF = SIGTM / MSIGT       ;
*
* VERIFICA CONVERGENZA ZONA
* E
* AGGIORNAMENTO AREA E DIMENSIONI TRAVE
COEFA= (1.-A)+(A * SIGTM / SLIM )  ;
ERSZ= ABS ((SIGTM - SLIM ) /SLIM ) ;
SEZP= COEFA * SEZ   ;
SI (CRIT < ERSZ )          ;
  SI (SEZM < SEZP )        ;
    SEZN = SEZP            ;
    DYN = ( RD * SEZN) ** 0.5 ;
    DZN = ( SEZN / DYN )   ;
    SI ( DYN < STEC )      ;
      DYN= STEC            ;
      DZN= SEZN / STEC     ;
    FINSI                  ;
    SI ( DZN < STEC )      ;
      DZN= STEC            ;
      DYN= SEZN / STEC     ;
    FINSI                  ;
    REMPL SCNEW N 0        ;
    REMPL TCOLD N 0        ;
  SINON                    ;
*  LIMITE TECNOLOGICO
   SI (( EXTR TCOLD N) EGA  1 ) ;
   REMPL SCNEW N 1         ;
   SINON                   ;
   REMPL SCNEW N 0         ;
   FINSI                   ;
   REMPL TCOLD N 1         ;
   SEZN = SEZM             ;
   DYN  = STEC             ;
   DZN  = STEC             ;
  FINSI                    ;
SINON                      ;
* CONVERGENZA TENSIONI
    DYP = ( RD * SEZP) ** 0.5 ;
    DZP = ( SEZP/ DYP )       ;
  SI(( CRIT >EG (( DYP - DY ) / DY )) OU
    (( CRIT >EG (( DZP - DZ ) / DZ )) OU
    ((DYP < STEC ) OU ( DZP < STEC )))) ;
* NON VA RIPARTITA L AREA
    REMPL SCNEW N 1 ;
    REMPL TCOLD N 0 ;
    SEZN= SEZ  ;
    DYN = DY   ;
    DZN = DZ   ;
  SINON        ;
*   VA RIPARTITA L AREA
    REMPL SCNEW N 0 ;
    REMPL TCOLD N 0 ;
    SEZN= SEZP ;
    DYN = DYP  ;
    DZN = DZP  ;
  FINSI                       ;
FINSI                         ;
*
DETR ( TTRA.N)     ;
TTRA.N = @CTRAVE DYN DZN ;
DETR (TCAR.N )     ;
TCAR.N=CARA TMOD.N SECT  (EXTR TTRA.N 3)
                   INRY  (EXTR TTRA.N 4)
                   INRZ  (EXTR TTRA.N 5)
                   TORS  (EXTR TTRA.N 6)
                   VECT   TVECT.N        ;
*CALCOLO PESO
PESZ=INTG TMOD.N TRHO.N TCAR.N  ;
SI ( N EGA 1 )                  ;
  PESO=PESZ                     ;
SINON                           ;
  PESO = PESO + PESZ            ;
FINSI ;
SI(( MULT NIT FSTAMP   ) OU ( NIT EGA 1 ));
MESS '---------------'     ;
MESS '  ZONA  ' N          ;
MESS '---------------'     ;
MESS '-----------------------------------' ;
MESS 'PESO ZONA............' PESZ          ;
MESS '-----------------------------------' ;
MESS 'DIM. Y  ATTUALE......' DYN           ;
MESS 'DIM. Z  ATTUALE......' DZN           ;
MESS 'SEZIONE ATTUALE......' SEZN          ;
MESS '-----------------------------------' ;
MESS 'RAPPORTO DIMENSIONI..' RD            ;
MESS '-----------------------------------' ;
MESS 'DIM. Y  PRECEDENTE...' DY            ;
MESS 'DIM. Z  PRECEDENTE...' DZ            ;
MESS 'SEZIONE PRECEDENTE...' SEZ           ;
MESS '-----------------------------------' ;
MESS 'ERRORE SU ZONA.......' ERSZ          ;
MESS '-----------------------------------' ;
MESS 'SIGMA TOTALE MAX MAX.' MSIGT         ;
MESS 'SIGMA TOTALE MAX.....' SIGTM         ;
MESS 'RAPPORTO UNIFORMITA..' RUNIF         ;
MESS 'MOMENTO MAX DIR. Z...' MCMZ          ;
MESS 'MOMENTO MAX DIR. Y...' MCMY          ;
MESS 'RAPPORTO MOMENTI.....' RMZY          ;
MESS 'MOMENTO SIGNIFICATIVO' MFS           ;
MESS '------------------------------------------------';
SI(( EXTR SCNEW N) EGA 1 );
 MESS '    ==* SI HA CONVERGENZA  PER LA ZONA *==     ';
SINON ;
 MESS '  ==* NON SI HA CONVERGENZA  PER LA ZONA *==   ';
FINSI ;
MESS '------------------------------------------------';
FINSI ;
*
SI(( EXTR SCNEW N) EGA 0 );
 NOCONV=0 ;
FINSI ;
*
SI(( MERS < ERSZ ) ET (( EXTR TCOLD N ) EGA 0));
 MERS=ERSZ         ;
FINSI              ;
FIN BOUC3          ;
*
SAUTER 1  LIGNE                  ;
MESS ' ============================================='  ;
MESS ' PESO STRUTTURA................. ' PESO          ;
MESS ' ============================================='  ;
MESS ' ============================================='  ;
MESS ' ERRORE MASSIMO SU TUTTE LE ZONE ' MERS          ;
MESS ' ============================================='  ;
SI ( NOCONV EGA 1 ) ;
 MESS '     ==* CONVERGENZA SU TUTTE LE ZONE *==     ' ;
 MESS ' =============================================' ;
FINSI ;
* OUTPUT GRAFICI
NITP=  NIT + 1                   ;
PP11=PPESO ;
PPESO=INSE PP11  NITP PESO       ; DETR PP11 ;
PI11=PITER ;
PITER=INSE PI11  NITP ( FLOT NIT );DETR PI11 ;
TITRE ' ANDAMENTO DEL PESO  ALL ITERAZIONE ' NIT     ;
GPESO= EVOL MANU 'ITERAZIONE' PITER 'PESO' PPESO ;
DESS GPESO ; DETR GPESO   ;
SI (NIT EGA 1 )           ;
PERSG= PROG MERS MERS     ;
SINON                    ;
PE11=PERSG ;
PERSG=INSE PE11 NITP MERS ; DETR PE11;
FINSI                     ;
TITRE ' ANDAMENTO DELL ERRORE SULLE SIGMA ALL ITERAZIONE  ' NIT    ;
GERSG= EVOL MANU 'ITERAZIONE' PITER 'ERR.SIG' PERSG ;
DESS GERSG ; DETR GERSG   ;
TITRE 'ZONE ROSSE NON ANCORA IN CONVERGENZA ALL ITERAZIONE' NIT    ;
N=0                        ;
REPE LL77 NZ               ;
 N=N+1                     ;
 SI('EGA' (EXTR SCNEW N) 1);
  TMAIN= COUL TMAI.N BLAN  ;
 SINON                     ;
  TMAIN= COUL TMAI.N ROUG  ;
 FINSI                     ;
 SI('EGA' N 1 )            ;
  TEL1= TMAIN              ;
 SINON                     ;
  TEL1=TEL1 ET TMAIN       ;
 FINSI                     ;
FIN LL77                   ;
TRAC PV TEL1               ;
DETR TEL1                  ;
*
SI ( NOCONV EGA 1 ) ;
 QUITTER BOUC1      ;
FINSI ;
MESS ' ------------------------------------------------';
MESS ' VUOI CONTINUARE LE ITERAZIONI O USCIRE ? (SI/NO)';
MESS ' ------------------------------------------------';
OBTENIR SSNN*MOT;
SI('EGA'SSNN 'NO');
 QUITTER BOUC1;
FINSI;
DETR SPOT ;
MESS ' ----------------------------------------------------------'  ;
MESS ' FORNIRE  IL NUOVO VALORE COEFFICIENTE DI RILASSAMENTO     '  ;
MESS ' COEFFICIENTE ATTUALE ' A                                     ;
MESS ' ----------------------------------------------------------'  ;
OBTENIR A1*FLOTTANT ;
SI('NEG' A1 'NON')  ;
A=A1;
FINSI;
FIN BOUC1;
N=0;
REPETER  LOP22 NZ ;
 N=N+1;
DY =EXTRAI (TTRA.N) 1;
DZ =EXTRAI (TTRA.N) 2;
SEZ=EXTRAI (TTRA.N) 3;
MESS '--------------------------------------------------------------';
MESS ' ZONA NUMERO ' N ;
MESS '--------------------------------------------------------------';
MESS 'DY =' DY ' DZ =' DZ ' SEZ=' SEZ  ;
FIN LOP22 ;
MESS '--------------------------------------------------------------';
FINPROC TTRA SPOT;
