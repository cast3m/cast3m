$$$$ MAYOTO
* MAYOTO    PROCEDUR  JC220346  12/09/12    21:15:07     7501
DEBP MAYOTO epp*mot tab*table;
opti dime 2 elem qua8;
si (ega epp ct);
* recuperation des donnees de la table
a= tab . a;
b= tab . b;
k= tab . k;
app= tab . app;


* demi-hauteur de l'eprouvette
h= 0.6 * b;
* distance verticale entre la fissure et l'eprouvette
h1= 0.275 * b;
* diametre des trous
Di= 0.25 * b;
* largeur du cote droit de l'eprouvette
c= 0.25 * b;
* epaisseur de l'eprouvette
ep= b / 2;

* geometrie du rectangle d'approche: demi-cote
v= b - a;
r= mini (prog (b / 75.E-1) v);


* geometrie du pave de quadrangle: demi-cote
* rayon de la couronne
p1= r / 2;

* nbre d'elts sur 1 cote (rectangle ou pave)
nr= 5 * k;

* taille des elts sur le cote du rectangle
tmr= r / nr;

*coef d'elim

coelim= tmr / 100;

* taille des elts dans le pave
tmp= p1 / nr;

* densite dans le pave du quadrangle
dens (3 * tmr);
dci= tmr;


* pt origine
p0= 0. 0.;

* pointe de fissure
pfis= a 0.;

* contour exterieur de la demi-eprouvette
pbd= b 0.;
phd= b h;
phg= (0. - c) h;
pbg= (0. - c) 0.;

* geometrie du trou
r1= di / 2;
ce= 0. h1;
c1= r1 h1;
c1p= (r1 * (cos 45)) (h1 + (r1 * (sin 45)));
c2= 0. (h1 + r1);
c2p= (0. - (r1 * (cos 45))) (h1 + (r1 * (sin 45)));
c3= (0. - r1) h1;
c4= 0. (h1 - r1);
tr11= cerc c1 ce c1p;
tr12= cerc c1p ce c2;
tr1= tr11 et tr12;
tr21= cerc c2 ce c2p;
tr22= cerc c2p ce c3;
tr2= tr21 et tr22;
tr3= cerc c3 ce c4;
tr4= cerc c4 ce c1;
trchar= tr1 et tr2 et tr3 et tr4;

* modelisation de la goupille
cgoup= tr12 et tr21;
goup= cout cgoup ce;
elim coelim goup trchar;

* geometrie du rectangle d'approche
prbd= pfis plus (r 0.);
prbg= pfis moins (r 0.);
pmh= pfis plus (0. r);
prhd= pfis plus (r r);
prhg= pfis moins (r (0. - r));
recd= prhd d nr prbd;
rech1= prhg d nr pmh;
rech2= pmh d nr prhd;
rech= rech1 et rech2;
recg= prbg d nr prhg;
conr= recd et recg et rech;


* densites du contour externe
dci= tmr;

pden=prhd moins pfis;
lden=(((coor 1 pden)**2)+((coor 2 pden)**2))**0.5;
coeden=dci / lden;

pden=phd moins pfis;
lden=(((coor 1 pden)**2)+((coor 2 pden)**2))**0.5;
denphd=coeden * lden;

pden=phg moins pfis;
lden=(((coor 1 pden)**2)+((coor 2 pden)**2))**0.5;
denphg=coeden * lden;

pden=pbg moins pfis;
lden=(((coor 1 pden)**2)+((coor 2 pden)**2))**0.5;
denpbg=coeden * lden;

pden=pbd moins pfis;
lden=(((coor 1 pden)**2)+((coor 2 pden)**2))**0.5;
denpbd=coeden * lden;

jj=prbg d pbg dini dci dfin denpbg;
si (neg r v);
  dt4= prbd d pbd dini dci dfin denpbd;
finsi;


*maillage d'approche
si (ega app 0);
*               maillage du pave de quadrangle
                ppbd= pfis plus (p1 0.);
                ppbd= pfis plus (p1 0.);
                ppbg= pfis moins (p1 0.);
                bapd= d nr pfis ppbd;
                bapg= pfis droit nr ppbg;
                basp= ordo (bapd et bapg) ;
                pave= basp tran nr (0. p1);
                copav2= pave cote 2;
                copav3= pave cote 3;
                copav4= pave cote 4;
*               maillage de la zone intermediaire pave-rectangle
                su3= copav3 regler nr rech;
                su4= copav4 regler nr recd;
                su2= copav2 regler nr recg;
                su= su2 et su3 et su4;
                apro= pave et su;
                elim coelim (su4 cote 4) (su3 cote 2);
                elim coelim (su2 cote 2) (su3 cote 4);
                fis= jj et bapg et (su2 cote 4);
                si (neg r v);
                        nfis= dt4 et bapd et (su4 cote 2);
                sinon;
                        nfis= bapd et (su4 cote 2);
                finsi;
*       maillage rayonnant
        sinon;
*               construction de la couronne
                poco= pfis plus (tmp 0.);
                pocf= pfis plus (p1 0.);
                genco= poco d (nr - 1) pocf;
                su21= genco rota nr 45. pfis;
                l21= su21 cote 4;
                c21= su21 cote 2;
                su22= (su21 cote 3) rota nr 45. pfis;
                l22= su22 cote 2;
                c22= su22 cote 4;
                su23= (su22 cote 3) rota nr 45. pfis;
                l23= su23 cote 4;
                c23= su23 cote 2;
                su24= (su23 cote 3) rota nr 45. pfis;
                l24= su24 cote 2;
                c24= su24 cote 4;
                cour= su21 et su22 et su23 et su24;
                lint1= l21 et l22;
                lext1= c21 et c22;
*       maillage de l'interieur de la couronne
                diap1= cout pfis lint1;
                lint2= l23 et l24;
                lext2= c23 et c24;
                diap2= cout pfis lint2;
                elim coelim diap1 diap2;
*       maillage de la partie intermediaire couronne-rectangle
                diad= pocf d nr prbd;
                diadh= prhd d nr (c21 poin final);
                diam= (c22 poin initial) d nr pmh;
                diagh= (c23 poin final) d nr prhg;
                diag= (c24 poin initial) d nr prbg;
                recd= inve recd;
                su1= dall diad recd diadh c21 plan;
                diam= inve diam;
                su2= dall c22 diam rech2 diadh plan;
                su3= dall c23 diagh rech1 diam plan;
                su4= dall c24 diag recg diagh plan;
                su= su1 et su2 et su3 et su4;
                elim coelim su2 su3;
                diap= diap1 et diap2;
                apro= su et cour et diap;
                c2pf= c24 poin initial;
                bapg= pfis d nr c2pf;
                fis= jj et (su24 cote 3) et (su4 cote 2) et bapg;
                elim coelim bapg (diap et su24);
                ll= pfis d nr (c21 poin initial);
                elim coelim ll (diap1 et su21);
                si (neg r v);
                        nfis= dt4 et (su1 cote 1) et genco et ll;
                sinon;
                        nfis= ll et genco et (su1 cote 1);
                finsi;
        finsi;

*maillage du contour exterieur de l'eprouvette
si (neg r v);
  context=recg et rech et recd et dt4;
  context=context et (d pbd phd dini denpbd dfin denphd);
  context=context et (d phd phg dini denphd dfin denphg);
  context=context et (d phg pbg dini denphg dfin denpbg);
  context=context et (d pbg prbg dini denpbg dfin dci);
sinon;
  context=recg et rech;
  context=context et (d prhd phd dini dci dfin denphd);
  context=context et (d phd phg dini denphd dfin denphg);
  context=context et (d phg pbg dini denphg dfin denpbg);
  context=context et (d pbg prbg dini denpbg dfin dci);
finsi;
trchar=inve trchar;

plaq1=context et trchar;
ss= surf plaq1 plan;
supe=apro et ss;


* obtention de la partie inferieur par symetrie

fis2= fis syme droit pbg pbd;
ce2= ce syme droit pbg pbd;
goup2= goup syme droit pbg pbd;
goup= goup et goup2;
infe= supe syme droi pbg pbd;
elim coelim fis2 infe;
elim coelim fis supe;
fis= fis et fis2;
epro= supe et infe;
elim coelim goup epro;
elim coelim (goup2 et ce2);
si (neg r v);
        elim coelim dt4 infe;
finsi;
elim coelim su2 su3;
si (ega app 1);
        elim coelim diap1 infe;
        elim coelim diad infe;
        elim coelim genco infe;
sinon;
        elim coelim (su4 cote 2) infe;
        elim coelim bapd infe;
finsi;

res= table;
res . e= epro;
res . p= pfis;
res . f= fis;
res . n= nfis;
res . i= ce;
res . m= ce2;
res . g= goup;

finsi;

si (ega epp ccp);
l= tab . l;
lf= tab . lf;
app= tab . app;

* calcul de la demi-hauteur de l'eprouvette
h= 2 * l;

* largeur de bande deposee (pour un letal bi-phase)
ldep= L / 4;

* nombre de decoupes sur 45 degres
n= 5;

* taille de maille maximum en pointe de fissure
*tmax= 10;

* geometrie de l'eprouvette: parametres intermediaires

* longueur de l'eprouvette non fissuree
ln= l - lf;

* demi-cote du rectangle d'approche
c= mini (prog (L / 5) ln lf);

* rayon du disque
r= c / 2;

* densite en peripherie du disque
te= 1.5 * ((pi * r) / (4. * n));

* taille des elts en pointe de fissure
tm= (( r / 2.) / n) - (((r / 2.) / n) / 10.);

* densite au premier arc
si (ega app 0);
        ta= tm * 2;
sinon;
       ta= tm;
finsi;

* coefs d'elim
coelimte= te / 100;
coelimtm= tm / 100;
coelimta= ta / 100;

dens ta;
p0= 0. 0.;
pfis= lf 0.;
si (ega app 1);
        p2= pfis plus (tm 0.);
sinon;
        p2= pfis plus ((tm * n * 2.) 0.);
finsi;
dens te;
p10= pfis plus (r 0.);
ligam0= p2 d p10;
su21= ligam0 rota n 45. pfis;
l21= su21 cote 4;
c21= su21 cote 2;
su22= (su21 cote 3) rota n 45. pfis;
l22= su22 cote 2;
c22= su22 cote 4;
su23= (su22 cote 3) rota n 45. pfis;
l23= su23 cote 4;
c23= su23 cote 2;
su24= (su23 cote 3) rota n 45. pfis;
l24= su24 cote 2;
c24= su24 cote 4;

rac1= l21 et l22;
rac2= l23 et l24;

rac= rac1 et rac2;
fis2= su24 cote 3;
su2= su21 et su22 et su23 et su24;
******************************************************
si (ega app 1);
        su0p= cout pfis rac1;
        su0d= cout pfis rac2;
        elim coelimte su0p su0d;
        su0= su0p et su0d;
        su01= chan su0 lign;
        fis1= su01 elem 4;
        p01= (pfis plus p2) / 2;
        lig0= su01 elem appuye strictement (pfis et (su0 poin proc
        p01) et p2);
        ligam0= ligam0 et lig0;
        l1= ligam0;
sinon;
        dens tm;
        qad= pfis plus ((tm * n) 0.);
        qag= pfis moin ((tm * n) 0.);
        basd= pfis d n qad;
        basg= pfis d n qag;
        bas= basg et basd;
        ordo bas;
        pave= bas tran n (0. (tm * n));
        copav1= pave cote 2;
        copav2= pave cote 3;
        copav3= pave cote 4;
        copav= copav1 et copav2 et copav3;

        joncd= qad d p2;
        joncg= qag d (su24 poin proch qag);
        sujon= dall copav joncd rac joncg plan;
        su0= sujon et pave;
        lig0= basd et joncd;
        ligam0= ligam0 et lig0;
        l1= ligam0;
        fis1= basg et joncg;
finsi;

dens (2. * te);
pdb= pfis plus (c 0.);
pdh= pfis plus (c c);
pmh= pfis plus  (0. c);
pgh= pfis plus (((-1) * c) c);
pgb= pfis plus (((-1) * c) 0.);

card= pdb d n pdh;
carhd= pdh d n pmh;
carhg= pmh d n pgh;
carg= pgh d n pgb;

decc= ((-1) * (enti ((c - r) / (1. * te)))) - 1;
diad= (c21 point initial) d decc pdb ;
diadh= (c22 poin final) d decc pdh;
diam= (c23 poin initial) d decc pmh;
diagh= (c24 poin final) d decc pgh;
diag= (c24 poin initial) d decc pgb;
su3= dall c21 diadh card diad plan;
su4= (dall c22 diam carhd diadh plan) et
     (dall c23 diagh carhg diam plan);
su5= dall c24 diag carg diagh plan;

carre= su0 et su2 et su3 et su4 et su5;
ligam1= ligam0 et diad;

* creation du maillage de la partie metal depose

clcritw= (c < (ln + coelimte)) et (c > (ln - coelimte));
clcrita= (c < (lf + coelimte)) et (c > (lf - coelimte));
cldep= (c < (ldep + coelimte)) et (c > (ldep - coelimte));

si ((non clcritw) et (non clcrita));
        vtl= 0. (ldep - c);
        vtg= (((-1) * (lf - c)) 0.);
        vtd= (ln - c) 0.;
        nfl= enti ((ldep - c) / (mesu (carhd elem 1) long));
        nfg= enti ((lf - c) / (mesu (carg elem 1) long));
        nfd= enti ((ln - c) / (mesu (card elem 1) long));
        si (ega nfg 0);
                nfg= 1;
                su7= carg tran nfg vtg;
        sinon;
                densi= (mesu (carg elem 1) long);
                densf= densi * (1 + (nfg / 20.));
                su7= carg tran dini densi dfin densf vtg;
        finsi;

        si (ega nfd 0);
                nfd= 1;
                su6= card tran nfd vtd;
        sinon;
                densi= (mesu (card elem 1) long);
                densf= densi * (1 + (nfd / 20.));
                su6= card tran dini densi dfin densf vtd;
        finsi;

        l8= (su7 cote 4) et (su6 cote 2) et carhd et carhg;
        l8 = ordo l8;

        densi= (mesu (carhd elem 1) long);
        densf= densi * (1 +(nfl / 10.));
        su8= l8 tran dini densi dfin densf vtl;

        mdep= carre et su6 et su7 et su8;
        l8b= su8 cote 3;
        ligam= ligam1 et (su6 cote 4);
        lsym1= (su7 cote 3) et (su8 cote 4);

finsi;

si (clcritw);
        mess 'Cas critique W';
        vtl= 0. (ldep - c);
        vtt= (((-1) * (lf - c)) 0.);
        nfl= enti ((ldep - c) / (mesu (carhd elem 1) long));
        nft= enti ((lf - c) / (mesu (carg elem 1) long));
        densi= (mesu (carg elem 1) long);
        densf= densi * (1 + (nft / 20.));
        su7= carg tran dini densi dfin densf vtt;
        l8= (su7 cote 4) et carhd et carhg;
        l8 = ordo l8;
        densi= (mesu(carhd elem 1) long);
        densf= densi * (1 + (nfl / 10.));
        su8= l8 tran dini densi dfin densf vtl;
        mdep= carre et su7 et su8;
        l8b= su8 cote 3;
        ligam= ligam1;
        lsym1= (su7 cote 3) et (su8 cote 4);
finsi;

si (clcrita);
        mess 'Cas critique A';
        vtl= 0. (ldep - c);
        vtt= (ln - c) 0.;
        nfl= enti ((ldep - c) / (mesu (carhd elem 1) long));
        nft= enti ((ln - c) / (mesu (card elem 1) long));
        densi= (mesu (card elem 1) long);
        densf= densi * (1 + (nft / 20.));
        su6= card tran dini densi dfin densf vtt;
        l8= (su6 cote 2) et carhd et carhg;
        l8 = ordo l8;
        densi= (mesu (carhd elem 1) long);
        densf= densi * (1 + (nfl / 10.));
        su8= l8 tran dini densi dfin densf vtl;
        mdep= carre et su6 et su8;
        l8b= su8 cote 3;
        l8b= inve l8b;
        ligam= ligam1 et (su6 cote 4);
        lsym1= (su8 cote 2) et carg;
finsi;

si (clcrita);
        fis= fis1 et fis2 et diag;
sinon;
        fis= fis1 et fis2 et diag et (su7 cote 2);
finsi;

si (clcrita);
        cd1= (su6 cote 2) et (su8 cote 2);
finsi;

si (clcritw);
        cd1= card et (su8 cote 2);
finsi;

si ((non clcrita) et (non clcritw));
        cd1= (su6 cote 3) et (su8 cote 2);
finsi;

* construction de la partie metal de base

l8b = ordo l8b;
pii= l8b poin initial;
pf= l8b poin final;
lu= (mesu l8b long) / (nbel l8b) *1.2;
fron1= (pii plus (0. lu)) d (enti ((nbel l8b) / 1.5))
       (pf plus (0. lu));
lub= ((mesu fron1 long) / (nbel fron1)) * 1.2;
pib= fron1 poin initial;
pfb= fron1 poin final;
fron2= (pib plus (0. lub)) d (enti ((nbel fron1) / 1.5))
       (pfb plus (0. lub));

join1= cout l8b fron1;
join2= cout fron1 fron2;
join= join1 et join2;
joinl= chan join1 ligne;
joinp= joinl poin droit p0 (p0 plus (0. 10.)) (tm / 10.);
lsym2= joinl elem appuye strictement joinp;

lrest= H - (ldep + lu + lub);
vt= 0. lrest;
le= mesu (fron2 elem 1) long;
corp= fron2 tran (enti (lrest / le)) vt;
lsym3= corp cote 2;
cd2= join1 cote 4;
cd3= join2 cote 4;
cd4= corp cote 4;
cd= cd1 et cd2 et cd3 et cd4;

mbas= corp et join;

* plan vertical de symetrie
lsym= lsym1 et lsym2 et lsym3;
lsym= lsym coul vert;
psm= p0;

* recuperation du cote haut pour definir face de chargement;
schar= corp cote 3;
schar= schar coul rouge;

* ligament
ligam= ligam coul bleu;
pel= ligam poin proch (p0 plus (l 0.));

* recuperation du maillage complet de la demi-eprouvette
epro= mbas et mdep;

* recuperation des resultats dans la table res
res= table;
res . e= epro;
res . s= schar;
res . p= pfis;
res . f= fis;
res . o= cd;
res . m= ligam;
res . v= lsym;
finsi;

si (ega epp senb);
*recuperation des elts de la table
s= tab.s;
b= tab.b;
a= tab.a;
app= tab.app;
k= tab.k;

* geometrie de l'eprouvette: calcul du demi cote du pave
c=s/2;

* geometrie de l'eprouvette: points definissant le contour
p0=0. 0.;
pfis=0. a;
pdh= c b;

* geometrie de l'eprouvette: demi cote du rectangle d'approche
t= b - a;
r= mini (prog c (b/5) a t);

* calcul du nbre d'elts sur 1 cote
nr=5 * k;

* taille des elts construits
tmr= r /nr;
coelim= tmr / 100;
* geometrie du rectangle : points definissant le contour
prhg=pfis plus (0. r);
prbg=pfis moin (0. r);
prhd=prhg plus (r 0.);
prbd=prbg plus (r 0.);
prmd=pfis plus (r 0.);

* geometrie du rectangle : construction des cotes
rech= prhg d nr prhd;
recd1= prhd d nr prmd;
recd2= prmd d nr prbd;
recb= prbd d nr prbg;
recd= ordo (recd1 et recd2);

* maillage des parties en dessous et/ou au dessus du rectangle
si (r neg t);
* maillage de la partie situee au dessus du rectangle
        dur= rech tran dini tmr dfin (10 * tmr) (0. (0. + (t - r)));
finsi;

si (r neg a);
* maillage de la partie situee au dessous du rectangle
        dere= recb tran dini tmr dfin (10 * tmr) (0. (0. - (a - r)));
finsi;

* maillage de la partie droite de l'eprouvette
cot=recd;

* si la partie située au dessus du rectangle a été construite *
si (r neg t);
        cot=(dur cote 2) et cot;
finsi;

* si la partie située en dessous du rectangle a été construite *
si (r neg a);
        cot=cot et (dere cote 4);
finsi;


cot = ordo cot;

* si les dimensions imposent une partie à droite
si (r neg c);
        sudr= cot tran dini tmr dfin (10 * tmr) ((c - r) 0.);
finsi;


* maillage quadrillé: géométrie du pave du quadrangle:calcul du demi-cote
* maillage rayonnant: géométrie de la couronne: calcul du rayon exterieur
q=r / 2;

* calcul du nbre d'elts sur 1 cote du pave du quadrangle
tmp= q / nr;

* approche avec pave de quadrangle
si (app ega 0);
*       géométrie du pave: points définissants le contour
        ppbg= pfis moins (0. q);
        pphg= pfis plus (0. q);
*       géométrie du pave: construction
        bphg= pfis d nr pphg;
        bpbg= pfis d nr ppbg;

        pave1= bphg tran nr (q 0.);
        pave2= bpbg tran nr (q 0.);
        elim pave1 pave2 coelim;
        pave= pave1 et pave2;
        coph= pave1 cote 2;
        copd1= pave1 cote 3;
        copd2= pave2 cote 3;
        copb= pave2 cote 2;
*       maillage de la partie intermediaire entre pave et rectangle
        spr1= coph regler dini tmp dfin tmr rech;
        spr2= copd1 regler dini tmp dfin tmr recd1;
        copd2= inve copd2;
        spr22= copd2 regler dini tmp dfin tmr recd2;
        copb= inve copb;
        spr3= copb regler dini tmp dfin tmr recb;
        spr= spr1 et spr2 et spr3 et spr22;
        elim spr1 spr2 coelim;
        elim spr2 spr22 coelim;
        elim spr22 spr3 coelim;

*       recuperation du maillage complet de la demi-eprouvette
        epro= spr et pave;
        si (neg r c);
                epro= epro et sudr;
        finsi;
        si (neg r a);
                epro=epro et dere;
        finsi;
        si (neg r t);
                epro= epro et dur;
        finsi;

*       recuperation de la fissure
        fis= bpbg et (spr3 cote 2);
        si (neg r a);
                fis= fis et (dere cote 2);
        finsi;
*       recuperation de la partie non fissuree
        nfis= bphg et (spr1 cote 4);
        si (neg r t);
                nfis= nfis et (dur cote 4);
        finsi;

sinon;
* approche rayonnante
*       construction de la couronne
        pcbg= pfis moins (0. q);
        gene= (pfis plus (0. (0. - tmp))) d (nr - 1) pcbg;
        suc1= gene rota nr 45. pfis;
        lint1= suc1 cote 4;
        lext1= suc1 cote 2;
        suc2=(suc1 cote 3) rota nr 45. pfis;
        lint2= suc2 cote 2;
        lext2= suc2 cote 4;
        suc3= (suc2 cote 3) rota nr 45. pfis;
        lint3= suc3 cote 4;
        lext3= suc3 cote 2;
        suc4= (suc3 cote 3) rota nr 45. pfis;
        lint4= suc4 cote 2;
        lext4= suc4 cote 4;
        suc= suc1 et suc2 et suc3 et suc4;
        lint2= inve lint2;
        linta= lint1 et lint2;
        lint1 = ordo lint1;
        lintb= lint3 et lint4;
        lextb= lext1 et lext2 et lext3 et lext4;

* maillage de l'interieur de la couronne
        sud1= cout pfis linta;
        sud2= cout pfis lintb;
        elim sud1 sud2 coelim;
        suc= suc et sud1 et sud2;

* maillage de la partie située entre la couronne et le rectangle
        recb= inve recb;
        su1= lext1 regler dini tmp dfin tmr recb;
        su2= lext2 regler dini tmp dfin tmr recd2;
        recd1= inve recd1;
        su3= lext3 regler dini tmp dfin tmr recd1;
        su4= lext4 regler dini tmp dfin tmr rech;
        su= su1 et su2 et su3 et su4;
        elim su1 su2 coelim;
        elim su2 su3 coelim;
        elim su3 su4 coelim;

* recuperation du maillage complet de la demi-eprouvette
        epro= su et suc;
        si (neg r c);
                epro= epro et sudr;
        finsi;
        si (neg r a);
                epro= epro et dere;
        finsi;
        si (neg R t);
                epro= epro et dur;
        finsi;

* recuperation de la fissure
        pf= lint1 point final;
        hj= sud1 elem contenant pf;
        hj= hj chan lign;
        gh=hj elem 3;
        fis= gene et (su1 cote 4) et gh;
        si (neg r a);
                fis=fis et (dere cote 2);
        finsi;
        kl= sud2 elem contenat (lint4 point final);
        kl=kl chan lign;
        kl=kl elem elem 3;
        nfis= (su4 cote 4) et (suc4 cote 3) et kl;
        si (neg r t);
                nfis= nfis et (dur cote 4);
        finsi;
finsi;
* rangement des parametres de sortie
pmh= epro poin proch (0. b);
pdb= epro poin proch (c 0.);
res= table;
res . e= epro;
res . p= pfis;
res . f= fis;
res . n= nfis;
res . i= pmh;
res . u= pdb;
finsi;
FINP res;



