$$$$ BRUCHE
* BRUCHE    PROCEDUR  MAGN      17/12/12    21:15:04     9663
'DEBPROC' bruche KAS*'MOT' nab*'ENTIER' rxt*'TABLE';

 GEO = rxt.'GEO'                                        ;
Si(nab <EG 3)                                           ;
 nub = nab                                              ;
 rxtsce  =rxt.'scenario'                                ;
 Briches =rxt.'TIC'                                     ;
 CTinj   =chai 'Tinj'                                   ;
 CKsi    =chai 'Ksi'                                    ;
 CQlj    =chai 'Qlj'                                    ;
 CHlj    =chai 'Hlj'                                    ;
                                                        ;
 Si (nub > 1)                                           ;
 rxtsce=rxt.(chai 'scenario' nub)                       ;
 CTinj    = chai CTinj nub                              ;
 CKsi     = chai CKsi nub                               ;
 CQlj     = chai CQlj nub                               ;
 CHlj     = chai CHlj nub                               ;
 Finsi                                                  ;
Sinon                                                   ;
 nub = nab - 3                                          ;
 ibr=index (rxt.'Breches')                              ;
 Briches =rxt.'Breches'.(ibr.nub)                       ;
 rxtsce  =Briches.'scenario'                            ;
 GEO     =Briches                                       ;
 CTinj   =chai 'Tinj'                                   ;
 CKsi    =chai 'Ksi'                                    ;
 CQlj    =chai 'Qlj'                                    ;
 CHlj    =chai 'Hlj'                                    ;
Finsi                                                   ;

* Modifications ANTS du 06/01/2009
* Evite d'avoir un debit d'injection nul

'SI' ('NON' ('EXIS' rxtsce Qmodif))                     ;
'MESS' '=============================================================' ;
'MESS' '=                       WARNING !!                          =' ;
'MESS' '=    LES DEBITS D INJECTION NULS ONT ETE IMPOSES A 1E-10    =' ;
'MESS' '=============================================================' ;
  lstqxxx = 'MOTS' 'qeau' 'qliq' 'qair' 'qhe'
                   'qh2'  'qo2'  'qn2'  'qco' 'qco2'    ;
  lmots1 = 'MOTS'                                       ;
  'REPE' bqxxx ('DIME' lstqxxx)                         ;
    lmots = 'EXTR' lstqxxx &bqxxx                       ;
    'SI' ('EXIS' rxtsce lmots)                          ;
      lmots1 = lmots1 'ET' ('MOTS' lmots)               ;
    'FINSI'                                             ;
  'FIN' bqxxx                                           ;
  mots1 = 'EXTR' lmots1 1                               ;
  'REPE' bvxxx ('DIME' rxtsce.mots1)                    ;
    indic = FAUX                                        ;
    'REPETER' bq1xxx ('DIME' lmots1)                    ;
      motsxx = 'EXTR' lmots1 &bq1xxx                    ;
      valxxx = 'EXTR' rxtsce.motsxx &bvxxx              ;
      'SI' ('>' valxxx 1.E-10)                          ;
        indic = VRAI                                    ;
        'QUIT' bq1xxx                                   ;
      'FINSI'                                           ;
    'FIN' bq1xxx                                        ;
    'SI' ('NON' indic)                                  ;
      'REMP' rxtsce.mots1 &bvxxx 1E-10                  ;
    'FINSI'                                             ;
  'FIN' bvxxx                                           ;
  rxtsce.Qmodif = VRAI                                  ;
'FINSI' ;
* Fin modifications ANTS du 06/01/2009


************************************************************************
********* DEBUT KAS1 ***************************************************
************************************************************************

Si (EGA KAS 'KAS1');

* Initialisation des debits
*? Mliqbr=0. ;
 Ksi   =1. ;
 qliq  =0. ; hliq  =0. ; Mliqj = 0.0 ;
 qeau  =0. ; hvap  =0. ; Yvapj = 0.0 ;
 qair  =0. ; hair  =0. ; Yairj = 0.0 ;
 qhe   =0. ; hhe   =0. ; Yhej  = 0.0 ;
 qh2   =0. ; hh2   =0. ; Yh2j  = 0.0 ;
 qo2   =0. ; ho2   =0. ; Yo2j  = 0.0 ;
 qn2   =0. ; hn2   =0. ; Yn2j  = 0.0 ;
 qco   =0. ; hco   =0. ; Ycoj  = 0.0 ;
 qco2  =0. ; hco2  =0. ; Yco2j = 0.0 ;

* Les donnees physiques
 Cpvap  = TBT.'Cpvap' ;
 Lv     = TBT.'Lv'    ;
 Rabs   = TBT.'Rabs'  ;
 Rgh2   = TBT.'Rgh2'  ; Mkh2   = TBT.'Mkh2'              ;
 Rghe   = TBT.'Rghe'  ; Mkhe   = TBT.'Mkhe'              ;
 Rgo2   = TBT.'Rgo2'  ; Mko2   = TBT.'Mko2'              ;
 Rgn2   = TBT.'Rgn2'  ; Mkn2   = TBT.'Mkn2'              ;
 Rgco2  = TBT.'Rgco2' ; Mkco2  = TBT.'Mkco2'             ;
 Rgco   = TBT.'Rgco'  ; Mkco   = TBT.'Mkco'              ;
 Rgvap  = TBT.'Rgvap' ; Mkvap  = TBT.'Mkvap'             ;
 Rgair  = TBT.'Rgair' ; Mkair  = TBT.'Mkair'             ;


  t        = rxtsce .'t'                                 ;

  TTINJ=FAUX                                             ;
  THINJ=FAUX                                             ;
  TTINJ=exist (rxtsce ) 'tinj'                           ;
  THINJ=exist (rxtsce ) 'hair'                           ;

  Si ((TTINJ et THINJ) ou ((NON TTINJ) et (NON THINJ)))  ;
 Mess '*********************************************************' ;
 Mess ' ERREUR   ERREUR   ERREUR   ERREUR   ERREUR   ERREUR     ' ;
 Mess '                                                         ' ;
 Mess 'On ne peut imposer enthalpie et température              ' ;
 Mess 'et il faut imposer au moins l une des deux !!            ' ;
 Mess '*********************************************************' ;
 ERREUR 21                                                    ;
  Finsi                                                       ;

*-- Formulation en Température imposée ---------------------------------
*------------------ TTINJ ----------------------------------------------
  'SI' TTINJ                                                  ;
 mess ' Formulation en Température imposée -';
   'SI' (TBT.'CATHARE2')                                      ;
      mess ' CATHAR2 ';
      mess ' HELLO   ';
      titi='EXTE' 'cat < CASCAR'                       ;
     tinj = titi.1 ; qair = titi.2 ; qeau = titi.3 ;
   mess 'tinj qair qeau = ' tinj qair qeau ;
*   Finsi                                                     ;
   'SINON'                                                    ;

  tinj      = 'IPOL' tps t (rxtsce .'tinj')                   ;
  qair      = 'IPOL' tps t (rxtsce .'qair')                   ;

     'SI' TBT.'THE'                                ;
       qhe= 'IPOL' tps t (rxtsce .'qhe')   ; Finsi ;
     'SI' TBT.'TH2'                                ;
       qh2= 'IPOL' tps t (rxtsce .'qh2')   ; Finsi ;
     'SI' TBT.'TO2'                                ;
       qo2= 'IPOL' tps t (rxtsce .'qo2')   ; Finsi ;
     'SI' TBT.'TN2'                                ;
       qn2= 'IPOL' tps t (rxtsce .'qn2')   ; Finsi ;
     'SI' TBT.'TCO'                                ;
       qco= 'IPOL' tps t (rxtsce .'qco')   ; Finsi ;
     'SI' TBT.'TCO2'                               ;
       qco2='IPOL' tps t (rxtsce .'qco2')  ; Finsi ;
     'SI' TBT.'VAPEUR'                             ;
       qeau = 'IPOL' tps t (rxtsce .'qeau'); Finsi ;
   'FINSI'                                                    ;

   Qj= qair + qhe + qh2 + qo2 + qn2 + qco + qco2 + qeau ;


   'SI' TBT.'VAPEUR'                                    ;
     Pt =extr tic.'PT'  ndl                             ;
     Tfm=extr tic.'Tfm' ndl                             ;

   'SI'('NON'('EGA' Qj 0.)) ; iQj=1./Qj ; 'SINON' ; iQj=0. ; 'FINSI' ;
    Yvapj = qeau '*' iQj ; Yairj = qair '*' iQj ; Yhej  = qhe  '*' iQj ;
    Yh2j  = qh2  '*' iQj ; Yo2j  = qo2  '*' iQj ; Yn2j  = qn2  '*' iQj ;
    Yco2j = qco2 '*' iQj ; Ycoj  = qco  '*' iQj ;


  Mj=((Yvapj/Mkvap)+(Yairj/Mkair)+(Yhej/Mkhe)+(Yh2j/Mkh2)+(Yo2j/Mko2)+
     (Yn2j*Mkn2)+(Yco2j/Mkco2)+(Ycoj/Mkco))**(-1.)      ;
       Xvj=Yvapj*Mj/Mkvap                               ;
       Pvj  = Pt * Xvj                                  ;

      'SI' (TBT.'CONDMAS')                              ;
       Pt =extr tic.'PT'  ndl                           ;
       Tfm=extr tic.'Tfm' ndl                           ;
     Qj=qeau + qair + qhe + qh2 + qo2 + qn2 + qco + qco2;
*a peu près Xvj = Yvj on fera mieux après
       Xvj  = qeau/Qt                                   ;
       Pvj  = Pt * Xvj                                  ;
       Hinj = VARI HVS Pvj (Tinj + 273.15)              ;
       Tsat = VARI TSATP  Pt                            ;
       Hliq = VARI HLS Pvj Tsat                         ;
       Hvap = VARI HVS Pvj Tsat                         ;
       mess ' Hinj=' Hinj ' Hliq=' Hliq ' Hvap=' Hvap   ;
       mess 'Pvj=' pvj ' Tsat = ' Tsat ' Tinj=' (Tinj + 273.15);
       Ksi=(Hinj - Hliq)/(Hvap - Hliq)                  ;
       Ksi=MINI (PROG 1. Ksi)                           ;
       mess ' Ksi  ' Ksi ' qeau= ' qeau ' qvap=' (qeau*Ksi);
**?       Mliqbr= Mliqbr + (qeau*(1. - Ksi))               ;
       qeau = qeau * Ksi                                ;
      'FINSI'                                           ;

   'SINON'                                              ;

   'SI'('NON'('EGA' Qj 0.)) ; iQj=1./Qj ; 'SINON' ; iQj=0. ; 'FINSI' ;
    Yvapj = qeau '*' iQj ; Yairj = qair '*' iQj ; Yhej  = qhe  '*' iQj ;
    Yh2j  = qh2  '*' iQj ; Yo2j  = qo2  '*' iQj ; Yn2j  = qn2  '*' iQj ;
    Yco2j = qco2 '*' iQj ; Ycoj  = qco  '*' iQj ;


   'FINSI'                                              ;

   Si(EGA Qj 0.)                                        ;
      Roj   = 0.                                        ;
   Sinon                                                ;
  Pt    = 'EXTR' (tic.'PT')    ndl                      ;
  Rgj   = (TBT.'Rgvap' * Yvapj) + (TBT.'Rgair' * Yairj) +
          (TBT.'Rghe'  * Yhej)  + (TBT.'Rgh2'  * Yh2j)  +
          (TBT.'Rgo2'  * Yo2j)  + (TBT.'Rgn2'  * Yn2j)  +
          (TBT.'Rgco'  * Ycoj)  + (TBT.'Rgco2' * Yco2j) ;

      Roj   = Pt '/' (Tinj '+' 273.15) '/' Rgj          ;
   Finsi                                                ;

  Finsi                                                 ;
*-------------- FIN TTINJ ----------------------------------------------

*-- Formulation en Enthalpie imposée -----------------------------------
*------------------ THINJ ----------------------------------------------
  Si THINJ                                                    ;
*  mess ' Enthalpie Imposee';
  qair      = 'IPOL' tps t (rxtsce .'qair')            ;
  hair      = 'IPOL' tps t (rxtsce .'hair')            ;
   'SI' TBT.'THE' ;
     qhe= 'IPOL' tps t (rxtsce .'qhe')   ;
     hhe= 'IPOL' tps t (rxtsce .'hhe')   ; Finsi ;
   'SI' TBT.'TH2' ;
     qh2= 'IPOL' tps t (rxtsce .'qh2')   ;
     hh2= 'IPOL' tps t (rxtsce .'hh2')   ; Finsi ;
   'SI' TBT.'TO2' ;
     qo2= 'IPOL' tps t (rxtsce .'qo2')   ;
     ho2= 'IPOL' tps t (rxtsce .'ho2')   ; Finsi ;
   'SI' TBT.'TN2' ;
     qn2= 'IPOL' tps t (rxtsce .'qn2')   ;
     hn2= 'IPOL' tps t (rxtsce .'hn2')   ; Finsi ;
   'SI' TBT.'TCO';
     qco= 'IPOL' tps t (rxtsce .'qco')   ;
     hco= 'IPOL' tps t (rxtsce .'hco')   ; Finsi ;
   'SI' TBT.'TCO2' ;
     qco2='IPOL' tps t (rxtsce .'qco2')  ;
     hco2='IPOL' tps t (rxtsce .'hco2')  ; Finsi ;

     Qji = qair + qhe + qh2 + qo2 + qn2 + qco + qco2    ;
     Qj  =  Qji                                         ;

*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
* Calcul Cpj, Cp du melange : CALCP  -- CAS SANS VAPEUR
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   'SI' (NON (TBT.'VAPEUR'))                                 ;

* Calcul des fractions massiques des incondensables a l'injection

   'SI'('NON'('EGA' Qji 0.)) ; iQj=1./Qji ; 'SINON' ; iQj=0. ; 'FINSI' ;
                           Yairj = qair '*' iQj ; Yhej  = qhe  '*' iQj ;
    Yh2j  = qh2  '*' iQj ; Yo2j  = qo2  '*' iQj ; Yn2j  = qn2  '*' iQj ;
    Yco2j = qco2 '*' iQj ; Ycoj  = qco  '*' iQj ;

* On traite ici les incondensables. On se donne l'enthalpie.
* Pour remonter a la temperature on considere le Cp constant
* pour chaque incondensable et on le choisit a 50degreC
* Cette hypothese est justifiee dans l'intervalle de temperature
* 0. a 250 degresC

Cph2j Cphej Cpo2j Cpn2j Cpco2j Cpcoj Cpairj = CALCP tinj;

* On calcule le Cp du melange a l'injection
  Cpj   = (Cpairj * Yairj)+(Cphej * Yhej)+
          (Cph2j  * Yh2j )+(Cpo2j * Yo2j)+
          (Cpn2j  * Yn2j )+(Cpcoj * Ycoj)+
          (Cpco2j * Yco2j)                   ;

* On etablit l'enthalpie a l'injection pour les incondensables
  Hj = hair + hhe + hh2 + ho2 + hn2 + hco + hco2             ;

* On en deduit la temperature a l'injection dans le cas ou il n'y
* a que des incondensables
  tinj=(Hj/Cpj) - 273.15                                     ;
   'FINSI'                                                   ;
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
* Calcul Cpj, Cp du melange : CALCP  -- CAS AVEC VAPEUR
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   'SI' TBT.'VAPEUR'                                    ;

    qeau = 'IPOL' tps t (rxtsce .'qeau')         ;
    hvap = 'IPOL' tps t (rxtsce .'hvap')         ;

* On etablit l'enthalpie a l'injection
  Hj = hair + hhe + hh2 + ho2 + hn2 + hco + hco2 + hvap ;

* Initialisations
* On prend comme temperature d'injection la temperature au
* pas de temps precedent.

    Pt = extr tic.'PT'  ndl                             ;
    Si(EXIST rxt.'scenario' 'Tj')                       ;
    tinj = rxt.'scenario'.'Tj'                          ;
    Sinon                                               ;
    tinj = extr tic.'Tfm' ndl                           ;
    Finsi                                               ;
    tinja= tinj                                         ;
    qvapj = qeau                                        ;

* Repeter CCP 10                                         ;
 Repeter CCP 1                                           ;
* Calcul des fractions massiques a l'injection
    Qj = qvapj + Qji                                    ;
   'SI'('NON'('EGA' Qj 0.)) ; iQj=1./Qj ; 'SINON' ; iQj=0. ; 'FINSI' ;
    Yvapj = qvapj '*' iQj ; Yairj = qair '*' iQj ; Yhej  = qhe  '*' iQj;
    Yh2j  = qh2   '*' iQj ; Yo2j  = qo2  '*' iQj ; Yn2j  = qn2  '*' iQj;
    Yco2j = qco2  '*' iQj ; Ycoj  = qco  '*' iQj ;

* On calcule d'abord les Cp des incondensables a la temperature d injection
* on considere le Cp constant pour chaque incondensable et on le choisit a 50deg
* Cette hypothese est justifiee dans l'intervalle de temperature
* 0. a 250 degresC

Cph2j Cphej Cpo2j Cpn2j Cpco2j Cpcoj Cpairj = CALCP tinj  ;

* On va chercher le Cp de la vapeur iterativement
    Cpvapj='VARI' 'DHVDT' Pt (tinj + 273.15)              ;
    Cpvakj='VARI' 'DHVDT' Pt (tinj + 273.15)              ;
*    Cpvapj=2200.;

* On calcule le Cp du melange a l'injection
  Cpj   = (Cpvapj * Yvapj)+(Cpairj * Yairj)+
          (Cphej  * Yhej )+(Cph2j  * Yh2j )+
          (Cpo2j  * Yo2j )+(Cpn2j  * Yn2j )+
          (Cpcoj  * Ycoj )+(Cpco2j * Yco2j)                  ;
mess  ' Iteration ' &ccp ' Cpj=' Cpj  'Tinj=' Tinj ' Cpvakj=' Cpvakj;

* Mais Toute la vapeur ne peut pas rentrer sous forme de vapeur

   Mj=((Yvapj/Mkvap)+(Yairj/Mkair)+(Yhej/Mkhe)+(Yh2j/Mkh2)+(Yo2j/Mko2)+
      (Yn2j*Mkn2)+(Yco2j/Mkco2)+(Ycoj/Mkco))**(-1.)      ;
        Xvj=Yvapj*Mj/Mkvap                               ;
        Pvj  = Pt * Xvj                                  ;
* La il s'agit de la pression partielle de vapeur a l'injection
* Si on injeccte de la vapeur pure c'est la pression de l'enceinte Pt
        Tsat = 'VARI' 'TSATP'  Pvj                           ;
        Hliqs= 'VARI' 'HLS' Pvj Tsat                         ;
        Hvaps= 'VARI' 'HVS' Pvj Tsat                         ;

* On en deduit le taux de vide a l'injection
  Si(Hj > Hvaps);
        Ksi = 1. ;
  Sinon ;
    Si(Hj < Hliqs);
        Ksi = 0. ;
    Sinon;
        Ksi=(Hj - Hliqs)/(Hvaps - Hliqs)                 ;
        Ksi=MINI (PROG 1. Ksi)                           ;
    Finsi;
  Finsi ;


Si ('EXIST' TBT 'Modinj') ; Modinj = TBT.'Modinj' ;Finsi;
Si ('EXIST' TBT 'Modinj2'); Modinj = TBT.'Modinj2';Finsi;
Si ('EXIST' TBT 'Modinj3'); Modinj = TBT.'Modinj3';Finsi;
Si ('EXIST' Briches 'Modinj'); Modinj = Briches.'Modinj';Finsi;



* On en deduit la temperature a l'injection
Si (EGA TBT.'Modinj' 'A')                               ;
* Temperature d'ebullition
mess ' Injection - modèle A : Température d ébullition' ;
 tinj=(VARI 'TSATP' Pt) - 273.15                        ;
Finsi                                                   ;
Si (EGA TBT.'Modinj' 'B')                               ;
* Temperature moyenne enceinte
mess ' Injection - modèle B : Température moyenne'      ;
 tinj=extr tic.'Tfm' ndl                                ;
Finsi                                                   ;
Si (EGA TBT.'Modinj' 'C')                               ;
* Temperature Saturation a Pvj
mess ' Injection - modèle C : Température de saturation';
 tinj =('VARI' 'TSATP'  Pvj) - 273.15                   ;
Finsi                                                   ;

  Si(Hj > Hvaps);
* Temperature H/Cp
mess ' Injection surchauffée : Température inj. =' tinj ;
 tinj =(Hj - Hvaps)/(Cpj + 1.e-5) + Tsat - 273.15       ;
Finsi                                                   ;

 dtinjr=(tinj - tinja)/(tinj + tinja)*2.                     ;
 dtinjr=abs dtinjr                                           ;
 tinja=tinj                                                  ;

 mess ' Xvj=' Xvj ' Yvapj=' Yvapj ' Tsat=' (Tsat - 273.15)  ;
 mess ' Hj =' Hj  ' hvap=' hvap ' Hliq=' Hliq ' Hvaps=' Hvaps ;
 mess ' Pvj=' pvj ' Pt  =' Pt                          ;
 mess ' Ksi  ' Ksi ' qeau= ' qeau ' qvapj=' (qeau*Ksi)  ;
*      Mliqbr= Mliqbr + (qeau*(1. - Ksi))               ;

       qvapj = qeau * Ksi                               ;
 Si (dtinjr < 1.e-3); quitter CCP ; Finsi               ;
Fin CCP;


      Si(EGA Xvj 0.)                                    ;
      Rovap = 0.                                        ;
      Sinon                                             ;
      tknj=tinj + 273.15                                ;
      tk2j=tknj * tknj                                  ;
      a=(TBT.'B0'*tk2j)+(TBT.'B1'*tknj)+TBT.'B2'        ;
      a=(abs a)/TBT.'Rgvap'/tk2j/tknj                         ;
      dlt=((4.*a*Pvj/TBT.'Rgvap'/tknj) + 1.)**0.5       ;
      Rovap=(dlt - 1.)/2./a.                            ;
      Finsi                                             ;

  Rginc = (TBT.'Rgair' * Yairj)+(TBT.'Rghe' * Yhej)+
          (TBT.'Rgh2'  * Yh2j) +(TBT.'Rgo2' * Yo2j)+
          (TBT.'Rgn2'  * Yn2j) +(TBT.'Rgco' * Ycoj)+
          (TBT.'Rgco2' * Yco2j)                         ;
      Si(EGA Rginc 0.)                                  ;
      Roinc = 0.                                        ;
      Sinon                                             ;
      Roinc=(1. - Xvj)*Pt/Rginc/tknj                    ;
      Finsi                                             ;
      Roj = Rovap + Roinc                               ;

      qliq = 'IPOL' tps t (rxtsce .'qliq')       ;
      qliq = qliq + (qeau*(1. - Ksi))                   ;
      hliq = 'IPOL' tps t (rxtsce .'hliq')       ;

      qeau = qvapj                                      ;
      Qlj  = qliq                                       ;

*      Mliqbr= Mliqbr + (qeau*(1. - Ksi))               ;
*      Mliq  = Mliq   + (qeau*(1. - Ksi)) + qliq        ;

   'FINSI'                                                   ;
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

 Finsi ;
*-------------- FIN THINJ --------------------

     rxtsce .'Yvapj'= Yvapj                      ;
     rxtsce .'Yairj'= Yairj                      ;
     rxtsce .'Yhej' = Yhej                       ;
     rxtsce .'Yh2j' = Yh2j                       ;
     rxtsce .'Yo2j' = Yo2j                       ;
     rxtsce .'Yn2j' = Yn2j                       ;
     rxtsce .'Yco2j'= Yco2j                      ;
     rxtsce .'Ycoj' = Ycoj                       ;

     Qj=qeau + qair + qhe + qh2 + qo2 + qn2 + qco + qco2;
     rxtsce .'Qj'   = Qj                         ;
     rxtsce .'Qlj'  = Qlj                        ;
     rxtsce .'Tj'   = tinj                       ;
     rxtsce .'Roj'  = Roj                        ;

  Briches.CTinj= Briches.CTinj 'ET' ('PROG' Tinj)        ;
  Briches.CKsi = Briches.CKsi  'ET' ('PROG' Ksi)         ;
  Briches.CQlj = Briches.CQlj  'ET' ('PROG' qliq)        ;
  Briches.CHlj = Briches.CHlj  'ET' ('PROG' hliq)        ;
*-------------------------------------------------------------
*Caractéristiques de l'injection au pas de temps N
* c'est a dire Dtn = tn - t(n-1) a l'instant n-1

 'SI' ( TBT.'IMPR' >EG 2)                                    ;
 'MESS' '==> Détail des débits injectés (kg/s) Brèche ' nub  ;
 'MESS' 'Qeau=' qeau 'Qliq=' qliq 'Qair=' qair 'Qhe =' qhe 'Qh2 =' qh2 ;
 'MESS' 'Qo2 =' qo2  'Qn2 =' qn2  'Qco =' qco  'Qco2=' qco2  ;
 'FINSI'                                                     ;

  resproc qeau qliq qair qhe qh2 qo2 qn2 qco qco2         ;

   QUITTER BRUCHE                                         ;
Finsi                                                     ;
************************************************************************
********* FIN KAS1 *****************************************************
************************************************************************

************************************************************************
********* DEBUT KAS2 ***************************************************
************************************************************************

Si (EGA KAS 'KAS2');

  CSbreche =chai 'Sbreche'  ;
  C$breche =chai '$breche'  ;
  C$brechc =chai '$brechec' ;
  Cfacgeo  =chai 'facgeo'   ;
  Cbrechei =chai 'brechei'  ;
  Cbrechec =chai 'brechec'  ;
  Cbreche  =chai 'breche'   ;

 Si ((nub > 1) et (nab < 4))        ;
  CSbreche  = chai 'Sbreche'  nub   ;
  C$breche  = chai '$breche'  nub   ;
  C$brechc  = chai '$brech' nub 'c' ;
  Cfacgeo   = chai 'facgeo'   nub   ;
  Cbrechei  = chai 'brech'  nub 'i' ;
  Cbrechec  = chai 'brech'  nub 'c' ;
  Cbreche   = chai 'breche'   nub   ;

 Finsi                            ;

 Si ((nub > 0) et (nab < 4))         ;
  diru    =TBT.(chai 'diru' nub)     ;
 Sinon                               ;
  diru    =Briches.'diru'            ;
 Finsi                               ;

 diru    = diru/(norm diru)          ;
 Sbreche = GEO.CSbreche              ;
 $breche = GEO.C$breche              ;
 facgeo  = GEO.Cfacgeo               ;
 brechei = GEO.Cbrechei              ;
 brechec = GEO.Cbrechec              ;
 breche  = GEO.Cbreche               ;

 $menvf  = rxt.'GEO'.'$menvf'        ;
 $vtf    = rxt.'GEO'.'$vtf'          ;

     Qj  = rxtsce .'Qj'                         ;
     Qlj = rxtsce .'Qlj'                        ;
     tinj= rxtsce .'Tj'                         ;
**************************************************************
**** Evolution de la source en fct du temps ******************

  t         = rxtsce .'t'                             ;

* Initialisation des fractions massiques
  Yvapj= rxtsce .'Yvapj'                              ;
  Yairj= rxtsce .'Yairj'                              ;
  Yhej = rxtsce .'Yhej'                               ;
  Yh2j = rxtsce .'Yh2j'                               ;
  Yo2j = rxtsce .'Yo2j'                               ;
  Yn2j = rxtsce .'Yn2j'                               ;
  Yco2j= rxtsce .'Yco2j'                              ;
  Ycoj = rxtsce .'Ycoj'                               ;


* Les donnees physiques
 Cpvap  = TBT.'Cpvap' ;
 Lv     = TBT.'Lv'    ;
 Rabs   = TBT.'Rabs'  ;
 Rgh2   = TBT.'Rgh2'  ; Mkh2   = TBT.'Mkh2'              ;
 Rghe   = TBT.'Rghe'  ; Mkhe   = TBT.'Mkhe'              ;
 Rgo2   = TBT.'Rgo2'  ; Mko2   = TBT.'Mko2'              ;
 Rgn2   = TBT.'Rgn2'  ; Mkn2   = TBT.'Mkn2'              ;
 Rgco2  = TBT.'Rgco2' ; Mkco2  = TBT.'Mkco2'             ;
 Rgco   = TBT.'Rgco'  ; Mkco   = TBT.'Mkco'              ;
 Rgvap  = TBT.'Rgvap' ; Mkvap  = TBT.'Mkvap'             ;
 Rgair  = TBT.'Rgair' ; Mkair  = TBT.'Mkair'             ;

* Calcul Cp (Tj)   CALCP
  Tj    = tinj                                               ;

Cph2j Cphej Cpo2j Cpn2j Cpco2j Cpcoj Cpairj = CALCP Tj       ;

* Calcul des Cvs pour l'injection
  Cpvapj      = TBT.'Cpvap'                                  ;
  Cvvapj      = Cpvapj '-'  Rgvap                            ;
  Cvhej       = Cphej  '-'  Rghe                             ;
  Cvh2j       = Cph2j  '-'  Rgh2                             ;
  Cvo2j       = Cpo2j  '-'  Rgo2                             ;
  Cvn2j       = Cpn2j  '-'  Rgn2                             ;
  Cvco2j      = Cpco2j '-'  Rgco2                            ;
  Cvcoj       = Cpcoj  '-'  Rgco                             ;
  Cvairj      = Cpairj '-'  Rgair                            ;

  Cvj   = (Cvvapj * Yvapj)+(Cvairj * Yairj)+(Cvhej * Yhej)+
          (Cvh2j  * Yh2j)+(Cvo2j * Yo2j)+(Cvn2j * Yn2j)+
          (Cvcoj  * Ycoj)+(Cvco2j * Yco2j)                   ;
  Cpj   = (Cpvapj * Yvapj)+(Cpairj * Yairj)+(Cphej * Yhej)+
          (Cph2j  * Yh2j)+(Cpo2j * Yo2j)+(Cpn2j * Yn2j)+
          (Cpcoj  * Ycoj)+(Cpco2j * Yco2j)                   ;
  Rgj   = (Rgvap * Yvapj)+(Rgair * Yairj)+(Rghe * Yhej)+
          (Rgh2  * Yh2j)+(Rgo2 * Yo2j)+(Rgn2 * Yn2j)+
          (Rgco  * Ycoj)+(Rgco2 * Yco2j)                     ;

 Qjf   = Qj '*' facgeo                                       ;

 'SI' ('EGA' Qj 0.0)                                         ;
      Roj   = 0.0                                            ;
      guj   = 0.0                                            ;
 'SINON'                                                     ;
*µµµµµ Roj µµµµµµµµµµµµµµµµµµµµ
   Si TBT.'Tsortie' ;
      Roj   = Rhom  ;
      guj   = (Qjf '/' Sbreche '/' Roj )                     ;
   Sinon ;
      Roj   = rxtsce .'Roj'                           ;
      guj   = (Qjf '/' Sbreche '/' Roj )                     ;
   Finsi ;
*  mess 'Roj=' Roj ' Qjf=' Qjf 'Sbreche=' Sbreche 'Tps=' Tps ;
*µµµµµ Roj µµµµµµµµµµµµµµµµµµµµ
 'FINSI'                                                     ;


  Qjf   = Qj '*' facgeo                                      ;
  Hj    = Cpj   '*' (Tj '+' 273.15)                          ;
  Ej    = Cvj   '*' (Tj '+' 273.15)                          ;

  Lbreche=Sbreche**(1./IDIM)                                 ;
  Knj=0.05 * guj                                             ;
  Enj=(Knj**1.5)/Lbreche                                     ;

  Rvapj = Roj   '*' Yvapj                                    ;
  Rairj = Roj   '*' Yairj                                    ;
  Rhej  = Roj   '*' Yhej                                     ;
  Rh2j  = Roj   '*' Yh2j                                     ;
  Ro2j  = Roj   '*' Yo2j                                     ;
  Rn2j  = Roj   '*' Yn2j                                     ;
  Rco2j = Roj   '*' Yco2j                                    ;
  Rcoj  = Roj   '*' Ycoj                                     ;

 'SI' ( TBT.'IMPR' >EG 2)                                    ;
 'MESS' '==> Caracteristiques de l injection ' nub           ;
 'MESS' ' Débit total Injecté Qj=' Qj 'kg/s'                 ;
 'MESS' ' Vitesse Injection  guj=' guj 'm/s'                 ;
 'MESS' ' Densité Injection  Roj=' Roj 'kg/m3'               ;
 'MESS' ' R/M     Injection  Rgj=' Rgj 'J/kg/K'              ;
 'FINSI'                                                     ;

*=============================================================
  src=0.;

'SI' TBT.'FPAROI'   ;
  brchp=breche      ;
'SINON'             ;
  brchp=brechei     ;
'FINSI'             ;

'SI' DIM3D                                                   ;
 $brechec= GEO.C$brechc                                      ;
  gj    = 'KCHT' $breche 'VECT' 'SOMMET' 'COMP'
           ('MOTS' 1UN 2UN 3UN) (diru * guj)                 ;
  gj    = 'REDU' gj brchp                                    ;
  gjc   = 'KCHT' $brechec 'VECT' 'SOMMET' (0.0 0.0 0.0)      ;
  gjx   = 'KCHT' $breche 'VECT' 'SOMMET' (diru *  guj)   gjc ;
'SINON'                                                      ;
  gj    = 'KCHT' $breche 'VECT' 'SOMMET' 'COMP'
          ('MOTS' 1UN 2UN) (diru * guj)                      ;
  gj    = 'REDU' gj brchp                                    ;
 gjc   = 'MANU' 'CHPO' brechec 2 'UX' 0.0 'UY' 0.0
                                 'NATURE' 'DISCRET'          ;
  gjx   = 'KCHT' $breche 'VECT' 'SOMMET' (diru * guj) gjc    ;
'FINSI'                                                      ;

  unvf0 = 'KCHT' $menvf  'VECT' 'SOMMET'  gjx                ;
  src   = 'DBIT' unvf0 $menvf                                ;

*=============================================================

 Si(nab > 3)                                                 ;
  Briches.'Rvapj'=Rvapj                                      ;
  Briches.'Rairj'=Rairj                                      ;
  Briches.'Rhej' =Rhej                                       ;
  Briches.'Rh2j' =Rh2j                                       ;
  Briches.'Ro2j' =Ro2j                                       ;
  Briches.'Rn2j' =Rn2j                                       ;
  Briches.'Rco2j'=Rco2j                                      ;
  Briches.'Rcoj' =Rcoj                                       ;
  Briches.'Rgj'  =Rgj                                        ;
*  Briches.'Hj'   =Hj                                         ;
*  Briches.'Ej'   =Ej                                         ;
  Briches.'Tj'   =Tj                                         ;
*  Briches.'Qj'   =Qj                                         ;
  Briches.'Roj'  =Roj                                        ;
  Briches.'gj'   =gj                                         ;
*  Briches.'guj'  =guj                                        ;
  Briches.'unvf0'=unvf0                                      ;
  Briches.'src'  =src                                        ;
 Finsi                                                       ;
 resproc
 Rvapj Rairj Rhej Rh2j Ro2j Rn2j Rco2j Rcoj Rgj Hj Ej
 Tj Qj Roj gj guj unvf0 src                                  ;

   QUITTER BRUCHE                                            ;

************************************************************************
********* FIN KAS2 *****************************************************
************************************************************************
'FINSI';

FINPROC ;




