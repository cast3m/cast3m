$$$$ PROBDENS
* PROBDENS  PROCEDUR  CHLOE     98/11/18    21:17:30     3360
'DEBPROC' PROBDENS MU*'FLOTTANT' SIGMA*'FLOTTANT'
                   RB1*'FLOTTANT' B2*'FLOTTANT';
*|=====================================================================
*|
*| << OBJET >> :
*|
*| Procedure permettant de définir la densite de probabilite et
*| la fonction de repartition d'une variable aletoire connaissant
*| ses parametres statistiques (moyenne, ecart-type, symetrie,
*| aplatissement)
*|
*|
*| NB : Les évolutions résultats sont données entre
*|           Mu - 6 * Sigma et Mu + 6 * Sigma
*|
*|
*| << ENTREE >> :
*| -- ARGUMENTS OBLIGATOIRES DANS TOUS LES CAS --
*|
*| MU = objet de type REEL designant la valeur moyenne
*|                    de la variable aleatoire.
*|
*| SIGMA = objet de type REEL designant l'ecart-type
*|                       de la variable aléatoire.
*|
*| RB1 = objet de type REEL designant la valeur du coefficient
*|                    de symétrie de la variable aleatoire.
*|
*| B2 = objet de type REEL designant la valeur du coefficient
*|                    d'aplatissement de la variable aleatoire.
*| << SORTIE >> :
*|
*|
*| PDF = objet de type EVOLUTION contenant la densite de probabilite
*|                     de la variable aléatoire
*|
*| CDF = objet de type EVOLUTION contenant la fonction de repartition
*|                     de la variable aléatoire
*|
*|=====================================================================
*
* ______________________
*
* DECLARATIONS GENERALES
* ______________________
*
SI = 'MOT' 'SI' ; SINON = 'MOT' 'SINON' ; FINSI = 'MOT' 'FINSI' ;
REPETER = 'MOT' 'REPETER'; QUITTER = 'MOT' 'QUITTER'; FIN = 'MOT' 'FIN';
EXISTE = 'MOT' 'EXISTE' ; EGA = 'MOT' 'EGA' ;
ERREUR = 'MOT' 'ERREUR' ;
ENTIER   = 'MOT' 'ENTIER  ' ;
FLOTTANT = 'MOT' 'FLOTTANT' ;
MOT      = 'MOT' 'MOT     ' ;
* _____
*
* TESTS
* _____
*
* _________________
*
* DEBUT DES CALCULS
* _________________
*
X1 = PROG;
Y1 = PROG;
Y2 = PROG;
oldy = 0.;
*
NPAS = 100;
REPE BOUC NPAS;
  absx = (MU - (6 * SIGMA)) + ((&BOUC*(12*SIGMA))/NPAS);
  X1 = X1 ET (PROG absx);
  newy = PROB MU SIGMA RB1 B2 absx;
  Y1 = Y1 ET (PROG ((newy - oldy)/((12*SIGMA)/NPAS)));
  Y2 = Y2 ET (PROG newy);
  oldy = newy;
FIN BOUC;
CDF = evol 'MANU' 'Valeur' X1 'Repartition' Y2 ;
PDF = evol 'MANU' 'Valeur' X1 'Densite' Y1 ;
'FINPROC' PDF CDF;
$$$$


