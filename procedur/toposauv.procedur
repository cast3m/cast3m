$$$$ TOPOSAUV
* TOPOSAUV  PROCEDUR  CB215821  18/03/22    21:15:05     9786

************************************************************************
** Procedure called by TOPOPTIM for saving some data
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** 2.0 2018/02/09 Always save the last cycle data in case of early
**                stop of the optimization process to allow restarting
**                it back and delete the previous cycle data if it had
**                not to be saved
** 1.0 2017/04/18
************************************************************************

DEBP TOPOSAUV tab0*'TABLE' ;
    SI (tab0.'MES_SAUVEGARDES'.'TOPOLOGIE' OU
        tab0.'MES_SAUVEGARDES'.'MAILLAGE'  OU
        tab0.'MES_SAUVEGARDES'.'RESOLUTION') ;
        cy0 = tab0.'WTABLE'.'CYCLE' ;

*       1) We save the data from the last cycle (in case the
*          optimization process is stopped prematurely)
        froz0 = EXIS tab0 'ZONE_FIGEE' ;
        tab0.'TOPOLOGIE'.cy0 = tab0.'WTABLE'.'TOPOLOGIE' ;
        SI froz0 ;
            tab0.'TOPOLOGIE'.cy0 = tab0.'TOPOLOGIE'.cy0 +
                                   tab0.'WTABLE'.'UN'.(2) ;
        FINS ;
        SI tab0.'MES_SAUVEGARDES'.'MAILLAGE' ;
            tab0.'MAILLAGE'.cy0 = tab0.'WTABLE'.'MAILLAGE'.'A' ;
            SI froz0 ;
                tab0.'MAILLAGE'.cy0 = tab0.'MAILLAGE'.cy0 ET
                                    tab0.'WTABLE'.'MAILLAGE'.(2) ;
            FINS ;
        FINS ;
        SI tab0.'MES_SAUVEGARDES'.'RESOLUTION' ;
            tab0.'RESOLUTION'.cy0 = tab0.'WTABLE'.'RESOLUTION' ;
        FINS ;

*       2) Delete the previous cycle if it is not in
*          CYCLES_SAUVEGARDES
        cy1 = cy0 - 1 ;
        SI (NEG cy1 0) ;
            SI (NON tab0.'MES_SAUVEGARDES'.'TOPOLOGIE') ;
                SI (EXIS (tab0.'TOPOLOGIE') cy1) ;
                    tab0.'TOPOLOGIE' = ENLE (tab0.'TOPOLOGIE') cy1 ;
                FINS ;
            FINS ;
            SI (NON (EXIS tab0.'CYCLES_SAUVEGARDES' cy1)) ;
                SI (EXIS (tab0.'TOPOLOGIE') cy1) ;
                    tab0.'TOPOLOGIE' = ENLE (tab0.'TOPOLOGIE') cy1 ;
                FINS ;
                SI (EXIS (tab0.'MAILLAGE') cy1) ;
                    tab0.'MAILLAGE' = ENLE (tab0.'MAILLAGE') cy1 ;
                FINS ;
                SI (EXIS (tab0.'RESOLUTION') cy1) ;
                    tab0.'RESOLUTION' = ENLE (tab0.'RESOLUTION') cy1 ;
                FINS ;
            FINS ;
        FINS ;
    FINS ;
FINP ;

$$$$

