$$$$ @STAT
* @STAT     PROCEDUR  CB215821  16/11/21    21:15:00     9195
*-----------------------------------------------------------
******              PROCEDURE @STAT                   ******
*-----------------------------------------------------------
*
*    CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*   A DISPOSITION DE LA COMMUNAUTE  CASTEM2000
* PAR  DELERUYELLE Fr.  (SOCOTEC-INDUSTRIE à l'IPSN/DES)
*
*-----------------------------------------------------------
*           OPERATIONS STATISTIQUES COURANTES
*----------------------------------------------------------
'DEBP' @STAT    LX*'LISTREEL' LY/'LISTREEL' ;

 XI  = 0. ;
 XI2 = 0. ;
 NX  = 'DIME' LX ;

'SI' ('EXIS' LY) ;
*  DEUX LISTES
   YI   = 0.      ;
   YI2  = 0.      ;
   XIYI = 0.      ;
   NY = 'DIME' LY ;

  'SI' (NX 'EGA' NY) ;
     K = 0 ;
    'REPE' BOU1 NX ;
       K = K '+' 1 ;
       XK = 'EXTR' LX K ;
       YK = 'EXTR' LY K ;

       XI   = XI   '+'  XK         ;
       XI2  = XI2  '+' (XK '**' 2) ;
       YI   = YI   '+'  YK         ;
       YI2  = YI2  '+' (YK '**' 2) ;
       XIYI = XIYI '+' (XK '*' YK) ;
    'FIN' BOU1 ;

     XM   = XI '/' NX ;
     YM   = YI '/' NY ;

     VX = 0.D0 ;
     VY = 0.D0 ;
    'REPE' BOU2 NX ;
       XK  ='EXTR' LX &BOU2             ;
       YK = 'EXTR' LY &BOU2             ;
       VX  = VX '+' ((XK '-' XM) '**' 2);
       VY  = VY '+' ((YK '-' YM) '**' 2);
    'FIN' BOU2 ;
     VX = VX '/'  NX    ;
     VY = VY '/'  NY    ;
     EX = VX '**' 0.5D0 ;
     EY = VY '**' 0.5D0 ;

     COXY =(XIYI '/'  NX) '-' (XM '*' YM) ;
     A    = COXY '/'  VX         ;
     B    = YM   '-' (A  '*' XM) ;
     R    = COXY '/' (EX '*' EY) ;

  'SINON' ;
    'MESS' '    ' ;
    'MESS' '*** STAT ==> Les listes lx et ly n ont pas meme dimension' ;
    'MESS' '             Dim(lx)=' ('DIME' LX) 'Dim(ly)=' ('DIME' LY) ;
    'MESS' '    ' ;
  'FINSI' ;
  'RESP' XM EX YM EY A B R ;

'SINON' ;
*  UNE SEULE LISTE
  'REPE' BOU1 NX ;
     XK  = 'EXTR' LX &BOU1       ;
     XI  = XI    '+'  XK         ;
  'FIN' BOU1 ;
   XM = XI  '/' NX               ;

   VX = 0.D0 ;
  'REPE' BOU2 NX ;
     XK  ='EXTR' LX &BOU2             ;
     VX  = VX '+' ((XK '-' XM) '**' 2);
  'FIN' BOU2 ;
   VX = VX '/'  NX    ;
   EX = VX '**' 0.5D0 ;
  'RESP' XM EX ;
'FINSI' ;

'FINP' ;

$$$$


