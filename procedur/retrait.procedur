$$$$ RETRAIT
* RETRAIT   PROCEDUR  CHAT      05/10/11    21:16:03     5209
DEBP RETRAIT NOMREG*MOT MOBET*MMODEL RAYSECH*MCHAML
             TCOULB*FLOTTANT  TEMPCOUR*FLOTTANT
             FLOT1*FLOTTANT RHOS*MCHAML FC28/FLOTTANT            ;
*
* RAYSECH  : Rayon de sechage en cm
* TCOULA   : Instants de coulage du beton en jours
* TEMPCOUR : Instant courant en jours
* FLOT1    : Humidite du milieu environnant (formulation BPEL)
* RHOS     : Taux d'armatures
* FC28     : Resistance caracteristique a 28 jours en Pa (Defaut : 40E6)
*
CHAMUNIT = MANU CHML MOBET SCAL 1. TYPE 'CONTRAINTES' STRESSES         ;
TCOULA=TCOULB*CHAMUNIT;
FLOT2=RHOS;
*
RAYSECH  = EXCO EPAI RAYSECH SCAL                                      ;
*
* MODIFICATIONS B. CIREE DU 25/02/03
* AJOUT DES FORMULATIONS BPEL MISE A JOUR 99
*
SI (EGA NOMREG 'BPEL99')                                               ;
*
  MESS 'Calcul du Retrait avec la formulation BPEL99'                  ;
  MESS '--------------------------------------------'                  ;
*
  SI (NON (EXIS FLOT1))                                                ;
    MESS '--- Pas de donnees pour l humidite du milieu environnant'    ;
    MESS '    pour la formulation BPEL99 - On quitte la procedure'     ;
    QUIT RETRAIT                                                       ;
  FINSI                                                                ;
  SI (NON (EXIS FC28))                                                 ;
    MESS '--- Pas de Resistance caracteristique du beton a 28 jours'   ;
    MESS '    On prend 40.E6 Pa par defaut  '                          ;
    FC28  = 40.E6                                                      ;
  FINSI                                                                ;
*
  MESS 'Instant de calcul du retrait          :' TEMPCOUR 'jours'      ;
  MESS 'Instant de calcul de coulage du beton :'
                                  (MINI TCOULA) (MAXI TCOULA) 'jours'  ;
  HUMREL  = FLOT1                                                      ;
  KFC28 = 18.                                                          ;
*
* --- RETRAIT ENDOGENE
*
  EPSRE = (FC28 - 20.E6) * 1.E-12
        * ((2.8 * CHAMUNIT)
               - (1.1 * (EXP (((TEMPCOUR*CHAMUNIT) - TCOULA) / -96.))));
*
*
* --- RETRAIT DE DESSICCATION
*
  A1 = KFC28 * ((72. * (EXP (-0.046E-6 * FC28))) + (75. - HUMREL))
     * 1.E-6                                                           ;
  A2 = CHAMUNIT + ((8.4 * RAYSECH * RAYSECH) *
       (((TEMPCOUR*CHAMUNIT) - TCOULA) ** -1.) )                       ;
  EPSRD = A1 * (A2 ** -1.)                                             ;
*
*
* --- RETRAIT TOTAL
*
  MCHAMC = (-1.) * ((EPSRE * CHAMUNIT) + EPSRD)
                 * ((CHAMUNIT + (15. * RHOS)) ** -1.)                  ;
*
  MESS 'retrait endogene :       ' (MINI EPSRE)  (MAXI EPSRE)          ;
  MESS 'retrait de dessiccation :' (MINI EPSRD)  (MAXI EPSRD)          ;
  MESS 'retrait total :          ' (MINI MCHAMC) (MAXI MCHAMC)         ;
*
FINSI                                                                  ;
*
SI (EGA NOMREG 'BPEL91')                                               ;
*
  MESS 'Calcul du Retrait avec la formulation BPEL91'                  ;
  MESS '--------------------------------------------'                  ;
*
  SI (NON (EXIS FLOT1))                                                ;
    MESS '--- Pas de donnees pour l humidite du milieu environnant'    ;
    MESS '    pour la formulation BPEL91 - On quitte la procedure'     ;
    QUIT RETRAIT                                                       ;
  FINSI                                                                ;
*
  HUMREL  = FLOT1                                                      ;
  KS      = 1.*((CHAMUNIT + (20. * RHOS))** -1. )                      ;
*
* ____ MODIFICATION DU 30 MARS 2001 F. PICARD
*
  EPSI01 = (100. - HUMREL)                                             ;
  A1 = (6.*CHAMUNIT)                                                   ;
  B1 = (10.*CHAMUNIT)                                                  ;
  C1 = (3.*RAYSECH)                                                    ;
  B1 = B1 + C1                                                         ;
  B1 = B1**-1.                                                         ;
  B1 = 80. * B1                                                        ;
  EPSI02 = A1 + B1                                                     ;
  EPSI0 = EPSI01 * EPSI02 * 1.E-6                                      ;
*
  NUMER = ((TEMPCOUR*CHAMUNIT) - TCOULA)                               ;
  DENOM = ((TEMPCOUR*CHAMUNIT) - TCOULA) + (9.*RAYSECH)                ;
  DENOM = DENOM**(-1)                                                  ;
  R_T   = NUMER * DENOM                                                ;

*
* --- DEFORMATION DIFFEREE DE RETRAIT : SCALAIRE
*
  MCHAMC = (-1.) * KS*EPSI0*R_T                                        ;
  MESS 'retrait total :          ' (MINI MCHAMC) (MAXI MCHAMC)         ;
*
FINSI                                                                  ;
*
SI (EGA NOMREG 'EC2')                                                  ;
*
  HUMREL   = FLOT1                                                     ;
  FCM      = FLOT2                                                     ;
  EPSIFCM  = (160. + (10.*5.*(9. - (FCM/10.))))*1.E-6                  ;
  SI (HUMREL < 99.)                                                    ;
    BETAHR = -1.55*(1. - ((HUMREL/100.)**3.))                          ;
  SINON                                                                ;
    BETAHR = 0.25                                                      ;
  FINSI                                                                ;
*
 ----------- MODIFICATION DU 29 MARS 2001 F. PICARD
*
  XX1 = ((TEMPCOUR*CHAMUNIT) - TCOULA)                                 ;
  XX2 = ((350.*((RAYSECH/100.)**2.)) +
               (TEMPCOUR*CHAMUNIT) - TCOULA)**0.5                      ;
  XX2 = XX2** -1.                                                      ;
  BETACIN  = XX1 * XX2                                                 ;

  MCHAMC = EPSIFCM*BETAHR*BETACIN                                      ;
*
FINSI                                                                  ;
*
SI (EGA NOMREG 'LG')                                                   ;
*
  SI (NON (EGA (TYPE FLOT1) 'FLOTTANT'))                               ;
    KSA    = 1./(1. + (16.*1.071E-2))                                  ;
    KRETRP = 1./(1. + (16.*3.790E-3))                                  ;
  SINON                                                                ;
    KSA    = FLOT1                                                     ;
    KRETRP = FLOT2                                                     ;
  FINSI                                                                ;
*
  K2 = (2.*160.*((3.*RAYSECH)**-1.))**2.                               ;
*
  EPSRE =  -81.355E-6                                                  ;
  ARE   =    0.88023                                                   ;
  BREJ  =  182.9674339*CHAMUNIT                                        ;
  EPSRD = -581.7077E-6                                                 ;
  ARD   =    0.9688749                                                 ;
  BRDJ  =   36.51898484*CHAMUNIT                                       ;
*
  RET   = EPSRE * (((TEMPCOUR*CHAMUNIT) - TCOULA)**ARE) *
          (((((TEMPCOUR*CHAMUNIT) - TCOULA)**ARE) + BREJ)**-1.)        ;
*
  RDK2T = EPSRD * ((K2*((TEMPCOUR*CHAMUNIT) - TCOULA))**ARD)
     ((((K2*((TEMPCOUR*CHAMUNIT) - TCOULA))**ARD) + BRDJ)**-1.)        ;
*
  MCHAMC = KSA*KRETRP*(RET + RDK2T)                                    ;
*
FINSI                                                                  ;
*
* --- CONSTRUCTION DU TENSEUR DE DEFORMATION DIFFEREE DIAGONALE
*     DE RETRAIT
*
*           | A 0 0 |
*  EPSRET = | 0 A 0 | AVEC A = MCHAMC EN SCALAIRE C'EST A DIRE :
*           | 0 0 A |
*
*  EPSRET(I,J) = MCHAMC*D(I,J), D(I,J) = 1 SI I = J ET 0 SINON
*
EPSRETXX = EXCO SCAL     MCHAMC  EPXX                                  ;
EPSRETYY = EXCO SCAL     MCHAMC  EPYY                                  ;
EPSRETZZ = EXCO SCAL     MCHAMC  EPZZ                                  ;
EPSRETXY = EXCO SCAL (0.*MCHAMC) GAXY                                  ;
EPSRETXZ = EXCO SCAL (0.*MCHAMC) GAXZ                                  ;
EPSRETYZ = EXCO SCAL (0.*MCHAMC) GAYZ                                  ;
*
EPSRET = EPSRETXX ET EPSRETYY ET EPSRETZZ ET
         EPSRETXY ET EPSRETXZ ET EPSRETYZ                              ;
EPSRET = CHAN 'TYPE' EPSRET 'DEFORMATIONS'                             ;
*
FINP EPSRET                                                            ;

