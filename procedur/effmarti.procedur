$$$$ EFFMARTI
* EFFMARTI  PROCEDUR  AF221230  16/11/03    21:15:02     9139
DEBPROC EFFMARTI SIG1*'MCHAML' MOD1*'MMODEL' MAT1*'MCHAML'
                  VECT1*'POINT' VECT2*'POINT'
                  H1*'FLOTTANT' ENRE1*'FLOTTANT' ENRI1*'FLOTTANT'
                  COT1*'FLOTTANT';
*
* --------------------------------------------------------
*  Procedure EFF_MARTI
*    Definition des efforts de membrane pour la couche 
*    superieure et inferieure pour un element coque
*
*  Develeppeurs:
*    Alberto FRAU
*    DEN/DANS/DM2S/SEMT/EMSI
*    Nicolas ILE
*    DEN/DANS/DM2S/SEMT/EMSI
*
*  ENTREE:
*    SIG1:          MCHAML des contraintes
*    MOD1:          MMODEL associé au SIG1
*    MAT1:          MCHAML associé au SIG1
*    VEC1:          direction 1 pour le calcul des contraintes
*    VEC2:          direction 2 pour le calcul des contraintes
*    H1:            epaisseur de l'element plaque
*    ENRE1:         enrobage couche externe
*    ENRi1:         enrobage couche interne
*    COT1:          terme cotg(theta)
*
*  SORTIE:
*    SIG2:          MCHAML avec les composants des efforts de membrane 
*                   pour la couche inferieure et superieure et les 
*                   efforts de tranchants hors plan
* --------------------------------------------------------
*
* controle dimension du calcul
'SI' (('VALE' 'DIME') 'NEG' 3);
  'MESS' 'Calcul possible seulement en 3D';
  QUIT EFFMARTI;
'FINSI';
*
*  Test sur l'orientation  du maillage
VERSSENS ('EXTR' SIG1 'MAIL');
*
*  Changement des directions
SIG1 = RTENS SIG1 MOD1 MAT1 VECT1 VECT2;
*
*  changement à GRAVITE
MAI1 = EXTR SIG1 MAIL;
SIG1B = 'CHAN' 'GRAVITE' MOD1 SIG1;
SIG1B = REDU SIG1B MAI1;
*
*
* Creaction des MCHAML vides
N11E = 'VIDE' 'MCHAML';
N12E = 'VIDE' 'MCHAML';
N22E = 'VIDE' 'MCHAML';
*
N11I = 'VIDE' 'MCHAML';
N12I = 'VIDE' 'MCHAML';
N22I = 'VIDE' 'MCHAML';
*
M11 = 'VIDE' 'MCHAML';
M22 = 'VIDE' 'MCHAML';
M12 = 'VIDE' 'MCHAML';
*
V1 = 'VIDE' 'MCHAML';
V2 = 'VIDE' 'MCHAML';
VR = 'VIDE' 'MCHAML';
*
* extraction des efforts globaux
M11 = M11 'ET' (EXCO SIG1B 'M11' 'M11T');
M22 = M22 'ET' (EXCO SIG1B 'M22' 'M22T');
M12 = M12 'ET' (EXCO SIG1B 'M12' 'M12T');
V1 = V1 'ET' (EXCO SIG1B 'V1' 'V1T');
V2 = V2 'ET' (EXCO SIG1B 'V2' 'V2T');
*
CHP1 = 'EXCO' V1 'V1T' 'SCAL';
CHP2 = 'EXCO' V2 'V2T' 'SCAL';
CHP3 = ((CHP1)*(CHP1)) + ((CHP2)*(CHP2));
CHP3 = ((CHP3)**(0.5)); 
AAA = MASQU CHP3 'EGINFE' (1.E-12);
CHP3 = CHP3 + (((AAA))*(1.E99));
VR = VR 'ET' (EXCO CHP3 'SCAL' 'VR');
'OUBL' CHP1;
'OUBL' CHP2;
'OUBL' CHP3;
*
* Determination des efforts de membrane pour les couches 
* inferieure et superieure
N11S = 'EXCO' SIG1B 'N11' 'SCAL';
N12S = 'EXCO' SIG1B 'N12' 'SCAL';
N22S = 'EXCO' SIG1B 'N22' 'SCAL';
M11S = 'EXCO' M11 'M11T' 'SCAL';
M22S = 'EXCO' M22 'M22T' 'SCAL';
M12S = 'EXCO' M12 'M12T' 'SCAL';
V1S = 'EXCO' V1 'V1T' 'SCAL';
V2S = 'EXCO' V2 'V2T' 'SCAL';
V0S = 'EXCO' VR 'VR' 'SCAL';
*
DD1 = H1 - ((ENRE1 + ENRI1)) ;
*
N11E = N11E 'ET' (((0.5)*(N11S)) + 
                ((M11S)/(DD1)) + 
                (((V1S)*(V1S))/(((2.0)*(COT1))*(V0S))));
N22E = N22E 'ET' (((0.5)*(N22S)) + 
                ((M22S)/(DD1)) + 
                (((V2S)*(V2S))/(((2.0)*(COT1))*(V0S))));
N12E = N12E 'ET' (((0.5)*(N12S)) + 
                ((M12S)/(DD1)) + 
                (((V1S)*(V2S))/(((2.0)*(COT1))*(V0S))));
N11I = N11I 'ET' (((0.5)*(N11S)) - 
                ((M11S)/(DD1)) + 
                (((V1S)*(V1S))/(((2.0)*(COT1))*(V0S))));
N22I = N22I 'ET' (((0.5)*(N22S)) - 
                ((M22S)/(DD1)) + 
                (((V2S)*(V2S))/(((2.0)*(COT1))*(V0S))));
N12I = N12I 'ET' (((0.5)*(N12S)) - 
                ((M12S)/(DD1)) + 
                (((V1S)*(V2S))/(((2.0)*(COT1))*(V0S))));
*
N11E = 'EXCO' N11E 'SCAL' 'N11E';
N22E = 'EXCO' N22E 'SCAL' 'N22E';
N12E = 'EXCO' N12E 'SCAL' 'N12E';
N11I = 'EXCO' N11I 'SCAL' 'N11I';
N22I = 'EXCO' N22I 'SCAL' 'N22I';
N12I = 'EXCO' N12I 'SCAL' 'N12I';
*
* Assemblage
SIG2 = N11E ET N22E ET N12E ET 
       N11I ET N22I ET N12I ET
       M11 ET M22 ET M12 ET
       V1 ET V2 ET VR;
*
FINPROC SIG2;
$$$$

