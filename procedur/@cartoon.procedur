$$$$ @CARTOON
* @CARTOON  PROCEDUR  KK2000    98/01/19    21:19:34     3057
'DEBPROC' @CARTOON TAB1*TABLE MAILL1*MAILLAGE
  BL1/RIGIDITE OEIL/POINT AMPLI/FLOTTANT OSCILLE/MOT;
  IDIM='VALE' DIME;
  'SI' (IDIM 'EGA' 3);
    'SI' ('NON' ('EXISTE' OEIL));
      'ERREUR' 'IL FAUT DONNER UN OEIL EN 3D';
    'FINSI';
  'FINSI';
  'SI' ('EXISTE' BL1);
    LVEC=VRAI;
  'SINON';
    LVEC=FAUX;
  'FINSI';
  'SI' ('EXISTE' OSCILLE);
    'SI' ('NON' (EGA OSCILLE 'NOSCIL'));
      'ERREUR' 'ON ATTEND  NOSCIL  COMME MOT';
    'FINSI';
  'FINSI';
  LP1='EXISTE' TAB1 'DEPLACEMENTS';
  'SI' LP1;
    MODEPL= 'CHAINE' 'DEPLACEMENTS';
  'SINON';
    MODEPL= 'CHAINE' 'RESUDEPL';
  'FINSI';
  IND='INDEX' (TAB1.MODEPL);
  NBINCR='DIME' IND;
  'SI' LP1 ;
    IINCR0=1;
    NBINCR=NBINCR - 1;
  'SINON';
    IINCR0=0;
  'FINSI';
  IINCR=IINCR0;
  MINAMPL=1.E+50;
  FMAX=1.;
  'SI' LVEC;
    XLONG=('MAXI' ( 'COOR' 1 MAILL1)) - ('MINI' ( 'COOR' 1 MAILL1));
    YLONG=('MAXI' ( 'COOR' 2 MAILL1)) - ('MINI' ( 'COOR' 2 MAILL1));
    'SI' (YLONG > XLONG);
      XLONG=YLONG;
    'FINSI';
    'SI' (IDIM 'EGA' 3);
      ZLONG=('MAXI' ( 'COOR' 3 MAILL1)) - ('MINI' ( 'COOR' 3 MAILL1));
      'SI' (ZLONG > XLONG);
        XLONG=ZLONG;
      'FINSI';
    'FINSI';
  'FINSI';
  'REPETER' BOU1 NBINCR;
    IINCR=IINCR+1;
    AMP1='EXTR'('DEFO' MAILL1 TAB1.MODEPL.(IND.IINCR)) 'AMPL';
    'SI' (AMP1 '<' MINAMPL);
      MINAMPL=AMP1;
    'FINSI';
    'SI' LVEC;
      FFMAX='MAXI' ('REAC' TAB1.MODEPL.(IND.IINCR) BL1) 'ABS';
      'SI' (FFMAX > FMAX);
        FMAX=FFMAX;
      'FINSI';
    'FINSI';
  'FIN' BOU1;
  'SI' ('EXISTE' AMPLI);
    MINAMPL=AMPLI;
  'FINSI';
  DEF1='DEFO' MAILL1 TAB1.MODEPL.(IND.1) 0 VERT;
  'SI' LVEC;
    AMPVEC=XLONG/(5.*FMAX);
    IINCR=IINCR0;
    'REPETER' BOU2 (NBINCR-1);
      IINCR=IINCR+1;
      REA1='REAC' BL1 TAB1.MODEPL.(IND.IINCR);
      'SI' (IDIM 'EGA' 3);
        VEC1='VECTEUR' REA1 FX FY FZ AMPVEC ROUG;
        'SINON';
        'SI' ('EGA' ('VALE' MODE) 'AXIS');
          VEC1='VECTEUR' REA1 FR FZ AMPVEC ROUG;
        'SINON';
          'SI' ('EGA' ('VALE' MODE) 'FOUR');
            VEC1='VECTEUR' REA1 FR FZ FT AMPVEC ROUG;
          'SINON';
            VEC1='VECTEUR' REA1 FX FY AMPVEC ROUG;
          'FINSI';
        'FINSI';
      'FINSI';
      DEF1=DEF1 ET
      ('DEFO' MAILL1 TAB1.MODEPL.(IND.IINCR) MINAMPL VEC1 BLEU);
*  ('DEFO' MAILL1 TAB1.MODEPL.(IND.IINCR) AMP1 VEC1 BLEU);
    'FIN' BOU2;
  'SINON';
    IINCR=IINCR0;
    'REPETER' BOU2 (NBINCR-1);
      IINCR=IINCR+1;
      DEF1=DEF1 ET
      ('DEFO' MAILL1 TAB1.MODEPL.(IND.IINCR) MINAMPL BLEU);
*  ('DEFO' MAILL1 TAB1.MODEPL.(IND.IINCR) AMP1 BLEU);
    'FIN' BOU2;
  'FINSI';
  'SI' ('EXISTE' OSCILLE);
    'SI' (IDIM 'EGA' 3);
      'TRAC' OEIL DEF1 ANIME;
    'SINON';
      'TRAC' DEF1 ANIME;
    'FINSI';
  'SINON';
    'SI' (IDIM 'EGA' 3);
      'TRAC' OEIL DEF1 OSCIL;
    'SINON';
      'TRAC' DEF1 OSCIL;
    'FINSI';
  'FINSI';
'FINPROC' DEF1;
$$$$

