$$$$ RECOMPOM
* RECOMPOM  PROCEDUR  PICA      92/07/03    21:34:12     619
*----------------------------------------------------------------------*
* PROCEDURE RECOMPOM                                                   *
*                                                                      *
* CALCUL DE LA RECOMPOSITION D'UN SIGNAL QUELCONQUE EN                 *
* ONDELETTE ORTHOGONALE                                                *
*                                                                      *
* M ACC=RECOMPOS MRESI MDECO DTIME (CRESI CDECO OPTIO);                *
*----------------------------------------------------------------------*
* ENTREE                                                               *
*                                                                      *
* MRESI : OBJET DE TYPE EVOLUTION CONTENANT LE RESIDU AU NIVEAU M      *
*         SOUS FORME DE FONCTION DE MODULATION                         *
*                                                                      *
* CRESI : OBJET DE TYPE FLOTTANT CONTENANT UN COEF DE PONDERATION      *
*         DE MRESI (DEFAUT 1)                                          *
*                                                                      *
* MDECO : OBJET DE TYPE EVOLUTION CONTENANT LA DECOMPOSITION EN M      *
*         NIVEAUX, DES BASES VERS LES HAUTES FREQUENCES, SOUS FORME DE *
*         FONCTION DE MODULATION                                       *
*                                                                      *
* CDECO : OBJET DE TYPE LISTREEL CONTENANT DES COEFF DE PONDERATION    *
*         POUR CHAQUE NIVEAU (DEFAUT PROG 1 1 .... )                   *
*                                                                      *
* DTIME : PAS DE TEMPS DU SIGNAL DE BASE                               *
*                                                                      *
* OPTIO : OBJET DE TYPE TABLE CONTENANT LES OPTION                     *
*----------------------------------------------------------------------*
* OPTIONS                                                              *
*                                                                      *
* OPTIO.'LREC'= ENTIER, NOMBRE DE NIVEAU DE RECOMPOSITION SOUHAITE,    *
*               DEFAUT PERMETTANT LE CALCUL MAXIMUM                    *
*                                                                      *
* OPTIO.'FORC'= LOGIQUE, AUTORISATION POUR FORCER LES CALCUL AU DELA   *
*               DES POSSIBILITES DE DECO ET SUIVANT LREC               *
*                                                                      *
* OPTIO.'BORD'= MOT, TRAITEMENT DES CONDITION DE BORD POUR LA          *
*               CORRELATION: SYME(trique) OU PADD(ing) de ZERO         *
*                                                                      *
* OPTIO.'TYPE'= MOT, TYPE D'ONDELETTE: MALL(at)                        *
*               DEFAUT MALL                                            *
*                                                                      *
* OPTIO.'AMPL'= FLOTTANT, PERTUBATION EN AMPLITUDE DES COEFICIENTS     *
*               DEFAUT PAS DE PERTURBATION                             *
*                                                                      *
* OPTIO.'INIT'= ENTIER, INITIALISATION DU TIRAGE ALEATOIRE             *
*               DEFAUT RIEN                                            *
*----------------------------------------------------------------------*
* SORTIE                                                               *
*                                                                      *
* M     : ENTIER, NOMBRE DE NIVEAU ATTEINT                             *
*                                                                      *
* ACC   : OBJET DE TYPE EVOLUTION CONTENANT LE SIGNAL                  *
*----------------------------------------------------------------------*
'DEBPROC' RECOMPOM MRESI*'EVOLUTION' MDECO*'EVOLUTION'
                   DTIME*'FLOTTANT'
                   CRESI/'FLOTTANT'  CDECO/'LISTREEL'  OPTIO/'TABLE';
*----------------------------------------------------------------------*
*
* Lecture des options
*
'SI' ('EXISTE' OPTIO);
  LOPTION=VRAI;
'SINON';
  LOPTION=FAUX;
'FINSI';
CAMPL='TEXT' ' '; CINIT='TEXT' ' ';
'SI' LOPTION;
  'SI' ('EXISTE' OPTIO 'AMPL');
    CAMPL='TEXT' 'AMPL' (OPTIO.'AMPL');
  'FINSI';
  'SI' ('EXISTE' OPTIO 'INIT');
    CINIT='TEXT' 'INIT' (OPTIO.'INIT');
  'FINSI';
'FINSI';
*
* Lecture des parametres secondaires
*
LOK=VRAI;
'SI' ('NON' ('EXISTE' CRESI));
  CRESI=1.;
'FINSI';
NMO='DIMENSION' MDECO;
'SI' ('NON' ('EXISTE' CDECO));
  CDECO=PROG NMO*1.;
'SINON';
  NMOB='DIMENSION' CDECO;
  'SI' ('NON' (NMOB 'EGA' NMO));
    'MESSAGE' 'RECOMPOM:Nb de courbes en ondelette (MDECO) et';
    'MESSAGE' '         nb de ponderations (CDECO) incoherents';
    LOK=FAUX;
  'FINSI';
'FINSI';
*
* Travail sur le residu
*
'SI' LOK;
  MRESIX='EXTRAIRE' MRESI 'ABSC' 1;
  TIRESI='EXTRAIRE' MRESIX 1;
  TRESI=('EXTRAIRE' MRESIX ('DIMENSION' MRESIX))-TIRESI;
  NPT='ENTIER' ((TRESI/DTIME) + 1.E-5);
  TTEST=TRESI-(NPT*DTIME);
  'SI' (('ABS' TTEST) '>' (DTIME*1.E-4));
    'MESSAGE' 'RECOMPOM:Le pas de temps (DTIME) est incoherent avec la';
    'MESSAGE' '         duree du signal residu (MRESI)';
    LOK=FAUX;
  'SINON';
    LOK=VRAI;
  'FINSI';
'FINSI';
'SI' LOK;
  RESIX='PROG' TIRESI 'PAS' DTIME 'NPAS' NPT;
  RESIY='IPOL' RESIX MRESIX ('EXTRAIRE' MRESI 'ORDO' 1);
  RESIYY='PERT' RESIY 'SIGN' CAMPL CINIT;
  RESIY=RESIYY;
  RESI ='EVOL' 'MANU' RESIX (CRESI*RESIY);
'FINSI'
*
* Travail sur les modulation
*
'SI' LOK;
  DDTIME=DTIME;
  I=0;
  'REPETER' LABCO NMO;
    I=I+1;
    MDECOIX='EXTRAIRE' MDECO 'ABSC' I;
    TIDECOI='EXTRAIRE' MDECOIX 1;
    TDECOI=('EXTRAIRE' MDECOIX ('DIMENSION' MDECOIX))-TIDECOI;
    NPT='ENTIER' ((TDECOI/DDTIME) + 1.E-5);
    TTEST=TDECOI-(NPT*DDTIME);
    'SI' (('ABS' TTEST) '>' (DTIME*1.E-4));
     'MESSAGE'
           'RECOMPOM:Le pas de temps (DTIME) est incoherent avec la';
     'MESSAGE' '         duree du signal ondelette (MDECO) courbe' I;
      LOK=FAUX;
      'QUITTER' LABCO;
    'FINSI';
    'SI' (('ABS' (TIDECOI-TIRESI)) '>' (DTIME*1.E-4));
      'MESSAGE'
  'RECOMPOM:Instant initiaux non egaux entre le signal residu (MRESI)';
      'MESSAGE' '         et le signal ondelette (MDECO) courbe' I;
      LOK=FAUX;
      'QUITTER' LABCO;
    'FINSI';
    CDECOI='EXTRAIRE' CDECO I;
    DECOIX='PROG' TIDECOI 'PAS' DDTIME 'NPAS' NPT;
    DECOIY='IPOL' DECOIX MDECOIX ('EXTRAIRE' MDECO 'ORDO' I);
    DECOIYY='PERT' DECOIY 'SIGN' CAMPL CINIT;
    DECOIY=DECOIYY;
    DECOI='EVOL' 'MANU' DECOIX (CDECOI*DECOIY);
    'SI' (I 'EGA' 1);
      DECO=DECOI;
    'SINON';
      DECO=DECO 'ET' DECOI;
    'FINSI';
    DDTIME=DDTIME/2;
  'FIN' LABCO;
'FINSI';
*
'SI' LOK;
  'SI' LOPTION;
    M ACC=RECOMPOS RESI DECO OPTIO;
  'SINON';
    M ACC=RECOMPOS RESI DECO;
  'FINSI';
'FINSI';
*
'FINPROC' M ACC;
