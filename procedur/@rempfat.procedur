$$$$ @REMPFAT
* @REMPFAT  PROCEDUR  MO        97/11/28    21:16:39     2950
* @REMPFAT PROCEDURE                                 96/11/12   09:00:00
* ----------------------------------------------------------------------
******                     PROCEDURE @REMPFAT                     ******
* ----------------------------------------------------------------------
*
*   CETTE PROCEDURE PERMET DE REMPLIR LA TABLE POUR LA FATIGUE
*
* ----------------------------------------------------------------------
*
*   LES DONNEES SONT :
*    ----> la table a remplir
*    ----> le premier indice de la table a remplir :
*            - 1 : liste des nombres d'occurences (une seule liste et une
*                  seule occurence par etat)
*            - 2 : listes des caracteristiques des materiaux, une liste
*                  par coupe
*    ----> la liste a fournir siuvant l'indice
*
* ----------------------------------------------------------------------
*   PAR Joseph MANNINO
* ----------------------------------------------------------------------
'DEBPROC' @REMPFAT TABFATI*TABLE IND*ENTIER LISTF*LISTREEL ;

SI ( ( IND > 3 ) OU ( IND < 1 ) ) ;
  MESSAGE 'L"INDICE DE LA TABLE A REMPLIR POUR LA FATIGUE NE CONVIENT'
  ' PAS' ;
  QUITTER @REMPFAT ;
FINSI ;

SI ( ( DIME TABFATI ) NEG 0 ) ;
  TABIND = INDEX ( TABFATI ) ;
  NIND   = DIME TABIND ;
  J      = 0 ;
  REPETER BOUCLE ( NIND ) ;
    I = &BOUCLE ;
    SI ( TABIND.I EGA IND ) ;
      J = 1 ;
      QUITTER BOUCLE ;
    FINSI ;
  FIN BOUCLE ;
  SI ( J EGA 0 ) ;
    TABFATI.IND = TABLE ;
  FINSI ;
SINON ;
  TABFATI.IND = TABLE ;
FINSI ;

N = DIME TABFATI.IND ;
N = N + 1 ;

SI ( IND EGA 1 ) ;
  SI ( N > 1 ) ;
    MESSAGE 'L"INDICE DE LA TABLE A REMPLIR POUR LA FATIGUE NE CONVIENT'
    ' PAS' ;
    QUITTER @REMPFAT ;
  FINSI ;
  NBOCCUR = DIME LISTF ; UY =TABFATI.IND;
  REPETER BOUCLE  NBOCCUR  ;
    UY . &BOUCLE  = EXTRAIRE LISTF &BOUCLE ;
  FIN BOUCLE ;
FINSI ;

SI ( IND EGA 2 ) ;
  TABFATI.IND.N = LISTF ;
FINSI ;
SI ( IND EGA 3 ) ;
  TABFATI.IND.N = LISTF ;
FINSI ;
FINPROC ;

