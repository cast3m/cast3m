$$$$ TOPOFILT
* TOPOFILT  PROCEDUR  CB215821  18/03/22    21:15:02     9786

************************************************************************
** Procedure called by TOPOPTIM for filtering the sensitivity field
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** 2.0 2017/11/11
************************************************************************

DEBP TOPOFILT tab0*'TABLE' ;
    frate0 = tab0.'TAUX_FILTRAGE' ;
    SI (frate0 > 0) ;
        dc0 = tab0.'WTABLE'.'SENSIBILITE' ;
        x0 = tab0.'WTABLE'.'TOPOLOGIE' ;
        mecha0 = EXIS tab0.'WTABLE' 'MECANIQUE' ;
        SI mecha0 ;
            mod1 = tab0.'WTABLE'.'MECANIQUE'.'MODELE'.(1) ;
        SINO ;
            mod1 = tab0.'WTABLE'.'THERMIQUE'.'MODELE'.(1) ;
            mat1 = tab0.'WTABLE'.'THERMIQUE'.'CARACTERISTIQUES'.(1) ;
            volElT1 = tab0.'WTABLE'.'THERMIQUE'.'VOLUME'.(1) ;
        FINS ;
        zdiv0 = tab0.'ZERO_DIVISION' ;

        field0 = dc0 * x0 ;
        REPE loop1 frate0 ;
            field0 = CHAN 'CHAM' (CHAN 'CHPO' mod1 field0 'MOYE') mod1
                          'GRAVITE' ;
        FIN loop1 ;

*       In thermal mode, 'GRAVITE' option is not respected
*       This problem is solved by INTG 'ELEM'
        SI (NON mecha0) ;
            field0 = (INTG 'ELEM' field0 mod1 mat1) / volElT1 ;
        FINS ;

        dc0 =  field0 / (BORN x0 'SCAL' 'MINI' zdiv0) ;

*       Save the filtered sensibility
        tab0.'WTABLE'.'SENSIBILITE' = dc0 ;
    FINS ;
FINP ;

$$$$

