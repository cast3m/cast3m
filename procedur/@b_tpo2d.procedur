$$$$ @B_TPO2D
* @B_TPO2D  PROCEDUR  AM        93/10/29    21:15:17     959
*-------------------------------------------------
******          PROCEDURE @B_TPO2D          ******
*-------------------------------------------------
*
*     CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*   A DISPOSITION DE LA COMMUNAUTE  CASTEM2000
*      PAR Miguel A. Bretones UPC Barcelona
*
*-------------------------------------------------
 'DEBPROC' @B_TPO2D TENS1*MCHAML MOD1*MMODEL FESC/FLOTTANT ;
 IDIM = 'VALE' DIME ;
 'SI' (IDIM 'NEG' 2) ;
   'MESS' 'Esta procedure solo trabaja en 2 dimensiones' ;
   'QUIT' B_TPO2D ;
 'FINSI' ;
 MALL1 = 'EXTR' TENS1 'MAIL' ;
 TEPR1 = 'PRIN' TENS1 MOD1 ;
 SIG11 = 'EXCO' TEPR1 'SI11' 'SCAL' ;
 SIG22 = 'EXCO' TEPR1 'SI22' 'SCAL' ;
 TENS2 = 'CHAN' 'CHPO' TENS1 MOD1 ;
 SIG1 = 'CHAN' 'CHPO' SIG11 MOD1 ;
 SIG2 = 'CHAN' 'CHPO' SIG22 MOD1 ;
 SX = 'EXCO' TENS2 'SMXX' 'SCAL' ;
 SY = 'EXCO' TENS2 'SMYY' 'SCAL' ;
 TXY = 'EXCO' TENS2 'SMXY' 'SCAL' ;
 MALL2 = 'CHAN' MALL1 'POI1' ;
 NPUN = 'NBNO' MALL2 ;
 CHA1T1 = 'MANU' 'CHPO' (0. 0.) 2 'VX' 0. 'VY' 0. ;
 CHA1C1 = 'MANU' 'CHPO' (0. 0.) 2 'VX' 0. 'VY' 0. ;
 CHA2T1 = 'MANU' 'CHPO' (0. 0.) 2 'VX' 0. 'VY' 0. ;
 CHA2C1 = 'MANU' 'CHPO' (0. 0.) 2 'VX' 0. 'VY' 0. ;
 'DETR' TEPR1 ; 'DETR' SIG11 ; 'DETR' SIG22 ; 'DETR' TENS2 ;
 IPUN = 0 ;
 'REPETER' BUCL1 NPUN ;
   IPUN = IPUN + 1 ;
   APUN = MALL2 'POIN' IPUN ;
   SXP = 'EXTR' SX 'SCAL' APUN ;
   SYP = 'EXTR' SY 'SCAL' APUN ;
   TXYP = 'EXTR' TXY 'SCAL' APUN ;
   SIG1P = 'EXTR' SIG1 'SCAL' APUN ;
   SIG2P = 'EXTR' SIG2 'SCAL' APUN ;
   'SI' ((ABS ((SIG1P * SIG2P) - (SXP *SYP))) <EG 1.E-5) ;
     MESS 'Indefinicion del angulo en el punto' ipun ;
     ALFA = 0. ;
   'SINON' ;
     ALFA = (ATG(2 * TXYP / (SXP - SYP))) / 2. ;
   'FINSI' ;
   'SI' (SIG1P >EG 0) ;
     CHA1T1 = CHA1T1 ET ('MANU' 'CHPO' APUN 2
                      'VX' ((COS (ALFA)) * SIG1P)
                      'VY' ((SIN (ALFA)) * SIG1P)) ;
   'SINON' ;
     CHA1C1 = CHA1C1 ET ('MANU' 'CHPO' APUN 2
                      'VX' ((COS (ALFA)) * SIG1P)
                      'VY' ((SIN (ALFA)) * SIG1P)) ;
   'FINSI' ;

   'SI' (SIG2P >EG 0) ;
     CHA2T1 = CHA2T1 ET ('MANU' 'CHPO' APUN 2
                      'VX' ((COS (ALFA + 90)) * SIG2P)
                      'VY' ((SIN (ALFA + 90)) * SIG2P)) ;
   'SINON' ;
     CHA2C1 = CHA2C1 ET ('MANU' 'CHPO' APUN 2
                      'VX' ((COS (ALFA + 90)) * SIG2P)
                      'VY' ((SIN (ALFA + 90)) * SIG2P)) ;
   'FINSI' ;
 'FIN' BUCL1 ;
 CHA1T2 = (-1.) * CHA1T1 ;
 CHA1C2 = (-1.) * CHA1C1 ;
 CHA2T2 = (-1.) * CHA2T1 ;
 CHA2C2 = (-1.) * CHA2C1 ;
 FLAGESC = 0 ;
 'SI' (('NON' ('EXISTE' FESC)) 'OU' (FESC <EG 0)) ;
   'SI' ('NON' ('EXISTE' FESC)) ;
     FLAGESC = 1 ;
   'FINSI' ;
   PTOEXT = MALL2 'POIN' 'PROC' (500 500) ;
   TAMC = (((PTOEXT 'COOR' 1)**2.) + ((PTOEXT 'COOR' 2)**2.))**0.5 ;
   MAXT = MAXI ABS (PROG (MAXI ABS CHA1T1) (MAXI ABS CHA2T1)
                         (MAXI ABS CHA1C1) (MAXI ABS CHA2C1)) ;
   FESC = TAMC * 0.5 / MAXT ;
   MESS 'Factor de escala empleado' FESC ;
 'FINSI' ;
 'REPETER' BUCL2 ;
   VVEC1 = ('VECT' CHA1T1 FESC 'VX' 'VY' ROUG) ET
           ('VECT' CHA1T2 FESC 'VX' 'VY' ROUG) ;
   VVEC2 = ('VECT' CHA1C1 FESC 'VX' 'VY' VERT) ET
           ('VECT' CHA1C2 FESC 'VX' 'VY' VERT) ;
   VVEC3 = ('VECT' CHA2T1 FESC 'VX' 'VY' ROUG) ET
           ('VECT' CHA2T2 FESC 'VX' 'VY' ROUG) ;
   VVEC4 = ('VECT' CHA2C1 FESC 'VX' 'VY' VERT) ET
           ('VECT' CHA2C2 FESC 'VX' 'VY' VERT) ;
   'TITRE' 'Tensiones Principales S11 ' ;
   'TRAC' MALL1 (VVEC1 'ET' VVEC2) ;
   'TITRE' 'Tensiones Principales S22 ' ;
   'TRAC' MALL1 (VVEC3 'ET' VVEC4) ;
   'TITRE' 'Tensiones Principales' ;
   'TRAC' MALL1 (VVEC1 'ET' VVEC2 'ET' VVEC3 'ET' VVEC4) ;
   'SI' (FLAGESC > 0) ;
     'MESS' 'Desea cambiar la escala (S/N)' ;
     'OBTE' RESP*MOT ;
     'SI' ((LOG1 = 'EGA' RESP 'S') OU (LOG2 = 'EGA' RESP 's')) ;
       'MESS' 'Introduzca el nuevo factor de escala' ;
       'OBTE' FESC ;
     'SINON' ;
       'QUIT' BUCL2 ;
     'FINSI' ;
   'SINON' ;
     'QUIT' BUCL2 ;
   'FINSI' ;
 'FIN' BUCL2 ;
 'FINPROC' ;
