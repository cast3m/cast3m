$$$$ @GATTPAR
* @GATTPAR  PROCEDUR  JC220346  12/09/12    21:15:07     7501
'DEBP' @GATTPAR fichier*'MOT' ;
*
*
'OPTI' 'ACQUERIR' fichier ;
*
*-- DEBUT DE LECTURE PREMIERE PARTIE DU FICHIER de nom fichier
*
*-- IDEGE : degre du polynome Em(T) decrivant le module d'Young
'ACQU' BLABLA*'MOT' ;
'ACQU' IDEGE*'ENTIER' ;
'SI' ('<' IDEGE 0) ;
  'MESS' 'ERREUR: le degre du polynome relatif au module d Young' ;
  'MESS' 'est negatif !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
*-- LEE liste des coefficients du polynome relatif au module d'Young
*       du degre 0 au degre IDEGE
LEE='PROG' ;
'REPE' BLOC (IDEGE+1) ;
  'ACQU' EEI*'FLOTTANT' ;
  LEE = LEE 'ET' ('PROG' EEI) ;
'FIN' BLOC ;
*
*-- IDEGG : degre du polynome Gm(T) decrivant le module de cisaillement
'ACQU' BLABLA*'MOT' ;
'ACQU' IDEGG*'ENTIER' ;
'SI' ('<' IDEGG 0) ;
  'MESS'
    'ERREUR: le degre du polynome relatif au module de cisaillement' ;
  'MESS' 'est negatif !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
*-- LGG liste des coefficients du polynome relatif au module de
*       cisaillement du degre 0 au degre IDEGG
LGG='PROG' ;
'REPE' BLOC (IDEGG+1) ;
  'ACQU' GGI*'FLOTTANT' ;
  LGG = LGG 'ET' ('PROG' GGI) ;
'FIN' BLOC ;
*
*-- IDEGAI : degre du polynome Alpham(T) decrivant
*--          le coefficient de dilatation thermique pour T < Talpha
'ACQU' BLABLA*'MOT' ;
'ACQU' IDEGAI*'ENTIER' ;
'SI' ('<' IDEGAI 0) ;
  'MESS'
    'ERREUR: le degre du polynome relatif au coefficient de dilatation';
  'MESS' 'thermique est negatif !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
*-- LALI liste des coefficients du polynome relatif au coefficient de
*--      dilatation thermique du degre 0 au degre IDEGAI
LALI='PROG' ;
'REPE' BLOC (IDEGAI+1) ;
  'ACQU' ALII*'FLOTTANT' ;
  LALI = LALI 'ET' ('PROG' ALII) ;
'FIN' BLOC ;
*
*-- IDEGAS : degre du polynome Alpham(T) decrivant
*--          le coefficient de dilatation thermique pour T > Talpha
'ACQU' IDEGAS*'ENTIER' ;
'SI' ('<' IDEGAS 0) ;
  'MESS'
    'ERREUR: le degre du polynome relatif au coefficient de dilatation';
  'MESS' 'thermique est negatif !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
*-- LALS liste des coefficients du polynome relatif au coefficient de
*--      dilatation thermique du degre 0 au degre IDEGAS
LALS='PROG' ;
'REPE' BLOC (IDEGAS+1) ;
  'ACQU' ALIS*'FLOTTANT' ;
  LALS = LALS 'ET' ('PROG' ALIS) ;
'FIN' BLOC ;
*
*-- TALPHA : temperature de transition pour les deux polynomes decrivant
*--          le coefficient de dilatation thermique
*
'ACQU' TALPHA*'FLOTTANT' ;
*
'SI' ('<EG' TALPHA 0) ;
  'MESS'
    'ERREUR: la temperature de transition pour les deux polynomes' ;
  'MESS' 'decrivant le coefficient de dilatation thermique' ;
  'MESS' 'est negative ou nulle !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*-- FIN DE LECTURE PREMIERE PARTIE DU FICHIER de nom fichier
*
*-- Liste de temperatures (en Kelvin)
TK = 'PROG' 273.15 'PAS' 10. 2603.15 ;
*-- On insere Talpha dans TK
TK = TK 'ET' ('PROG' TALPHA) ;
TK = 'ORDO' TK ;
NTK = 'DIME' TK ;
L273 = 'PROG' NTK * 273.;
*-----------------------------
*-- Construction des ordonnees : partie temperature
OEET = 'PROG' NTK*('EXTR' LEE 1) ;
'SI' ('>' ('DIME' LEE) 1) ;
  'REPE' BLOC (('DIME' LEE) - 1) ;
    OEET = OEET + (('EXTR' LEE (&BLOC+1))*(TK**(&BLOC)));
  'FIN' BLOC ;
'FINS' ;
*
'MESS'
  'sur la plage de temperatures' ('MINI' TK) '< T(K) <' ('MAXI' TK) ;
'MESS' 'et pour une porosite nulle' ;
MIOEET = 'MINI' OEET ;
MAOEET = 'MAXI' OEET ;
'MESS' 'module d Young            compris entre' MIOEET 'et' MAOEET ;
'SI' ('<EG' MIOEET 0) ;
  'MESS' 'ERREUR : module d Young negatif ou nul !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
OGGT = 'PROG' NTK*('EXTR' LGG 1) ;
'SI' ('>' (DIME LGG) 1) ;
  'REPE' BLOC (('DIME' LGG) - 1) ;
    OGGT = OGGT + (('EXTR' LGG (&BLOC+1))*(TK**(&BLOC)));
  'FIN' BLOC ;
'FINS' ;
*
MIOGGT = 'MINI' OGGT ;
MAOGGT = 'MAXI' OGGT ;
'MESS' 'module de cisaillement    compris entre' MIOGGT 'et' MAOGGT ;
'SI' ('<EG' MIOGGT 0) ;
  'MESS' 'ERREUR : module de cisaillement negatif ou nul !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
OALIT = 'PROG' NTK*('EXTR' LALI 1) ;
'SI' ('>' (DIME LALI) 1) ;
  'REPE' BLOC (('DIME' LALI) - 1) ;
    OALIT = OALIT + (('EXTR' LALI (&BLOC+1))*(TK**(&BLOC)));
  'FIN' BLOC ;
  OALIT = OALIT / (TK - L273);
'FINS' ;
*
OALST = 'PROG' NTK*('EXTR' LALS 1) ;
'SI' ('>' (DIME LALS) 1) ;
  'REPE' BLOC (('DIME' LALS) - 1) ;
    OALST = OALST + (('EXTR' LALS (&BLOC+1))*(TK**(&BLOC)));
  'FIN' BLOC ;
  OALST = OALST / (TK - L273);
'FINS' ;
*
ALTI = IPOL TALPHA TK OALIT ;
ALTS = IPOL TALPHA TK OALST ;
*
NTKI = TK 'MASQ' 'INFERIEUR' TALPHA 'SOMM' ;
OALIT = 'EXTR' OALIT ('LECT' 1 'PAS' 1 NTKI) ;
OALST = 'EXTR' OALST ('LECT' (NTKI+1) 'PAS' 1 NTK) ;
OALT = OALIT 'ET' OALST ;
*
* On tronque ALPHA_moy en dessous de 293K
NT293 = TK 'MASQ' 'INFERIEUR' 293. 'SOMM';
XEALTT = 'EXTR' TK ('LECT' (NT293+1) 'PAS' 1 NTK) ;
YEALTT = 'EXTR' OALT ('LECT' (NT293+1) 'PAS' 1 NTK) ;
* On remplace la partie enlevée par une IPOL entre
* ALPHA_moy(T=0) et ALPHA_moy(T=293)
AL0 = ('EXTR' LALI 1) / (0. - 273);
AL293 =  (('EXTR' LALI 1) +
         (('EXTR' LALI 2)*293.) +
         (('EXTR' LALI 3)*293.*293.) +
         (('EXTR' LALI 4)*293.*293.*293.)) / (293. - 273.);
XEALTN = (PROG 0. 293.) ET XEALTT;
YEALTN = (PROG AL0 AL293) ET YEALTT;
*
MIOALT = 'MINI' YEALTN ;
MAOALT = 'MAXI' YEALTN ;
'MESS' 'coefficient de dilatation compris entre' MIOALT 'et' MAOALT ;
'SI' ('<' MIOALT 0) ;
  'MESS' 'ERREUR : coefficient de dilatation negatif !' ;
  'QUIT' @GATTPAR ;
'FINS' ;
*
ERRALT=100.*(ALTS-ALTI)/ ALTS ;
'SI' ('>' ERRALT 1.D-02) ;
  'MESS' 'ATTENTION : a la temperature de transition :' TALPHA 'K' ;
  'MESS' 'les deux valeurs du coefficient de dilatation thermique' ;
  'MESS' 'different de' ERRALT '%' ALTI ALTS ;
'FINS' ;
*
*-- Construction des evolutions obtenues pour une porosite nulle
EEET = 'EVOL' 'MANU' 'T' TK     'YOUN'  OEET   ;
EGGT = 'EVOL' 'MANU' 'T' TK     'G'     OGGT   ;
EALT = 'EVOL' 'MANU' 'T' XEALTN 'ALPH'  YEALTN ;
*
*-- DEBUT DE LECTURE SECONDE PARTIE DU FICHIER de nom fichier
*
'ACQU' BLABLA*'MOT' ;
'ACQU' PORY*'FLOTTANT' ;
'ACQU' BLABLA*'MOT' ;
'ACQU' PORG*'FLOTTANT' ;
'ACQU' BLABLA*'MOT' ;
'ACQU' PORA*'FLOTTANT' ;
*
*-- FIN DE LECTURE SECONDE PARTIE DU FICHIER de nom fichier
*
*-- Liste de porosites
PP = 'PROG' 0. 'PAS' 0.001 0.3 ;
NPP = 'DIME' PP ;
*-- Construction des ordonnees : partie porosite
OEEP = ('PROG' NPP*1.) - (PORY*PP) ;
MIOEEP = 'MINI' OEEP ;
'SI' ('<EG' MIOEEP 0) ;
  'MESS' 'ATTENTION !'
  'MESS' 'sur la plage de porosite :' ('MINI' PP) '< P <' ('MAXI' PP) ;
  'MESS ' 'module d Young negatif ou nul !' ;
'FINS' ;
*
OGGP = ('PROG' NPP*1.) - (PORG*PP) ;
MIOGGP = 'MINI' OGGP ;
'SI' ('<EG' MIOGGP 0) ;
  'MESS' 'ATTENTION !'
  'MESS' 'sur la plage de porosite :' ('MINI' PP) '< P <' ('MAXI' PP) ;
  'MESS ' 'module de cisaillement negatif ou nul !' ;
'FINS' ;
*
OALP = ('PROG' NPP*1.) - (PORA*PP) ;
MIOALP = 'MINI' OALP ;
'SI' ('<EG' MIOALP 0) ;
  'MESS' 'ATTENTION !'
  'MESS' 'sur la plage de porosite :' ('MINI' PP) '< P <' ('MAXI' PP) ;
  'MESS ' 'coefficient de dilatation negatif ou nul !' ;
'FINS' ;
*
*-- Construction des nuages
*
PP1 = 'EXTR' PP 1 ;
*
EE = 'NUAG' 'PORO'*'FLOTTANT' 'YOUN'*'EVOLUTION'
            ('EXTR' PP 1)     (EEET*('EXTR' OEEP 1)) ;
'REPE' BLOC (NPP-1) ;
  EEI = 'NUAG' 'PORO'*'FLOTTANT'     'YOUN'*'EVOLUTION'
               ('EXTR' PP (&BLOC+1)) (EEET*('EXTR' OEEP (&BLOC+1))) ;
  EE = EE 'ET' EEI ;
'FIN' BLOC ;
*
ETT1 = 'EVOL' 'MANU' 'T' TK 'NU' ('PROG' ('DIME'TK)*1.) ;
NU = 'NUAG' 'PORO'*'FLOTTANT' 'NU'*'EVOLUTION'
            ('EXTR' PP 1)
        ((-1.*ETT1)+(0.5*EEET*('EXTR' OEEP 1)/(EGGT*('EXTR' OGGP 1)))) ;
'REPE' BLOC (NPP-1) ;
  NUI = 'NUAG' 'PORO'*'FLOTTANT'     'NU'*'EVOLUTION'
               ('EXTR' PP (&BLOC+1))
        ((-1.*ETT1)+(0.5*(EEET*('EXTR' OEEP (&BLOC+1)))/
          (EGGT*('EXTR' OGGP (&BLOC+1))))) ;
  NU = NU 'ET' NUI ;
'FIN' BLOC ;
*
AL = 'NUAG' 'PORO'*'FLOTTANT' 'ALPH'*'EVOLUTION'
            ('EXTR' PP 1)     (EALT*('EXTR' OALP 1)) ;
'REPE' BLOC (NPP-1) ;
  ALI = 'NUAG' 'PORO'*'FLOTTANT'     'ALPH'*'EVOLUTION'
               ('EXTR' PP (&BLOC+1)) (EALT*('EXTR' OALP (&BLOC+1))) ;
  AL = AL 'ET' ALI ;
'FIN' BLOC ;
*
*-- Construction de la table contenant les informations
MATAB = 'TABL' ;
MATAB . 'YOUN' = EE ;
MATAB . 'NU'   = NU ;
MATAB . 'ALPH' = AL ;
*
*-- DEBUT DE LECTURE DERNIERE PARTIE DU FICHIER de nom fichier
*
  LCOMP = 'MOTS'  'RHO'   'R'     'DG0'   'DG'    'DGCR'
                  'K0'    'EXKP'  'KPAF'  'AP'    'BP'  'QP'
                  'K1'    'M1'    'Q1'    'N1'    'K2'  'M2'  'Q2'  'N2'
                  'OMEG'  'OMCR'  'Q'     'H'     'BETA'
                  'GAM1'  'EXTO'  'DYN1'  'DYN2'  'DYN3'
                  'K'     'A'     'Q3'    'N3'
                  'ADEN'  'KGON'  'POR0'  'BUMI'  'EFIS'
                  'CR1'   'CR2'   'CR3'   'CR';
*
'REPE' BLOC ('DIME' LCOMP) ;
  'ACQU' BLABLA*'MOT' ;
  'ACQU' XX*'FLOTTANT' ;
  MOTI = 'EXTR' LCOMP &BLOC ;
  MATAB . MOTI = XX ;
'FIN' BLOC ;
*
*-- Calcul de KP pour fluage primaire UO2
MATAB.'KPUO' = MATAB.'K0' * (MATAB.'DG' ** MATAB.'EXKP') ;
*-- Temps caracteristique TAU pour la fct de couplage dynamique UO2
MATAB.'TOUO' = MATAB.'GAM1' / (MATAB.'DG' ** MATAB.'EXTO') ;
*
*-- FIN DE LECTURE DERNIERE PARTIE DU FICHIER de nom fichier
*

'FINP' MATAB ;
$$$$


