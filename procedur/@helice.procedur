$$$$ @HELICE
* @HELICE   PROCEDUR  AM        92/06/11    21:15:09     592
*-------------------------------------------------
******          PROCEDURE @HELICE           ******
*-------------------------------------------------
*
*
*    CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*   A DISPOSOTION DE LA COMMUNAUTE  CASTEM2000
*    PAR M. LIBEYRE ( CEA/DSM/DRFC )
*
*     TEL : ( 33 1 ) 42 25 46 03
*
*-------------------------------------------------
*
******************************************************
*     PROCEDURE @HELICE DE MAILLAGE EN HELICE
******************************************************
* Syntaxe : GEO2 GEO3 = @HELICE GEO1 TYP1 P0 V0 PAS ALPHA NP
* en entree :
*   GEO1  : Base de l'helice (point ou maillage de type ligne
*           ou surface)
*   TYP1  : Type de la base (POIN,LIGN ou SURF)
*   PO    : Point de l'axe de l'helice
*   VO    : Vecteur directeur de l'axe de l'helice
*   PAS   : Pas de l'helice
*   ALPHA : Angle de rotation  de l'helice
*   NP    : Nombre d'elements de l'helice
*
* en sortie :
*   GEO2 : Sommet de l'helice (homeomorphe a GEO1)
*          (de type ligne, surface ou volume)
*   GEO3 : Maillage de la trajectoire de l'helice
*          (de type ligne, surface ou volume)
*******************************************************
DEBPROC @HELICE P1/POINT GEO1/MAILLAGE TYP1*MOT P0*POINT
V0*POINT PAS*FLOTTANT ALPHA*FLOTTANT NP*ENTIER ;
DALPHA = ALPHA / NP ;
DVT = (V0 / (NORM V0)) * (DALPHA / 360.) * PAS ;
*------------------------------------------------
*   CAS OU LA BASE EST UN POINT
*------------------------------------------------
SI (EGA TYP1 'POIN') ;
 PF1 = P1 ;
 IB = 0 ;
 REPETER BOUC1 NP ;
 IB = IB + 1 ;
 PI1 = PF1 ;
 PF1 = (PI1 PLUS DVT) TOUR DALPHA P0 (P0 PLUS V0) ;
 LIG1 = DROIT 1 PI1 PF1 ;
 SI (EGA IB 1) ;
  GEO3 = LIG1 ;
 SINON ;
  GEO3 = GEO3 ET LIG1 ;
 FINSI ;
 FIN BOUC1 ;
 GEO2 = PF1 ;
FINSI ;
*------------------------------------------------
*   CAS OU LA BASE EST UNE LIGNE
*------------------------------------------------
SI (EGA TYP1 'LIGN') ;
 LIGF1 = GEO1 ;
 IB = 0 ;
 REPETER BOUC2 NP ;
 IB = IB + 1 ;
 LIGI1 = LIGF1 ;
 LIGF1 = (LIGI1 PLUS DVT) TOUR DALPHA P0 (P0 PLUS V0) ;
* S1 = DALL1 1 LIGI1 LIGF1 ;
 S1 = LIGI1 REGLER 1 LIGF1 ;
 SI (EGA IB 1) ;
  GEO3 = S1 ;
 SINON ;
  GEO3 = GEO3 ET S1 ;
 FINSI ;
 FIN BOUC2  ;
 GEO2 = LIGF1 ;
FINSI ;
*------------------------------------------------
*   CAS OU LA BASE EST UNE SURFACE
*------------------------------------------------
SI (EGA TYP1 'SURF') ;
 SUF1 = GEO1 ;
 IB = 0 ;
 REPETER BOUC3 NP ;
 IB = IB + 1 ;
 SUI1 = SUF1 ;
 SUF1 = (SUI1 PLUS DVT) TOUR DALPHA P0 (P0 PLUS V0) ;
 V1 = SUI1 VOLU 1 SUF1 ;
 SI (EGA IB 1) ;
  GEO3 = V1 ;
 SINON ;
  GEO3 = GEO3 ET V1 ;
 FINSI ;
 FIN BOUC3  ;
 GEO2 = SUF1 ;
FINSI ;
FINPROC GEO2 GEO3;
