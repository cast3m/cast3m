$$$$ HTC_PER
* HTC_PER   PROCEDUR  AM        11/09/13    21:15:02     7126
*-----------------------------------------------------------------------
*23456789012345678901234567890123456789012345678901234567890123456789012
*-----------------------------------------------------------------------
*------------------------------------------------
* HTC_PER Permeability of heated concrete
* (after BAZANT and THONGUTHAI)
* Date: 16-03-98
*------------------------------------------------
'DEBP' HTC_PER T0*CHPOINT HR*CHPOINT TAV1*TABLE ;
*------------------------------------------------
*
 FLG1= TAV1.'FLG1';
'SI' FLG1; 'MESS' 'PROCEDURE-HTC_PER'; 'FINSI';
 TREF=25.;
 GR=9.80665;
 HC=.75;
 A0=TAV1. 'A0';
 TRK = 273.15 + TREF;
 F2T95='EXP' (2700. * ((368.15 - TRK)/(TRK * 368.15)));
 A0P=A0*F2T95;
*
 RESO1= T0 'MASQUE' 'EGINFE'    95.;
 RESO2= T0 'MASQUE' 'SUPERIEUR' 95.;
 RESO3= HR 'MASQUE' 'EGSUPE'    1. ;
 RESO4= HR 'MASQUE' 'INFERIEUR' 1. ;
 RESO5= T0 'MASQUE' 'EGSUPE'    TREF;
*
'SI' (('MINI' RESO1) > .9);
*  T0 TUTTO UGUALE SOTTO 95
*  ----> f2t
  TTT = 273.15 + T0 ;
  F2T = 'EXP' (RESO5 * 2700. * ((1. / TRK)-(TTT**-1)));
*  CASO MISTO T SOTTO 95 E  H SOTTO E SOPRA 1
*f1h dans le cas h >= 1
  F1HS  = RESO3;
*f1h dans le cas h <  1
  ALFAT = (1. + ((19./(95.-TREF))*(95. - T0)))**-1;
  F1HI  = RESO4 * (ALFAT + ((1.-ALFAT)/(1.+(((1.- HR)/(1.-HC))**4))));
  F1H   = F1HS + F1HI;
  AGDG=A0*F1H*F2T/GR;
*
 'DETR' TTT  ; 'DETR' ALFAT; 'DETR' F2T  ;
 'DETR' F1H  ; 'DETR' F1HS ; 'DETR' F1HI ;
 'DETR' RESO1; 'DETR' RESO2; 'DETR' RESO3;
 'DETR' RESO4; 'DETR' RESO5;
'SINON';
 'SI' (('MINI' RESO2) > .9);
* T0 TUTTO SOPRA 95
   T095=  T0 - 95. ;
   F3T = 'EXP' (T095/(0.881 + (0.214*T095)));
   AGDG=A0P*F3T/GR;
  'DETR' T095; 'DETR' F3T;
  'DETR' RESO1; 'DETR' RESO2; 'DETR' RESO3;
  'DETR' RESO4; 'DETR' RESO5;
 'SINON';
*T0 SOPRA E SOTTO 95
   T095= RESO2 * ( T0 - 95.);
   F3T = 'EXP' (T095/(0.881 + (0.214*T095)));
*f1h dans le cas h >= 1
   F1HS  = RESO3;
*f1h dans le cas h <  1
   ALFAT= (1. + ((19./(95.-TREF))*(95. - T0)))**-1;
   F1HI =RESO4 * (ALFAT + ((1.-ALFAT)/(1.+(((1.- HR)/(1.-HC))**4))));
   F1H  = F1HS + F1HI;
*  ----> f2t
   TTT = 273.15 + T0 ;
   F2T = 'EXP' (RESO5 * 2700. * ((1. / TRK)-(TTT**-1)));
   AMA95=RESO2*(A0P*F3T);
   AIE95=RESO1*(A0*F1H*F2T);
   AGDG = (AMA95 + AIE95)/ GR;
  'DETR' T095; 'DETR' F3T ;
  'DETR' TTT  ; 'DETR' ALFAT; 'DETR' F2T  ;
  'DETR' F1H  ; 'DETR' F1HS ; 'DETR' F1HI ;
  'DETR' AMA95; 'DETR' AIE95;
  'DETR' RESO1; 'DETR' RESO2; 'DETR' RESO3;
  'DETR' RESO4; 'DETR' RESO5;
 'FINSI';
'FINSI';
*-----------------------------------------------------------------------
'FINPROC' AGDG;

