$$$$ IN_MINI
* IN_MINI   PROCEDUR  JC220346  14/12/09    21:15:08     8314
  'DEBPROC' IN_MINI  FCAN*'MAILLAGE'   HS*'CHPOINT' ORIG*'POINT'
    B_ANTI*'TABLE' ;
************************************************************************
*   integration   de v  par minimisation  moindres carres
*   en entree   maillage frontiere cote phi et hs sur ce maiilage
*   en sortie   V(b)= phi(b)-psi(b)  chpoint sur fcan
*    avec psi(p1) = phi(p1)
************************************************************************
  HX='EXCO' HS BX ;HY='EXCO' HS BY ; HZ='EXCO' HS BZ ;
  nbi = 'NBNO' ( fcan 'ELEM' 1 ) ;
  I= 0 ;
  'MESS' ' SEPARATION ' (FCAN NBNO)  'POINTS'  (FCAN NBEL) 'ELEM ';
  IMENA = 0 ;
  MESS ' CALCUL DU RHS ' ;
  'REPE' BLOCALC (NBEL FCAN);
  imena = imena + 1 ;
  I= I + 1 ;
  SEGCOU = FCAN 'ELEM' I;
  P1 = SEGCOU 'POIN' 1;
  P2 = SEGCOU 'POIN' 2;
  HX1 = 'EXTR'  HX  'SCAL' P1 ; HX2 = 'EXTR'  HX 'SCAL'  P2 ;
  HY1 = 'EXTR'  HY  'SCAL' P1 ; HY2 = 'EXTR'  HY 'SCAL'  P2 ;
  HZ1 = 'EXTR'  HZ  'SCAL' P1 ; HZ2 = 'EXTR'  HZ 'SCAL'  P2 ;
  HMOY =(( HX1 +  HX2) / 2.) ( ( HY1 + HY2) / 2.) ((HZ1 + HZ2) / 2.);

  VL= P2 'MOINS' P1 ;DL= 'NORM' VL ;VL=VL / DL ;
  DV =  VL 'PSCA'  HMOY  ;DVI= DV * -1.;
  SEGCOU1 = CHAN 'POI1' SEGCOU ;
  HH= 'MANU' 'CHPO' SEGCOU1 1 'Q'  ('PROG'  DVI DV ) NATURE DISCRET ;
  'SI' ('EGA' I 1);
  HTH  = HH ;
  'SINON' ;
  HTH1= HTH 'ET' HH ;
 'DETR' HTH ;HTH= HTH1 ;  'DETR' HH;
  'FINSI' ;
   'SI' ('EGA' IMENA 100 ) ; 'MESS' ' menage' ;
 'MENAGE'  ;imena = 0 ; 'FINSI' ;
   'FIN' BLOCALC ;
********
    OB1 = FCAN MODE THERMIQUE ISOTROPE BARR ;
    MAT1 = MATE OB1 'K' 1. ;
    CAR1 = CARA  OB1  'SECT'  1. ;
    RIGT = CONDUC OB1 (MAT1 ET CAR1 ) ;
********
  III = 'INDEX' B_ANTI ;INI = 'DIME' III ;
     IZ = 0 ;
            'REPETER' BENS  INI ;
   IZ = IZ + 1 ;STN = B_ANTI.IZ;

   'SI' ('NON' ('EXISTE'  STN 'MTYP' )) ;

       'SI' ('EXISTE' STN  'IMPOSE' )  ;
 'MESS' ' APPLICATION POTENTIEL IMPOSE  '  ;
   TBB = 'BLOQUER' (EXTR (STN . 'IMPOSE') 'MAILLAGE' )  'T';
   FFF = DEPIMP   TBB  (STN.'IMPOSE') ;
   HTH = HTH ET FFF ;
   RIGT = RIGT ET TBB ;
       'SINON'  ;
*  ON A PASSE  DIRECTEMENT UN OBJET DE TYPE RAIDEUR
   RIGT = RIGT 'ET' ( STN.'COND'  ) ;
       'FINSI' ;

   'SINON' ;
 MMMM = STN.'MTYP' ;
       'SI' ( 'EGA' MMMM  'TBLOQ' ) ;
   GEOG =STN.'LGEO' ;
 'MESS' ' APPLICATION COND. LIM. CALCUL DU SAUT ' MMMM ;
   TBB =    'BLOQUER' 'T' GEOG   ; ;
   RIGT = RIGT 'ET' TBB                    ;

       'FINSI' ;
       'SI' ( 'EGA' MMMM  'RENSE' ) ;
   GEOG =STN.'LGEO' ;
'MESS' ' APPLICATION COND. LIM. CALCUL DU SAUT ' MMMM ;
   RIGT = RIGT 'ET' ( 'RELA' 'ENSE' 'T' GEOG ) ;
       'FINSI' ;
 RIGT = RIGT 'ET' ('BLOQUER' 'T' ORIG) ;
   'FINSI' ;
             'FIN' BENS ;

  HHHH= 'RESOU'  RIGT   HTH ;
  HHHH= ('ENLEVER' HHHH LX ) 'NOMC' 'SCAL';
  'FINPROC' HHHH ;



