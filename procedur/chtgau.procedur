$$$$ CHTGAU
* CHTGAU    PROCEDUR  DEGAY     95/05/11    21:15:14     1663
DEBPROC CHTGAU TAB1*TABLE;
********************************************************************
*  PROCEDURE CHTGAU : Calcul du champ de température créé par le   *
*              déplacement d'un arc de soudure gaussien            *
********************************************************************
*  Entrées : TABLE :                                               *
*            'PUISSANCE' : REEL : Puissance de l'arc (en W)        *
*            'RENDEMENT' : REEL : Rendement de l'arc : Rapport de  *
*               la puissance recue par la pièce et de la puissance *
*               de l'arc                                           *
*            'DIFFUSVITE' : REEL : Diffusivité thermique du        *
*               matériau (en m2/s)                                 *
*            'CONDUCTIVITE' : REEL : Conductivité thermique du     *
*               matériau (en W/Km2)                                *
*            'VITESSE' : REEL : Vitesse de déplacement de l'arc    *
*               (en m/s)                                           *
*            'T0' : REEL : Température ambiante (en °C ou en K)    *
*            'NTERMES' : ENTIER : Nombre de termes de la somme     *
*            'MAILLAGE' : MAILLAGE : Maillage support du champ de  *
*               température                                        *
*            'EPAISSEUR' : REEL : Epaisseur de la pièce (en m)     *
*            'LOCAL' : BOOLEEN : VRAI si la pièce est décrite dans *
*               le repère local à l'arc                            *
*            'INSTANT' : REEL : Si 'LOCAL' est FAUX, instant       *
*               auquel il faut calculer le champ de température    *
*               (l'abscisse de l'arc est alors V*t) (en s)         *
*            'GAUSS' : BOOLEEN : VRAI si la source est gaussienne  *
*            'ECART-TYPE' : REEL : Ecart-type de la gaussienne     *
*               (en m)                                             *
*  Sortie  : CHPOINT : Champ de température (en °C ou en K)        *
********************************************************************
OBJ  = TAB1.'MAILLAGE';
Q    = TAB1.'PUISSANCE';
ECT  = TAB1.'ECART-TYPE';
T0   = TAB1.'T0';
TAB1.'XPOS'      = 0.;
TAB1.'YPOS'      = 0.;
TAB1.'PUISSANCE' = 0.5*Q/3.497;
TAB1.'T0'        = 0.;
CHT              = ROSENT TAB1;
I = 0;
REPETER BOUCLE 6;
   I = I + 1;
   ANG1             = 30. + (I*60.);
   TAB1.'XPOS'      = 0.5*ECT*(COS ANG1);
   TAB1.'YPOS'      = 0.5*ECT*(SIN ANG1);
   TAB1.'PUISSANCE' = 0.3107*Q/3.497;
   CHT1             = ROSENT TAB1;
   CHT = CHT + CHT1;
FIN BOUCLE;
I = 0;
REPETER BOUCLE 6;
   I = I + 1;
   ANG1             = (I - 1)*60.;
   TAB1.'XPOS'      = ECT*(COS ANG1);
   TAB1.'YPOS'      = ECT*(SIN ANG1);
   TAB1.'PUISSANCE' = 0.1468*Q/3.497;
   CHT1             = ROSENT TAB1;
   CHT = CHT + CHT1;
FIN BOUCLE;
I = 0;
REPETER BOUCLE 6;
   I = I + 1;
   ANG1             = 30. + (I*60.);
   TAB1.'XPOS'      = 1.5*ECT*(COS ANG1);
   TAB1.'YPOS'      = 1.5*ECT*(SIN ANG1);
   TAB1.'PUISSANCE' = 0.0420*Q/3.497;
   CHT1             = ROSENT TAB1;
   CHT = CHT + CHT1;
FIN BOUCLE;
TAB1.'PUISSANCE' = Q;
TAB1.'T0' = T0;
CHT0 = OBJ MANU 'CHPO' 1 SCAL T0;
CHT = CHT + CHT0;
RESP CHT;
FINPROC;
