$$$$ KRESP
* KRESP     PROCEDUR  MAGN      17/02/24    21:15:19     9323
* ======================================================================
*
*     PROCEDURE DE PREPARATION DE LA MATRICE DE PRECONDITIONNEMENT
*                  AVANT L APPEL A KRES
*
* La procédure accepte des objets MATRIK ou des objets RIGIDITE
* On utilise KRES  si on a un objet MATRIK
* On utilise RESOU si on a un objet RIGIDITE
* ======================================================================
'DEBP' KRESP ;
 'ARGU' MAT1/'MATRIK' MAT2/'RIGIDITE' MOT1*'MOT' TAB1*'TABLE'
                                    MOT2*'MOT' CHP1*'CHPOINT'
                                    MOT3*'MOT' CHP2*'CHPOINT'
                                    MOT4*'MOT' ENT1*'ENTIER' ;

MRIG=VRAI                             ;
Si (EXIST MAT1) ; MRIG=FAUX; Finsi    ;
MRIK= NON MRIG                        ;

Si MRIK                               ;
'SI' ('>EG' TAB1 . 'TYPINV' 2)        ;
  lword1 = 'EXTR' MAT1 'COMP'         ;
  word1 = 'EXTR' lword1 1             ;

  'SI' ('NON' ('EXIS' TAB1 'TABRES')) ;
    TAB1 . 'TABRES' = 'TABLE'         ;
  'FINSI' ;

  'SI' (TAB1 . 'CALPREC')                       ;
    'SI' ('NON' ('EXIS' TAB1.'TABRES' word1))   ;
      TAB1 . 'TABRES' . word1 = 'TABLE'         ;
      TAB1 . 'TABRES' . word1 . 'MATASS' = MAT1 ;
      TAB1 . 'TABRES' . word1 . 'MAPREC' = MAT1 ;
      'MESS' 'On recalcule le preconditionneur' ;
    'SINON' ;
      TAB1 . 'TABRES' . word1 . 'MATASS' = MAT1 ;
      TAB1 . 'TABRES' . word1 . 'MAPREC' = MAT1 ;
      'MESS' 'On recalcule le preconditionneur' ;
    'FINSI' ;
  'FINSI' ;
  TAB1 . 'MATASS' = TAB1.'TABRES'.word1.'MATASS' ;
  TAB1 . 'MAPREC' = TAB1.'TABRES'.word1.'MAPREC' ;
'FINSI'                                          ;

 resi = 'KRES'  MAT1 MOT1 TAB1
                     MOT2 CHP1
                     MOT3 CHP2
                     MOT4 ENT1                   ;
FINSI                                            ;

Si MRIG                                          ;
*mess '===================> RESOU';
*mess '===================> RESOU';
*mess '===================> RESOU';
resi = 'RESOU' MAT2 (CHP1 et CHP2)               ;
FINSI                                            ;

'RESP' resi                                      ;

'FINP'                                           ;





