$$$$ VITEUNIL
* VITEUNIL  PROCEDUR  PV        16/07/21    21:15:03     9042
  'DEBPROC'   VITEUNIL  ROP*'RIGIDITE' M*'RIGIDITE' V*'CHPOINT'
 DX*'CHPOINT' DEPINI*'CHPOINT'  H*'FLOTTANT'  RESI*'CHPOINT'
PRECED*'TABLE';
* CORRECTION DE LA VITESSE EN CAS D'APPUI UNILATERAUX EN CAS DE DYNAMIQUE
* ROP   MATRICE (K '+' 4M/H2)
* M     MATRICE MASSE
* V VITESSE CALCULEE PAR L'ALGORITHME
* DX INCREMENT DE DPLACEMENT AU COURS DU PAS
* DEPINI DEPLACEMENT INITIAL DU PAS
* H PAS DE TEMPS ( ON POURRAIT S'EN PASSER)
* RESI  SECOND MEMEBRE INITIAL DU DÉBUT DU PAS ( CONTIEBNT LES JEUX INITIAUX)
*   EN SORTIE LA NOUVELLE VITESSE
*
  MTA= 'EXTRAIRE' ROP   'CONTACT';
           MDI = 'DIME' MTA;
          VAUX = V;
          'SI' ( 'EGA' MDI 0) ;  'QUIT' VITEUNIL; 'FINS';

  BLOT = MTA. 7 ;
  MLX = 'EXTR' BLOT 'MAILLAGE' ;
  BLOI= 'EXTR' MTA . 1  'RIGI' 'MULT' ;

  BLO = BLOI  'ET' BLOT;

NEWLAM=('EXCO' DX 'LX' 'NOID' ) 'EXTR' 'MAILLAGE';
'SI' ( NEWLAM 'EGA' 0) ;
* NEWLAM EST VIDE ((ALORS QUE OLDLAM NE L'EST PAS))
   'QUITTER' VITEUNIL;
'SINON';
   SUPPO = NEWLAM;
'FINSI';
'SI' ( 'NBEL' MLX 'EGA' 0) 'QUITTER' VITEUNIL;
'SINON';
SUPPO = SUPPO 'INTE' ( 'CHAN' 'POI1' MLX);
'FINSI';
*SI IMPPP; 'MESS' ' NOUVEAU SUPPO '; 'LIST' SUPPO;FINS;
FFLX = ('EXCO' RESI 'FLX' 'NOID' 'FLX')  'REDU' SUPPO;

LLX = ('EXCO' (DX '*' -1.)'LX' 'NOID' 'LX') 'REDU' SUPPO ;
*SI IMPPP; 'MESS' 'FFLX '; 'LIST' FFLX; 'MESS' ' LLX'; 'LIST' LLX;'FINSI';
ENIC = FFLX LLX '*' ( 'MOTS' 'FLX' ) ( 'MOTS' 'LX') ( 'MOTS' 'SCAL');
VGEN = BLO '*' V ;VGENFLX = ( VGEN 'EXCO' 'FLX' 'FLX' ) 'REDU' SUPPO;
MBLO = M 'ET' BLOI;
ENINUL = 'POINT' ENIC  'EGALE' 0.;
*SI IMPPP ; 'MESS' ' ENINUL ' ; 'LIST' ENINUL;'FINSI';
'SI' (( 'NBNO' ENINUL )  'NEG' 0) ;
  'SI' (( 'NBNO' ENINUL ) 'EGA' ( 'NBNO' SUPPO)) ;
    'QUITTER' VITEUNIL;
  'FINSI';
  SUPPO = 'DIFF' SUPPO ENINUL;
  ENIC  = 'REDU' ENIC  SUPPO ;
  LLX   = 'REDU' LLX   SUPPO ;
'FINSI';
*  TEST SI LA CORRECTION EST NECESSAIRE
  ENICR = ('RESU' ENIC) 'MAXI' 'ABS';
  ENCIN = 'XTMX' V M;
  'SI' (ENICR '<' (ENCIN '*' 1D-11)); 'QUITTER' VITEUNIL; 'FINSI';

LI0 = 'MANU' 'CHPO' SUPPO 1 'SCAL' -0.2 'NATURE' 'DIFFUS';
LI20= 'MANU' 'CHPO' SUPPO 1 'SCAL' -20. 'NATURE' 'DIFFUS';
LI1 = 'MANU' 'CHPO' SUPPO 1 'SCAL' 1.   'NATURE' 'DIFFUS';
ENIC2= ENIC '/' 2.;
VGENFLX = ( VGEN 'EXCO' 'FLX' 'FLX' ) 'REDU' SUPPO;
LLXSCAL= LLX  'NOMC' 'SCAL';
VGENLLX =(VGENFLX '*' LLXSCAL )'NOMC' 'SCAL';
*SI IMPPP; 'MESS' ' VGENLLX ' ; 'LIST' VGENLLX; 'FINSI';
H2SUR2 = H '*' H '*' 0.5;
ITE = 0;NEQ3= 'NBNO' SUPPO '*' 20;
'SI' ( NEQ3 '<' 500);
  NEQ3=500;
'FINSI';
RELAX   = 0.5;
UNMRELAX= 0.5;
PLUSGR  = -20.;
CC = 1;
AREF = 'MAXI' 'ABS' (V    'ENLE' 'LX');
*'MESS' 'AREF ' AREF; 'LIST' V;
'REPETER' NFOI ;  ITE = ITE '+' 1;

  FSECF= LLX '*' LI0 '*' BLO;
*'MESS' ' FSECF ' ; 'LIST' FSECF;
  REM = ('RESOU' MBLO  ( -1. '*' FSECF )   ) '*' -1. ;
*'MESS'  ' REM'; 'LIST' REM;
  CTE = ENIC2 '*' LI0;
  PREM = VGENLLX '*' H '*' LI0;
REMBLOR = (REM '*' BLO) 'REDU' SUPPO;
MREMBLOR = REMBLOR 'EXTR' 'MAIL';
'SI' ('EGA' ('TYPE' MREMBLOR) 'ENTIER'); 'QUIT' VITEUNIL; 'FINSI';
'SI' (('NBEL' MREMBLOR) 'EGA' 0);'QUIT' VITEUNIL; 'FINSI';
AZ= H2SUR2 '*' LLXSCAL;
  DEUXI =   H2SUR2 '*' LLXSCAL '*'
 REMBLOR 'NOMC' 'SCAL';

 LIBIS= 'RAVC' DEUXI PREM CTE;

*'MESS' ' SOLNE '  ; 'LIST' SOLNE;
'SI' (ITE '>' 50); SOLNE = (SOLNE '+' LIBIS)'*' 0.5;
'SINON' ; SOLNE = LIBIS ; 'FINSI';
*SI IMPPP; 'MESS' 'LIBIS';   'LIST' LIBIS;'FINSI';
 'SI' ( ('NBNO' SUPPO ) 'EGA' 1 ) ; 'QUIT' NFOI;'FINSI';
  'SI' (ITE '>' 1) ; AA = LI0 '-' SOLNE;
         'SI' ( AREF 'NEG' 0.) ;
               CC  = (AA / AREF ) 'MAXI' 'ABS' ;
          'SINON';
          'MESS' ' AREF NUL';
              CC = 1000.;
          'FINSI';
*                'MESS' ' ITE CC ' ITE CC ;
              'SI' ( CC '<' 1E-8 ) ;
*                 'MESS' ' SORTIE DE LA BOUCLE ITÉRATION ' ITE;
                 'QUITTER' NFOI;
             'FINSI';
  'FINSI';
  'SI' (( &NFOI '>' NEQ3) 'ET' (CC '>EG' CCOLD)) ;
    'MESS' ' PAS DE CONVERGENCE APRÉS ' NEQ3 'ITÉRATIONS DANS VITEUNIL';
    PRECED . 'RATE_VITEUNIL'= VRAI; 'QUITTER' VITEUNIL;
  'FINSI';
LI0 = SOLNE;
'FIN' NFOI;

*'MESS' ' SOLNE ' ; 'LIST' SOLNE;
LR2 = ((SOLNE '*'  LLX  '*' ROP  ) 'ENLEVER' 'FLX' ) '*' H;
*'MESS' ' AVANT RESOU';
DV2 = ('RESOU' MBLO  (-1. '*' LR2) )'*' -1. ;
VAUXNEP= V '+' DV2;
*'MESS' ' DV2 ' ; 'LIST' DV2;
*'MESS' ' VAUXNEP ' ; 'LIST' VAUXNEP;
VAUX = VAUXNEP 'ENLE' 'LX';
LISEA = MTA. 6;MALI = 'MAXI' LISEA;
'SI' ( 'EGA' MALI 1 ) ;
    MV =    M  '*' VAUX ;
    VP = 'RESOU'  ( M 'ET' BLO)  'NOUNIL' MV;
    V2 = ('ENLE' VP   'LX' )'*'  2.    '-' VAUX;
    VAUX = V2;
'FINSI';
*NNO= 'NBNO' SUPPO  / 2;
*'MESS' 'MODIFICATION VITESSES DUE AUX'  NNO  ' APPUIS UNILARÉRAUX';
* 'MESS'S ' VAUX   '; 'LIST' 'RESU'ME VAUX;
*VCINN=  'XTMX' M VAUX; ; VCINANC= ENCIN;
* 'MESS' 'ENER CIN AVANT '  VCINANC;
* 'MESS' 'ENER CIN APRES '  VCINN;
* 'MESS' ' ENER INJECTÉE PAR REACTIONS ' ('MAXI' ('RESU' ENIC));
'FINPROC' VAUX;
$$$$

