$$$$ @LAMAT
* @LAMAT    PROCEDUR  KK2000    99/11/30    21:15:16     3720
*
************************************************************************
*                        PROCEDURA @LAMAT
************************************************************************
*
* Descrizione : permette di ottenere, secondo il tipo di omogeneizzazion
*               richiesta:
*               - la matrice di Hooke omogeneizzata e le caratteristiche
*                 equivalenti (se TAB_LAM.'TIPO'='OMOG')
*               - gli oggetti di tipo MATERIAU e CARACTER relativi ad
*                 ogni lamina eccentrica (se TAB_LAM.'TIPO'='MLAY')
*
* Sintassi    : TAB_MAT = @LAMAT  TAB_LAM  NZONE ;
*
*
* Autore/Data : Miliozzi A.  10/04/1997
*
* Revisioni   : Miliozzi A.  3/06/1999 - estensione omog a coq4
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_LAM  Tavola delle caratteristiche del laminato
*
* NZONE    Entier che specifica il numero della zona sulla
*          quale effettuare il calcolo
*
*--- OUTPUT
*==========
*
* TAB_MAT   Tavola degli oggetti MATERIAU o MAHOOK e CARACTER per la
*           i-esima zona (indici MAT e CAR).
*
************************************************************************
*
DEBPROC @LAMAT  TL*TABLE NZONE*ENTIER ;
*
TT      = TL.NZONE;
TIP1    = TL.TIPO ;
OBJ     = TT.MOD1 ;
MET     = TT.METRIF;
TTM     = TL.MCOL ;
*
TM      = TABLE   ;
TM.TIPO = TIP1    ;
TM.MOD1 = OBJ     ;
TM.MAT  = TABLE   ;
*
*----------------------------------------------------------
* Controllo che non sia OMOG per elementi COQ6 COQ8
*
SI ( (EGA TIP1 'OMOG') ET ((EXIST OBJ ELEM COQ6) OU
                           (EXIST OBJ ELEM COQ8)) );
 MESS ' Error with OMOG parameter: ' ;
 MESS ' With the thick shell elements it is not possible';
 MESS ' to perform the calculation using a global HOOKE matrix';
 quitter @lamat ;
FINSI;
* ---------------------------------------------------------
*
NLAM = (DIME TT) - 6 ;
*
NN = 1 ;
REPETER LOOP1 NLAM ;
*
EP1 = TT.NN.EPA ; EX1 = TT.NN.EXC ;
AN1 = TT.NN.ANG ; ID1 = TT.NN.IDM ;
*
SI (EXIST (TT.NN) G13 ) ; G1 = TT.NN.G13  ; G2 = TT.NN.G23  ;
SINON ;                   G1 = TTM.ID1.G13; G2 = TTM.ID1.G23;
FINSI ;
*
*-----------------------------------------------------------
* Definizione del matriale per gusci spessi
*
SI ( (EXIST OBJ ELEM COQ4) OU (EXIST OBJ ELEM COQ6)
         OU (EXIST OBJ ELEM COQ8) OU (EXIST OBJ ELEM DST)) ;
  SI (EGA MET 'DIRE') ;
     MAT1 = MATE OBJ
            DIRECTION TT.DIRRIF  INCL AN1    TT.DIRNOR
            YG1  TTM.ID1.YG1  YG2 TTM.ID1.YG2
            NU12 TTM.ID1.NU12 G12 TTM.ID1.G12
            G23  G2           G13 G1
            RHO  TTM.ID1.RHO
            EPAI EP1        EXCE EX1  ;
  SINON ;
     MAT1 = MATE OBJ
            RADIAL TT.DIRRIF  INCL AN1    TT.DIRNOR
            YG1  TTM.ID1.YG1  YG2 TTM.ID1.YG2
            NU12 TTM.ID1.NU12 G12 TTM.ID1.G12
            G23  G2           G13 G1
            RHO  TTM.ID1.RHO
            EPAI EP1        EXCE EX1  ;
  FINSI ;
* ----------------------------------------------------------

SINON ;
  SI (EGA MET 'DIRE') ;
     MAT1 = MATE OBJ
            DIRECTION TT.DIRRIF  INCL AN1    TT.DIRNOR
            YG1  TTM.ID1.YG1  YG2 TTM.ID1.YG2
            NU12 TTM.ID1.NU12 G12 TTM.ID1.G12
            RHO  TTM.ID1.RHO
            EPAI EP1        EXCE EX1  ;
  SINON ;
     MAT1 = MATE OBJ
            RADIAL TT.DIRRIF  INCL AN1    TT.DIRNOR
            YG1  TTM.ID1.YG1  YG2 TTM.ID1.YG2
            NU12 TTM.ID1.NU12 G12 TTM.ID1.G12
            RHO  TTM.ID1.RHO
            EPAI EP1        EXCE EX1  ;
  FINSI ;
*
FINSI;
*
SI ( EGA TIP1 'OMOG')             ;
    SI (NN EGA 1)                 ;
      HOOT   = HOOKE OBJ MAT1 REFE;
      EPATOT = EP1                ;
    SINON                         ;
      HOO1   = HOOKE OBJ MAT1 REFE;
      HOOT = HOOT + HOO1          ;
      DETR HOO1                   ;
      EPATOT = EPATOT + EP1       ;
    FINSI                         ;
  SINON                           ;
    TM.MAT.NN = MAT1              ;
  FINSI                           ;
*
  NN = NN + 1 ;
*
FIN LOOP1 ;
*
SI ( EGA TIP1 'OMOG')             ;
 CAREQ     = CARA OBJ EPAI EPATOT ;
 TM.MAT.1  = HOOT                 ;
 TM.CAR    = TABLE                ;
 TM.CAR.1  = CAREQ                ;
FINSI                             ;
*
FINPROC TM                   ;
*
************************************************************************
*                       FINE PROCEDURA @LAMAT
************************************************************************
*


