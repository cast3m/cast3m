$$$$ CHAMINT
* CHAMINT   PROCEDUR  GBM       04/12/22    21:15:02     4973
*//////////////////////////////////////////////////////////////////
* integre le chargement sur le temps, sort un chargement
'DEBPROC'  CHAMINT CH1*'CHARGEMENT' ;
  ncha = 'DIME' CH1 ;
  xstart = -1.D15;
* boucle sur les sous chargements
  'REPETER' bouc ncha ;
*    on extrait la nieme evolution
     lstp = 'EXTRAIRE' CH1 'EVOL' &bouc ;
     px = 'EXTRAIRE'  lstp ABSC ;
     py = 'EXTRAIRE'  lstp ORDO ;
     xdeb = 'EXTRAIRE'  px 1 ;
     xfin = 'EXTRAIRE'  px ('DIME'  px) ;
     ydeb = 'EXTRAIRE'  py 1 ;
     yfin = 'EXTRAIRE'  py ('DIME'  py) ;
     n = 'DIME'  px ;
     xx0 =  xdeb ;
     yy0 = ydeb ;
     pry = 0.D0 ;
     p_prim = 'PROG'  pry ;
     chsp = 'EXTRAIRE' CH1 'CHAM' &bouc ;
     SI (maxi (abs chsp) > 1.D-30);
       i = 2 ;
       'REPETER' bb (n - 1) ;
        xx1 = 'EXTRAIRE' px i ;
        dx = xx1 - xx0 ;
        yy1 = 'EXTRAIRE' py i ;
*       integration trapeze
        pry = (yy1 + yy0) * dx * 0.5D0 + pry ;
        p_prim = p_prim 'ET' ('PROG' pry);
        'SI' ((('ABS' (yy1)) > 1.D-30) 'ET' (xx1 > xstart));
*           'MESSAGE' 'CEST INFERIEUR' yy1;
           xstart = xx1;
        'FINSI';
        xx0 = xx1 ;
        yy0 = yy1 ;
        i = i + 1 ;
        'FIN'  bb ;
*       On insere au nouveau chargement
        ev2 = 'EVOL'  manu px p_prim ;
        'SI' (&bouc 'EGA' 1) ;
           CH2 = 'CHARGEMENT' chsp ev2 ;
        'SINON' ;
           CH2 = CH2 'ET' ('CHARGEMENT' chsp ev2) ;
        'FINSI' ;
     SINON ;
        'SI' (&bouc 'EGA' 1) ;
           CH2 = 'CHARGEMENT' chsp lstp ;
        'SINON' ;
           CH2 = CH2 'ET' ('CHARGEMENT' chsp lstp) ;
        'FINSI' ;
     FINSI;
  'FIN' bouc;
*  mess 'SALUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUT' xstart;
'FINPROC' CH2 xstart;
*\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
$$$$

