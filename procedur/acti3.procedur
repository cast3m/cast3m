$$$$ ACTI3
* ACTI3     PROCEDUR  NOBODY    92/01/01    00:00:00     001
*  appele par INCREME pour extrapoler en utilisant une estimation
*  de la matrice tangente

'DEBPROC' ACTI3 CHD3*CHPOINT CHD2*CHPOINT CHD1*CHPOINT CHD0*CHPOINT
 FO3*CHPOINT FO2*CHPOINT FO1*CHPOINT FO0*CHPOINT ;

L = 0 ; M = 0 ; N = 0 ;

'REPETER' LABEUR 1;

LMT = 'MOTS' 'FX' 'FY' 'FZ' 'FR' 'FT' 'MX' 'MY' 'MZ' 'MR' 'MT';

F1_F0 = FO1 - FO0 ;  C0_C1 = CHD0 - CHD1 ;
F2_F0 = FO2 - FO0 ;  C0_C2 = CHD0 - CHD2 ;
F3_F0 = FO3 - FO0 ;  C0_C3 = CHD0 - CHD3 ;

F12 = 'XTX' F1_F0 ;   F22 = 'XTX' F2_F0;   F32 = 'XTX' F3_F0;

F1F2= F1_F0 'XTY' F2_F0 LMT LMT ;
F1F3= F1_F0 'XTY' F3_F0 LMT LMT ;
F2F3= F2_F0 'XTY' F3_F0 LMT LMT ;
F0F1=   FO0 'XTY' F1_F0 LMT LMT ;
F0F2=   FO0 'XTY' F2_F0 LMT LMT ;
F0F3=   FO0 'XTY' F3_F0 LMT LMT ;

A1=F12 ;  B1=F1F2 ;  C1=F1F3 ;                J1 = (-1)*F0F1;
D1=F1F2;  E1=F22  ;  F1=F2F3 ;                K1 = (-1)*F0F2;
G1=F1F3;  H1=F2F3 ;  I1=F32  ;                L1 = (-1)*F0F3;

'SI' (ABS(I1) < 1E-30) ; 'QUITTER' LABEUR; 'FINSI' ;

A2=A1-(G1*C1/I1); B2=B1-(H1*C1/I1);   J2=J1-(L1*C1/I1);
D2=D1-(G1*F1/I1); E2=E1-(H1*F1/I1);   K2=K1-(L1*F1/I1);

'SI' (ABS(E2) < 1E-30) ; 'QUITTER' LABEUR; 'FINSI' ;

A3=A2-(D2*B2/E2);   J3=J2-(K2*B2/E2);

'SI' (ABS(A3) < 1E-30) ; 'QUITTER' LABEUR; 'FINSI' ;

L=J3/A3;

B4=B2;      J4=J2-(A2*L);

'SI' (ABS(B4) < 1E-30) ; 'QUITTER' LABEUR; 'FINSI' ;

M=J4/B4;

C5=C1;   J5=J1-(A1*L)-(B1*M);

'SI' (ABS(C5) < 1E-30) ; 'QUITTER' LABEUR; 'FINSI' ;

N=J5/C5;

'FIN' LABEUR;

'MESS' 'coefficients acceleration ' (L*-1) (M*-1) (N*-1) ;

TEMP1 = L * C0_C1 ;
TEMP2 = M * C0_C2 ;
TEMP3 = N * C0_C3 ;
TEMP4 = TEMP1 + TEMP2 ;
RES = TEMP4 + TEMP3 ;

'DETRUIRE' TEMP1 ; 'DETRUIRE' TEMP2 ; 'DETRUIRE' TEMP3 ;
'DETRUIRE' TEMP4 ;
'DETRUIRE' F1_F0 ; 'DETRUIRE' F2_F0 ; 'DETRUIRE' F3_F0 ;
'DETRUIRE' C0_C1 ; 'DETRUIRE' C0_C2 ; 'DETRUIRE' C0_C3 ;

'FINPROC' RES ;
