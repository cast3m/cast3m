$$$$ REPIX
* REPIX     PROCEDUR  TTMF3     96/10/10    22:16:59     2322
'DEBPROC' REPIX   ;
'ARGU' RV*'TABLE' ;
*====================================================================
* REPIX enlève de la table créée par EQEX les objets MATRIK et MATRAK
*--------------------------------------------------------------------
* Les objets MATRIK indésirables se trouvent
* 1) à l'indice 'MATESI' des tables de sous types 'DOMAINE' stockées
* dans la table rangée à l'indice 'DOMINC';
* 2) à l'indice 'MATC' de la table stockée à l'indice 'PRESSION';
* 3) à l'indice 'MATESI' de la table stockée à l'indice 'DOMAINE';
* 4) à l'indice 'MATELM' des tables de sous type 'KIZX'.
*====================================================================
'SI' ('EXIS' RV 'SOUSTYPE' ) ;
   'SI' ('NEG' RV . 'SOUSTYPE' 'EQEX' ) ;
      'QUIT' REPIX ;
   'FINS' ;
'SINO' ;
   'QUIT' REPIX ;
'FINS' ;
 TAB1 = 'INDEX' RV ;
 NOP1 = 'DIME' TAB1 ;
'SI' ('EGA' NOP1 0) ;
   'QUIT' REPIX ;
'FINS' ;
*
*-----------------
'REPE' BOU1 NOP1 ;
*-----------------
*
    IND1 = MOT TAB1 . &BOU1 ;
*
*- Cas 1)
*
   'SI' ('EGA' IND1 'DOMINC' ) ;
      TAB2 = 'INDEX' ( RV . IND1 ) ;
      NOP2 = 'DIME' TAB2 ;
      'SI' ('NEG' NOP2 0) ;
         'REPE' BOU2 NOP2 ;
              IND2 = MOT TAB2 . &BOU2 ;
              'SI' ('NEG' IND2  'SOUSTYPE' ) ;
                 OBJ2 = RV . IND1 . IND2 ;
                 TYP2 = 'TYPE' OBJ2 ;
                 'SI' ('EGA' TYP2 'TABLE   ' ) ;
                    'SI' ('EXIS' OBJ2 'SOUSTYPE' ) ;
                       'SI' ('EGA' OBJ2 . 'SOUSTYPE' 'DOMAINE' ) ;
                          OBJ2 . 'MATESI' = 0 ;
                       'FINS' ;
                    'FINS' ;
                 'FINS' ;
              'FINS' ;
         'FIN' BOU2 ;
      'FINS' ;
       ITERER BOU1 ;
   'FINS' ;
*
*- Cas 2)
*
   'SI' ('EGA' IND1  'PRESSION' ) ;
       OBJ1 = RV . IND1 ;
       TYP1 = 'TYPE' OBJ1 ;
      'SI' ('EGA' TYP1 'TABLE   ' ) ;
         'SI' ('EXIS' OBJ1 'SOUSTYPE' ) ;
            'SI' ('EGA' OBJ1 . 'SOUSTYPE' 'EQPR' ) ;
               'SI' ('EXIS' OBJ1 'MATC' ) ;
                   OBJ1 . 'MATC' = 0 ;
               'FINS' ;
            'FINS' ;
         'FINS' ;
      'FINS' ;
       ITERER BOU1 ;
   'FINS' ;
*
*- Cas 3)
*
   'SI' ('EGA' IND1  'DOMAINE' ) ;
       OBJ1 = RV . IND1 ;
       TYP1 = 'TYPE' OBJ1 ;
      'SI' ('EGA' TYP1 'TABLE   ' ) ;
         'SI' ('EXIS' OBJ1 'SOUSTYPE' ) ;
            'SI' ('EGA' OBJ1 . 'SOUSTYPE' 'DOMAINE' ) ;
               'SI' ('EXIS' OBJ1 'MATESI' ) ;
                   OBJ1 . 'MATESI' = 0 ;
               'FINS' ;
            'FINS' ;
         'FINS' ;
      'FINS' ;
       ITERER BOU1 ;
   'FINS' ;
*
*- Cas 4)
*
   'SI' ('NEG' IND1  'SOUSTYPE' ) ;
       OBJ1 = RV . IND1 ;
       TYP1 = 'TYPE' OBJ1 ;
      'SI' ('EGA' TYP1 'TABLE   ' ) ;
         'SI' ('EXIS' OBJ1 'SOUSTYPE' ) ;
            'SI' ('EGA' OBJ1 . 'SOUSTYPE' 'KIZX' ) ;
                OBJ1 . 'MATELM' = 0 ;
            'FINS' ;
         'FINS' ;
      'FINS' ;
       ITERER BOU1 ;
   'FINS' ;
*
*-----------
'FIN' BOU1 ;
*-----------
*
'FINPROC' ;

