$$$$ MAILVORO
* MAILVORO  PROCEDUR  FD218221  15/12/17    21:15:12     8757
*----------------------------------------------------------------------*
* Procedure de maillage volumique d'une partition de voronoi           *
*----------------------------------------------------------------------*
*
* ENTREE :
*---------
*  - TAB1    : TABLE ISSUE DE L'OPERATEUR VORO
*  - ENV0    : ENVELOPPE DE LA PARTITION
*  - NBDIV0  : NOMBRE D'ELEMENTS CIBLE PAR DIAMETRE DE CELLULE
*  - COEF0   : PERMET DE CALCULER CRIT1 = COEF0 * HCEL
*              AVEC, POUR CHAQUE CELLULE :
*              - HCEL  := LE PAS DE MAILLAGE CIBLE CHAQUE CELLULE
*              - CRIT1 := LE CRITERE DE SELECTION DES PETITES ARETES
*
* SORTIE :
*---------
*  - MTRIA1 = TABLE DES DONNEES DU MAILLAGE VOLUMIQUE
*----------------------------------------------------------------------*
DEBP MAILVORO TAB1*'TABLE' ENV0*'MAILLAGE' NBDIV/'ENTIER'
              COEF1/'FLOTTANT' MO1/'MOT';
IDIM     = VALE 'DIME' ;
*======================================================================*
*                          Initialisation                              *
*======================================================================*
SI (NON(EXIS NBDIV));
 NBDIV = 4;
FINS;
*
SI (NON(EXIS COEF1));
 COEF1 = 1./3;
FINS;
*
TABI1   = INDE (TAB1 .CELL);
NBCEL0  = DIME TABI1;
IA = INDE (TAB1 .ARTS);
*
SI (IDIM EGA 3);
ARE0 = CHAN LIGN ENV0;
MENV0   = TRIA ENV0;
SINO;
ARE0 = ENV0;
MENV0   = SURF ENV0;
FINS;
*
VENV0   = MESU MENV0;
MCELREF =  (VENV0/(NBCEL0));
TDENSC  = TABLE;
TDENSS  = TABLE;
*
*======================================================================*
* Determiner la taille de maille cible des cellules                    *
*======================================================================*
*
REPE B0 (DIME TABI1);
 P0      = TABI1 . (&B0);
 OPTI 'ERRE' 'IGNO' ;
 MAILCEL = TRIA (CHAN POI1 (TAB1 .CELL.P0 .VISU));
 OPTI 'ERRE' 'NORM' ;
 SI (NON(EXIS MAILCEL));
  MCEL = MCELREF;
 SINO;
  MCEL    = MESU (MAILCEL);
 FINS;
*
 SI (MCEL > MCELREF);
 HCEL    = (MCEL**(1./IDIM)) / NBDIV;
 TDENSC . P0 = HCEL;
 SINO;
 TDENSC . P0 = ((MCELREF**(1./IDIM)) / NBDIV);
 FINS;
FIN B0;
*
*=====================================================================*
*                  Tables des correspondances
*=====================================================================*
SI (IDIM EGA 3);
* table de correspondance fac ---> cel
 TFC = TABLE;
 IC = INDE (TAB1.CELL);
 REPE B0 (DIME IC);
  P0 = IC . &B0;
  LFAC0 = TAB1.CELL.P0 .FACS;
  REPE B01 (DIME LFAC0);
   NFAC01 = EXTR LFAC0 &B01;
   SI (NON(EXIS TFC NFAC01));
    TFC . NFAC01 = MANU POI1 P0;
   SINO;
    SI (NON (DANS P0 (TFC.NFAC01)));
     TFC . NFAC01 = (TFC . NFAC01) ET P0;
    FINS;
   FINS;
  FIN B01;
 FIN B0;
* table correspondance art ---> fac et art ---> cel
 TAFC = TABL;
 IF = INDE (TAB1.FACS);
 REPE B0 (DIME IF);
  NFAC0 = IF . &B0;
  LART0 = TAB1.FACS.NFAC0 .ARTS;
  REPE B01 (DIME LART0);
   NART01 = EXTR LART0 &B01;
   SI (NON(EXIS TAFC NART01));
    TAFC . NART01 = TABL;
    TAFC . NART01 . 'FACS' = LECT;
    TAFC . NART01 . 'FACS' = TAFC . NART01 . 'FACS' ET NFAC0;
    TAFC . NART01 . 'CELL' = TFC . NFAC0;
   SINO;
    SI (NON(EXIS (TAFC . NART01 . 'FACS') NFAC0));
     TAFC . NART01 . 'FACS' = (TAFC . NART01 . 'FACS') ET NFAC0;
    FINS;
    REPE B001 (NBNO (TFC.NFAC0));
     P001 = (TFC.NFAC0) POIN &B001;
     SI (NON (DANS P001 (TAFC.NART01 .'CELL')));
      TAFC.NART01 .'CELL' = (TAFC.NART01 .'CELL') ET P001;
     FINS;
    FIN B001;
   FINS;
  FIN B01;
 FIN B0;
* Table correspondance sommet ---> art
 TSA = TABL;
 LARTINI = LECT;
 REPE B0 (DIME IA);
  NART0 = IA . &B0;
  LARTINI = LARTINI ET NART0;
  S1 = (TAB1. ARTS.NART0) POIN 1;
  S2 = (TAB1. ARTS.NART0) POIN 2;
  SMT = S1 ET S2;
  REPE B00 2;
   S0 = SMT POIN &B00;
   SI (NON(EXIS TSA S0));
    TSA . S0 = TABL;
    TSA . S0 . 'ARTS' = LECT;
    TSA . S0 . 'ARTS' = TSA . S0 . 'ARTS' ET NART0;
    TSA . S0 . 'FACS' = TAFC . NART0 . 'FACS';
    TSA . S0 . 'CELL' = TAFC . NART0 . 'CELL';
   SINO;
    SI (NON(EXIS (TSA.S0 .'ARTS') NART0));
     TSA . S0 . 'ARTS' = TSA . S0 . 'ARTS' ET NART0;
    FINS;
    REPE B01 (DIME (TAFC . NART0 . 'FACS'));
     NFAC01 = EXTR (TAFC . NART0 . 'FACS') &B01;
     SI (NON(EXIS (TSA . S0 . 'FACS') NFAC01));
      TSA . S0 . 'FACS' = (TSA . S0 . 'FACS') ET NFAC01;
     FINS;
    FIN B01;
*
    REPE B02 (NBNO (TAFC . NART0 . 'CELL'));
     P02 = (TAFC . NART0 . 'CELL') POIN &B02;
     SI (NON (DANS P02 (TSA . S0 . 'CELL')));
      TSA . S0 . 'CELL' = (TSA . S0 . 'CELL') ET P02;
     FINS;
    FIN B02;
   FINS;
  FIN B00;
 FIN B0;
*
FINS;
*
SI (IDIM EGA 2);
* table de correspondance ART ---> cel
TAC = TABLE;
IC = INDE (TAB1.CELL);
*
REPE B0 (DIME IC);
P0 = IC . &B0;
LART0 = TAB1.CELL.P0 .ARTS;
 REPE B01 (DIME LART0);
  NART01 = EXTR LART0 &B01;
  SI (NON(EXIS TAC NART01));
   TAC . NART01 = MANU POI1 P0;
  SINO;
   SI (NON (DANS P0 (TAC.NART01)));
   TAC . NART01 = (TAC . NART01) ET P0;
   FINS;
  FINS;
 FIN B01;
FIN B0;
* Table correspondance sommet ---> art
TSA = TABL;
LARTINI = LECT;
REPE B0 (DIME IA);
 NART0 = IA . &B0;
 LARTINI = LARTINI ET NART0;
 S1 = (TAB1. ARTS.NART0) POIN 1;
 S2 = (TAB1. ARTS.NART0) POIN 2;
 SMT = S1 ET S2;
 REPE B00 2;
  S0 = SMT POIN &B00;
  SI (NON(EXIS TSA S0));
   TSA . S0 = TABL;
   TSA . S0 . 'ARTS' = LECT;
   TSA . S0 . 'ARTS' = TSA . S0 . 'ARTS' ET NART0;
   TSA . S0 . 'CELL' = TAC . NART0;
  SINO;
*
   SI (NON(EXIS (TSA.S0 .'ARTS') NART0));
    TSA . S0 . 'ARTS' = TSA . S0 . 'ARTS' ET NART0;
   FINS;
*
   REPE B02 (NBNO (TAC . NART0));
    P02 = (TAC . NART0) POIN &B02;
    SI (NON (DANS P02 (TSA . S0 . 'CELL')));
     TSA . S0 . 'CELL' = (TSA . S0 . 'CELL') ET P02;
    FINS;
   FIN B02;
*
  FINS;
 FIN B00;
FIN B0;
FINS;
* Determiner les densites aux sommets
ITSA = INDE TSA;
REPE B0 (DIME TSA);
S0 = ITSA . &B0;
 REPE B00 (NBNO (TSA . S0 . 'CELL'));
  P02 = (TSA . S0 . 'CELL') POIN &B00;
  SI (NON(EXIS TDENSS S0));
   TDENSS . S0 = TDENSC . P02;
  SINON;
   SI ((TDENSC . P02) < (TDENSS . S0));
   TDENSS . S0 = TDENSC . P02;
   FINS;
  FINS;
 FIN B00;
FIN B0;
*======================================================================*
*                  ELIMINATION DES PETITES ARETES                      *
*======================================================================*
*
TAB2          = TABLE;
TAB2 . 'CELL' = TABLE;
TAB2 . 'FACS' = TABLE;
TAB2 . 'ARTS' = TABLE;
*
TSMT = TABLE;
TANT = TABLE;
*
NBARTS = DIME IA;
LART1  = LARTINI;
LARTNEW = LECT;
* Determiner les sommets sur l'enveloppe;
SMT0   = CHAN POI1 (TAB1.VISU);
SI (IDIM EGA 3);
SMTIN  = INCL SMT0 MENV0 'VOLU' 'STRI' 1E-400;
SINO;
SMTIN  = INCL SMT0 MENV0 'STRI' 1E-400;
FINS;
SMTOUT = DIFF SMT0 SMTIN;
OUBL SMT0; OUBL STMIN;
*
REPE B1 NBARTS;
*
 NART1 = IA . &B1;
 ART01 = TAB1 .'ARTS'.NART1;
* Verifier que ART111 n'est pas une arete de l'enveloppe
 TEST = ART01 INTE ARE0 NOVERIF;
 SI ((NBEL TEST) > 0);
  LARTNEW = LARTNEW ET NART1;
  OUBL TEST;
  ITER B1;
 FINS;
 OUBL TEST;
*
 S1 = ART01 POIN 1; S2 = ART01 POIN 2;
*
 SI (EXIS TSMT S1);
  SN1 = TSMT . S1;
  ANT1 = TANT . SN1;
 SINO;
  SN1 = S1;
  ANT1 = VIDE MAILLAGE/POI1;
 FINS;
*
 SI (EXIS TSMT S2);
  SN2 = TSMT . S2;
  ANT2 = TANT . SN2;
 SINO;
  SN2 = S2;
  ANT2 = VIDE MAILLAGE/POI1;
 FINS;
* Verifier que SN1 et SN2 ne sont pas confondus
 SI (SN1 EGA SN2 1.E-10);
  ITER B1;
 FINS;
*
 ART1 = D 1 SN1 SN2;
* Verifier que ART1 n'est pas une arete de l'enveloppe
 TEST = ART1 INTE ARE0 NOVERIF;
 SI ((NBEL TEST) > 0);
  SI (NON(EXIS LARTNEW NART1));
  LARTNEW = LARTNEW ET NART1;
  FINS;
  OUBL TEST;
  ITER B1;
 FINS;
 OUBL TEST;
*
 CRIT1 = MINI (((TDENSS . SN1)*COEF1) ET ((TDENSS . SN2)*COEF1));
 SI ((MESU ART1) < CRIT1);
* Determiner SM0 point de fusionnement de S1 et S2                  *
  SI ((DANS SN1 SMTOUT) OU (DANS SN2 SMTOUT));
   SI (IDIM EGA 3);
    ENV0A = ARET ENV0 1;
   SINO;
    ENV0A = ENV0;
   FINS;
   TBOR = 'TABLE';
   PILE1 = 1 ET 2;
*
   SI (IDIM EGA 3);
   'REPE' B2 ('NBEL' ENV0);
    BORDI = ENV0 'ELEM' &B2;
    BORDIM = BORDI HOMO 1.0000001 (BARY BORDI);
    BORDIP = 'CHAN' 'POI1' BORDI;
    BORDIA = 'CHAN' LIGN BORDI;
    BORDIA = BORDIA INTE ENV0A NOVERIF;
****
    PILE0 = PILE1;
    'REPE' B20 (DIME PILE0);
     S10 = ART1 POIN (EXTR PILE0 &B20);
* Test appartenance de S10 a l'element BORDI
     TEST1 = 'INCLU' (MANU POI1 S10) BORDIM 'LARG' 'NOID';
     'SI' ((NBNO TEST1) 'EGA' 1);
      N1    = ((BORDIP 'POIN' 1) 'MOIN' (BORDIP 'POIN' 3))
              PVEC ((BORDIP 'POIN' 2) 'MOIN' (BORDIP 'POIN' 3));
      PRJ1  = S10 'PROJ' 'CYLIN' N1
              'PLAN' (BORDIP POIN 1) (BORDIP POIN 2) (BORDIP 'POIN' 3);
      D1 = 'NORME' (PRJ1 'MOINS' S10);
      'SI' (D1 < 1.E-10);
       TEST3 = (MANU POI1 S10) INTE (CHAN POI1 BORDI) NOVERIF;
* Test coincidence S10 avec sommets enveloppe                          *
       'SI' ((NBNO TEST3) EGA 1);
        TBOR . (EXTR PILE0 &B20) = TEST3;
        OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
        PILE1 = ENLE PILE1 OUNUM;
        'SI' ((DIME PILE1) EGA 0);
         QUIT B2;
        'SINO';
         ITER B20;
        'FINS';
       'FINS';
* Test appartenance S10 a une des aretes "vives" du plan considere     *
       SI ((NBEL BORDIA) > 0);
       'REPE' B200 (NBEL BORDIA);
         A100 = BORDIA ELEM &B200;
         V1 = (A100 POIN 1) MOIN S10; V2 = (A100 POIN 2) MOIN S10;
         TEST5 = (V1 PSCA V2)/((NORM V1)*(NORM V2));
         'SI' ((ABS(TEST5 + 1.0)) < 1E-10);
          TBOR . (EXTR PILE0 &B20) = A100;
          OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
          PILE1 = ENLE PILE1 OUNUM;
          'SI' ((DIME PILE1) EGA 0);
            QUIT B2;
          'SINO';
            ITER B20;
          'FINS';
         'FINS';
       'FIN' B200;
       FINS;
* Les Tests precedents sont negatifs ==> S10 dans l'element BORDI      *
       TBOR . (EXTR PILE0 &B20) = BORDI;
       OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
       PILE1 = ENLE PILE1 OUNUM;
       'SI' ((DIME PILE1) EGA 0);
        QUIT B2;
       'SINO';
        ITER B20;
       'FINS';
      'FINS';
     'FINS';
 'FIN' B20;
**
'FIN' B2;
FINS;
*
SI(EGA IDIM 2);
'REPE' B2 ('NBEL' ENV0);
  BORDI = ENV0 'ELEM' &B2;
  PB1 = BORDI POIN 1;PB2 = BORDI POIN 2;
  PILE0 = PILE1;
  'REPE' B20 (DIME PILE0);
    S10 = ART1 POIN (EXTR PILE0 &B20);
* Test coincidence S10 avec sommets enveloppe                          *
    SI (EGA S10 PB1 1E-10);
        TBOR . (EXTR PILE0 &B20) = MANU POI1 S10;
        OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
        PILE1 = ENLE PILE1 OUNUM;
        'SI' ((DIME PILE1) EGA 0);
         QUIT B2;
        'SINO';
         ITER B20;
        'FINS';
    FINS;
*
    SI (EGA S10 PB2 1E-10);
        TBOR . (EXTR PILE0 &B20) = MANU POI1 S10;
        OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
        PILE1 = ENLE PILE1 OUNUM;
        'SI' ((DIME PILE1) EGA 0);
         QUIT B2;
        'SINO';
         ITER B20;
        'FINS';
    FINS;
*
    V1 = PB1 MOIN S10; V2 = PB2 MOIN S10;
* Test appartenance de S10 a l'element BORDI
    TEST5 = (V1 PSCA V2)/((NORM V1)*(NORM V2));
     'SI' ((ABS(TEST5 + 1.0)) < (1E-04));
       TBOR . (EXTR PILE0 &B20) = BORDI;
       OUNUM = POSI (EXTR PILE0 &B20) DANS PILE1;
       PILE1 = ENLE PILE1 OUNUM;
       'SI' ((DIME PILE1) EGA 0);
        QUIT B2;
       'SINO';
        ITER B20;
       'FINS';
     FINS;
 FIN B20;
FIN B2;
FINS;
*
   REPE QIT0 1;
*
     SI ((DIME TBOR) EGA 1);
       SM0 = ART1 POIN ((INDE TBOR) . 1);
       SMTOUT = SMTOUT ET SM0;
       QUIT QIT0;
     FINS;
*
     SI ((DIME TBOR) EGA 2);
       INFO1 = NBNO (TBOR . 1);
       INFO2 = NBNO (TBOR . 2);
*
       SI (INFO1 EGA INFO2);
        SI (info1 EGA 3);
         SI (EGA (TBOR . 1) (TBOR . 2));
          SM0 = 0.5*(SN1 'PLUS' SN2);
          SMTOUT = SMTOUT ET SM0;
          QUIT QIT0;
         SINO;
          CONT1 = CHAN POI1 (TBOR . 1);
          CONT2 = CHAN POI1 (TBOR . 2);
          INTC12 = CONT1 INTE CONT2;
          SI ((NBNO INTC12) EGA 2);
* Determination de la projection de S1 sur INTC12
           PARK1 = INTC12 'POIN' 1;
           PARK2 = INTC12 'POIN' 2;
           PARK12 = PARK2 'MOIN' PARK1;
           NPARK12 = 'NORME' PARK12;
           VAL1  = (S1 'MOIN' PARK1) 'PSCAL' PARK12;
           VAL1 = VAL1 '/' ((NPARK12)**2);
           SM0 = PARK1 'PLUS' (VAL1 '*' (PARK12));
           SMTOUT = SMTOUT ET SM0;
           QUIT QIT0;
          SINO;
           SI ((NBNO INTC12) EGA 1);
            SM0 = INTC12 POIN 1;
            QUIT QIT0;
           SINO;
            ERREUR 5;
           FINS;
         FINS;
         FINS;
        SINO;
         TEST = (CHAN POI1 (TBOR . 1)) INTE (CHAN POI1 (TBOR . 2));
         SI ((NBNO TEST) EGA 2);
          SM0 = 0.5*(SN1 'PLUS' SN2);
          SMTOUT = SMTOUT ET SM0;
          QUIT QIT0;
         SINO;
          SM0 = TEST POIN 1;
          QUIT QIT0;
         FINS;
*
        FINS;
       FINS;
*
       'SI' (INFO1 < INFO2);
         SM0 = SN1;
         SMTOUT = SMTOUT ET SM0;
         QUIT QIT0;
       'SINO';
          SM0 = SN2;
          SMTOUT = SMTOUT ET SM0;
         QUIT QIT0;
       'FINS';
     'FINS';
   'FIN' QIT0;
   SINO;
       SM0 = 0.5*(SN1 'PLUS' SN2);
   FINS;
*
  SI (EXIS TSMT SM0);
   SM0 = TSMT.SM0;
  FINS;
*
  SI (EXIS TSMT S1);
   REPE B11 (NBEL ANT1);
    TSMT . (ANT1 POIN &B11) = SM0;
   FIN B11;
  FINS;
*
  SI (EXIS TSMT S2);
   REPE B11 (NBEL ANT2);
    TSMT . (ANT2 POIN &B11) = SM0;
   FIN B11;
  FINS;
*
   TSMT . S1 = SM0; TSMT . S2 = SM0;
   TSMT. SN1 = SM0; TSMT. SN2 = SM0;
   TANT . SM0 = (S1 ET S2 ET ANT1 ET ANT2 ET SN1 ET SN2);
*
  TDENSS . SM0 = CRIT1/COEF1;
 SINO;
  LARTNEW = LARTNEW ET NART1;
 FINS;
MENAGE;
FIN B1;
*
* Construir la nouvelle table des aretes
REPE B2 (DIME LARTNEW);
 NART2 = EXTR LARTNEW &B2;
 ART2  = TAB1 .'ARTS'.NART2;
 S1 = ART2 POIN 1; S2 = ART2 POIN 2;
 SI (EXIS TSMT S1);
  SF1 = TSMT . S1;
 SINO;
  SF1 = S1;
 FINS;
 SI (EXIS TSMT S2);
  SF2 = TSMT . S2;
 SINO;
  SF2 = S2;
 FINS;
*
 SI(NON(SF1 EGA SF2 1.E-10));
 TAB2 . 'ARTS'. NART2 = D 1 SF1 SF2;
* MISE Ã€ JOUR DES DENSITES
 DIMA = MESU (TAB2 . 'ARTS'. NART2);
 TDENSS.SF1 = MINI((TDENSS.SF1) ET (1.3*DIMA));
 TDENSS.SF2 = MINI((TDENSS.SF2) ET (1.3*DIMA));
 FINS;
FIN B2;
*
SI (IDIM EGA 3);
* Creer la nouvelle table des faces
SAVEARTB = TABLE;
ITF = INDE (TAB1 . 'FACS');
 REPE B3 (DIME ITF);
  NFAC3 = ITF . &B3;
  LART3 = TAB1 . 'FACS' . NFAC3 . 'ARTS';
  LARTN = LECT;
  VISUN = 'VIDE' 'MAILLAGE'/SEG2;
*
  REPE B31 (DIME LART3);
   NART31 = EXTR LART3 &B31;
   SI (EXIS (TAB2 .'ARTS') NART31);
    LARTN = LARTN ET NART31;
    VISUN = VISUN ET (TAB2 . 'ARTS' .NART31);
   FINS;
  FIN B31;
*
  SI (DIME LARTN < 2);
   ITER B3;
  FINS;
*
  SI ((DIME LARTN) EGA 2);
   NARTA = EXTR LARTN 1; NARTB = EXTR LARTN 2;
   SAVEARTB . NARTB = NARTA;
   ITER B3;
  FINS;
   TAB2 . 'FACS' . NFAC3 = TABLE;
   TAB2 . 'FACS' . NFAC3 . 'VISU' = VISUN;
   TAB2 . 'FACS' . NFAC3 . 'ARTS' = LARTN;

 FIN B3;
* Remplacer NARTB par NARTA dans les faces concernees
SI ((DIME SAVEARTB) > 0);
ISARTB = INDE SAVEARTB;
REPE B4 (DIME SAVEARTB);
 NART4 = ISARTB . &B4;
 LFAC4 = TAFC . NART4 . 'FACS';
 REPE B41 (DIME LFAC4);
  NFAC41 = EXTR LFAC4 &B41;
  SI (EXIS (TAB2 . 'FACS') NFAC41);
   POSB = POSI NART4 DANS (TAB2 . 'FACS'. NFAC41.'ARTS');
   SI (POSB > 0);
*
    NARTB = NART4;
    REPE B411;
    NARTA = SAVEARTB.NARTB;
    SI (EXIS SAVEARTB NARTA);
     NARTB = NARTA;
     ITER B411;
    SINO;
     QUIT B411;
    FINS;
    FIN B411;
*
    SI(NON(EXIS (TAB2 . 'FACS'. NFAC41.'ARTS') NARTA));
      REMP (TAB2 . 'FACS'. NFAC41.'ARTS') POSB NARTA;
    SINO;
      TAB2 . 'FACS'. NFAC41.'ARTS' = ENLE (TAB2 . 'FACS'. NFAC41.'ARTS')
                                     POSB;
    FINS;
*
   FINS;
  FINS;
 FIN B41;
FIN B4;
*
* eliminer les NARTB de tab2;
 REPE B40 (DIME SAVEARTB);
  TAB2 . 'ARTS' = ENLE (TAB2 . 'ARTS') (ISARTB . &B40);
 FIN B40;
FINS;
* Creer les indices VISU des faces
 ITFN = INDE (TAB2 . 'FACS');
 REPE B5 (DIME ITFN);
  NFAC5 = ITFN . &B5;
  LART5 = TAB2 . 'FACS' . NFAC5 . 'ARTS';
  TAB2 . 'FACS' . NFAC5 . 'VISU' = VIDE MAILLAGE/SEG2;
  REPE B51 (DIME LART5);
   NART51 = EXTR LART5 &B51;
   TAB2 . 'FACS' . NFAC5 . 'VISU' = (TAB2 . 'FACS' . NFAC5 . 'VISU')
                                  ET (TAB2 .'ARTS'.NART51);
  FIN B51;
 FIN B5;
* Creer la nouvelle table des cellules
ITC = INDE (TAB1 . 'CELL');
SAVEFACB = TABLE;
REPE B6 (DIME ITC);
 P6 = ITC . &B6;
 LFAC6 = TAB1 .'CELL'.P6 . 'FACS';
 LFACN = LECT;
*
  REPE B61 (DIME LFAC6);
   NFAC61 = EXTR LFAC6 &B61;
   SI (EXIS (TAB2 .'FACS') NFAC61);
    LFACN = LFACN ET NFAC61;
   FINS;
  FIN B61;
*
  SI (DIME LFACN < 2);
   MESS 'NB FACS' (DIME LFACN);
   ITER B6;
  FINS;
*
  SI ((DIME LFACN) EGA 2);
   NFACA = EXTR LFACN 1; NFACB = EXTR LFACN 2;
   SAVEFACB . NFACB = NFACA;
   TAB2 . 'FACS' = ENLE (TAB2 .'FACS') NFACB;
   ITER B6;
  FINS;
   TAB2 . 'CELL' . P6 = TABLE;
   TAB2 . 'CELL' . P6 . 'FACS' = LFACN;
   TAB2 . 'CELL'  .P6 . 'VOIS' = TAB1 . 'CELL'  .P6 . 'VOIS';
* Creer l indice VISU de la cellule
  CVISU = 'VIDE' 'MAILLAGE'/'SEG2';
  REPE B62 ('DIME' LFACN);
   NFAC62 = EXTR LFACN &B62;
   FAC62 = TAB2.FACS.NFAC62 .VISU;
   CVISU = ((CVISU ET FAC62) DIFF (CVISU INTE FAC62 noverif));
  FIN B62;
   TAB2 . 'CELL' . P6 . 'VISU' = CVISU;
*
FIN B6;
* Remplacer NFACB par NFACA dans les cellules concernees
SI ((DIME SAVEFACB) > 0);
ISFACB = INDE SAVEFACB;
REPE B7 (DIME SAVEFACB);
 NFAC7 = ISFACB . &B7;
 LCEL7 = TFC . NFAC7;
 REPE B71 (NBNO LCEL7);
  P71 = LCEL7 POIN &B71;
  SI (EXIS (TAB2 . 'CELL') P71);
   POSB = POSI NFAC7 DANS (TAB2 . 'CELL'.P71 .'FACS');
   SI (POSB > 0);
    SI (EXIS SAVEFACB (SAVEFACB.NFAC7));
     REMP (TAB2 . 'CELL'. P71 .'FACS')
     POSB (SAVEFACB. (SAVEFACB .NFAC7));
    SINO;
     REMP (TAB2 . 'CELL'. P71 .'FACS') POSB (SAVEFACB.NFAC7);
    FINS;
   FINS;
  FINS;
 FIN B71;
FIN B7;
FINS;
*
SINO;
* Creer la nouvelle table des cellules
ITC = INDE (TAB1 . 'CELL');
SAVEARTB = TABLE;
REPE B6 (DIME ITC);
 P6 = ITC . &B6;
 LART6 = TAB1 .'CELL'.P6 . 'ARTS';
 LARTN = LECT;
*
  REPE B61 (DIME LART6);
   NART61 = EXTR LART6 &B61;
   SI (EXIS (TAB2 .'ARTS') NART61);
    LARTN = LARTN ET NART61;
   FINS;
  FIN B61;
*
  SI (DIME LARTN < 2);
   ITER B6;
  FINS;
*
  SI ((DIME LARTN) EGA 2);
   NARTA = EXTR LARTN 1; NARTB = EXTR LARTN 2;
   SAVEARTB . NARTB = NARTA;
*   TAB2 . 'ARTS' = ENLE (TAB2 .'ARTS') NARTB;
   ITER B6;
  FINS;
   TAB2 . 'CELL' . P6 = TABLE;
   TAB2 . 'CELL' . P6 . 'ARTS' = LARTN;
   TAB2 . 'CELL'  .P6 . 'VOIS' = TAB1 . 'CELL'  .P6 . 'VOIS';
* Creer l indice VISU de la cellule
  CVISU = 'VIDE' 'MAILLAGE'/'SEG2';
  REPE B62 ('DIME' LARTN);
   NART62 = EXTR LARTN &B62;
   ART62 = TAB2.ARTS.NART62;
   CVISU = ((CVISU ET ART62) DIFF (CVISU INTE ART62 noverif));
  FIN B62;
   TAB2 . 'CELL' . P6 . 'VISU' = CVISU;
*
FIN B6;
* Remplacer NARTB par NARTA dans les cellules concernees
SI ((DIME SAVEARTB) > 0);
ISARTB = INDE SAVEARTB;
REPE B7 (DIME SAVEARTB);
 NART7 = ISARTB . &B7;
 LCEL7 = TAC . NART7;
 REPE B71 (NBNO LCEL7);
  P71 = LCEL7 POIN &B71;
  SI (EXIS (TAB2 . 'CELL') P71);
   POSB = POSI NART7 DANS (TAB2 . 'CELL'.P71 .'ARTS');
   SI (POSB > 0);
    SI (EXIS SAVEARTB (SAVEARTB.NART7));
     REMP (TAB2 . 'CELL'. P71 .'ARTS')
     POSB (SAVEARTB. (SAVEARTB .NART7));
    SINO;
     REMP (TAB2 . 'CELL'. P71 .'ARTS') POSB (SAVEARTB.NART7);
     LISTE (TAB2 . 'CELL'. P71 .'ARTS');
    FINS;
   FINS;
  FINS;
 FIN B71;
FIN B7;
FINS;
FINS;
*
*======================================================================*
*             MAILLAGE VOLUMIQUE DE LA PARTION DE VORONOI              *
*======================================================================*
*
* A t on demande une coloration aleatoire des cellules ?
  LOG1     = FAUX ;
  SI (EXIS MO1) ;
    SI (EGA (TYPE MO1) 'MOT') ;
      SI (EGA MO1 'COUL') ;
        LOG1     = VRAI ;
        LCOUL1   = MOTS 'BLEU' 'ROUG' 'JAUN' 'VERT' 'TURQ' 'AZUR'
                        'ORAN' 'VIOL' 'OCEA' 'OLIV' 'GRIS' ;
      FINS ;
    FINS ;
  FINS ;
* Type d'elements a creer et nature des bords des cellules selon la
* dimension
  SI (EGA IDIM 2) ;
    TYPEL1   = MOT 'TRI3' ;
    TYPEL2   = MOT 'SEG2' ;
    MBORD    = MOT 'ARTS' ;
  FINS ;
  SI (EGA IDIM 3) ;
    TYPEL1   = MOT 'TET4' ;
    TYPEL2   = MOT 'TRI3' ;
    MBORD    = MOT 'FACS' ;
  FINS ;
* Initialisation de la table de sortie (sur la meme base que celle issue
* de l'operateur VORO)
  TAB20              = TABL ;
  TAB20 . 'MAIL'     = VIDE 'MAILLAGE'/TYPEL1 ;
  TAB20 . 'CELL' = TABL ;
  SI (EGA IDIM 3) ;
    TAB20 . 'FACS'  = TABL ;
  FINS ;
  TAB20 . 'ARTS'   = TABL ;
* Recuperation des tables d'entree
  T1C      = TAB2 . 'CELL' ;
  IT1C     = INDE T1C ;
  SI (EGA IDIM 3) ;
    T1F      = TAB2 . 'FACS' ;
  FINS ;
  T1A      = TAB2 . 'ARTS' ;
  ITA1 = INDE T1A;
* Boucle 1 : maillage des aretes
  REPE B1 (DIME T1A) ;
    A1       = T1A . (ITA1 . &B1) ;
    P1       = A1 POIN 1 ;
    P2       = A1 POIN 2 ;
    L12      = NORM (P2 MOIN P1) ;
    MA1      = D P1 P2 'DINI' (TDENSS . P1) 'DFIN' (TDENSS . P2) ;
    TAB20 . 'ARTS' . (ITA1 . &B1) = MA1 ;
  FIN B1 ;
* Boucle 2 : maillage des faces a partir des aretes (dimension 3)
  SI (EGA IDIM 3) ;
    IT1F = INDE T1F;
    REPE B1 (DIME T1F) ;
      TAB20 . 'FACS' . (IT1F . &B1)        = TABL ;
      TAB20 . 'FACS' . (IT1F . &B1). 'MAIL' = 0 ;
      LA2      = LECT ;
*     construction du contour de la face &B1
      CONT1    = VIDE 'MAILLAGE'/'SEG2' ;
      LA1      = T1F . (IT1F . &B1) . 'ARTS' ;
      REPE B2 (DIME LA1) ;
        IA2      = EXTR LA1 &B2 ;
        CONT1    = CONT1 ET (TAB20 . 'ARTS' . IA2) ;
        LA2      = LA2 ET IA2 ;
      FIN B2 ;
*     construction de la surface a l'interieur du contour
*     ce contour peut etre compose de plusieurs parties, on boucle sur
*     les differentes parties connexes
      S1       = VIDE 'MAILLAGE'/'TRI3' ;
      TC       = CCON CONT1 ;
      REPE B2 (DIME TC) ;
        OPTI 'ERRE' 'IGNO' ;
        S2       = SURF (TC . &B2) 'PLANE' 1.0;
        OPTI 'ERRE' 'NORM' ;
        S1       = S1 ET S2 ;
      FIN B2 ;
      TAB20 . 'FACS' . (IT1F . &B1) . 'MAIL'   = S1 ;
      TAB20 . 'FACS' . (IT1F . &B1) . 'ARTS' = LA2 ;
    FIN B1 ;
  FINS ;
*
* Boucle 3 : maillage des cellules a partir des faces/aretes
  REPE B1 (DIME T1C) ;
    P1       = IT1C . &B1 ;
    TAB20 . 'CELL' . P1 = TABL ;
    LAF2     = LECT ;
*   assemblage des bords de la cellule
    BORD1    = VIDE 'MAILLAGE'/TYPEL2 ;
    LAF1     = T1C . P1 . MBORD ;
    MV1      = T1C . P1 . 'VOIS' ;
    REPE B2 (DIME LAF1) ;
      IAF2     = EXTR LAF1 &B2 ;
      SI (EGA IDIM 2) ;
        BORD1    = BORD1 ET (TAB20 . MBORD . IAF2) ;
      FINS ;
      SI (EGA IDIM 3) ;
        BORD1    = BORD1 ET (TAB20 . MBORD . IAF2 . 'MAIL') ;
      FINS ;
      LAF2     = LAF2 ET  IAF2 ;
    FIN B2 ;
*   determination d'une couleur pour la cellule aleatoirement et
*   differente de celles des cellules voisines
    COU1     = MOT 'DEFA' ;
    SI LOG1 ;
      RAND1    = EXTR (BRUI 'BLAN' 'UNIF' 0.5 0.5 1) 1 ;
      RAND1    = RAND1 * (DIME LCOUL1) ;
      I1       = (ENTI RAND1) + 1 ;
      COU1     = EXTR LCOUL1 I1 ;
      L2       = MOTS ;
      REPE B2 (NBEL MV1) ;
        P2       = MV1 POIN &B2 ;
        SI (EXIS (TAB20 . 'CELL') P2) ;
          COU2       = TAB20 . 'CELL' . P2 . 'COUL' ;
          L2       = L2 ET (MOTS COU2) ;
          SI (EXIS L2 COU1) ;
            REPE B3 (DIME LCOUL1) ;
              I1       = I1 + 1 ;
              SI (I1 > (DIME LCOUL1)) ;
                I1       = 1 ;
              FINS ;
              COU1     = EXTR LCOUL1 I1 ;
              SI (EXIS L2 COU1) ;
                ITER B3 ;
              SINO ;
                QUIT B3 ;
              FINS ;
            FIN B3 ;
          FINS ;
        FINS ;
      FIN B2 ;
    FINS ;
*   construction de la cellule a l'interieur du bord
*   ce bord peut etre compose de plusieurs parties, on boucle sur
*   les differentes parties connexes
    CEL1     = VIDE 'MAILLAGE'/TYPEL1 ;
    TC       = CCON BORD1 ;
*
    REPE B2 (DIME TC) ;
      SI (EGA IDIM 2) ;
        CEL11    = SURF (TC . &B2) ;
      FINS ;
      SI (EGA IDIM 3) ;
        CEL11    = VOLU (TC . &B2) ;
      FINS ;
      CEL1     = CEL1 ET CEL11 ;
    FIN B2 ;
    CEL1     = CEL1 COUL COU1 ;
    TAB20 . 'CELL' . P1 . 'MAIL' = CEL1 ;
    TAB20 . 'CELL' . P1 . MBORD  = LAF2 ;
*   maillage des cellules voisines
    MV2      = VIDE 'MAILLAGE'/'POI1' ;
    REPE B2 (NBEL MV1) ;
      PV2      = MV1 POIN &B2 ;
      MV2      = MV2 ET PV2 ;
    FIN B2 ;
    TAB20 . 'CELL' . P1 . 'VOIS' = MV2 ;
*   couleur de la cellule
    TAB20 . 'CELL' . P1 . 'COUL' = COU1 ;
*   maillage global de la partition
    TAB20 . 'MAIL'                      = (TAB20 . 'MAIL') ET CEL1 ;
  FIN B1 ;
*
MTRIA1 = TAB20;
*
FINP MTRIA1 ;
*
$$$$


