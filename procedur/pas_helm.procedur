$$$$ PAS_HELM
* PAS_HELM  PROCEDUR  AM        17/12/20    21:15:27     9675
DEBPROC PAS_HELM PRECED*'TABLE';
*
* objet de la procedure :
* preparation des calculs en NON_LOCAL, modele HELM
*
 WTAB = PRECED . 'WTABLE' ;
 TAHELM = WTAB . 'HELMHOLTZ' ;
 NHELM  =  TAHELM . 'N_VARI_NL' ;
 MCA = TAHELM . 'H_CAPA' ;
 MATHELM = 'REDU' WTAB.'NLOC_MODL'  WTAB.'MAT1' ;
*
 'REPE' BH NHELM ;
     LEMO = TAHELM . &BH. 'NOM' ;
     LOGCTE = TAHELM . &BH. 'CTE' ;
     MOPRE = 'EXTR' LEMO 1 3 ;

*  le cas autre que ERFi

     'SI' ('NEG' MOPRE 'ERF') ;
        'SI' (('NON' LOGCTE) 'OU'
           (LOGCTE 'ET' ('NON' ('EXIS' TAHELM . &BH 'H_OPER'))));
           LWL2=  'EXCO' MATHELM 'LCAR' 'SCAL' ;
           KWL2=  0.5*(LWL2**2);
           MOWL2= TAHELM . &BH . 'H_MODELE' ;
           MAWL2= 'MATE' MOWL2 'K'  KWL2   ;
           COWL2= 'COND' MOWL2 MAWL2 ;
           TAHELM .&BH.'H_OPER' = COWL2 ET MCA  ;
        'FINS' ;

*  les cas ERFi

     'SINON' ;
        'SI' (('NON' LOGCTE) 'OU'
           (LOGCTE 'ET' ('NON' ('EXIS' TAHELM . &BH 'H_OPER'))));
           MOPOS ='EXTR' LEMO 4;
           MOVR  = 'CHAI' 'VR'  MOPOS;
           MOLCAR= 'CHAI' 'LCF' MOPOS;
           LRF1='EXCO' MATHELM MOLCAR 'SCAL' ;
           ver1='EXCO' MATHELM  ('CHAI' MOVR '1') 'SCAL';
           ver2='EXCO' MATHELM  ('CHAI' MOVR '2') 'SCAL';
           ver3='EXCO' MATHELM  ('CHAI' MOVR '3') 'SCAL';
           KRF1=0.5*(LRF1**2);
           kr11=KRF1*(ver1*ver1);
           kr22=KRF1*(ver2*ver2);
           kr33=KRF1*(ver3*ver3);
           kr21=KRF1*(ver1*ver2);
           kr31=KRF1*(ver3*ver1);
           kr32=KRF1*(ver3*ver2);
           MORF1= TAHELM . &BH . 'H_MODELE' ;
           MARF1='MATE' MORF1 'DIRECTION' (1. 0. 0.)
              ( 0. 1. 0.) 'K11' kr11 'K22' kr22 'K33' kr33
              'K21' kr21 'K31' kr31 'K32' kr32  ;
           CORF1 = 'COND' MORF1 MARF1 ;
           TAHELM .&BH.'H_OPER' = CORF1 ET MCA  ;
        'FINS' ;
     'FINS' ;
 'FIN' BH ;

'FINPROC' ;
$$$$



