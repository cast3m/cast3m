$$$$ PAS_MATE
* PAS_MATE  PROCEDUR  CB215821  16/11/14    21:15:01     9167
'DEBP' PAS_MATE PRECED*'TABLE'  ETAT*'MCHAML';
WTAB=PRECED.'WTABLE';

'SI' (WTAB.'MATVAR' 'OU' WTAB.'MODAL' 'OU' WTAB.'FREQUENTIEL');

'SI' WTAB.'MEC1';
   MA=WTAB.'MAT_MEC';
   MO=WTAB.'MOD_MEC';
  'SI'  WTAB.'POR1' ;
     MA=MA 'ET' WTAB.'MAT_POR';
     MO=MO 'ET' WTAB.'MOD_POR';
  'FINSI';
'SINON';
   MA=WTAB.'MAT_POR';
   MO=WTAB.'MOD_POR';
'FINSI';

'SI' ('EXISTE' WTAB 'MOD_LIA') ;
   MO = MO 'ET' WTAB.'MOD_LIA' ;
   MA = MA 'ET' WTAB.'MAT_LIA' ;
'FINSI' ;

'SI' ('EXISTE' WTAB 'MOD_CHA') ;
   MO = MO 'ET' WTAB.'MOD_CHA' ;
   MA = MA 'ET' WTAB.'MAT_CHA' ;
'FINSI' ;

*Pour certains materiaux on ne fait aucune instanciation
*pour les passer a l'operateur 'COMP' --------------
ISPE    = FAUX ;
ISTA    = FAUX ;
IBET    = FAUX ;
TABMOD  = 'EXTR' MO 'ZONE' ;
ISTRUB1 = 1 ;
'REPE' STRUB1 ((DIME TABMOD) / 2) ;
   MMO= TABMOD.ISTRUB1;
  'SI'   (( 'EXISTE' MMO 'MATE' 'ENDOMMAGEABLE'  )
      'OU' ('EXISTE' MMO 'MATE' 'VISCODOMMAGE'   )
      'OU' ('EXISTE' MMO 'MATE' 'PLASTIQUE_ENDOM')) ;
    'SI' ISPE;
       MOD_SPE = MOD_SPE 'ET' MMO           ;
       MAT_SPE = MAT_SPE 'ET'('REDU' MA MMO);
    'SINON';
       MOD_SPE = MMO          ;
       MAT_SPE ='REDU' MA  MMO;
       ISPE=VRAI              ;
    'FINSI';

  'SINON';
    'SI'  ( ('EXISTE' MMO  'MATE' 'DRUCKER_PARFAIT' )
    'OU' ('EXISTE' MMO 'MATE' 'MAZARS')  );
      'SI' IBET;
         MOD_BET=MOD_BET 'ET' MMO;
         MAT_BET=MAT_BET 'ET'  ('REDU'  MA  MMO);
      'SINON';
         MOD_BET= MMO           ;
         MAT_BET='REDU'  MA  MMO;
         IBET   = VRAI          ;
      'FINSI';

    'SINON';
      'SI' ISTA;
         MOD_STA= MOD_STA 'ET' MMO            ;
         MAT_STA= MAT_STA 'ET' ('REDU' MA MMO);
      'SINON';
         MOD_STA= MMO         ;
         MAT_STA='REDU' MA MMO;
         ISTA   = VRAI        ;
      'FINSI';
    'FINSI';
  'FINSI';
   ISTRUB1 = ISTRUB1 + 2;
'FIN' STRUB1;

'SI' IBET;
  'SI' WTAB.'BETON_HT';
*    traitement particulier pour beton HT
     LDEV= EXTR MAT_BET 'DEVA';
    'SI' ('EXIS' LDEV 'T   ');
       MAT_BET IMAUVAIS=IDBHT MAT_BET MOD_BET WTAB.'TET1' WTAB.'TET2';
      'SI' IMAUVAIS;
         'QUITTER' PASAPAS;
      'FINSI';
    'FINSI';
  'FINSI';

  'SI' ISTA ;
     MAT_STA= MAT_STA 'ET' MAT_BET;
     MOD_STA= MOD_STA 'ET' MOD_BET;
  'SINON';
     MAT_STA= MAT_BET;
     MOD_STA= MOD_BET;
     ISTA   = VRAI   ;
  'FINSI';
'FINSI';

'SI' ISTA;
   MMM  =           'VARI' 'NUAG' MOD_STA MAT_STA ETAT 'RIGIDITE' ;
   WTAB.'MA_COMP'= MMM ;
  'SI' ISPE;
     MMM= MMM 'ET' ('VARI' 'NUAG' MOD_SPE MAT_SPE ETAT 'RIGIDITE');
     WTAB.'MA_COMP'= MMM 'ET' MAT_SPE ;
  'FINSI';

'SINON';
   MMM  =           'VARI' 'NUAG' MOD_SPE MAT_SPE ETAT 'RIGIDITE';
   WTAB.'MA_COMP'= MAT_SPE ;
'FINSI';

'SINO';
   MMM  = WTAB.'CARACTERISTIQUES';
'FINS';

'FINPROC' MMM ;
$$$$

