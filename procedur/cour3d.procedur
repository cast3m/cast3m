$$$$ COUR3D
* COUR3D    PROCEDUR  FANDEUR   14/10/10    21:15:12     8178
 'DEBPROC' COUR3D BBBB*'MAILLAGE'  FFAC1*'MAILLAGE' FFAC2*'MAILLAGE'
  MMM*'MOT'   J*'FLOTTANT'  NORJ/'LOGIQUE' ;
************************************************************************
*         CALCUL DU CHPO DES DENSITES DE COURANTS SUR UNE BOBINE       *
*         BBBB MAILLAGEGE DE LA BOBINE 3D                              *
*         FFAC1 FACE  D ENTREE DU COURANT                              *
*         FFAC2 GACE DE SORTIE DU COURANT                              *
*         MMMM MOT  AMP  ( AMPERES/UNITE SURFACE OU AT AMPERES TOTAUX  *
*         J FLOTTANT   DENSITE OU AMP TOTAUX   SUIVANT MMMM            *
*   SORTIE CHPO DES DENSITES DE COURANT AUX NOEUDS DE BBBB             *
*                 COMPOSANTES T,X   T,Y   T,Z
*         NORJ   LOGIQUE  VRAI SI  L ON RECTIFIE J DANS LES COURBURES  *
************************************************************************
 'SI' ( 'NON' ( 'EXISTE' NORJ)) ; NORJ = FAUX ; 'FINSI' ;
 OBOB = BBBB 'MODE' 'THERMIQUE' 'ISOTROPE' ;
 MATO = 'MATE' OBOB 'K' 1. ;
 RRRR = 'COND' OBOB MATO ;
 ENC1 = 'BLOQUER'  'T' FFAC1 ;
 FFFF = 'FLUX'  OBOB  1.  FFAC2 ;
 U = ('EXTR' ( 'RESU' FFFF ) 'MAILLAGE' )'POINT' 1 ;
 SUR = 'EXTR' ( 'RESU' FFFF) 'Q' U ;
      'SI' ( 'EGA' MMM 'AT') ;
 J =  J / SUR  ;
 'MESS' ' NOMBRES D AMPERES PAR BLOCS '  (J * SUR) ;
      'FINSI' ;
 POTE = 'RESOU' ( RRRR 'ET' ENC1 ) ( FFFF * J )  ;
 JJJ = 'CHAN' 'CHPO' ('GRAD' POTE OBOB) OBOB  ;
   'SI'  NORJ  ;
*****************************************************************
* normalisation des courants dans les coudes
*****************************************************************
 RRR =( (('EXCO' JJJ T,X ) ** 2 ) + (('EXCO' JJJ T,Y ) ** 2)
         + (('EXCO' JJJ T,Z ) ** 2)    ) ** .5 ;
 JJJ = JJJ / RRR * J ;
   'FINSI' ;
 JJX = ('EXCO' JJJ T,X) 'NOMC' JX ;
 JJY = ('EXCO' JJJ T,Y) 'NOMC' JY ;
 JJZ = ('EXCO' JJJ T,Z) 'NOMC' JZ ;
 JJJ = JJX + JJY + JJZ ;
*
  'FINPROC' JJJ ;

