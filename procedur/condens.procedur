$$$$ CONDENS
* CONDENS   PROCEDUR  MAGN      18/06/08    21:15:06     9840
DEBPROC CONDENS RXT*TABLE $paroic*MMODEL TP*MOT TF*MOT MRVP*MOT
 KHcu*'FLOTTANT';

*-------------------------------------------------*
* calcul du flux condensé Jv par Chilton-Colburn  *
* et la corrélation de convection naturelle       *
*   Sh = kL/Dv = 0.13 (Gr Sc)**1/3                *
*   Jv = k ro (Yv - Yvsat)   en kg/m²s            *
*-------------------------------------------------*
*
*  indice P pour l'interface fluide (à la distance YP)              en P
*  indice I pour l'interface paroi/condensante                      en I
*
*  Rv  = TBT.'Rgvap' constante des gaz parfait pour la vapeur
*
*  YVI = ROVI/ROI fraction massique de vapeur à l'interface         en I
*  ROVI Densité de vapeur à l'interface        (Kg/m**3)            en I
*
*
*  ROP   densité mélange                                            en P
*  PvP = ROVP * Rv * (TFP+273.15) pession partielle de vapeur       en P
*  ROVP  densité vapeur                                             en P
*
*  TFP   : Température bulk du mélange gazeux                       en P
*  Pt    : Pression totale dans l'enceinte (0D)                     en P
*
*
*  En sortie
*  KKC coefficient d'échange de masse sur l'équation de
*      densité de vapeur :   KKC*(ROVAP - ROVI)   (m/s)
*
*  ROVI Densité de vapeur à l'interface        (Kg/m**3)            en I
*
*  HT   Coefficient d'échange thermique pour  le gaz
*       du à la convection   (W/m**2/°c)
*
*  Fcond densite de flux condense
*
*  FHP Fcond*Lv + HT*(Tgaz -Tparoi)
*
*------------------------------------------------------------
* Modif 11/05: Choix du modèle de corrélation MODCOND (cf. notice ENCEINTE)
*
*-------------------------------------------------------------
* La première chose à faire !!!!!
            TBT=rxt.'TBT'     ;
* La première chose à faire !!!!!

Si (non TBT.'VAPEUR')         ;
QUITTER CONDENS               ;
Finsi                         ;

tic=rxt.'TIC';
Si (exist tic 'FACTEU');
fac=tic.'FACTEU';
Sinon;
fac=1.;
Finsi;

*Géométrie
GEO=rxt.'GEO';
paroic = doma $paroic maillage ;
Diagpc = 'DOMA' $paroic 'XXDIAGSI'          ;
*
Mui=redu  (tic.'Mu') paroic ;

ndlc=dime tic.'PT'  ;
Pt=extr tic.'PT' ndlc ;
Rhomn=extr tic.'Rhomn' ndlc ;
Lv=TBT.'Lv' ;
Cpm= extr tic.'Cpm' ndlc ;
************************************************************************
* Réduction des grandeurs aux parois

TBP=redu tic.TP paroic ;
TFP=redu tic.TF paroic;
ROVPP = redu tic.MRVP paroic     ;
ROVPP = 'KOPS' ROVPP '|<' 1.e-10 ;

RH2P  = 0.                       ;
Si TBT.'TH2'                     ;
RH2P  = redu tic.'RH2' paroic    ;
RH2P  = 'KOPS' RH2P  '|<' 1.e-10 ;
Finsi                            ;

RHEP  = 0.                       ;
Si TBT.'THE'                     ;
RHEP  = redu tic.'RHE' paroic    ;
RHEP  = 'KOPS' RHEP  '|<' 1.e-10 ;
Finsi                            ;

RO2P  = 0.                       ;
Si TBT.'TO2'                     ;
RO2P  = redu tic.'RO2' paroic    ;
RO2P  = 'KOPS' RO2P  '|<' 1.e-10 ;
Finsi                            ;

RN2P = 0.                        ;
Si TBT.'TN2'                     ;
RN2P = redu tic.'RN2' paroic     ;
RN2P  = 'KOPS' RN2P  '|<' 1.e-10 ;
Finsi                            ;

RCO2P= 0.                        ;
Si TBT.'TCO2'                    ;
RCO2P= redu tic.'RCO2' paroic    ;
RCO2P = 'KOPS' RCO2P '|<' 1.e-10 ;
Finsi                            ;

RCOP = 0.                        ;
Si TBT.'TCO'                     ;
RCOP = redu tic.'RCO'  paroic    ;
RCOP  = 'KOPS' RCOP  '|<' 1.e-10 ;
Finsi                            ;

* Densité du mélange en limite fluide                 (en P)
* (ROP issu de la loi d'état si BM1)
ROP=redu tic.'RHO' paroic         ;
ROP='KOPS' ROP '|<' 1.e-10        ;

* Densité du mélange hors l'air en paroi : ROPa
ROPa= ROVPP + RH2P + RHEP + RO2P + RN2P + RCO2P + RCOP ;

* On en déduit la densité de l'air en limite fluide   (en P)
RAIRP=ROP - ROPa                  ;
RAIRP='KOPS' RAIRP '|<' 1.e-10    ;

IROP= INVE ROP                    ;

* D'où les fractions massiques                        (en P)
YVP   = ROVPP * IROP             ;
YH2P  = RH2P  * IROP             ;
YHEP  = RHEP  * IROP             ;
YO2P  = RO2P  * IROP             ;
YN2P  = RN2P  * IROP             ;
YCO2P = RCO2P * IROP             ;
YCOP  = RCOP  * IROP             ;
YAIRP = RAIRP * IROP             ;

* pression de saturation à l'interface                (en I)
PSATI= PSATT (TBP + 273.15) ;
*mess ' Mini Maxi Psat ' (mini Psat) (maxi Psat) ;

* pression de vapeur pres de la paroi                 (en P)
RVT = TBT.'Rgvap' * (Tfp + 273.15);
PvP = ROVPP * RVT ;
*mess ' Mini Maxi PvP ' (mini PvP) (maxi PvP) ;

* masse volumique à l'interface                       (en I)
RVTI = TBT.'Rgvap' * (TBP + 273.15);
ROVI = PSATI * (inve RVTI) ;

YINCP = (YVP * 0.0) + 1.0 - YVP ;

YO2NP = YO2P + (YAIRP * 0.21) ;
YN2NP = YN2P + (YAIRP * 0.79) ;

Rginc = (TBT.'Rgo2'*YO2P) + (TBT.'Rgn2'*YN2P) + (TBT.'Rghe'*YHEP)
      + (TBT.'Rgh2'*YH2P) + (TBT.'Rgco'*YCOP) + (TBT.'Rgco2'*YCO2P)
      + (TBT.'Rgair'*YAIRP) ;

* Pression partielle des incondensables
PINC = Pt  -  PSATI ;
*mess ' Mini Maxi Pinc ' (mini Pinc) (maxi Pinc) ;

RAVI = PINC * YINCP *
 ((((abs Rginc) + 1.) * ((abs (TBP + 273.15)) + 1.e-5)) ** (-1.));
*((((abs Rginc) + 1.) * (TBP + 273.15)) ** (-1.)) ;

ROI =  ROVI + RAVI ;

* Fraction massique vapeur à l'interface a saturation
 YVI =  ROVI * (inve ROI) ;

* coefficient de diffusion de la vapeur dans le mélange (m2/s)
DV = VAPDIF Pt (TFP+273.15) YVP YH2P YHEP YO2NP YN2NP YCO2P YCOP ;
*mess 'Mini DV maxi DV ' (mini DV) (MAxi DV)            ;

* coefficient d'échange de masse k (m/s)

BB = 0.13 * ((9.81 * Rhomn *(inve MUi))**0.33333 );
*mess ' mini maxi BB ' (mini bb) (maxi BB) ;
DRO = ((ABS (ROP - ROI))*IROP) ** 0.33333  ;
'SI' ('EGA' TBT.'MODCOND' 'CHIL1');
   may1 = 'MASQUE' (1.-YVI) 'SUPERIEUR' 0.0001;
   denom = (may1 * (1.-YVI)) + ((1.-may1) * 0.0001);
   DRO = DRO * ('INVE' DENOM);
'FINSI' ;
KK = (DV  **  0.66667)* DRO *  BB  ;

* Calcul du coefficient Echange convectif
* Ht = DRO*BB*Lambda*(rho*(inve Mui))**0.333
* or Pr = 1 donc Lambda = Mui*Cp
LAMBM = Mui* Cpm ;
HT = DRO * BB * LAMBM * ((Rhomn *(inve Mui))**0.33333) ;

* fonction indicatrice de la condensation
DPV =  PvP - PSATI ;
*mess 'Mini maxi DPV ' (mini dpv) (maxi dpv) ;
IND = DPV  MASQUE  'EGSUP'  0. ;
IND = KCHT $paroic SCAL SOMMET IND ;

HT = (HT*IND) - ((IND-1.)*KHcu) ;

INDH=HT MASQ 'EGSUP' KHcu ;
HT = (HT*INDH) - ((INDH-1.)*KHcu) ;

A1 = 5000. ;
AL1 = 0.8; ALA1 = AL1*A1;
B1 = ALA1 *((2.*(1.-AL1))**(-1.));
IK1 = MASQUE HT 'INFERIEUR' ALA1;
HT = (IK1*HT) + ((1.-IK1)*A1*(HT+B1)*(INVE (A1+HT+B1)));

KKC = KK * IND ;
KKC1 = 'KCHT' $paroic 'SCAL' 'SOMMET' KKC ;
KKC1 = KKC1 * fac ;
KKC=NOEL $paroic KKC1 ;

* coefficient d'échange pour yvap (kg/m2/s)

KRO = (KK * ROP) * IND ;
KRO=kcht $paroic 'SCAL' 'SOMMET' kro ;

* calcul de la masse de vapeur condensée QC (kg/s)
DYV = YVP - YVI ;
FCOND =  DYV * KRO ;
QC  = Diagpc  *  FCOND ;
QC  = ABS (SOMT QC)  ;
Econd = TBT.'Cvvap' * (SOMT (Diagpc  *  FCOND * (Tfp + 273.15))) ;
Hcond = TBT.'Cpvap' * (SOMT (Diagpc  *  FCOND * (Tfp + 273.15))) ;
Econd = ABS (Econd) ;
Hcond = ABS (Hcond) ;
* Sorties

**** Affichage de grandeurs moyennes
volp = 'SOMT' (Diagpc) ;
*'MESS' '==> Tgaz moyen paroi = '((somt (Diagpc * Tfp))/volp) ;
*'MESS' '==> Tpar moyen paroi = '((somt (Diagpc * Tbp))/volp) ;
*'MESS' '==> Yvpa moyen paroi = '((somt (Diagpc * YVP))/volp) ;
*'MESS' '==> KKC (mini) (maxi) = ' (MINI KKC)  (MAXI KKC)   ;

ROVI  = 'KCHT' $paroic 'SCAL' 'SOMMET' rovi;
Fcond = 'KCHT' $paroic 'SCAL' 'SOMMET' FCOND ;

*------------------------------------------------------
* FHP  = densite de flux transmise au mur (W/m2)
* On prend la somme de la convection et de la condensation
* FHP  = FCOND * Lv + HT* (Tgaz-Tparoi)
*


Fconv = ( Tfp - Tbp ) * HT       ;
Fconv = 'KCHT' $paroic 'SCAL' 'SOMMET' Fconv ;
Fconv = 'NOEL' $paroic Fconv ;
FHP = ((noel $paroic (FCOND * Lv)) + Fconv ) ;

HT = HT * fac;
HT =noel $paroic ht ;


* Fin calcul du flux condensé Jv par Chilton-Colburn  *
*******************************************************
FINPROC QC Fcond Econd Hcond KKC ROVI FHP HT  ;


