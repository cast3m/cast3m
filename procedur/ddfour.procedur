$$$$ DDFOUR
* DDFOUR    PROCEDUR  TTMF3     96/06/04    21:15:12     2157
 'DEBPROC'  DDFOUR GEO*'MAILLAGE' CHARM*'MAILLAGE'  NHARM*'ENTIER'
  SOL*'CHPOINT'  RHARM*'FLOTTANT'  ORIG/'POINT' LIS*'LOGIQUE'
ITYPE/'ENTIER' ;
*********************************************************************
*        ANALYSE HARMONIQUE  DU POTENTIEL VECTEUR
*        GEO    MAILLAGE SUPPORT SOLUTION GENERALE                  *
*        CHARM  ARC DE CERCLE SUR LEQUEL ON A LE POTENTIEL          *
*        NHARM  NOMBRE D HARMONIQUES A CALCULER                     *
*        SOL  SOLUTION  EN POTENTIEL                                *
*        RHARM  RAYON DE NORMALISATION                              *
*        ORIG   ORIGINE CERCLE D ANALYSE                            *
*        LIS LOGIQUE  FAUX SI PAS DE LISSAGE POLYNOMIAL             *
*        ITYPE  ORDRE DE SYMETRIE  POUR L EXPANSION POLY SI LIS=VRAI*
*********************************************************************
 1P = CHARM 'POINT' 'INITIAL' ;2P= CHARM 'POINT' 'FINAL' ;
        'SI' ( 'EXISTE' ORIG ) ;
        RCIRC = 'NORM' ( 1P 'MOINS' ORIG ) ;
        'SINON' ;
        RCIRC = 'NORM' 1P ;ORIG = 0. 0. ;
        'FINS' ;
 X1 Y1 = 'COOR' ( 1P 'MOINS' ORIG );X2 Y2 = 'COOR' 2P ;
 PT = 2P 'PROJETER' (( Y1 * -1. )  X1 )  'DROITE' ORIG 1P ;
 H = 'NORM' ( 2P 'MOINS' PT ) ;
     'SI' ( (X1 + X2 ) < 1.E-3 ) ;
 NUM = 2 ;
 'MESS'   'SOLUTION DONNEE SUR  180  DEGRES ' ;
     'SINON' ;
 ANG = 'ATG' H ( 'NORM' ( PT 'MOINS' ORIG)) ;
 NUM = ( 'ENTI' ( 360.1 / ANG )) ;
 'MESS'   'SOLUTION DONNEE SUR ' ANG ' DEGRES '   ;
     'FINSI' ;
   'SI' ( 'NON' ( LIS )) ;
 SCIRC = INT_COMP  GEO   SOL  CHARM    ;
 SCIRC = SCIRC 'NOMC' 'T' ;
   'SINON' ;
* lissage polynomial
* CHLI  = PROI POLY CHARM GEO   SOL    ITYPE  'PLAN' ;
 CHLI  = 'PROI' POLY GEO CHARM   SOL    ITYPE  'PLAN' ;
 SCIRC = ('EXCO' CHLI  'A' ) 'NOMC' 'T' ;
   'FINS' ;
 RRAP = RHARM / RCIRC ;
 CQTT RTOT = F_S2PI CHARM  SCIRC  NUM ORIG  ;
  EQTT = 'EVOL' 'ROUG' 'CHPO' CQTT  'T' RTOT ;
  AAAA=  'EXTR' EQTT 'ABSC' ;
* QTOT=  'EXTR' EQTT 'ORDO' ;
* LIST QTOT ;
* NI1= (DIME AAAA ) - 1  ;
* IVAL = VALE ELEM ;
* OPTION ELEM SEG2 ;I1 = 0. 0. ; I2 = 360. 0. ;
* RTOT = DROI  NI1  I1 I2  ;
 OBA_RMA = 'MODEL'  RTOT  'MECANIQUE'  'COQ2';
* OBA_RMA= 'AFFECTE' RTOT ('MODELE' 'STANDARD') COQ2;
 PERIOD= 'MAXI' AAAA ;
 MULT= 360. / PERIOD ;
 ANGVEC= AAAA * MULT  ;
* MESS ' PERIODE '  PERIOD ;
 N=0;
 'MESS'   '  ANALYSE CONFORME A POISSON  HARMON ';
 'MESS'   ' HARMONIQUE     POTENTIEL    CHAMP ';
 'MESS' ' NOMBRE DE SEGMENTS DE DR ' NI1 ;
 'MESS' ' DIME DE ANGVEC ' ( DIME ANGVEC ) ;
 CHPA= 'MANU' 'CHPO' RTOT 1 'SCAL' ANGVEC ;
* CQTT = MANU CHPO RTOT 1 'POT' QTOT ;
 'REPETER' BLOCIT NHARM ;
 N= N + 1 ;
 RAN= RRAP ** N ;
 ANGLEN= CHPA  *  N ;
 CNX=  'COS'  ANGLEN ;
 SNX=  'SIN'  ANGLEN ;
 EVREL= CQTT * CNX ;EVIMA= CQTT * SNX ;
  CEVREL =EVREL 'CHAN' 'CHAM' OBA_RMA 'RIGIDITE' ;
   CEVIMA = EVIMA 'CHAN' 'CHAM' OBA_RMA 'RIGIDITE' ;
 AK= (( 'INTG' OBA_RMA CEVREL ) / PERIOD ) * 2. * RAN ;
 BK= (( 'INTG' OBA_RMA CEVIMA ) / PERIOD ) * 2. * RAN ;
 CK= (( AK * AK ) + ( BK * BK ) ) ** .5 ;
 FF= ( 'FLOT' N) / RHARM ;
 AKK= AK * FF ;
 BKK= BK * FF ;
 CKK= CK * FF ;
 'SI' (N 'EGA' 1 );AKN= 'PROG' AKK ; 'SINON' ;
AKN = AKN 'ET' ('PROG' AKK ) ; 'FINSI' ;
 'SAUTER' 1 'LIGNE' ;
* MESS   N AK BK CK AKK BKK CKK ;
 'MESS'   N   AK  BK  CK  ;
 'MESS'   N  AKK BKK CKK  ;
 'FIN' BLOCIT ;
* OPTION ELEM IVAL ;
 'FINPROC' AKN  ANGVEC ;

