$$$$ @LAVIS
* @LAVIS    PROCEDUR  KK2000    99/11/30    21:15:26     3720
*
************************************************************************
*                        PROCEDURA @LAVIS
************************************************************************
*
* Descrizione : visualizza l'impilamento delle lamine per una
*               zona richiesta
*
* Sintassi    : @LAVIS TAB_LAM NUM_ZONA ;
*
*
* Autore/Data : Miliozzi A.  10/04/1997
*
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_LAM   Tavola caratteristica dei laminati compositi
*
* NUM_ZONA  Numero della zona da visualizzare
*
*--- OUTPUT
*==========
*
*
************************************************************************
*
DEBPROC @LAVIS TCOMP*TABLE NZONE*ENTIER ;
*
  OPTI DIME 3 ELEM CUB8 ;
  LISCOU = MOTS 'ROUG' 'VERT' ;
*
  SI (EXIST (TCOMP.NZONE) ) ;
*
* --- RICERCA DEL MASSIMO SPESSORE
*
    N   = 1 ;
    BB  = 0.;
    TC  = TCOMP.NZONE ;
    LEX = PROG ;
    LEP = PROG ;
    LAN = PROG ;
    NPLY = (DIME TC) - 6 ;
    REPETER LOOP0 NPLY ;
      AN = TC.N.ANG ;
      SP = TC.N.EPA ;
      EX = TC.N.EXC ;
      LEX = INSERER LEX N EX ;
      LEP = INSERER LEP N SP ;
      LAN = INSERER LAN N AN ;
      SI (SP > BB ) ; BB = SP ; FINSI ;
      N = N + 1 ;
    FIN LOOP0 ;
*
* --- TRACCIAMENTO STRATIFICAZIONE
*
    OEIL   = 100. -100. 50. ;
    NN   = 1 ;
    II   = 1 ;
    REPETER LOOP1 NPLY ;
      EX = EXTRAIRE LEX NN ;
      EP = EXTRAIRE LEP NN ;
      AN = EXTRAIRE LAN NN ;
      CC = EXTRAIRE LISCOU II ;
      SI (II < 2); II = II + 1 ; SINON ; II = 1 ; FINSI ;
      KK = (FLOTTANT NN) / (FLOTTANT NPLY) ;
      C1 = 2. * BB          ; C2 = 10. * BB * KK    ;
      C3 = EX - ( EP / 2. ) ; C4 = EX + ( EP / 2. ) ;
      D1 = BB * ( COS AN )  ; D2 = BB * ( SIN AN  ) ;
      AM = 0. 0. C3 ;
      AP = 0. 0. C4 ;
      L1 = DROI 1 AP AM ;
      S1 = L1 TRANS 1 ( 0. C1 0. ) ;
      V1 = S1 VOLU TRANS 1 ( C2 0. 0. ) ;
      PP = V1 POIN PROC ( C2 C1 C3 ) ;
      FOX= FORCE (BB  0. 0. ) PP ;
      FOY= FORCE ( 0. BB 0. ) PP ;
      FOO= FORCE ( D1 D2 0. ) PP ;
      SI (NN EGA 1) ;
        VTOT = COUL V1 CC ;
        VETO = (VECT FOX 1. FX FY FZ BLEU ) ET
               (VECT FOY 1. FX FY FZ BLEU ) ET
               (VECT FOO 2. FX FY FZ ROSE ) ;
      SINON ;
        VTOT = VTOT ET ( COUL V1 CC ) ;
        VE1  = (VECT FOX 1. FX FY FZ BLEU ) ET
               (VECT FOY 1. FX FY FZ BLEU ) ET
               (VECT FOO 2. FX FY FZ ROSE ) ;
        VETO = VETO ET VE1 ;
      FINSI ;
      NN = NN + 1 ;
    FIN LOOP1 ;
*
    TITR ' ZONE NUMBER ' NZONE ;
    TRAC OEIL VTOT FACE VETO ;
*
  SINON ;
    MESS ' NON-EXISTENT ZONE NUMBER !' ;
  FINSI ;
*
FINPROC ;
*
************************************************************************
*                       FINE PROCEDURA @LAVIS
************************************************************************
*

