$$$$ UNILATER
* UNILATER  PROCEDUR  MB234859  19/01/21    21:15:15     10049
*  MODIF JUILLET 1987 POUR PERMETTRE DES JEUX DANS LES LES 2 DIRECTIONS
*
*  CETTE PROCEDURE EST APPELER PAR RESOU
*  LES ARGUMENTS SONT : MATAB TABLE CONTENANT LES INFORMATIONS UTILES
*                         C'EST A DIRE RRR BBB STAT SUP RITOU ET LISEA
*                       FFF   FORCE A METTRE AU SECOND MEMBRE
*
*           RRR   MATRICE DE RIGIDITE SANS LES APPUIS
*           BBB   MATRICE DE RIGIDITE DES APPUIS
*           STAT  LOGIQUE INDIQUANT SI C'EST LE PREMIER PASSAGE
*           SUP   SUPER ELEMENT EQUIVALENT
*           RITOU DERNIERE RAIDEUR UTILISEE DANS LA RESOLUTION
*           LISEA DESCRIPTEUR DES APPUIS COURAMMENT ACTIFS
*
  DEBPROCEDURE  UNILATER
  MATAB*'TABLE'     FFF*'CHPOINT'   MCLE/'MOT' FL/'CHPOINT';
*
* si il y a lieu,nbnegs est le nombre de multiplicateurs de lagrange contenus da
nbnegs = 0; nbnegt = 0; mostab = 'MOT' 'NOSTAB';
'SI' ('EGA' 'INIB' MCLE);
   'ARGU' TEMP*'RIGIDITE';  MATAB.7 = TEMP;
   'ARGU' TEMP*'LISTENTI';  MATAB.6 = TEMP;
   MATAB.'OK' = VRAI ;
'FINSI';
*
*RRR =  MATAB.1    ;
 BBB =  MATAB.2    ;
*
'SI' ('EGA' ('TYPE' FL) 'CHPOINT ');
  FADZ = 'EXCO' FL 'FADH' 'NOID' 'LX' ;
  FL   = 'EXCO' FL 'LX'   'NOID' 'LX' ;
  chp2 = BBB * FADZ ;
  chp_z = BBB * FL ;  FFF = FFF + chp_z + chp2 ;
  'DETR' chp_z ;
'FINSI';
*
 STAT = MATAB.3    ;
*
'SI'  STAT ;
    'SI' MATAB.'VRAISUP';
        SUP = MATAB.4 ;
    'SINON';
       RISUP= MATAB.4 ;
    'FINSI';
'SINON'    ;
*
*   CALCUL DU SUPER ELEMENT  SI BESOIN
*
    MMM = 'EXTR' MATAB.1 'MAILLAGE';MMB=  'EXTR' BBB 'MAILLAGE';
    MMU = MMM 'ELEM' 'APPU' 'LARG' MMB;
    NTOT= 'NBEL' MMM ;
    NAPP='NBEL' MMU;
*   MESS ' unilater ntot napp ' NTOT NAPP ;
    'SI' (((NAPP * 2)  > NTOT ) 'OU' MATAB.'NSYM' );
* mess ' unilater pas de super element' napp ntot;
       RISUP= MATAB.1 ;
       MATAB.4=MATAB.1 ;
       MATAB.'VRAISUP'=FAUX;
   'SINON';
* mess ' on calcule le super element';
* je remets l'option NOMU car il y a un problème de normalisation avec les mults
* A voir si elle ne pose pas des problèmes de surcontraintes.
   SUP = 'SUPER' 'RIGI' MATAB.1  BBB 'NOMU';
      MATAB.4  =  SUP ;
      MATAB.'VRAISUP'=VRAI;
   'FINSI';
'FINSI' ;
*
ISUP=MATAB.'VRAISUP'  ;
MATAB.3=VRAI;
*
* AU CAS OU IL N'Y AURAIT PAS DE DEPIMP
*
FFF0= 'DEPIMP' BBB 0.; FA = FFF + FFF0;
DEPTOT = FA  'EXCO' 'FLX'  'FLX' ;
'SI' ISUP ;
    RISUP=  ('EXTRAI' SUP 'RIGI') ;
**  list resu risup;
**  risup = cano risup;
**  list resu risup;
    MASUP= 'EXTRAI' RISUP 'MAIL' ;
    DEPTOT = DEPTOT - (DEPTOT REDU MASUP);
   F =   'SUPER'  'CHAR' SUP  FA ;
   f_lx = f 'EXCO' 'FLX' 'NOID';
     f_lx_m = f_lx 'EXTR' 'MAIL';
   'SI' ('NEG' ('TYPE' F_LX_m) 'ENTIER');
     nbnegs = f_lx_m  nbno;
    'FINSI';
'SINON';
  RISUP = MATAB.1 * 1;
  F=FA 'ENLE' 'FLX';
'FINSI';
'SI' ('NON' matab.'VRAISUP');
     nbnegs = 0;
'FINSI';
  FDEPTO=F + DEPTOT ;
'DETR' FFF0   ;
*
*    INITIALISATION
*
    'SI' ('NON' STAT); MATAB.'OK' = VRAI; 'FINSI';
    'SI' (STAT 'ET' MATAB.'OK') ;
      RITOU  = MATAB.5   ;
      LISEA  = MATAB.6   ;
***   BLOINI = BBB ;
      FDEPTOT=FDEPTO;
    'SINON'   ;
     'SI' ('EXISTE' MATAB 7) ;
      BLOINI = MATAB.7;
      RITOU = RISUP 'ET' MATAB.7 ;
      LISEA  = MATAB.6 'ET' ('LECT' ) ;
      FDEPTOT=FDEPTO;
     'SINON';
      BLOINI = BBB ;
*     RITOU = RISUP * 1.;
      RITOU = RISUP 'ET' BLOINI ;
*     INITIALISER LISEA A UNE VALEUR IMPOSSIBLE
      LISEA = 'LECT' -1 ;
      FDEPTOT=F ;
     'FINSI';
**    'MESS' 'on est bien la ';
**    RITOU = RISUP 'ET' BLOINI ;
**    RITOU = RISUP * 1.        ;
    'FINSI'   ;
*
MATAB.'OK' = VRAI; matab.5 = 1 ; matab.6 = 1 ;

      LITOT = 'LECT' ;
     BLOTOT = BBB ;
*
*   RECHERCHE DES BLOQUAGES ACTIFS
    OPTION = 'MOT' 'RAPIDE';
    'SI' ('EXISTE' FL) ;
      OPTIO2='MOT' 'INIT';
    'SINON';
      OPTIO2='MOT' 'CONT';
    'FINSI';
*
   I = 0    ; NCONV= VRAI ;
*
'REPETER'   BOUCL1 ;
   I = I + 1 ;
    MAFREIN*'MAILLAGE'  NBE*'ENTIER' DE =
        'RESOU' 'NOID' 'ENSE' 'NOUNIL' MOSTAB RITOU FDEPTOT;
**  nbnegt = 'MAXI' ('LECT' nbnegt ('DIAG' ritou));
    nbnegt = 'DIAG' ritou;
'SI' ('EGA' ('TYPE' FL) 'CHPOINT ');
  BLOTO LISEN LOG = BLOTOT  'EXCITER' DEPTOT  DE MAFREIN OPTION FL ;
'SINON';
  BLOTO LISEN LOG = BLOTOT  'EXCITER' DEPTOT  DE MAFREIN OPTION ;
'FINSI';
    'SI' ('NON' ('VERI' DE));
        DE = 'VIDE' 'CHPOINT'/'DIFFUS';
        'MESS' 'mise a zero des deplacements';
        'QUITTER' boucl1;
    'FINSI';
*list lisen;
'SI' ((I 'EGA' 5) 'ET' ('EGA' OPTION 'RAPIDE'));
    OPTION= 'MOT' 'MOYEN';
'FINSI';
'SI' (('NEG' 'INIB' MCLE) 'OU' (I 'NEG' 1));
'SI' ((LISEN 'EGA' LISEA) 'OU' LOG) ;
    'SI' (I 'NEG' 1) ;
    'SI' ((NBE 'EGA' 0) 'OU' ('EGA' OPTION 'QAPIDE'));
     NCONV=  FAUX ;
     'QUITTER' BOUCL1;
    'SINON';
     M1 = 'EXTR' (RISUP ET BLOTO) 'MAIL';
     M2 = 'EXTR' RITOU  'MAIL';
     M3 = M1 'DIFF' M2;
     'SI' (('NBEL' M3) 'EGA' 0) ;
      NCONV=  FAUX ;
      'QUITTER' BOUCL1;
      'FINSI';
     'FINSI';
    'FINSI';
   'FINSI';
   'SI' ((LISEN 'DANS' LITOT) 'OU' (('MULT' &BOUCL1 100) 'ET' ('NEG' OPTION 'LENT'))) ;
     'SI' ('EGA' OPTION 'LENT');
      'SI' ('EGA' OPTIO2 'CONT');
       OPTIO2= 'MOT' 'INIT';
       OPTION= 'MOT' 'RAPIDE';
       BLOTO=BBB;

       LITOT= 'LECT' ;
       LISEN=LISEN * -1;
   'MESS' ' *** UNILATER stratégie alternative à la ' I 'ème itération';
      mostab = MOT 'STAB';
       I=0;
      'SINON';
      'QUITTER' BOUCL1;
      'FINSI';
     'SINON';
      OPTION = 'MOT' 'LENT';
      LITOT = 'LECT';
      'MESS' ' *** UNILATER stratégie lente à la ' I 'ème itération';
     'FINSI';
   'FINSI';
'FINSI';
   LITAN=LITOT;
   LITOT=LITOT 'ET' LISEN;
 'DETRUIRE' LITAN;                  ;  'DETRUIRE' DE  ;
* 'SI' ('EXISTE' MATAB 5);
*   'SI' ('NEG' RITOU  (MATAB.5)); 'DETRUIRE' RITOU; 'FINSI';
* 'SINON';
   'DETRUIRE' RITOU;
* 'FINSI';
*    'SI' (I 'NEG' 1);GEO='EXTRAI' BLOT 'MAIL';'DETR' GEO 'TOUT';
*    'DETR' BLOT 'ELEM'  ;'FINSI';
     RITOU = RISUP 'ET' BLOTO ;
     BLOT = BLOTO;
     LISEA=LISEN;
     FDEPTOT=FDEPTO;
'FIN' BOUCL1;
*
* CFBUG = 'CHAINE' ' *** UNILATER convergence à l''''itération ';
* 'MESS'   CFBUG I;
'SI' (NBE 'NEG' 0) ; MATAB.'OK' = FAUX; 'FINSI';
*
      MATAB.5 =  RITOU ;
      MATAB.6 =  LISEA ;
      MATAB.7 = BLOTO;
      MATAB.13 = nbnegt - nbnegs;

*
   'SI'  NCONV  ;
     NCONV=FAUX;
     MATAB.'OK'=FAUX;
   'FINSI';

   'SI'  NCONV  ;
     'MESS'  ' *** UNILATER il n y a pas de solution au système ';
      RITOU = RISUP 'ET' BBB ;
      LISEA = 'LECT' 0;
      MATAB.5 =  RITOU ;
      MATAB.6 =  LISEA ;
      MATAB.7 = BLOTO;
   'SINON';
*
**CFBUG = 'CHAINE' ' *** UNILATER convergence à l''''itération ';
'SI' ('NON' MATAB.'OK');
CFBUG = 'CHAINE' ' *** UNILATER non convergence à l''''itération ';
'MESS'   CFBUG I nbe;
 MATAB.13 = 1234 ;
'FINSI';
MATAB.'NITER' = I;
*
*  CALCUL SUR TOUTE LA STRUCTURE
*
'SI' ISUP;
    DE1=  'SUPER' 'DEPL'   SUP   DE    FFF;
    DE2 = DE1 'ET' (DE 'ENLE' 'LX') 'NOER';
    'DETRUIRE' DE1;  'DETR' DE; 'DETR' FA ;
'SINON';
    DE2= DE;
'FINSI' ;
'SI' ('EGA' ('TYPE' FL) 'CHPOINT ');
  DE2 = DE2 - FADZ ;
  FL = FL REDU (BBB EXTR 'MAIL' 'MULT');
  DE3 = DE2 - FL ; 'DETR' DE2;DE2 = DE3;
'FINSI';
*
  'FINSI' ;
*
'DETR' F  ;'DETR' DEPTOT ;'DETR' FDEPTOT ;
*
'SI' ( 'EXISTE' MATAB 8 ) ;
   rrrr = MATAB.8 ;
  DE3 = 'MREM'  DE2  rrrr matab.9 bloto matab.12 matab.10 MATAB.11;
  matab.10=0; matab.11=0;
* mess ' dans unilater de3 ';list de3;
  'DETR' DE2 ;'RESPRO' DE3;
'SINON' ;
* mess ' dans unilater de2 ';list de2;
  'RESPRO'  DE2  ;
'FINSI' ;
*
'SI' ('EGA' 'INIB' MCLE);
  'SI' ('EGA' ('TYPE' FL) 'CHPOINT ');
    'RESPRO' 'bid' 'bid' 'bid' 'bid';
  'SINON';
    'RESPRO' 'bid' 'bid' 'bid';
  'FINSI';
'FINSI';
'FINPRO'  ;
$$$$




















