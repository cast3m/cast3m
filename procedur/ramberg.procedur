$$$$ RAMBERG
* RAMBERG   PROCEDUR  VALETA    95/02/14    21:15:07     1480
'DEBP' RAMBERG  TAB1*'TABLE';
****************************************************
***** Determination des coef de Ramberg-Osgood *****
****************************************************

**************** Procedure RAMBERG ************************************
***** PARAMETRES D'ENTREE
***** -------------------
***** TAB1.'COURBE_TRACTION' : evolution de la courbe de traction SIGMA
*****                          en fct de EPSI
***** TAB1.'EPS0'   : flottant -> valeur de la deformation d'un point
*****                 de la courbe de traction.
***** TAB1.'SIG0'   : flottant -> valeur de la contrainte d'un point de la
*****                 courbe de traction.
*****
***** PARAMETRES DE SORTIE
***** ---------------------
***** TAB1.'COURBE_RAMBERG' : evolution de la courbe de traction
*****                         sous la forme Ramberg - Osgood
***** TAB1.'ALPHA' : coefficient alpha de la loi de Ramberg - Osgood
***** TAB1.'N' : valeur de la puissance de la loi de Ramberg - Osgood
***********************************************************************
COURBE = TAB1.'COURBE_TRACTION';
EPS0 = TAB1.'EPS0';
SIG0 = TAB1.'SIG0';

sigm = 'EXTR' COURBE 'ORDO';
epsi = 'EXTR' COURBE 'ABSC';
Nb = 'DIME' sigm;

***** Calcul des valeurs Ai,Bi et Nb
LA = 'LOG' ((epsi / eps0) - (sigm / sig0));
LB = 'LOG' ( sigm / sig0 );

***** Calcul des sommes
i    = 0;
SAi  = 0.;
SBi  = 0.;
SBi2 = 0.;
SABi = 0.;
SAi2 = 0.;
REPETER Boucle Nb;
   i    = i+1;
   Ai   = 'EXTR' LA i;
   Bi   = 'EXTR' LB i;
   SAi  = SAi + Ai;
   SBi  = SBi + Bi;
   SBi2 = SBi2 + (Bi ** 2);
   SAi2 = SAi2 + (Ai ** 2);
   SABi = SABi + (Ai * Bi);
'FIN' Boucle;
S2Bi = SBi ** 2;
S2Ai = SAi ** 2;

***** Calcul des coef Beta , n et Alpha
n = (SABi - ((SAi * SBi) / Nb)) / (SBi2 - (S2Bi / Nb));
Beta = (SAi - (n * SBi)) / Nb;
Alph = 'EXP' Beta ;

***** Courbe Ramberg - Osgood
epss = EPS0 * ((sigm / SIG0) + (Alph * (( sigm / SIG0)**n)));
COURBS = evol manu 'EPS' epss 'SIG' sigm;

***** Calcul du coefficient d'ajustement
aju = (SABi - ((SAi * SBi) / Nb)) / (SAi2 - (S2Ai / Nb));
aju = aju * n;

***** Sortie resultats
MESS 'ALPHA = ' Alph;
MESS 'n     = ' n;
MESS 'coefficient d ajustement = ' aju;
SI (AJU < 0.95);
   MESS 'ATTENTION COEFFICIENT D AJUSTEMENT < 0.95';
FINSI;

***** Stockage des resultats
TAB1.'ALPHA' = Alph;
TAB1.'N' =  n;
TAB1.'COURBE_RAMBERG' = COURBS;
TAB1.'AJUSTEMENT' = aju;

'FINP' TAB1;
