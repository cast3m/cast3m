$$$$ FPAL
* FPAL      PROCEDUR  MAGN      98/09/26    21:15:03     3310
*X FPAL (Procedure)
*
*  procedure  FPAL
*  ---------------
*
*    objet : calcule la vitesse de depot AK de particules microniques
*            en ecoulement laminaire
*
*            AK = (flux depose sur la paroi)/(concentration a l'infini)
*
*
*  SYNTAXE :    AK = FPAL  NU ROF UET NOR ROG RAP $PAROI ;
*  -------
*
*   AK    CHPOINT SCAL CENTRE PAROI  vitesse de depot (m/s)
*   NU    FLOTTANT                   viscosite cinematique du gaz (m2/s)
*   ROF   FLOTTANT                   masse volumique du gaz (kg/m3)
*   UET   CHPOINT SCAL CENTRE PAROI  vitesse de frottement (m/s)
*   NOR   CHPOINT VECT FACE          champ des normales aux faces (m)
*   ROG   POINT                      masse vol. part.x gravite (kg/m2s2)
*   RAP   FLOTTANT                   rayon des particules (m)
*   $PAROI MMODEL TYPE 'NAVIER_STOKES'   maillage paroi
*
*-----------------------------------------------------------------------

DEBPROC FPAL ;
ARGU NU*FLOTTANT ROF*FLOTTANT UET*CHPOINT NOR*CHPOINT ROG*POINT
     RAP*FLOTTANT $PAROI*MMODEL ;

* coefficient de correction de Cunningham pour les petites particules
* LL = libre parcors moyen des molecules de gaz
* KK = nombre de KNUDSEN

LL  = 6.53E-8 ;
KK  = LL/RAP ;
CUN = 1. + (1.257*KK) + (0.4*KK/(EXP(1.1/KK))) ;

*--------------------------------------- SEDIMENTATION -----------------
* vitesse de sedimentation incomplete

CRN  = CUN/(ROF*NU)      ;
VSI  = 2.*CRN*RAP*RAP/9. ;

* produit scalaire verticale.normale

NORP = KCHT $PAROI VECT CENTRE NOR ;
VSI2 = KCHT $PAROI VECT CENTRE ROG ;

AKI = PSCA  NORP VSI2 (MOTS UX UY) (MOTS UX UY) ;

* vitesse de depot par sedimentation

AKS = VSI*AKI ;

*--------------------------------------- DIFFUSION ---------------------
* coefficient de diffusion brownienne

DIF = 2.145E-22*CRN/RAP ;

* nombre de SCHMIDT

SC = NU/DIF ;

* epaisseur de la couche limite massique adimensionnelle (5<Cste<20)

YPLUS =  5./(SC**0.333) ;

* epaisseur de la couche limite massique dimensionnee

UET1 = UET**(-1.)    ;
YC = (YPLUS*NU)*UET1 ;

* vitesse de depot par diffusion

YC1 = YC**(-1.) ;
AKD = DIF*YC1 ;

*-------------------------- VITESSE DE DEPOT TOTALE --------------------

AK0 = AKS + AKD ;
AK = KCHT $PAROI SCAL CENTRE  AK0 ;

FINPROC AK ;



