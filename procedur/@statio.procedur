$$$$ @STATIO
* @STATIO   PROCEDUR  AM        98/12/30    21:15:23     3421
DEBP @STATIO ttt*TABLE ;

*-----------------------------------------------------------------------
* @STATIO : teste si le probleme est elastique et gere les iterations
*          - si la solution est elastique : arret du programme
*          - sinon : lancement de la boucle des iterations (ALGSTA)
*            jusqu'a atteindre le nombre maximal, ou bien jusqu'a la
*            convergence (. 'CONVERGENCE' . i = vrai)
*-----------------------------------------------------------------------

*** initialisation des tableaux de calcul

  ttt = @INITIA ttt ;

*** calcul du critere de plasticite pour le probleme a l'iteration 0

  ttt . 'CRITERE_PLASTICITE' . 0 = @CRIPL (ttt . 'MODELE')
        (ttt . 'CONTRAINTES' . 0) (ttt . 'VARIABLES_INTERNES' . 0)
        (ttt . 'CARACTERISTIQUES') 0. ;

*** test du critere de plasticite

  si (ttt . 'CRITERE_PLASTICITE' . 0 . PL) ;

*** probleme plastique : calcul stationnaire

  mess ' ' ;
  mess '***                CALCUL STATIONNAIRE                ***' ;
  mess ' ' ;

  repeter INCC (ttt . 'MAXITERATION') ;
    i = &INCC ;
    ttt = @ALGSTA i ttt ;
    si (ttt . 'CONVERGENCE' . i) ;
      quit INCC ;
    finsi ;
    si (i ega (ttt . 'MAXITERATION')) ;
      mess ' ' ; mess ' ' ;
      mess '*********************************************************';
      mess '           LE NB MAXI D ITERATIONS EST ATTEINT' ;
      mess '*********************************************************';
    finsi ;
  fin INCC ;

*** probleme elastique : arret du programme

  sinon ;
    mess '**** LA STRUCTURE NE PLASTIFIE PAS ****' ;
    mess '****        ARRET DU CALCUL        ****' ;
  finsi ;

FINP ;

