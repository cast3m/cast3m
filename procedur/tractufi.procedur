$$$$ TRACTUFI
* TRACTUFI  PROCEDUR  CB215821  15/12/02    21:15:16     8719
'DEBPROC' TRACTUFI ETAB*'TABLE';
************************************************************************
*                                                                      *
*         PROCEDURE TRANSFORMANT LA COURBE DE TRACTION                 *
*                                                                      *
*          (SIG,EPS) DU MATERIAU EN COURBE DE TRACTION                 *
*                                                                      *
*           EXPRIMEE EN TERMES DE VARIABLES GLOBALES                   *
*                                                                      *
*                       MOMENT/ROTATION                                *
*                                                                      *
************************************************************************
************************************************************************
*                                                                      *
*         LA PROCEDURE 'TRACTUFI' UTILISE :                            *
*                                                                      *
*      SOIT UNE BASE DE DONNEES EXPERIMENTALES                         *
*                - DEFR
*                                                                      *
*      SOIT DES METHODES SIMPLIFIEES                                   *
*                - TADA-PARIS                                          *
*                - LBB-NRC                                             *
*                - LBB1                                                *
*                - LBB2                                                *
*                                                                      *
*         ELLE FAIT APPEL AUX ARGUMENTS SUIVANTS:                      *
*                                                                      *
* ENTREES                                                              *
*                                                                      *
*    ETAB.'METHODE'   =  MOT CLE INDIQUANT LA METHODE EMPLOYEE         *
*    ETAB.'COUTRA'    =  COURBE DE TRACTION (SIG,EPS) DU MATERIAU      *
*    ETAB.'YOUN'      =  MODULE D'YOUNG                                *
*    ETAB.'SIG1'      =  CONTRAINTE POUR UNE DEFORMATION DE 0.002      *
* OU ETAB.'SIGF'      =  CONTRAINTE D'ECOULEMENT                       *
*    ETAB.'REXT'      =  RAYON EXTERIEUR DU TUYAU FISSURE              *
*    ETAB.'EPAI'      =  EPAISSEUR DU TUYAU FISSURE                    *
*    ETAB.'ANGLE'     =  ANGLE TOTAL DE LA FISSURE (EN DEGRES)         *
*    ETAB.'COUL'      =  COULEUR AFFECTEE A LA COURBE                  *
*    ETAB.'ALFA'      =  VALEURS FACULTATIVES PERMETTANT DE FITER      *
*    ETAB.'N'         =  LA COURBE DE TRACTION (METHODE LBBNRC)        *
*                                                                      *
*                                                                      *
* SORTIES                                                              *
*                                                                      *
*    MP               =  COURBE  (M,PHI)                               *
*    CM               =  COEFFICIENT DE COMPLAISANCE                   *
*    KF               =  POUR LE CALCUL DU FACTEUR D"INTENSITE DES     *
*                        CONTRAINTES                                   *
*                                                                      *
************************************************************************
*-----------------------------------------------------------------------
* RECEPTION DES ARGUMENTS

METHODE=ETAB.'METHODE';
COUTRA=ETAB.'COUTRA';E=ETAB.'YOUN';
REXT=ETAB.'REXT';EPAI=ETAB.'EPAI';AN=ETAB.'ANGLE';

'SI' ('OU' (AN < 0.) (AN > 360.));
     MESS 'ANGLE DE FISSURE IRREALISTE';
     'QUITTER' TRACTUFI;
'FINSI';

RAYOM=REXT - (EPAI / 2.D0);
THETA=AN / 2.D0;
XMSS0=4.D0 * RAYOM * RAYOM * EPAI *
 (('COS' (THETA / 2.D0)) - (0.5D0 * ('SIN' THETA)));
THETAD=THETA;
THETA=THETA * PI / 180.D0;

LEPS='EXTR' COUTRA ABSC;
LSIG='EXTR' COUTRA ORDO;
NCOURB='DIME' LSIG;
PM='PROG' NCOURB * 0.;
PPHI='PROG' NCOURB * 0.;
SIGM='EXTR' LSIG NCOURB;

'SI' ('EXISTE' ETAB 'SIGF');
    SIGF= ETAB . 'SIGF';
  'SINON';
    SIG1 = ETAB . 'SIG1';
    SIGF= (SIG1 + SIGM) / 2.;
'FINSI';
SIGY='EXTR' LSIG 2;
************************************************************************
*                                                                      *
* DETERMINATION DE LA COMPLAISANCE  CM  DE L'ELEMENT ET DU COEFFICIENT *
* KF                                                                   *
************************************************************************
RST=RAYOM / EPAI;
'SI' (RST < 10.D0) ;
   AXX=(0.125D0 * RST - 0.25D0) ** 0.25D0;
  'SINON';
   AXX=(0.4D0 * RST - 3.D0) ** 0.25D0;
'FINSI';
TSP=THETA / PI;
TSP1=TSP ** 1.5D0;
TSP2=TSP ** 4.24D0;
TSP3=TSP ** 3.D0;
TSP4=TSP ** 5.74D0;
TSP5=TSP ** 8.48D0;
FM=AXX * ((5.26D0 * TSP1) + (1.69D0 * TSP2));
FM=FM + 1.D0;
FM=FM +
  (AXX * AXX * ((8.46D0 * TSP3) + (6.28D0 * TSP4) + (1.33D0*TSP5)));
CM=2.D0 * THETA * THETA * FM;
CM=CM / E / PI / RAYOM / RAYOM / EPAI;

'SI' (THETAD < 100.);
      FF = 1. + (AXX * ((4.6 * (TSP ** 1.5)) +
                                 (2.64 * (TSP ** 4.24))));

'SINON';
      'MESS' 'ANGLE DE FISSURE TROP GRAND';
'FINSI';
 K1 = PI * RAYOM * THETA;
 K2 = K1 ** 0.5;
 K3 = K2 * FF;
 KF = K3 / PI / RAYOM / RAYOM / EPAI;



************************************************************************
*                                                                      *
*  DETERMINATION DES COEFFICIENTS DE LA LOI DE RAMBERG - OSGOOD        *
*  POUR LA METHODE LBB-NRC                                             *
*                                                                      *
************************************************************************
'SI' (('EXISTE' ETAB 'ALFA') ET ('EXISTE' ETAB 'N'));
          ALFA = ETAB.'ALFA';
          N = ETAB.'N';
'SINON';
   'SI' ('EGA' METHODE 'LBBNRC');
       EPSF='IPOL' SIGF LSIG LEPS;
       EPSY='EXTR' LEPS 2;SIGY='EXTR' LSIG 2;
       ALFA=EPSF / EPSY - 1.D0;
       I=1;
       'REPETER' BOUCR (NCOURB - 1);
         I=I + 1;
         SIG2='EXTR' LSIG I;
         'SI' (SIGF '<EG' SIG2);
            SIG1='EXTR' LSIG (I - 1);
            EPS2=('EXTR' LEPS I) - (('EXTR' LEPS I) / E);
            EPS1=('EXTR' LEPS (I - 1)) - (('EXTR' LEPS (I - 1)) / E);
            PENTE=(EPS2 - EPS1) / (SIG2 - SIG1);
            'QUITTER' BOUCR;
         'FINSI';
       'FIN' BOUCR;
       N=(SIGF * PENTE / ALFA / EPSY) + ((SIGF / SIGY - 1.D0) / ALFA);
       'SI' (('VALE' IMPI) 'EGA' 1);
          I=1;
          'REPETER' BOUCLE (NCOURB - 1);
              I=I + 1;
              VSIG='EXTR' LSIG I;
              RSIG=VSIG / SIGF;
              REPS=RSIG + (ALFA * (RSIG ** N));
              VEPS=EPSY * REPS;
              'REMP' PM I VSIG;'REMP' PPHI I VEPS;
          'FIN' BOUCLE;
          COURAM='EVOL' ROUG MANU 'EPS' PPHI 'SIG' PM;
          'DESS' (COUTRA ET COURAM) MARQ CARR ETOI;
       'FINSI';
   'FINSI';
'FINSI';
************************************************************************
*                                                                      *
*   DETERMINATION DU POINT CORRESPONDANT A LA LIMITE ELASTIQUE         *
*                                                                      *
************************************************************************
SIGY='EXTR' LSIG 2;
MY=XMSS0 * SIGY;
PHIY=CM * MY;
'REMP' PM 2 MY;'REMP' PPHI 2 PHIY;
************************************************************************
*                                                                      *
* POUR LA METHODE TADA-PARIS DETERMINATION DE THELIM ET BETA           *
*                                                                      *
************************************************************************
'SI' ('OU' ('OU' ('EGA' METHODE 'TADA') ('EGA' METHODE 'LBBNRC'))
 ('EGA' METHODE 'LBB1'));
    SP=4. * (('COS' (THETAD / 2.D0)) - (0.5D0 * ('SIN' THETAD))) / PI;
    THETA0=THETA;
    K=0;
   'REPETER' BOUC1 200;
       K=K + 1;
       TSP=THETA / PI;
       FZM=1.D0 + (AXX * ((4.6D0 * (TSP ** 1.5D0))
          + (2.64D0 * (TSP ** 4.24D0))));
       DFZM=AXX * ((6.9D0 * (TSP ** 0.5D0))
          + (2.64D0 * 4.24D0 * (TSP ** 3.24D0)));
       THELIM=THETA0 + ((THETA * FZM) / (FZM + (2.D0 * TSP * DFZM)));
      'SI' (((THELIM - THETA) / THETA) > 1.D-5);
         THETA=THELIM;
        'SINON';
         'QUITTER' BOUC1;
      'FINSI';
   'FIN' BOUC1;
   'SI' (K 'EGA' 200);
      'MESS' 'PAS DE CONVERGENCE POUR LA DETERMINATION DE THELIM';
      'QUITTER' TRACTUFI;
   'FINSI';
    THETAY=THELIM - THETA0;
    TSP=THELIM / PI;
    FZM=1.D0 + (AXX * ((4.6D0 * (TSP ** 1.5D0))
       + (2.64D0 * (TSP ** 4.24D0))));
    GZM=THELIM * FZM * FZM;
    DGZM=GZM / THETAY;
    BETA=(SP ** 2) * DGZM;
'FINSI';
************************************************************************
*                                                                      *
* POUR LA METHODE DEFR    DETERMINATION DE LA COURBE PHIPLSEL-EPSPLSEL *
*                                                                      *
************************************************************************
   'SI' ('EGA' METHODE 'DEFR');
*       ----------------------------------------------------------------

*       COURBES (EPSPL/EL,PHIPL/EL)

LP15=PROG 0. 0.67 2.67 4.54 5.31 6.06 6.87 8.05 9.14 10.69 11.43 13.37
 15.07 16.93 18.12 20.77 22.14 23.11 25.08 27.78 29.35 30.53 35.14 35.91
 36.51 37.28 39.21 39.57 40.34;
LP15=LP15 * 100.;

LE15=PROG 0. 0.69 2.02 3.14 3.48 3.79 4.13 4.45 4.75 5.19 5.35 5.69 6.03
 6.30 6.49 6.85 7.03 7.16 7.47 7.75 7.92 8.07 8.39 8.48 8.58 8.76 9.22
 9.43 9.79;
LE15=LE15 * 10.;

N=DIME LP15;
X1=EXTR LP15 (N-1);
X2=EXTR LP15 N;
Y1=EXTR LE15 (N-1);
Y2=EXTR LE15 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP15=LP15 ET (PROG X3);
LE15=LE15 ET (PROG 201.);

INT15=EVOL MANU 'PHIPLSEL' LP15 'EPSPLSEL' LE15;
*
LP30=PROG 0. 0.27 0.53 0.74 0.96 1.17 1.40 1.73 2.08 2.43 2.78 3.12 3.46
 3.91 4.48 5.07 5.64 6.32 6.99 7.57 8.26 8.82 9.27 9.62 10.78 11.56
 12.12 12.95 13.46 13.80 14.43 14.66 14.89 15.12;
LP30=LP30 * 100.;

LE30=PROG 0. 1.21 2.24 3.02 3.64 4.25 4.67 5.24 5.64 6.03 6.38 6.67 6.94
 7.36 7.88 8.33 8.73 9.29 9.82 10.08 10.58 11.04 11.45 11.71 12.73 13.34
 13.75 14.61 15.22 15.62 16.59 17.24 17.89 18.54;
LE30=LE30 * 10.;

N=DIME LP30;
X1=EXTR LP30 (N-1);
X2=EXTR LP30 N;
Y1=EXTR LE30 (N-1);
Y2=EXTR LE30 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP30=LP30 ET (PROG X3);
LE30=LE30 ET (PROG 201.);

INT30=EVOL ROUG MANU LP30 LE30;
*
LP45=PROG 0. 0.15 0.27 0.35 0.43 0.52 0.60 0.69 0.78 0.91 1.04 1.17 1.30
 1.43 1.56 1.69 1.78 2.00 2.13 2.43 2.60 2.77 3.00 3.26 3.43 3.56 3.79
 3.92 4.04 4.14 4.19 4.30 4.36 4.41 4.49 4.54;
LP45=LP45 * 100.;

LE45=PROG 0. 0.88 1.78 2.41 2.93 3.39 3.84 4.17 4.50 4.90 5.24 5.52 5.83
 6.18 6.43 6.67 6.86 7.26 7.53 7.91 8.20 8.50 8.87 9.19 9.36 9.63 9.88
 9.98 10.03 10.19 10.28 10.47 10.68 10.83 11.20 11.45;
LE45=LE45 * 10.;

N=DIME LP45;
X1=EXTR LP45 (N-1);
X2=EXTR LP45 N;
Y1=EXTR LE45 (N-1);
Y2=EXTR LE45 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP45=LP45 ET (PROG X3);
LE45=LE45 ET (PROG 201.);

INT45=EVOL ROUG MANU LP45 LE45;
*
LP60=PROG 0. 0.09 0.17 0.56 0.89 1.24 1.60 2.01 2.47 2.86 3.28 3.72 4.14
 4.68 5.07 5.46 5.88 6.34 6.75 7.19 7.63 8.02 8.65 9.04 9.40 10.20 10.79
 11.26 11.96 12.41 12.86 13.26;
LP60=LP60 * 10.;

LE60=PROG 0. 1.79 2.36 3.55 4.23 4.66 4.90 5.16 5.44 5.65 5.87 6.19 6.33
 6.62 6.84 6.99 7.23 7.54 7.80 8.06 8.26 8.44 8.74 8.94 9.23 10.15 10.79
 11.07 11.55 11.91 12.20 12.49;
LE60=LE60 * 10.;

N=DIME LP60;
X1=EXTR LP60 (N-1);
X2=EXTR LP60 N;
Y1=EXTR LE60 (N-1);
Y2=EXTR LE60 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP60=LP60 ET (PROG X3);
LE60=LE60 ET (PROG 201.);

INT60=EVOL ROUG MANU LP60 LE60;
*
LP75=PROG 0. 0.2 0.49 0.72 1.05 1.36 1.63 2.03 2.30 2.70 3.09 3.48 3.74
 4.12 4.50 4.87 5.25 5.50 5.96 6.28 6.53 6.79 6.98 7.18;
LP75=LP75 * 10.;

LE75=PROG 0. 0.70 1.12 1.95 2.53 3.14 3.55 4.06 4.34 4.76 5.16 5.51 5.74
 6.16 6.54 6.99 7.38 7.67 8.14 8.51 8.99 9.53 10.06 11.34;
LE75=LE75 * 10.;

N=DIME LP75;
X1=EXTR LP75 (N-1);
X2=EXTR LP75 N;
Y1=EXTR LE75 (N-1);
Y2=EXTR LE75 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP75=LP75 ET (PROG X3);
LE75=LE75 ET (PROG 201.);

INT75=EVOL ROUG MANU LP75 LE75;
*
LP90=PROG 0. 0.11 0.18 0.28 0.37 0.54 0.72 0.82 0.94 1.14 1.27 1.47 1.58
 1.68 1.77 1.90 1.99 2.10 2.28 2.37 2.45 2.57 2.79 3.04;
LP90=LP90 * 10.;

LE90=PROG 0. 1.44 2.29 3.09 3.69 4.35 4.95 5.15 5.38 5.75 5.92 6.23 6.41
 6.65 6.81 7.02 7.17 7.40 7.84 8.06 8.31 8.72 9.44 10.27;
LE90=LE90 * 10.;

N=DIME LP90;
X1=EXTR LP90 (N-1);
X2=EXTR LP90 N;
Y1=EXTR LE90 (N-1);
Y2=EXTR LE90 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP90=LP90 ET (PROG X3);
LE90=LE90 ET (PROG 201.);

INT90=EVOL ROUG MANU LP90 LE90;
*
LP120=PROG 0. 0.38 0.80 1.34 1.62 2.47 3.65 4.23 4.66 5.86 6.37 7.09
 7.83 8.48 9.11 10.41 11.83 12.32 12.90 13.47 14.70 15.63 16.58;

LE120=PROG 0. 0.43 1.14 1.82 2.08 3.04 3.96 4.23 4.42 4.86 5.07 5.34
 5.62 5.83 6.04 6.68 7.18 7.44 7.67 8.00 8.60 9.09 10.19;
LE120=LE120 * 10.;

N=DIME LP120;
X1=EXTR LP120 (N-1);
X2=EXTR LP120 N;
Y1=EXTR LE120 (N-1);
Y2=EXTR LE120 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP120=LP120 ET (PROG X3);
LE120=LE120 ET (PROG 201.);

INT120=EVOL ROUG MANU LP120 LE120;
*
LP150=PROG 0. 0.64 0.85 1.3 1.76 2.16 2.55 2.8 3.13 4.1 4.56 5.33 6.01
 6.43 7.06 7.63 8.08 8.48 8.93 9.78 10.56 10.79 11.24 11.69 12.87 13.75
 14.38;

LE150=PROG 0. 2.01 3. 3.63 4.23 4.69 5.1 5.38 5.67 6.12 6.34 6.78 7.2
 7.44 7.82 8.17 8.44 8.73 8.98 9.52 10.09 10.32 10.63 10.94 12.13 13.35
 15.20;
 LE150=LE150 * 10.;

N=DIME LP150;
X1=EXTR LP150 (N-1);
X2=EXTR LP150 N;
Y1=EXTR LE150 (N-1);
Y2=EXTR LE150 N;
P=(Y2 - Y1) / (X2 - X1);
X3=((201. - Y2) / P) + X2;
LP150=LP150 ET (PROG X3);
LE150=LE150 ET (PROG 201.);

INT150=EVOL ROUG MANU LP150 LE150;
*       ----------------------------------------------------------------
* RECHERCHE DE LA COURBE DE TRAVAIL
*       ----------------------------------------------------------------
TAB=TABLE;
J=15.;TAB.J=INT15;
J=30.;TAB.J=INT30;
J=45.;TAB.J=INT45;
J=60.;TAB.J=INT60;
J=75.;TAB.J=INT75;
J=90.;TAB.J=INT90;
J=120.;TAB.J=INT120;
J=150.;TAB.J=INT150;
NIND=8;
PARAM=AN;
COURBE=RESEAU TAB NIND PARAM;
FINSI;
************************************************************************
*                                                                      *
*   DETERMINATION DES AUTRES POINTS DE LA COURBE (M,PHI) :             *
*   UTILISATION DES DIFFERENTES METHODES                               *
*                                                                      *
************************************************************************
I=2;
'REPETER' BOUCLE (NCOURB - 2);
    I=I + 1;
    SIG='EXTR' LSIG I;
    SIG0=SIGY + ((SIGF - SIGY) * (SIG - SIGY) / (SIGM - SIGY));
    XM=XMSS0 * SIG0;
*   --------------------------------------------------------------------
*   --------------------------------------------------------------------

*   METHODE DE LA DEFORMATION DE REFERENCE

   'SI' ('EGA' METHODE 'DEFR');

LPAN='EXTR' COURBE ABSC;
LEAN='EXTR' COURBE ORDO;
*       ----------------------------------------------------------------
   EPS='EXTR' LEPS I;
   PHIELC=CM * XM / 2.D0;
   EPSEL=SIG / E;
   EPSPLSEL=EPS / EPSEL - 1.;
   'SI' (EPSPLSEL < 0.);
        PHI=PHIELC;
   'SINON';
        PHIPLSEL='IPOL' EPSPLSEL LEAN LPAN;
        PHI=(PHIPLSEL + 1.) * PHIELC;
   'FINSI';
    PHI=PHI * 2.D0;
*   --------------------------------------------------------------------
   'FINSI';
*   --------------------------------------------------------------------
*   --------------------------------------------------------------------
*   AUTRES METHODES

    EPS='IPOL' SIG0 LSIG LEPS;

*   METHODES TADA - PARIS, LBB-NRC, LBB1

   'SI' ('OU' ('OU' ('EGA' METHODE 'TADA') ('EGA' METHODE 'LBBNRC'))
    ('EGA' METHODE 'LBB1'));
       S=SIG0 / SIGF * SP;
       THETA=THETA0;
       K=0;
      'REPETER' BOUC2 200;
          K=K + 1;
          TSP=THETA / PI;
          FZM=1.D0 + (AXX * ((4.6D0 * (TSP ** 1.5D0))
             + (2.64D0 * (TSP ** 4.24D0))));
          DFZM=AXX * ((6.9D0 * (TSP ** 0.5D0))
             + (2.64D0 * 4.24D0 * (TSP ** 3.24D0)));
          HZM=2.D0 * TSP * DFZM;
          THETAF=(THETA0 - ((S ** 2) * THETA * FZM * HZM / BETA))
                / (1.D0 - ((S ** 2) * FZM * (FZM + HZM) / BETA));
         'SI' (ABS(((THETAF-THETA) / THETA)) > 1.D-5);
             THETA=THETAF;
            'SINON';
            'QUITTER' BOUC2;
         'FINSI';
      'FIN' BOUC2;
      'SI' (K 'EGA' 200);
         'MESS' 'PAS DE CONVERGENCE POUR LA DETERMINATION DE THETAF';
         'QUITTER' TRACTUFI;
      'FINSI';
       TSP=THETAF / PI;
       TSP1=TSP ** 1.5D0;
       TSP2=TSP ** 4.24D0;
       TSP3=TSP ** 3.D0;
       TSP4=TSP ** 5.74D0;
       TSP5=TSP ** 8.48D0;
       FM=AXX * ((5.26D0 * TSP1) + (1.69D0 * TSP2));
       FM=FM + 1.D0;
       FM=FM +
   (AXX * AXX * ((8.46D0 * TSP3) + (6.28D0 * TSP4) + (1.33D0 * TSP5)));
       CMEFF=2.D0 * THETA * THETA * FM;
       CMEFF=CMEFF / E / PI / RAYOM / RAYOM / EPAI;
       PHI=CMEFF * XM;
*      -----------------------------------------------------------------
      'SI' ('EGA' METHODE 'LBBNRC');
          COEF=ALFA * (S ** (N - 1.D0));
          PHI=(COEF + 1.D0) * PHI;

      'FINSI';
*      -----------------------------------------------------------------
      'SI' ('EGA' METHODE 'LBB1');
          EPSE=SIG0 / E;
          EPSP=EPS - EPSE;
          COEF=EPSP / EPSE;
          PHI=(COEF + 1.D0) * PHI;
      'FINSI';
*      -----------------------------------------------------------------
   'FINSI';
*   --------------------------------------------------------------------
*   --------------------------------------------------------------------

*   METHODES LBB2

   'SI' ('EGA' METHODE 'LBB2');
       PHI=CM * XM;
       EPSE=SIG0 / E;
       EPSP=EPS - EPSE;
       COEF=EPSP / EPSE;
       PHI=(COEF + 1.D0) * PHI;
   'FINSI';
*   --------------------------------------------------------------------
   'REMP' PM I XM;
   'REMP' PPHI I PHI;
'FIN' BOUCLE;
'SI' ('EXISTE' ETAB 'COUL');COUL=ETAB.'COUL';
    MP='EVOL' COUL MANU 'ROTATION' PPHI 'MOMENT' PM;
   'SINON';
    MP='EVOL' MANU 'ROTATION' PPHI 'MOMENT' PM;
'FINSI';
*
'FINPROC' MP CM KF;

