$$$$ REPART
* REPART    PROCEDUR  CHLOE     99/03/18    21:15:47     3528
debproc repart  TPARI*table xi*flottant;
*
*xi flottant
*
*tpari table contenant les parametres  de la va
*
*sortie rep valeur de la fonction de répartition en xi
*
*loi rectangulaire
TVAI =tpari . typva;
SI (EGA TVAI loi_uniforme);
 A=TPARI . A;
 B=TPARI . B;
 SI (<EG XI A);
  REP=0.;
  quitter repart;
 SINON;
  SI (>EG XI B);
   REP=1.;
  quitter frepart;
  SINON;
   SI (ET (> XI A) (< XI B));
    REP = XI - A / ( B - A );
  quitter repart;
   FINSI;
  FINSI;
 FINSI;
FINSI;
*
*loi laplace
SI (EGA TVAI loi_de_laplace);
 SI (>EG XI 0.);
  REP=2. - (EXP (0. - XI)) /2;
  quitter repart;
 SINON;
  REP= EXP XI /2;
  quitter repart;
 FINSI;
FINSI;
*
*loi normale standard
SI (EGA TVAI loi_normale_standard);
  REP=1 - (erf (-0.7071067811865475 * Xi )) * 0.5;
  quitter repart;
FINSI;

*loi exponentielle
SI (EGA TVAI loi_exponentielle);
 LBDA=TPARI . LAMBDA;
 MU= TPARI . MU;
 SI (< XI MU);
  REP=0.;
 SINON;
  REP=1. - (EXP (LBDA * (MU - XI)));
 FINSI;
  quitter repart;
FINSI;
*
*loi lognormale
SI (EGA TVAI loi_lognormale);
      SI (<EG XI 0.);
         REP=0.;
      SINON;
         MOY = TPARI . MOYENNE ;
         FSIG= TPARI . ECART_TYPE;
         MOY2 = MOY * MOY;
         FSIG2=FSIG * FSIG;
*N pour ecarttype et moyenne de la loi normale associe
         NSIG2=log((MOY2+FSIG2)/MOY2);
         NSIG = NSIG2 ** 0.5;
         NMOY = log(moy2/((MOY2+FSIG2)** 0.5));
         aux=(log xi - NMOY) / NSIG;
         rep =1 - (erf (-0.7071067811865475 * aux )) * 0.5;
      FINSI;
      quitter repart;
FINSI;
*
*loi normale
SI (EGA TVAI loi_normale);
    MOY = TPARI . MOYENNE ;
    FSIG= TPARI . ECART_TYPE;
    AUX=(XI-MOY)/FSIG;
    rep =1 - (erf (-0.7071067811865475 * aux )) * 0.5;
    quitter repart;
finsi;
*
*loi weibull min
SI (EGA TVAI loi_weibull_min);
 TAU= TPARI . TAU;
 K= TPARI . K;
 W= TPARI . W;
 SI (<EG XI TAU);
  REP=0.;
 SINON;
  AUX=XI-TAU/(W-TAU);
  REP=1. - (EXP (0. - (AUX ** K)));
 FINSI;
 quitter repart;
FINSI;
*
*loi normale tronquee
SI (EGA TVAI loi_normale_tronquee);
 MM  = TPARI . MOYENNE;
 SSIG= TPARI . ECART_TYPE;
 MMIN= TPARI . MIN;
 MMAX= TPARI . MAX;
 SI (<EG XI MMIN);
  REP=0.;
 SINON;
  SI (>EG XI MMAX);
   REP=1.;
  SINON;
   AUX=(MMIN-MM)/SSIG;
   AUX1=1 - (erf (-0.7071067811865475 * aux )) * 0.5;
   AUX=(MMAX-MM)/SSIG;
   AUX2=1 - (erf (-0.7071067811865475 * aux )) * 0.5;
   AUX=(XI-MM)/SSIG;
   AUX3=1 - (erf (-0.7071067811865475 * aux )) * 0.5;
   REP=(AUX3-AUX1)/(AUX2-AUX1);
  FINSI;
 FINSI;
 quitter repart;
FINSI;
*
*loi exponentielle tronquee
SI (EGA TVAI loi_exponentielle_tronquee);
 LBDA=TPARI . LAMBDA;
 MU=  TPARI . MU  ;
 MIN= TPARI . MIN;
 MAX= TPARI . MAX;
 SI (<EG XI MIN);
  REP=0.;
 SINON;
  SI (>EG XI MAX);
   REP=1.;
  SINON;
   SI (< XI MU);
    REP=0.;
   SINON;
    SI (< MIN MU);
     AUX1=0.;
    SINON;
     AUX1=1. - (EXP (LBDA*(MU-MIN)));
    FINSI;
    SI (< MAX MU);
     AUX2=0.;
    SINON;
     AUX2=1. - (EXP (LBDA*(MU-MAX)));
    FINSI;
    SI (< XI MU);
     AUX3=0.;
    SINON;
     AUX3=1. - (EXP (LBDA*(MU - Xi)));
    FINSI;
    REP=AUX3-AUX1/(AUX2-AUX1);
   FINSI;
  FINSI;
 FINSI;
 quitter repart;
FINSI;
*
**loi gumbel max
SI (EGA TVAI loi_gumbel_max);
 LBDA=TPARI . LAMBDA;
 MU=  TPARI . MU    ;
 AUX=LBDA * (XI - MU);
 REP=EXP (0. - (EXP (0. - AUX)));
 quitter repart;
FINSI;
*
*loi normale tronquee inf
SI (EGA TVAI loi_normale_tronquee_inf);
 MM  = TPARI . MOYENNE;
 SSIG= TPARI . ECART_TYPE;
 MMIN= TPARI . MIN;
 SI (<EG XI MMIN);
  REP=0.;
 SINON;
  AUX=(MMIN-MM)/SSIG;
  AUX1=1 - (erf (-0.7071067811865475 * aux )) * 0.5;
  AUX=(XI-MM)/SSIG;
  AUX3=1 - (erf (-0.7071067811865475 * aux )) * 0.5;
  REP=(AUX3-AUX1)/(1. - AUX1);
 FINSI;
 quitter repart;
FINSI;
*
* Loi de Frechet
SI (EGA TVAI loi_de_frechet);
  U = TPARI . U;
  K = TPARI . K;
  B = TPARI . B;
  SI (<EG XI B);
    REP = 0.;
  SINON;
    XAUX = ((U - B) / (XI - B)) ** K;
    REP = EXP (-1. * XAUX);
  FINSI;
 quitter repart;
FINSI;
*
FINPROC REP;

