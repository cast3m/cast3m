$$$$ VITETFOR
* VITETFOR  PROCEDUR  AM        96/09/09    21:15:09     2269
'DEBPROC' VITETFOR   MTA*'TABLE' ETAB12*'TABLE'
                     FCORU*'CHPOINT' ;
*
* MTA   table de la rigidite associee  au contact
*
  FLAG = VRAI ;
  MAN = ETAB12.'FMAN' ;
  MAN = 'CHAN' 'ATTRIBUT' MAN 'NATURE' 'DISCRET' ;
*
  MDI = 'DIME' MTA;
 'SI' ( 'NEG' MDI 0) ;
    A = MTA.7 ;
    LISEA = MTA.6;


    MALI = 'MAXI' LISEA;
   'SI' ( 'EGA' MALI 1 ) ;
      GEO1 = 'EXTR' A 'MAIL' 'MULT' ;
*
*  on recupere quelques quantites necessaires
*
      H = ETAB12.'DT' ;
      H2 = H * H ;

*------
*
*  calcul de l'operateur
*
      ATOT = ETAB12.'MASSE'  'ET' A ;
*
*  calcul du 1-er second membre et de COE1
*
      FU =  ( -0.25 * H2 ) * MAN;

      VDT =  ( H * ETAB12.'VITI')  +  ETAB12.'DEPI' ;
      VDT = 'CHAN' 'ATTRIBUT' VDT 'NATURE' 'DIFFUS' ;
      FL = 'EXCO' ( A * VDT) 'FLX' 'FLX'  ;
      FL = FL - ( 'REDU' FCORU GEO1 ) ;
      FT = FL 'ET' FU ;
      COE1 = 'RESO'  ATOT FT 'NOUNIL' ;
*
*  calcul du 2-eme second membre et de COE2
*
      FL =  'EXCO' ( A * ETAB12.'VITI' ) 'FLX' 'FLX' ;
      FU =  (  -0.5 * H ) * MAN;
      FT = FL 'ET' FU ;
      COE2 = 'RESO'  ATOT FT 'NOUNIL' ;
*
*   calcul des vecteurs VADD et FADD
*
      DTL =  (( 2. / H ) * COE1 ) - COE2 ;
      DTLL = 'REAC' A  DTL ;
      VADD =  'RESO' ETAB12.'MASSE'  DTLL  ;
      FADD = (-4./ H2 ) *  ( 'REAC' A COE1) ;
      'SI' ( 'EXIS' ETAB12 'AMORTISSEMENT' ) ;
           FADD = FADD - (ETAB12.'AMORTISSEMENT' * VADD );
      'FINSI' ;
       FLAG = FAUX ;
   'FINSI';
 'FINSI';
*
'SI' FLAG ;
  VADD = 0. * ETAB12.'VITI' ;
  FADD = 0. * MAN ;
'FINSI' ;
VADD =  'ENLE' VADD 'LX' ;
*

'FINPROC' VADD FADD ;




