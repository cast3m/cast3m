$$$$ @PRCA3
* @PRCA3    PROCEDUR  MB234859  18/11/21    21:15:02     10007
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*                    PROCEDURE DE CALCUL @PRCA3
*
*   Calcul du profil d usure : DeltaH = alpha * beta * phi
*
* Entrees :
*   T1    : Table de PASAPAS
*   ID_BU : Entier donnant le numero de la BOITE D'USURE a considerer
*
* Sortie :
*   T1    : Table de PASAPAS avec les booleens mis a jour
*
* Appelee par @EVN_MNG
*
***********************************************************************
DEBP @PRCA3 T1*'TABLE' ID_BU*'ENTIER';

* Booleen pour le trace
  OPEvUsCu = FAUX ;

  BOITE_US = T1.'BOITES_USURES'. ID_BU       ;
  ALPHA    = BOITE_US.'COEFFICIENT_USURE'    ;
  BETA     = BOITE_US.'FACTEUR_ACCELERATION' ;
  SUR_APPL = BOITE_US.'SURFACE_APPLICATION'  ;
  NCNECC   = BOITE_US.'NCNECC'  ;

* Usure obtenue par le produit : Energie Dissipee * Alpha * Beta
* Avec Alpha le coefficient d'usure et Beta le facteur d'acceleration
  AmplFctr = MANU 'CHPO' SUR_APPL 1 'SCAL' (ALPHA*BETA) ;

* Cas Usure cyclique
  SI (BOITE_US.'USURE_CYCLIQUE') ;
    EnerDiss = BOITE_US.'ENERGIE_DISSIPEE_SUR_CYCLE'.  NCNECC ;
    SI (NCNECC EGA 1) ;
      CumuUsU2 = MANU 'CHPO' SUR_APPL 1 'UU2' 0.0 ;
    SINON ;
      CumuUsU2 = BOITE_US.'USURE_CUMULEE'. (NCNECC - 1) ;
    FINSI ;
* Cas Usure incrementale
  SINON ;
    Inc = BOITE_US.'INDICE-STOCKAGE' ;
    EnerDiss = BOITE_US.'ENERGIE_DISSIPEE_SUR_INCREMENT'. Inc ;
    FirstInc = (BOITE_US.'EntreeNC') ET (NON (BOITE_US.'TermineC')) ;
    SI FirstInc ;
      CumuUsU2 = MANU 'CHPO' SUR_APPL 1 'UU2' 0.0 ;
    SINON ;
      CumuUsU2 = BOITE_US.'USURE_CUMULEE'. (Inc - 1) ;
    FINSI ;
  FINSI ;
  NouvUsU2 = AmplFctr * EnerDiss ('MOTS' 'SCAL') ('MOTS' 'ED')
                                 ('MOTS' 'UU2') ;

  CumuUsU2 = CumuUsU2 + NouvUsU2 ;

  EvNouvUs = EVOL 'JAUN' 'CHPO' NouvUsU2 SUR_APPL ;
  EvNouvUs = EXTR EvNouvUs 'COUR' 1 ;
  EvNouvUs = CHAN EvNouvUs 'LEGE' 'Usure Nouvelle' ;

  EvCumuUs = EVOL 'BLEU' 'CHPO' CumuUsU2 SUR_APPL ;
  EvCumuUs = EXTR EvCumuUs 'COUR' 1 ;
  EvCumuUs = CHAN EvCumuUs 'LEGE' 'Usure Cumulee' ;

  SI OPEvUsCu ;
    DESS (EvNouvUs ET EvCumuUs) 'LEGE' ;
  FINSI ;

  BOITE_US.'USURE_APPLIQUEE'     . NCNECC = NouvUsU2 ;
  BOITE_US.'USURE_CUMULEE'       . NCNECC = CumuUsU2 ;
  BOITE_US.'EVO_USURE_APPLIQUEE' . NCNECC = EvNouvUs ;
  BOITE_US.'EVO_USURE_CUMULEE'   . NCNECC = EvCumuUs ;

FINP T1 ;
$$$$

