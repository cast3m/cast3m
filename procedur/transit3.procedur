$$$$ TRANSIT3
* TRANSIT3  PROCEDUR  FANDEUR   14/10/10    21:15:27     8178
* TRANSIT3  PROCEDUR  JEANV     94/10/10    21:51:02     1285
'DEBPROC' TRANSIT3 ETAB*'TABLE   ' ;
*----------------------------------------------------------------------*
*                                                                      *
*                             T R A N S I T 3                          *
*                             ---------------                          *
*                                                                      *
*     RESOLUTION D'UN PROBLEME DE THERMIQUE TRANSITOIRE                *
*     METHODE A DEUX PAS DE TEMPS ( ALGORITHME DE DUPONT2 )            *
*                                                                      *
*     ETAB, TABLE CONTENANT EN ENTREE :                                *
*                                                                      *
*     INDICE 'SOUSTYPE'     THERMIQUE                                  *
*     INDICE 'INITIAL(0)'   CHAMP DE TEMPERATURE INITIAL AU PAS 0      *
*     INDICE 'INITIAL(1)'   CHAMP DE TEMPERATURE INITIAL AU PAS 1      *
*                           ( LA DONNEE DE CE CHAMP EST FACULTATIVE )  *
*     INDICE 'COQUE'        TYPE D'ELEMENT COQUE UTILISE               *
*     INDICE 'EPAI '        EPAISSEUR DE LA COQUE                      *
*     INDICE 'PEAU'         PEAU SUR LAQUELLE ECHANGE DE SURFACE       *
*                           S'EFFECTUE DANS LE CAS DES  COQUE ('INFE'  *
*                           OU 'SUPE')                                 *
*     INDICE 'CONDUCTIVITE' MATRICE DE CONDUCTIVITE                    *
* OU  INDICE 'TABCOND'      TABLE DES OBJETS D'EVOLUTION K(T) INDICEE  *
*                           PAR LES OBJETS MMODELS DES SOUS-ZONES      *
*     INDICE 'CAPACITE'     MATRICE DE CAPACITE                        *
* OU  INDICE 'TABCAPA'      TABLE DES OBJETS D'EVOLUTION CRHO(T) INDICEE
*                           PAR LES OBJETS MMODELS DES SOUS-ZONES      *
*     INDICE 'MODELE'       OBJET MODELE DE L'ENSEMBLE                 *
*     INDICE 'IMPOSE'       VALEURS IMPOSEES ( CHPOINT OU CHARGEMENT ) *
*     INDICE 'BLOCAGE'      MATRICE DE BLOCAGE                         *
*     INDICE 'FLUX'         CHARGEMENT DES FLUX EQUIVALENTS            *
*     INDICE 'CONVECTION'   TABLE DES TABLES POUR UNE CONDITION        *
*                           DE CONVECTION :                            *
*            'TABCONV1'     TABLE DES COEFFICIENTS D'ECHANGE OU TABLE  *
*                           DES EVOLUTIONS DU COEFFICIENT D'ECHANGE    *
*                           FONCTION DE LA TEMPERATURE H(T) INDICEE    *
*                           PAR LES OBJETS  MMODELS CORRESPONDANT AUX  *
*                           DIFFERENTES SURFACES DE CONVECTION         *
*            'TABTE1'       TABLE DES TEMPERATURES EXTERIEURES         *
*                           ( FLOTTANTS OU CHPOINTS ) OU TABLE DES     *
*                           EVOLUTIONS DE LA TEMPERATURE EXTERIEURE    *
*                           FONCTION DE LA TEMPERATURE DE LA SURFACE   *
*                           DE CONVECTION TE(T) INDICEE PAR LES OBJETS *
*                           MMODELS CORRESPONDANT AUX   DIFFERENTES    *
*                           SURFACES DE CONVECTION                     *
*     OU                                                               *
*            'TABCONV2'     TABLE DES COEFFICIENTS D'ECHANGE OU TABLE  *
*                           DES EVOLUTIONS DU COEFFICIENT D'ECHANGE    *
*                           FONCTION DU TEMPS H(TEMPS) INDICEE PAR     *
*                           LES OBJETS  MMODELS CORRESPONDANT AUX      *
*                           DIFFERENTES SURFACES DE CONVECTION         *
*            'TABTE2'       TABLE DES TEMPERATURES  EXTERIEURES        *
*                           ( FLOTTANTS OU CHPOINTS ) OU TABLE DES     *
*                           DES EVOLUTIONS DE LA TEMPERATURE EXTERIEURE*
*                           FONCTION DU TEMPS :TE(TEMPS) INDICEE PAR   *
*                           LES OBJETS  MMODELS CORRESPONDANT AUX      *
*                           DIFFERENTES  SURFACES DE CONVECTION        *
*     INDICE 'PHASE'        TABLE TAB1 POUR L'OPERATEUR CAPACITE       *
*     INDICE 'TINI'         TEMPS INITIAL ( TINI=0. PAR DEFAUT )       *
*     INDICE 'PAS'          VALEUR DU PAS DE TEMPS                     *
*     INDICE 'NPAS'         NOMBRE DE PAS DE CALCUL                    *
*                           ( NPAS=1 PAR DEFAUT )                      *
*     INDICE 'NSOR'         ON VEUT UN RESULTAT TOUS LES NSOR PAS      *
*                           ( NSOR=1 PAR DEFAUT )                      *
*     INDICE 'COEF'         VALEUR DU COEFFICIENT DE RELAXATION        *
*                           (VALEUR PAR DEFAUT 0.25)                   *
*     INDICE 'REPRISE'      'OUI' POUR UNE REPRISE DE CALCUL           *
*                                                                      *
*     ETAB CONTENANT EN SORTIE :                                       *
*                                                                      *
*     INDICE NUMPAS NUMERO DU PAS DE SORTIE                            *
*     LA TABLE ( ETAB NUMPAS ) CONTENANT :                             *
*     INDICE 'TEMPERATURE'  LE CHAMP DE TEMPERATURE                    *
*     INDICE 'INSTANT'      L'INSTANT DE SORTIE                        *
*                                                                      *
*     D.R., LE 29 JUIN 1988. VERSION DU 18 JANVIER 1989.               *
*                                                                      *
*----------------------------------------------------------------------*
'REPETER' PROC 1 ;
***
'SAUTER' 1 'LIGNE' ;
'MESS' '  Calcul transitoire non linéaire méthode DupontII' ;
***
*
*---  RECUPERATION DE L'INFORMATION CONTENUE DANS "ETAB"
*
'SI' ( 'NEG' ( ETAB.'SOUSTYPE' ) 'THERMIQUE' ) ;
   'MESS' 'SOUS TYPAGE DE LA TABLE EN ENTREE INCORRECT|' ;
   'QUITTER' PROC ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'NIVEAU' ) ;
   NIV_MESS = 'ENTIER' ( ETAB.'NIVEAU' ) ;
'SINON' ;
   NIV_MESS = 0 ;
'FINSI' ;
'SI' ( NIV_MESS '>EG' 1 ) ;
   'SAUTER' 1 'LIGNE' ;
   'MESS' '***  DEBUT DE LA PROCEDURE "TRANSIT3"  ***' ;
'FINSI' ;
T_REP = 0 ;NREP = 0 ;IREP = 0 ;ITEM = 0 ;
'SI' ( 'EXISTE' ETAB 'REPRISE' ) ;
   'REPETER' BOUC_REP ;
      IREP = IREP + 1 ;
      'SI' ( 'NON' ( 'EXISTE' ETAB IREP ) ) ;
         'QUITTER' BOUC_REP ;
      'FINSI' ;
   'FIN' BOUC_REP ;
   NREP = IREP - 1 ;
   U0_1 = ETAB . ( NREP - 1 ) . 'TEMPERATURE' ;
   U0_0 = ETAB.NREP.'TEMPERATURE' ;
   T_REP = ETAB.NREP.'INSTANT' ;
   T_INI = T_REP ;
   DTREP = T_REP - ETAB.(NREP - 1).'INSTANT';
   'SI' ( ABS(DTREP - (ETAB.'PAS')) >EG 1.e-6 ) ;
       'ERREUR' 'Conditions de reprise impossibles' ;
       'QUITTER' PROC ;
   'FINSI' ;
'SINON' ;
   U0_1     = 'COPIER' ( ETAB.'INITIAL(0)' ) 'GEOM' ;
   'SI' ( 'EXISTE' ETAB 'INITIAL(1)' ) ;
      U0_0     = 'COPIER' ( ETAB.'INITIAL(1)' ) 'GEOM' ;
   'SINON' ;
      U0_0 = U0_1 ;
   'FINSI' ;
   'SI' ( 'EXISTE' ETAB 'TINI' ) ;
      T_INI = ETAB.'TINI' ;
   'SINON' ;
      T_INI = 0. ;
   'FINSI' ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
   TC_TPS = ETAB.'FLUX' ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'CONDUCTIVITE' ) ;
   MAT_COND = ETAB.'CONDUCTIVITE' ;
'SINON' ;
   'SI' ( 'EXISTE' ETAB 'TABCOND' ) ;
      TAB_COND = ETAB.'TABCOND' ;
   'SINON' ;
      'SAUTER' 1 'LIGNE' ;
      'MESS' '*** LA DONNEE DE LA CONDUCTIVITE EST INCORRECTE | ***' ;
      'QUITTER' PROC ;
   'FINSI' ;
'FINSI' ;
*
'SI' ( 'EXISTE' ETAB 'PHASE' ) ;
   TABP2 = ETAB.'PHASE' ;
   'MESS' '    avec changement de phase' ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'BLOCAGE' ) ;
   MAT_BLOC = ETAB.'BLOCAGE'  ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'IMPOSE' ) ;
   VAL_IMP  = ETAB.'IMPOSE'   ;
'FINSI' ;
TYP_IMP = 'TYPE' VAL_IMP ;
'SI' ( 'NEG' TYP_IMP 'CHARGEME' ) ;
   VAL_IMPO = VAL_IMP ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'COEF' ) ;
   LAMBDA   = ETAB.'COEF' ;
'SINON' ;
   LAMBDA   = 0.25 ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'MODELE' ) ;
   OBJ_MOD = ETAB.'MODELE' ;
   MOD_CAPA=OBJ_MOD;
'FINSI' ;
 'SI' ('EXISTE' ETAB 'COQUE') ;
       TYP_COQ = ETAB.'COQUE' ;
       'SI' ( 'EXISTE' ETAB 'EPAI' )  ;
          EP = ETAB.'EPAI' ;
       'SINON' ;
         'SAUTER' 1 'LIGNE' ;
         'MESS' '*** IL MANQUE L EPAISSEUR DE LA COQUE | *** ';
         'QUITTER' PROC ;
       'FINSI';
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'CAPACITE' ) ;
   MAT_CAPA = ETAB.'CAPACITE' ;
'SINON' ;
   'SI' ( 'EXISTE' ETAB 'TABCAPA' ) ;
      TAB_CAPA = ETAB.'TABCAPA' ;
   'SINON' ;
      'SAUTER' 1 'LIGNE' ;
      'MESS' '*** LA DONNEE DE LA CAPACITE EST INCORRECTE | ***' ;
      'QUITTER' PROC ;
   'FINSI' ;
'FINSI' ;
*
*---  GESTION DE LA CONDITION DE CONVECTION FORCEE
*+*
'SI' ( 'EXISTE' ETAB 'CONVECTION' ) ;
  'SI' ( 'EXISTE' ETAB 'COQUE' ) ;
     'SI' ( 'EXISTE' ETAB 'PEAU' ) ;
       PE = ETAB.'PEAU' ;
     'SINON' ;
      'MESS' '*** PRECISER LA PEAU | ***' ;
      'QUITTER' PROC ;
     'FINSI' ;
  'FINSI' ;
   CTAB = ETAB.'CONVECTION' ;
   'SI' ('EXISTE' CTAB 'TABCONV1') ;
       IT=1 ;
       TAB_CON1=CTAB.'TABCONV1'  ;
       'SI' ('EXISTE' CTAB 'TABTE1') ;TAB_TE1 = CTAB.'TABTE1'  ;
       'SINON' ;TAB_TE1 = TABLE ;FINSI ;
    'SINON' ;
       IT=2 ;
       'SI' ('EXISTE' CTAB 'TABTE2') ;TAB_TE2 = CTAB.'TABTE2'  ;
       'SINON' ;TAB_TE2 = TABLE ;'FINSI' ;
       TAB_CON2 = CTAB.'TABCONV2'  ;
    'FINSI'  ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'NPAS' ) ;
   NBRE_PAS = 'ENTIER' ( ETAB.'NPAS' ) ;
'SINON' ;
   NBRE_PAS = 1 ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'NSOR' ) ;
   N_SOR = 'ENTIER' ( ETAB.'NSOR' ) ;
'SINON' ;
   N_SOR = 1 ;
'FINSI' ;
'SI' ( 'EXISTE' ETAB 'PAS' ) ;
   T_PAS = ETAB.'PAS' ;
'SINON' ;
   'SAUTER' 1 'LIGNE' ;
   'MESS' 'IL MANQUE LE PAS DE TEMPS |' ;
   'OBTENIR' T_PAS*'FLOTTANT' ;
'FINSI' ;
T0_0 = T_INI ;
'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
   QTPS_0 = 'TIRER' TC_TPS  T0_0 ;
'FINSI' ;
T0_1 = T_INI + T_PAS ;
'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
   QTPS_1 = 'TIRER' TC_TPS  T0_1 ;
'FINSI' ;
*
*---  "ETAB" EST UNE TABLE DE TABLES, INDICEE PAR LE NUMERO DU PAS
*
NUM_PAS = NREP ;
'SI' ( 'NON' ( 'EXISTE' ETAB REPRISE ) ) ;
   STN = 'TABLE'  ;
   ETAB.NUM_PAS      = 'TABLE' STN ;
   STN.'INSTANT'     = T0_0        ;
   STN.'TEMPERATURE' = U0_0        ;
   'SI' ( NIV_MESS '>EG' 1 ) ;
      'SAUTER' 1 'LIGNE' ;
      'MESS' 'Sortie du resultat pour le temps   :' T0_0    ;
      'MESS' 'Numéro du pas de sortie            :' NUM_PAS ;
      'SI' ( NIV_MESS '>EG' 2 ) ;
         'SAUTER' 1 'LIGNE' ;
         'MESS' 'CHAMP DE TEMPERATURE AU TEMPS      :' T0_0 ;
         'LISTE' U0_0 ;
      'FINSI' ;
   'FINSI' ;
   NUM_PAS = 1    ;
   STN = 'TABLE'  ;
   ETAB.NUM_PAS      = 'TABLE' STN ;
   STN.'INSTANT'     = T0_1        ;
   STN.'TEMPERATURE' = U0_1        ;
   'SI' ( NIV_MESS '>EG' 1 ) ;
      'SAUTER' 1 'LIGNE' ;
      'MESS' 'Sortie du resultat pour le temps   :' T0_1    ;
      'MESS' 'Numéro du pas de sortie            :' NUM_PAS ;
      'SI' ( NIV_MESS '>EG' 2 ) ;
         'SAUTER' 1 'LIGNE' ;
         'MESS' 'CHAMP DE TEMPERATURE AU TEMPS      :' T0_1 ;
         'LISTE' U0_1 ;
      'FINSI' ;
   'FINSI' ;
   ITEM = 1 ;
'FINSI' ;
IPAS = 0 ;
*
*---  BOUCLE SUR LES PAS DE TEMPS
*
'REPETER' BOUC_SOR ;
*
         'SAUTER' 1 'LIGNE' ;
         'MESS' 'Numéro du pas                      :' NUM_PAS ;
*
   ITEM = ITEM + 1 ;
   IPAS = IPAS + 1 ;
   'SI' ('EXISTE' ETAB 'COQUE' ) ;
     U0_T  = 'ENLEVE' U0_1 'LX'  ;
   'SINON' ;
     U0_T = 'EXCO' 'T' U0_1 'NOID' 'T' ;
   'FINSI' ;
*
*---  TEST SI FIN DE BOUCLE
*
   'SI' ( ITEM '>' NBRE_PAS ) ;
      'QUITTER' BOUC_SOR ;
   'FINSI' ;
   T1 = T_INI + ( T_PAS * ITEM ) ;
   'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
      FLUX_TPS = 'COLI' QTPS_0 0.5 QTPS_1 0.5 ;
   'FINSI' ;
   'SI' ( 'EGA' TYP_IMP 'CHARGEME' ) ;
      VAL_IMPO = 'TIRER' VAL_IMP  T1 ;
   'FINSI' ;
   'SI' ( 'EXISTE' ETAB 'TABCOND' ) ;
*
*---  CALCUL DE LA MATRICE DE CONDUCTIVITE
*
      U1_CALC = 'COLI' U0_0 1.5 U0_1 -0.5      ;
      'SI' ('EXISTE' ETAB 'COQUE' ) ;
         UC1_TI = 'EXCO' 'TINF' U1_CALC 'NOID' 'T' ;
         UC1_TS = 'EXCO' 'TSUP' U1_CALC 'NOID' 'T' ;
         UC1_TM = 'EXCO' 'T   ' U1_CALC 'NOID' 'T' ;
         UC1_T  = (UC1_TI + UC1_TS +UC1_TM )/3.0  ;
       'SINON';
        UC1_T   = 'EXCO' 'T' U1_CALC 'NOID' 'T'  ;
      'FINSI';
       IDCOND = 'INDE' TAB_COND ;
       ICOND=0      ;
       'REPE' BOUCOND ('DIME' TAB_COND)   ;
          ICOND = ICOND + 1  ;
          MOD1 = IDCOND.ICOND  ;
          VCOND = TAB_COND.MOD1  ;
          TYPC = 'TYPE' VCOND ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             CC = 'VARI' MOD1 VCOND UC1_T 'NOEUD' ;
             CCC = 'EXCO' CC 'SCAL' 'NOID' 'K' ;
          'SINON' ;
             CCC = VCOND ;
          'FINSI'  ;
          'SI' ('EXISTE' ETAB 'COQUE' )   ;
              MMM = 'MATE' MOD1 'K' CCC 'EPAI' EP ;
          'SINON'  ;
              MMM = 'MATE' MOD1 'K' CCC ;
          'FINSI'  ;
           MI= 'CONDUCTION' MOD1 MMM;
          'SI' (ICOND 'EGA' 1) ;
              MAT_COND = MI;
           'SINON' ;
              MAT_COND =MAT_COND ET MI ;
           'FINSI'     ;
        'FIN' BOUCOND  ;
    'FINSI'   ;
   'SI' ( 'EXISTE' ETAB 'TABCAPA' ) ;
*
*---  CALCUL DE LA MATRICE DE CAPACITE
*
      U2_CALC = 'COLI' U0_0 1.5 U0_1 -0.5      ;
      'SI' ( 'EXISTE' ETAB 'PHASE' ) ;
*          initialisation de la table pour le changement de phase
             TABP2.'CHAMP THERMIQUE 1' = 'ENLEVER' U0_0 'LX' ;
             TABP2.'CHAMP THERMIQUE 2' = 'ENLEVER'
                                ( COLI 2. U0_0 -1. U0_1)'LX' ;
      'FINSI' ;
      'SI' ('EXISTE' ETAB 'COQUE' ) ;
         UC2_TI = 'EXCO' 'TINF' U2_CALC 'NOID' 'T' ;
         UC2_TS = 'EXCO' 'TSUP' U2_CALC 'NOID' 'T' ;
         UC2_TM = 'EXCO' 'T   ' U2_CALC 'NOID' 'T' ;
         UC2_T  = (UC2_TI + UC2_TS + UC2_TM)/0.3   ;
       'SINON';
      UC2_T   = 'EXCO' 'T' U2_CALC 'NOID' 'T'  ;
      'FINSI';
       IDCAPA = 'INDE' TAB_CAPA ;
       ICAPA=0     ;
       'REPE' BOUCAPA ('DIME' TAB_CAPA)   ;
          ICAPA = ICAPA + 1  ;
          MOD1  = IDCAPA.ICAPA  ;
          VCAPA = TAB_CAPA.MOD1  ;
          TYPC = 'TYPE' VCAPA ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
            CC = 'VARI' MOD1 VCAPA UC2_T 'NOEUD' ;
            CCC = 'EXCO' CC 'SCAL' 'NOID' 'C' ;
          'SINON'  ;
             CCC = VCAPA  ;
          'FINSI'  ;
          'SI' ('EXISTE' ETAB 'COQUE' )   ;
              MMM = 'MATE' MOD1 'C' CCC  'RHO' 1. 'EPAI' EP ;
          'SINON'  ;
              MMM = 'MATE' MOD1 'C' CCC 'RHO' 1.;
          'FINSI'  ;
          'SI' ( 'EXISTE' ETAB 'PHASE' ) ;
               MI = 'CAPACITE'  MOD1 MMM TABP2 ;
          'SINON' ;
               MI = 'CAPACITE'  MOD1 MMM ;
          'FINSI' ;
          'SI' (ICAPA 'EGA' 1) ;
              MAT_CAPA = MI  ;
              MOD_CAPA=MOD1;
           'SINON' ;
               MAT_CAPA = MAT_CAPA  ET  MI ;
               MOD_CAPA=MOD_CAPA ET MOD1;
           'FINSI'     ;
        'FIN' BOUCAPA  ;
    'FINSI'   ;
*
   SCA1    = 0.5 + LAMBDA          ;
   SCA2    = 1. / T_PAS            ;
   SCA3    = 0.5 - ( 2. * LAMBDA ) ;
   SCA4    = ( 2. * LAMBDA ) - 0.5 ;
   A2 = MAT_COND * SCA1 ;
   A1 = MAT_CAPA * SCA2 ;
   MAT_RIGI = A1 'ET' A2 ;
   'SI' ( 'EXISTE' ETAB 'BLOCAGE' ) ;
        MAT_RIGI = MAT_RIGI 'ET' MAT_BLOC ;
   'FINSI' ;
   B3 = MAT_COND * SCA4 ;
   B1 = A1 'ET' B3 ;
   B2 = MAT_COND * ( -1 * LAMBDA ) ;
   C1 = B1 * U0_0 ;
* Correction DR 1/9/92  U0_0 -> U0_1
   C2 = B2 * U0_1 ;
   MAT_CHPO = C1 + C2 ;
   'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
      MAT_CHPO = MAT_CHPO + FLUX_TPS ;
   'FINSI' ;
   'SI' ( 'EXISTE' ETAB 'IMPOSE' ) ;
      MAT_CHPO = MAT_CHPO + VAL_IMPO ;
   'FINSI' ;
   'SI' ( 'EXISTE' ETAB 'CONVECTION' ) ;
*
*---  ON GENERE LES TERMES DE CONVECTION
*
   'SI' ('EGA' IT 1 ) ;
* CORRECTION AM 4/8/92  PERMUTATION U0_0 ET U0_1
      CHT_CNV  = 'COLI' U0_0 1.5 U0_1 -0.5  ;
      'SI' ('EXISTE' ETAB 'COQUE' ) ;
        PE =ETAB.'PEAU';
        'SI' ('EGA' PE 'TINF') ;
          CHT_CNV = 'EXCO' 'TINF' CHT_CNV 'NOID' 'T' ;
        'FINSI'   ;
        'SI' ('EGA' PE 'TSUP') ;
          CHT_CNV = 'EXCO' 'TSUP' CHT_CNV 'NOID' 'T' ;
        'FINSI'  ;
      'FINSI' ;
       IDCONV = 'INDE' TAB_CON1 ;
       NCONV =  'DIME' IDCONV ;
       NTE = 'DIME' TAB_TE1 ;
       'SI' (NTE 'NEG' 0 ) ;IDTE  =  'INDE' TAB_TE1 ;'FINSI' ;
*       'SI' ('NEG' NCONV NTE ) ;
*          'SAUTER' 1 'LIGNE' ;
*    'MESS' '*** LA DONNEE DE LA CONVECTION EST INCORRECTE *** ' ;
*     'QUITTER' PROC ;
*       'FINSI'    ;
        ICONV = 0 ;
        'REPETE' BOUCONV NCONV ;
          ICONV = ICONV + 1 ;
          MOD1 = IDCONV.ICONV ;
          VCONV = TAB_CON1.MOD1 ;
          TYPC = 'TYPE' VCONV ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             CC = 'VARI' MOD1 VCONV CHT_CNV 'NOEUD' ;
             CCC = 'EXCO' CC 'SCAL'  'NOID' 'H' ;
             MAT_CONV = 'MATE' MOD1 'H' CCC ;
          'SINON'  ;
             MAT_CONV = 'MATE'  MOD1 'H' VCONV ;
          'FINSI'   ;
           MOD_CONV = MOD1 ;
         'SI' ('EXISTE' ETAB 'COQUE' ) ;
            RIG_CONV = 'CONDUCTIVITE' MOD_CONV MAT_CONV PE ;
         'SINON'  ;
            RIG_CONV = 'CONDUCTIVITE' MOD_CONV MAT_CONV ;
         'FINSI'  ;
         'SI' ('EGA' ICONV 1 ) ;
            RIG_CON1 = RIG_CONV ;
         'SINON'  ;
            RIG_CON1 = RIG_CONV ET RIG_CON1  ;
         'FINSI'       ;
       'FIN' BOUCONV  ;
       'SI' (NTE 'NEG' 0 )  ;
        ICONV = 0 ;
        'REPETE' BOUCTE NTE ;
          ICONV = ICONV + 1 ;
          MOD1 = IDTE.ICONV ;
          VCONV = TAB_CON1.MOD1 ;
          TYPC = 'TYPE' VCONV ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             CC = 'VARI' MOD1 VCONV CHT_CNV 'NOEUD' ;
             CCC = 'EXCO' CC 'SCAL'  'NOID' 'H' ;
             MAT_CONV = 'MATE' MOD1 'H' CCC ;
          'SINON'  ;
             MAT_CONV = 'MATE'  MOD1 'H' VCONV ;
          'FINSI'   ;
          VTE = TAB_TE1.MOD1  ;
          TYPC = 'TYPE' VTE ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             EE = 'VARI' MOD1 VTE CHT_CNV 'NOEUD' ;
             VAL_TE = 'CHAN' 'CHPO' MOD1 EE ;
          'SINON'  ;
             VAL_TE = VTE   ;
          'FINSI'   ;
          MOD_CONV = MOD1 ;
          TYP_TE = 'TYPE' VAL_TE ;
         'SI' ( 'NEG' TYP_TE 'CHPOINT ' ) ;
           'SI' ('EXISTE' ETAB 'COQUE' ) ;
             PE =ETAB.'PEAU';
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV 'T' VAL_TE PE ;
           'SINON' ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV 'T' VAL_TE ;
           'FINSI';
         'SINON' ;
           'SI' ('EXISTE' ETAB 'COQUE' ) ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV  VAL_TE PE ;
           'SINON' ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV VAL_TE     ;
          'FINSI';
         'FINSI' ;
         'SI' ('EGA' ICONV 1 ) ;
            CHAL_TE1 = CHAL_TEM ;
         'SINON'  ;
            CHAL_TE1 = CHAL_TEM ET CHAL_TE1  ;
         'FINSI'       ;
       'FIN' BOUCTE  ;
     'FINSI'    ;
    'SINON'    ;
       IDCONV = 'INDE' TAB_CON2 ;
       NCONV =  'DIME' IDCONV ;
       NTE = 'DIME' TAB_TE2 ;
       'SI' (NTE 'NEG' 0) ;IDTE  =  'INDE' TAB_TE2 ;'FINSI';
*       'SI' ('NEG' NCONV NTE ) ;
*          'SAUTER' 1 'LIGNE' ;
*    'MESS' '*** LA DONNEE DE LA CONVECTION EST INCORRECTE *** '  ;
*    'QUITTER' PROC ;
*       'FINSI'    ;
        ICONV = 0 ;
        'REPETE' BOUCONV1 NCONV ;
          ICONV = ICONV + 1 ;
          MOD1 = IDCONV.ICONV ;
          VCONV = TAB_CON2.MOD1 ;
          TYPC = 'TYPE' VCONV ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             LIS_TEM = 'EXTR' VCONV 'TEMPS'  ;
             LIS_H   = 'EXTR' VCONV 'H' ;
             CC = 'IPOL' T1 LIS_TEM LIS_H ;
             MAT_CONV = 'MATE' MOD1 'H' CC ;
          'SINON'  ;
             MAT_CONV = 'MATE'  MOD1 'H' VCONV ;
          'FINSI'   ;
           MOD_CONV = MOD1 ;
         'SI' ('EXISTE' ETAB 'COQUE' ) ;
            RIG_CONV = 'CONDUCTIVITE' MOD_CONV MAT_CONV PE ;
         'SINON'  ;
            RIG_CONV = 'CONDUCTIVITE' MOD_CONV MAT_CONV ;
         'FINSI'  ;
         'SI' ('EGA' ICONV 1 ) ;
            RIG_CON1 = RIG_CONV ;
         'SINON'  ;
            RIG_CON1 = RIG_CONV ET RIG_CON1  ;
         'FINSI'       ;
       'FIN' BOUCONV1  ;
       'SI' (NTE 'NEG' 0 ) ;
        ICONV = 0 ;
        'REPETE' BOUCTE1 NTE ;
          ICONV = ICONV + 1 ;
          MOD1 = IDTE.ICONV ;
          VCONV = TAB_CON2.MOD1 ;
          TYPC = 'TYPE' VCONV ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             LIS_TEM = 'EXTR' VCONV 'TEMPS'  ;
             LIS_H   = 'EXTR' VCONV 'H' ;
             CC = 'IPOL' T1 LIS_TEM LIS_H ;
             MAT_CONV = 'MATE' MOD1 'H' CC ;
          'SINON'  ;
             MAT_CONV = 'MATE'  MOD1 'H' VCONV ;
          'FINSI'   ;
          VTE = TAB_TE2.MOD1  ;
          TYPC = 'TYPE' VTE ;
          'SI' ('EGA' TYPC 'EVOLUTIO' ) ;
             LIS_TEM = 'EXTR' VTE 'TEMPS'  ;
             LIS_TE   = 'EXTR' VTE 'TE' ;
             VAL_TE = 'IPOL' T1 LIS_TEM LIS_TE ;
          'SINON'  ;
             VAL_TE = VTE   ;
          'FINSI'   ;
          MOD_CONV = MOD1 ;
          TYP_TE = 'TYPE' VAL_TE ;
         'SI' ( 'NEG' TYP_TE 'CHPOINT ' ) ;
           'SI' ('EXISTE' ETAB 'COQUE' ) ;
             PE =ETAB.'PEAU';
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV 'T' VAL_TE PE ;
           'SINON' ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV 'T' VAL_TE ;
           'FINSI';
         'SINON' ;
           'SI' ('EXISTE' ETAB 'COQUE' ) ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV  VAL_TE PE ;
           'SINON' ;
             CHAL_TEM = 'CONVECTION' MOD_CONV MAT_CONV VAL_TE     ;
          'FINSI';
         'FINSI' ;
         'SI' ('EGA' ICONV 1 ) ;
            CHAL_TE1 = CHAL_TEM ;
         'SINON'  ;
            CHAL_TE1 = CHAL_TEM ET CHAL_TE1  ;
         'FINSI'       ;
       'FIN' BOUCTE1  ;
      'FINSI'   ;
     'FINSI'     ;
      MAT_RIGI = MAT_RIGI 'ET' RIG_CON1 ;
      'SI' (NTE 'NEG' 0 ) ;MAT_CHPO = MAT_CHPO + CHAL_TE1 ;'FINSI';
   'FINSI' ;
*
*---  CALCUL D'UN PAS
*
   U1 = 'RESOUDRE' MAT_RIGI MAT_CHPO ;
   U1_LX  = 'EXCO' 'LX' U1 'NOID' 'LX' ;
   U1_AUX = U1 - U1_LX ;
      'SI' ('EXISTE' ETAB 'COQUE' ) ;
         U1_T  =  'ENLEVE' U1 'LX'   ;
       'SINON';
         U1_T = 'EXCO' 'T' U1 'NOID' 'T' ;
      'FINSI';
*
*
*---  ARCHIVAGE DES RESULTATS DANS "ETAB"
*
   'SI' ( IPAS 'EGA' N_SOR ) ;
      NUM_PAS = NUM_PAS + 1 ;
      STN = 'TABLE' ;
      ETAB.NUM_PAS      = 'TABLE' STN ;
      STN.'INSTANT'     = T1          ;
      STN.'TEMPERATURE' = U1_T        ;
      IPAS = 0 ;
      'SI' ( NIV_MESS '>EG' 0 ) ;
         'MESS' 'Fin du calcul du temps             :' T1      ;
         'SI' ( NIV_MESS '>EG' 2 ) ;
            'SAUTER' 1 'LIGNE' ;
            'MESS' 'CHAMP DE TEMPERATURE AU TEMPS      :' T1 ;
            'LISTE' U1_T ;
         'FINSI' ;
      'FINSI' ;
   'FINSI' ;
   T0_0   = T1     ;
   U0_1   = U0_0   ;
   U0_0   = U1_AUX ;
   'SI' ( 'EXISTE' ETAB 'FLUX' ) ;
      QTPS_1 = QTPS_0 ;
      QTPS_0 = 'TIRER' TC_TPS T0_0 ;
   'FINSI' ;
'FIN' BOUC_SOR ;
*
*---  ON FAIT LE MENAGE...
*
*'MENAGE' ;
'SI' ( NIV_MESS '>EG' 1 ) ;
   'SAUTER' 1 'LIGNE' ;
   'MESS' '***   Fin de la procedure "TRANSIT3"   ***' ;
'FINSI' ;
 'FIN' PROC ;
'FINPROC' ETAB ;

