$$$$ @ORTHO
* @ORTHO    PROCEDUR  FANDEUR   14/10/10    21:15:01     8178
*-------------------------------------------------
******          PROCEDURE @ORTHO            ******
*-------------------------------------------------
*
*    CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*   A DISPOSOTION DE LA COMMUNAUTE  CASTEM2000
*    PAR M. LIBEYRE ( CEA/DSM/DRFC )
*
*     TEL : ( 33 1 ) 42 25 46 03
*
*  PROCEDURE MISE A JOUR PAR OF LE 09/03/2009
*-------------------------------------------------
DEBPROC @ORTHO  MAIL1*'MAILLAGE' LIG1*'MAILLAGE'
                CH1*'CHPOINT' CH2*'CHPOINT'
                ALPHA*'FLOTTANT' LL*'LISTREEL'
                TYPEMOD/'MOT' TYPEELT/'MOT' PTGENE/'POINT' ;

'SI' ('<' ('DIMENSION' LL) 13) ;
  'ERREUR' 'La liste des caracteristiques doit avoir 13 valeurs' ;
'FINSI' ;

DIMGEO = 'VALEUR' 'DIME' ;  MODGEO = 'VALEUR' 'MODE' ;
'SI' ('NON' ('EXISTE' TYPEMOD)) ;
  TYPEMOD = MODGEO ;
'FINSI' ;
'SI' ('NON' ('EXISTE' TYPEELT)) ;
  TYPEELT = 'MOT' 'COQ3' ;
'FINSI' ;
Mot_DPGE = 'TEXTE' ' ' ;  Pt_DPGE = 'TEXTE' ' ' ;

'SI' (('NEG' TYPEMOD 'COEP') 'ET' ('NEG' TYPEMOD 'COMI') 'ET'
      ('NEG' TYPEMOD MODGEO)) ;
  'ERREUR' ('CHAINE' 'Mot cle ' TYPEMOD ' non reconnu') ;
'FINSI' ;
'SI' ('EGA' TYPEMOD 'COMI') ;
  'SI' ('NON' ('EXISTE' ('MOTS' 'COQ3' 'DKT ') TYPEELT)) ;
    'ERREUR' 'Element de COque MInce a support TRI3 inconnu' ;
  'FINSI' ;
'FINSI' ;
'SI' ('EGA' TYPEMOD 'COEP') ;
  'SI' ('NEG' DIMGEO 3) ;
    'ERREUR' 'Modelisation COEP incompatible avec la DIMEnsion' ;
  'FINSI' ;
'FINSI' ;

'SI' ('EGA' DIMGEO 1) ;
  'ERREUR' 'Cas DIME=1 non prevu actuellement' ;
'FINSI' ;
'SI' ('EGA' DIMGEO 3) ;
  'SI' ('EGA' TYPEMOD 'TRID') ;
    lm_geo = 'MOTS' 'TET4' 'PRI6' 'PYR5' 'CUB8'
                    'TE10' 'PR15' 'PY14' 'CU20' ;
    lm_elt = lm_geo ;
  'FINSI' ;
  'SI' ('EGA' TYPEMOD 'COMI') ;
    lm_geo = 'MOTS' 'TRI3' ;
    lm_elt = 'MOTS' TYPEELT ;
  'FINSI' ;
  'SI' ('EGA' TYPEMOD 'COEP') ;
    lm_geo = 'MOTS' 'TRI3' 'QUA4' 'TRI6' 'QUA8' ;
    lm_elt = 'MOTS' 'DST ' 'COQ4' 'COQ6' 'COQ8' ;
  'FINSI' ;
'FINSI' ;
'SI' ('EGA' DIMGEO 2) ;
  'SI' ('EGA' MODGEO 'PLANGENE') ;
    'SI' ('NON' ('EXISTE' PTGENE)) ;
      'ERREUR' 'Le point support de la DPGE n est pas defini' ;
    'FINSI' ;
    Mot_DPGE = 'MOT' 'DPGE' ;  Pt_DPGE = PTGENE ;
  'FINSI' ;
  lm_geo = 'MOTS' 'TRI3' 'QUA4' 'TRI6' 'QUA8' ;
  lm_elt = lm_geo ;
  'SI' ('EGA' TYPEMOD 'COMI') ;
    lm_geo = 'MOTS' 'SEG2' ;
    lm_elt = 'MOTS' 'COQ2' ;
  'FINSI' ;
'FINSI' ;

lm_mai = 'ELEM' MAIL1 'TYPE' ;
'REPETER' Boucle ('DIMENSION' lm_mai) ;
  str_z = 'EXTRAIRE' lm_mai &Boucle ;
  'SI' ('NON' ('EXISTE' lm_geo str_z)) ;
    'ERREUR' 'Element geometrique incompatible avec la modelisation' ;
  'FINSI' ;
'FIN' Boucle ;

i_z = 0 ;
'REPETER' Boucle ('DIMENSION' lm_geo) ;
  str_z = 'EXTRAIRE' lm_geo &Boucle ;
  'SI' ('EXISTE' lm_mai str_z) ;
    mail_z = MAIL1 'ELEM' str_z ;
    str_elt = 'EXTRAIRE' lm_elt &Boucle ;
    modl_z = 'MODE' mail_z 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE'
                           str_elt 'CONS' 'MECA' Mot_DPGE Pt_DPGE ;
    i_z = i_z + 1 ;
    'SI' ('EGA' i_z 1) ;
      MODL1 = modl_z ;
    'SINON' ;
      MODL1 = MODL1 'ET' modl_z ;
    'FINSI' ;
  'FINSI' ;
'FIN' Boucle ;

GEO = 'CHANGER' 'POI1' MAIL1 ;
NBPT1 = 'NBNO' GEO ;

L1 = 'EXTRAIRE' CH1 'COMP' ;
L2 = 'EXTRAIRE' CH2 'COMP' ;

'SI' (DIMGEO 'EGA' 3) ;
  L11 = 'EXTRAIRE' 1 L1 ;
  L12 = 'EXTRAIRE' 2 L1 ;
  L13 = 'EXTRAIRE' 3 L1 ;
  L21 = 'EXTRAIRE' 1 L2 ;
  L22 = 'EXTRAIRE' 2 L2 ;
  L23 = 'EXTRAIRE' 3 L2 ;
  LCOMP_K  = 'MOTS' 'KX  ' 'KY  ' 'KZ  ' ;
  LCOMP_V1 = 'MOTS' 'V1X ' 'V1Y ' 'V1Z ' ;
  LCOMP_V2 = 'MOTS' 'V2X ' 'V2Y ' 'V2Z ' ;
  'REPETER' Boucle NBPT1 ;
    POINMAIL = GEO 'POINT' &Boucle ;
    POINCOUR = LIG1 'POINT' 'PROCHE' POINMAIL ;
    CH1MAIL = 'MANU' 'CHPO' POINMAIL 3
                            'KX  ' ('EXTRAIRE' CH1 L11 POINCOUR)
                            'KY  ' ('EXTRAIRE' CH1 L12 POINCOUR)
                            'KZ  ' ('EXTRAIRE' CH1 L13 POINCOUR)
                            'NATURE' 'DIFFUS' ;
    CH2MAIL = 'MANU' 'CHPO' POINMAIL 3
                            'KX  ' ('EXTRAIRE' CH2 L21 POINCOUR)
                            'KY  ' ('EXTRAIRE' CH2 L22 POINCOUR)
                            'KZ  ' ('EXTRAIRE' CH2 L23 POINCOUR)
                            'NATURE' 'DIFFUS' ;
    'SI' ('EGA' &Boucle 1) ;
      CH1GEO = CH1MAIL ;
      CH2GEO = CH2MAIL ;
    'SINON' ;
      CH1GEO = CH1GEO 'ET' CH1MAIL ;
      CH2GEO = CH2GEO 'ET' CH2MAIL ;
    'FINSI' ;
  'FIN' Boucle ;
'FINSI' ;
'SI' (DIMGEO 'EGA' 2) ;
  L11 = 'EXTRAIRE' 1 L1 ;
  L12 = 'EXTRAIRE' 2 L1 ;
  L21 = 'EXTRAIRE' 1 L2 ;
  L22 = 'EXTRAIRE' 2 L2 ;
  LCOMP_K  = 'MOTS' 'KX  ' 'KY  ' ;
  LCOMP_V1 = 'MOTS' 'V1X ' 'V1Y ' ;
  LCOMP_V2 = 'MOTS' 'V2X ' 'V2Y ' ;
  'REPETER' Boucle NBPT1 ;
    POINMAIL = GEO 'POINT' &Boucle ;
    POINCOUR = LIG1 'POINT' 'PROCHE' POINMAIL ;
    CH1MAIL = 'MANU' 'CHPO' POINMAIL 2
                            'KX  ' ('EXTRAIRE' CH1 L11 POINCOUR)
                            'KY  ' ('EXTRAIRE' CH1 L12 POINCOUR)
                            'NATURE' 'DIFFUS' ;
    CH2MAIL = 'MANU' 'CHPO' POINMAIL 2
                            'KX  ' ('EXTRAIRE' CH2 L21 POINCOUR)
                            'KY  ' ('EXTRAIRE' CH2 L22 POINCOUR)
                            'NATURE' 'DIFFUS' ;
    'SI' ('EGA' &Boucle 1) ;
      CH1GEO = CH1MAIL ;
      CH2GEO = CH2MAIL ;
    'SINON' ;
      CH1GEO = CH1GEO 'ET' CH1MAIL ;
      CH2GEO = CH2GEO 'ET' CH2MAIL ;
    'FINSI' ;
  'FIN' Boucle ;
'FINSI' ;

CHP1 = (('COS' ALPHA)*CH1GEO) + (('SIN' ALPHA)*CH2GEO) ;
CHP2 = (((-1.*('SIN' ALPHA))*CH1GEO)) + (('COS' ALPHA)*CH2GEO) ;

CHPV1 = 'NOMC' LCOMP_K LCOMP_V1 CHP1 ;
CHMV1 = 'CHANGER' 'CHAM' CHPV1 MODL1 'RIGIDITE' 'CARACTERISTIQUES' ;
CHPV2 = 'NOMC' LCOMP_K LCOMP_V2 CHP2 ;
CHMV2 = 'CHANGER' 'CHAM' CHPV2 MODL1 'RIGIDITE' 'CARACTERISTIQUES' ;

YOUNG1S = 'EXTRAIRE' LL  1 ;
YOUNG2S = 'EXTRAIRE' LL  2 ;
YOUNG3S = 'EXTRAIRE' LL  3 ;
NU1S    = 'EXTRAIRE' LL  4 ;
NU2S    = 'EXTRAIRE' LL  5 ;
NU3S    = 'EXTRAIRE' LL  6 ;
CIS1S   = 'EXTRAIRE' LL  7 ;
CIS2S   = 'EXTRAIRE' LL  8 ;
CIS3S   = 'EXTRAIRE' LL  9 ;
ALPH1S  = 'EXTRAIRE' LL 10 ;
ALPH2S  = 'EXTRAIRE' LL 11 ;
ALPH3S  = 'EXTRAIRE' LL 12 ;
RHOS    = 'EXTRAIRE' LL 13 ;

YOUNG1 = 'MANU' 'CHML' MODL1 'YG1 ' YOUNG1S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
YOUNG2 = 'MANU' 'CHML' MODL1 'YG2 ' YOUNG2S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
YOUNG3 = 'MANU' 'CHML' MODL1 'YG3'  YOUNG3S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
NU1    = 'MANU' 'CHML' MODL1 'NU12' NU1S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
NU2    = 'MANU' 'CHML' MODL1 'NU23' NU2S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
NU3    = 'MANU' 'CHML' MODL1 'NU13' NU3S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
CIS1   = 'MANU' 'CHML' MODL1 'G12 ' CIS1S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
CIS2   = 'MANU' 'CHML' MODL1 'G23 ' CIS2S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
CIS3   = 'MANU' 'CHML' MODL1 'G13 ' CIS3S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
ALPH1  = 'MANU' 'CHML' MODL1 'ALP1' ALPH1S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
ALPH2  = 'MANU' 'CHML' MODL1 'ALP2' ALPH2S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
ALPH3  = 'MANU' 'CHML' MODL1 'ALP3' ALPH3S
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;
RHO    = 'MANU' 'CHML' MODL1 'RHO ' RHOS
                       'TYPE' 'CARACTERISTIQUES' 'RIGIDITE' ;

'SI' ('EGA' TYPEMOD 'COMI') ;
  MAT11 = YOUNG1 'ET' YOUNG2 'ET' NU1 ;
  MAT22 = CIS1 ;
  'SI' ('EGA' DIMGEO 3) ;
    MAT33 = ('EXCO' ('MOTS' 'V1X ' 'V1Y ') CHMV1) 'ET' ;
    MAT33 = ('EXCO' ('MOTS' 'V2X ' 'V2Y ') CHMV2) ;
  'SINON' ;
    MAT33 = CHMV1 'ET' CHMV2 ;
  'FINSI' ;
  MAT44 = ALPH1 'ET' ALPH2 'ET' RHO ;
'FINSI' ;
'SI' ('EGA' TYPEMOD 'COEP') ;
  MAT11 = YOUNG1 'ET' YOUNG2 'ET' NU1 ;
  MAT22 = CIS1 'ET' CIS2 'ET' CIS3 ;
  'SI' ('EGA' DIMGEO 3) ;
    MAT33 = ('EXCO' ('MOTS' 'V1X ' 'V1Y ') CHMV1) 'ET' ;
    MAT33 = ('EXCO' ('MOTS' 'V2X ' 'V2Y ') CHMV2) ;
  'SINON' ;
    MAT33 = CHMV1 'ET' CHMV2 ;
  'FINSI' ;
  MAT44 = ALPH1 'ET' ALPH2 'ET' RHO ;
'FINSI' ;
'SI' ('EGA' TYPEMOD 'PLANCONT') ;
  MAT11 = YOUNG1 'ET' YOUNG2 'ET' YOUNG3 'ET' NU1 'ET' NU2 'ET' NU3 ;
  MAT22 = CIS1 ;
  MAT33 = CHMV1 'ET' CHMV2 ;
  MAT44 = ALPH1 'ET' ALPH2 'ET' RHO ;
'FINSI' ;
'SI' (('EGA' TYPEMOD 'PLANDEFO') 'OU' ('EGA' TYPEMOD 'PLANGENE') 'OU'
      ('EGA' TYPEMOD 'AXIS')) ;
  MAT11 = YOUNG1 'ET' YOUNG2 'ET' YOUNG3 'ET' NU1 'ET' NU2 'ET' NU3 ;
  MAT22 = CIS1 ;
  MAT33 = CHMV1 'ET' CHMV2 ;
  MAT44 = ALPH1 'ET' ALPH2 'ET' ALPH3 'ET' RHO ;
'FINSI' ;
'SI' (('EGA' TYPEMOD 'FOUR') 'OU' ('EGA' TYPEMOD 'TRID')) ;
  MAT11 = YOUNG1 'ET' YOUNG2 'ET' YOUNG3 'ET' NU1 'ET' NU2 'ET' NU3 ;
  MAT22 = CIS1 'ET' CIS2 'ET' CIS3 ;
  MAT33 = CHMV1 'ET' CHMV2 ;
  MAT44 = ALPH1 'ET' ALPH2 'ET' ALPH3 'ET' RHO ;
'FINSI' ;

MATGEO = MAT11 'ET' MAT22 'ET' MAT33 'ET' MAT44 ;

'FINPROC' MATGEO MODL1 ;



