$$$$ GDFLIM1
* GDFLIM1   PROCEDUR  AM        09/12/07    21:15:36     6578
DEBPROC GDFLIM1 tab1*TABLE;
*-----------------------------------------------------------------
* Calcul des reponses du modele MECA aux pas de calcul du gradient
* Entree :
*       - nombre de variables             : tab1.nbre_variables
*       - vecteur actuel X                : tab1.vecx
*       - increments sur les axes         : tab1.delx
*       - indicateur de calcul            : tab1.calcul
*       - compteur d'appels               : tab1.appels
*       - parametres modele MECA          : tab1.parametres
* Sortie :
*       - vecteur des reponses            : tab1.valeurs_G
*       - indice d'erreur                 : err1
*-----------------------------------------------------------------

* Recuperation des entrees

nva1=tab1.nbre_variables;
vec1=tab1.vecx;
del1=tab1.delx;
cal1=tab1.calcul;
SI (EXISTE tab1.parametres);
  para1=tab1.parametres;
FINSI;

* Controle des entrees

err1=0;
SI ((DIME vec1) NEG nva1);
 MESS 'HASOFER  / GDFLIM1 : incoherence DIME vecx / nva';
 err1=1;
 QUIT GDFLIM1;
FINSI;
SI ((DIME del1) NEG nva1);
 MESS 'HASOFER  / GDFLIM1 : incoherence DIME delta_x / nva';
 err1=1;
 QUIT GDFLIM1;
FINSI;
SI ((DIME cal1) NEG nva1);
 MESS 'HASOFER  / GDFLIM1 : incoherence DIME calcul / nva';
 err1=1;
 QUIT GDFLIM1;
FINSI;

* Declarations et initialisations

valg1=PROG;
tabx1=TABLE;
SI (EXISTE para1);
  tabx1.parametres=para1;
FINSI;
n_appel = 0;

* Calcul des reponses (si calcul demande)

REPE bouc1 nva1;

 SI (EGA (EXTR &bouc1 cal1) 1);
   n_appel = n_appel + 1;
   incr1=(EXTR &bouc1 del1);
   xi1=(EXTR &bouc1 vec1)+incr1;
   REMP vec1 &bouc1 xi1;

   tabx1.variables=vec1;
   err1=FLIM1 tabx1;
   SI (EGA err1 0);
     valg1=valg1 ET (PROG tabx1.valeur_G);
   SINON;
     QUIT GDFLIM1;
   FINSI;

   xi1=(EXTR &bouc1 vec1)-incr1;
   REMP vec1 &bouc1 xi1;
 SINON;
   valg1=valg1 ET (PROG 0.);
   n_appel = n_appel + 0;
 FINSI;

FIN bouc1;

tab1.valeurs_G = valg1;
tab1.appels = n_appel;

FINPROC err1;


