$$$$ PAS_REPR
* PAS_REPR  PROCEDUR  CB215821  15/09/30    21:15:02     8651
'DEBP' PAS_REPR preced*'TABLE';
tt = preced.'REPRISE';
temp= preced.'TEMPS';
ns= ('DIME' temp  ) - 1;
t0=temp . 0;nsor=0;
'REPE' bou ns;
  t1= temp . &bou;
  dt= t1 - t0 / 1.e6;
  'SI' ( tt < (t1 + dt));
     'SI' ( tt > (t1-dt));
       NSOR=  &bou;
*       mess 'on a trouve le temps ' tt;
       'QUITTER' bou;
     'FINSI';
  'FINSI';
  t0=t1;
'FIN' bou;
'SI' ( nsor 'EGA' 0);
   'ERREUR' 1010;
'FINSI';
ind = index preced;
nin= dime ind;idep=faux;icon=faux;ivar=faux;item=faux;ipha=faux;
ivit=faux;irea=faux;iqth=faux;iqdi=faux;
'REPETER' bou2 nin;
  'SI' ( 'EGA'  ind . &bou2  'WTABLE') ;
      'OTER' PRECED 'WTABLE';
  'FINSI';
  'SI' ( 'EGA'  ind . &bou2  'CONTINUATION');
     'OTER' PRECED 'CONTINUATION';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'DEPLACEMENTS');
     idep=vrai;dep=preced.'DEPLACEMENTS';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'CONTRAINTES');
     icon=vrai;con=preced.'CONTRAINTES';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'VARIABLES_INTERNES');
     ivar=vrai;var=preced.'VARIABLES_INTERNES';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'TEMPERATURES');
     item=vrai;tem= preced.'TEMPERATURES';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'PROPORTIONS_PHASE');
     ipha=vrai;pha =preced . 'PROPORTIONS_PHASE';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'VITESSES');
     ivit=vrai;vit=preced.'VITESSES';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'REACTIONS');
     irea=vrai;rea=preced.'REACTIONS';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'REACTIONS_THERMIQUES');
     iqth=vrai;qth=preced.'REACTIONS_THERMIQUES';
  'FINSI';
  'SI' ('EGA'   ind . &bou2 'REACTIONS_DIFFUSIONS');
     iqdi=vrai;qdi=preced.'REACTIONS_DIFFUSIONS';
  'FINSI';
'FIN' bou2;

nsup= ns - nsor;
'SI' ( nsup > 0);
  'REPE' bou3 nsup;
    ie= nsor + &bou3;
    tt= temp.ie;'OTER' temp ie;
    mess ' suppression du temps ' tt;
    'SI' idep; 'OTER' dep ie;'FINSI';
    'SI' icon; 'OTER' con ie;'FINSI';
    'SI' ivar; 'OTER' var ie;'FINSI';
    'SI' item; 'OTER' tem ie;'FINSI';
    'SI' ipha; 'OTER' pha ie;'FINSI';
    'SI' ivit; 'OTER' vit ie;'FINSI';
    'SI' irea; 'OTER' rea ie;'FINSI';
    'SI' iqth; 'OTER' qth ie;'FINSI';
    'SI' iqdi; 'OTER' qdi ie;'FINSI';
  'FIN' bou3;
'FINSI';
'FINP' ;






