$$$$ @POINTIR
* @POINTIR  PROCEDUR  CB215821  15/04/13    21:15:01     8482
'DEBP' @POINTIR ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* PTS1 = @POINTIR | 'UNIF' N1                | (MAIL1) ('PINI' PTS2)  *
*                 | 'EXCL' N1 'SPHE' D1 (N2) |                        *
*                                                                     *
*    ... ('GERM' IGER1) ;                                             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* Acquisition des arguments :                                         *
'ARGU' DISTRI1*'MOT' NPTS1*'ENTIER' ;
*                                                                     *
* Dimensions de travail (IDIM1) :
IDIM1    = 'VALE' 'DIME' ;
*                                                                     *
* Je cree le texte 'VOLU' 'SI' 'DIME' = 3 pour l'op. 'INCL' :         *
'SI' (IDIM1 'EGA' 3) ;
  TEXT1    = 'TEXT' ('MOT' 'VOLU') ;
'SINO' ;
  TEXT1    = 'TEXT' ('MOT' ' ') ;
'FINS' ;
*                                                                     *
* Precision pour l'operateur INCLus :                                 *
TOL1     = -1.E-5 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                         DISTRIBUTION UNIFORME                       *
*                                                                     *
'SI' ('EGA' DISTRI1 'UNIF') ;
*                                                                     *
* Arguments optionnels :                                              *
*                                                                     *
* Initialisation des donnees sur le domaine du tirage :               *
  'ARGU' MAIL1/'MAILLAGE' ;
  IMAIL1   = FAUX ;
  TMOY1    = 'TABL' ;
  TECA1    = 'TABL' ;
  'SI' ('EXIS' MAIL1) ;
    IMAIL1   = VRAI ;
    MESTIR1  = 1. ;
    LCPTMIN1 = 'PROG' ;
    'REPE' B0 IDIM1 ;
      MINI1    = 'MINI' ('COOR' &B0 MAIL1) ;
      MAXI1    = 'MAXI' ('COOR' &B0 MAIL1) ;
      MESTIR1  = MESTIR1 * (MAXI1 - MINI1) ;
      LCPTMIN1 = LCPTMIN1 'ET' ('PROG' MINI1) ;
      TMOY1 . &B0 = 0.5 * (MAXI1 + MINI1) ;
      TECA1 . &B0 = 0.5 * (MAXI1 - MINI1) ;
    'FIN' B0 ;
    RTIR1    = 1.2 * MESTIR1 / ('MESU' MAIL1) ;
  'SINO' ;
    'REPE' B0 IDIM1 ;
      TMOY1 . &B0 = 0.5 ;
      TECA1 . &B0 = 0.5 ;
    'FIN' B0 ;
    RTIR1    = 1. ;
  'FINS' ;
*                                                                     *
* Valeur du germe :                                                   *
  IGER1    = 1 ;
  IAUTO1   = FAUX ;
  'ARGU' MOT1/'MOT' ;
  'SI' ('EXIS' MOT1) ;
    'SI' ('EGA' MOT1 'GERM') ;
      'ARGU' MOT1/'MOT' ;
      'SI' (('EXIS' MOT1) 'ET' ('EGA' MOT1 'AUTO')) ;
        IAUTO1   = VRAI ;
        'OPTI' 'ERRE' 'CONT' ;
        'OPTI' 'ACQU' 10 'ACQU' './germe' ;
        'ACQU' IGER2*'ENTIER' ;
        'OPTI' 'ERRE' 'NORM' ;
        'SI' ('EGA' ('TYPE' IGER2) 'ENTIER') ;
          IGER1    = 'ABS' IGER2 ;
        'FINS' ;
      'SINO' ;
        'ARGU' IGER1*'ENTIER' ;
      'FINS' ;
    'FINS' ;
  'FINS' ;
*                                                                     *
* Tirage des points :                                                 *
  NB1      = 'ENTI' (('FLOT' NPTS1) * RTIR1) ;
  TVAR1    = 'TABL' ;
  'REPE' B0 IDIM1 ;
    TVAR1 . &B0 = 'BRUI' 'BLAN' 'UNIF' (TMOY1 . &B0) (TECA1 . &B0)
      NB1 IGER1 ;
    IGER1    = 'ABS' ('ENTI' (1.E5 * ('EXTR' (TVAR1 . &B0) 1))) ;
  'FIN' B0 ;
  NPTSI1   = 0 ;
  'REPE' B1 NB1 ;
    IP1      = &B1 ;
    LCPTI1   = 'PROG' ;
    'REPE' B0 IDIM1 ;
      LCPTI1   = LCPTI1 'ET' ('PROG' ('EXTR' (TVAR1 . &B0) IP1)) ;
    'FIN' B0 ;
    SI ((VALE DIME) EGA 1);
      PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1) IP1) ;
    FINSI;
    SI ((VALE DIME) EGA 2);
      PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1) IP1) ;
    FINSI;
    SI ((VALE DIME) EGA 3);
      PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1) IP1) ;
    FINSI;
* CB215821 : Ancienne Syntaxe retirée en 2015
*    PTSI1    = 'POIN' LCPTI1 ;
    'SI' IMAIL1 ;
      PTINCI1  = ('MANU' 'POI1' PTSI1) 'INCL' MAIL1 TEXT1 'NOID' TOL1 ;
      'SI' (('NBNO' PTINCI1) 'EGA' 0) ;
        'ITER' B1 ;
      'FINS' ;
    'FINS' ;
    'SI' (NPTSI1 'EGA' 0) ;
      PTS1     = 'MANU' 'POI1' PTSI1 ;
    'SINO' ;
      PTS1     = PTS1 'ET' PTSI1 ;
    'FINS' ;
    NPTSI1   = NPTSI1 + 1 ;
    'SI' (NPTSI1 'EGA' NPTS1) ;
      'QUIT' B1 ;
    'FINS' ;
  'FIN' B1 ;
*                                                                     *
* Nouveau germe si germe auto :                                       *
  'SI' IAUTO1 ;
    VECH1    = 'VALE' 'ECHO' ;
    IMPR1    = 'VALE' 'IMPR' ;
    'OPTI' 'ECHO' 0 ;
    'OPTI' 'IMPR' 10 'IMPR' './germe' ;
    'MESS' ('ABS' IGER1) ;
    'OPTI' 'IMPR' IMPR1 ;
    'OPTI' 'ECHO' VECH1 ;
  'FINS' ;
*                                                                     *
'FINS' ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                        PROCESSUS D'EXCLUSION                        *
*                                                                     *
* REPU = ancienne synthaxe pour le processus d'exclusion (repulsion)  *
'SI' (('EGA' DISTRI1 'EXCL') 'OU' ('EGA' DISTRI1 'REPU')) ;
*                                                                     *
* Arguments processus d'exclusion :                                   *
  'ARGU' ZREP1*'MOT' ;
  'SI' ('EGA' ZREP1 'SPHE') ;
    'ARGU' DREP1*'FLOTTANT' ;
  'SINO' ;
    'MESS' 'On attend le mot-cle SPHE.' ;
    'QUIT' @POINTIR ;
  'FINS' ;
*                                                                     *
* Initialisation des donnees sur le domaine du tirage :               *
  'ARGU' NTIR1/'ENTIER' MAIL1/'MAILLAGE' ;
  IMAIL1   = FAUX ;
  TMOY1    = 'TABL' ;
  TECA1    = 'TABL' ;
  'SI' ('EXIS' MAIL1) ;
    IMAIL1   = VRAI ;
    MESTIR1  = 1. ;
    LCPTMIN1 = 'PROG' ;
    'REPE' B0 IDIM1 ;
      MINI1    = 'MINI' ('COOR' &B0 MAIL1) ;
      MAXI1    = 'MAXI' ('COOR' &B0 MAIL1) ;
      MESTIR1  = MESTIR1 * (MAXI1 - MINI1) ;
      LCPTMIN1 = LCPTMIN1 'ET' ('PROG' MINI1) ;
      TMOY1 . &B0 = 0.5 * (MAXI1 + MINI1) ;
      TECA1 . &B0 = 0.5 * (MAXI1 - MINI1) ;
    'FIN' B0 ;
    RTIR1    = 1.2 * MESTIR1 / ('MESU' MAIL1) ;
  'SINO' ;
    'REPE' B0 IDIM1 ;
      TMOY1 . &B0 = 0.5 ;
      TECA1 . &B0 = 0.5 ;
    'FIN' B0 ;
    RTIR1    = 1. ;
  'FINS' ;
*                                                                     *
* Initialisation du nombre de tirages : NTIR1                         *
  'SI' ('NON' ('EXIS' NTIR1)) ;
    NTIR1    = 25 * ('ENTI' (('FLOT' NPTS1) * RTIR1)) ;
  'FINS' ;
*                                                                     *
* Initialisation des points du tirage et du germe :                   *
  IPTS2    = FAUX ;
  IGER1    = 1 ;
  IAUTO1   = FAUX ;
  'REPE' XB0 2 ;
    'ARGU' MOT1/'MOT' ;
    'SI' ('EXIS' MOT1) ;
      'SI' ('EGA' MOT1 'PINI') ;
        'ARGU' PTS2*'MAILLAGE' ;
          IPTS2    = VRAI ;
        'ITER' XB0 ;
      'FINS' ;
      'SI' ('EGA' MOT1 'GERM') ;
        'ARGU' MOT1/'MOT' ;
        'SI' (('EXIS' MOT1) 'ET' ('EGA' MOT1 'AUTO')) ;
          IAUTO1   = VRAI ;
          'OPTI' 'ERRE' 'CONT' ;
          'OPTI' 'ACQU' 10 'ACQU' './germe' ;
          'ACQU' IGER2*'ENTIER' ;
          'OPTI' 'ERRE' 'NORM' ;
          'SI' ('EGA' ('TYPE' IGER2) 'ENTIER') ;
            IGER1    = 'ABS' IGER2 ;
          'FINS' ;
        'SINO' ;
          'ARGU' IGER1*'ENTIER' ;
        'FINS' ;
      'FINS' ;
    'FINS' ;
  'FIN' XB0 ;
*                                                                     *
* Tirage des points :                                                 *
  NB1      = NTIR1 ;
  TVAR1    = 'TABL' ;
  'REPE' B0 IDIM1 ;
    TVAR1 . &B0 = 'BRUI' 'BLAN' 'UNIF' (TMOY1 . &B0) (TECA1 . &B0)
      NB1 IGER1 ;
    IGER1    = 'ABS' ('ENTI' (1.E5 * ('EXTR' (TVAR1 . &B0) 1))) ;
  'FIN' B0 ;
  NPTSI1   = 0 ;
  'REPE' B1 NB1 ;
    IP1      = &B1 ;
    LCPTI1   = 'PROG' ;
    'REPE' B0 IDIM1 ;
      LCPTI1   = LCPTI1 'ET' ('PROG' ('EXTR' (TVAR1 . &B0) IP1)) ;
    'FIN' B0 ;
     SI ((VALE DIME) EGA 1);
       PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1 ) IP1) ;
     FINSI;
     SI ((VALE DIME) EGA 2);
       PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1 ) IP1)
                         ('EXTR' (TVAR1 . 2 ) IP1) ;
     FINSI;
     SI ((VALE DIME) EGA 3);
       PTSI1    = 'POIN' ('EXTR' (TVAR1 . 1 ) IP1)
                         ('EXTR' (TVAR1 . 2 ) IP1)
                         ('EXTR' (TVAR1 . 3 ) IP1) ;
     FINSI;
* CB215821 : Ancienne Syntaxe retirée en 2015
*    PTSI1    = 'POIN' LCPTI1 ;
    'SI' IMAIL1 ;
      PTINCI1  = ('MANU' 'POI1' PTSI1) 'INCL' MAIL1 TEXT1 'NOID' TOL1 ;
      'SI' (('NBNO' PTINCI1) 'EGA' 0) ;
        'ITER' B1 ;
      'FINS' ;
    'FINS' ;
    'SI' IPTS2 ;
      'SI' (NPTSI1 'EGA' 0) ;
        PTSI2    = PTS2 'POIN' 'PROC' PTSI1 ;
      'SINO' ;
        PTSI2    = (PTS1 'ET' PTS2) 'POIN' 'PROC' PTSI1 ;
      'FINS' ;
      DI2      = 'NORM' (PTSI2 'MOIN' PTSI1) ;
      'SI' (DI2 '>EG' DREP1) ;
        'SI' (NPTSI1 'EGA' 0) ;
          PTS1     = 'MANU' 'POI1' PTSI1 ;
        'SINO' ;
          PTS1     = PTS1 'ET' PTSI1 ;
        'FINS' ;
        NPTSI1   = NPTSI1 + 1 ;
      'FINS' ;
    'SINO' ;
      'SI' (NPTSI1 'EGA' 0) ;
        PTS1     = 'MANU' 'POI1' PTSI1 ;
        NPTSI1   = NPTSI1 + 1 ;
      'SINO' ;
         PTSI2    = PTS1 'POIN' 'PROC' PTSI1 ;
         DI2      = 'NORM' (PTSI2 'MOIN' PTSI1) ;
         'SI' (DI2 '>EG' DREP1) ;
           PTS1     = PTS1 'ET' PTSI1 ;
           NPTSI1   = NPTSI1 + 1 ;
         'FINS' ;
      'FINS' ;
    'FINS' ;
    'SI' (NPTSI1 'EGA' NPTS1) ;
      'QUIT' B1 ;
    'FINS' ;
  'FIN' B1 ;
*                                                                     *
* Creation d'un maillage vide si aucun point tire :                   *
  'SI' (NPTSI1 'EGA' 0) ;
    PTSI1    = 'MANU' 'POI1' PTSI1 ;
    PTS1     = PTSI1 'DIFF' PTSI1 ;
  'FINS' ;
*                                                                     *
* Nouveau germe si germe auto :                                       *
  'SI' IAUTO1 ;
    VECH1    = 'VALE' 'ECHO' ;
    IMPR1    = 'VALE' 'IMPR' ;
    'OPTI' 'ECHO' 0 ;
    'OPTI' 'IMPR' 10 'IMPR' './germe' ;
    'MESS' ('ABS' IGER1) ;
    'OPTI' 'IMPR' IMPR1 ;
    'OPTI' 'ECHO' VECH1 ;
  'FINS' ;
*                                                                     *
'FINS' ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* Message :                                                           *
'SAUT' 1 'LIGN' ;
'MESS' '*** Procedure @POINTIR :' ;
'MESS' '*** ' NPTSI1 '/' NPTS1 'points places pour' IP1
  'tirages effectues.' ;
'SAUT' 1 'LIGN' ;
*                                                                     *
'RESP' PTS1 ;
'FINP' ;
$$$$


