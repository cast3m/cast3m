$$$$ @LISSE
* @LISSE    PROCEDUR  CB215821  15/12/02    21:15:02     8719
************************************************************************
************************************************************************
*-----------------------------------------------------------------------
*
*   PROCEDURE  LISSE                   MLN/CERN JANVIER 91
*
*
*   LISSAGE D'UNE EVOLUTION PAR LA METHODE D'ENERGIE MINIMUM DE
*   DEFORMATION
*
*-----------------------------------------------------------------------
*
*   ENTREE
*
*
*    EVO_1            Evolution a lisser
*
*    FLO_1            Angle a l'origine en degre (flottant)
*                                ( -45 < FLO1 < 45 )
*    FLO-2            Angle a l'extremite en degre (flottant)
*                                ( -45 < FLO2 < 45 )
*
*    ENT_1            Nombre de points sur la courbe
*
*    ENT_2            1 : la latte est sur l'abcisse
*                     2 : la latte est sur l'ordonnee
*
*-----------------------------------------------------------------------
*
*   SORTIE
*
*    EVO_2            Evolution lissee
*
*
*-----------------------------------------------------------------------
'DEBP' @LISSE  EVO_1*'EVOLUTION' FLO_1*'FLOTTANT' FLO_2*'FLOTTANT'
 ENT_1*'ENTIER' ENT_2*'ENTIER' ;
'TITRE' ' LISSAGE EVOLUTION' ;
'OPTI' 'DIME' 3 'ELEM' 'SEG2' 'MODE' 'TRIDIM' ;
EVO_1Y = 'EXTR' EVO_1 'ABSC' ;
EVO_1Z = 'EXTR' EVO_1 'ORDO' ;
NBP = 'DIME' EVO_1Y ;
I = 0 ;
PY = 'TABLE' ;
PZ = 'TABLE' ;
PP = 'TABLE' ;
BLO = 'TABLE' ;
DIP = 'TABLE' ;
*
* la latte est sur l'abcisse
*
'SI' ('EGA' ENT_2 1) ;
*
'REPE' BOU_1 NBP ;
I =  I + 1 ;
PY.I =  ('EXTR' EVO_1Y I) ;
PZ.I =  ('EXTR' EVO_1Z I) ;
PPF = 'EXTR' EVO_1Y NBP ;
PPI = 'EXTR' EVO_1Y 1 ;
PPP = PPF - PPI ;
NELT = ENT_1 - 1 ;
DEN1 = PPP / NELT  ;
'DENS' DEN1 ;
PP.I = (0. PY.I 0.) ;
'SI' ('EGA' 2 I) ;
MAI_1 = PP.(I - 1) 'DROI' PP.I ;
'FINSI' ;
'SI' (I >  2) ;
  'SI' ('EGA' PY.I PPF) ;
    NEL1 = 'NBEL' MAI_1 ;
    NEL2 = NELT - NEL1 ;
    MAI_1 = MAI_1 'DROI' NEL2 PP.I ;
  'SINON' ;
    MAI_1 = MAI_1 'DROI' PP.I ;
  'FINSI' ;
'FINSI' ;
BLO.I = 'BLOQ' 'UZ' PP.I ;
DIP.I = 'DEPI' BLO.I PZ.I ;
'FIN' BOU_1 ;
BLOX = 'BLOQ' 'UX' 'UY' PP.1 ;
BLOY =  @STBL  BLO ;
BLOYZ = 'BLOQ' 'RY' 'RZ' PP.1 ;
DIPY =  @STBL  DIP ;
    'SI' ((FLO_1 <EG 45.) 'ET' (FLO_2 <EG 45.)) ;
       ANG1 = FLO_1 * (PI / 180.) ;
       BLOR1 = 'BLOQ' 'RX' (MAI_1 'POIN' 1) ;
       DIPR1 = 'DEPI' BLOR1 ANG1 ;
       ANG2 = FLO_2 * (PI / 180.) ;
       BLOR2 = 'BLOQ' 'RX' (MAI_1 'POIN' 'FINA') ;
       DIPR2 = 'DEPI' BLOR2 ANG2 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR1 'ET' BLOR2 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR1 'ET' DIPR2 ;
    'FINSI' ;
    'SI' ((FLO_1 > 45.) 'ET' (FLO_2 > 45.)) ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOYZ ;
       DIPT = DIPY ;
    'FINSI' ;
    'SI' ((FLO_1 <EG 45.) 'ET' (FLO_2 > 45.)) ;
       ANG1 = FLO_1 * (PI / 180.) ;
       BLOR1 = 'BLOQ' 'RX' (MAI_1 'POIN' 1) ;
       DIPR1 = 'DEPI' BLOR1 ANG1 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR1 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR1 ;
    'FINSI' ;
    'SI' ((FLO_1 > 45.) 'ET' (FLO_2 <EG 45.)) ;
       ANG2 = FLO_2 * (PI / 180.) ;
       BLOR2 = 'BLOQ' 'RX' (MAI_1 'POIN' 'FINA') ;
       DIPR2 = 'DEPI' BLOR2 ANG2 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR2 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR2 ;
    'FINSI' ;
*
'FINSI' ;
*
* la latte est sur l'ordonnee
*
'SI' ('EGA' ENT_2 2) ;
*
'REPE' BOU_1 NBP ;
I =  I + 1 ;
PY.I =  ('EXTR' EVO_1Y I) ;
PZ.I =  ('EXTR' EVO_1Z I) ;
PPF = 'EXTR' EVO_1Z NBP ;
PPI = 'EXTR' EVO_1Z 1 ;
PPP = PPF - PPI ;
NELT = ENT_1 - 1 ;
DEN1 = PPP / NELT  ;
'DENS' DEN1 ;
PP.I = (0. 0. PZ.I) ;
'SI' ('EGA' 2 I) ;
MAI_1 = PP.(I - 1) 'DROI' PP.I ;
'FINSI' ;
'SI' (I >  2) ;
  'SI' ('EGA' PZ.I PPF) ;
    NEL1 = 'NBEL' MAI_1 ;
    NEL2 = NELT - NEL1 ;
    MAI_1 = MAI_1 'DROI' NEL2 PP.I ;
  'SINON' ;
    MAI_1 = MAI_1 'DROI' PP.I ;
  'FINSI' ;
'FINSI' ;
BLO.I = 'BLOQ' 'UY' PP.I ;
DIP.I = 'DEPI' BLO.I PY.I ;
'FIN' BOU_1 ;
'TASS' MAI_1 ;
BLOX = 'BLOQ' 'UX' 'UZ' PP.1 ;
BLOY =  @STBL  BLO ;
BLOYZ = 'BLOQ' 'RY' 'RZ' PP.1 ;
DIPY =  @STBL  DIP ;
    'SI' ((FLO_1 <EG 45.) 'ET' (FLO_2 <EG 45.)) ;
       ANG1 = FLO_1 * (PI / 180.) ;
       BLOR1 = 'BLOQ' 'RX' (MAI_1 'POIN' 1) ;
       DIPR1 = 'DEPI' BLOR1 ANG1 ;
       ANG2 = FLO_2 * (PI / 180.) ;
       BLOR2 = 'BLOQ' 'RX' (MAI_1 'POIN' 'FINA') ;
       DIPR2 = 'DEPI' BLOR2 ANG2 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR1 'ET' BLOR2 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR1 'ET' DIPR2 ;
    'FINSI' ;
    'SI' ((FLO_1 > 45.) 'ET' (FLO_2 > 45.)) ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOYZ ;
       DIPT = DIPY ;
    'FINSI' ;
    'SI' ((FLO_1 <EG 45.) 'ET' (FLO_2 > 45.)) ;
       ANG1 = FLO_1 * (PI / 180.) ;
       BLOR1 = 'BLOQ' 'RX' (MAI_1 'POIN' 1) ;
       DIPR1 = 'DEPI' BLOR1 ANG1 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR1 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR1 ;
    'FINSI' ;
    'SI' ((FLO_1 > 45.) 'ET' (FLO_2 <EG 45.)) ;
       ANG2 = FLO_2 * (PI / 180.) ;
       BLOR2 = 'BLOQ' 'RX' (MAI_1 'POIN' 'FINA') ;
       DIPR2 = 'DEPI' BLOR2 ANG2 ;
       BLOT = BLOX 'ET' BLOY 'ET' BLOR2 'ET' BLOYZ ;
       DIPT = DIPY 'ET' DIPR2 ;
    'FINSI' ;
*
'FINSI' ;
*
NNN = NBNO MAI_1 ;
'MESS' NNN ;
ME = 'TEXT' 'MECANIQUE ELASTIQUE' ;
AFF1 = 'MODE' MAI_1 ME 'POUTRE' ;
MAT1 = 'MATE' AFF1 'YOUN' 1. 'NU' .25 ;
CAR1 = 'CARA' AFF1 'SECT' 1. 'INRY' 1. 'INRZ' 1. 'TORS' 2. ;
RIG1 = 'RIGI' AFF1 (MAT1 ET CAR1) ;
RIGT = RIG1 ET BLOT ;
DEP1 = 'RESO' RIGT DIPT ;
DEF0 = MAI_1 'PLUS' DEP1 ;
I = 0 ;
NBP1 = 'NBNO' DEF0 ;
'REPE' BOU_2 NBP1 ;
I = I + 1 ;
'SI' ('EGA' I 1) ;
LIY = 'PROG' ('COOR' 2 (DEF0 'POIN' I)) ;
LIZ = 'PROG' ('COOR' 3 (DEF0 'POIN' I)) ;
'SINON' ;
LIY = LIY 'ET' ('PROG' ('COOR' 2 (DEF0 'POIN' I))) ;
LIZ = LIZ 'ET' ('PROG' ('COOR' 3 (DEF0 'POIN' I))) ;
'FINSI' ;
'FIN' BOU_2 ;
EVO_2 = 'EVOL' 'ROUGE' 'MANU' LIY LIZ ;
DESSIN (EVO_1 ET EVO_2) ;
*
'FINPROC' EVO_2 ;
$$$$



