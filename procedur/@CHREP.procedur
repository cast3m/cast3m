$$$$ @CHREP
* @CHREP    PROCEDUR  MB234859  17/09/26    21:15:02     9567
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*     PROCEDURE DE CHANGEMENT DE REPERE DE COORDONNEES (@CHREP)
*
*   Passage du repere (O,x,y) au repere (O_u,U1,U2)
*
* Entrees  :
*   PTFIXE   : Point repere O_u
*   VDU1     : Vecteur directeur U1
*   VDU2     : Vecteur directeur U2
*   MAIL2CHR : Maillage concerne par le changement de repere
*
* Sorties :
*   CU1_MAIL : coordonnee X
*   CU2_MAIL : coordonnee Y
*
* Appelee par @PRCA4
*
***********************************************************************
DEBP @CHREP PTFIXE*'POINT' VDU1*'POINT' VDU2*'POINT'
            MAIL2CHR*'MAILLAGE' ;

* Coordonnees des points sujets au changement de repere
  CUX_M2C = COOR MAIL2CHR 1 ;
  CUY_M2C = COOR MAIL2CHR 2 ;

* Coordonnees du point fixe
  PO_UX    = COOR PTFIXE 1 ;
  PO_UY    = COOR PTFIXE 2 ;

* Translation vers le nouveau point fixe
  CUTX_M2C  = CUX_M2C - PO_UX ;
  CUTY_M2C  = CUY_M2C - PO_UY ;

* Extraction des composantes des vecteurs directeurs du nouveau repere
  U1_X = COOR VDU1 1 ;
  U1_Y = COOR VDU1 2 ;
  U2_X = COOR VDU2 1 ;
  U2_Y = COOR VDU2 2 ;

* Calcul de l'angle du vecteur U1 par rapport a UX
  CosMns1  = ACOS U1_X ;
  SI (U1_Y > 0.) ;
    AngUXU1 = CosMns1 ;
  SINON ;
    AngUXU1 = -1. * CosMns1 ;
  FINSI ;

* Pseudo matrice de rotation
  CosAng = COS AngUXU1 ;
  SinAng = SIN AngUXU1 ;

* Formule de rotation (changement de repere : rotation de - AngUXU1)
*  U1 = ( CosAng * UX + SinAng * UY)
*  U2 = (-SinAng * UX + CosAng * UY)
  CU1_MAIL = (CosAng * CUTX_M2C) + (SinAng * CUTY_M2C) ;
  CU2_MAIL = (CosAng * CUTY_M2C) - (SinAng * CUTX_M2C) ;

FINP CU1_MAIL CU2_MAIL ;
$$$$

