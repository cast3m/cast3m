$$$$ @LAMASS
* @LAMASS   PROCEDUR  KK2000    99/11/30    21:15:15     3720
*
************************************************************************
*                          PROCEDURA @LAMASS
************************************************************************
*
* DESCRIZIONE : Calcola la matrice di massa di un laminato
*
* Sintassi    : mass1 = @lamass tcomp ;
*
* Autore/Data : A. Miliozzi 10/04/97
*
* Revisioni   :
*
************************************************************************
*
* Input  :   tcomp = tavola caratteristica del multistrato (TABLE)
*
* Output :   mass1 = matrice di massa (MCHAML)
*
************************************************************************
*
DEBPROC @LAMASS TT*TABLE ;
*
 LOG1 = EGA (TT.TIPO) 'OMOG' ;
 SI LOG1 ; TT.TIPO = MOT 'MLAY' ; FINSI ;
*
 NZON = (DIME TT) - 2 ;
 NN1    = 1 ;
*
 REPETER LOOP1 NZON ;
*
   OBJ = TT.NN1.MOD1   ;
   TM  = @LAMAT TT NN1 ;
*
   NLAM = (DIME TT.NN1) - 6 ;
   NN2    = 1 ;
*
   REPETER LOOP2 NLAM ;
*
     MAT1 = TM.MAT.NN2        ;
     MASS1 = MASS OBJ MAT1    ;
     SI ( (NN1+NN2) EGA 2 )   ;
       MASSA = MASS1          ;
     SINON                    ;
       MASSA = MASSA ET MASS1 ;
     FINSI                    ;
*
     NN2 = NN2 + 1 ;
*
   FIN LOOP2 ;
*
   NN1 = NN1 + 1 ;
*
 FIN LOOP1 ;
*
 SI LOG1 ; TT.TIPO = MOT 'OMOG' ; FINSI ;
*
FINPROC MASSA ;
*
************************************************************
*                FINE PROCEDURA @LAMASS
************************************************************
*


