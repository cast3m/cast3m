$$$$ KWEIB1
* KWEIB1    PROCEDUR  AM        09/12/07    21:15:52     6578
DEBPROC KWEIB1 x1*flottant;
*-------------------------------------------------------------------------------
* Calcul du parametre K pour la loi de Weibull min avec TAU=0 et 0<CV<1 donne
*-------------------------------------------------------------------------------
*
*    PROCEDURE GRACIEUSEMENT MISE A DISPOSITION PAR F. DUPRAT
*

SI (x1 <EG 0.)
 MESS 'KWEIB1 : X<0';
 k3=0.;
 QUIT KWEIB1;
FINSI;
SI (x1 >EG 1.)
 MESS 'KWEIB1 : X>1';
 k3=0.;
 QUIT KWEIB1;
FINSI;

prec1=1.e-8;
itmax1=100;
k1=1.;
dk1=0.1/x1;
REPE bouc1;
 SI (&bouc1 > itmax1);
    quit bouc1;
 FINSI;
 f1=(GAM1 (1.+(1./k1)));
 f2=(GAM1 (1.+(2./k1)));
 g1=f2-((f1**2)*(1.+(x1*x1)));
 SI (g1 < 0.);
   k2=k1;
   k1=k1-dk1;
   QUIT bouc1;
 SINON;
   k1=k1+dk1;
 FINSI;
FIN bouc1;
SI (&bouc1 > itmax1);
 MESS 'KWEIB1 : iter>100 (1)';
 QUIT KWEIB1;
 k3=0.;
FINSI;
REPE bouc1;
 SI (&bouc1 > itmax1);
    quit bouc1;
 FINSI;
 k3=(k1+k2)/2.;
 f1=(GAM1 (1.+(1./k3)));
 f2=(GAM1 (1.+(2./k3)));
 g1=f2-((f1**2)*(1.+(x1*x1)));
 SI (ABS(g1) < prec1);
   QUIT bouc1;
 SINON;
   SI (g1 > 0.);
     k1=k3;
   SINON;
     k2=k3;
   FINSI;
 FINSI;
FIN bouc1;
SI (&bouc1 > itmax1);
 MESS 'KWEIB1 : iter>100 (2)';
 k3=0.;
 QUIT KWEIB1;
FINSI;

FINPROC k3;


