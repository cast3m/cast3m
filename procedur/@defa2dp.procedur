$$$$ @DEFA2DP
* @DEFA2DP  PROCEDUR  AM        95/05/02    21:15:28     1649
DEBPROC @DEFA2DP TAI1*'FLOTTANT' ORI1*'POINT' VEC1*'POINT' TYP1*'MOT' ;
* [][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][]
* J.Y. COGNARD & D. DUREISSEIX   L.M.T. STRUCTURES & CMAO    le 03-03-93
*
* Procedure pour construire un appui en 2D (voir @DEFA2DL)
*     DEFinition d'un APpui en 2D pour un Point
*
* on envoie
*         TAI1  FLOTTANT pour definir la taille
*         ORI1  POINT    pour definir l'origine
*         VEC1  POINT    vecteur interieur pour l'orientation
*         TYP1  MOT      pour definir le type d'appui
*                        'roul' pour un appui simple
*                        'enca' pour un encastrement (par defaut)
* on recupere
*         MAI1  MAILLAGE definissant l'appui
* ======================================================================
VEC1 = VEC1 / ( 'NORME' VEC1 ) ;  RAC3 = ( 3. ** 0.5 ) / 2. ;
VEC2 = ( -1. * ( 'COOR' 2 VEC1 ) ) ( 'COOR' 1 VEC1 ) ;
* triangle
PT2 = ( ORI1 'MOINS' ( ( TAI1 * RAC3 ) * VEC1 ) )
             'MOINS' ( ( TAI1 * 0.5  ) * VEC2 );
PT3 = PT2 'PLUS' ( TAI1 * VEC2 ) ;
TRI1 = 'MANU' 'TRI3' ORI1 PT2 PT3 ;
* bati
PT4 = PT3 ;  COE1 = TAI1 * 0.3 ;
PT5 = ( PT4 'PLUS' ( COE1 * VEC2) ) 'MOINS' ( COE1 * VEC1) ;
BAT0 = MANU 'SEG2' PT4 PT5 ;  BAT1 = BAT0 ;
I1 = 0 ;  VCI1 = ( TAI1 / 4. ) * VEC2 ;
'REPETE' BLO1 4;
  I1 = I1 + 1;  BAT0 = BAT0 'MOINS' VCI1 ;  BAT1 = BAT1 'ET' BAT0;
'FIN' BLO1;
*
'SI' ( 'EGA' TYP1 'roul' ) ;
*   roulettes
*   """""""""
  RAY1 = TAI1 * 0.2 ;  VCI1 = RAY1 * VEC1 ;  VCI2 = RAY1 * VEC2 ;
  PTR2 = ORI1 'PLUS' VCI1 ;  PTR3 = ORI1 'MOINS' VCI1 ;
  PTR4 = ORI1 'PLUS' VCI2 ;  PTR5 = ORI1 'MOINS' VCI2 ;
  ROU0 = ( 'CERC' PTR2 ORI1 PTR4 4 ) 'ET' ( 'CERC' PTR4 ORI1 PTR3 4 )
    'ET' ( 'CERC' PTR3 ORI1 PTR5 4 ) 'ET' ( 'CERC' PTR5 ORI1 PTR2 4 ) ;
  ROU1 = ROU0 'MOINS' ( ( ( TAI1 * RAC3 ) + RAY1 ) * VEC1 ) ;
  VCI2 = ( ( TAI1 * ( 0.5 - RAC3 ) ) * VEC2 ) ;
  ROU2 = ROU1 'PLUS' VCI2 ;  ROU3 = ROU1 'MOINS' VCI2 ;
*
*   translation du bati
  BAT1 = BAT1 'ET' ( 'MANU' 'SEG2' PT2 PT3 ) ;
  BAT1 = BAT1 'MOINS' ( ( TAI1 * 0.4 ) * VEC1 ) ;
*   maillage final
  MAI1 = TRI1 'ET' BAT1 'ET' ROU2 'ET' ROU3 ;
'SINON' ;
* point fixe
* """"""""""
* maillage final
  MAI1 = TRI1 'ET' BAT1;
'FINSI' ;
*
'FINPROC' MAI1 ;
