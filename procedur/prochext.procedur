$$$$ PROCHEXT
* PROCHEXT  PROCEDUR  MAGN      17/12/11    21:15:01     9661
 'DEBPROC' PROCHEXT TPS*'FLOTTANT' rxt*'TABLE' KASX*'MOT';
*
* Cette procédure vise à imposer un coefficient d'échange variable
* en temps et/ou en espace.
* Attention! Les propriétés physiques sont à ajuster!
IOK=0                                               ;
mess ' Debut PROCHEXT TPS=' TPS ' KASX=' KASX       ;

Si ('EGA' KASX 'KAS1')                              ;
IOK=1                                               ;
* KAS1 On impose un coefficient d'échange possiblement variable en temps
* mais différent sur la partie immergée de la coque (mer) et sur la
* partie émmergée de la coque (air). Le niveau de l'eau peut aussi
* varier en temps.
*

ARGU HZ*'EVOLUTION' HAIR*'EVOLUTION' TAIR*'EVOLUTION'
                    HMER*'EVOLUTION' TMER*'EVOLUTION';
 $parext=rxt.'GEO'.'$parext'                        ;
 pare=doma $parext 'MAILLAGE'                       ;
 cz= coor 3 pare                                    ;

 hhz=ipol tps hz                                    ;
 czbas  = masq cz 'INFERIEUR' HHZ                   ;
 czhaut = masq cz 'SUPERIEUR' HHZ                   ;

 hhmer=ipol tps hmer                                ;
 ttmer=ipol tps tmer                                ;
 hhair=ipol tps hair                                ;
 ttair=ipol tps tair                                ;
hext =(czbas*hhmer) + (czhaut*hhair)                ;
tpext=(czbas*ttmer) + (czhaut*ttair)                ;
 rxt.'TIC'.'HEXTc'   = noel hext  $parext           ;
 rxt.'TIC'.'TPEXTc'  = noel tpext $parext           ;
 rxt.'TIC'.'HEXT'    = hext                         ;
 rxt.'TIC'.'TPEXT'   = tpext                        ;

'FINSI'                                             ;

Si (EGA KASX 'KAS2')                                ;
IOK=1                                               ;
* KAS2 On impose un coefficient d'échange variable
* de convection naturelle sur la coque différent sur la partie
* immergée (mer) et sur la partie émmergée.
* Les propriétés physique de l'air et de l'eau son imposées.
nuair = 1.8e-5 ; nueau=1.e-6                        ;
lamair= 2.7e-2 ; lameau=0.6                         ;
*

ARGU HZ*'EVOLUTION'
 TAIR*'EVOLUTION' LAIR*'FLOTTANT'
 TMER*'EVOLUTION' LMER*'FLOTTANT'                   ;
 $parext=rxt.'GEO'.'$parext'                        ;
 pare=doma $parext 'MAILLAGE'                       ;
 cz= coor 3 pare                                    ;
 checz  = 'CHAN' 'CHAM' cz pare                     ;
 hhz=ipol tps hz                                    ;
 parhaut= elem checz 'SUPERIEUR' HHZ                ;
 parbas = diff pare parhaut                         ;
 parbas = parbas  coul bleue                        ;
 parhaut= parhaut coul rouge                        ;
 parb1= chan 'POI1' parbas                          ;
 parh1= chan 'POI1' parhaut                         ;
 mit = inter parb1 parh1                            ;
 mit = mit coul rouge                               ;

*Propriétés physiques complémentaires
 ttair=ipol tps tair                                ;
 ttmer=ipol tps tmer                                ;
 Betair=1./(Ttair + 273.15)                         ;
 Beteau=3.e-3                                       ;


* Corrélation cv nat pour coef échange en air
 tpexta=(kcht $parext scal sommet Ttair)redu parhaut;
 tpxa = (rxt.'TIC'.'TP') redu parhaut               ;
 dtxa = tpxa - tpexta abs                           ;
 dtair=0.5*((maxi dtxa)+(mini dtxa))                ;
 lhair=(Nuair*((Lair/9.81/Betair/dtair )**0.5)**0.5);
 rxt.'TIC'.'lhair'=lhair                            ;
 mess 'dtair=' dtair ' lhair=' lhair ' lair=' lair  ;
 Lhair=Lair                                         ;
 grxa = 9.81* Betair*dtxa*(Lair**3.)/(nuair**2.)    ;
 hexta = 0.113*(grxa**0.33)*lamair/lhair            ;
                                                    ;
* Corrélation cv nat pour coef échange en eau
 tpexte=(kcht $parext scal sommet Ttmer) redu parbas;
 tpxe = (rxt.'TIC'.'TP') redu parbas                ;
 dtxe = tpxe - tpexte abs                           ;
*dteau=0.5*((maxi dtxe)+(mini dtxe))                ;
*lhmer=(Nueau*((Lmer/9.81/Beteau/dteau )**0.5)**0.5);
*rxt.'TIC'.'lhmer'=lhmer                            ;
*mess 'dteau=' dteau ' lhmer=' lhmer ' lmer=' lmer  ;
 Lhmer=Lmer                                         ;
 grxe = 9.81* Beteau*dtxe*(Lmer**3.)/(nueau**2.)    ;
 hexte = 0.113*(grxe**0.33)*lameau/lhmer            ;

 hext = hexta  +  hexte                             ;
 tpext= tpexta + tpexte                             ;

 hexti=(redu hext mit)*0.5                          ;
 hext = hext - hexti                                ;
 tpexti=(redu tpext mit)*0.5                        ;
 tpext = tpext - tpexti                             ;
*trace hext pare titr 'hext';                       ;
*trace tpext pare titr 'tpext'                      ;

 hextc   = 'NOEL' hext $parext 'CENTRE'             ;
 Tpextc  = 'NOEL' tpext $parext 'CENTRE'            ;
  rxt.'TIC'.'HEXTc'   = hextc                       ;
  rxt.'TIC'.'TPEXTc'  = tpextc                      ;
  rxt.'TIC'.'HEXT'    = hext                        ;
  rxt.'TIC'.'TPEXT'   = tpext                       ;

'FINSI'                                             ;

Si(IOK 'EGA' 0)                                      ;
Mess ' Cas ' KASX ' non prévu dans PROCHEXT'        ;
Mess ' On attend KAS1 ou KAS2 '                     ;
ERREUR 5                                            ;
Finsi                                               ;

 'FINPROC'                                          ;

