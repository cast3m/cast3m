$$$$ SIGNENVE
* SIGNENVE  PROCEDUR  CB215821  15/12/02    21:15:16     8719
'DEBPROC'   SIGNENVE  ZZTA*'TABLE   '  ;
* CALCUL DE L'ENVELOPPE ET DE LA CORRECTION DE L'ETAT INSTATIONNAIRE
*'MESS' 'DEBUT DU CALCUL DE L ENVELOPPE ' ; 'TEMPS' ;
*        SIGNENVE    ZZTA
*
*******************************************************************
*    ZZTA  :  OBJET DE TYPE TABLE CONTENANT
*
*          INDICE       TYPE          COMMENTAIRES
*
*           TYPENVE       MOT      TYPE DE L'ENVELOPPE
*           NPT         ENTIER     NOMBRE DE POINTS DU SIGNAL
*           DUREE      FLOTTANT    DUR{E DU SIGNAL
*           TDEBUT     FLOTTANT    DEBUT DU PLATEAU
*           TFIN       FLOTTANT    FIN DU PLATEAU
*           EPS        FLOTTANT    AMORTISSEMENT
*
*******************************************************************
*
*    ZDF             PAS EN FREQUENCE       = 1 / ZDUREE
*    ZDT             PAS EN TEMPS           =  ZDUREE /  ZNPT
*
*    ZFRQ0  ZFRQ     TABLE DES FREQUENCES (AVEC ET SANS FREQUENCE NULLE)
*                     ZFRQ0 = ZFRC1 + ZFRC2  ET DE LONGUEUR ZNBFR
*    ZNBFR           (ZNPT / 2 ) + 1          : NB DE PTS DU SPECTRE
*    ZTEMP           TABLE DES TEMPS  AYANT  ZNPT POINTS
*
*
*'MESS' 'DEBUT D EXECUTION DE LA PROCEDURE ENVE ' ;  'TEMPS' ;
*
*      CREATION DE L'ENVELOPPE
*
TYPENVE = 'MOT' 'TYPENVE'       ;  NPT     = 'MOT' 'NPT'         ;
DUREE   = 'MOT' 'DUREE'         ;
TDEBUT  = 'MOT' 'TDEBUT'        ;  TFIN    = 'MOT' 'TFIN'        ;
EPS     = 'MOT' 'EPS'           ;
*
  ZTYPENV = ZZTA.  TYPENVE      ;
ZCST = 'MOT' 'CONST'; ZNEG = 'MOT' 'N'; ZPTLIN = 'MOT' 'PLATLIN';
'SI' ( 'NON' ( 'EXIS' ZTYPENV ) ) ; ZTYPENV = ZNEG ; 'FINSI' ;
  ZNPT    = ZZTA. NPT           ;  ZDUREE  = ZZTA.  DUREE        ;
  ZNPT2   = ZNPT '/' 2          ;
'SI' ( 'EGA' ZTYPENV ZPTLIN ) ;
  ZTDEBUT = ZZTA.  TDEBUT       ;  ZTFIN   = ZZTA.  TFIN         ;
'FINSI' ;
  ZEPS    = ZZTA.  EPS          ;
ZDF       = 1.  '/' ZDUREE      ;  ZDT     = ZDUREE  '/'  ZNPT   ;
*
ZPI = PI ; Z2PI   = ZPI  '*'  2. ; Z4PI   = ZPI '*' 4.   ;
ZE  = EXP 1.    ; Z2PIDF = Z2PI '*' ZDF ; Z4PIDF = 2. '*' Z2PIDF;
             Z2PIEPS = Z2PI '*' ZEPS ;  Z4PIEPS = 2. '*' Z2PIEPS ;
Z4PIDFEP =  Z4PIDF '*' ZEPS ;
ZI   =  0 ;
'SI' ( ( 'EGA' ZTYPENV ZNEG ) 'OU' ( 'EGA' ZTYPENV ZCST ) ) ;
      ZLRENV = 'PROG'  ZNPT '*' 1.0  ;
*               CALCUL DES TERMES CORRECTIFS DUS A L ETAT INSTATIONNAIRE
      ZDEXP = 'EXP' ( -1. '*' Z4PI  )   ;
      ZEXP = 1.  ;
      ZLRCORR = 'PROG' ;
      REPETER LOOPINS1 ZNPT2 ;
          ZI =   ZI '+' 1 ;
          ZOM0 = Z2PIDF '*' ZI ;    Z2OM0EP = Z4PIDFEP '*' ZI ;
          ZEXP = ZEXP '*' ZDEXP ;
          ZCORR = ( 1. '-' ZEXP ) '/' ( Z2OM0EP '*' ZOM0 '*' ZOM0 ) ;
     ZLRCORR =  'INSERER' ZLRCORR ZI ZCORR ;
     'FIN'  LOOPINS1 ;
'SINON'  ;
     'SI' ( 'EGA' ZTYPENV ZPTLIN )     ;
          ZT3  =  ZDUREE ;
          ZT1 =  'ENTI' ( ZTDEBUT '/' ZDT ) '*' ZDT   ;
          ZT2 =  'ENTI' ( ZTFIN   '/' ZDT ) '*' ZDT   ;
          ZT3T2 = ZDUREE '-' ZT2       ;
          ZZA   =  1  '/' ZT1   ;  ZZC  =  ZT1 '-' ZDT   ;
          ZLRENV1 = 'PROG' 'LINE' 'A' ZZA 'PROG' 0. 'PAS' ZDT ZZC ;
          ZLRENV2 = 'PROG' 'LINE' 'A' 0. 'B' 1.
          'PROG' ZT1 'PAS' ZDT ZT2 ;
          ZZA  =   -1. '/'  ZT3T2  ;  ZZB  =  ZDUREE  '/'  ZT3T2  ;
          ZZC  =  ZT2 '+' ZDT      ;  ZZD  =  ZDUREE '-' ZDT  ;
          ZLRENV3 = 'PROG' 'LINE' 'A' ZZA 'B' ZZB
                                    'PROG' ZZC 'PAS' ZDT ZZD;
          ZLRENV  = ZLRENV1 'ET' ZLRENV2 'ET' ZLRENV3  ;
          'OUBLI'  ZLRENV1 ; 'OUBLI'  ZLRENV2 ; 'OUBLI'  ZLRENV3 ;
*
*    CALCUL DES TERMES CORRECTIFS DUS A L ETAT INSTATIONNAIRE
          ZT3T1     = ZT3 '-' ZT1 ;    ZT3T2 = ZT3 '-' ZT2 ;
          Z2T3T2    = 2. '*' ZT3T2  ; ZT3T2C  = ZT3T2 '*' ZT3T2 ;
          Z4T3T2C   = 4. '*' ZT3T2C ;
          Z2T1 = 2. '*' ZT1 ; ZT1C = ZT1 '*' ZT1 ; Z4T1C = 4. '*' ZT1C ;
          Z4PIEP31  = Z4PIEPS '*' ZT3T1 ; Z4PIEP32 = Z4PIEPS '*' ZT3T2 ;
          Z4PIEP3   = Z4PIEPS '*' ZT3   ;
          Z4PDFE31  =  -1. '*' Z4PIDFEP '*' ZT3T1  ;
          Z4PDFE32  =  -1. '*' Z4PIDFEP '*' ZT3T2  ;
          Z4PDFE3   =  -1. '*' Z4PIDFEP '*' ZT3   ;
          ZEXT3T1   = 1.  ; ZEXT3T2   = 1.  ;  ZEXT3   = 1.  ;
*
          ZLRENTI   = 'PROG' 1  'PAS' 1   ZNPT2  ;
          ZLRUNIT   = 'PROG' ZNPT2  '*'  1.  ;
          ZEVUNIT   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLRUNIT ;
*
          ZLROM0    = ZLRENTI '*' Z2PIDF ;
          ZEVOM0    = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLROM0 ;
          ZLROM0EP  = ZLROM0 '*' ZEPS ;
          LR2OM0EP  = ZLROM0EP '*' 2 ; LROM0EP2 = ZLROM0EP '**' 2 ;
*
          ZLRAAAA   =   LROM0EP2 '*' Z2T1     ;
          ZEVAAAA   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLRAAAA ;
          ZLRBBBB   =   LROM0EP2 '*' Z2T3T2   ;
          ZEVBBBB   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLRBBBB ;
          LROM0EP3  =   ZLROM0EP '**' 3  ;
          ZLRCCCC   =   LROM0EP3 '*' Z4T3T2C  ;
          ZEVCCCC   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLRCCCC ;
          ZLRDDDD   =  LROM0EP3 '*' Z4T1C    ;
          ZEVDDDD   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLRDDDD ;
*******
          ZLREXT31 = ZLRENTI '*' Z4PDFE31 ; ZLREXT31 = 'EXP' ZLREXT31 ;
          ZLREXT32 = ZLRENTI '*' Z4PDFE32 ; ZLREXT32 = 'EXP' ZLREXT32 ;
          ZLREXT3  = ZLRENTI '*' Z4PDFE3 ; ZLREXT3 = 'EXP' ZLREXT3   ;
          ZEVEXT31 = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLREXT31 ;
          ZEVEXT32 = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLREXT32 ;
*
          ZLREEEE   =   ZLREXT31 '-' ZLREXT3  ;
          ZEVEEEE   = 'EVOL' 'MANU'  'ABSC' ZLRENTI 'ORDO' ZLREEEE ;
*
          ZEVINVB   =   ZEVUNIT  '/' ZEVBBBB ;
          ZEVINVC   =   ZEVUNIT  '/' ZEVCCCC ;
*******
          'MESS' ZTITRE 'CORRECTION EFFETS INSTATIONNAIRES' ;
          'TITRE' ZTITRE 'CORRECTION EFFETS INSTATIONNAIRES' ;
          ZEVCORR = ( ZEVEEEE '/' ZEVDDDD ) '-' ( ZEVEXT31 '/' ZEVAAAA )
                      '-' ( ZEVEXT32 '*' ( ZEVINVB '+' ZEVINVC ) )
                      '+' ( ZEVUNIT '/' ZEVCCCC ) ;
          ZEVCORR = ZEVCORR '/' ( 2. '*' ZEVOM0 '*' ZEVOM0 )  ;
          ZLRCORR = 'EXTR' ZEVCORR 'ORDO' 1 ;
*     'FINSI';
    'SINON'  ;
          'ERREUR' 'TYPE D ENVELOPPE NON PREVU ' ;
    'FINSI' ;
'FINSI' ;
'SI' ( 'NON' ( IMPII '<' 1 ) ) ; 'DESS' ZEVENV ;  'FINSI' ;
*
ZENV = 'TABLE' ;
ZENV.'LRENV'   = ZLRENV   ;
ZENV.'LRCORR'  = ZLRCORR  ;
'FINPROC'    ZENV ;

