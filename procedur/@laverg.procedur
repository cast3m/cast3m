$$$$ @LAVERG
* @LAVERG   PROCEDUR  KK2000    99/11/30    21:15:24     3720
*
************************************************************************
*                        PROCEDURA @LAVERG
************************************************************************
*
* Descrizione : permette di effettuare una verifica grafica del
*               rateo di rottura relativo ad un criterio
*
* Sintassi    : @LAVERG TAB_CRIT NPLY OEIL1 ;
*
*
* Autore/Data : Miliozzi A.  10/04/1997
*
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_CRIT  Tavola dei ratei di rottura strato per strato
*           (Prodotta da LACRIT)
*
* NPLY      Numero dello strato che si vuole verificare
*
* OEIL1     Punto di vista
*
*--- OUTPUT
*==========
*
*
************************************************************************
*
DEBPROC @LAVERG TAB1*TABLE NPLY*ENTIER OEIL1*POINT ;
*
  GEO1 = EXTRAIRE (TAB1.NPLY) 'MAIL' ;
  NEL1 = NBEL GEO1 ;
  NN = 0 ;
*
*-----------------------------------------------------------------------
  REPETER LOOP1 NEL1 ;
*
    NN = NN + 1 ;
*
    EL1 = GEO1 ELEM NN ;
    CR1 = REDU (TAB1.NPLY) EL1 ;
    VAL1= MAXI CR1 ;
    DETR CR1 ;
*
    SI ( VAL1 < 0.2 ) ;
        EL2 = COUL EL1 BLEU ;       FINSI ;
    SI ( (VAL1 >EG 0.2) ET (VAL1 < 0.4 ) ) ;
        EL2 = COUL EL1 TURQ ;       FINSI ;
    SI ( (VAL1 >EG 0.4) ET (VAL1 < 0.6 ) ) ;
        EL2 = COUL EL1 VERT ;       FINSI ;
    SI ( (VAL1 >EG 0.6) ET (VAL1 < 0.8 ) ) ;
        EL2 = COUL EL1 ROSE ;       FINSI ;
    SI ( (VAL1 >EG 0.8) ET (VAL1 < 0.9 ) ) ;
        EL2 = COUL EL1 JAUN ;       FINSI ;
    SI ( (VAL1 >EG 0.9) ET (VAL1 < 1.0 ) ) ;
        EL2 = COUL EL1 BLAN ;       FINSI ;
    SI ( VAL1 >EG 1.0 ) ;
        EL2 = COUL EL1 ROUG ;       FINSI ;
*
    SI (NN EGA 1) ;
      ST1 = EL2  ;
    SINON ;
      ST1 = ST1 ET EL2  ;
    FINSI ;
*
  FIN LOOP1 ;
*-----------------------------------------------------------------------
*
  TITR ' 0. BLEU .2 TURQ .4 VERT .6 ROSE .8 JAUN .9 BLAN 1. ROUG ';
  TRAC OEIL1 ST1 FACE ;
***********************************************************************
*                       FINE PROCEDURA LAVERG
***********************************************************************
FINPROC ;


