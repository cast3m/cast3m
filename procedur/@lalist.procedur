$$$$ @LALIST
* @LALIST   PROCEDUR  KK2000    99/11/30    21:15:13     3720
*
************************************************************************
*                        PROCEDURA @LALIST
************************************************************************
*
* Descrizione : produce un listato delle caratteristiche, zona per
*               zona, dei laminati contenuti nella tavola di
*               definizione
*
* Sintassi    : LALIST TAB_COMP ;
*
* Autore/Data : Miliozzi A.  10/04/1997
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_COMP  tavola caratteristica dei laminati compositi
*
*--- OUTPUT
*==========
*
*
***********************************************************************
*
DEBPROC @LALIST TCOMP*TABLE ;
*
IECHO = VALE ECHO ;
OPTI ECHO 0 ;
*
MESS '                                                 '   ;
MESS ' ************************************************'   ;
MESS ' ************************************************'   ;
MESS ' ***   TABLE LISTING FOR LAMINATE COMPOSITE   ***'   ;
MESS ' ************************************************'   ;
MESS ' ************************************************'   ;
MESS '                                                 '   ;
MESS ' METHOD TYPE         = ' TCOMP.TIPO                  ;
MESS '                                                 '   ;
*
NMAT = DIME TCOMP.MCOL ;
MID = 0 ;
*
REPETER LOOPM NMAT ;
MID = MID + 1  ;
*
MESS '                                                 '   ;
MESS ' ************************************************'   ;
MESS ' ** M A T E R I A L   NUMBER : ' MID                 ;
MESS ' ************************************************'   ;
MESS '                                                 '   ;
MESS ' LONG.  YOUNG MODULUS= ' TCOMP.MCOL.MID.YG1     ;
MESS ' TRANS. YOUNG MODULUS= ' TCOMP.MCOL.MID.YG2     ;
MESS ' SHEAR MODULUS  G12  = ' TCOMP.MCOL.MID.G12     ;
MESS ' SHEAR MODULUS  G23  = ' TCOMP.MCOL.MID.G23     ;
MESS ' SHEAR MODULUS  G13  = ' TCOMP.MCOL.MID.G13     ;
MESS ' POISSON COEFFICIENT = ' TCOMP.MCOL.MID.NU12    ;
MESS ' LONG.  THERMAL COEF.= ' TCOMP.MCOL.MID.ALP1    ;
MESS ' TRANS. THERMAL COEF.= ' TCOMP.MCOL.MID.ALP2    ;
MESS ' DENSITY             = ' TCOMP.MCOL.MID.RHO     ;
MESS ' LONG.  TRAC. LIMIT  = ' TCOMP.MCOL.MID.XTR     ;
MESS ' LONG.  COMP. LIMIT  = ' TCOMP.MCOL.MID.XCO     ;
MESS ' TRANS. TRAC. LIMIT  = ' TCOMP.MCOL.MID.YTR     ;
MESS ' TRANS. COMP. LIMIT  = ' TCOMP.MCOL.MID.YCO     ;
MESS ' SHEAR LIMIT         = ' TCOMP.MCOL.MID.SSS     ;
*
FIN LOOPM ;
*
NZON = (DIME TCOMP) - 2                                    ;
N1    = 1                                                  ;
*
REPETER LOOP1 NZON                                         ;
*
MESS '                                                 '   ;
MESS ' ************************************************'   ;
MESS ' ** Z O N E    NUMBER : ' N1                         ;
MESS ' ************************************************'   ;
MESS '                                                 '   ;
xx = ' ' ;
nn = 0 ;
ll = TCOMP.N1.FELF ; dd = dime ll ;
repe loopx dd ;
nn = nn + 1 ;
xx = chain xx (extr ll nn) ;
fin loopx;
MESS ' FINITE ELEMEMTS TYPE= ' xx                          ;
MESS ' METHOD OF REFERNCE  = ' TCOMP.N1.METRIF             ;
MESS ' REFERENCE DIRECTION '                               ;
MESS '     - COORD. X      = ' (COOR 1 TCOMP.N1.DIRRIF)    ;
MESS '     - COORD. Y      = ' (COOR 2 TCOMP.N1.DIRRIF)    ;
MESS '     - COORD. Z      = ' (COOR 3 TCOMP.N1.DIRRIF)    ;
MESS ' NORMAL DIRECTION   '                                ;
MESS '     - COORD. X      = ' (COOR 1 TCOMP.N1.DIRNOR)    ;
MESS '     - COORD. Y      = ' (COOR 2 TCOMP.N1.DIRNOR)    ;
MESS '     - COORD. Z      = ' (COOR 3 TCOMP.N1.DIRNOR)    ;
*
NLAM = (DIME TCOMP.N1) - 6                                 ;
N2    = 1                                                  ;
*
REPETER LOOP2 NLAM                                         ;
*
MESS '                                                 '   ;
MESS ' ================================================'   ;
MESS '    L A M I N A    NUMBER : ' N2                     ;
MESS ' ================================================'   ;
MESS '                                                 '   ;
MESS ' THICKNESS           = ' TCOMP.N1.N2.EPA        ;
MESS ' ECCENTRICITY        = ' TCOMP.N1.N2.EXC        ;
MESS ' FIBER LAYOUT ANGLE  = ' TCOMP.N1.N2.ANG        ;
MESS ' MATERIAL ID.        = ' TCOMP.N1.N2.IDM        ;
SI (EXIST (TCOMP.N1.N2) G13) ;
MESS ' CORRECTIVE ACTIONS ON : '                      ;
MESS ' SHEAR MODULUS  G23  = ' TCOMP.N1.N2.G23        ;
MESS ' SHEAR MODULUS  G13  = ' TCOMP.N1.N2.G13        ;
FINSI ;
*
N2 = N2 + 1                                           ;
FIN LOOP2                                             ;
*
N1 = N1 + 1                                           ;
FIN LOOP1                                             ;
*
OPTI ECHO IECHO ;
*
FINPROC  ;
*
************************************************************************
*                       FINE PROCEDURA @LALIST
************************************************************************
*

