$$$$ FORNOD
* FORNOD    PROCEDUR  WANG      96/09/17    21:16:05     2278

'DEBPROC' FORNOD LIG1*'MAILLAGE' COM1*'MOT' FO*'FLOTTANT' ;

*---------------------------------------------------------------------
*
*     POUR CALCULER LES FORCES NODALES EQUIVALENTES D'UNE FORCE
*     SURFACIQUE  Fo
*     ELEMENT HORIZONTAL DE TYPE SEG3
*     MODE DE CALCUL : 'PLANDEFO' 'AXIS' ET 'FOUR 0, 1'
*
*     LIG1     'MAIILAGE'     ligne horizontale sur laquelle on
*                             applique la force surfacique
*     COM      'MOT'          nom de la composante de la force
*                             PLAN DEFO  : FX, FY
*                             FOUR 1     : FR ,FZ ,FT
*
*     FO       'FLOTTANT'     valeur de la composante de la force
*                             surfacique
*
*---------------------------------------------------------------------

TYP_MODE  = 'MOT' ('VALEUR' 'MODE') ;
'SI'   ('EGA' TYP_MODE 'PLANDEFO') ;
  ITYP = 1 ;
'SINON' ; 'SI' ('EGA' TYP_MODE 'FOUR') ;
  ITYP = 2 ;
'SINON' ;
  'MESSAGE' '           *****  ERREUR  *****' ;
  'MESSAGE' '   On ne traite pas les modèles tridimensionnelles.'
  'MESSAGE' '   Arrêt de la procédure.' ;
'QUITTER' BOUCERR ;
'FINSI' ; 'FINSI' ; 'FINSI' ;
*
*   Boucle sur les éléments
*
NB_ELEM = 'NBEL' LIG1 ;
I_EL = 0 ;
'REPETER' BOUCK NB_ELEM            ;
  I_EL = I_EL + 1                  ;
  ELEM_I = LIG1 'ELEMENT' I_EL     ;
  ELEM_B = 'CHANGER' ELEM_I 'POI1' ;
  NOEU1 = ELEM_B 'POINT' 1 ;
  NOEU2 = ELEM_B 'POINT' 2 ;
  NOEU3 = ELEM_B 'POINT' 3 ;
  R1 = 'COOR' NOEU1 1      ;
  R3 = 'COOR' NOEU3 1      ;
  LONG = 'ABS' (R3 - R1)   ;

***   MODE DEFO PLAN    ***

'SI' ('EGA' ITYP 1)      ;
F1 = LONG * FO / 6.      ;
F2 = F1 * 4.             ;
F3 = F1                  ;

'SI' ('EGA' I_EL 1) ;
FOR1 = ( 'FORCE' COM1 F1 NOEU1 ) 'ET' ( 'FORCE' COM1 F2 NOEU2 )
  'ET' ( 'FORCE' COM1 F3 NOEU3 ) ;
'SINON' ; FOR1 = FOR1 'ET' ( 'FORCE' COM1 F1 NOEU1 ) 'ET'
         ( 'FORCE' COM1 F2 NOEU2 ) ET ( 'FORCE' COM1 F3 NOEU3 ) ;
'FINSI' ;
'FINSI' ;

***   MODE AXIS ET FOURIER   ***

'SI' ( 'EGA' ITYP 2 ) ;
 F1 = PI * LONG * FO *   R1 / 6. ;
 F2 = PI * LONG * FO * ( R1 + R3 ) / 3. ;
 F3 = PI * LONG * FO *   R3 / 6. ;
'SI' ('EGA' I_EL 1) ;
 FOR1 = ( 'FORCE' COM1 F1 NOEU1 ) 'ET' ( 'FORCE' COM1 F2 NOEU2 )
       'ET' ( 'FORCE' COM1 F3 NOEU3 ) ;
'SINON' ;
 FOR1 = FOR1 'ET' ( 'FORCE' COM1 F1 NOEU1 ) 'ET'
          ( 'FORCE' COM1 F2 NOEU2 ) 'ET' ( 'FORCE' COM1 F3 NOEU3 ) ;
'FINSI' ;
'FINSI' ;
'FIN' BOUCK ;
'FINPROC' FOR1  ;
***********************************************************************


