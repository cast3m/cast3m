$$$$ SIGNCORR
* SIGNCORR  PROCEDUR  BP208322  16/05/24    21:15:17     8932
'DEBPROC'   SIGNCORR  ZZTB*'TABLE   '  ;
*
*    ZZTB  :  OBJET DE TYPE TABLE CONTENANT
*
*   LE SIGNAL A CORRIGER           EVSIG1          EVOLUTION
*   L' ACCELERATION MAXIMALE       GAMMA           FLOTTANT
*
*                                  RECHERCHE DU MAXIMUM DU SIGNAL
EVSIG1   = 'MOT' 'EVSIG1'   ;   ZEVSIG   = ZZTB. EVSIG1  ;
GAMMA  = 'MOT' 'GAMMA'          ;   ZGAMMA   = ZZTB. GAMMA         ;
*
ZSIG   = 'EXTR' ZEVSIG 'ORDO' 1   ; ZTEMP   = 'EXTR' ZEVSIG 'ABSC' 1 ;
ZPR    = .01 ; ZNPT = 'DIME' ZSIG ; 'MESS'    'NB NPTS'  ZNPT ;
AZSIG  = 'ABS' (  ZSIG  )         ; ZMM     = 'MAXI' AZSIG ;
'MESS' ' VALEUR ABSOLUE DU MAXIMUM DU SIGNAL    = '   ZMM  ;
'SI' ( '<' ( ( 'ABS' ( ZMM '-' ZGAMMA ) ) '/' ZGAMMA ) ZPR );
    'MESS' 'ON A ATTEINT L ACCELERATION MAXIMALE. PAS DE CORRECTION' ;
    ZEVPIC1    = ZEVSIG ;
'SINON'          ;
* CORRECTION : DEUX CAS POSSIBLES    ZMM < ZGAMMA    ET   ZMM > ZGAMMA
    'SI'   ( '<' ZMM ZGAMMA ) ;
        ZPICSIG = AZSIG 'MASQUE' 'EGALE' ZMM ;
        ZNBPICS = 1 ;
    'SINON' ;
        ZPICSIG = AZSIG 'MASQUE' 'SUPERIEUR' ZGAMMA ;
        ZEVO    = 'EVOL' MANU 'ABSC'   ZTEMP  'ORDO' ZPICSIG ;
        ZNBPICS =  'EXTR' ( 'COMT'  ZEVO    10. )  1  ;
    'FINSI' ;
    'MESS' 'NOMBRE DE PICS = ' ZNBPICS ;
    'SI' ( '>' ZNBPICS 4 ) ;
        'MESS' ' ATTENTION LE NOMBRE DE PICS A CORRIGER EST DE'
                      ZNBPICS ;
    'FINSI' ;
*       CHAQUE PIC EST DEFINI PAR LES INDICES DE DEBUT : ZPICCORD
*                                             DE FIN   : ZPICCORF
*           ET LA CORRECTION = GAMMA/VALEUR DU MAXIMUM : ZPICCOEF
     ZPICCORD = 'PROG'  ; ZPICCORF = 'PROG'  ; ZPICCOEF = 'PROG'  ;
*                                       REPERAGE DES PASSAGES A ZERO
     ZSIG1 = ZSIG '*'  1. ;   ZSIG2 = ZSIG '*'  1. ;
     ZSIG1 =  'INSE' ZSIG1 1 0. ;
      NPT1 = ZNPT '+' 1 ;
       ZSIG2 =  'INSE' ZSIG2 NPT1 0. ;
     ZZ = ZSIG1 '*' ZSIG2 ;  ZZ = 'ENLE' ZZ 1 ;
     ZPICZER    =  ZZ   'MASQUE'   'SUPERIEUR'  0.  ;
     'REMP' ZPICZER ZNPT 1. ;
*                          METTRE DES 1 SUR TOUS LES PICS A CORRIGER
     ZNUMPIC = 0 ; Z1 = 0 ;
    'REPETER' BOUCA ZNBPICS ;
         ZNUMPIC = ZNUMPIC '+' 1 ;
         'REPETER' BOUC1 ZNPT    ;
              Z1 = Z1 '+' 1 ;
             'SI' ( '>'  Z1  ZNPT ) ;
                  ZNUMPIC = ZNUMPIC '-' 1 ;
                  'QUIT'  BOUCA ;
             'FINSI' ;
*                RECHERCHE DU PROCHAIN PIC (ZNUMPIC) DEFINI PAR ZA=1
              ZA = 'EXTR' ZPICSIG Z1 ;
             'SI' ( 'EGA'  ZA  1  ) ;  'QUIT'  BOUC1 ; 'FINSI' ;
         'FIN' BOUC1 ;
*                 DEBUT DE DEPASSEMENT DE ZGAMMA, RECHERCHE DU MAXIMUM
         Z2 = Z1 ;  ZMAX = 'EXTR' AZSIG Z1 ;   ZMP = Z1 ;
         'REPETER' BOUCMAX  ZNPT    ;
*              RECHERCHE DE LA VALEUR DU MAX ZMAX ET DE SON INDICE ZMP
*                           LE MAX SE TROUVE DANS LA ZONE ZA = 1
              Z1 = Z1 '+' 1;
              'SI' ( '>' Z1 ZNPT )   ; 'QUIT'  BOUCMAX  ; 'FINSI' ;
              ZA = 'EXTR' ZPICZER Z1 ;
*                                        SI ZA=0 ON A DEPASSE LE MAX
              'SI' ( 'EGA'  ZA  0  ) ; 'QUIT'  BOUCMAX  ; 'FINSI' ;
              ZMA = 'EXTR' AZSIG Z1 ;
              'SI' ( '>' ZMA  ZMAX ) ; ZMAX = ZMA ; ZMP = Z1 ; 'FINSI';
         'FIN' BOUCMAX  ;
*                                 ZA = 0 : PASSAGE A ZERO (FIN DU PIC)
         Z1 = Z1 '-' 1 ;
         ZPICCORF =   'INSE' ZPICCORF ZNUMPIC Z1 ;
         'SI' ( 'NON' ( IMPII '<' 1 ) ) ;
              'MESS' 'MAX  '  ZNUMPIC ZMP ZMAX ;
         'FINSI' ;
*                          MISE DE ZCOEFF DS LA TABLE DES COEFFICIENTS
         ZPICCOEF = 'INSE' ZPICCOEF ZNUMPIC  ( ZGAMMA '/' ZMAX ) ;
*                          RECHERCHE DU DEBUT DU PIC
         'REPETER' BOUC2 ZNPT    ;
              Z2 = Z2 '-' 1;
              'SI' ( '<'  Z2  1    ) ;  'QUIT'  BOUC2 ; 'FINSI' ;
              ZB = 'EXTR' ZPICZER Z2 ;
              'SI' ( 'EGA'  ZB  0  ) ;  'QUIT'  BOUC2 ; 'FINSI' ;
         'FIN' BOUC2 ;
*                               ZB = 0 : PASSAGE A ZERO (DEBUT DU PIC )
         ZPICCORD = 'INSE' ZPICCORD ZNUMPIC Z2 ;
    'FIN' BOUCA ;
*        CORRECTION DU NOMBRE REEL DE PICS EN CAS DE PICS MULTIPLES
    'SI' ( '>' ZNBPICS ZNUMPIC ) ; ZNBPICS = ZNUMPIC ; 'FINSI' ;
    ZPICCOR1 = 'PROG' ;  ZPICCOR2 = 'PROG' ;
    ZF = 0  ;
    ZNUMPIC = 0 ;
    'REPETER' BOUC4 ZNBPICS ;
        ZNUMPIC  = ZNUMPIC '+' 1  ;
        ZD       = 'EXTR' ZPICCORD  ZNUMPIC ;
        ZD1      = ZD '-' ZF ;
       'SI' ( '>' ZD1 0.01 ) ;
          ZPICCOR0 = 'PROG' ZD1 '*' 1 ;
          ZPICCOR1 = ZPICCOR1 'ET' ZPICCOR0 ;
       'FINSI' ;
        ZCOEF    = 'EXTR' ZPICCOEF  ZNUMPIC ;
        ZF       = 'EXTR' ZPICCORF  ZNUMPIC ;
        ZD2      = ZF '-' ZD ;
       'SI' ( '>' ZD2 0.01 ) ;
          ZPICCOR0 = 'PROG' ZD2 '*' ZCOEF ;
          ZPICCOR1 = ZPICCOR1 'ET' ZPICCOR0 ;
       'FINSI' ;
    'FIN' BOUC4 ;
    ZD1      = ZNPT '-' ZF ;
    'SI' ( '>' ZD1 0.01 ) ;
      ZPICCOR0 = 'PROG' ZD1 '*' 1 ;
      ZPICCOR1 = ZPICCOR1 'ET' ZPICCOR0 ;
    'FINSI' ;
    ZPICCOR2 = ZPICCOR1 'MASQUE' 'EGALE' 1 ;
    ZPICCOR3 = ZPICCOR1 'MASQUE' 'DIFFERENT' 1 ;
*                                             CALCULER LES SURFACES
    ZEVPIC1  = 'EVOL' 'BLEU' 'MANU' 'ABSC' ZTEMP 'ORDO' ZPICCOR1 ;
    ZEVPIC2  = 'EVOL' 'VERT' 'MANU' 'ABSC' ZTEMP 'ORDO' ZPICCOR2 ;
    ZEVPIC3  = 'EVOL' 'ROUG' 'MANU' 'ABSC' ZTEMP 'ORDO' ZPICCOR3 ;
    ZEVPIC1  =  ZEVPIC1 '*' ZEVSIG     ;
    ZEVPIC2  =  ZEVPIC2 '*' ZEVSIG     ;
    ZSIG2    = 'EXTR' ZEVPIC2 'ORDO' 1 ;
    AZSIG2   = 'ABS' (  ZSIG2  )       ; ZM2 = 'MAXI' AZSIG2 ;
    'MESS' 'VALEUR ABSOLUE DU SIGNAL ' ZM2 ;
    SURF     = 'INTG'  ZEVSIG    ;  SURFSIG = 'EXTR'  SURF 1  ;
    SURF     = 'INTG'  ZEVPIC1   ;  SURF1   = 'EXTR'  SURF 1  ;
    SURF     = 'INTG'  ZEVPIC2   ;  SURF2   = 'EXTR'  SURF 1  ;
    'MESS' 'SURFACE DU SIGNAL NON CORRIGE '  SURFSIG ;
    'MESS' 'SURFACE DU SIGNAL CORRIGE DES POINTES '   SURF1 ;
    'MESS' 'SURFACE RESTANT A CORRIGER '              SURF2 ;
     ZC  = ( SURFSIG '-' SURF1 ) '/' SURF2 ; ZC = ZC '+' 1. ;
    'MESS' ' CORRECTION DE SURFACE ZC  '  ZC ;
    'SI' ( ( '<' ZC .8  ) 'OU' ( '>' ZC 1.2  ) ) ;
         'MESS' 'LA SURFACE DU SIGNAL N EST PAS RAMENEE A ZERO' ;
         'MESS' 'ESSAYER UN AUTRE TIRAGE AU SORT'  ;
    'SINON' ;
         'SI' ( '>' (ZC '*' ZM2 ) ZGAMMA ) ; ZC = ZGAMMA '/' ZM2 ;
         'FINSI' ;
         'MESS' ' CORRECTION DE SURFACE ZC' ZC ;
          ZPICCOR2 =  ZPICCOR2   '*' ZC  ;
          ZPICCOR2 =  ZPICCOR2   '+' ZPICCOR3 ;
          ZEVPIC2  = 'EVOL' 'VERT' 'MANU' 'ABSC' ZTEMP 'ORDO' ZPICCOR2;
          ZEVPIC1  = ZEVPIC2 '*' ZEVPIC1 ;
          SURF     = 'INTG'  ZEVPIC1     ;  SURFF   = 'EXTR'  SURF 1  ;
          'MESS' 'SURFF '   SURFF ;
     'FINSI'    ;
'FINSI'    ;
ZSIGN = 'TABLE'   ;     ZSIGN.'EVSIG2' =  ZEVPIC1  ;
'FINPROC'  ZSIGN     ;

