$$$$ VAPDIF
* VAPDIF    PROCEDUR  MAGN      00/10/12    21:17:54     3973
'DEBPROC' VAPDIF PM*FLOTTANT TM*CHPOINT YV*CHPOINT
                 YH2*CHPOINT YHE*CHPOINT YO2*CHPOINT
                 YN2*CHPOINT YCO2*CHPOINT YCO*CHPOINT;
*___________________________________________________________________
* Procedure donnant le coefficient de diffusion de la vapeur dans le
* melange
*___________________________________________________________________
*  E/ PM   : Pression totale (Pa)
*     TM   : Temperature du melange (K)
*     YV   : Fraction massique de vapeur
*     YH2  : Fraction massique de hydrogene
*     YHE  : Fraction massique de helium
*     YO2  : Fraction massique de oxygene
*     YN2  : Fraction massique de azote
*     YCO2 : Fraction massique de CO2
*     YCO  : Fraction massique de CO
*  /S DV   : coefficient de diffusion en m2/s
*___________________________________________________________________
* Constante absolue des gaz parfaits (produit du nombre d'Avogadro par
* la constante de Boltzmann) en J.mol-1.K-1
RABS = 6.022*1.38066;
* Masse molaire en kg.mol-1
MVAP = 0.018 ;
MH2 = 0.002 ;
MHE = 0.004 ;
MO2 = 0.032 ;
MN2 = 0.028 ;
MCO2 = 0.044 ;
MCO = 0.028 ;

* Constantes relatives des différents gaz en J.kg-1.K-1
RVAP = RABS/MVAP;
RH2 = RABS/MH2;
RHE = RABS/MHE;
RO2 = RABS/MO2;
RN2 = RABS/MN2;
RCO2 = RABS/MCO2;
RCO = RABS/MCO;

* Constante relative du mélange
Rmel = (YV*RVAP) + (YH2*RH2) + (YHE*RHE) + (YO2*RO2) + (YN2*RN2)
       + (YCO2*RCO2) + (YCO*RCO) ;

* Masse molaire du melange
Mmel = RABS*(Rmel**(-1.));

* Fractions molaires
XV = YV * Mmel / MVAP ;
XH2 = YH2 * Mmel / MH2 ;
XHE = YHE * Mmel / MHE ;
XO2 = YO2 * Mmel / MO2 ;
XN2 = YN2 * Mmel / MN2 ;
XCO2 = YCO2 * Mmel / MCO2 ;
XCO = YCO * Mmel / MCO ;

* Calcul du coefficient de diffusion DV
DV = (YV * 0.0) ;
TR = TM / 273.0 ;
N = 1.75 ;
A = 7.8 ;
DVH2 = ( TR ** N ) * A / PM ;
DV = DV + (XH2 * (INVE DVH2)) ;
N = 1.75 ;
A = 7.3 ;
DVHE = ( TR ** N ) * A / PM ;
DV = DV + (XHE * (INVE DVHE)) ;
N = 1.71 ;
A = 2.4 ;
DVO2 = ( TR ** N ) * A / PM ;
DV = DV + (XO2 * (INVE DVO2)) ;
N = 1.75 ;
A = 2.27 ;
DVN2 = ( TR ** N ) * A / PM ;
DV = DV + (XN2 * (INVE DVN2)) ;
N = 1.75 ;
A = 1.55 ;
DVCO2 = ( TR ** N ) * A / PM ;
DV = DV + (XCO2 * (INVE DVCO2)) ;
N = 1.75 ;
A = 2.2 ;
DVCO = ( TR ** N ) * A / PM ;
DV = DV + (XCO * (INVE DVCO)) ;
N = 0.0 ;
A = 27.7 ;
DVV = ( TR ** N ) * A / PM ;
DV = DV + (XV * (INVE DVV)) ;

DV = INVE DV ;
'RESPROC' DV ;
'FINPROC' ;

