$$$$ EPAIFUT
* EPAIFUT   PROCEDUR  CHAT      02/01/04    21:15:10     4282
*----------------------------------------------------------------
*
* La procedure EPAIFUT permet de determiner le rayon de rm de
* sechage intervenant dans les formulations BPEL 91 et Eurocode 2
* (EC2). Ce rayon est un champ par element qui est deduit du
* maillage du coffrage de beton par le calcul de la somme des
* epaisseurs des elements rencontres en allant de l'intrados vers
* l'extrados du mur
*
*
DEBP EPAIFUT MAYAV*MAILLAGE MAYAS1*MAILLAGE MAYAS2*MAILLAGE       ;
*
PPMAYAV = MAYAV CHAN POI1                                         ;
*
I = 0                                                             ;
REPETER BCL1 (NBNO MAYAV)                                         ;
  I     = I+1                                                     ;
  PCOUR = (ELEM I PPMAYAV) POIN INIT                              ;
  PS1   = MAYAS1 POIN PROC PCOUR                                  ;
  PS2   = MAYAS2 POIN PROC PCOUR                                  ;
  VPS1  = PCOUR MOIN PS1                                          ;
  VPS2  = PCOUR MOIN PS2                                          ;
  DS1   = NORM VPS1                                               ;
  DS2   = NORM VPS2                                               ;
  DS1S2 = DS1 + DS2                                               ;
  PPCOUR = PCOUR MANU POI1                                        ;
  CHPLOCAL = MANU CHPO PPCOUR 1 'EPAI' DS1S2 NATURE DIFFUS        ;
  SI (I EGA 1)                                                    ;
    CHPEPAI = CHPLOCAL                                            ;
  SINON                                                           ;
    CHPEPAI = CHPEPAI + CHPLOCAL                                  ;
  FINSI                                                           ;
FIN BCL1                                                          ;
*
FINP CHPEPAI                                                      ;


