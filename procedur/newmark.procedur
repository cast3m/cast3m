$$$$ NEWMARK
* NEWMARK   PROCEDUR  CHAT      09/06/26    21:15:02     6430
*---------------------------------------------------------------------
*             PROCEDURE NEWMARK   VERSION DU 15/01/88
*---------------------------------------------------------------------
*   CETTE PROCEDURE EFFECTUE UN CALCUL DYNAMIQUE D'UN PAS
*              SELON L'ALGORITHME DE NEWMARK
*
*   EN ENTREE :
*   ---------
*               U1    : DEPLACEMENT AU DEBUT DU PAS
*               V1    : VITESSE AU DEBUT DU PAS
*               A     : OPERATEUR DYNAMIQUE
*               K     : RIGIDITE QUI CONTIENT LES BLOQUAGES
*               M     : MASSE
*               F1    : FORCES AU DEBUT DU PAS QUI CONTIENT LES FLX
*               F2    : FORCES A LA FIN DU PAS QUI CONTIENT LES FLX
*               H     : PAS DE TEMPS
*
*   EN SORTIE :
*   ---------
*               U2    : DEPLACEMENT A LA FIN DU PAS
*               V2    : VITESSE A LA FIN DU PAS
*
*---------------------------------------------------------------------
*
      'DEBPROC' NEWMARK U1*'CHPOINT' V1*'CHPOINT' A*'RIGIDITE'
      K*'RIGIDITE' M*'RIGIDITE' F1*'CHPOINT' F2*'CHPOINT' H*'FLOTTANT';

tbid=table;
 K1 = 'EXTRAI' K 'RIGI' 'NOMU';
 K2 = 'EXTRAI' K 'RIGI' 'MULT';
 maico='EXTRAI' K 'MAIL' 'UNIL';
 nbcont= 'NBELEM' maico;

 F1NOMU = 'ENLEVER' F1 'FLX';

 SCA1 = 4. / H ;
 SCA2 = 2. / H ;
W1 = SCA1 * V1 ;
FA = M * W1 ;   'DETR' W1 ;
UAUX = 'EXCO' 'LX' U1 'NOID' 'LX' ;
UU1 = U1 - UAUX ;
W2 = 2. * UU1 ;
FB = K1 * W2 ; 'DETR' W2 ;
FC = FA - FB ; 'DETR' FA ; 'DETR' FB  ;
FD = F1NOMU + F2 ;
*
UUV=U1;
*en cas de cliaisons unilaterales on tue les mult associés.
'SI' (NBCONT > 0); UUR= 'REDU' U1 maico;UUV = U1 - UUR;'FINSI';
FG = K2 * UUV ;
'DETR' F1NOMU;
FF = FC + FD ; 'DETR' FC  ; 'DETR' FD  ; FH = FF - FG;
'DETR' FG  ; 'DETR' FF ;
DU = 'RESOU' A FH ;
'DETR' K1 ;
* 'DETR' K2 ;
U2 = UU1 + DU ;
 'DETR' UAUX  ;
W1 = ( 'ENLE' DU 'LX') * SCA2 ;
V2 = W1 - V1 ;
*

* y-a-t-il des contacts unilateraux?
  ICONTA=VRAI;
  MTA= 'EXTRAIRE' A   'CONTACT';  MDI = 'DIME' MTA;
  'SI' ( 'EGA' MDI 0) ;  ICONTA=FAUX; 'FINSI';
* EN CAS D'APPUIS UNILATERAUX
'SI' ICONTA;
   V2 = VITEUNIL A M V2   du   u1  h   fh  tbid;
*   si (( maxi ( vuu - v2) ) > 1.e-3); mess ' contact' ;finsi;
*   mess ' reactions ' ; list ( 'REAC'  k2  u2);
*   mess ' deplacements ' ; list u2;
*   mess ' vitesses'; list v2;
  'SI' ( 'EXIS' tbid 'RATE_VITEUNIL') ;
    'MESS' ' erreur dans viteunil ';
    'ERRE' 5;
  'FINSI';
'FINSI';
'DETR' W1 ; 'DETR' DU ;   'DETR' uu1;  'DETR' FH  tbid;
*-----------------------------------
*       SORTIE DE LA PROCEDURE
*-----------------------------------
   'FINPROC' U2 V2 ;
*---------------------------------------------------------------------



