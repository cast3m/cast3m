$$$$ ANIME
* ANIME     PROCEDUR  PV        97/08/01    21:15:02     2777
DEBPROC ANIME
  NFOIS*'ENTIER' GEO*'MAILLAGE'
  DEF*'CHPOINT'  AMPLD*'FLOTTANT'
  VEC/'CHPOINT';

'OUBLIER' CHPO;
'OUBLIER' CHEL;

TEX='TEXTE' ' ';
'SI' ('EXISTE' VEC);
  'ARGUMENT' AMPLV/'FLOTTANT' LACOUL/'MOT';
  'SI' ('NON' ('EXISTE' LACOUL)); LACOUL='VERT'; FINSI;
  'SI' ('EXISTE' AMPLV);
     TEX= TEXTE TEX 'VECTX';
     'ARGUMENT' CHPO/CHPOINT;
     'SI' ('EXISTE' CHPO); TEX = 'TEXTE' TEX 'CHPOX'; 'FINSI';
  'SINON';
     CHPO=VEC;  OUBLIER VEC;
     TEX = 'TEXTE' TEX 'CHPOX';
  'FINSI';
'FINSI';

'SI' ('NON' ('EXISTE' CHPO));
  'ARGUMENT' CHEL/'MCHAML';
'FINSI';

'SI' ('EXISTE' CHEL);
  'ARGUMENT' MODEL*'MMODEL';
  TEX = 'TEXTE' TEX 'CHELX' 'MODEL';
'FINSI';

 LEMODE='VALEUR' 'MODE';
  'SI' ('EGA' LEMODE 'PLANCONT'); LESCOMPS=TEXT 'FX FY'; FINSI;
  'SI' ('EGA' LEMODE 'PLANDEFO'); LESCOMPS=TEXT 'FX FY'; FINSI;
  'SI' ('EGA' LEMODE 'AXIS'); LESCOMPS=TEXT 'FR FZ'; FINSI;
  'SI' ('EGA' LEMODE 'FOUR'); LESCOMPS=TEXT 'FR FZ'; FINSI;
  'SI' ('EGA' LEMODE 'TRID'); LESCOMPS=TEXT 'FX FY FZ'; FINSI;

'SI' ('EXISTE' VEC); VECTX='VECTEUR' VEC 0 LESCOMPS LACOUL;'FINSI';
'SI' ('EXISTE' CHPO); CHPOX= CHPO * 0; 'FINSI';
'SI' ('EXISTE' CHEL); CHELX= CHEL * 0; 'FINSI';

DEFTOT = 'DEFO' GEO DEF 0 TEX BLEU;

'REPETER' BOUCLE1 NFOIS;

  SI (EXISTE CHEL); CHELX = CHEL * (&BOUCLE1 *1. / NFOIS ) ; FINSI;
  SI (EXISTE CHPO); CHPOX = CHPO * (&BOUCLE1 *1. / NFOIS ) ; FINSI;
  SI (EXISTE VEC); VECTX='VECTEUR' VEC (AMPLV*&BOUCLE1/NFOIS)
                                           LESCOMPS LACOUL;'FINSI';

  DEFX='DEFO'  DEF GEO (AMPLD * &BOUCLE1/NFOIS) TEX 'ROUGE';
DEFTOT=DEFTOT 'ET' DEFX ;

'FIN'     BOUCLE1;
'FINPROC' DEFTOT;



