$$$$ NORMALIM
* NORMALIM  PROCEDUR  BP208322  16/05/24    21:15:12     8932
*----------------------------------------------------------------------*
*                                                                      *
* PROCEDURE NORMALIM                                                   *
*                                                                      *
* NORMALISATION A 1 DE CHAQUE FONCTION M                               *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* ENTREE                                                               *
*                                                                      *
* M      M/T CONTENANT N FONCTION DE MODULATIONS                       *
*        TINI ET TFIN EGAUX POUR CHAQUE COURBE                         *
*                                                                      *
* DTIME  PAS DE TEMPS D'ANALYSE DE LA PREMIERE COURBE (POUR ONDELETTE) *
*                                                                      *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* SORTIE                                                               *
*                                                                      *
* MNORME M/T NORME                                                     *
*                                                                      *
* VALSPE (LISTREEL) MOYENNE DU SPECTRE DANS CHAQUE BANDE               *
*                                                                      *
*----------------------------------------------------------------------*
'DEBPROC' NORMALIM M*'EVOLUTION' DTIME/'FLOTTANT';
*----------------------------------------------------------------------*
'SI' ('EXISTE' DTIME);
  LONDEL=VRAI;
  DDTIME=DTIME;
'SINON';
  LONDEL=FAUX;
'FINSI';
*
'SI' ('NON' LONDEL);
  T1='EXTRAIRE' M 'ABSC' 1;
  TI='EXTRAIRE' T1 1;
  TF='EXTRAIRE' T1 ('DIMENSION' T1);
  NI=1000;PAS=(TF-TI)/NI;
  TT='PROG' TI 'PAS' PAS TF;
'FINSI';
*
VALSPE='PROG';
*
*----------------------------------------------------------------------*
NCOURB='DIMENSION' M;
ICOURB=0;
'REPETER' LABC NCOURB;
  ICOURB=ICOURB+1;
  MI='EXTRAIRE' M 'COURBE' ICOURB;
*
  'SI' LONDEL;
    MIX='EXTRAIRE' MI 'ABSC' 1;
    TI='EXTRAIRE' MIX 1;
    TE=('EXTRAIRE' MIX ('DIMENSION' MIX)) - TI;
    NPT='ENTIER' ((TE/DDTIME) + 1.E-5);
    TTEST=TE-(NPT*DDTIME);
    'SI' (('ABS' TTEST) '>' (DTIME*1.E-4));
      'MESSAGE' 'Le pas de temps (DTIME) est incoherent avec la';
      'MESSAGE' ' duree du signal ondelette (M) courbe' ICOURB;
      'QUITTER' LABC;
    'FINSI';
    TT='PROG' TI 'PAS' DDTIME 'NPAS' NPT;
  'FINSI';
*
  MMT='IPOL' TT ('EXTRAIRE' MI 'ABSC' 1) ('EXTRAIRE' MI 'ORDO' 1);
*
  'SI' LONDEL;
    NORME=('LTL' MMT) * DDTIME;
  'SINON';
    MMT2=MMT**2;
    MTT2='EVOL' 'MANU' TT MMT2;
    NORME='EXTRAIRE' ('INTG' MTT2) 1;
  'FINSI';
*
  NORME=NORME ** .5;
  VALSPE='INSERER' VALSPE NORME ICOURB;
*
  'SI' (NORME EGA 0.); MINORME=MI;
  'SINON';             MINORME=MI/NORME; 'FINSI';
  'SI' (ICOURB EGA 1); MNORME=MINORME;
  'SINON';             MNORME=MNORME 'ET' MINORME; 'FINSI';
*
  'SI' LONDEL; DDTIME=DDTIME/2; 'FINSI';
'FIN' LABC;
*----------------------------------------------------------------------*
'FINPROC' VALSPE MNORME;

