$$$$ CALCDISP
* CALCDISP  PROCEDUR  GBM       04/12/22    21:15:01     4973
*---------------------------------------------------------------------
*---------PROCEDURE DE CALCUL DE LA DISPERSIVITE----------------------
*---------------------------------------------------------------------
'DEBP'  CALCDISP QELEM*'CHPOINT' DISPL*'CHPOINT' DISPT*'CHPOINT'     ;
*  |-----------------------------------------------------------------|
*  | Généralités : CALCDISP calcule le tenseur de dispersion         |
*  |               du problème de transport convection-diffusion.    |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*  |-----------------------------------------------------------------|
*  |                         ENTREES                                 |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*  | ALPHAL   : coefficient longitudinal de dispersivité             |
*  |                                                                 |
*  | ALPHAT   : coefficient transverse de dispersivité               |
*  |                                                                 |
*  | QELEM    : vitesse au centre de chaque élément                  |
*  |-----------------------------------------------------------------|
*  |                         SORTIES                                 |
*  |-----------------------------------------------------------------|
*  |                                                                 |
*  |                                                                 |
*  | dif_disp : C'est le tenseur de dispersion                       |
*  |                                                                 |
*  |                                                                 |
**********************************************************************



* Seulement si présence de convection et présence de
* dispersivité et disp_l et disp_t

'SI' (DISPERSI )                                                       ;
* vpetit = 1.D-20 sur le meme nombre de zone que DISPL
   vpetit =  'MANUEL' 'CHPO' ('EXTRAIRE' DISPL MAILLAGE) 'SCAL' 1.D-30 ;
   vpetit = 'NOMC' 'SCAL' vpetit 'NATU' DISCRET                        ;
   DISPL = 'NOMC' 'SCAL' DISPL                                         ;
   DISPT = 'NOMC' 'SCAL' DISPT                                         ;


*   on calcul la dispersivité
*   dimension 2
   'SI' ('EGA' ('VALEUR' 'DIME') 2)                                    ;
      V1 = 'EXCO' Qelem 'VX' 'SCAL'                                    ;
      V2 = EXCO Qelem 'VY' 'SCAL'                                      ;
      VC = (V1 * V1) '+' (V2 * V2)                                     ;
      VC = (VC**0.5)                                                   ;
      VC = VC + vpetit                                                 ;
      VCM1 = VC '**' -1                                                ;
      D11P = ((DISPL - DISPT) * V1*V1*VCM1) + (DISPT * VC)             ;
      D22P = ((DISPL - DISPT) * V2*V2*VCM1) + (DISPT * VC)             ;
      D21P = (DISPL - DISPT) * V1*V2*VCM1                              ;

      D11P = 'NOMC' K11 D11P                                           ;
      D21P = 'NOMC' K21 D21P                                           ;
      D22P = 'NOMC' K22 D22P                                           ;
      dif_disp = D11P '+' D22P '+' D21P                                ;

   'FINSI'                                                             ;
*  dimension 3
   'SI' ('EGA' ('VALEUR' 'DIME') 3)                                    ;
      V1 = 'EXCO' Qelem 'VX' 'SCAL'                                    ;
      V2 = EXCO Qelem 'VY' 'SCAL'                                      ;
      V3 = EXCO Qelem 'VZ' 'SCAL'                                      ;
      VC = (V1 * V1) '+' (V2 * V2) '+' (V3 *  V3)                      ;
      VC = VC**0.5                                                     ;
      VC = VC + vpetit                                                 ;
      VCM1 =  VC '**' -1                                               ;
      D11P = ((DISPL - DISPT) * V1*V1*VCM1) + (DISPT * VC)             ;
      D22P = ((DISPL - DISPT) * V2*V2*VCM1) + (DISPT * VC)             ;
      D33P = ((DISPL - DISPT) * V3*V3*VCM1) + (DISPT * VC)             ;
      D21P = (DISPL - DISPT) * V1*V2*VCM1                              ;
      D31P = (DISPL - DISPT) * V1*V3*VCM1                              ;
      D32P = (DISPL - DISPT) * V2*V3*VCM1                              ;

      D11P = 'NOMC' K11 D11P                                           ;
      D21P = 'NOMC' K21 D21P                                           ;
      D22P = 'NOMC' K22 D22P                                           ;
      D31P = 'NOMC' K31 D31P                                           ;
      D32P = 'NOMC' K32 D32P                                           ;
      D33P = 'NOMC' K33 D33P                                           ;

      dif_disp = D11P '+' D22P '+' D33P '+' D21P '+' D31P '+' D32P     ;
   'FINSI'                                                             ;
'FINSI'                                                                ;

'FINPROC' dif_disp                                                     ;



