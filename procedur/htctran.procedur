$$$$ HTCTRAN
* HTCTRAN   PROCEDUR  FANDEUR   14/10/10    21:15:15     8178
*-----------------------------------------------------------------------
*23456789012345678901234567890123456789012345678901234567890123456789012
*-----------------------------------------------------------------------
*-----------------------------------------------------------------------
'DEBPROC' HTCTRAN   N_PAS*'ENTIER'  NN_PAS*'ENTIER' N_ITER*'ENTIER'
                    T_PAS*'FLOTTANT' MODL1*'MMODEL'   TAV1*'TABLE';
*-----------------------------------------------------------------------
'MESS' '--------------------------------------------------------------';
'MESS' '------------------------H T C T R A N-------------------------';
'MESS' '                                                              ';
'MESS' '---Bazant coupled moisture and heat transfer in concrete------';
'MESS' '                                                              ';
'MESS' ' Conservation of mass                                         ';
'MESS' '                                                              ';
'MESS' '     d W   dWd                   a                            ';
'MESS' '     --- - --- = - div J , J= - --- grad P                    ';
'MESS' '     d t   d t                   g                            ';
'MESS' '                                                              ';
'MESS' ' Heat balance                                                 ';
'MESS' '                                                              ';
'MESS' '         d T      d W                                         ';
'MESS' '  (De Cp)--- - Ca --- - Cw J grad T = - div q , q= - k grad T ';
'MESS' '         d t      d t                                         ';
'MESS' '--------------------------------------------------------------';
'MESS' '---last mod. - : 22-08-11 - hour :-18.00----------------------';
'MESS' '--------------------------------------------------------------';
*-----------------------------------------------------------------------
* Auteur  : G.M. Giannuzzi, ENEA-UTRINN-PCI    (1998)
* ----------------------------------------------------------------------
* Modifié : G.M. Giannuzzi, ENEA-UTRINN-PCI le 14-07-2011
*-----------------------------------------------------------------------
*
* DESCRIPTION:     Procedure for heat and moisture
*                  transfer analysis in concrete based
*                  on a modified Bazant model.
*                  Axisymmetric and plane cases and 3D
*                  with several boundaries
*                  exchanging vapour and heat.
*
*-----------------------------------------------------------------------
*
'SI'('NON'('EXIS' TAV1 'DETR' ));
  FDET = VRAI; TAV1.'DETR'= FDET;
'SINON';
  FDET=TAV1.'DETR';
'FINSI';
'SI'('NON'('EXIS' TAV1 'GRAF' ));
  GRAF = FAUX; TAV1.'GRAF'= GRAF;
'SINON';
  GRAF=TAV1.'GRAF';
'FINSI';
'SI'('NON'('EXIS' TAV1 'FLG1' ));
  FLG1 = FAUX; TAV1.'FLG1'= FLG1;
'SINON';
  FLG1=TAV1.'FLG1';
'FINSI';
'SI'('NON'('EXIS' TAV1 'FLG' ));
  FLG = 0; TAV1.'FLG'= FLG;
'SINON';
  FLG=TAV1.'FLG';
'FINSI';
*
*  Mesh  and model definition
*
MAGLIA='EXTR' MODL1 'MAIL';
*
'SI' ('EGA' ('VALE' 'MODE') 'AXIS');
  FF1= 'MOT' 'T,R';
  FF2= 'MOT' 'T,Z';
  FF3= 'MOT' '  ';
  LICF='MOTS'  FF1    FF2;
  LICJ='MOTS' 'JX_R' 'JY_Z';
'SINON' ;
 'SI' (('EGA' ('VALE' 'MODE') 'PLANDEFO') 'OU'
       ('EGA' ('VALE' 'MODE') 'PLANCONT'));
   FF1= 'MOT' 'T,X';
   FF2= 'MOT' 'T,Y';
   FF3= 'MOT' '  ' ;
   LICF='MOTS'  FF1    FF2;
   LICJ='MOTS' 'JX_R' 'JY_Z';
 'SINON';
  'SI' ('EGA' ('VALE' 'MODE') 'TRID');
    FF1= 'MOT' 'T,X';
    FF2= 'MOT' 'T,Y';
    FF3= 'MOT' 'T,Z';
    LICF='MOTS'  FF1    FF2    FF3;
    LICJ='MOTS' 'JX_R' 'JY_Z' 'JZ_Z';
  'SINON';
   'MESS' '*** ERREUR -  MODELE INCOMPATIBLE ' ;
   'QUITTER' HTCTRAN;
  'FINSI';
 'FINSI';
'FINSI';
*
 LIPT='MOTS' 'T';
 LICP='MOTS' 'P'    'LX'  ;
 LICT='MOTS' 'T'    'LX'  ;
*
*==================================================
*   ANALISYS OF THE DATA SUPPLIED IN INPUT TABLE
*==================================================
*
*PARAMETRI DI CONTROLLO
*
'SI'('NON'('EXIS' TAV1 'GAMMA'));
  GAMMA=1.;TAV1.'GAMMA'=GAMMA;
'SINON';
  GAMMA=TAV1.'GAMMA';
'FINSI';
*
* VARIAZIONE DI P E T
* PER CUI SCATTA LA DERIVATA TANGENTE
*
'SI'('NON'('EXIS' TAV1 'EPSILON' ));
  EPS1=1.E-8;TAV1.'EPSILON'=EPS1;
'SINON';
  EPS1=TAV1.'EPSILON';
'FINSI';
*
* ERRORI SULLA CONVERGENZA
*
'SI'('NON'('EXIS' TAV1 'ERPM' ));
  ERPM=1.E-4;TAV1.'ERPM'=ERPM;
'SINON';
  ERPM=TAV1.'ERPM';
'FINSI';
'SI'('NON'('EXIS' TAV1 'ERTM' ));
  ERTM=1.E-4;TAV1.'ERTM'=ERTM;
'SINON';
  ERTM=TAV1.'ERTM';
'FINSI';
*
*  Crank-Nicholson's scheme: time integration constant
*
'SI' ( 'NON' ( 'EXIS' TAV1 'LAMBDA' ));
  LAMBDA=0.5;TAV1 . 'LAMBDA'= LAMBDA;
'SINON';
  LAMBDA= TAV1 . 'LAMBDA';
'FINSI';
 UMLAMBDA=1.-LAMBDA;
*
*  Reference temperature [øC ]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'TEMPERATURE_INITIALE' ));
  TREF=25.;TAV1 . 'TEMPERATURE_INITIALE'= TREF;
'SINON';
  TREF = TAV1 . 'TEMPERATURE_INITIALE' ;
'FINSI';
*
*  Reference pressure [N/mmq= Mpa]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'PRESSION_INITIALE' ));
 'MESS' '*** ERREUR - IL MANQUE LA PRESSION INITIALE' ;
 'QUITTER' HTCTRAN;
'SINON';
  PREF = TAV1 . 'PRESSION_INITIALE' ;
'FINSI';
*
*  Water saturation content at 25 °C [Kg/mc]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'W1' ));
  TAV1.'W1'=100.;
'FINSI';
*
* Dehydration water specific heat   [J/Kg]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'CAD' ));
  CAD=0.2328E6;
  TAV1.'CAD'= CAD;
'SINON';
  CAD=TAV1.'CAD';
'FINSI';
*Heat capacity of interstitial and bound water J/kg-C
 CBW=3760.;
*
* Smooth for Ca as a function of h
*
*PL1 = 'PROG' 0. 95. 95.5 96. 96.5 97. 98. 99. 100. 101. 102.
*          103. 103.5  104. 104.5  105. 1000. ;
*PL2 = 'PROG' 0. 0. 0.0062 0.0245 0.0545 0.0955 0.2061 0.3455 0.5
*          0.6545 0.7939 0.9045 0.9455 0.9755 0.9938  1.  1. ;
*
 PL1 = 'PROG' 0. 1. 'PAS' 0.005 1.06 20. ;
 PL2 = 'PROG' 1. 1. 0. 0. ;
'REPE' LOOPA (('DIME' PL1) - 4) ;
  I = &LOOPA + 2 ;
  VPL1 = 'EXTR' PL1 I ;
  VPL2 = ('COS'((VPL1 - 1.)/0.06*90.))**2;
  PL2  = 'INSE' PL2 I VPL2 ;
'FIN' LOOPA;
 EVCA = 'EVOL' 'MANU' PL1 'SCAL' PL2 'SCAL' ;
*
*  Cement content [Kg/mc]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'C' ));
  CC=300.;TAV1.'C'=CC;
'SINON';
  CC=TAV1.'C';
'FINSI';
*
*  Initial permeability [m/s]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'A0' ));
  TAV1.'A0'=1.E-13;
'FINSI';
*
*  Thermal capacity of the drying concrete [J/(m3*C)]
*
*
* Density of dry concrete a 25 °C Kg/m3
*
'SI' ( 'NON' ( 'EXIST' TAV1 'DEN_SEC' ));
  DEN_SEC= 2400.; TAV1 . 'DEN_SEC' = DEN_SEC;
'SINON';
  DEN_SEC=TAV1 . 'DEN_SEC';
'FINSI';
*
* Specific heat of dry concrete a 25 °C J/kg-C
*
'SI' ( 'NON' ( 'EXIST' TAV1 'CCP_SEC' ));
  CCP_SEC= 880.; TAV1 . 'CCP_SEC' = CCP_SEC;
'SINON';
  CCP_SEC=TAV1 . 'DEN_SEC';
'FINSI';
 ROCSEC=CCP_SEC * DEN_SEC;
 TAV1 . 'ROCSEC' = ROCSEC;
*
* Thermal conductivity of the concrete [W/m*K]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'K0_SEC' ));
  K0_SEC =1.92    ; TAV1 . 'K0_SEC' = K0_SEC;
  K1_SEC =-0.00125; TAV1 . 'K1_SEC' = K1_SEC;
'SINON';
  K0_SEC= TAV1 . 'K0_SEC';
  K1_SEC= TAV1 . 'K1_SEC';
'FINSI';
*
*  Volumetric elasticity modulus MPa
*
'SI' ( 'NON' ( 'EXIST' TAV1 'E0' ));
  E0=35000. ;TAV1.'E0'= E0 ;
'SINON';
  E0=TAV1.'E0' ;
'FINSI';
'SI' ( 'NON' ( 'EXIST' TAV1 'NU' ));
  NU=.18;TAV1.'NU'=NU;
'SINON';
  NU=TAV1.'NU' ;
'FINSI';
* --->  BM = E0/3(1-2v) * E/E0(T)
'SI' ( 'NON' ( 'EXIST' TAV1 'EE0T' ));
* Due to  DTU
  EE01 = 'PROG' 1. 1. .5   .15  .05   ;
  TTT1 = 'PROG' 0. 50. 200. 400. 600. ;
'SINON';
  EE0T= TAV1.'EE0T';
  TTT1='EXTR' EE0T 'ABSC';
  EE01='EXTR' EE0T 'ORDO';
'FINSI';
 EE01 =(EE01/(3.*(-2.* NU + 1.)))*E0;
 EBM ='EVOL' 'MANU' TTT1 EE01 ;
 TAV1.'EBM'=EBM;
*
*  Coefficient of linear thermal dilatation[/øC]
*
'SI' ( 'NON' ( 'EXIST' TAV1 'ALFA' ));
  ALFA=9.E-6;TAV1.'ALFA'=ALFA;
'SINON';
  ALFA = TAV1.'ALFA';
'FINSI';
*
* Dehydration curve of concrete
*
'SI' ( 'NON' ( 'EXIST' TAV1 'EWD' ));
*  PT  = 'PROG'    0. 105. 180. 200. 220. 240. 260.
*                280. 300. 320. 340. 360. 400. 600. ;
*  PWD = 0.66 * ('PROG'  0.    0.  .264  .302  .498  1.057 3.358
*              5.283 6.377 7.019 7.283 7.472 7.660 8.075 )*(CC/100.);
  'SI' ( 'NON' ( 'EXIST' TAV1 'F_STE'));
*Stoichiometric factor
    F_STE = 0.24; TAV1 . 'F_STE' = F_STE;
  'SINON';
    F_STE=TAV1 .'F_STE';
  'FINSI';
  'SI' ( 'NON' ( 'EXIST' TAV1 'F_INV'));
*Aging factor
    F_INV = 0.95; TAV1 . 'F_INV' = F_INV;
  'SINON';
    F_INV=TAV1 .'F_INV';
  'FINSI';
  PT1 = 'PROG' 105. 'PAS' 5. 800.;
  PT  = ('PROG' 0. ) 'ET' PT1;
  N_PT1='DIME' PT1; PU='PROG' N_PT1*1.;
  PWD=(PU+ (SIN ((180./2.)*
      (PU-(2.*(EXP (-0.004*(PT1-(105.*PU)))))))))/2.;
  PWD= (F_STE*F_INV*CC)*(('PROG' 0.) 'ET' PWD);
  EWD ='EVOL' 'MANU' PT  PWD ;TAV1.'EWD'=EWD;
'SINON';
  EWD=TAV1.'EWD';
  PWD='EXTR' EWD 'ORDO';
'FINSI';
*DESS EWD 'TITR' 'Water released by dehydrazion Kg/m3';
WD0='MAXI' PWD;
*
* Thermal stiffness matrix
*
'SI' ( 'EXIST' TAV1 'CONDUCTIVITE_THERMIQUE' );
 'MESS' ' CONDUCTIVITE THERMIQUE ' ;
  RIG_V = TAV1 . 'CONDUCTIVITE_THERMIQUE' ;
'FINSI';
*
*  Thermal load
*
 IFT  =FAUX ;
 IFTC =FAUX ;
'SI' ( 'EXIST' TAV1 'CONVECTION_THERMIQUE' );
 'MESS' ' CONVECTION THERMIQUE' ;
  TCHARCT = TAV1 . 'CONVECTION_THERMIQUE' ;
  TCONDC='EXTR' RIG_V 'RIGI' 'NOMU';
  IFT  =VRAI ;
  IFTC =VRAI ;
'FINSI';
 IFTB =FAUX ;
'SI' (  'EXIST' TAV1 'BLOCAGES_THERMIQUE' );
 'MESS' ' BLOCAGES THERMIQUE' ;
  TCHARTI = TAV1 . 'BLOCAGES_THERMIQUE' ;
  TCONDB='EXTR' RIG_V 'RIGI' 'MULT';
  IFT  =VRAI ;
  IFTB =VRAI ;
'FINSI';
'SI' (  'EXIST' TAV1 'FLUX_THERMIQUE' );
 'MESS' ' FLUX THERMIQUE ' ;
  TCHARFT = TAV1 . 'FLUX_THERMIQUE' ;
  IFT = VRAI ;
'FINSI';
*
* Hydraulic stiffness matrix
*
'SI' ( 'EXIST' TAV1 'CONDUCTIVITE_HYDRAULIQUE' );
  MESS ' CONDUCTIVITE HYDRAULIQUE';
  PCOND2 = TAV1 . 'CONDUCTIVITE_HYDRAULIQUE' ;
'FINSI';
*
*  Hydraulic load
*
 IFP = FAUX;
 IFPC = FAUX;
'SI' ( 'EXIST' TAV1 'CONVECTION_HYDRAULIQUE' );
 'MESS' ' CONVECTION HYDRAULIQUE' ;
  PCHARCH = TAV1 . 'CONVECTION_HYDRAULIQUE' ;
  PCONDC='EXTR' PCOND2 'RIGI' 'NOMU';
  IFP = VRAI ;
  IFPC = VRAI ;
'FINSI';
 IFPB = FAUX ;
'SI' ( 'EXIST' TAV1 'BLOCAGES_HYDRAULIQUE' );
 'MESS' ' BLOCAGES_HYDRAULIQUE' ;
  PCHARPI = TAV1 . 'BLOCAGES_HYDRAULIQUE' ;
  PCONDB='EXTR' PCOND2 'RIGI' 'MULT';
  IFP  = VRAI ;
  IFPB = VRAI ;
'FINSI';
'SI' ( 'EXIST' TAV1 'FLUX_HYDRAULIQUE' );
 'MESS' ' FLUX_HYDRAULIQUE ' ;
  PCHARFH = TAV1 . 'FLUX_HYDRAULIQUE' ;
  IFP = VRAI ;
'FINSI';
'SI' IFP;
 'SI' ('EXIST' TAV1 'FRONTIERES_PRESSION');
   NFRONT= 'DIME' TAV1.'FRONTIERES_PRESSION';
 'SINON';
  'MESS' '*** ERREUR - DANS LES CONDITIONS AUX LIMITES'
         ' EN PRESSION ' ;
 'FINSI';
'FINSI';
*--------------------------------------------------
*               RESTART PROCEDURE
*--------------------------------------------------
*
'SI' ( 'NON' ('EXIST' TAV1 'RESULTATS' ));
  RESTART1= FAUX;
  TEMPO = 0.;
  T0    =  'MANU' 'CHPO' MAGLIA  1 'SCAL' TREF ;
  P0    =  'MANU' 'CHPO' MAGLIA  1 'SCAL' PREF ;
  VT0   =  'MANU' 'CHPO' MAGLIA  1 'SCAL' 0. ;
  VP0   =  'MANU' 'CHPO' MAGLIA  1 'SCAL' 0. ;
  WI   HI   = HTC_WWW   T0       P0       TAV1 ;
  WT1 HT1   = HTC_WWW  (T0 + 1.) P0       TAV1 ;
  DWT0      = WT1 - WI ;
  WP1 HP1   = HTC_WWW   T0      (P0 + 0.00001) TAV1 ;
  DWP0      = (WP1 - WI)/0.00001 ;
  DWD0      = 'MANU' 'CHPO' MAGLIA 1 'SCAL'  0.;
  WD        = 'MANU' 'CHPO' MAGLIA  1 'SCAL' 0.;
  TAV1.'WDM'=  WD;
*
  CHI='CHAN' 'CHAM' MODL1 WI;
  MI ='INTG' MODL1 CHI;
  MT = MI ;
  PSAT = HTC_WTR TAV1 'SATU' T0 ;
*
  TAV1.'TMAX'=T0;
  T0='NOMC' 'T'  T0;
  P0='NOMC' 'T'  P0;
*
 'MESS' '-----------------------------------';
 'MESS' ' MASSE D EAU  INITIALE ' MI;
 'MESS' ' H.R.         INITIALE ' ('MAXI' HI);
 'MESS' ' PRE. SAT.    INITIALE ' ('MAXI' PSAT);
*
  PERMAS=0.;
  IPAS=0;
*
  TAV1.'MI'             = MI;
  TAV1.'RESULTATS'      = 'TABLE';
  TAV1.'RESULTATS'.IPAS = 'TABLE';
  TAV1.'RESULTATS'.IPAS.'INSTANT'           = 0. ;
  TAV1.'RESULTATS'.IPAS.'TEMPERATURE'       = T0 ;
  TAV1.'RESULTATS'.IPAS.'PRESSION'          ='NOMC' 'P'  P0;
  TAV1.'RESULTATS'.IPAS.'HUMIDITE_RELATIVE' ='NOMC' 'HR' HI;
  TAV1.'RESULTATS'.IPAS.'EAU_LIBRE'         ='NOMC' 'WF' WI;
  TAV1.'RESULTATS'.IPAS.'EAU_LIEE'          ='NOMC' 'WD' WD;
  TAV1.'RESULTATS'.IPAS.'MASSE_PERDUE'      = PERMAS;
* TAV1.'RESULTATS'.IPAS.'MASSE_FLUX'        = JFL  ;
  TAV1.'RESULTATS'.IPAS.'MASSE_BILAN'       = MT   ;
  TAV1.'RESULTATS'.IPAS.'MASSE_RELACHE'     = 0.   ;
  TAV1.'RESULTATS'.IPAS.'PRES_SAT'          = 'NOMC' 'PSAT' PSAT;
  TAV1.'VT0'  = VT0 ;
  TAV1.'VP0'  = VP0 ;
  TAV1.'DWD0' = DWD0 ;
  TAV1.'DWT0' = DWT0 ;
  TAV1.'DWP0' = DWP0 ;
 'SI' FDET;
  'DETR' HI  ; 'DETR' WI  ; 'DETR' CHI;
  'DETR' WT1 ; 'DETR' HT1 ;
  'DETR' WP1 ; 'DETR' HP1 ;
 'FINSI';
'SINON';
  RESTART1 = VRAI ;
  MI=TAV1.'MI';
  NPSAUVE= 'DIME' (TAV1 . 'RESULTATS');
  IPAS=NPSAUVE-1;
  TEMPO  = TAV1.'RESULTATS' . IPAS . INSTANT;
  T0     = TAV1.'RESULTATS' . IPAS . 'TEMPERATURE';
  P0     ='NOMC' LICP LICT  TAV1.'RESULTATS'.IPAS.'PRESSION';
  PERMAS = TAV1 . 'RESULTATS' . IPAS . 'MASSE_PERDUE';
  MT     = TAV1 . 'RESULTATS' . IPAS . 'MASSE_BILAN' ;
  MD     = TAV1 . 'RESULTATS' . IPAS . 'MASSE_RELACHE';
'FINSI';
*
 USDT = 1./T_PAS ;
*
 II = 1 ;
 N1_PAS= 0;
*
*===================================
*    BOUCLE SUR LES PAS DE TEMPS
*===================================
*
'REPE' TOTO N_PAS;
*
 ISTANT0 = ((II - 1 )*T_PAS )+ TEMPO ;
 ISTANT1 = ( II      *T_PAS )+ TEMPO ;
 ITER = 0 ;
*
'MESS' ' ';
'MESS' '---------------------------------------------';
'MESS' '** PAS   ' II ' INSTANT ' ISTANT1 ' **';
'MESS' '---------------------------------------------';
*
*T0S   temp. comp. scal e senza  lagr. passo pre.
*P0S   pres. comp. scal e senza  lagr. passo pre.
*T1S   temp. comp. scal e senza  lagr. passo att. iter. pre.
*P1S   pres. comp. scal e senza  lagr. passo att. iter. pre.
*VT0   velocita temp. iter. o passo pre.
*VP0   velocita pres. iter. o passo pre.
*
VT0 =TAV1.'VT0';
VP0 =TAV1.'VP0';
DWD0=TAV1.'DWD0';
DWT0=TAV1.'DWT0';
DWP0=TAV1.'DWP0';
*
 T0S = 'EXCO' 'T' T0 'SCAL';
 P0S = 'EXCO' 'T' P0 'SCAL';
 T1S = T0S+(T_PAS*GAMMA*VT0);
 P1S = P0S+(T_PAS*GAMMA*VP0);
*
ISTANTM = (UMLAMBDA*ISTANT0) + (LAMBDA*ISTANT1);
*
*  Thermal Loads
*
'SI' IFT ;
  TCONV = 0.;
 'SI' ('EXIST' TAV1 'CONVECTION_THERMIQUE' );
   TCNVCT = 'TIRE' TCHARCT  ISTANTM  ;
   TCONV  = TCONV + TCNVCT ;
 'FINSI';
 'SI' ('EXIST' TAV1 'BLOCAGES_THERMIQUE' );
*   TCNVTI = 'TIRE' TCHARTI  ISTANT1  ;
   TCNVTI = 'TIRE' TCHARTI  ISTANTM  ;
   TCONV  = TCONV + TCNVTI ;
 'FINSI';
 'SI' ('EXIST' TAV1 'FLUX_THERMIQUE' );
   TCNVFT = 'TIRE' TCHARFT  ISTANTM  ;
   TCONV  = TCONV + TCNVFT ;
 'FINSI';
'FINSI';
*
*  Hydraulic Loads
*
'SI' IFP ;
  PCONV = 0.;
 'SI' ('EXIST' TAV1 'CONVECTION_HYDRAULIQUE' );
   PCNVCH = 'TIRE' PCHARCH  ISTANTM  ;
   PCONV  = PCONV + PCNVCH;
 'FINSI';
 'SI' ('EXIST' TAV1 'BLOCAGES_HYDRAULIQUE' );
*   PCNVPI = 'TIRE' PCHARPI  ISTANT1  ;
   PCNVPI = 'TIRE' PCHARPI  ISTANTM  ;
   PCONV =PCONV +  PCNVPI;
 'FINSI';
 'SI' ('EXIST' TAV1 'FLUX_HYDRAULIQUE' );
   PCNVFH = 'TIRE' PCHARFH  ISTANTM  ;
   PCONV =PCONV +  PCNVFH;
 'FINSI';
'FINSI';
IFITER = FAUX ;
*===================================
*          ITERATIONS
*===================================
'REPE' OTO  ( N_ITER + 1 );
*
 ITER= ITER + 1;
'MESS' '** ITERATION  ' ITER ' **';
*
*------------------------------------
* Specific  Water Content of Concrete
* Calculation of the derivatives
* dW/dT and dW/dP
* and Relative Humidity
*------------------------------------
*
 DT1A = T1S-T0S ;
 DP1A = P1S-P0S ;
 DT1Z = 'MASQUE' ('ABS' DT1A) 'EGINFE' EPS1;
 DP1Z = 'MASQUE' ('ABS' DP1A) 'EGINFE' EPS1;
 DT1Z1 = 'MASQUE' (1. - DT1Z) 'SUPERIEUR' .9;
 DP1Z1 = 'MASQUE' (1. - DP1Z) 'SUPERIEUR' .9;
 DT1 = DT1A + DT1Z;
 DP1 = DP1A + DP1Z;
*
'SI' FDET ;
 'DETR' DT1A  ; 'DETR' DP1A  ;
'FINSI';
*
  'SI' ('EGA' ITER 1);
    WT0P0 H00 = HTC_WWW  T0S P0S TAV1 ;
  'FINSI';
    WT1P0 H10 = HTC_WWW  T1S P0S TAV1 ;
    WT1P1 H11 = HTC_WWW  T1S P1S TAV1 ;
    WT0P1 H01 = HTC_WWW  T0S P1S TAV1 ;
*---> dW/dT
    DWTM=(WT1P0-WT0P0+WT1P1-WT0P1)/2.;
    DWT =(DWTM / DT1 * DT1Z1) + (DT1Z*DWT0);
*---> dW/dP
    DWPM=(WT0P1-WT0P0+WT1P1-WT1P0)/2.;
    DWP =(DWPM / DP1 * DP1Z1) + (DP1Z*DWP0);
*---> W HR
    PPP=(UMLAMBDA*P0S)+(LAMBDA*P1S);
    TTT=(UMLAMBDA*T0S)+(LAMBDA*T1S);
    WWW HHH = HTC_WWW  TTT PPP TAV1 ;
*
'SI' FDET ;
 'DETR' H11  ;
 'DETR' H10  ; 'DETR' H01  ;
 'DETR' WT1P0; 'DETR' WT0P1 ;
 'DETR' WT1P1;
'FINSI';
*
'SI' (FLG '>EG' 4);
 'MESS' '----------------------------------------------'
        '----------------------------------------------';
 'MESS' 'DWDT-MAX' ('MAXI' DWT )  'DWDT-MIN' ('MINI' DWT )
        'DWDP-MAX' ('MAXI' DWP )  'DWDP-MIN' ('MINI' DWP );
 'MESS' '----------------------------------------------'
        '----------------------------------------------';
'FINSI';
*
'SI' GRAF;
* titr 'hhh in 'ISTANT1 ITER;
* trac maglia HHH;
* titr 'www in 'ISTANT1 ITER;
* trac maglia WWW;
* titr 'T in 'ISTANT1 ITER;
* trac maglia TTT;
* titr 'P in 'ISTANT1 ITER;
* trac maglia PPP;
 titr ' dwp in'ISTANT1 ITER;
 trac maglia DWP;
 titr ' dwt in'ISTANT1 ITER;
 trac maglia DWT;
'FINSI';
*
*--------------------------------------
* Chemically Bound Water and Derivative
*--------------------------------------
*
     WD DF1= HTC_CHBW T1S HHH TAV1 ;
     DWD   =(DF1 / DT1 * DT1Z1) + (DT1Z * DWD0);
*
*-------------
* Permeability
*-------------
*
     AGDG1=HTC_PER TTT HHH TAV1 ;
     AGDG = 1.E6 * AGDG1 ;
'SI' GRAF;
  titr 'Permeabilita' ISTANT1 ITER;
  trac AGDG maglia;
'FINSI';
*
'SI' (FLG '>EG' 5);
 'MESS' 'RANGES OF RELATIVE HUMIDITY, WATER CONTENT, PERMEABILITY';
 'MESS' 'H  -MAXI ' ('MAXI' HHH)  'H  -MINI ' ('MINI' HHH);
 'MESS' 'W  -MAXI ' ('MAXI' WWW)  'W  -MINI ' ('MINI' WWW);
 'MESS' 'A  -MAXI ' ('MAXI' AGDG) 'A  -MINI ' ('MINI' AGDG);
'FINSI';
*
*-----------------------
* Specific heat of water
*-----------------------
*
 CW= HTC_WTR TAV1 'CW' TTT;
*
*---------------------------
* Heat of water vaporization
*---------------------------
*
 CA= HTC_WTR TAV1 'HFG' TTT;
*
*---------------------
* Heat convection term
*---------------------
 TTTT ='NOMC' 'T' TTT;
 PPPP ='NOMC' 'T' PPP;
 GRAP='GRAD' MODL1 PPPP;
 GRAT='GRAD' MODL1 TTTT;
 GRATX='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF1  GRAT 'SCAL') RIGIDITE;
 GRATY='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF2  GRAT 'SCAL') RIGIDITE;
 GRAPX='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF1  GRAP 'SCAL') RIGIDITE;
 GRAPY='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF2  GRAP 'SCAL') RIGIDITE;
 ZZZZ1    = 'CHAN' 'NOEUD' MODL1 GRAPX;
 GRAPCX   = 'CHAN' 'CHPO'  MODL1 ZZZZ1;
 ZZZZ2    = 'CHAN' 'NOEUD' MODL1 GRAPY;
 GRAPCY   = 'CHAN' 'CHPO'  MODL1 ZZZZ2;
'SI' ('NEG' FF3 '  ');
  GRATZ='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF3  GRAT 'SCAL') RIGIDITE;
  GRAPZ='MANU' 'CHML' MODL1 'SCAL' ('EXCO' FF3  GRAP 'SCAL') RIGIDITE;
  ZZZZ4    = 'CHAN' 'NOEUD' MODL1 GRAPZ;
  GRAPCZ   = 'CHAN' 'CHPO'  MODL1 ZZZZ4;
'FINSI';
 LM1= 'MOTS' 'SCAL' 'SCAL';
 LM2= 'MOTS' 'SCAL' 'SCAL';
 LM3= 'MOTS' 'SCAL' 'SCAL';
 GRAPTX   = MODL1 GRAPX * GRATX LM1 LM2 LM3;
 GRAPTY   = MODL1 GRAPY * GRATY LM1 LM2 LM3;
 GRAPTXY  = GRAPTX + GRAPTY;
'SI' ('NEG' FF3 '  ');
  GRAPTZ   = MODL1 GRAPZ * GRATZ LM1 LM2 LM3;
  GRAPTXY  = GRAPTXY + GRAPTZ;
'FINSI';
 ZZZZ3    = 'CHAN' 'NOEUD' MODL1 GRAPTXY;
 GRAPTCXY = 'CHAN' 'CHPO'  MODL1 ZZZZ3  ;
*--->  - cw * a/g * grad p * grad t
 AGPGT = 'SOURCE' MODL1 (-1. * CW * AGDG * GRAPTCXY);
'SI' FDET;
 'DETR' PPPP   ; 'DETR' TTTT    ;
 'DETR' GRAP   ; 'DETR' GRAT    ;
 'DETR' GRATX  ; 'DETR' GRATY   ;
 'DETR' GRAPTX ; 'DETR' GRAPTY  ;
 'DETR' GRAPTXY; 'DETR' GRAPTCXY;
 'DETR' ZZZZ1  ; 'DETR' ZZZZ2   ; 'DETR' ZZZZ3;
 'SI' ('NEG' FF3 '  ');
  'DETR' GRATZ   ;'DETR' GRAPTZ  ;'DETR' ZZZZ4;
 'FINSI';
'FINSI';
*--------------------------------------------
* Water Evaporation Heat on various frontiers
* Ca*J*n
*--------------------------------------------
'SI' IFP;
  IFR=0;
  CAJN_T = 0.;
 'REPE' BOUC1 NFRONT;
   IFR=IFR + 1;
   COSXR =TAV1.'FRONTIERES_PRESSION'. IFR .'CODIRXR' ;
   COSYZ =TAV1.'FRONTIERES_PRESSION'. IFR .'CODIRYZ' ;
   MAI_FP=TAV1.'FRONTIERES_PRESSION'. IFR .'MAILLAGE';
  'SI' ('NEG' FF3 '  ');
    COSZZ=TAV1.'FRONTIERES_PRESSION'. IFR .'CODIRZZ' ;
    CAJN ='REDU'(1.*AGDG*CA*((GRAPCX*COSXR)+(GRAPCY*COSYZ)
                                           +(GRAPCZ*COSZZ))) MAI_FP;
  'SINON';
    CAJN ='REDU'(1.*AGDG*CA*((GRAPCX*COSXR)+(GRAPCY*COSYZ))) MAI_FP;
  'FINSI';
   FCAJN='FLUX' MODL1 CAJN  ;
   CAJN_T  = CAJN_T + FCAJN ;
 'FIN' BOUC1;
'FINSI';
*
*---> Ca*dW/dP*dP/dt
*
*list hhh;
 SMCA = 'NOMC' 'SCAL' ('VARI' EVCA HHH );
*list smca;
 CADWPVP='SOURCE' MODL1 (SMCA * CA * DWPM * USDT);
*
*---> (dWd/dT-dW/dT)*dT/dt
*
 DWDMDWT='SOURCE' MODL1 ((DF1 - DWTM) * USDT);
*
*-----------------------------------
* Apparent specific heat of concrete
*-----------------------------------
*
*---> (roC)sec
* In caso di dipendenza dalla temperatura ins. qui
*---> W*Cw
 WCW    = CW * WWW;
*list  WCW ;
*--->  Cad*dWd/dT
 CADDWD= CAD*DWD;
*list CADDWD;
*---> -Ca*dW/dT
 CADWT  =-1. * SMCA * CA * DWT;
*---> (Wd0-Wd)*Cbw
WD0DCBW=(WD0-(TAV1.'WDM')) * CBW;
*list   CADWT ;
*---> Apparent specific heat of concrete
 ROCAP1=ROCSEC+CADDWD+WCW+CADWT+WD0DCBW;
 ROCAP = 'CHAN' 'CHAM' ROCAP1  MODL1 ;
*list ROCAP
*
 KBET_SEC=K0_SEC   + (K1_SEC * (TAV1.'TMAX'));
 KBET    =NOMC 'K' (KBET_SEC * (1. + ((4./DEN_SEC)* WWW)));
 KBET=CHAN 'CHAM' KBET MODL1 'RIGIDITE';
 MABET= 'MATE' MODL1  'K' KBET 'RHO' 1. 'C' ROCAP   ;
*
*=================================================
*              THERMAL SOLUTION
*=================================================
*
'SI'(NON IFITER );
*
*  Thermal capacity matrix
*
 TCAPM='CAPACITE' MODL1 MABET;
 A2= USDT * TCAPM ;
*
*  Thermal stiffness matrix
*
 RIG_M='CONDUCTIVITE' MODL1 MABET;
*
 'SI' IFTC;
   KT = RIG_M 'ET' TCONDC;
 'SINON';
   KT = RIG_M;
 'FINSI';
  A1 =         LAMBDA * KT;
  A3 =(-1. * UMLAMBDA)* KT;
*
 'SI' IFTB;
   MAT_RIGI = A1 'ET' A2 'ET' TCONDB ;
 'SINON';
   MAT_RIGI = A1 'ET' A2 ;
 'FINSI';
*
  B2 = A2 'ET' A3;
*
*  Loads
*
  B1 = AGPGT + CADWPVP;
*
 'SI' IFP ;
   B1 = CAJN_T  + B1;
 'FINSI';
 'SI' IFT ;
   B1 = B1 + TCONV ;
 'FINSI';
  B3  =  B2 * ('NOMC' 'T' T0S);
  MAT_CHPO = B1 + B3;
*
* Resolution
*-----
  T1 = 'RESOU' MAT_RIGI MAT_CHPO;
*-----
  TPS=T1S;
  T1S='EXCO' 'T' T1 'SCAL';
  VT0=(T1S-T0S)*USDT;
'FINSI' ;
*
*Valori fine iterazione
*
'SI' (FLG '>EG' 2);
 'MESS' 'T1-MAX' ('MAXI' T1S) 'T1-MIN' ('MINI' T1S)
        'VT-MAX' ('MAXI' VT0) 'VT-MIN' ('MINI' VT0) ;
'FINSI';
*
*===============================================
*          MOISTURE TRANSFER SOLUTION
*===============================================
*
* Stiffness matrix
*
 CHA1 = 'CHAN' 'CHAM'  AGDG  MODL1 ;
 CHA2 = 'CHAN' 'CHAM'  DWP   MODL1 ;
*
 PMATR1 = 'MATE' MODL1 'RHO' 1. 'K' CHA1 'C' CHA2 ;
 PCOND1 = 'CONDUCTIVITE' MODL1 PMATR1 ;
*
* Capacity matrix
*
 PCAPA1 = 'CAPACITE' MODL1 PMATR1 ;
*
 'SI' IFPC;
   KP = PCOND1 'ET' PCONDC;
  'SINON';
   KP = PCOND1;
 'FINSI';
 PA1 = KP * LAMBDA ;
 PA2 = PCAPA1 * USDT ;
 PA3 =  -1. * UMLAMBDA * KP ;
 PB2 = PA2 'ET' PA3;
'SI' IFPB;
  PMAT_RIG = PA1 'ET' PA2 'ET' PCONDB;
'SINON';
  PMAT_RIG = PA1 'ET' PA2  ;
'FINSI';
*
*  Loads
*
 PB1 = DWDMDWT ;
'SI' IFP;
  PB1 = PCONV + PB1;
'FINSI';
 PB3 = PB2 * ('NOMC' 'T' P0S);
*
PMAT_CHP = PB1 + PB3 ;
*
* Resolution
*-----
  P1 = 'RESOU' PMAT_RIG PMAT_CHP ;
*-----
  PPS=P1S;
  P1S='EXCO' 'T' P1 'SCAL';
  VP0=(P1S-P0S)*USDT;
*
WWW HHH = HTC_WWW  T1S P1S TAV1 ;
WD  DF1 = HTC_CHBW T1S HHH TAV1 ;
CHW     ='CHAN' 'CHAM' MODL1 WWW;
MW      ='INTG' MODL1 CHW;
CHD     ='CHAN' 'CHAM' MODL1 WD;
MD      ='INTG' MODL1 CHD;
'SI' FDET;
 'DETR' DF1;'DETR' CHW;'DETR' CHD;
'FINSI';
*
* --------------------------------------------------
'SI' (FLG '>EG' 2);
 'MESS' 'P1-MAX' ('MAXI' P1S) 'P1-MIN' ('MINI' P1S)
        'VP-MAX' ('MAXI' VP0) 'VP-MIN' ('MINI' VP0) ;
'FINSI';
* --------------------------------------------------
'SI' (FLG '>EG' 3);
'MESS' 'H -MAX' ('MAXI' HHH) 'H -MIN' ('MINI' HHH)
       'W -MAX' ('MAXI' WWW) 'W -MIN' ('MINI' WWW) ;
'FINSI';
* --------------------------------------------------
 ERPM1 ='MAXI'((P1S - PPS)/PPS) 'ABS' ;
 ERTM1 ='MAXI'((T1S - TPS)/TPS) 'ABS' ;
* --------------------------------------------------
      GP_1  ='GRAD'  MODL1 ('NOMC' T P1S);
      GP_2  ='CHAN' 'NOEUD' MODL1  GP_1;
      GP_3  ='CHAN' 'CHPO'  MODL1  GP_2;
* --->  J=-a grad p
      JFL='NOMC' LICF LICJ (-1.*AGDG * GP_3);
      'SI' FDET ;
       'DETR' GP_1; 'DETR' GP_2;'DETR' GP_3;
      'FINSI';
*
'SI' GRAF;
  titr ' t1' ISTANT1 ITER;
  trac maglia t1;
  titr ' p1' ISTANT1 ITER;
  trac maglia p1;
  titr 'hhh' ISTANT1 ITER;
  trac maglia hhh;
  titr ' www ' ISTANT1 ITER;
  trac maglia www;
*  titr 'jfl_x' ISTANT1 ITER;
*  trac maglia (exco JX_R jfl);
*  titr ' wd ' ISTANT1 ITER;
*  trac maglia wd;
'FINSI';
* ---------------------------------------------------------
*PERDITA DI MASSA
*----------------------------------------------------------
 PMAS_T = 0.;
'SI' IFP;
  PPPT = (UMLAMBDA*P0)+(LAMBDA*P1);
 'SI' ('EXIST' TAV1 'CONVECTION_HYDRAULIQUE' );
*  PCVCH = 'TIRE' PCHARCH  ISTANTM ;
  PMAS_T=PMAS_T + ('MAXI'(('RESU'((PCONDC * PPPT )- PCNVCH ))*T_PAS)
                   'AVEC'('MOTS' 'Q'));
 'FINSI';
 'SI' ('EXIST' TAV1 'BLOCAGES_HYDRAULIQUE' );
  PMAS_T=PMAS_T + ('MAXI'(('RESU'(  PCONDB * PPPT)) * T_PAS)
                   'AVEC'('MOTS' 'Q'));
*  PCVPI = 'TIRE' PCHARPI ISTANT1 ;
*  PMAS_T=PMAS_T + ('MAXI'(('RESU'((PCONDB * P1 ) - PCVPI)) * T_PAS)
*                   'AVEC'('MOTS' 'Q'));
 'FINSI';
 'SI' ('EXIST' TAV1 'FLUX_HYDRAULIQUE' );
*  PCVFH = 'TIRE' PCHARFH  ISTANTM ;
  PMAS_T=PMAS_T - ('MAXI'(('RESU'  PCNVFH) * T_PAS)
                   'AVEC'('MOTS' 'Q'));
 'FINSI';
'FINSI';
 PMAST1=PMAS_T+PERMAS;
 M0 = MT ;
 MT = MW - MD + PMAST1 ;
 ERM_ABS = ((MT - MI)/MI*100.);
 ERM_REL = 'ABS'((MT - M0)/M0)  ;
'SI' (FLG '>EG' 1);
 'MESS''-------------------------------------------------------------'
       '--------------------------------------------';
 'MESS' 'M.IN' MI '=< M.FW' MW '- M.RBW' MD '+ M.LW' PMAST1 '>=' MT ;
'FINSI';
'MESS''-------------------------------------------------------------'
      '--------------------------------------------';
'MESS' 'ER_M_P =' ERPM1 'ER_M_T =' ERTM1 'ERMAS_ABS_% =' ERM_ABS
       'ERMAS_REL =' ERM_REL  ;
'MESS''-------------------------------------------------------------'
      '--------------------------------------------';
* ---------------------------------------------------------
 CCC =  P1S 'MASQ' 'EGINFE' EPS1;
'SI' (('MAXI'CCC ) > .9);
 'MESS' '==> LOW PRESSURE <==';
* P0 =  (1. * CCC) + (P1EXT * (1. - CCC))  ;'DETR' CCC;
'FINSI';
*
'SI' (ITER < 2) ;
 'ITER' OTO;
'FINSI'  ;
'SI' IFITER ;
 'QUITTER' OTO ;
'FINSI' ;
*
'SI' ((ERPM1 <EG ERPM) 'ET' (ERTM1 <EG ERTM));
 IFITER = VRAI ;
 'MESS' '==> CONVERGENCE <==';
*'QUITTER' OTO;
'FINSI';
*
'SI' (ITER 'EGA' N_ITER );
  IFITER = VRAI ;
'FINSI';
*
'FIN' OTO ;
*======================================
*         FIN DES ITERATIONS
*======================================
*     un peu de ménage
*
         'MENAGE';
*
*======================================
PERMAS=PERMAS + PMAS_T;
DWT0 = DWT ;
DWD0 = DWD ;
DWP0 = DWP ;
DTMAX=('EXCO' 'T' T1 'SCAL')- TAV1.'TMAX';
MDTMAX=MASQUE DTMAX 'SUPERIEUR' 0. ;
TAV1.'TMAX' = TAV1.'TMAX' + (  DTMAX * MDTMAX);
TAV1.'WDM'  = WD;
TAV1.'VT0'  = VT0;
TAV1.'VP0'  = VP0;
TAV1.'DWD0' = DWD0 ;
TAV1.'DWT0' = DWT0 ;
TAV1.'DWP0' = DWP0 ;
*
*========================================
*       SAUVEGARDE DES RESULTATS
*========================================

N1_PAS=N1_PAS + 1;
'SI' (( EGA NN_PAS N1_PAS) OU ( EGA II N_PAS) OU (EGA II 1));
  IPAS=IPAS+1;
 'MESS' ' ';
 'MESS' 'SAUVEGARDE  - PAS ' II ' INSTANT ' ISTANT1;
  TAV1.'RESULTATS'.IPAS = 'TABLE';
  TAV1.'RESULTATS'.IPAS.'INSTANT'           = ISTANT1;
  TAV1.'RESULTATS'.IPAS.'TEMPERATURE'       = T1 ;
  TAV1.'RESULTATS'.IPAS.'PRESSION'          ='NOMC' LICT LICP P1;
  TAV1.'RESULTATS'.IPAS.'HUMIDITE_RELATIVE' ='NOMC' 'HR' HHH;
  TAV1.'RESULTATS'.IPAS.'EAU_LIBRE'         ='NOMC' 'WF' WWW;
  TAV1.'RESULTATS'.IPAS.'EAU_LIEE'          ='NOMC' 'WD' WD ;
  TAV1.'RESULTATS'.IPAS.'MASSE_PERDUE'      = PERMAS;
  TAV1.'RESULTATS'.IPAS.'MASSE_FLUX'        = JFL  ;
  TAV1.'RESULTATS'.IPAS.'MASSE_BILAN'       = MT   ;
  TAV1.'RESULTATS'.IPAS.'MASSE_RELACHE'     = MD   ;
  PSAT = 'NOMC' 'PSAT' (HTC_WTR TAV1 'SATU' T1);
  TAV1.'RESULTATS'.IPAS.'PRES_SAT'          = PSAT;
*
 'SI' (EGA NN_PAS N1_PAS);
   N1_PAS=0;
 'FINSI';
'FINSI';
*
*
II = II + 1 ;
*
P0  =  P1 ;
T0  =  T1 ;
*
*=======================================
*
*             FIN DES PAS
*
*=======================================
*
'FIN' TOTO;
*
'FINPROC' TAV1;




