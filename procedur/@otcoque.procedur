$$$$ @OTCOQUE
* @OTCOQUE  PROCEDUR  AM1       92/10/29    21:15:25     734
*----------------------------------------------------------------------
*
*   OTTIMIZZAZIONE PIASTRE  A PESO MINIMO
*   E TENSIONE LIMITE IMPOSTA.
*
*   REVISIONE : 30-7-92  AUTORI : DONARELLI, GIANNUZZI.
*   E N E A - VEL MEP TERMO
*----------------------------------------------------------------------
*
DEBPROC @OTCOQUE CRIT*FLOTTANT               NMIT*ENTIER
                 SPEI*FLOTTANT VLIM*FLOTTANT STEC*FLOTTANT
                 TMOD*TABLE    TMAT*TABLE
                 FORZE*CHPOINT VINCO*RIGIDITE
                 PV*POINT ;
*---------------------------------------------------------------
* CRIT  ERRORE SU SIGMA LIMITE ---------------------------------
* NMIT  NUMERO MASSIMO ITERAZIONI ------------------------------
* SPEI  SPESSORE INIZIALE ZONE ---------------------------------
* VLIM  SIGMA EQUIVALENTE VMISES LIMITE ------------------------
* STEC  SPESSORE MINIMO ----------------------------------------
* TMOD  TAVOLA CONTENENTE GLI OGG. MODELE DELLE ZONE -----------
* TMAT  TAVOLA CONTENENTE GLI OGG. MATERIAU DELLE ZONE ---------
* FORZE CAMPO DI FORZE -----------------------------------------
* VINCO RIGIDEZZA VINCOLI + ZONE NON OTTIMIZZATE ---------------
* PV    PUNTO DI VISTA PER TRACCIAMENTI  -----------------------
* --------------------------------------------------------------
MESS ' ---------------------------------------------------' ;
MESS ' ---------------------------------------------------' ;
MESS '                                                    ' ;
MESS '      OTTIMIZZAZIONE PIASTRE  A PESO MINIMO         ' ;
MESS '                                                    ' ;
MESS ' ---------------------------------------------------' ;
MESS ' ---------------------------------------------------' ;
MESS '                                                    ' ;
MESS ' ---------------------------------------------------' ;
MESS ' OGNI QUANTE ITERAZIONI VUOI UNA STAMPA COMPLETA ?  ' ;
MESS ' ---------------------------------------------------' ;
OBTENIR FSTAMP*ENTIER                                       ;
MESS ' ----------------------------------------------------------'  ;
MESS ' VALORE USUALE DEL COEFFICIENTE DI RILASSAMENTO  -1.-      '  ;
MESS ' CONFERMARE O CAMBIARE VALORE                              '  ;
MESS ' ----------------------------------------------------------'  ;
A=1. ;
OBTENIR A1*FLOTTANT ;
SI('NEG' A1 'NON' ) ;
A=A1;
FINSI;
*
TCAR = TABLE  ;
TRHO = TABLE  ;
TRIG = TABLE  ;
TMAI = TABLE  ;
NZ= DIME TMOD ;
SCNEW = LECT NZ*0   ;
TCOLD = LECT NZ*0   ;
*
* CARICAMENTO SPESSORI INIZIALI
SPZO  = PROG NZ * SPEI;
*
* CARATTERISTICHE GEOMETRICHE
N=0           ;
PESO   =0.    ;
REPETER CA1 NZ;
N=N+1         ;
TCAR.N = CARA TMOD.N EPAI SPEI ;
TRHO.N = EXCO TMAT.N RHO       ;
TMAI.N = EXTR TMOD.N 'MAIL'    ;
PESZ   = INTG TMOD.N TRHO.N TCAR.N    ;
PESO   = PESO   +  PESZ        ;
SI( N EGA 1)                   ;
TTMOD  = TMOD . 1              ;
SINON                          ;
TTMOD  = TTMOD ET TMOD.N       ;
FINSI                ;
FIN CA1              ;
PPESO = PROG PESO    ;
PITER = PROG 0.      ;
*
* CICLO PRINCIPALE
*
NIT=0             ;
REPETER BOUC1 NMIT;
PESO=0.           ;
MERS= 0.          ;
NOCONV=1          ;
NIT = NIT+1       ;
SAUTER PAGE        ;
*SAUTER 2  LIGNE   ;
MESS '                               '  ;
MESS ' =============================='  ;
MESS '    ITERAZIONE NUMERO ' NIT       ;
MESS ' =============================='  ;
MESS '                               '  ;
*SAUTER 1 LIGNE     ;
*
* COSTRUZIONE RIGIDEZZE
RIGT= VINCO         ;
N=0                 ;
REPETER LOP11 NZ    ;
 N=N+1              ;
 SI((EXTR SCNEW N) EGA 0) ;
  SI( NIT > 1 ) ;
  DETR ( TRIG.N) ;
  FINSI          ;
  TRIG.N= RIGI  TMOD.N (TMAT.N ET TCAR.N)  ;
 FINSI;
 RIGT  = RIGT ET  TRIG.N    ;
FIN LOP11 ;
*
* RISOLUZIONE ALL ITERAZIONE -NIT-
*
DE1=RESO RIGT FORZE;
*
SI ( NIT EGA 1 );
TITRE 'MAGLIATURA DELLA STRUTTURA ' ;
MAGLIA= EXTR RIGT MAIL NOMU ;
CMA = CONT MAGLIA;
REPETER LOP55 ;
MESS ' ------------------------------------------------';
MESS ' DARE COEFFICIENTE DI AMPLIFICAZIONE FORZE       ';
MESS ' ------------------------------------------------';
OBTENIR AMPX*FLOTTANT ;
VF1   = VECT FORZE AMPX FX FY FZ ROUG ;
TRAC PV RIGT  VF1;
MESS ' ------------------------------------------------';
MESS ' VUOI COMBIARE L AMPLIFICAZIONE         ? (SI/NO)';
MESS ' ------------------------------------------------';
OBTENIR   SSNN*MOT ;
SI('EGA' SSNN 'NO');
   QUITTER LOP55 ;
FINSI            ;
FIN LOP55        ;
FINSI            ;
*
N=0 ;
REPETER BOUC3 NZ;
N=N+1;
*
* CALCOLO SPOSTAMENTI E TENSIONI PER OGNI ZONA
DEZ    = REDU  DE1 TMAI.N        ;
SIGZ   = SIGMA DEZ TMOD.N (TMAT.N ET TCAR.N) ;
DETR DEZ ;
*
* TENSIONE VON MISES
VMZ    = VMIS TMOD.N SIGZ TCAR.N ;
SI ( N EGA 1) ;
  VMT  = VMZ  ;
SINON         ;
  VMT   = VMT ET VMZ ;
FINSI         ;
DETR SIGZ     ;
*
* TENSIONI MAX VON MISES E ERRORE MAX PER ZONA
MVMZ =MAXIMUM VMZ                        ;
XRSZ= ABS((MVMZ - VLIM) / VLIM )         ;
ERSZ= XRSZ                               ;
* AGGIORNAMENTO DELLO SPESSORE
COEF=  (1-A)+(A * MVMZ / VLIM )          ;
SPE =  EXTRAI SPZO N                     ;
SPEN=  SPE   *  COEF                     ;
*
* VERIFICA CONVERGENZA
* E
* AGGIORNAMENTO SPESSORI
SI (ERSZ <EG CRIT )                     ;
* TENSIONI CONVERGENTI
 REMPLACE SCNEW N 1                     ;
 SPEN = SPE                             ;
SINON                                   ;
* TENSIONI NON CONVERGENTI
 SI (SPEN <EG STEC)                     ;
  SPEN= STEC                            ;
  ERSZ= 0.                 ;
  SI ((EXTR TCOLD N) EGA 1);
   REMPLACE SCNEW N 1      ;
  SINON                    ;
   NOCONV = 0              ;
   REMPLACE SCNEW N 0      ;
   REMPLACE TCOLD N 1      ;
  FINSI                    ;
 SINON                     ;
  NOCONV= 0                ;
  REMPLACE SCNEW N  0      ;
 FINSI                     ;
FINSI                      ;
SI(MERS < ERSZ)            ;
 MERS = ERSZ               ;
FINSI                      ;
*
SI(( MULT  NIT FSTAMP  ) OU ( NIT EGA 1 ));
MESS '---------------'     ;
MESS '  ZONA  ' N          ;
MESS '---------------'     ;
MESS '-------------------------------------' ;
MESS 'PESO ZONA...............' PESZ         ;
MESS '-------------------------------------' ;
MESS 'SPESSORE ATTUALE........' SPEN         ;
MESS '-------------------------------------' ;
MESS 'SPESSORE PRECEDENTE.....' SPE          ;
MESS '-------------------------------------' ;
MESS 'ERRORE SU ZONA..........' XRSZ         ;
MESS '-------------------------------------' ;
MESS 'SIGMA MAX DI VON MISES..' MVMZ         ;
MESS '------------------------------------------------';
SI(( EXTR SCNEW N) EGA 1 );
 MESS '    ==* SI HA CONVERGENZA  PER LA ZONA *==     ';
SINON ;
 MESS '  ==* NON SI HA CONVERGENZA  PER LA ZONA *==   ';
FINSI ;
MESS '------------------------------------------------';
FINSI ;
  REMPLACE SPZO N SPEN             ;
  DETR  TCAR.N                     ;
  TCAR.N= CARA TMOD.N  EPAIS  SPEN ;
  PESZ    = INTG TMOD.N TRHO.N TCAR.N     ;
  PESO    = PESO + PESZ            ;
FIN BOUC3                          ;
*
SI (NOCONV EGA 1 )   ;
   SAUTER PAGE       ;
   SAUTER LIGNE      ;
   MESS ' ================================================';
   MESS '    *** SI HA CONVERGENZA SU TUTTE LE ZONE ***   ';
   MESS ' ================================================';
FINSI                ;
SAUTER 1 LIGNE       ;
MESS ' =======================================';
MESS ' ERRORE MASSIMO...........' MERS         ;
MESS ' =======================================';
MESS ' PESO STRUTTURA...........' PESO         ;
MESS ' =======================================';
SAUTER    PAGE  ;
SAUTER 2  LIGNE ;
MESS ' ------------------------------------------------';
MESS ' VUOI GRAFICARE ANDAMENTI PESO ED ERRORE         ';
MESS ' E ISOVALORI DI VON MISES               ? (SI/NO)';
MESS ' ------------------------------------------------';
OBTENIR ZZNN*MOT    ;
BOL1='NEG' ZZNN 'NO';
NITP=NIT + 1 ;
PP11=PPESO ;
PPESO=INSE PP11 NITP PESO          ; DETR PP11           ;
PI11=PITER ;
PITER=INSE PI11 NITP ( FLOT NIT )  ; DETR PI11           ;
TITRE ' ANDAMENTO DEL PESO FINO ALL ITERAZIONE ' NIT     ;
GPESO= EVOL MANU 'ITERAZIONE' PITER 'PESO' PPESO ;
SI (BOL1) ;DESS GPESO ;FINSI;
DETR GPESO                ;
SI (NIT EGA 1 )           ;
PERSG= PROG MERS MERS     ;
SINON                     ;
PE11=PERSG ;
PERSG=INSE PE11 NITP MERS      ;  DETR PE11        ;
FINSI                     ;
TITRE ' ANDAMENTO DELL ERRORE SULLE SIGMA ALL ITERAZIONE ' NIT   ;
GERSG= EVOL MANU 'ITERAZIONE' PITER 'ERR.SIG' PERSG ;
SI (BOL1) ;DESS GERSG ;FINSI;
DETR GERSG                                          ;
* DISEGNO DELLE ISOVONMISES
TITRE ' ISOVALORI DI VON MISES '                    ;
SI (BOL1) ;TRAC PV MAGLIA TTMOD VMT  CMA      ;FINSI;
DETR VMT             ;
SAUTER LIGNE         ;
SI (NOCONV EGA 1 )   ;
   QUITTER BOUC1     ;
FINSI                ;
*
MESS ' ------------------------------------------------';
MESS ' VUOI CONTINUARE LE ITERAZIONI O USCIRE ? (SI/NO)';
MESS ' ------------------------------------------------';
OBTENIR SSNN*MOT ;
SI('EGA' SSNN 'NO') ;
   QUITTER BOUC1 ;
FINSI            ;
SAUTER LIGNE     ;
MESS ' ----------------------------------------------------------'  ;
MESS ' FORNIRE  IL NUOVO VALORE COEFFICIENTE DI RILASSAMENTO     '  ;
MESS ' COEFFICIENTE ATTUALE ' A                                     ;
MESS ' ----------------------------------------------------------'  ;
OBTENIR A1*FLOTTANT ;
SI('NEG' A1 'NON')  ;
A=A1;
FINSI;
DETR DE1            ;
FIN BOUC1           ;
SAUTER PAGE         ;
SAUTER 1 LIGNE      ;
MESS ' =======================================';
MESS ' NUMERO ITERAZIONI EFFETTUATE..' NIT     ;
MESS ' =======================================';
MESS ' PESO STRUTTURA................' PESO    ;
MESS ' =======================================';
SAUTER 1 LIGNE ;
MESS ' ------------------------------------------------';
N=0;
REPETER LOP22 NZ ;
N=N+1;
MESS ' ZONA ' N ' SPESSORE ' (EXTR SPZO N)              ;
MESS ' ------------------------------------------------';
FIN LOP22;
FINPROC SPZO DE1;
*
*----FINE PROCEDURA OTTIMIZZAZIONE PIASTRE A PESO MINIMO
*
*
