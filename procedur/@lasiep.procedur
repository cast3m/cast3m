$$$$ @LASIEP
* @LASIEP   PROCEDUR  KK2000    99/11/30    21:15:22     3720
*
************************************************************************
*                        PROCEDURA @LASIEP
************************************************************************
*
* Descrizione : calcola strato per stato le tensioni e le deformazioni
*               per una zona e le riporta nel sistema di riferimento
*               di ortotropia.
*
* Sintassi    : TAB_SIEP = @LASIEP TABLAM NBZON DEP1 (MOT1) ;
*
* Autore/Data : Miliozzi A.  10/04/1997
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_ZONA   Tavola caratteristica del laminato
*
* NBZON      Numero della zona
*
* DEP1       Campo di spostamenti
*
* MOT1       Parola chiave secondo la quale si possono richiedere
*            i soli campi di tensione o di deformazione od entrambi
*            Essa e' opzionale e puo' valere:
*                                'ALL' : entrambe         (Default)
*                                'SIG' : solo tensioni
*                                'EPS' : solo deformazioni
*
*--- OUTPUT
*==========
*
* TAB_SIEP   Tavola contenente i campi di tensione e di deformazione
*
************************************************************************
*
DEBPROC @LASIEP TT1*TABLE NZONE*ENTIER SP*CHPOINT MOT1/MOT ;
*
* - Controllo tipo di dati richiesti in output
MOT2 = 'ALL' ;
SI (EXIST MOT1) ;
  SI (EGA MOT1 'SIG'); MOT2=MOT1 ; FINSI ;
  SI (EGA MOT1 'EPS'); MOT2=MOT1 ; FINSI ;
FINSI;
*
* - CALCOLO MATERIAU E CARACTER PLY BY PLY
TCTIP    = TT1.TIPO         ;
TT1.TIPO = 'MLAY'           ;
TCAR1    = @LAMAT TT1 NZONE ;
TT1.TIPO = TCTIP            ;
*
* - CALCOLO TENSIONI E DEFORMAZIONI PLY BY PLY
* - E ROTAZIONE NEL SISTEMA DI RIFERIMENTO COMUNE
TT2  = TT1 . NZONE    ;
NPLY = DIME TCAR1.MAT ;
*
TSIEP = TABLE ;
SI ( (EGA MOT2 'ALL') OU (EGA MOT2 'SIG') ) ;
                             TSIEP.SIG1 = TABLE ; FINSI ;
SI ( (EGA MOT2 'ALL') OU (EGA MOT2 'EPS') ) ;
                             TSIEP.EPS1 = TABLE ; FINSI ;
*
N = 1 ;
REPETER LOOP0 NPLY ;
*
SI ( (EGA MOT2 'ALL') OU (EGA MOT2 'SIG') ) ;
  SIGE1        = SIGMA TT2.MOD1 TCAR1.MAT.N SP    ;
  TSIEP.SIG1.N = RTENS SIGE1 TT2.MOD1 TCAR1.MAT.N ;
  DETR SIGE1 ;
FINSI;
SI ( (EGA MOT2 'ALL') OU (EGA MOT2 'EPS') ) ;
  EPSI1        = EPSI TT2.MOD1 SP TCAR1.MAT.N ;
  TSIEP.EPS1.N = RTENS EPSI1 TT2.MOD1 TCAR1.MAT.N ;
  DETR EPSI1 ;
FINSI;
N = N + 1 ;
*
FIN LOOP0 ;
*
FINPROC TSIEP ;
*
************************************************************************
*                        FINE PROCEDURA @LASIEP
************************************************************************
*


