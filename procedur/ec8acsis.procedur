$$$$ EC8ACSIS
* EC8ACSIS  PROCEDUR  CB215821  15/12/02    21:15:03     8719
****************************************************************************
*                                            DATE     25/05/90
*   Procedure EC8ACSIS
*
*   RS = EC8ACSIS PARAM;
****************************************************************************
*   objet:
*
* a)
*
*   Generation d'un spectre de reponse RS (objet de type EVOLUTION
*   comportant une seule courbe) selon les directive de l' EUROCODE
*   numero 8 et suivant les parametres PARAM (objet de type TABLE).
*
*   L' EUROCODE 8 propose l' evolution en periode suivante:
*
*    0  < T  < T1       Be = 1 + T/T1 * (B0-1)
*
*    T1 < T  < T2       Be = B0
*
*    T2 < T             Be = (T2/T)**K * B0
*
*   Be(B0) etant le spectre de reponse d' acceleration normalise, pour un
*   amortissement de 5%, RS est obtenu apres introduction de l'acceleration
*   maximale Ag du sol et de le coeficient d'amortissement (Khi) (<.7):
*
*                       RE =  Ag * Be(Eta*B0)  Eta=(.05/Khi) **.5
*
*   Pour des periode elevees le code doit etre complete. Pour les terrains
*   standards, on limite le deplacement :
*
*                        d < dmax = (Ag/g) * D0
*
*   ou g est l'acceleration de la pesanteur.
*
*   Dans ce cas le RE sera definie par le dmax multiplier par le facteur
*   d'amplification pour les hautes periodes AMPF (AMPF =1.4).
*
*   Des jeux de donnees (T1,T2,B0,K et D0) sont prevus pour les terrains
*   de type A, B ou C.
*
*   Le spectre RS est genere sur une grille de periode compris entre
*   TINI (<T1) et TFIN (>T2).
*
*
* b)
*
*    Generation d'un spectre de reponse RS pour utiliser dans les analyses
*    Lineaires. Dans ce cas B0 est divise par le facteur de comportement
*
*          B0 = B0 / Q
*
*    et par T > T2 la condition de deplacement constant n'est pas imposee.
*    c.a.d.
*
*                RE =  Ag * Be(Eta*B0/Q)
*
*    sujet a la condition
*
*                RE >= 0.2 Ag
*
****************************************************************************
*   parametres obligatoires:
*
*   Les parametres sont contenues dans PARAM (objet de type TABLE).
*
*   PARAM.'TYPE'= objet de type MOT indiquant le type de spectre a
*                 savoir:
*
*              a) 'ALIN' (spectre de reponse pour l'analyse lineaire
*                 (linear analysis design spectra)). dans ce cas
*                 on est oblige de founire le paramaitre Q coeficient
*                 de comportement (behaviour factor) dans la table
*
*              b) 'GSIG' (spectre de reponse pour la generation des
*                 signaux artificieles

*   PARAM.'AG'  = objet de type FLOTTANT indiquant l'acceleration
*                 maximalle .
*
*   PARAM.'SOIL'= objet de type MOT indiquant le type de terrain
*                 'A' 'B' ou 'C'
*      ou
*
*   PARAM.'T1'
*   PARAM.'T2'  = objets de type FLOTTANT indiquant T1, T2 et B0.
*   PARAM.'B0'
*
*   PARAM.'TINI'
*   PARAM.'TFIN'= objets de type FLOTTANT donnant TINI et TFIN.
****************************************************************************
*   parametres optionnels:
*
*   PARAM.'K'   = objet de type FLOTTANT representant K (defaut = 1
*                 conduisant a une vitesse constante)
*
*   PARAM.'D0'  = objet de type FLOTTANT indiquant D0
*                 (defaut = pas de limitation).
*
*   PARAM.'AMOR' = objet de type FLOTTANT indiquant Khi (defaut = 0.05)
*
*
*   PARAM.'GRAN'= objet de type mot representant la grandeur physique de
*                 reponse: ACCE(leration), VITE(sse) ou DEPL(acement
*                 relatif). Le defaut est ACCE.
*
*   PARAM.'ABSC'= objet de type mot representant la grandeur physique des
*                 abcisses: PERI(ode) ou FREQ(uence). Le defaut est PERI.
*
*
*   PARAM.'N'   = objets de type ENTIER indiquant le nombre de points sur
*   PARAM.'NP'    les branches du spectre au-dela de T2 (defaut = 25).
****************************************************************************
'DEBPROC' EC8ACSIS PARAM*'TABLE';
*
*MESSAGE 'debut'
*----- Lecture des donnees dans la table et setting des defauts
*
*--- Presence des donnees obligatoires
*
LOK=VRAI;
SS=1.;
'SI' ('EXISTE' PARAM 'SOIL');
  SOIL= PARAM.'SOIL';
'SI' ('EGA' SOIL 'A'); T1=.2; T2=.4; B0=2.5;K=1;D0= 60.; SS=1.;'SINON';
'SI' ('EGA' SOIL 'B'); T1=.2; T2=.6; B0=2.5;K=1;D0= 90.; SS=1.;'SINON';
'SI' ('EGA' SOIL 'C'); T1=.3; T2=.8; B0=2.5;K=1;D0=120.; SS=.8;'SINON';
  'MESSAGE' 'LE SOIL EST A, B OU C'; LOK=FAUX; 'FINSI';'FINSI';'FINSI';
'SINON';
 SOIL='specifique';
 'SI' ('NON' ('EXISTE' PARAM 'T1'));
   'MESSAGE' 'LE PARAMETRE T1 DOIT ETRE PRESENT DANS LA TABLE';
   LOK=FAUX;
 'SINON'; T1 = PARAM.'T1';
 'FINSI';
 'SI' ('NON' ('EXISTE' PARAM 'T2'));
   'MESSAGE' 'LE PARAMETRE T2 DOIT ETRE PRESENT DANS LA TABLE';
   LOK=FAUX;
 'SINON'; T2 = PARAM.'T2';
 'FINSI';
 'SI' ('NON' ('EXISTE' PARAM 'B0'));
   'MESSAGE' 'LE PARAMETRE B0 DOIT ETRE PRESENT DANS LA TABLE';
   LOK=FAUX;
 'SINON'; B0 = PARAM.'B0';
 'FINSI';
'FINSI';
'SI' ('NON' ('EXISTE' PARAM 'TINI'));
  'MESSAGE' 'LE PARAMETRE TINI DOIT ETRE PRESENT DANS LA TABLE';
  LOK=FAUX;
'SINON'; TINI = PARAM.'TINI';
'FINSI';
'SI' ('NON' ('EXISTE' PARAM 'TFIN'));
  'MESSAGE' 'LE PARAMETRE TFIN DOIT ETRE PRESENT DANS LA TABLE';
  LOK=FAUX;
'SINON'; TFIN = PARAM.'TFIN';
'FINSI';
'SI' ('NON' ('EXISTE' PARAM 'AG'));
  'MESSAGE' 'LE PARAMETRE AG DOIT ETRE PRESENT DANS LA TABLE';
  LOK=FAUX;
'SINON'; AG = PARAM.'AG';
'FINSI';
****************************************************************
'SI' ('NON' ('EXISTE' PARAM 'TYPE'));
  'MESSAGE'
  'LE MOT CLEE QUI DEFINE LE TYPE DOIT ETRE PRESENT DANS LA TABLE';
  LOK=FAUX;
'SINON'; MTYPE = PARAM.'TYPE';
         'SI' ('EGA' MTYPE 'ALIN');
              'SI' ('EXISTE' PARAM 'Q'); QFAC = PARAM.'Q';
              'SINON';
              'MESSAGE' 'LE FACTEUR Q DOIT ETRE PRESENT DANS LA TABLE';
               LOK=FAUX;
              'FINSI';
         'FINSI';
         'SI' ('EGA' MTYPE 'GSIG'); QFAC = 1.0; 'FINSI';
    'SI' (('EGA' MTYPE 'GSIG') 'OU' ('EGA' MTYPE 'ALIN'));
    'SINON'
    'MESSAGE' ' LES TYPES POSSIBLES SON "ALIN" et "GSIG"';
    LOK=FAUX;
    'FINSI';
'FINSI';
*MESSAGE 'fin presence donnee obligatoire';
*
*--- Presence des donnees facultatives
*
'SI' LOK;
  'SI' ('NON' ('EXISTE' PARAM 'SOIL'));
    'SI' ('EXISTE' PARAM 'K' ); K =PARAM.'K' ;
    'SINON';                    K =1 ;         'FINSI';
    'SI' ('EXISTE' PARAM 'D0'); D0=PARAM.'D0';
    'SINON';                    D0=0.;         'FINSI';
  'FINSI';
  'SI' ('EXISTE' PARAM 'AMOR'); AMOR=PARAM.'AMOR';
*                              ETA = (0.05/AMOR)**0.5;
                               AAA=0.05/AMOR; ETA = AAA**0.5;
  'SINON';                     ETA=1;           'FINSI';
  'SINON';                     AMOR=0.05;       'FINSI';
  'SI' ('EXISTE' PARAM 'N')  ; N  =PARAM.'N' ;
  'SINON';                     N  =25;         'FINSI';
  'SI' ('EXISTE' PARAM 'NP') ; NP =PARAM.'NP' ;
  'SINON';                     NP =25;         'FINSI';
  'SI' ('EXISTE' PARAM 'GRAN');GR =PARAM.'GRAN';
  'SINON';                     GR ='ACCE';        'FINSI';
  'SI' ('EXISTE' PARAM 'ABSC');ABSC =PARAM.'ABSC';
  'SINON';                     ABSC ='PERI';        'FINSI';
'FINSI';
*MESSAGE 'fin presence donnee facultative';
*
*--- Les constantes
*
*    AMPF = Facteur d'amplification pour les hautes periodes
*
 DPI=2 * PI; G=980.665; AMPF=1.4;
*
*--- Verification des donnees
*
*
'SI' LOK;
  'SI' (TINI '>EG' T1);
    'MESSAGE' 'LE PARAMETRE TINI DOIT ETRE INFERIEUR A T1';
    LOK=FAUX;
  'FINSI';
  'SI' (TFIN '<EG' T2);
    'MESSAGE' 'LE PARAMETRE TFIN DOIT ETRE SUPERIEUR A T2';
    LOK=FAUX;
  'FINSI';
  'SI' ((D0 'EGA' 0.) 'OU' ('EGA' MTYPE 'ALIN')); T3=T2;
  'SINON';
           DMAX= (AG/G) * D0 * AMPF;
*          T3  = (((DPI**2)/(T2**K))*(D0/(B0*G)))**(1./(2-K));
           T3  = ((DPI**2)/(T2**K))*(D0*AMPF/(B0*G));
           EXPO=1./(2-K);
           T3  = T3 ** EXPO;
  'FINSI';
  'SI' (T3 '<' T2);
    'MESSAGE' 'T3 (DEBUT DE LA ZONE A D CST DOIT ETRE SUPERIEUR A T2';
    LOK=FAUX;
  'FINSI';
  'SI' (T3 '>EG' TFIN); T3=T2; 'FINSI';
'FINSI';
*MESSAGE 'fin verification';
*
*----- Calcul du Spectre de reponse
*
'SI' LOK;
  B0 = B0 * ETA * SS / QFAC ;
  RES='PROG'; PER='PROG';
*
*--- Premiere zone (acceleration lineairement croissante)
*
  J=1; T=TINI;
  UNPXI='EXP' ( (('LOG' T1) - ('LOG' TINI)) / 5 );
  'REPETER' LAB0 5;
    RES ='INSERER' RES J (1+((B0-1.)*(T/T1))); PER= 'INSERER' PER J T;
    J=J+1;T=T*UNPXI;
  'FIN' LAB0;
*MESSAGE 'fin premiere zone';
*
*--- Seconde zone (acceleration constante)
*
  RES='INSERER' RES J B0; PER='INSERER' PER J T1;
  J=J+1;
  RES='INSERER' RES J B0; PER='INSERER' PER J T2;
*MESSAGE 'fin seconde zone';
*
*--- Troisieme zone (acceleration K decroissante)
*
  T=T2;
  'SI' (T2 'EGA' T3);
    UNPXI='EXP' ( (('LOG' TFIN) - ('LOG' T2)) / N );
  'SINON';
    UNPXI='EXP' ( (('LOG' T3) - ('LOG' T2)) / N );
  'FINSI';
  'REPETER' LAB1 (N-1);
    J=J+1;T=T*UNPXI;
*   RES='INSERER' RES J (B0/((T/T2)**K));PER= 'INSERER' PER J T;
    AAA=T/T2; B1 = B0/(AAA**K); CR11 = AG / G / 5;
        'SI' ( CR11 '>EG' B1 );
         B1 = CR11;
        'FINSI';
         RES='INSERER' RES J B1 ; PER='INSERER' PER J T;
  'FIN' LAB1;
  J=J+1;
  'SI' (T2 'EGA' T3); T=TFIN;
  'SINON'; T=T3;              'FINSI';
* B1=B0/((T/T2)**K);
  AAA=T/T2;  B1 = B0/(AAA**K); CR11 = AG / G / 5;
        'SI' ( CR11 '>EG' B1 );
         B1 = CR11;
        'FINSI';
         RES='INSERER' RES J B1 ;PER= 'INSERER' PER J T;
*B1=B0/(AAA**K);
*  RES='INSERER' RES J B1;PER= 'INSERER' PER J T;
*MESSAGE 'fin troisieme zone';
*
*--- Quatrieme zone (acceleration KP decroissante)
*
  'SI' ('NON' (T2 'EGA' T3));
    UNPXI='EXP' ( (('LOG' TFIN) - ('LOG' T3)) / NP );
    'REPETER' LAB2 (NP-1);
      J=J+1;T=T*UNPXI;
      RES='INSERER' RES J (B1/((T/T3)**2));PER= 'INSERER' PER J T;
    'FIN' LAB2;
    J=J+1;
    RES='INSERER' RES J (B1/((TFIN/T3)**2));PER= 'INSERER' PER J TFIN;
  'FINSI';
*MESSAGE 'fin quatrieme zone';
*
*--- Normalisation
*
  RES=RES*AG;
*MESSAGE 'fin normalisation';
*
*--- Generation en vitesse
*
  'SI' ('EGA' GR 'VITE');
    DRES=J; J=0;
    'REPETER' LABV DRES;
      J=J+1;
      T='EXTRAIRE' PER J; RA='EXTRAIRE' RES J;
      'REMPLACER' RES J (RA * (T / DPI));
    'FIN' LABV;
  'FINSI';
*
*--- Generation en deplacement
*
  'SI' ('EGA' GR 'DEPL');
    DRES=J; J=0;
    'REPETER' LABD DRES;
      J=J+1;
      T='EXTRAIRE' PER J; RA='EXTRAIRE' RES J;
      'REMPLACER' RES J (RA * ((T / DPI)**2));
    'FIN' LABD;
  'FINSI';
*
*--- Abcisses en frequence
*
  'SI' ('EGA' ABSC 'FREQ');
    DRES=J; J=0;
    FPER= 'PROG';
    FRES= 'PROG';
     'REPETER' BLO1 DRES;
      J=J+1;
      T='EXTRAIRE' PER (DRES - J + 1); RA='EXTRAIRE' RES (DRES - J + 1);
      TF=1. / T;
      FPER='INSE' FPER J TF;
      FRES='INSE' FRES J RA;
     'FIN' BLO1;
    PER = FPER;
    RES = FRES;
  'FINSI';
*
*--- Generation de l'objet evol contenant le RS
*
ETA1 = ETA * 0.05;
ALF1 = AG / G;
*
'SI' ('EGA' MTYPE 'ALIN');
'TITRE' 'EC8 (LADS): Soil ' SOIL 'q =' QFAC 'Eta =' ETA1 'Alfa =' ALF1;
'SINON';
'TITRE' 'EC8 Response Spectrum: Soil ' SOIL 'Eta =' ETA1 'Alfa =' ALF1;
'FINSI';
  RS ='EVOL' 'MANU' ABSC PER GR RES;
*
'FINSI';
*
'FINPROC' RS;


