$$$$ POT_VECT
* POT_VECT  PROCEDUR  FANDEUR   14/10/10    21:15:20     8178
 'DEBPROC' POT_VECT  MATAB*'TABLE'   SOLIN/'MOT' ;
************************************************************************
*        MAGNETOSTATIQUE  2D  EN POTENTIEL VECTEUR                     *
************************************************************************
*      MATAB  TABLE D ENTREE CONTENANT
*    MATAB.'MUAIR'   PERMEABILITE DE L AIR (PAR DEFAUT UNITE METRE     *
*    MATAB.'AIR'     PARTIE AIR  NON   REDUITE A UN SUPER ELEMENT      *
*
*    MATAB.'TABNUSEC' = TABMAT
*            TABMAT doit etre une table indexé par les objets modeles  *
*  des differentes zones ferro_magnétiques
*       les modeles doivent obligatoirement etre :
*                        THERMIQUE ISOTROPE ou THERMIQUE ORTHOTROPE    *
*           TABMAT.OBMOD1 = STN1 ;
*           TABMAT.OBMOD2 = STN2 ;

*      structure  de la table STN
*        a) pour un materiau isotrope
*             STN.'EV1' = KEV1  courbe  1./MU0*MUREL(B)
*                                                    type evolution    *
*        b) pour un materiau orthotrope
*             STN.'EV1' = KEV1  courbe 1./MU0*MUREL(B) direction DIR1 *
*                           peut eter obtenue par H_B
*             STN.'DIR1' =(X1 Y1) direction associée a  KEV1
*                                                                      *
*             STN.'EV2' = KEV2  courbe 1./MU0*MUREL(B) direction DIR2
*      nécessairement  perpendiculaire à DIR1                          *
*                                                                      *
*                                                                      *
*    MATAB.'AIRSUP'  PARTIE AIR  TRAITEE EN SUPER (NON OBLIGATOIRE)    *
*    MATAB.'MAITRES'   POINT MAITRES SI SUPER ELEMENT                  *
*    MATAB.'ENCS '   COND. LIMITE SUR SUPER (EVENTUELLEMENT)           *
*                                                                      *
*    MATAB.'BLOQUE'  CONDITIONS LIMITES GENERALES REGROUPEES           *
*    MATAB.'IMPOSE'  CHPOINT (CREE PAR DEPIMP) LA PARTIE RIGIDITE
*
*                  DOIT ETRE DANS MATAB.'BLOQUE'
*                                                                      *
*    MATAB.'COUR'   TABLE  DE TABLES CONTENANT LA DESCRIPTION DES      *
*                   BLOCS DE COURANTS   CONSTITUEE PAR UN OU DES       *
*                   APPEL(S) A LA PROCEDURE DESCOUR                    *
*    MATAB.'AXI' =  VRAI     SI PROBLEME AXISYMETRIQUE                 *
*    SOLIN   MOT OPTIONNNEL  POUR LE CALCUL DU PREMIER PAS LINEAIRE    *
************************************************************************
*        EN SORTIE MATAB CONTIENT LES OBJETS NECESSAIRES               *
*           AU CALCUL NON LINEAIRE                                     *
*    ET LA SOLUTION DU PREMIER PAS SI DEMANDEE DS MATAB.'POTENTIEL     *
************************************************************************
  AXI= FAUX ;
   'SI' ( 'EXISTE' MATAB 'AXI' ); AXI = MATAB.'AXI' ;FINSI ;
 MUAIR = 4 * PI * 1.E-7 ;
   'SI' ( 'EXISTE'  MATAB 'MUAIR') ;MUAIR = MATAB.'MUAIR' ;'FINSI' ;
* MATAB.'MUAIR'= MUAIR ;
 AIR = MATAB.'AIR' ;
TABMU   = MATAB.'TABNUSEC' ;
IDMUR = 'INDE' TABMU  ;
NMUR = 'DIME' IDMUR ;
IMUR = 0 ;
'REPETER' BOUCMUR NMUR ;
    IMUR = IMUR + 1 ;
    MOD_FERI= IDMUR.IMUR ;
    STN   = TABMU.MOD_FERI;
 LIST STN ;
    ORTT = FAUX ;
*
     'SI' ('EXISTE' STN 'EV2' ) ;
   ORTT = VRAI ;
  MU1  = EXTR ( EXTR (STN.'EV1') ORDO ) 1  ;
    DIR1 = STN.'DIR1' ;
  MU2  =  EXTR ( EXTR (STN.'EV2') ORDO ) 1  ;

  MAT_FERi= 'MATE' MOD_FERi  DIRECTION DIR1
   'K1'  MU1    'K2'  MU2    ;
    'MESS' IMUR 'Materiau Ferromag. de Permeabilite :' MU1  MU2 ;
    'MESS' 'DIRECTION ' (COOR 1 DIR1) ( COOR 2 DIR1 ) ;
     'SINON' ;
 MU1   =  EXTR ( EXTR (STN.'EV1') ORDO ) 1  ;
    'MESS' IMUR 'Materiau Ferromag. de Permeabilite :' MU1   ;
    MAT_FERI= 'MATE' MOD_FERI 'K'   MU1    ;
     'FINSI' ;
*
    'SI' AXI ;

        RFER = 'COOR' 1 MAT_FERI ;
   RFER = ('CHAN' 'RIGIDITE' MOD_FERI RFER ) ** -1. ;
   INVRFER= 'CHAN ' 'NOEUD'  MOD_FERI RFER  ;
           'SI' ORTT ;
        K1 =   MU1 * INVRFER       ;
        K2 =   MU2 * INVRFER       ;
      MAT_FERI = 'MATE' MOD_FERI  DIRECTION DIR1
      'K1'  K1  'K2'    K2 ;
           'SINON' ;
    K1 =  MU1 * INVRFER  ;
        MAT_FERI = 'MATE' MOD_FERI 'K' K1       ;
           'FINSI' ;
    'FINSI';
    'SI' ('EGA' IMUR 1 ) ;
        MAT_FER = MAT_FERI ;
        OBJ2 = MOD_FERi ;
        'SINON' ;
        MAT_FER  = MAT_FER 'ET' MAT_FERI ;
        OBJ2 = OBJ2 ET  MOD_FERi ;
    'FINSI' ;
 STN.KLAST = MAT_FERi ;
FIN BOUCMUR ;
    RIGF    = 'CONDUC' OBJ2  MAT_FER ;
*
 OBJ1= 'MODE' AIR  THERMIQUE ISOTROPE ;
 MAT1= 'MATE' OBJ1  'K' (1./MUAIR) ;
  'SI'  AXI  ;
*             rectification  conductibilites
*=================
    RAIR = ('CHAN' 'RIGIDITE' OBJ1  ('COOR' 1 MAT1 )) ** -1.    ;
    KAIR  = ('CHAN' 'NOEUD' OBJ1 RAIR)/MUAIR   ;
*=================
  MAT1= 'MATE' OBJ1 'K' KAIR  ;
   'FINSI' ;
   'SI' ( 'EXISTE' MATAB 'AIRSUP') ;
 AIRSUP = MATAB.'AIRSUP' ;
 OBJ3='MODE' AIRSUP  'THERMIQUE' 'ISOTROPE' ;
 MAT3= 'MATE' OBJ3  'K' ( 1./MUAIR);
    'SI'  AXI  ;
  RSIR =('CHAN' 'RIGIDITE' OBJ3  ('COOR' 1 MAT3 )) ** -1.    ;
  KAI3  = ('CHAN' 'NOEUD' OBJ3 RSIR ) / MUAIR ;
  MAT3 = MATE OBJ3 'K'  KAI3  ;
    'FINSI' ;
      'SI' ( 'EXISTE' MATAB 'ENCS' );
 RIGB= ('CONDUC' OBJ3 MAT3 ) 'ET' ( 'BLOQUER' (MATAB.'ENCS') 'T' ) ;
      SFAC = MATAB.'MAITRES';
      SUP1 = 'SUPER' 'RIGIDITE' RIGB SFAC ;
      MATAB.'SUPER' = SUP1 ;
      'FINSI' ;
   'FINS' ;
 RIGA= 'CONDUC' OBJ1 MAT1 ;
*RIGF= 'CONDUC' OBJ2 MAT2 ;
   'SI' ( 'EXISTE' MATAB 'MAITRES') ;
 RIGCON= RIGA 'ET' ( 'EXTRAI' SUP1 'RIGI' );
   'SINON' ;
 RIGCON = RIGA ;
   'FINSI' ;
*                    charge
  TABCOUR = TABLE ;
  TABCOUR = MATAB.'COUR';
  III = 'INDEX' TABCOUR ;
  IZ= 0 ;

  'REPETER' BOUC ;
  IZ= IZ + 1 ;
    'SI' ( 'NON'  ('EXISTE' III IZ )) ;'QUITTER' BOUC ; 'FINSI' ;
    STN = TABCOUR.IZ ;GEO = STN.'GEO' ;
    obbb = 'MODE' GEO 'THERMIQUE' 'ISOTROPE' ;

   'SI' ('NON'( 'EXISTE' STN 'FIL' )) ;
    FEIZ = 'SOURCE' obbb 1.  GEO  ;

    SSS='EXTR' ('RESU' FEIZ) 'Q' (( 'EXTR' FEIZ 'MAIL' ) 'POINT' 1);
*
           'SI' ( 'EXISTE' STN 'AMP' ) ;
        J = STN.'AMP' ;
        STN.'AT' = SSS * J ;
        FEIZ = FEIZ * J  ;
            'SINON' ;
        SDO = STN.'AT' ;
        J = SDO / SSS ;
        STN.'AMP'= J ;
        FEIZ = FEIZ * J  ;
            'FINSI' ;
      'FINSI' ;
    'SI' ( 'EXISTE' STN 'FIL') ;
        J= STN.'FIL' ;nn = GEO 'NBNO' ;
        FEIZ = 'MANU' 'CHPO' GEO 1 'Q' ( 'PROG' nn  * J ) ;
        STN.'AT' = J * nn ;
        STN.'AMP' = J ;
        'FINSI' ;
 sauter 5 lignes ;
*
 'MESS'       ' Rappel  des zones de courants  ' ;
 'MESS' ' BLOC  '  IZ  '  JAMP ' J ' NI'  STN.'AT'  ;
     'SI' ( 'EGA' IZ 1 ) ; FE = FEIZ ; 'SINON' ;
    FE = FE + FEIZ ;
     'FINSI' ;
   'FIN' BOUC ;


 MATAB.'RIGCON'= RIGCON;
 MATAB.'RIGFER'= RIGF  ;
     'SI' ( 'EXIST' SOLIN ) ;
 'MESS' '  *****************************************************';
 'MESS' '  *       CALCUL DE LA SOLUTION LINEAIRE              *';
 'MESS' '  *****************************************************';

       'SI' ( 'EXISTE' MATAB  'BLOQUE');
       BBB = MATAB.'BLOQUE' ;
       'FINSI' ;

        'SI' ('EXISTE' MATAB 'IMPOSE' );
        FE  = FE 'ET' ( MATAB.'IMPOSE' ) ;
        'FINSI' ;

  MATAB.'RHS'= FE ;
  RHS = FE ;
*
       'SI' ( 'EXISTE' BBB ) ;
  SOL1= 'RESOU' ( RIGF 'ET' RIGCON 'ET' BBB ) RHS    ;
       'SINON' ;
  SOL1= 'RESOU' ( RIGF 'ET' RIGCON ) RHS    ;
       'FINSI' ;
  MATAB.'POTENTIEL'= SOL1 ;
     'FINSI' ;
*
   'FINPROC' ;
$$$$



