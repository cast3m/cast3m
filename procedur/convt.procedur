$$$$ CONVT
* CONVT     PROCEDUR  MAUGIS    07/10/26    21:15:15     5712
************************************************************************
* NOM         : CONVT
* DESCRIPTION : convertit un temps (s) en une chaîne de caractères
*               contenant ce temps exprimé dans l'unité pertinente
*               (fs,ps,ns,us,ms,s,h,j,a)
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Pascal MAUGIS (CEA/DEN/DM2S/SFME/MTMS)
*               mél : maugis@semt2.smts.cea.fr
*
**********************************************************************
*
* VERSION    : v2, 11/02/2003, version évoluée
* HISTORIQUE : v1, 11/02/2003, création
* HISTORIQUE : v2, 29/07/2004, chgt nom de @CONVT en CONVT
* HISTORIQUE : v3, 07/08/2007, incorpore plus d'unités
* HISTORIQUE : v4, 07/09/2007, données des unités possible en minuscule
*                              ou anglaises
*
************************************************************************
* Prière de PRENDRE LE TEMPS de compléter les commentaires
* en cas de modification de ce sous-programme afin de faciliter
* la maintenance !
************************************************************************
*
*
'DEBPROC' CONVT tps*'FLOTTANT' nn/'ENTIER' unit/'MOT' ;

* si nul, on ne convertit ni n'affiche d'unité
  'SI' ('EGA' tps 0.) ;
     out = '0' ;
     'QUITTER' CONVT ;
  'FINSI' ;

* nombre de chiffres après la virgule
  'SI' ('NON' ('EXISTE' nn)) ;
    nn = 2 ;
  'FINSI' ;

* unité cible (parmi US,MS,S,H,J,D,A,Y)
  LIUNIT = 'MOTS' 'FS' 'PS' 'NS' 'US' 'MS' 'S' 'H' 'J' 'D' 'A' 'Y'
                  'fs' 'ps' 'ns' 'us' 'ms' 's' 'h' 'j' 'd' 'a' 'y' ;

  'SI' ('NON' ('EXISTE' unit)) ;
     'SI'           (tps < 1.d-12) ;
         unit = 'MOT' 'FS' ;
     'SINON' ; 'SI' (tps < 1.d-9) ;
         unit = 'MOT' 'PS' ;
     'SINON' ; 'SI' (tps < 1.d-6) ;
         unit = 'MOT' 'NS' ;
     'SINON' ; 'SI' (tps < 1.d-3) ;
         unit = 'MOT' 'US' ;
     'SINON' ; 'SI' (tps < 1.) ;
         unit = 'MOT' 'MS' ;
     'SINON' ; 'SI' (tps < 3600.) ;
         unit = 'MOT' 'S' ;
     'SINON' ; 'SI' (tps < (3600. * 24.)) ;
         unit = 'MOT' 'H' ;
     'SINON' ; 'SI' (tps < (3600. * 24. * 365.25)) ;
         unit = 'MOT' 'J' ;
     'SINON' ;
         unit = 'MOT' 'A' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
     'FINSI' ;
  'SINON' ;
     'SI' ('NON' ('EXISTE' liunit unit)) ;
        'ERREUR' 'Unité de temps ' UNIT ' non reconnue' ;
     'FINSI' ;
  'FINSI' ;

* détermination coef de conversion
  'SI' (('EGA' unit 'FS') 'OU' ('EGA' unit 'fs')) ;
     coef = 1.d15 ;
     ntps = 'MOT'  'fs' ;
  'FINSI' ;
  'SI' (('EGA' unit 'PS') 'OU' ('EGA' unit 'ps')) ;
     coef = 1.d12 ;
     ntps = 'MOT'  'ps' ;
  'FINSI' ;
  'SI' (('EGA' unit 'NS') 'OU' ('EGA' unit 'ns')) ;
     coef = 1.d9 ;
     ntps = 'MOT'  'ns' ;
  'FINSI' ;
  'SI' (('EGA' unit 'US') 'OU' ('EGA' unit 'us')) ;
     coef = 1.d6 ;
     ntps = 'MOT'  'us' ;
  'FINSI' ;
  'SI' (('EGA' unit 'MS') 'OU' ('EGA' unit 'ms')) ;
     coef = 1.d3;
     ntps = 'MOT'  'ms' ;
  'FINSI' ;
  'SI' (('EGA' unit 'S') 'OU' ('EGA' unit 's')) ;
     coef = 1. ;
     ntps = 'MOT'  's' ;
  'FINSI' ;
  'SI' (('EGA' unit 'H') 'OU' ('EGA' unit 'h')) ;
     coef = 1. / 3600. ;
     ntps = 'MOT'  'h' ;
  'FINSI' ;
  'SI' (('EGA' unit 'J') 'OU' ('EGA' unit 'j')) ;
     coef = 1. / (3600. * 24.) ;
     ntps = 'MOT'  'j' ;
  'FINSI' ;
  'SI' (('EGA' unit 'D') 'OU' ('EGA' unit 'd')) ;
     coef = 1. / (3600. * 24.) ;
     ntps = 'MOT'  'd' ;
  'FINSI' ;
  'SI' (('EGA' unit 'A') 'OU' ('EGA' unit 'a')) ;
     coef = 1. / (3600. * 24. * 365.25) ;
     ntps = 'MOT'  'a' ;
  'FINSI' ;
  'SI' (('EGA' unit 'Y') 'OU' ('EGA' unit 'y')) ;
     coef = 1. / (3600. * 24. * 365.25) ;
     ntps = 'MOT'  'y' ;
  'FINSI' ;

* convertion
out = 'CHAINE' (@FIX (tps * coef) nn) ntps ;

'FINPROC' out ;
$$$$


