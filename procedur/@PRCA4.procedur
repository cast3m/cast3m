$$$$ @PRCA4
* @PRCA4    PROCEDUR  MB234859  18/11/21    21:15:02     10007
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*                    PROCEDURE DE CALCUL @PRCA4
*
*  Application du profil d'usure au maillage
*
* Entrees :
*   T1    : Table de PASAPAS
*   ID_BU : Entier donnant le numero de la BOITE D'USURE a considerer
*
* Sortie :
*   T1    : Table de PASAPAS avec les booleens mis a jour
*
* A appeler dans le jeu de donnees apres sortie de PASAPAS
*
***********************************************************************
DEBP @PRCA4 T1*'TABLE' ID_BU*'ENTIER';

* OPDefMsh : VRAI -> Trace le maillage deforme
* OPEvOrIm : VRAI -> Trace Evolutions Ordonnees Importantes (dans le repere d'us
  OPDefMsh = FAUX ;
  OPEvOrIm = FAUX ;

  BOITE_US = T1.'BOITES_USURES'. ID_BU        ;
  VOL_REPA = BOITE_US.'VOLUME_REPARTITION'    ;
  SUR_APPL = BOITE_US.'SURFACE_APPLICATION'   ;
  SUR_APPU = BOITE_US.'SURFACE_APPUI'         ;
  PT_REF_U = BOITE_US.'POINT_O_U'             ;
  VEC_APPL = BOITE_US.'DIRECTION_APPLICATION' ;
  OuiUsCyl = BOITE_US.'USURE_CYCLIQUE' ;
  NCNECC   = BOITE_US.'NCNECC' ;
* Indice associe aux resultats du pas courant
  Inc = BOITE_US.'INDICE-STOCKAGE' ;

  SI OuiUsCyl ;
    NouvUsU2 = BOITE_US.'USURE_APPLIQUEE' . NCNECC ;
  SINON;
    NouvUsU2 = BOITE_US.'USURE_APPLIQUEE' . (Inc - 1) ;
  FINSI ;

*********************************************************
**  Changement de repere des coordonnees cartesiennes  **
*********************************************************
**  U1 et U2 a exprimer dans le repere UX et UY        **

* Direction du Vecteur Directeur U2 donnee par -VEC_APPL
  VEC_US_X = COOR VEC_APPL 1 ;
  VEC_US_Y = COOR VEC_APPL 2 ;
  N_VEC_US = ((VEC_US_X**2) + (VEC_US_Y**2))**(0.5) ;
  U2_X     = -1. * VEC_US_X / N_VEC_US ;
  U2_Y     = -1. * VEC_US_Y / N_VEC_US ;
  VDU2     = POIN U2_X U2_Y ;

* Direction normale a U2 => Vecteur Directeur U1
  U1_X     = U2_Y ;
  U1_Y     = -1. * U2_X ;
  VDU1     = POIN U1_X U1_Y ;

* Application du changement de repere
  CU1_VREP CU2_VREP = @CHREP PT_REF_U VDU1 VDU2 VOL_REPA ;
  CU1_SAPU CU2_SAPU = @CHREP PT_REF_U VDU1 VDU2 SUR_APPU ;
  CU1_SAPL CU2_SAPL = @CHREP PT_REF_U VDU1 VDU2 SUR_APPL ;

* Construction des surfaces Sources aux calculs d'interpolation
*   Extraction sous forme de valeurs pour construire des evolutions croissantes
  CU1_SAPU = EXTR CU1_SAPU 'VALE' ;
  CU2_SAPU = EXTR CU2_SAPU 'VALE' ;
  CU1_SAPL = EXTR CU1_SAPL 'VALE' ;
  CU2_SAPL = EXTR CU2_SAPL 'VALE' ;

  EVU_SAPU = EVOL 'ROUG' 'MANU' 'U1' CU1_SAPU 'U2' CU2_SAPU ;
  EVU_SAPL = EVOL 'VERT' 'MANU' 'U1' CU1_SAPL 'U2' CU2_SAPL ;

  EVU_SAPU = ORDO EVU_SAPU 'CROI' ;
  EVU_SAPL = ORDO EVU_SAPL 'CROI' ;

*  Calcul du taux d'application d'usure via interpolation des positions d'appli
  U2APU_VR = IPOL CU1_VREP EVU_SAPU ;
  U2APL_VR = IPOL CU1_VREP EVU_SAPL ;

  TauxApVR = (CU2_VREP - U2APU_VR) / (U2APL_VR - U2APU_VR) ;

* Calcul de l'usure a appliquer via interpolation du profil d'usure
* calcule et application du taux d'appli
  PU2_SAPL = EXTR NouvUsU2 'VALE' ;
  EVPU2    = EVOL 'ROUG' 'MANU' 'U1' CU1_SAPL 'PU2' PU2_SAPL ;
  EVPU2    = ORDO EVPU2  'CROI'  ;

  SI OPEvOrIm ;
    DESS (EVU_SAPU ET EVU_SAPL ET EVPU2) 'LEGE';
  FINSI ;

  PU2APL_V = IPOL CU1_VREP EVPU2 ;
  PU2_VREP = TauxApVR * PU2APL_V ;

* /!\ le profil d'usure doit Ãªtre defini positivement pour user de la matiere
* /!\ et negativement pour creer de la matiere

* Retour vers les coordonnees initiales : Recuperation des composantes en X et Y
* via les composantes du vecteur d'application d'usure
  PUX_VREP = PU2_VREP * VEC_US_X / N_VEC_US ;
  PUY_VREP = PU2_VREP * VEC_US_Y / N_VEC_US ;

* Transformation de ces donnees pour creer le Champ par point de deplacement des
  ValPUX_V = EXTR PUX_VREP 'VALE' ;
  ValPUY_V = EXTR PUY_VREP 'VALE' ;

  VREP_NOD = EXTR (COOR VOL_REPA 1) 'MAIL' ;

  CHPO_Dep = MANU 'CHPO' VREP_NOD 2 'UX' ValPUX_V 'UY' ValPUY_V ;
*
* On effectue le delacement des noeuds sur la configuration deformee
  'FORM' (T1.'WTABLE'.'GE0_DEB') ;
  DEPL 'PLUS' VREP_NOD CHPO_Dep ;
  'FORM' (T1.'WTABLE'.'FOR0') ;
*
* Pour afficher le temps calcule
  T1.'FIN_CYCLE' = VRAI ;
*
  SI OPDefMsh ;
    TRAC VOL_REPA  ;
  FINSI ;

  FINP ;
$$$$


