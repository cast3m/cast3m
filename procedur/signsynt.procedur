$$$$ SIGNSYNT
* SIGNSYNT  PROCEDUR  BP208322  16/05/24    21:15:17     8932
'DEBPROC'   SIGNSYNT  MOT1*'MOT '   ZTAB*'TABLE   ' ;
*******************************************************************
*******************************************************************
*
*            PROCEDURE   S I G N S Y N T
*            ---------------------------
*
*    AUTEUR D.BROCHARD  (DEMT/EMSI) TEL 6994
*
*           CREATION DE SIGNAUX SYNTHETIQUES PAR RECOMBINAISON
*                   DE SINUSOIDES A PHASES ALEATOIRES
*
*                         DATE : 09/09/1988
*
*    REMARQUES
*    ---------
*
*       1-  LA METHODE DE GENERATION UTILISE LA TFR. ON DOIT DONC
*           UTILISER 2**NPT
*       2-  ON PEUT GENERER   :
*                        -  UN BRUIT BLANC        : OPTION BBLANC
*                        -  UN SIGNAL SYNTHETIQUE : OPTION FABR
*
*******************************************************************
*
*        SIGNSYNT    TAB1
*
*                  - TAB1      :   OBJET DE TYPE TABLE
*
*
*******************************************************************
*   OPTION   MOT1 = 'FABR'
*
*    TAB1  :  objet de type TABLE contenant
*
*       Indice       Type            Commentaires
*
*        MOTIT        MOT            TEXTE SUR 16 CARACT}RES
*        SEISME      TABLE
*                            Indice       Type          Commentaires
*
*                            SPECTRE    EVOLUTION   Spectre de r{f{rence
*                            AMORT      LISTREEL    Les amortissements
*                            TYPSP      MOT         'ACCE' 'VITE' 'DEPL'
*        SIGNAL      TABLE
*                            Indice       Type          Commentaires
*
*                            ENVE       MOT         Type de l'enveloppe
*                            NP         ENTIER      Puissance de 2
*                            DUREE      FLOTTANT    Dur{e du signal
*                            FRCOUP     FLOTTANT    FREQ. LIMITE
*
*        NBITER      ENTIER           Nombre d'it{rations
*        NBSIGN      ENTIER           Nombre de signaux
*        NALEAT      ENTIER           Param}tre d'initialisation des
*                                     phases
*
*******************************************************************
*
*    ZTITRE          TITRE
*    ZNP             NOMBRE DE POINTS EN PUISSANCE DE 2
*
*    ZEVSPE1(ZFREQ)       SP. DE REF. ENTRE EN DONNEES  DE TYPE TYPSP
*    ZEVSPEC(ZFREQ,ZSPEC)        IDEM EN DEPLACEMENT
*
*    ZEVSPREF(ZFRQ,ZSPREF)  SP. DE REF. APRES INTERPOLATION EN DEPL
*                             (SERT POUR LA CORRECTION,CALCUL DE RAP)
*
*    ZEVSPRE1(ZFRQ)         SP. DE REF. APRES INTERPOLATION EN TYPSP
*                                  (NE SERT QUE POUR LE TRACE)
*
*    ZEVSPO(ZFRQ)           SP. D'OSCILLATEUR EN DEPLACEMENT
*    ZEV1(ZFRQ)              IDEM DE TYPE TYPSP POUR LE TRACE
*
*    ZEPS            AMORTISSEMENT
*    ZNITER          NOMBRE D'ITERATIONS
*
*    ZNPT            NOMBRE DE POINTS DU SIGNAL
*                                           = 2 ** ZNP
*    ZDF             PAS EN FREQUENCE       = 1 / ZDUREE
*    ZDT             PAS EN TEMPS           = ZDUREE / (2 ** ZNP)
*
*    ZFRQ0  ZFRQ     TABLE DES FREQUENCES (AVEC ET SANS FREQUENCE NULLE)
*                     ZFRQ0 = ZFRC1 + ZFRC2  ET DE LONGUEUR ZNBFR
*    ZNBFR           (ZNPT / 2 ) + 1          : NB DE PTS DU SPECTRE
*    ZLRTEMP           TABLE DES TEMPS  AYANT  ZNPT POINTS
*
*
*******************************************************************
*    OPTION   BBLANC  :   SIGNAL BRUIT BLANC COMBINAISON DE SINUSOIDES
*                         A PHASES  ALEATOIRES
*    TAB1  :  objet de type TABLE contenant
*
*       Indice       Type            Commentaires
*
*        MOTIT        MOT            TEXTE SUR 16 CARACTERES
*        OPTION       MOT            'BBLANC'
*        NP          ENTIER          nombre de points en puissance de 2
*        DELTAF      FLOTTANT        Pas en fr{quence
*        NALEAT      ENTIER          Param}tre d'initialisation des
*                                    phases
*
*******************************************************************
*
*   ZTITRE         TITRE
*   ZNP            NOMBRE DE POINTS EN PUISSANCE DE 2
*   ZNPT           NB DE FREQUENCES = (2**ZNP)
*   ZDELTAF        PAS EN FREQUENCE
*   ZINIT          PARAMETRE INITIALISATION DES PHASES
*
*         SORTIE
*   EVBB0 'EVOLUTIO' CONTENANT LE SIGNAL
*
*=========================================================
*
ZOPTION = MOT1 ;   IMPII   = 'VALEUR' 'IMPI'     ;
MOTIT   = 'MOT' 'MOTIT'    ;   ZTITRE  = ZTAB. MOTIT      ;
'SI' ( 'EGA' ZOPTION 'FABR' ) ;
  SEISME  = 'MOT' 'SEISME'    ;    SPECTRE = 'MOT' 'SPECTRE'  ;
  AMORT   = 'MOT' 'AMORT'     ;    TYPSP   = 'MOT' 'TYPSP'  ;
  SIGNAL  = 'MOT' 'SIGNAL'    ;    DUREE   = 'MOT' 'DUREE'    ;
  NP      = 'MOT' 'NP'        ;    TDEBUT  = 'MOT' 'TDEBUT'   ;
  TFIN    = 'MOT' 'TFIN'      ;    ENVE    = 'MOT'  'ENVE'    ;
  NBITER  = 'MOT' 'NBITER'    ;    NBSIGN  = 'MOT' 'NBSIGN'   ;
  NALEAT  = 'MOT' 'NALEAT'    ;    FRCOUP  = 'MOT' 'FRCOUP'   ;
  OPTSORT = 'MOT' 'OPTSORT' ;
*
  ZEVSPE1 = ZTAB. SEISME . SPECTRE  ;
  ZEPS    = ZTAB. SEISME . AMORT    ;
  ZTYPSP  = ZTAB. SEISME . TYPSP  ;
  ZNP     = ZTAB. SIGNAL . NP       ;
  ZDUREE  = ZTAB. SIGNAL . DUREE    ;
  ZNITER  = ZTAB. NBITER     ;      ZNBSIGN = ZTAB. NBSIGN   ;
  ZNALEAT = ZTAB. NALEAT     ;      ZFRCOUP = ZTAB. FRCOUP   ;
  ZOPTSOR = 'MOT' ZTAB. OPTSORT    ;
*    TEST LONGUEUR DU TITRE
  'TITR' ZTITRE 'SPECTRE ITERATION NUM' ZNITER
                'MOYENNE DE'   ZNBSIGN  'SIGNAUX' ;
  'SI' ( 'NON' ( 'EXIS' ZOPTSOR )  ) ;  ZOPTS = 0 ;
  'SINON'  ;
     'SI' ( 'EGA' ZOPTSOR 'SIGNAUX' )  ;  ZOPTS = 2 ;  'FINSI' ;
     'SI' ( 'EGA' ZOPTSOR 'SPECTRE' )  ;  ZOPTS = 1 ;  'FINSI' ;
  'FINSI' ;
  ZPTLIN  = 'MOT' 'PLATLIN'    ;
  'SI' ( 'NON' ( 'EXIS'  ZTAB SIGNAL  ENVE  ) ) ;
       ZENVE = 'MOT' 'N' ;
  'SINON' ;
    ZENVE   = ZTAB. SIGNAL . ENVE     ;
      'SI' ( 'EGA' ZENVE ZPTLIN )  ;
          ZTDEB   = ZTAB. SIGNAL. TDEBUT ;
          ZTFIN   = ZTAB. SIGNAL. TFIN ;
      'FINSI' ;
  'FINSI' ;
*
*     TRANSFORMATION SI BESOIN EST DU SRO EN SRO EN DEPL
*
  ZDEPL = 'MOT' 'DEPL' ;
  'SI'  ( 'EGA' ZTYPSP ZDEPL ) ;
       ZEVSPEC = ZEVSPE1 ;
  'SINON' ;
       ZEVSPEC = 'CHSP' ZEVSPE1 'ENTR' ZTYPSP 'SORT' ZDEPL ;
  'FINSI' ;
*
  ZPI = PI ;  ZE = EXP 1. ;
*
  ZNPT = 2 '**' ZNP ; ZNPTP1 = ZNPT '+' 1 ; ZDF = 1. '/' ZDUREE;
  ZNBFRM1 = ZNPT '/' 2  ;  ZNBFRM2 = ZNBFRM1 '-' 1 ;
  ZNBFR = ZNBFRM1 '+' 1 ;
  ZDT = ZDUREE '/' ZNPT ;       ZFMAX = 0.5 '/' ZDT  ;
*            CALCUL DES TABLES DES FREQUENCES ET DES TEMPS
  ZLRFRQ0  =  'PROG'  0.     'PAS'  ZDF   'NPAS'  ZNBFRM1 ;
  ZLRFRQ   =  'PROG'  ZDF    'PAS'  ZDF   'NPAS'  ZNBFRM2 ;
* 'MESS' 'DUREE ET DT        = '  ZDUREE    ZDT    ;
  'SI' ( 'EGA' ZENVE ZPTLIN )  ;
     'MESS' 'DEBUT ET FIN DE L ENVELOPPE PLATEAU' ZTDEB ZTFIN ;
  'FINSI' ;
* 'MESS' 'FREQUENCE MAXIMALE       = '  ZFMAX  ;
* 'MESS' 'NOMBRE DE PTS DU SPECTRE = ' ZNBFRM1 ;
* 'MESS' 'NOMBRE DE PTS DU SIGNAL  = '   ZNPT  ;
*
  ZFC = ( 2. '*' ZEPS  ) '**' .5  ;
  ZFC =  ZE  '/' ( 2. '*' ZDUREE '*' ZFC ) ;
  ZNBC1 = 'ENTI' ( ZFC '/' ZDF ) ; ZNBC11 = ZNBC1 '-' 1 ;
  ZNBC2 = ZNBFR '-' ZNBC1 ;
  ZNBC21 = ZNBC2 '-' 1; ZNBC22 = ZNBC21 '-' 1 ;
  ZLRFRC1 = 'PROG' 0. 'PAS' ZDF 'NPAS' ZNBC11 ;
  ZDF1 = ( ZNBC1 '+' 1 ) '*' ZDF              ;
  ZLRFRC2 = 'PROG' ZDF1 'PAS' ZDF  'NPAS' ZNBC22  ;
*
  ZLRTEMP = 'PROG' 0. 'PAS' ZDT 'NPAS' ( ZNPT '-' 1) ;
*
*      CREATION DE L'ENVELOPPE
*
  ZTABENV1 = 'TABLE'  ;
  ZTABENV1.'TYPENVE' = ZENVE ;
  ZTABENV1.'NPT'     = ZNPT  ;  ZTABENV1.'DUREE' = ZDUREE ;
  'SI' ( 'EGA' ZENVE ZPTLIN ) ;
     ZTABENV1.'TDEBUT' = ZTDEB ; ZTABENV1.'TFIN' = ZTFIN ;
  'FINSI' ;
  ZTABENV1.'EPS'     = ZEPS  ;
*
  ZTABENV2  =  SIGNENVE    ZTABENV1      ;
*
  ZLRENV = ZTABENV2.'LRENV' ; ZLRCORR = ZTABENV2.'LRCORR' ;
  ZEVENV  = 'EVOL' 'MANU' 'TEMPS' ZLRTEMP 'ENVELOPPE' ZLRENV ;
  ZEVCORR = 'EVOL' 'MANU' 'TEMPS' ZLRFRQ  'ENVELOPPE' ZLRCORR ;
*
*      INTERPOLATION LOGARITHMIQUE SUR LE SPECTRE DE REFERENCE EN DEPL
*               AVEC ZNBFR POINTS SANS FREQUENCE NULLE
*
  ZLRFREQ = 'EXTR' ZEVSPEC 'ABSC' 1; ZLRSPEC = 'EXTR' ZEVSPEC 'ORDO' 1;
  ZLOGFRQ  = 'LOG' ZLRFRQ ; ZLOGFREQ = 'LOG' ZLRFREQ ;
  ZLOGSPEC = 'LOG' ZLRSPEC ;
  ZLRSPREF = 'IPOL'  ZLOGFRQ   ZLOGFREQ    ZLOGSPEC    ;
  ZLRSPREF = 'EXP'  ZLRSPREF  ;
  'TITR' 'SP. DE REF. APRES INTERPOLATION EN DEPL ' ;
  ZEVSPREF = 'EVOL' ROUG 'MANU' 'FREQU.' ZLRFRQ 'SPEC DE REF' ZLRSPREF ;
  'OUBLIER' ZLRSPREF  ;
******
*           CALCUL DU SPECTRE DE REFERENCE DE TYPE TYPSP POUR TRACES
  'SI'  ( 'EGA' ZTYPSP ZDEPL ) ;  ZEVSPRE1 = ZEVSPREF ;
  'SINON' ; ZEVSPRE1 = 'CHSP' ZEVSPREF 'ENTR' ZDEPL 'SORT' ZTYPSP ;
  'FINSI' ;
*          PAS DE FREQUENCES AU DELA DE FRCOUP
  ZNN = 'DIME' ZLRFRQ ;
  ZN1 = 'ENTI' ( ZFRCOUP  '/'  ZDF )  ;
  ZN2 = ZNN  '-' ZN1 ;
  ZC1 = 'PROG' ZN1 '*' 1.  ;      ZC2 = 'PROG' ZN2 '*' 0. ;
  ZLRCOUP = ZC1 'ET' ZC2 ;
  ZEVCOUP = 'EVOL' 'MANU' 'FREQU.' ZLRFRQ 'SPEC DE REF' ZLRCOUP ;
  ZEVSPREF = ZEVSPREF '*' ZEVCOUP ;
  'OUBLIER' ZLRCOUP ; 'OUBLIER' ZEVCOUP ;
*
*        CALCUL DE L' ACCELERATION MAXIMALE
*
  ZN     = 'DIME' ZLRFREQ    ;
  ZFM    = 'EXTR' ZLRFREQ ZN ;   ZSPM   = 'EXTR' ZLRSPEC ZN ;
  ZGAMMA =  ZSPM '*' 4. '*' ZPI '*' ZPI '*' ZFM '*' ZFM ;
  'MESS' 'ACCELERATION MAXIMALE  = '   ZGAMMA  ;
*
*                 CREATION DES PHASES ALEATOIRES
*
  ZTABPHI = 'TABLE' ;
  ZI = 0 ;
  'REPETER' LOOPHI ZNBSIGN ;
     ZI =  ZI '+' 1 ;
     ZNALEAT1 = ZNALEAT '+' 1 ;
     ZEVPHI = 'BRUI' 'BLAN' 'UNIF'  0. 180. ZNALEAT1 ZLRFRQ0 ;
     ZLRPHI = 'EXTR' ZEVPHI 'ORDO' 1                 ;
     'REMP' ZLRPHI 1 0.                  ;
     ZTABPHI. ZI = ZLRPHI ;
  'FIN'  LOOPHI ;
*
  ZCB = 2. '*' ZEPS ; ZCB = ZCB '**' 0.5 ; ZCB = 2. '*' ZDUREE '*' ZCB ;
  ZLRCBF1 = 'PROG'  ZNBC1 '*'  ZE ;  ZLRCBF2 = ZCB  '*'  ZLRFRC2 ;
  ZLRCBF = ZLRCBF1 'ET' ZLRCBF2 ;
*
  ZLRMU2 =  2. '*' ( 'LOG'  ZLRCBF )   ;
  'TITRE' ZTITRE 'MU 2' ;
  ZEVMU2 = 'EVOL' MANU 'ABSC' ZLRFRQ 'ORDO' ZLRMU2 ;
  'SI' ( 'NON' ( IMPII '<' 1 ) ) ;  DESS ZEVMU2 LOGX LOGY MIMA ;
  'FINSI' ;
*
  ZEVCOEFR = ZEVCORR '*' ZEVMU2 ;
  ZLRCOEFR = 'EXTR' ZEVCOEFR 'ORDO' 1 ;
  ZEVSPP   = ZEVSPREF       ;
  ZEVDSP = ZEVSPP '*' ZEVSPP '*' ZDUREE '*' ZDUREE '*' .5  '/' ZEVCOEFR;
  ZLRDSP   = 'EXTR' ZEVDSP 'ORDO' 1 ;
  ZLRTFR   = ZLRDSP  '**' 0.5  ;
      ZLRTFR = 'INSERER'    ZLRTFR 1 0.     ;
*
*                   CREATION DES SIGNAUX AVANT ITERATION
*
  ZI = 0 ;
  'REPETER' LOOP1 ZNBSIGN  ;
     ZI = ZI '+' 1 ;
     ZLRPHI = ZTABPHI. ZI ;
     ZEVTFR = 'EVOL' 'COMP' 'MOPH' 'FREQ' ZLRFRQ0 'MODU' ZLRTFR 'PHASE'
              ZLRPHI;
     'TITR' 'SIGNAL NO : ' ZI 'AVANT ITERATION' ;
     ZEVSIG = ( 'TFRI' ZEVTFR ) '*' ZEVENV   ;
    'SI' ( '>'  ZOPTS  1  ) ; DESS ZEVSIG MIMA ;  'FINSI' ;
*
    'TITR' ZTITRE ' SPECTRE AVANT ITERATION '   ;
     ZEVSPI = 'SPO' ZEVSIG 'AMOR' ( 'PROG' ZEPS ) 'FREQ' ZLRFRQ ZDEPL ;
    'SI'  ('EGA' ZI  1 ) ;   ZEVSPO = ZEVSPI ;
    'SINON' ;                ZEVSPO = ZEVSPI '+' ZEVSPO ;
    'FINSI' ;
  'FIN' LOOP1 ;
  ZEVSPO = ZEVSPO '/' ZNBSIGN ;
  'SI'  ( 'EGA' ZTYPSP ZDEPL ) ;  ZEV1 = ZEVSPO ;
  'SINON' ;  ZEV1 = 'CHSP' ZEVSPO 'ENTR' ZDEPL 'SORT' ZTYPSP ;
  'FINSI' ;
  'TITR' 'PREMIERS SPECTRES   '  ;
     ZEV = ZEV1 'ET' ZEVSPRE1 ;    'DESS' ZEV LOGX LOGY MIMA   ;
*
*                           ITERATIONS
*
  ZTEVSIG = 'TABLE' ;
  ZITER = 0 ;
  'REPETER'   BOUCL  ZNITER   ;
     ZITER = ZITER '+' 1 ;
*
*          MODIFICATION DU MODULE DE LA TFR DU PROCESSUS STATIONNAIRE
*
     ZLRRAP = 'EXTRAIRE' (  ZEVSPREF '/' ZEVSPO  )  'ORDO'  1  ;
       ZLRRAP = INSERER ZLRRAP 1 0. ;
     ZEVRAP = 'EVOL' MANU 'ABSC' ZLRFRQ0  'ORDO'  ZLRRAP ;
     ZLRTFR =  ZLRTFR  '*'  ZLRRAP ;
*
*                          CREATION DES SIGNAUX
*
     ZI = 0 ;
     'REPETER' LOOP2 ZNBSIGN  ;
        ZI = ZI '+' 1 ;
       'TITR' 'ITERATION ' ZITER  'SIGNAL' ZI ;
        ZLRPHI = ZTABPHI. ZI ;
        'TITR' ZTITRE 'ITERATION NUM : ' ZITER ' SIGNAL NUM : ' ZI ;
        ZEVTFR = 'EVOL' 'COMP' 'MOPH' 'FREQ' ZLRFRQ0 'MODU' ZLRTFR
                 'PHASE' ZLRPHI ;
        'TITR' ZTITRE 'SIGNAL NO :' ZI ' ITERATION' ZITER ;
        ZTEVSIG.ZI = ( 'TFRI' ZEVTFR ) '*' ZEVENV ;
        'SI' ( 'NON' ( IMPII '<' 1 ) ); 'DESS' ZTEVSIG.ZI MIMA; 'FINSI';
        'SI' ( '>' ZITER 1 ) ;
*                           CORRECTION DES POINTES DU SIGNAL
           ZTABCOR1 = 'TABLE' ;
           ZTABCOR1.'EVSIG1' = ZTEVSIG.ZI ; ZTABCOR1.'GAMMA' = ZGAMMA ;
           ZTABCOR2  =  SIGNCORR   ZTABCOR1 ;
           ZTEVSIG.ZI = ZTABCOR2.'EVSIG2' ;
        'FINSI' ;
*                           SIGNAL APRES CORRECTION
        'SI' ( '>' ZOPTS 1 ) ; 'DESS' ZTEVSIG.ZI 'MIMA' ; 'FINSI' ;
        'TITR' ZTITRE 'SPECTRE ITERATION NUM' ZITER
                      'MOYENNE DE' ZNBSIGN 'SIGNAUX' ;
        ZEVSPI = 'SPO' ZTEVSIG.ZI 'AMOR' ( 'PROG' ZEPS )
                 'FREQ' ZLRFRQ ZDEPL ;
        'SI'  ('EGA' ZI  1 ) ;  ZEVSPO = ZEVSPI ;
        'SINON' ;               ZEVSPO = ZEVSPI '+' ZEVSPO ;
        'FINSI' ;
     'FIN' LOOP2 ;
     ZEVSPO = ZEVSPO '/' ZNBSIGN ;
    'SI'  ( 'EGA' ZTYPSP ZDEPL ) ;  ZEV1 = ZEVSPO ;
    'SINON' ;  ZEV1 = 'CHSP' ZEVSPO 'ENTR' ZDEPL 'SORT' ZTYPSP ;
    'FINSI' ;
     ZEV   = ZEV1 'ET' ZEVSPRE1 ;  'DESS'  ZEV LOGX LOGY  MIMA   ;
*   'SI'  ( 'NEG' ZTYPSIG ZACCE ) ;
*      ZI = 0 ;
*     'REPETER' LOOP3 ZNBSIGN  ;
*         ZI = ZI '+' 1 ;
*     'FIN' LOOP3 ;
*   'FINSI' ;
FIN BOUCL   ;
'MESS' '**********   FIN   ************' ;     'TEMPS'  ;
'SINON' ;
        'SI' ( 'EGA' ZOPTION 'BLAN' )       ;
*
DELTAF = 'MOT' 'DELTAF' ; NALEAT = 'MOT' 'NALEAT' ; NP = 'MOT' 'NP' ;
*'MESS' 'LECTURE' NALEAT  NP  DELTAF ;
ZINIT = ZTAB. NALEAT  ; ZEFRQ   = ZTAB. NP ; ZDFRQ   = ZTAB. DELTAF ;
*
*TEST ZINIT EXISTE T IL
'SI' ( 'NON' ( 'EXIS' ZINIT ) )  ; ZINIT = 1 ; 'FINSI' ;
*
ZNFRQ = 2 '**' ZEFRQ ;
ZLRFREQ = 'PROG' 0.  'PAS' ZDFRQ 'NPAS'  ZNFRQ   ;
ZEVPHI = 'BRUI' 'BLAN' 'UNIF'  0. 180. ZINIT ZLRFREQ  ;
'SI' ( 'NON' ( IMPII '<' 1 ) ) ; 'DESS' ZEVPHI MIMA ; 'FINSI' ;
ZLRPHI = 'EXTR' ZEVPHI 'ORDO' 1 ;
'REMP' ZLRPHI 1 0. ;
'REMP' ZLRPHI ('DIME' ZLRPHI) 0.;
ZMOD = 1.0 '/' ( ( ( 2. '*' ZDFRQ ) '*' ( ZNFRQ '+' 1 ) )
        '**' 0.5 ) ;
ZLRMO = 'PROG' ( ZNFRQ '+' 1 ) '*' ZMOD ;
'TITR' 'BRUIT BLANC' ZTITRE ;
ZEVTFR = 'EVOL' 'COMP' 'MOPH' 'FREQ' ZLRFREQ 'MODU'  ZLRMO
                                            'PHASE' ZLRPHI ;
ZEVBB00 = 'TFRI' ZEVTFR;
*
*               NORMALISATION DU SIGNAL(ECART-TYPE= 1)
*
ZLRDSP0 = 'DSPR' ( ZEFRQ '+' 1 )  ZEVBB00 ;
'SI' ( 'NON' ( IMPII '<' 1 ) ) ; 'DESS' ZLRDSP0 MIMA ; 'FINSI' ;
ZLCOEF = ( 'INTG' ZLRDSP0 ) '**' 0.5 ; ZCOEF = 'EXTR' ZLCOEF 1 ;
ZTEVSIG = ZEVBB00 '/'  ZCOEF  ;
*
*******************************************************************
        'SINON'   ;
         ERREUR ' MOT CLE NON PREVU ACTUELLEMENT ' ;
        'FINSI' ;
'FINSI'         ;
***********************************************************************
'FINPROC'  ZTEVSIG   ;


