$$$$ SHFDT
* SHFDT     PROCEDUR  WANG      16/01/25    21:15:06     8799

DEBPROC SHFDT TABDON ;
************************************************************************
*    CALCUL DE FONCTION DE TRANSFERT D'UN CHAMP D'ONDE SH INCLINEE
*    PAR RAPPORT A UN POINT DE REFERENCE
*
*    TABDON : TABLE D'ENTREE AVEC LES INDICES SUIVANTES :

*      PDC*POINT: POINT DE CONTROLE A LA SURFACE DU SOL
*      DIR*MOT: DIRECTION DU MOUVEMENT DES PARTICULES DE SOL
*               DIR = UX : ONDE SH SE PROPAGEANT DANS LE PLAN YZ
*               DIR = UY : ONDE SH SE PROPAGEANT DANS LE PLAN XZ
*      ANGLE*FLOTTANT: ANGLE D'INCIDENCE PAR RAPPORT A L'AXE OZ
*      VS*FLOTTANT: VITESSE DE PROPAGATION D'ONDE S
*      PDF*POINT: POINT DE CIBLE QUELCONQUE DANS LE SOL
*      KSI*FLOTTANT: TAUX D'AMORTISSEMENT DU SOL, CONSTANT PAR DEFAUT,
*                    VARIABLE SELON LE MODELE DE RAYLEIGH SI F1 ET F2
*                    EXISTENT
*      F1*FLOTTANT, F2*FLOTTANT: FREQUENCES POUR LE MODELE DE RAYLEIGH
************************************************************************

ANGLE = TABDON.ANGLE ;
VS    = TABDON.VS ;
KSI   = TABDON.KSI ;
LF    = TABDON.LF ;

F0 = EXTR LF 1 ;
SI (F0 < 1E-5) ;
I_ENLE = 1 ;
LF = ENLE LF 1;
FINSI ;

LOMEG = 2*PI*LF ;
NF    = DIME LF ;
L1    = PROG NF * 1.0 ;
LKSI  = PROG NF * KSI ;

SI ((EXIS TABDON F1) ET (EXIS TABDON F2)) ;
OM1 = 2*PI*TABDON.F1 ;
OM2 = 2*PI*TABDON.F2 ;
A0  = 2*KSI*OM1*OM2/(OM1 + OM2) ;
A1  = 2*KSI/(OM1 + OM2) ;
LKSI = (A0*L1 / (2*LOMEG)) + (A1*LOMEG/2.) ;
FINSI;

EV1 = EVOL COMP REIM LF L1 (2*LKSI) ;
RIMP EV1 ;

* Calcul de la racine carre de EV1

L2  = (EXTR EV1 ORDO 1)**0.5 ;
L3  = (EXTR EV1 ORDO 2)/2 ;
EV2 = EVOL COMP MOPH LF L2 L3 ;

RIMP EV2 ;
EVVS = VS*EV2 ;

L0     = PROG NF * 0.0 ;
EVI    = EVOL COMP REIM LF L0 L1 ;
EVOMEG = EVOL COMP REIM LF LOMEG L0 ;

X0 Y0 Z0 = COOR TABDON.PDC ;
X1 Y1 Z1 = COOR TABDON.PDF ;

DX = X1 - X0 ;
DY = Y1 - Y0 ;
DZ = Z1 - Z0 ;

SI (EGA TABDON.DIR 'UX') ;

EVA = -1*EVI*EVOMEG*DY*(SIN ANGLE)/EVVS ;
EVB = -1*EVI*EVOMEG*DZ*(COS ANGLE)/EVVS ;
EVC =    EVI*EVOMEG*DZ*(COS ANGLE)/EVVS ;

SINON;
  SI (EGA TABDON.DIR 'UY') ;
  EVA = -1*EVI*EVOMEG*DX*(SIN ANGLE)/EVVS ;
  EVB = -1*EVI*EVOMEG*DZ*(COS ANGLE)/EVVS ;
  EVC =    EVI*EVOMEG*DZ*(COS ANGLE)/EVVS ;
  FINSI ;
FINSI ;

TITR 'EVA1' ;
LMOA = EXP (EXTR EVA ORDO 1) ;
LPHA = 180*(EXTR EVA ORDO 2)/PI ;
EVA1 = EVOL COMP MOPH LF LMOA LPHA ;
RIMP EVA1 ;

TITR 'EVB1' ;
LMOB = EXP (EXTR EVB ORDO 1) ;
LPHB = 180*(EXTR EVB ORDO 2)/PI ;
EVB1 = EVOL COMP MOPH LF LMOB LPHB ;
RIMP EVB1 ;

TITR 'EVC1' ;
LMOC = EXP (EXTR EVC ORDO 1) ;
LPHC = 180*(EXTR EVC ORDO 2)/PI ;
EVC1 = EVOL COMP MOPH LF LMOC LPHC ;
RIMP EVC1 ;

TITR 'FDT ONDE SH' ;
FDTSH = (EVA1 * (EVB1 + EVC1))/2. ;
RIMP FDTSH ;

SI (EGA I_ENLE 1) ;
LF    = EXTR FDTSH ABSC   ;
LMO   = EXTR FDTSH ORDO 1 ;
LPH   = EXTR FDTSH ORDO 2 ;
LF    = (PROG F0) ET LF ;
LMO   = (PROG (EXTR LMO 1 )) ET LMO ;
LPH   = (PROG (EXTR LPH 1 )) ET LPH ;
FDTSH = EVOL COMP MOPH LF LMO LPH ;
FINSI ;

FINPROC FDTSH ;
************************************************************************
$$$$


