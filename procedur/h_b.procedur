$$$$ H_B
* H_B       PROCEDUR  BAZE      97/01/30    21:15:07     2468
 'DEBPROC' H_B MU0*'FLOTTANT'  EVO1/'EVOLUTION'  F/'FLOTTANT'  ;
*************************************************************************
*     definition de la courbe  de permeabilite magnetostatique 2d 3d    *
*                                                                       *
*   ENTREES :
*           MU0   permeabilité du vide       ( S.I.  4 pi 10-7 )        *
*           EVO1  évolution    abscisse  H  ordonne  B                  *
*           F   Packing factor     1 par defaut
*************************************************************************
*   SORTIES :
*                                                                       *
*    MU(H)   3d methode a 2 potentiels scalaires                        *
* 1./MU(B)   2d plan ou axi potentiel vecteur                           *
*                                                                       *
*                                                                       *
*************************************************************************

 'SI' ('NON'  ('EXISTE'  EVO1 )) ;
  TTTT = 'B(H)__STANDARD'  ;
  MESS '***********************************************************';
  MESS ' ON UTILISE UNE COURBE B(H) STANDARD  TESLA-AMPERE/METRE ' ;
  MESS '***********************************************************';
 B= 'PROG' 0. .8 1.2 1.4 1.5  'PAS' .05 2.15 2.175 2.2 2.25 2.28 2.3
 2.3443
 2.3996 2.4905 2.5627 2.6706 2.8498 3.2074 3.5644 4.2782 4.8134 5.7052
 6.4186 7.4887 17.48 27.48   ;
 H= 'PROG' 0. 159.2 294.4 501.3 795.8 1154. 1795. 2862. 4383. 6044.
 8122.
 10590. 13610. 17220. 21170. 26750. 33760. 43800. 52440. 66000. 99470.
 120960. 141210. 169600. 212170. 283130. 339890. 425040. 566950.
 850760.
 1134600. 1702300. 2128000. 2837700. 3405100. 4256700. 12215578.
  20174457. ;
 'SINON' ;
  TTTT = 'B(H)__UTILISATEUR'  ;
 H = 'EXTR' EVO1 'ABSC'  ; B = 'EXTR' EVO1 'ORDO'  ;
 'FINSI' ;
  'SI' ('NON' ('EXISTE' F)) ; F= 1. ;'FINSI' ;

  TUTU = CHAIN TTTT '_______PACKING   '  F  ;
  'MESS' '********************************************' ;
  'MESS'   TUTU ;
  'MESS' '********************************************' ;
   NN = DIME H ;
 H_MAX = MAXI H ;
 B_MAX = MAXI B ;
 B_EXT = 5.e4  ;  H_EXT = H_MAX + (( B_EXT - B_MAX)/MU0 ) ;
 H =  H ET ( 'PROG' H_EXT) ;
 B =  B ET ( 'PROG' B_EXT) ;
   'SI' (f < 1.) ;
     ip = 1 ;
   BRED = PROG 0. ;
       'REPETER' BUB  nn   ;
     ip = ip + 1 ;
     Bi = EXTR B IP ; Hi = EXTR H IP ; MUI = Bi/Hi ;
     MURED =( MU0 * ( 1. - f )) +  (MUI *  f )  ;
     BRED =  BRED et (PROG ( Hi * MURED)) ;
     'FIN' BUB ;
  B = BRED ;
  'FINSI' ;

  BHEVO=EVOL MANU  'H'   H   'B' B ;
*  DESS BHEVO;
 H_1=  'ENLEVER' H  1 ;B_1= 'ENLEVER' B 1  ;
 MUV= H_1 / B_1;TU= 'EXTRA' MUV 1;MUV= ( 'PROG' TU )  'ET' MUV ;
  IFOR = 'VALE' DIME ;
 'SI' ('EGA' IFOR   3 );
 BOBO= 'TEXTE'   ' MU(H)______POTENTIEL_SCALAIRE' ;
 'TITRE' BOBO ;
 REVOL = 'EVOL' MANU 'TEMPERATURE ' H   'CONDUCTIVITE ' ( MUV ** -1.) ;
  'SINON' ;
 BOBO= 'TEXTE'   ' MU(B)______POTENTIEL_VECTEUR ';
 'TITRE' BOBO ;
 REVOL='EVOL' 'MANU' 'TEMPERATURE' B 'CONDUCTIVITE'  MUV   ;
 'FINSI' ;
*
 'SAUTER' 1 'LIGNE' ;
 'MESS' '********************************************' ;
 'MESS' BOBO ;
 'MESS' '********************************************' ;
 'SAUTER' 1 'LIGNE' ;
 'FINPROC' REVOL ;


