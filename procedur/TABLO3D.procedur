$$$$ TABLO3D
* TABLO3D   PROCEDUR  JC220346  16/05/10    21:15:00     8926
************************************************************************
* NOM         : TABLO3D
* DESCRIPTION : Affiche sous forme graphique 3D (matrice de barres
*               colorees) un tableau de valeurs numeriques.
************************************************************************
* ENTREES : LO    [MOT]      = MOT-CLE VALANT LINE, LOGA, CLOG OU ZLOG
*           SMALL [FLOTTANT] = VALEUR EN-DESSOUS DE LAQUELLE LV VAUT 0.
*           TR    [LOGIQUE]  = TRACER UNE TRIANGLE SUR LA 1ERE BARRE ?
*           NL    [ENTIER]   = NOMBRE DE LIGNES
*           NC    [ENTIER]   = NOMBRE DE COLONNES
*           LV    [LISTREEL] = LISTE DES NL*NC VALEURS
*           TIT   [MOT]      = TITRE DU GRAPHIQUE
* SORTIES : AUCUNE
************************************************************************
* SYNTAXE :
*
*            TABLO3D | ('LINE') | (ZERO) NLIG NCOL LVAL (TIT) ;
*                    |  'LOGA'  |
*                    |  'CLOG'  |
*                    |  'ZLOG'  |
*
************************************************************************
DEBP TABLO3D LO/'MOT' SMALL*'FLOTTANT' TR/'LOGIQUE'
             NL*'ENTIER' NC*'ENTIER' LV*'LISTREEL' TIT/'MOT' ;

*  MODIFICATION DES OPTIONS
   NDIM1 = VALE 'DIME' ;
   ELTY1 = VALE 'ELEM' ;
   ECHO1 = VALE 'ECHO' ;
   SI (NDIM1 EGA 1) ;
      ERRE 709 'AVEC' NDIM1 ;
   FINS ;
   OPTI 'DIME' 3 'ELEM' 'CUB8' 'ECHO' -1 ;

*  ===================
*  PARAMETRES D'ENTREE
*  ===================

*  GESTION DE L'ECHELLE
   KCLOG = FAUX ;
   KZLOG = FAUX ;
   MCOMP = 'X' ;
   MOECH = MOTS 'LINE' 'LOGA' 'CLOG' 'ZLOG' ;
   SI (EXIS LO) ;
      ILO = POSI LO 'DANS' MOECH ;
      SI (ILO > 0) ;
         SI ((ILO EGA 2) OU (ILO EGA 3)) ;
            KCLOG = VRAI ;
            MCOMP = '10^X' ;
         FINS ;
         SI ((ILO EGA 2) OU (ILO EGA 4)) ;
            KZLOG = VRAI ;
         FINS ;
      SINON ;
         SI (EXIS TIT) ;
            ERRE 1052 'AVEC' 'LINELOGACLOGZLOG' ;
         SINON ;
            TIT = LO ;
         FINS ;
      FINS ;
   FINS ;

*  GESTION DE LA VALEUR MINI
   MIN1 = 0.1 * (MINI (ABS LV)) ;
   SI (KCLOG OU KZLOG) ;
      SI (EXIS SMALL) ;
         SMLOG = (LOG SMALL) / (LOG 10.) ;
      SINON ;
         SMLOG = ENTI 'INFE' ((LOG MIN1) / (LOG 10.)) ;
         SMALL = 10.**SMLOG ;
      FINS ;
   SINON ;
      SI (NON (EXIS SMALL)) ;
         SMALL = MIN1 ;
      FINS ;
   FINS ;

*  TRACE DU MARQUEUR DE LA POSITION (1,1) ?
   SI (NON (EXIS TR)) ;
      TR = VRAI ;
   FINS ;

*  GESTION DE LA LISTE DES VALEURS
   NV = DIME LV ;
   SI (NV EGA 0) ;
      ERRE 1027 'AVEC' 'LISTREEL' ;
   FINS ;
   SI (NL <EG 0) ;
      ERRE 36 'AVEC' NL ;
   FINS ;
   SI (NC <EG 0) ;
      ERRE 36 'AVEC' NC ;
   FINS ;
   SI (NV NEG (NL*NC)) ;
      ERRE 199 ;
   FINS ;

*  TITRE DU GRAPHIQUE
   SI (NON (EXIS TIT)) ;
      TIT = ' ' ;
   FINS ;


*  =====================
*  CREATION DU GRAPHIQUE
*  =====================

   MAIL1 = (0. NL 0.) DROI NC (NC NL 0.) TRAN NL (0. (-1*NL) 0.) ;
   MAIL2 = MAIL1 ELEM 1 ;
   MAIL2 = CHAN 'POI1' (HOMO MAIL2 0.9 (BARY MAIL2)) ;
   MAIL2 = MANU 'TRI3' (MAIL2 POIN 1) (MAIL2 POIN 4) (BARY MAIL2) ;
   MACU8 MAQU4 = VIDE 'MAILLAGE'/'CUB8' 'MAILLAGE'/'QUA4' ;
   LVCU8 LVQU4 = VIDE 'LISTREEL'*2 ;
   REPE K NV ;
      VV = EXTR LV &K ;
      SS = SIGN VV ;
      VA = ABS VV ;
      EL = MAIL1 ELEM &K ;
      SI KCLOG ;
         VC = (LOG VA) / (LOG 10.) ;
      SINON ;
         VC = VV ;
      FINS ;
      SI (VA > SMALL) ;
         SI KZLOG ;
            VA = ((LOG VA) / (LOG 10.)) - SMLOG ;
         SINON ;
            VA = VA / SMALL ;
         FINS ;
         MACU8 = MACU8 ET (EL VOLU 'TRAN' 1 (0. 0. (SS*VA))) ;
         LVCU8 = LVCU8 ET VC ;
         SI (&K EGA 1) ;
            MAIL2 = MAIL2 ET (MAIL2 PLUS (0. 0. (SS*VA))) ;
            VMA2 = VC ;
         FINS ;
      SINON ;
         MAQU4 = MAQU4 ET EL ;
         LVQU4 = LVQU4 ET VC ;
         SI (&K EGA 1) ;
            VMA2 = VC ;
         FINS ;
      FINS ;
   FIN K ;
   ELIM MACU8 SMALL ;

*  AFFICHAGE DES DONNEES
   $MACU8 = MODE MACU8 'THERMIQUE' 'ISOTROPE' ;
   $MAQU4 = MODE MAQU4 'THERMIQUE' 'ISOTROPE' ;

   CHCU8 = MANU 'CHML' $MACU8 'REPA' MCOMP LVCU8 'GRAVITE' ;
   CHQU4 = MANU 'CHML' $MAQU4 'REPA' MCOMP LVQU4 'GRAVITE' ;

   $MAIL = $MACU8 ET $MAQU4 ;
   CHML2 = CHCU8 ET CHQU4 ;

   SI TR ;
      $MAIL2 = MODE MAIL2 'THERMIQUE' 'ISOTROPE' ;
      LVMA2 = PROG (NBEL MAIL2)*VMA2 ;
      CHMA2 = MANU 'CHML' $MAIL2 'REPA' MCOMP LVMA2 'GRAVITE' ;

      $MAIL = $MAIL ET $MAIL2 ;
      CHML2 = CHML2 ET CHMA2 ;
   FINS ;

   TRAC $MAIL CHML2 (1.E6 -1.2E6 0.9E6) 'TITR' TIT ;

*  RETABLISSEMENT DES OPTIONS D'ORIGINE
   SI (NEG ELTY1 '    ') ;
      OPTI 'ELEM' ELTY1 ;
   FINS ;
   OPTI 'DIME' NDIM1 'ECHO' ECHO1 ;

FINP ;
$$$$


