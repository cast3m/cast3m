$$$$ @COUPLE
* @COUPLE   PROCEDUR  CHAT      96/01/03    21:17:53     1971
************************************************************************
*
*   PROCEDURE  @COUPLE                  MLN/CERN JANVIER 91
*
*
*   LISSAGE D'UNE LISSE PAR LA METHODE D'ENERGIE MINIMUM DE
*   DEFORMATION
*
*-----------------------------------------------------------------------
*
*   ENTREE
*
*
*    EVO_1            Evolution dans le plan YOZ
*
*    EVO_2            Evolution dans le plan XOZ
*
*    ENT_1            Entier = 1 ,le couple est dans le plan XOZ
*                              2 ,le couple est dans le plan YOZ
*
*
*-----------------------------------------------------------------------
*
*   SORTIE
*
*    COU_1            Couple 3D
*
*
*-----------------------------------------------------------------------
'DEBP' @COUPLE  EVO_1*'EVOLUTION' EVO_2*'EVOLUTION' ENT_1*'ENTIER' ;
'TITRE' ' COUPLE' ;
'OPTI' 'DIME' 3 'ELEM' 'SEG2' 'MODE' 'TRIDIM' ;
'SI' ('EGA' ENT_1 1) ;
EVO_1X = 'EXTR' EVO_2 'ABSC' ;
EVO_1Y = 'EXTR' EVO_1 'ABSC' ;
EVO_1Z = 'EXTR' EVO_2 'ORDO' ;
'FINSI' ;
'SI' ('EGA' ENT_1 2) ;
EVO_1X = 'EXTR' EVO_2 'ABSC' ;
EVO_1Y = 'EXTR' EVO_1 'ABSC' ;
EVO_1Z = 'EXTR' EVO_1 'ORDO' ;
'FINSI' ;
NBP = 'DIME' EVO_1X ;
I = 0 ;
PX = 'TABLE' ;
PY = 'TABLE' ;
PZ = 'TABLE' ;
PP = 'TABLE' ;
'REPE' BOU_1 NBP ;
I =  I + 1 ;
PX.I =  ('EXTR' EVO_1X I) ;
PY.I =  ('EXTR' EVO_1Y I) ;
PZ.I =  ('EXTR' EVO_1Z I) ;
PP.I = (PX.I PY.I PZ.I) ;
'SI' ('EGA' 2 I) ;
    MAI_1 = (PP.(I - 1) 'DROI' 1 PP.I) ;
'FINSI' ;
'SI' (I >  2) ;
    MAI_1 = MAI_1 'ET' (PP.(I - 1) 'DROI' 1 PP.I) ;
'FINSI' ;
'FIN' BOU_1 ;
CON_1 = 'MAXI' ('ABS' EVO_1Z) ;
CON_1 = CON_1 / 10. ;
O = 0. 0. 0. ;
O1 = 1E5 1E5 1E5 ;
X = O 'DROI' 1 (CON_1 0. 0.) ;
Y = O 'DROI' 1 (0. CON_1 0.) ;
Z = O 'DROI' 1 (0. 0. CON_1) ;
X = X 'COUL' 'VERT' ;
Y = Y 'COUL' 'VERT' ;
Z = Z 'COUL' 'VERT' ;
MAI_1 = MAI_1 'COUL' 'ROUG' ;
'TRAC' O1 'QUAL' (X ET Y ET Z ET MAI_1) ;
*
'FINPROC' MAI_1 ;
$$$$

