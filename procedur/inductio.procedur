$$$$ INDUCTIO
* INDUCTIO  PROCEDUR  FANDEUR   14/10/10    21:15:16     8178
 'DEBPROC' INDUCTIO GEO*'MAILLAGE'  SOL1*'CHPOINT'  AXI*'LOGIQUE';
******************************************************************
*  estimation de B aux points de Gauss et puis chan chpo
*                 2D  UNIQUEMENT
*          calcul; de l induction  en potentiel vecteur        *
*          GEO  maillage sur lequel on recherche B             *
*          SOL1   solution en potentiel vecteur                *
*          AXI    logique  vrai si axi                         *
****************************************************************
 OBJ0 = GEO 'MODE' 'THERMIQUE' 'ISOTROPE' ;
 DERIV   = 'GRAD' OBJ0 SOL1 ;
 BXX= ('EXCO' 'T,Y' DERIV   'SCAL' )    ;
 BYY= ('EXCO' 'T,X' DERIV   'SCAL' )    ;
 BXX = BXX 'CHAN' 'TYPE' 'SCALAIRE';
 BYY = BYY 'CHAN' 'TYPE' 'SCALAIRE';
              'SI'   AXI   ;
 'MESS' '*  axisymetrique   '   ;
 1SRAY = (COOR 1 DERIV ) ** -1. ;
 BXX=EXCO ( BXX  * 1SRAY * -1. ) 'SCAL' 'BX' ;
 BYY=EXCO ( BYY  * 1SRAY       ) 'SCAL' 'BY' ;
 BXX = BXX 'CHAN' 'TYPE' 'SCALAIRE';
 BYY = BYY 'CHAN' 'TYPE' 'SCALAIRE';
              'SINON' ;
'MESS' '*  probleme plan   ';
 BXX =EXCO  BXX 'SCAL'    'BX' ;
 BYY =EXCO (BYY * -1. ) 'SCAL' 'BY'  ;
 BXX = BXX 'CHAN' 'TYPE' 'SCALAIRE';
 BYY = BYY 'CHAN' 'TYPE' 'SCALAIRE';
              'FINSI' ;
 BTOT = BXX + BYY  ;
 BTOT = 'CHAN' 'CHPO' BTOT OBJ0 ;
 'FINPROC' BTOT  ;




