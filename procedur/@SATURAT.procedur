$$$$ @SATURAT
* @SATURAT  PROCEDUR  PASCAL    08/10/06    21:15:05     6176
* PROCEDURE @SATURAT
*---------------------------------------------------------------------*
* NOM         : @SATURAT                                              *
*                                                                     *
* DESCRIPTION :                                                       *
*                                                                     *
* SYNTAXE     :                                                       *
*                                                                     *
*                                                                     *
* LANGAGE     : GIBIANE-CAST3M                                        *
* AUTEUR      : S. PASCAL (CEA/DEN/DM2S/SEMT/LM2S)                    *
* COURRIEL    : serge.pascal@cea.fr                                   *
*---------------------------------------------------------------------*
* VERSION    : v1, xx/xx/xx08, version initiale                       *
* HISTORIQUE : v1, xx/xx/xx08, creation                               *
* HISTORIQUE :                                                        *
* HISTORIQUE :                                                        *
*---------------------------------------------------------------------*
* Priere de PRENDRE LE TEMPS de completer les commentaires            *
* en cas de modification de ce sous-programme afin de faciliter       *
* la maintenance !                                                    *
*---------------------------------------------------------------------*
'DEBP' @SATURAT ;
'ARGU' CHTK1*'CHPOINT' CHPC1*'CHPOINT' ;
* Ok !
* ENTREES                                                             *
*  - Temperature (K)                                                  *
*  - Pression capillaire (Pa)                                         *
* SORTIE                                                              *
*  - Saturation (-) : SW0                                             *
* PARAMETRES                                                          *
* |-------------|---------|---------|---------|-----------|
* |coefficient  | BO      |   BHP   |   MO    |     MH    |
* ||------------|---------|---------|---------|-----------|
* |a[MPa]       |18.6237  |46.9364  |37.5479  |96.2837    |
* |-------------|---------|---------|---------|-----------|
* |b[-]         |2.2748   |2.0601   |2.1684   |1.9540     |
* |-------------|---------|---------|---------|-----------|
* Q2       : parametre de calcul du parametre "a", relatif a T>Tcrit  *
* Q3       : parametre de calcul du parametre "a", relatif a T=20Â°C   *
* PB1      : exposant "b"                                             *
* GN1      : exposant "N" de E(theta)                                 *
* PZ1      : parametre "z" de E(theta) qd T > TCRI1                   *
* E0       : valeur inconnue => 1. !                                  *
* TREF1    : Temperature (K) "ambiante" (initiale ?)                  *
* TSAT1    : Temperature (K) au-dessus de laquelle le param. "a" vari *
* TCRI1    : Temperature (K) critique                                 *
Q2       = 7.E6 ;
Q3       = 18.62E6 ;
PB1      = 2.27 ;
GN1      = 1.2 ;
PZ1      = 0.5 ;
E0       = 1. ;
TREF1    = 293.15 ;
TSAT1    = 373.15 ;
TCRI1    = 647.15 ;
*                                                                     *
* Calcul du parametre "a" : PA0                                       *
Q0       = (CHTK1 - TSAT1) / (TCRI1 - TSAT1) ;
Q02      = -3. * (Q0 ** 2) ;
Q03      =  2. * (Q0 ** 3) ;
Q0       = (Q3 - Q2) * (1. + Q02 + Q03) ;
MASQI1   = CHTK1 'MASQ' 'EGIN' TSAT1 ;
MASQS1   = CHTK1 'MASQ' 'SUPE' TSAT1 ;
PA0      = (Q3 * MASQI1) + ((Q0 + Q2) * MASQS1) ;
*                                                                     *
* Calcul de E(theta) : ETHETA0                                        *
MASQI2   = CHTK1 'MASQ' 'INFE' TCRI1 ;
MASQS2   = CHTK1 'MASQ' 'EGSU' TCRI1 ;
CHTKSC1  = MASQI2 * CHTK1 ;
ETHETA1  = (((TCRI1 - CHTKSC1) ** -1) * (TCRI1 - TREF1)) ** GN1 ;
ETHETA2  = GN1 / PZ1 * (CHTK1 - TCRI1 + PZ1) ;
ETHETA2  = E0 * (1. + ETHETA2) ;
* ATTENTION : jeux de donnees SdP ne distinguent pas T>TSAT1...       *
*ETHETA0  = MASQI1 + (ETHETA1 * MASQI2 * MASQS1) + (ETHETA2 * MASQS2) ;
ETHETA0  = ETHETA1 ;
*                                                                     *
* La saturation :                                                     *
M1SPB1   = -1. / PB1 ;
PA0      = PA0 ** -1 ;
SW0      = (ETHETA0 * PA0 * CHPC1) ** (1. / (1. + M1SPB1)) + 1. ;
SW0      = SW0 ** M1SPB1 ;
*                                                                     *
'RESP' SW0 ;
*                                                                     *
'FINP' ;
*---------------------------------------------------------------------*
* FIN PROCEDURE @SATURAT
$$$$

