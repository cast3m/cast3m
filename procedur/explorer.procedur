$$$$ EXPLORER
* EXPLORER  PROCEDUR  PASCAL    16/10/13    21:15:09     9125
DEBPROC  EXPLORER ;
*=======================================================================
*
* PROCEDURE DONT LE BUT EST D'EXPLORER LES RESULTATS ISSUS DE :
* - PASAPAS
* - VIBR (ajout bp, 2016-02)
*
* SYNTAXE :
* EXPLORER MATAB | (LMOC1) | (TOPT1) ;
*                |  'CHAR' |
* avec :
* -MATAB : table de resultats issues de PASAPAS ou de VIBR
* -CHAR  : mot-cle si l'on souhaite visualiser le chargement
* -LMOC1 : mots-cles definissant les actions a produire
* -TOPT1 : table d'options
*
*=======================================================================

MESS '==============   DEBUT DE LA PROCEDURE EXPLORER   ==============';

*----------------------------------------------------------------------*
*          RECUP et VERIF DES DONNEES D'ENTREE                         *
*----------------------------------------------------------------------*

ARGU MATAB*'TABLE' ;
ARGU MOT1/'MOT' ;
ARGU LMOC1/'LISTMOTS' ;
ARGU TOPT1/'TABLE' ;

* quelques logiques :
FLMOT1 = EXIS MOT1;
FLMOC1 = EXIS LMOC1;
FLOPT1 = EXIS TOPT1;
FLTRACX= EGA (VALE 'TRAC') 'X';
* Le choix est interactif si pas de mot-cles et trace X
FLCHOI = FLTRACX ET (NON FLMOC1);


* Quel est le type de resultats a traiter?
SI (EXIS MATAB 'SOUSTYPE');
  TYPTAB = MOT MATAB . 'SOUSTYPE';
SINON;
  MESS '*  SOUSTYPE DE LA TABLE INCONNU :';
  MESS '*  => ON SUPPOSE QU IL S AGIT D UNE TABLE PASAPAS';
  TYPTAB = MOT 'PASAPAS';
FINSI;


*----------------------------------------------------------------------*
*                   CAS D'UNE TABLE ISSUE DE VIBR                      *
*----------------------------------------------------------------------*

SI (    (EGA TYPTAB 'BASE_MODALE')
     OU (EGA TYPTAB 'LIAISONS_STATIQUES')
     OU (EGA TYPTAB 'BASE_DE_MODES') );
*  on teste l'existence des arguments
   SI (NON FLMOC1);
     LMOC1 = MOTS 'TABL' 'DEFO' 'DEF0' 'MAIL';
   FINSI;
   SI FLOPT1;
     POSTVIBR MATAB LMOC1 TOPT1;
   SINON;
     POSTVIBR MATAB LMOC1;
   FINSI;
   QUITTER EXPLORER;
FINSI;


* ON VERIFIE LE TYPE AVANT DE POURSUIVRE
SI (NEG TYPTAB 'PASAPAS');
  MESS '* TABLE DE SOUSTYPE PASAPAS, BASE_MODALE OU LIAISONS_STATIQUES';
  MESS '* ATTENDUE EN ENTREE D EXPLORER !';
  ERRE 21;
FINSI;


*----------------------------------------------------------------------*
*                   CAS D'UNE TABLE ISSUE DE PASAPAS                   *
*----------------------------------------------------------------------*

* Si MATAB ne contient pas de resultat ==> proposer option CHAR :      *
IDEPL1   = EXIS MATAB 'DEPLACEMENTS' ;
ISIGM1   = EXIS MATAB 'CONTRAINTES' ;
ITHER1   = EXIS MATAB 'TEMPERATURES' ;
SI IDEPL1 ;
  IDEPL1   = (DIME MATAB . 'DEPLACEMENTS') '>' 1 ;
FINSI ;
SI ISIGM1 ;
  ISIGM1   = (DIME MATAB . 'CONTRAINTES' ) '>' 1 ;
FINSI ;
SI ITHER1 ;
  ITHER1   = (DIME MATAB . 'TEMPERATURES') '>' 1 ;
FINSI ;
IRESU1   = (IDEPL1 ET ISIGM1) 'OU' ITHER1 ;
*
MESG00   = ' ' ;
SI (NON IRESU1) ;
  SI (NON (EXIS MOT1)) ;
    MOT1     = MOT 'CHAR' ;
    MESG00   = ' PAS DE RESULTAT => option CHAR : ' ;
  FINSI ;
FINSI ;


*----------------------------------------------------------------------*
*              SOUS-CAS DU CHARGEMENT D'UNE TABLE PASAPAS              *
*----------------------------------------------------------------------*

SI (EGA MOT1 'CHAR') ;

* Le choix est interactif si pas de table d'options et trace X
  FLCHOI = FLTRACX ET (NON FLOPT1);

  MOD1     = MATAB . 'MODELE' ;
  CGT0     = MATAB . 'CHARGEMENT' ;
  MAIL1    = EXTR MOD1 'MAIL' ;
*
*  Boucle principale :
  LCHAR1   = EXTR CGT0 'COMP' ;
  REPE BPRIN1 ;
*
*   Choix du chargement a vualiser :
    SI FLCHOI;
*     en interactif
      TRAC 'NCLK' MAIL1 ;
      MESG0    = CHAI MESG00 'Choisissez le chargement a visualiser ';
      MRET1    = MENU MESG0 LCHAR1 ;
      MESG00   = ' ' ;
      SI (EGA MRET1 'Quitter')
        QUITTER BPRIN1 ;
      FINSI ;
    SINON;
*     par defaut
      MRET1 = EXTR LCHAR1 1;
*     par une table d option
      SI FLOPT1;
        SI (EXIS TOPT1 'TYPE');
           MRET1 = TOPT1 . 'TYPE';
        FINSI;
      FINSI;
    FINSI;
    CGI1     = EXTR CGT0 MRET1 ;

*   Petit travail si chargement est defini par 2 tables
    OPTI 'ERRE' 'CONT' ;
    EVI1     = EXTR CGI1 'EVOL' ;
    OPTI 'ERRE' 'NORM' ;
    SI (EGA (TYPE EVI1) 'ANNULE') ;
      TX1 TY1  = EXTR CGI1 MRET1 'TABL' ;
      LI1      = PROG ;
      REPE BX1 (DIME TX1) ;
        LI1     = LI1 ET (PROG TX1 . (&BX1 - 1)) ;
      FIN BX1 ;
    SINON ;
      LI1      = EXTR EVI1 'ABSC' ;
    FINSI ;
*
*   Traitement CHPOINT / MCHAML
    I2       = 1 ;
    TI2      = EXTR LI1 I2 ;
    CH2      = TIRE CGI1 MRET1 TI2 ;
    SI (EGA (TYPE CH2 ) 'MCHAML') ;
      MM1      = MOD1 ;
    SINON ;
      SI (EGA (TYPE CH2 ) 'CHPOINT') ;
        MM1      = MAIL1 ;
      SINON ;
        ERRE ' On attend un MCHAML ou un CHPOINT ' ;
      FINSI ;
    FINSI ;

*   composante
    LCOMP1   = EXTR CH2  'COMP' ;
    LCOMP2   = LCOMP1 ;
    MCOMP1   = EXTR LCOMP1 1 ;
    SI FLOPT1;
      SI (EXIS TOPT1 'COMP');
         MCOMP1 = TOPT1 . 'COMP';
      FINSI;
    FINSI;

*   Boucle sur les instants du chargement
    NLI1     = DIME LI1 ;
    REPE BINST1 ;
*
*     Traitement de l'echelle d'isovaleur
      XMIN1    = 'MINI' CH2  'AVEC' ('MOTS' MCOMP1) ;
      XMAX1    = 'MAXI' CH2  'AVEC' ('MOTS' MCOMP1) ;
      REPE BLEG1 NLI1 ;
        TI3      = EXTR LI1 &BLEG1 ;
        CHI3     = TIRE CGI1 MRET1 TI3 ;
        XMINI1   = 'MINI' CHI3 'AVEC' ('MOTS' MCOMP1) ;
        XMAXI1   = 'MAXI' CHI3 'AVEC' ('MOTS' MCOMP1) ;
        SI (XMINI1 '<' XMIN1) ; XMIN1 = XMINI1 ; FINSI ;
        SI (XMAXI1 '>' XMAX1) ; XMAX1 = XMAXI1 ; FINSI ;
      FIN BLEG1 ;
      XINT1    = XMAX1 - XMIN1 ;
      XPAS1    = XINT1 / 18. ;
      SI (XPAS1 NEG 0.) ;
        EE1      = ENTI 'INFE' (('LOG' XPAS1) / ('LOG' 10.)) ;
        YPAS1    = ENTI 'INFE' (XPAS1 / (10. ** EE1)) ;
        YPAS1    = YPAS1 * (10. ** EE1) ;
        NN1      = ENTI 'INFE' (XMIN1 / YPAS1) ;
        YMIN1    = NN1 * YPAS1 ;
        NN1      = ENTI 'INFE' (XINT1 / YPAS1) ;
        YMAX1    = YMIN1 + ((NN1 + 1) * YPAS1) ;
        LISO1    = PROG YMIN1 'PAS' YPAS1 YMAX1 ;
      SINON ;
        LISO1    = TEXT ' ' ;
      FINSI ;

*     Cas non-interactif :
      SI (NON FLCHOI);
*       on itere betement
        TI2 = EXTR LI1 I2 ;
        CH2 = TIRE CGI1 MRET1 TI2 ;
        CH2 = 'EXCO' CH2  MCOMP1 MCOMP1 ;
        MESG1  = CHAI ' Chargement ' MRET1 ' a l instant '
                (CHAI 'FORMAT' '(E10.2)' TI2) ;
        TRAC CH2  MM1 'TITR' MESG1 LISO1 ;
*       on itere betement
        I2  = I2 + 1 ;
        SI (I2 > NLI1); QUITTER BINST1; FINSI;
        ITERER BINST1;
      FINSI;
*
*     Choix des instants a vualiser
      TRAC 'NCLK' CH2  MM1 LISO1 ;

      MESG1    = CHAI ' Chargement ' MRET1 ' a l instant '
                (CHAI 'FORMAT' '(E10.2)' TI2) ;
      MRET2    = MENU MESG1 ' Visualiser ' ' Composante '
                   ' <-- ' ' --> ' ' No instant' ;
*
*     On quitte pour boucle principale
      SI (EGA MRET2 'Quitter') ;
        QUITTER BINST1 ;
      FINSI ;
*
*     On visualise le champ a l'instant demande
      SI (EGA MRET2 ' Visualiser ') ;
        TRAC CH2  MM1 'TITR' MESG1 LISO1 ;
      FINSI ;
*
*     Traitement choix instant suivant / precedent
      SI (EGA MRET2 ' <-- ') ;
        I2        = I2 - 1 ;
        I2        = I2 - ((I2 - NLI1) / NLI1 * NLI1) ;
      FINSI ;
      SI (EGA MRET2 ' --> ') ;
        I2        = I2 + 1 - (I2 / NLI1 * NLI1) ;
      FINSI ;
*
*     Traitement choix No instant
      SI (EGA MRET2 ' No instant') ;
        MESG2     = CHAI
          ' Entrez le No du pas de temps a visualiser (1 a '
          'FORMAT' '(I)' NLI1 ') : ' ;
        REPE BSAIS1 ;
          I2        = SAIS MESG2 'ENTIER' ;
          SI ((I2 '<' 0) 'OU' (I2 '>' NLI1)) ;
            MESG2     = CHAI ' Nombre inacceptable !' MESG2 ;
            'ITER' BSAIS1 ;
          FINSI ;
          QUITTER BSAIS1 ;
        FIN BSAIS1 ;
      FINSI ;
*
*     Champ pour nouvel instant choisi
      TI2     = EXTR LI1 I2 ;
      CH2     = TIRE CGI1 MRET1 TI2 ;
      CH2     = 'REDU' CH2  MM1 ;

*     Traitement des options d'affichage
*      SI (EGA MRET2 ' Options ') ;
*        TRAC 'NCLK' CH2  MM1 'TITR' MESG1 ;
*        MRET3   = MENU ' Choisissez une option d affichage '
*                  ' Composante ' ;
*      FINSI ;
*
*     Traitement choix composante
      SI (EGA MRET2 ' Composante ') ;
        MRET3   = MENU ' Choisissez la composante a afficher '
          (LCOMP1 ET ('MOTS' 'Tout')) ;
        SI (EXIS LCOMP1 MRET3) ;
          LCOMP2  = 'MOTS' MRET3 ;
        FINSI ;
        SI (EGA MRET3 'Tout') ;
          LCOMP2  = LCOMP1 ;
        FINSI ;
      FINSI ;
      SI (NEG LCOMP1 LCOMP2) ;
        CH2     = 'EXCO' CH2  LCOMP2 LCOMP2 ;
      FINSI ;
      MCOMP1   = EXTR LCOMP2 1 ;
*List Resu CH2  ;
*
*     Traitement choix vecteur
*      SI (EGA MRET3 ' Vecteur ') ;
*        TYP3     = EXTR CH2  'TYPE' ;
*        SI (EGA (TYPE TYP3) 'LISTMOTS') ;
*          MESG1     = CHAI
*            ' On attend un CHPOINT !' ;
*        FINSI ;
*        List TYP3 ;
*        SI ((EGA TYP3 'FORCES') 'OU' (EGA TYP3 'DEPLACEM')) ;
*        SINON ;
*          MESG1     = CHAI
*            ' On attend un CHPOINT de FORCES ou DEPLACEMENTS !' ;
*          TRAC 'NCLK' CH2  MM1 'TITR' MESG1 ;
*        FINSI ;
*      FINSI ;
*
    FIN BINST1 ;

    SI (NON FLCHOI); QUITTER BPRIN1; FINSI;

  FIN BPRIN1 ;
  QUITTER EXPLORER ;

FINSI ;


*----------------------------------------------------------------------*
*              SOUS-CAS DES RESULTATS D'UNE TABLE PASAPAS              *
*----------------------------------------------------------------------*

* INITIALISATIONS DES NOMS DES COMPOSANTES DES EFFORTS
leforces = TEXT 'FX FY FZ';
SI (EGA (VALE DIME) 2);
   leforces = TEXT 'FX FY';
FINSI;
SI (EGA (VALE 'MODE') 'AXIS');
   leforces = TEXT 'FR FZ';
FINSI;
SI (EGA (VALE 'MODE') 'FOUR');
   leforces = TEXT 'FR UT FZ';
FINSI;

* INITIALISATIONS ------------------------------------------------------

* ON COMMENCE PAR TRACER LE MAILLAGE lorsqu'on est en interactif
lemail = EXTR MATAB . 'MODELE' 'MAIL';
* suite a la demande de CB
SI (EXIS MATAB 'RIGIDITE_CONSTANTE');
  SI (EGA (TYPE MATAB . 'RIGIDITE_CONSTANTE') 'RIGIDITE');
    lemail = lemail et (EXTR MATAB . 'RIGIDITE_CONSTANTE' 'MAIL');
  FINSI;
FINSI;
SI FLCHOI; TRAC 'NCLK' 'CACH' lemail; FINSI;

* ON VERIFIE L'EXISTENCE DES CHAMPS -> lopt
lopt = MOTS;
SI (EXIS matab 'DEPLACEMENTS');
   tdpl = MATAB . 'DEPLACEMENTS';
   lopt = MOTS 'Depl';
FINSI;
SI ( EXIS matab 'CONTRAINTES');
   lopt = lopt ET (MOTS 'Cont');
   tctr = MATAB . 'CONTRAINTES';
   modmec = EXTR  MATAB . 'MODELE' 'FORMULATION' 'MECANIQUE';
   matmec = REDU MATAB . 'CARACTERISTIQUES' modmec;
FINSI;
SI ( EXIS matab 'VARIABLES_INTERNES');
   tvar = MATAB . 'VARIABLES_INTERNES';
   lopt = lopt ET (MOTS 'Var_');
FINSI;
SI ( EXIS matab 'REACTIONS');
   trea = MATAB . 'REACTIONS';
   lopt = lopt ET (MOTS 'Reac');
FINSI;
SI ( EXIS matab 'TEMPERATURES');
   lopt = lopt ET (MOTS 'Temp');
   modthe = EXTR  MATAB . 'MODELE' 'FORMULATION' 'THERMIQUE';
   maithe = EXTR modthe 'MAILLAGE';
   tctem = MATAB . 'TEMPERATURES';
FINSI;
lopt = lopt ET (MOTS 'Quitter');

* ON INDEXE LA TABLE DES TEMPS (DES PAS DE CALCUL)
ttem = MATAB . 'TEMPS';
tindex = INDEX ttem;
ltindex = DIME tindex - 1;
ind=0;d1=d1;

* VALEUR PAR DEFAUT DES LOGIQUES
evolu = FAUX;
defor = FAUX;
ampdef = TEXT ' ';
contr = FAUX;
react = FAUX;
tempe = FAUX;
evo_esp = FAUX;
evo_tem = FAUX;
depla  = FAUX;
var_int= FAUX;
quit   = FAUX;

* En cas de modele de contact, valeur amplitude deformee = 1 par defaut
si (exis MATAB.modele form contact) ;
  ampdef = 1. ;
fins ;

* BOUCLE PRINCIPALE ----------------------------------------------------

REPE bouc2;

SI  (EXIS matab 'DEPLACEMENTS');
  SI FLCHOI;
*   Choix interactif
    defor contr react evolu quit= CHOI 'cochez les options désirées'
    defor contr react evolu quit;
  SINON;
    SI FLMOC1;
*     Choix par un listmots en argument
      defor = EXIS LMOC1 'DEPL';
      contr = EXIS LMOC1 'CONT';
      react = EXIS LMOC1 'REAC';
      evolu = EXIS LMOC1 'EVOL';
      var_int = EXIS LMOC1 'VAR';
    SINON;
*     Choix par defaut
      defor = VRAI;
      contr = VRAI;
    FINSI;
    SI FLOPT1;
      SI (EXIS TOPT1 'AMPL');
        ampdef = TOPT1 . 'AMPL';
      FINSI;
    FINSI;
  FINSI;
SINON;
  SI ( EXIS matab 'TEMPERATURES');
    SI FLCHOI;
      tempe evolu quit = CHOI 'cochez les options désirées'
      tempe evolu quit;
    SINON;
      SI FLMOC1;
        tempe = EXIS LMOC1 'TEMP';
      SINON;
        tempe = VRAI;
      FINSI;
    FINSI;
  SINON ;
    MESS '* LA TABLE NE CONTIENT NI DEPLACEMENTS NI TEMPERATURES !';
    QUITTER EXPLORER;
  FINSI;
FINSI;

* CHOIX = QUIT  -->  ON SORT
SI quit ; QUITTER EXPLORER; FINSI;
* 1 seule iteration si pas de choix interactif
SI ((NON FLCHOI) ET (EGA &bouc2 2)); QUITTER EXPLORER; FINSI;

* AUCUN CHOIX = QUIT  -->  ON NE FAIT QUE REPROPOSER UN NOUVEAUX CHOIX
SI ((NON defor) ET (NON contr) ET (NON react)
      ET ( NON evolu) ET (NON tempe));
   ITERER bouc2;
FINSI;

* CHOIX = EVOLU
SI evolu;

  SI FLCHOI;
*   Choix interactif
    ret = MENU '  Faites votre choix' 'evo_espace' 'evo_temps' ;
    SI ( EGA ret 'Quitter' ) ; QUITTER EXPLORER; FINSI;
    SI ( EGA ret 'evo_espace') ;
      evo_esp=VRAI; evo_tem=FAUX;
    SINON;
      evo_esp=FAUX;
      SI ( EGA ret 'evo_temps' ); evo_tem=VRAI; FINSI;
    FINSI;
  SINON;
*   Choix par une table d'option en argument
    SI (NON FLOPT1);
      MESS '* Une table d options est necessaire en argument ';
      MESS '* dans le cas du trace d evolutions';
      ERRE 21;
    FINSI;
    SI (NON (EXIS TOPT1 'EVOL'));
      MESS '* Une table d options avec l indice EVOL est necessaire ';
      MESS '* en argument dans le cas du trace d evolutions';
      ERRE 21;
    FINSI;
    SI (NON (EXIS TOPT1 . 'EVOL' 'TYPE'));
      MESS '* Une table d options avec l indice EVOL . TYPE est ' ;
      MESS '* necessaire en argument dans le cas du trace d evolutions';
      ERRE 21;
    FINSI;
    evo_esp = EGA  TOPT1 . 'EVOL' . 'TYPE'  'ESPA';
    evo_tem = EGA  TOPT1 . 'EVOL' . 'TYPE'  'TEMP';
*   logiques deja affectes + haut
    depla = defor;
    lret = mots ;
    si depla; ret = mot 'Depl'; lret = lret et (mots ret); finsi;
    si contr; ret = mot 'Cont'; lret = lret et (mots ret); finsi;
    si react; ret = mot 'Reac'; lret = lret et (mots ret); finsi;
    si tempe; ret = mot 'Temp'; lret = lret et (mots ret); finsi;
    si var_int; ret = mot 'Var_'; lret = lret et (mots ret); finsi;
    SI ((dime lret) NEG 1);
       MESS '* Champ concerne par l evolution ambigu parmi :';
       LIST lret;
       MESS '* Veuillez fournir un LISTMOT plus explicite';
       ERRE 21;
    FINSI;
  FINSI;

* EVOLUTION SPATIALE
  SI evo_esp;
*    evolution spatiale sur quelle ligne? et de quelle grandeur?
     SI FLCHOI;
*       Choix interactif
        depla=FAUX; contr=FAUX; react=FAUX; tempe=FAUX; var_int=faux;
        mai = SAIS ' Nom de la ligne suivant laquelle tracer : '
             'MAILLAGE' ;
        ret = MENU 'Choisissez le champ concerne' lopt;
        MESS ' retour de menu ' ret;
        SI ( EGA ret 'Quitter' ); QUITTER EXPLORER; FINSI;
        SI ( EGA ret 'Depl') ; depla=VRAI; FINSI;
        SI ( EGA ret 'Cont') ; contr=VRAI; FINSI;
        SI ( EGA ret 'Reac') ; react=VRAI; FINSI;
        SI ( EGA ret 'Temp') ; tempe=VRAI; FINSI;
        SI ( EGA ret 'Var_') ; var_int=VRAI;FINSI;
     SINON;
*       Choix par une table d'option en argument
        SI (EXIS TOPT1 . 'EVOL' 'LIGN');
          mai=TOPT1 . 'EVOL' . 'LIGN';
        SINON;
          MESS '* Une table d options avec l indice EVOL . LIGN est ';
          MESS '* necessaire en argument dans le cas du trace ';
          MESS '* d evolutions spatiales';
          ERRE 21;
        FINSI;
     FINSI;
     SI ( EGA ret 'Depl') ;
       ICHPO=VRAI; mot1 = MOT 'DEPLACEMENTS';
     FINSI;
     SI ( EGA ret 'Cont') ;
       ICHPO=FAUX; mot1 = MOT 'CONTRAINTES';
     FINSI;
     SI ( EGA ret 'Reac') ;
       ICHPO=VRAI;  mot1 = MOT 'REACTIONS';
     FINSI;
     SI ( EGA ret 'Temp') ;
       ICHPO=VRAI;  mot1 = MOT 'TEMPERATURES';
     FINSI;
     SI ( EGA ret 'Var_') ;
       ICHPO=FAUX;  mot1 = MOT 'VARIABLES_INTERNES';
     FINSI;
     lcomp= EXTR   matab . mot1 . 1  'COMP';
     SI FLCHOI;
*      Choix interactif
       compo = MENU  'Composantes?' lcomp;
     SINON;
       SI (EXIS TOPT1 . 'EVOL' 'COMP');
*        Choix par une table d'option en argument
         compo = TOPT1 . 'EVOL' . 'COMP';
       SINON;
*        Choix par defaut
         compo = EXTR lcomp 1;
       FINSI;
     FINSI;
     cp= ttem . ind;
     SI ( EGA compo 'Quitter' ); QUITTER EXPLORER; FINSI;
*    titre du trace
     SI FLCHOI;
*      Choix interactif
       tit= SAIS 'Donner un titre pour le dessin (entrer quotes) :'
                 'MOT';
     SINON;
       SI (EXIS TOPT1 . 'EVOL' 'TITR');
*        Choix par une table d'option en argument
         tit = TOPT1 . 'EVOL' . 'TITR';
       SINON;
*        Choix par defaut
         tit = CHAI 'evolution spatiale de ' compo;
       FINSI;
     FINSI;
     tcp = ttem . ind;
     ch = CHAI tit ' pour le temps ' tcp;
     TITRE ch;
*    trace initial
     SI ( depla 'OU' tempe 'OU' react );
       SI depla; mata=MATAB . 'DEPLACEMENTS';FINSI;
       SI tempe; mata=MATAB . 'TEMPERATURES';FINSI;
       SI react; mata=MATAB . 'REACTIONS';FINSI;
       tcp= ttem . ind;
       ev = EVOL 'CHPO'  mata . ind mai compo;
       ichpo=vrai;
*        dess ev;
     SINON ;
       SI var_int; mata =  MATAB . 'VARIABLES_INTERNES' ;FINSI;
       SI contr; mata= MATAB . 'CONTRAINTES'; FINSI;
       rdt = CHAN 'CHPO' mata . ind MATAB . 'MODELE';
       ichpo=FAUX;
       ev =  EVOL 'CHPO'  rdt  mai compo;
     FINSI;
*    boucle second niveau sur les pas de temps ......
     REPE bouc77 ;
       SI FLCHOI;
*        Choix interactif
         dess ev;
         cha= CHAI 'pas courant :' ind;
         ret= MENU cha 'options' 'n°' 'retracer' 'precedent' 'suivant';
         SI (EGA ret 'options') ; QUITTER bouc77  ; FINSI;
         SI (EGA ret 'precedent') ; ind = ind - 1; FINSI;
         SI (EGA ret 'suivant')   ; ind = ind + 1; FINSI;
         SI (EGA ret 'retracer')  ; ind = ind    ; FINSI;
         SI (EGA ret 'Quitter')   ; QUITTER bouc2; FINSI;
         SI (EGA ret 'n°'); ind=SAIS 'Numéro du pas : ' 'ENTIER'; FINS;
         SI (ind > ltindex); ind = 0      ; FINSI;
         SI (ind < 0);       ind = ltindex; FINSI;
       FINSI;
       tcp = ttem . ind;
       ch = CHAI tit ' pour le temps ' tcp;
       TITRE ch;
       SI  ichpo;
          ev = EVOL 'CHPO'  mata . ind mai compo;
       SINON;
          rdt= CHAN 'CHPO' mata . ind MATAB . 'MODELE';
          ev =  EVOL 'CHPO'  rdt  mai compo;
       FINSI;
       dess ev;
       SI (NON FLCHOI);
*        non interactif : on trace tous les pas de temps
         ind = ind + 1;
         SI (ind > ltindex); QUITTER bouc77; FINSI;
       FINSI;
     FIN bouc77;
*    fin de la boucle second niveau sur les pas de temps ......
  FINSI;

* EVOLUTION TEMPORELLE
  SI evo_tem;
*    evolution temporelle en quel point? et de quelle grandeur?
     SI FLCHOI;
*      Choix interactif
       depla=FAUX;contr=FAUX; react=FAUX; tempe=FAUX;var_int=faux;
*        ret  = MENU 'Choisissez le champ concerne'
*               'depla' 'contr' 'react' 'tempe' 'var_int';
       ret  = MENU 'Choisissez le champ concerne' lopt;
       SI ( EGA ret 'Quitter' ); QUITTER explorer; FINSI;
     FINSI;
     SI ( EGA ret 'Depl') ; ichpo=VRAI;
           mot1 = MOT 'DEPLACEMENTS';FINSI;
     SI ( EGA ret 'Cont') ; ichpo=FAUX;
           mot1 = MOT 'CONTRAINTES'; FINSI;
     SI ( EGA ret 'Reac') ; ichpo=VRAI;
           mot1 = MOT 'REACTIONS';FINSI;
     SI ( EGA ret 'tempe') ; ichpo=VRAI;
           mot1 = MOT 'TEMPERATURES';FINSI;
     SI ( EGA ret 'Var_') ; ichpo=FAUX;
          mot1 = MOT 'VARIABLES_INTERNES';FINSI;
     lcomp= EXTR   matab . mot1 . 1 'COMP';
     SI FLCHOI;
*      Choix interactif
       compo=  MENU  'Composantes?' lcomp;
       SI ( EGA compo 'Quitter' ); QUITTER EXPLORER; FINSI;
       SI ichpo;
          P1= SAIS 'Point concerne? : '  'POINT';
       SINON;
          N1=SAIS 'Numero de la zone concernee : '  'ENTIER';
          N2=SAIS 'Numero de l element concerne : '  'ENTIER';
          N3=SAIS 'Numero du point de gauss concerne : '  'ENTIER';
       FINSI;
     SINON;
       SI (EXIS TOPT1 . 'EVOL' 'COMP');
*        Choix par une table d'option en argument
         compo = TOPT1 . 'EVOL' . 'COMP';
       SINON;
*        Choix par defaut
         compo = EXTR lcomp 1;
       FINSI;
       SI ichpo;
         SI (EXIS TOPT1 . 'EVOL' 'POIN'); p1 = TOPT1 . 'EVOL' . 'POIN';
         SINON; MESS '* Le point concerne doit etre fourni a lindice'
                'EVOL . POIN de la table d option';
                ERRE 21;
         FINSI;
       SINON;
         SI (EXIS TOPT1 . 'EVOL' 'ZONE'); n1 = TOPT1 . 'EVOL' . 'ZONE';
         SINON; n1 = 1;
         FINSI;
         SI (EXIS TOPT1 . 'EVOL' 'ELEM'); n2 = TOPT1 . 'EVOL' . 'ELEM';
         SINON; n2 = 1;
         FINSI;
         SI (EXIS TOPT1 . 'EVOL' 'PTG' ); n3 = TOPT1 . 'EVOL' . 'PTG' ;
         SINON; n3 = 1;
         FINSI;
       FINSI;
     FINSI;
     SI ichpo; ev1 = evol 'TEMP' matab mot1 compo p1;
     SINON;    ev1 = evol 'TEMP' matab mot1 compo n1 n2 n3;
     FINSI;
     dess ev1;
  FINSI;

* CHOIX = AUTRE QUE EVOLU
SINON;

* boucle second niveau sur les pas de temps ......
  REPE bouc1;

    cha = CHAI 'pas courant :' ind;
    SI FLCHOI;
*     Choix interactif
      ret = MENU cha 'options' 'n°' 'retracer' 'precedent' 'suivant';
      SI (EGA ret 'options') ; QUITTER bouc1  ; FINSI;
      SI (EGA ret 'precedent') ; ind = ind - 1; FINSI;
      SI (EGA ret 'suivant')   ; ind = ind + 1; FINSI;
      SI (EGA ret 'retracer')  ; ind = ind    ; FINSI;
      SI (EGA ret 'Quitter')   ; QUITTER bouc2; FINSI;
      SI (EGA ret 'n°') ; ind = SAIS 'Numéro du pas : ' 'ENTIER'; FINSI;
    FINSI;

*   indice du pas
    SI (ind > ltindex); ind = 0      ; FINSI;
    SI (ind < 0);       ind = ltindex; FINSI;

*   objet a tracer
    SI defor; dpl = DEFO lemail tdpl.ind ampdef;FINSI;
    SI contr;
      ctr = tctr.ind;
      moctr = mot 'contraintes';
    SINON;
      SI var_int;
        ctr = tvar.ind;
        moctr = mot 'variables internes';
      FINSI;
    FINSI;
    SI react; vec = VECT trea.ind leforces;FINSI;
    SI (defor ET react);dpv = DEFO lemail tdpl.ind ampdef vec;FINSI;

*   tracer
    SI (defor ET (NON contr) ET (NON react));
      TITRE 'deforme au temps ' MATAB . 'TEMPS' . ind;
      TRAC lemail dpl;
    FINSI;
    SI ((NON defor) ET contr ET (NON react));
      TITRE moctr ' au temps ' MATAB . 'TEMPS' . ind;
      TRAC ctr modmec matmec ;
    FINSI;
    SI (defor ET contr ET (NON react));
      TITRE moctr ' au temps ' MATAB . 'TEMPS' . ind;
      TRAC ctr modmec matmec dpl;
    FINSI;
    SI ((NON defor) ET (NON contr) ET react);
      TITRE 'reactions au temps ' MATAB . 'TEMPS' . ind;
      TRAC lemail vec;
    FINSI;
    SI (defor ET (NON contr) ET react);
      TITRE 'deforme au temps ' MATAB . 'TEMPS' . ind;
      TRAC dpv lemail;
    FINSI;
    SI ((NON defor) ET contr ET react);
      TITRE moctr ' au temps ' MATAB . 'TEMPS' . ind;
      TRAC ctr modmec matmec lemail vec;
    FINSI;
    SI (defor ET contr ET react);
      TITRE 'contraintes au temps ' MATAB . 'TEMPS' . ind;
      TRAC ctr modmec matmec  dpv ;
    FINSI;
    SI tempe;
      tctemp=tctem.ind;
      TITRE ' Temperatures au temps ' MATAB . 'TEMPS' . ind;
      TRAC tctemp  maithe ;
    FINSI;

    SI (NON FLCHOI);
      ind = ind + 1;
      SI (ind > ltindex); QUITTER bouc1; FINSI;
    FINSI;

  FIN bouc1;
* boucle second niveau sur les pas de temps ......

FINSI;
* FIN DISTINCTION CHOIX = EVOLU / AUTRE QUE EVOLU

FIN bouc2;
* FIN BOUCLE PRINCIPALE ------------------------------------------------


FINPROC  ;

$$$$





