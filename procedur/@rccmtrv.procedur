$$$$ @RCCMTRV
* @RCCMTRV  PROCEDUR  CB215821  18/06/07    21:15:01     9836
* @RCCMTRV  PROCEDURE                                96/10/01   09:10:00
* ----------------------------------------------------------------------
******                     PROCEDURE @RCCMTRV                     ******
* ----------------------------------------------------------------------
*
*   CETTE PROCEDURE PERMET D'EFFECTUER LE CALCUL DU TRESCA ET DU
*   VON MISES A PARTIR DES CONTRAINTES ELEMENTAIRES
*    6 valeurs a rentre :
*     -----> 4 valeurs pour un modele 2D + 2 valeurs nulles
*     -----> 6 valeurs pour un modele 3D
*
* ----------------------------------------------------------------------
*
*   LES DONNEES SONT :
*    ----> les 4 ou 6 contraintes elementaires
*
*   EN SORTIE :
*    ----> le TRESCA
*    ----> le VON MISES
*
* ----------------------------------------------------------------------
*   PAR Jean-Marc PIETTE
* ----------------------------------------------------------------------
'DEBPROC' @RCCMTRV  SX*FLOTTANT SY*FLOTTANT SZ*FLOTTANT SXY*FLOTTANT
                    SYZ/FLOTTANT SZX/FLOTTANT   ;
SI (NON (EXISTE SYZ)) ;
  SYZ = 0. ;
FINSI ;
SI (NON (EXISTE SZX)) ;
  SZX = 0. ;
FINSI ;
CC  =   SX + SY + SZ  / 3. ;
SG1 =   SX - CC      ;
SG2 =   SY  - CC    ;
SG3 =  SZ  - CC     ;

C2 = SG1*SG1 +(SG2*SG2)+(SG3*SG3) * .5;
C2= SXY*SXY+(SYZ*SYZ)+(SZX*SZX) + C2;
C3A = SG2*SG3 - (SYZ*SYZ) *  SG1 ;
C3B = SYZ*SZX  - (SXY*SG3) * SXY;
C3C = SXY*SYZ - (SG2*SZX) * SZX;
C3  = C3A + C3B + C3C ;
T   =  C2 * 2. / 3.  ** .5 ;
SI (T < 1.E-10) ;
  XTRES = 0. ;
  XVMIS = 0. ;
  RESPRO XTRES ;
  RESPRO XVMIS ;
  QUITTER @RCCMTRV ;
FINSI ;
A   =  C3 * (2. ** 0.5)  / ( T ** 3 ) ;
SI ( ( ABS A )  > 1. ) ;
  A = SIGNE A ;
FINSI ;
TCS = ( 1. - ( A * A ) ) ** 0.5 ;
A   = ATG TCS A;
A   = A / 3. ;
T   = T * (2 ** 0.5) ;
* ----------------------------------------------------------------------
* calcul des contraintes principales
* ----------------------------------------------------------------------
AUX = 120. ;
AA3 = A + AUX  ;
AA4 = A - AUX  ;
S1 = COS A * T + CC;
S2 = COS AA3 * T + CC;
S3 = COS AA4 * T + CC;
SSS = PROG S1 S2 S3 ;
* ----------------------------------------------------------------------
* calcul du TRESCA et du VON MISES
* ----------------------------------------------------------------------
XTMAX = MAXI SSS ;
XTMIN = MINI SSS ;
XTRES = XTMAX - XTMIN ;
XVMIS = ( ( S1 * S1 ) + ( S2 * S2 ) + ( S3 * S3 ) - ( S1 * S2 )
        - ( S2 * S3 ) - ( S1 * S3 ) ) ** .5 ;
RESPRO XTRES ;
RESPRO XVMIS ;
FINPROC  ;


