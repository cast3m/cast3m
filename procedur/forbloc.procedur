$$$$ FORBLOC
* FORBLOC   PROCEDUR  FANDEUR   14/10/10    21:15:15     8178
'DEBPROC'  FORBLOC  BLOC*'MAILLAGE' SOL1*'CHPOINT' COUR*'FLOTTANT'
                                            AXI/'LOGIQUE'  ;
************************************************************************
*      MAGNETOSTATIQUE  2D POTENTIEL VECTEUR
*   BLOC  maillage  non complexe
*   SOL1   SOLTUTION EN POTENTIEL  DEFINIE AU MOINS SUR BLOC
*  integration de j vectoriel b  sur les elements
*   RPT     champ par point ( FX FY ) aux noeuds
*   RFB     resultante  des forces au barycentre de la GEOMETRIE
*   AXI     logique valant vrai si pb axisym ( defaut  faux pb plan)
*     en plan  forces/unite de longueur
*     en axisymetrique  forces/radian attention signification
************************************************************************
 'SI'( 'NON' ('EXISTE' AXI) ) ; AXI = FAUX ; 'FINSI' ;
 BT = INDUCTIO BLOC  SOL1 AXI  ;
 OBJ0 = BLOC  'MODE' 'THERMIQUE' 'ISOTROPE' ;
 BBY = 'EXCO' BT  BY    ;
 BBX = 'EXCO' BT  BX    ;
*  DENSITE DE FORCES AUX NOEUDS
 DFX = COUR * BBY  * -1. ;
 DFY = COUR * BBX  ;
  SI AXI ;
 RX = COOR 1 BLOC ;
 DFX = DFX * RX   ;
 DFY = DFY * RX   ;
  FINSI ;
*  ON INTEGRE  SUR LES ELEMENTS
 RPTX = ('SOURCE' OBJ0 DFX  ) 'NOMC' 'FX' ;
 RPTY = ('SOURCE' OBJ0 DFY  )'NOMC' 'FY' ;
 RPT = RPTX + RPTY ;
 CDG = 'BARY' BLOC ;
 RFB = 'RESU' RPT ;
 IP1 =  ( 'EXTR' RFB 'MAILLAGE'  ) 'POINT' 1 ;
 RFB = RFB 'PLUS' ( CDG 'MOINS' IP1 ) ;
  'SI' axi  ;
 rpt = rpt * -1. ;
 rfb = rfb * -1. ;
   'FINSI' ;
*
 'FINPROC'  RPT RFB ;


