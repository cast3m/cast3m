$$$$ FILC
* FILC      PROCEDUR  BP208322  18/10/02    21:15:01     9943
DEBPROC FILC EV1*'EVOLUTIO' F0*'FLOTTANT' DF1*'FLOTTANT';
*
**************************************************************
*  COMMENTAIRE:    Alberto FRAU (alberto.frauetcea.fr)
*                                    DEN/DANS/DM2S/SEMT/EMSI
*                  Benjamin RICHARD (alberto.frauetcea.fr)
*                                    DEN/DANS/DM2S/SEMT/EMSI
*
* La procedure filtre une signal pour une frequence F0 donné
* avec une intervalle DF1 de frequence sur lequel effectuer
* couper le contenue frequentielle.
*
* INPUT:
*    EV1 Evolution temporelle à filtrer
*    F0  Frequence de coupure
*    DF1 intervalle autour de F01 pour aducir le filtrage
*
* OUTPUT:
*    EV3 Evolution temporelle filtrée
*
*    | Modu               Fonction de filtrage
*    |
*    |                          DF1
*    |                    |------------|
*    |
*    |____________________1
*    |                     * *
*    |                         *
*    |                           *
*    |                           *
*    |                             *  * _____________0
*    -------------------------------------------------
*    |---------------------------|                Freq
*                F0
**************************************************************
*
*
LX1 = 'PROG' 0. 'PAS' 0.05 1.;
LY1 = ((2.)*((LX1)**(3))) - ((3.)*((LX1)**(2)))
         + ((1.)*('PROG' ('DIME' LX1) * 1.));
LX1 = LX1*DF1;
LX2 = 'PROG' 0. (F0 - (DF1/2));
LX2 = LX2 'ET' (LX1 + ('PROG' ('DIME' LX1) * F0));
LY2 = 'PROG' 1. 1.;
LY2 = LY2 'ET' (LY1);
*
NN1 = 'DIME' ('EXTR' EV1 'ABSC');
NN2 = 'ENTI'(('LOG' NN1)/('LOG' 2));
'SI' (((2)**(NN2)) < NN1);
  NN2 = NN2 + 1;
'FINSI';
*
TFR1 = 'TFR' NN2 EV1 'MOPH';
LF1 = 'EXTR' TFR1 'ABSC';
LM1 = 'EXTR' TFR1 'ORDO' 1;
LP1 = 'EXTR' TFR1 'ORDO' 2;
*
LX3 = LF1;
LY3 = 'IPOL' LF1 LX2 LY2;
FIL1 = 'EVOL' 'COMP' 'MOPH' LX3 LY3 (0.*LY3);
*
TFR2 = TFR1*FIL1;
*
EV2 = 'TFRI' TFR2;
*
TITX = 'EXTR' EV1 'LEGX' 1;
TITY = 'EXTR' EV1 'LEGY' 1;
*
NN3 = ('DIME' ('EXTR' EV2 'ABSC')) - NN1;
'SI' (NN3 'NEG' 0);
  L_X1 = 'COPI' ('EXTR' EV2 'ABSC');
  L_Y1 = 'COPI' ('EXTR' EV2 'ORDO');
  'REPE' IND1 (NN3);
    L_X2 = ('ENLE' L_X1 ('DIME' L_X1));
    L_Y2 = ('ENLE' L_Y1 ('DIME' L_Y1));
    'DETR' L_X1;
    'DETR' L_Y1;
    L_X1 = 'COPI' L_X2;
    L_Y1 = 'COPI' L_Y2;
    'DETR' L_X2;
    'DETR' L_Y2;
  'FIN' IND1;
'SINON';
  L_X1 = 'COPI' ('EXTR' EV2 'ABSC');
  L_Y1 = 'COPI' ('EXTR' EV2 'ORDO');
'FINSI';
*
EV3 = 'EVOL' 'MANU' TITX L_X1 TITY L_Y1;
*
FINPROC EV3;
*
$$$$





