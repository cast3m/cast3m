$$$$ @DESLIS
* @DESLIS   PROCEDUR  MAUGIS    06/07/05    21:15:01     5480
* @DESLIS    PROCEDUR  MAUGIS    04/10/08    21:16:18     4934
'DEBP' @DESLIS LISTREE/'LISTREEL' LISTENT/'LISTENTI' ;
* MT1/'MOT' LT1/'MOT' MT2/'MOT' LT2/'MOT' MT3/'MOT'
* LT3/'MOT' MT4/'MOT' XBOR1/'FLOTTANT' XBOR2/'FLOTTANT' MT5/'MOT'
* YBOR1/'FLOTTANT' YBOR2/'FLOTTANT' MOT#/'MOT' ;
************************************************************************
*
*   Trace des dessins pour les évolutions contenus dans la table TBR
*   construite par TRACHIS ou TRACHIT
*
*   LISTREE : liste de réels
*   LISTENT : liste d'entiers
*   MOT#    : mot, option simple de dessin :
*             'LOGX' 'LOGY' 'GRIL' 'CARR' 'DATE' 'LOGO' 'AXES' 'NCLK'
*   MT1 MT2 MT3 : mots-clefs 'TITR' 'TITX' 'TITY' suivis des chaînes de
*             caractères correspondantes LT1 LT2 LT3
*   MT4 MT5 : mots-clefs 'XBOR' 'YBOR' suivis respectivement de
*   XBOR1 XBOR2 YBOR1 YBOR2 : FLOTTANTS valant les bornes d'abscisse et
*             d'ordonnée respectivement
*
*   La liste doit être donnée en premier ;
*   les autres options avec mot-clef viennent ensuite dans un ordre quelconque.
*   On ignore la donnée du mot-clef 'MIMA' puisque c'est l'option par défaut
*
************************************************************************

*  Récupération de l'objet à tracer
*  --------------------------------

   'SI' ((('NON' ('EXISTE' LISTREE)) 'ET' ('NON' ('EXISTE' LISTENT)))
    'OU' (('EXISTE' LISTREE) 'ET' ('EXISTE' LISTENT)) );
      'ERREUR' 'Donnez une liste de reels ou d entiers en entree' ;
   'FINSI' ;
   'SI' ('EXISTE' LISTREE) ;
      LIS1 = LISTREE ;
   'SINON' ;
      LIS1 = 'FLOTTANT' LISTENT ;
   'FINSI' ;

   nn = 'DIME' LIS1 ;
   'SI' (nn '<EG' 1) ;
      'LISTE' lis1 ;
      'ERREUR'
       'La liste donnee doit avoir au moins 2 valeurs pour etre tracee';
   'FINSI' ;
   LIX = 'PROG' 1. 'PAS' 1. nn ;
   ttx = 'MOT' 'Ordre' ;
   tty = 'MOT' 'Valeur' ;
   ev1 = 'EVOL' 'MANU' ttx lix tty lis1 ;

*  Récupération des options de dessin
*  ----------------------------------
*  et construction de la table des options

*--Valeurs par défaut :
   TTR   = 'CHAINE' 'Evolution des valeurs de la ' ('TYPE' LIS1) ;

*  table des options LOP
   LOP   = 'TABLE' ;
   IOP   = 1 ;

*  On lit une par une les données précisées par des mots-clefs
   'REPETER' bcl ;
     'ARGUMENT' mot1/'MOT'  ;
     'SI' ('NON' ('EXISTE' MOT1)) ;
        'QUITTER' bcl ;
     'FINSI' ;

*    options simples de DESSIN
     'SI' (     ('EGA' MOT1 'LOGX') 'OU' ('EGA' MOT1 'LOGY')
           'OU' ('EGA' MOT1 'GRIL') 'OU' ('EGA' MOT1 'CARR')
           'OU' ('EGA' MOT1 'DATE') 'OU' ('EGA' MOT1 'LOGO')
           'OU' ('EGA' MOT1 'SEPA') 'OU' ('EGA' MOT1 'AXES')
           'OU' ('EGA' MOT1 'NCLK') 'OU' ('EGA' MOT1 'MIMA') ) ;
        LOP.IOP     = 'MOT' MOT1 ;
        IOP         = IOP + 1 ;
        'ITERER' bcl ;
     'FINSI' ;

*    options avec arguments ou spécifiques
     'SI' (('EGA' MOT1 'TITR') 'OU' ('EGA' MOT1 'TITRE')) ;
        'ARGUMENT' TTR*'MOT' ;
        LOP.IOP     = 'MOT' 'TITR' ;
        LOP.(IOP+1) = 'MOT' TTR ;
        IOP         = IOP + 2 ;
        'ITERER' bcl ;
     'FINSI' ;
     'SI' ('EGA' MOT1 'TITX') ;
        'ARGUMENT' TTX*'MOT' ;
        LOP.IOP     = 'MOT' 'TITX' ;
        LOP.(IOP+1) = 'MOT' TTX ;
        IOP         = IOP + 2 ;
        'ITERER' bcl ;
     'FINSI' ;
     'SI' ('EGA' MOT1 'TITY') ;
        'ARGUMENT' TTY*'MOT' ;
        LOP.IOP     = 'MOT' 'TITY' ;
        LOP.(IOP+1) = 'MOT' TTY ;
        IOP         = IOP + 2 ;
        'ITERER' bcl ;
     'FINSI' ;
     'SI' ('EGA' MOT1 'XBOR') ;
        'ARGUMENT' XBOR1*'FLOTTANT' XBOR2*'FLOTTANT' ;
        LOP.IOP     = 'MOT' 'XBOR' ;
        LOP.(IOP+1) = 'MINIMUM' ('PROG' XBOR1 XBOR2) ;
        LOP.(IOP+2) = 'MAXIMUM' ('PROG' XBOR1 XBOR2) ;
        IOP         = IOP + 3 ;
        'ITERER' bcl ;
     'FINSI' ;
     'SI' ('EGA' MOT1 'YBOR') ;
        'ARGUMENT' YBOR1*'FLOTTANT' YBOR2*'FLOTTANT' ;
        LOP.IOP     = 'MOT' 'YBOR' ;
        LOP.(IOP+1) = 'MINIMUM' ('PROG' YBOR1 YBOR2) ;
        LOP.(IOP+2) = 'MAXIMUM' ('PROG' YBOR1 YBOR2) ;
        IOP         = IOP + 3 ;
        'ITERER' bcl ;
     'FINSI' ;

*    donnée non identifiée
     'ERREUR' ('CHAINE' 'Dans les donnees de @DESLIS, mot-clef '
                         mot1 ' inconnu.');
   'FIN' bcl ;

*  on complète les options jusqu'au nb maximal par des MIMA
   NOP    = IOP - 1 ;
   NOPMAX = 40 ;
   'SI' (NOP < NOPMAX) ;
      NB2 = NOPMAX - NOP ;
      'REPETER' BLOC4 NB2 ;
         LOP.IOP = 'MOT' 'MIMA' ;
         IOP     = IOP + 1  ;
      'FIN' BLOC4 ;
   'FINSI'  ;

*  Desszin :
*  --------
*  construction de la commande
   'DESSIN' EV1 'XBOR' 1. nn 'TITRE' ttr
            LOP.1 LOP.2 LOP.3 LOP.4 LOP.5 LOP.6 LOP.7 LOP.8
            LOP.9 LOP.10 LOP.11 LOP.12 LOP.13 LOP.14 LOP.15 LOP.16
            LOP.17 LOP.18 LOP.19 LOP.20 LOP.21 LOP.22 LOP.23 LOP.24
            LOP.25 LOP.26 LOP.27 LOP.28 LOP.29 LOP.30 LOP.31 LOP.32
            LOP.33 LOP.34 LOP.35 LOP.36 LOP.37 LOP.38 LOP.39 LOP.40 ;

'FINP';



