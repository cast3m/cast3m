$$$$ SILAM
* SILAM     PROCEDUR  AM1       92/06/22    21:16:41     600
************************************************************************
*                        PROCEDURA SILAM
************************************************************************
*
* Descrizione : visualizzazione dell'andamento delle tensioni
*               nello spessore in corrispondenza di un punto
*
* Sintassi    : SILAM TAB_LAM DEPL1 NZON VET1 P0 ;
*
*
* Autore/Data : Miliozzi A.  03/02/1992
*
*
* Revisioni   :
*
************************************************************************
*
*--- INPUT
*=========
*
* TAB_LAM  Tavola caratteristica del laminato
*
* DEPL1    Campo di spostamenti
*
* NZON     Numero della zona considerata
*
* VET1     Direzione di orientazione del campo di tensioni
*
* P0       Punto sul quale visualizzare le tensioni
*
*--- OUTPUT
*==========
*
*
************************************************************************
*
DEBPROC SILAM TCOMP*TABLE DEPL1*CHPOINT NZON*ENTIER
              VET1*POINT  P0*POINT ;
*
  TCTIP = TCOMP.NZON.TIPO   ;
  TCOMP.NZON.TIPO = 'MLAY'  ;
  TCAR1 = LAMAT (TCOMP.NZON);
  TCOMP.NZON.TIPO = TCTIP   ;
*
  OBJ = TCOMP.NZON.MOD1 ;
*
  LS11 = PROG ; LI11 = PROG ;
  LS22 = PROG ; LI22 = PROG ;
  LS12 = PROG ; LI12 = PROG ;
  LEXC = PROG ; LEPA = PROG ;
*
  NPLY = DIME TCAR1.MAT ;
*
  N   = 1 ;
  REPETER LOOP0 NPLY ;
*
    SIGE1 = SIGMA OBJ (TCAR1.MAT.N ET TCAR1.CAR.N) DEPL1 ;
    SIGE2 = RTENS SIGE1 OBJ VET1 ; DETR SIGE1 ;
    SIGE3S = CALP SIGE2 TCAR1.CAR.N OBJ 'SUPE' ;
    SIGE3I = CALP SIGE2 TCAR1.CAR.N OBJ 'INFE' ;
    DETR SIGE2 ;
    SIGSUP = CHANGE CHPO OBJ SIGE3S ;
    SIGINF = CHANGE CHPO OBJ SIGE3I ;
    DETR SIGE3S ; DETR SIGE3I ;
*
    SUP11 = EXTRA SIGSUP SMXX P0 ; SDO11 = EXTRA SIGINF SMXX P0 ;
    SUP22 = EXTRA SIGSUP SMYY P0 ; SDO22 = EXTRA SIGINF SMYY P0 ;
    SUP12 = EXTRA SIGSUP SMXY P0 ; SDO12 = EXTRA SIGINF SMXY P0 ;
*
    EX = TCOMP.NZON.EXC.N        ; EP = TCOMP.NZON.EPA.N        ;
*
    LS11 = INSERER LS11 N SUP11  ; LI11 = INSERER LI11 N SDO11  ;
    LS22 = INSERER LS22 N SUP22  ; LI22 = INSERER LI22 N SDO22  ;
    LS12 = INSERER LS12 N SUP12  ; LI12 = INSERER LI12 N SDO12  ;
    LEXC = INSERER LEXC N EX     ; LEPA = INSERER LEPA N EP     ;
*
    N = N + 1 ;
  FIN LOOP0 ;
*
* DETR DEP1 ;
  L11 = PROG ; L22 = PROG ; L12 = PROG ; LQUOTA = PROG ;
*
  N   = 1 ;
  REPETER LOOP1 NPLY ;
*
    K1 = (2 * N) - 1 ; K2 = 2 * N ;
*
    Z1 = EXTR LEXC N ;
    Z2 = (EXTR LEPA N) / 2. ;
*
    ZZ = Z1 + Z2 ;
    LQUOTA = INSERER LQUOTA K1 ZZ ;
    L11 = INSERER L11    K1 (EXTR LS11 N) ;
    L22 = INSERER L22    K1 (EXTR LS22 N) ;
    L12 = INSERER L12    K1 (EXTR LS12 N) ;
*
    ZZ = Z1 - Z2 ;
    LQUOTA = INSERER LQUOTA K2 ZZ ;
    L11 = INSERER L11    K2 (EXTR LI11 N) ;
    L22 = INSERER L22    K2 (EXTR LI22 N) ;
    L12 = INSERER L12    K2 (EXTR LI12 N) ;
*
    N = N + 1 ;
*
  FIN LOOP1 ;
*
  DETR LS11 ; DETR LS22 ; DETR LS12 ;
  DETR LI11 ; DETR LI22 ; DETR LI12 ;
  DETR LEXC ; DETR LEPA ;
*
  TITRE 'ANDAMENTO TENSIONE NELLO SPESSORE' ;
  EV11 = EVOL ROUG MANU 'SIGMA11' L11 'QUOTA Z' LQUOTA ;
  EV22 = EVOL TURQ MANU 'SIGMA22' L22 'QUOTA Z' LQUOTA ;
  EV12 = EVOL VERT MANU 'SIGMA12' L12 'QUOTA Z' LQUOTA ;
*
  DESSIN EV11 ;
  DESSIN EV22 ;
  DESSIN EV12 ;
*
  DETR LQUOTA ; DETR L11 ; DETR L22 ; DETR L12 ;
*
FINPROC ;
*******************************************************************
*                    FINE PROCEDURA SILAM
********************************************************************
