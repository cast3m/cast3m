$$$$ CALCULER
* CALCULER  PROCEDUR  FANDEUR   14/10/10    21:15:12     8178
'DEBPROCEDURE'      CALCULER     ;
*
*  CETTE PROCEDURE PERMET UNE SAISIE ASSISTEE DES DONNEES POUR
*  EFFECTUER UN CALCUL SIMPLE EN 2D ACTUELLEMENT
*
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
'MESS' '               *************************     ';
'MESS' '               *   PROCEDURE CALCULER  *     ';
'MESS' '               *************************     ';
'SAUTE' 2 'LIGN' ;
'MESS' '      CETTE PROCEDURE PERMET UNE SAISIE ASSISTEE DES DONNEES ';
'MESS' '              NECESSAIRES POUR EFFECTUER UN CALCUL   ';
'MESS' '            EN ELASTICITE LINEAIRE BIDIMENSIONNELLE   ';
'SAUTER' 2 'LIGN' ;
'MESS' '             DEFINITION DU TYPE DE CALCUL ' ;
'MESS' '             ---------------------------- ' ;
'SAUTER' 2 'LIGN' ;
'MESS' 'CALCUL EN CONTRAINTES  PLANES (REPONDEZ 1)' ;
'MESS' 'OU     EN DEFORMATIONS PLANES (REPONDEZ 2)  ? ';
'REPETER' BLOC1;
     'OBTENIR' REP*'ENTIER' ;
  'SI' ('EGA' REP 1);
        'OPTIO' 'MODEL' 'PLAN' 'CONTR';
      'QUITTER' BLOC1;
  'FINSI';
  'SI' ('EGA' REP 2);
       'OPTIO' 'MODEL' 'PLAN' 'DEFOR';
      'QUITTER' BLOC1;
  'FINSI';
  'MESS' 'REPONDEZ PAR 1 OU 2';
'FIN' BLOC1 ;
*
MOTMOD = 'TEXT' 'MECANIQUE ELASTIQUE';
*
LICOUL='TABLE';
LICOUL.1 LICOUL.2 LICOUL.3 LICOUL.4 LICOUL.5 LICOUL.6 LICOUL.7   =
  'VERT' 'JAUNE'  'BLEU'  'TURQ'   'BLAN'     'ROSE'    'ROUGE'   ;
I=0;
DEB=VRAI ;
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
'MESS' '        DEFINITION DES PROPRIETES MATERIELLES ' ;
'MESS' '        ------------------------------------- ' ;
'SAUTER' 2 'LIGN' ;
'REPETER' BLOC2;
'SI' ( 'EGA' I  0 ) ;
'MESS' 'DONNEZ LE NOM D UNE ZONE GEOMETRIQUE CONSTITUEE D UN ' ;
'MESS' '              MATERIAU HOMOGENE  ' ;
'SAUTER' 1 'LIGN' ;
'FINSI' ;
'SI' (  > I 0 );
'SAUTER' 'PAGE' ;
'SAUTER' 3 'LIGN' ;
'MESS' 'DONNEZ LE NOM D UNE AUTRE ZONE GEOMETRIQUE CONSTITUEE D UN ' ;
'MESS' '                   MATERIAU HOMOGENE  ' ;
'SAUTER' 1 'LIGN' ;
'MESS' 'OU REPONDEZ NON SI IL N Y EN N A PLUS';    FINSI ;
I = I + 1; 'SI' (  'EGA' I  7) ; I = 1; 'FINSI';
  'OBTENIR' ZONE ;
   'SI' ('NON'('EGA' (TYPE ZONE)  MAILLAGE )) ;
     'QUITTER' BLOC2;
   'FINSI';
  ZONE = ZONE 'COULEUR' LICOUL.I;
  MOD1='MODE' ZONE  MOTMOD  'QUA4'  'TRI3'  'COQ2'  'TRI6'  'QUA8';
*
'SAUTER' 2 'LIGN' ;
'MESS' 'DONNEZ LA VALEUR DU MODULE D YOUNG';
  Y=LIREFLOT 1E-30 1E30;
'MESS' 'DONNEZ LA VALEUR DU COEFFICIENT DE POISSON';
  P=LIREFLOT 0.0 0.4999999 ;
 MA1= 'MATE'  MOD1  'YOUNG' Y 'NU' P;
'SI' DEB;
TOTO = ZONE  ;
 DEB=FAUX;
 MAT=MA1;
 MODT=MOD1 ;
'SINON' ;
 MAT =MAT 'ET' MA1;
 MODT=MODT 'ET' MOD1 ;
TOTO = TOTO 'ET'  ZONE ;
'FINSI';
'FIN' BLOC2 ;
*
*  CALCUL DE LA RAIDEUR DE LA STRUCTURE
RI = 'RIGIDITE' MODT MAT;
*
*  ENTREE DES CONDITIONS AUX LIMITES
'OPTIO' 'COUL' LICOUL.7;
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
IMIL = 0 ;
'MESS' '        DEFINITION DES CONDITIONS AUX LIMITES ' ;
'MESS' '        ------------------------------------- ' ;
'SAUTER' 2 'LIGN' ;
'REPETER' BLOC3;
'SI' ( IMIL 'EGA' 0 ) ;
'MESS' 'DONNEZ LE NOM D UNE ZONE GEOMETRIQUE OU IL FAUT IMPOSER ' ;
'MESS' '              DES BLOCAGES   ' ;
'FINSI' ;
'SI' ( IMIL > 0 ) ;
'SAUTER' 'PAGE' ;
'SAUTER' 3 'LIGN' ;
'MESS' 'DONNEZ LE NOM D UNE AUTRE ZONE GEOMETRIQUE OU IL FAUT IMPOSER';
'MESS' '                    DES BLOCAGES   ' ;
'SAUTER' 'LIGN' ;
'MESS' 'OU  REPONDEZ NON SI IL N Y EN N A PLUS';
'FINSI' ;
IMIL = IMIL + 1 ;
TYP= 'TYPE'  (0. 0. );
  'OBTENIR' ZONE  ;
    'SI' ('ET' ('NON'('EGA' ('TYPE' ZONE)  MAILLAGE ))
              ('NON'('EGA' ('TYPE' ZONE)  TYP)));
      'QUITTER' BLOC3;
    'FINSI';
'MESS' 'DONNEZ LE TYPE DE BLOCAGES. LES REPONSES POSSIBLES SONT :';
'SAUTER' 'LIGN' ;
'MESS' ' DEPL   ...  POUR BLOQUER TOUTES LES TRANSLATIONS';
'MESS' ' ROTA   ...  POUR BLOQUER TOUTES LES ROTATIONS';
'MESS' ' UX     ...  POUR BLOQUER LES TRANSLATIONS SELON X ' ;
'MESS' ' UY     ...  POUR BLOQUER LES TRANSLATIONS SELON Y ' ;
'REPETER' BLOC4;
  'OBTENIR' TYPE*'MOT';
    'SI' ('EGA' TYPE 'DEPL');
      RI1= 'BLOQUER' ZONE 'DEPLA';
      RI=RI 'ET' RI1;
     'QUITTE' BLOC4;
    'FINSI';
    'SI' ('EGA' TYPE 'ROTA');
      RI1= 'BLOQUER' ZONE 'ROTA' ;
      RI=RI 'ET' RI1;
     'QUITTER' BLOC4;
    'FINSI';
    'SI' ('EGA' TYPE 'UX'  );
      RI1= 'BLOQUER' ZONE 'UX'   ;
      RI=RI 'ET' RI1;
     'QUITTER' BLOC4;
    'FINSI';
    'SI' ('EGA' TYPE 'UY'  );
      RI1= 'BLOQUER' ZONE 'UY'   ;
      RI=RI 'ET' RI1;
     'QUITTER' BLOC4;
    'FINSI';
'MESS' 'DONNEZ UNE REPONSE CORRECTE ...';
'FIN' BLOC4;
'FIN' BLOC3;
*
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
'MESS' '        DEFINITION DES FORCES PONCTUELLES   ' ;
'MESS' '        --------------------------------- ' ;
'SAUTER' 2 'LIGN' ;
'REPETER' DEFFORC;
DEB=VRAI;
'REPETE' BLOC5;
'SI' DEB;
'MESS' 'DONNEZ LE NOM D UN POINT SUR LEQUEL APPLIQUER UNE FORCE ';
 'SINON';  'SAUTER' 'PAGE'; 'SAUTER' 2 'LIGN' ;
'MESS' 'DONNEZ LE NOM D UN AUTRE POINT SUR LEQUEL APPLIQUER UNE FORCE ';
'SAUTER' LIGN ;
'MESS' 'OU REPONDEZ NON SI IL N Y EN A PLUS';
'FINSI' ;
 'OBTENIR' ZONE*'POINT';
'SI' ('EGA' ZONE 'NON');
 'QUITTER' BLOC5;
'FINSI';
'SAUTER' 'LIGN' ;
'MESS' 'DONNEZ LES 2 COMPOSANTES DU VECTEUR FORCE  ' ;
'SAUTER' 'LIGN' ;
'MESS' 'PAR EXEMPLE :   2.45  -0.51  ' ;
 'OBTENIR' X*'FLOTTANT' Y*'FLOTTANT';
FO = 'FORCE' (X Y) ZONE;
'SI' DEB;
 FOR = FO ;
 DEB=FAUX;
'SINON';
 FOR = FOR 'ET' FO;
'FINSI';
'FIN'     BLOC5;
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
'MESS' '        VISUALISATION DES DONNEES  ' ;
'MESS' '        ------------------------- ' ;
'REPETE' DESFO;
'SAUTER' 2 'LIGN' ;
'MESS' 'DONNEZ LE COEFFICIENT D AMPLIFICATION DES FORCES POUR LE TRACE';
CO1 = LIREFLOT 1E-30 1E30;
V1='VECTEUR' FOR CO1 'FX' 'FY' 'ROUGE';
* IL FAUDRAIT EXTRAIRE DE RI LE MAILLAGE
'TRAC' 'FACE' V1 RI  ;
'SAUTER' 3 'LIGN' ;
'MESS' 'VOULEZ-VOUS CHANGER LE COEFFICIENT D AMPLIFICATION ? (OUI/NON)';
'OBTENIR' REP*'MOT';
'SI' ( 'EGA' REP 'NON');
  'QUITTER' DESFO;
'FINSI';
'FIN' DESFO;
'SAUTE'  'LIGN' ;
'MESS' 'VOULEZ-VOUS CORRIGER LES FORCES ? (OUI/NON)';
'OBTENIR' REP*'MOT';
'SI' ('EGA' REP 'NON');
'QUITTER' DEFFORC;
'FINSI';
'FIN' DEFFORC;
'SAUTER'  'LIGN' ;
'MESS' 'VOULEZ-VOUS INTERROMPRE LE CALCUL ? (OUI NON)';
'OBTENIR' REP*'MOT';
'REPETER'  FINCAL  1;
'SI' ('EGA' REP 'OUI');
'MESS' 'INTERRUPTION DU CALCUL';
  'QUITTER' FINCAL;
'FINSI';
'SAUTER' 'PAGE' ;
'SAUTER' 2 'LIGN' ;
'MESS' 'DEBUT DE LA RESOLUTION ' ;
DE='RESOU'  RI FOR;
'SAUTE' 2 'LIGN' ;
'MESS' 'FIN DU CALCUL DES DEPLACEMENTS';
RE='REAC' DE RI;
'SAUTER' 2 'LIGN' ;
'MESS' 'FIN DU CALCUL DES REACTIONS AUX APPUIS ';
SIG = 'SIGMA' MODT MAT DE;
'SAUTER' 2 'LIGN' ;
'MESS' 'FIN DU CALCUL DES CONTRAINTES ';
VMIC='VMISES' MODT SIG;
'SAUTER' 'PAGE';
'SAUTER' 2 'LIGN' ;
'MESS' '        VISUALISATION DES RESULTATS' ;
'MESS' '        --------------------------- ' ;
'REPETE' DESTOT;
'SAUTER' 2 'LIGN' ;
'MESS'  ' DONNEZ LE COEFFICIENT D AMPLIFICATION DE LA DEFORMEE';
CO2 = LIREFLOT 1E-30 1E30;
V2='VECTEUR' RE  CO1 'FX' 'FY' 'BLEU';
DEF='DEFOR' TOTO  DE (V1 'ET' V2) CO2 'JAUN';
'TRAC' MODT VMIC DEF;
'SAUTER' 2 'LIGN' ;
'MESS' 'VOULEZ-VOUS CHANGER LE COEFFICIENT D AMPLIFICATION ?(OUI/NON)';
'OBTENIR' REP*'MOT';
'SI' ( 'EGA' REP 'NON') ;
  'QUITTER' DESTOT;
'FINSI';
'FIN' DESTOT;
'FIN' FINCAL;
'SAUTER' 'PAGE';
'SAUTER' 4 'LIGN' ;
'MESS' '           ******************************************      ';
'MESS' '           *   FIN DE LA PROCEDURE  "" CALCULER ""  *      ';
'MESS' '           ******************************************      ';
'FINPROC';


