$$$$ ENERMODE
* ENERMODE  PROCEDUR  BP208322  13/06/27    21:15:07     7785
'DEBP' ENERMODE TBASS*'TABLE' TRESD*'TABLE' TINIT/'TABLE'
                        LOG1/'LOGIQUE';

**********************************************
*
* CALCUL DU BILAN ENERGETIQUE
*
* TBASS  : TABLE DE SOUTYPE 'BASE_MODALE' OU 'ENSEMBLE_DE_BASES';
* TRESD : TABLE DE SOUSTYPE 'RESULTAT_DYNE' OU TABLE CONTENANT
*          DES TABLES DE SOUSTYPE 'RESULTAT_DYNE'
* TINIT : TABLE DE CONDITIONS INITIALES
*
* LOG1  :  RESULTAT POUR CHAQUE MODE SI LOG1
*
**********************************************

TTABD = 'TABL' ;
TTABD.'TITRE'= 'TABL' ; TTAB = TTABD.'TITRE';
TTAB.1 = 'MOT' 'TRAVAIL EXTERIEUR';
TTAB.2 = 'MOT' 'TRAVAIL INTERIEUR';
TTAB.3 = 'MOT' 'ENERGIE CINETIQUE';
TTAB.4 = 'MOT' 'BILAN ENERGETIQUE';

'SI' ('EXIS' TRESD 'SOUSTYPE') ;
 'SI' ('EGA' TRESD.'SOUSTYPE' 'RESULTAT_DYNE');
  DIM_REP = 1 ; IREP = FAUX;
 'SINON' ;
  DIM_REP = ('DIME' TRESD) - 1 ; IREP = VRAI;
 'FINSI' ;
'SINON' ;
 DIM_REP = 'DIME' TRESD; IREP = VRAI;
'FINSI' ;


'SI' ('EXISTE' TBASS 'SOUSTYPE') ;
 'SI' ('EGA' TBASS.'SOUSTYPE' 'ENSEMBLE_DE_BASES');
   IDIM = ('DIME' TBASS) - 1 ; IBAS = FAUX ;
'SINON' ;
  'SI' ('EGA' TBASS.'SOUSTYPE' 'BASE_MODALE');
   IDIM = 1 ; IBAS = VRAI ;
  'SINON' ;
   'MESS' 'ON NE TROUVE PAS DE BASE MODALE';
   'QUIT' ENERMODE ;
  'FINSI' ;
'FINSI';
'SINON' ;
  'MESS' 'ON NE TROUVE PAS DE BASE MODALE';
  'QUIT' ENERMODE ;
'FINSI';

TEXT = 'TABLE' ; TINT = 'TABLE'; TCIN = 'TABLE'  ;
'REPETER' BOU4 IDIM ;

 'SI' IBAS ; TBAS = TBASS ; 'SINON'; TBAS = TBASS.&BOU4 ; 'FINSI';

DIM1 = ('DIME' TBAS.MODES) - 2 ;

'REPETER' BOU2 DIM1 ;
 P1 = TBAS.'MODES'.&BOU2 .'POINT_REPERE' ;
  'SI' (&BOU2 'EGA' 1) ; MAIL1 = P1 ; 'SINON' ;
  MAIL1 = MAIL1 ET P1 ; 'FINSI' ;
'FIN' BOU2 ;
CHVIT = 'MANU' 'CHPO' MAIL1 1 'ALFA' 0. ;
 'SI' ('EXISTE' TINIT) ;
  'SI' ('NEG' TINIT.'SOUSTYPE' 'INITIAL')  ;
    'MESS' 'ON NE TROUVE PAS DE TABLE DE CONDITIONS INITIALES';
    'MESS' 'LA VITESSE INITIALE EST MISE A ZERO';
  'FINSI' ;
   'SI' ('EXISTE' TINIT 'VITESSE') ; CHVIT = CHVIT + TINIT.'VITESSE';
   'FINSI' ;
 'FINSI' ;

'REPETER' BOU1 DIM1 ;

'REPETER' BOU3 DIM_REP ;
'SI' IREP ;
 'SI' ('EXISTE' TRESD &BOU3) ;
  TRES1 = TRESD.&BOU3 ;
 'SINON' ; 'QUIT' BOU3 ;'FINSI' ;
'SINON'; TRES1 = TRESD ; 'FINSI' ;

 'SI' ('NEG' TRES1.'SOUSTYPE' 'RESULTAT_DYNE') ;
   MESS 'ON NE TROUVE PAS DE RESULTAT DE DYNE'; 'QUIT' ENERMODE;
 'FINSI';

DIM1 = ('DIME' TBAS.'MODES') - 2 ;
LIS_TEM0 = TRES1.'TEMPS_DE_SORTIE' ;
DIMTEMPS = 'DIME' LIS_TEM0 ;

 P1 = TBAS.'MODES'.&BOU1 .'POINT_REPERE' ;
 M1 = TBAS.'MODES'.&BOU1 .'MASSE_GENERALISEE';
 NUM_MODE = TBAS.'MODES'.&BOU1 .'NUMERO_MODE' ;
 V0 = 'EXTR' CHVIT 'ALFA' P1 ;
 WCIN0 = .5*M1*V0*V0 ; LISWCIN0 = 'PROG' DIMTEMPS*WCIN0 ;
 EVV = 'EVOL' 'SOLU' TRES1 'VITE' P1 'ALFA' ;
 LISWCIN = (('EXTR' EVV 'ORDO')**2)*.5*M1 ;
 LISWCIN = LISWCIN - LISWCIN0 ;
'SI' (&BOU3 'EGA' 1) ;
 EVCIN = 'EVOL' 'TURQ'  'MANU' 'TEMPS' LIS_TEM0 LISWCIN ;
 EVEXT = 'EVOL' 'SOLU' TRES1 'TRAVAIL_EXTERIEUR' P1 'ALFA' ;
 EVINT = 'EVOL' 'VERT' 'SOLU' TRES1 'TRAVAIL_INTERIEUR' P1 'ALFA' ;
'SINON' ;
 EVCIN = ('EVOL' 'TURQ' 'MANU' 'TEMPS' LIS_TEM0 LISWCIN) 'CONC' EVCIN;
 EVEXT = ('EVOL' 'SOLU' TRES1 'TRAVAIL_EXTERIEUR' P1 'ALFA')
                                            'CONC' EVEXT;
 EVINT = ('EVOL' 'VERT'  'SOLU' TRES1 'TRAVAIL_INTERIEUR' P1 'ALFA')
                                           'CONC' EVINT ;
'FINSI' ;
 'SI' (&BOU3 'EGA' DIM_REP) ;
   'TITRE' 'BASE MODALE' &BOU4 '  MODE NUMERO' NUM_MODE;
   EVBILAN = EVEXT - EVINT - EVCIN ;
   EVBILAN = 'COUL' 'ROUG' EVBILAN ;
  'SI' ('EXISTE' LOG1) ;
   'SI' LOG1 ;
   'DESS'  (EVEXT ET EVINT ET EVCIN ET EVBILAN) TTABD 'LEGE' ;
   'FINSI';
  'FINSI';
 'FINSI' ;
'FIN' BOU3 ;


*** BILAN TOTAL POUR UNE BASE ;
  'SI' (&BOU1 'EGA' 1) ;
    EVEXTT = EVEXT ; EVINTT = EVINT ; EVCINT = EVCIN ;
  'SINON' ;
    EVEXTT = EVEXTT + EVEXT ; EVINTT = EVINTT + EVINT ;
    EVCINT = EVCINT + EVCIN ;
  'FINSI';

'FIN' BOU1 ;

TEXT.&BOU4 = EVEXTT;TINT.&BOU4 = EVINTT;TCIN.&BOU4 = EVCINT;
'FIN' BOU4 ;

*** BILAN TOTAL POUR L'ENSEMBLE DE BASES ;
'REPETER' BOU4 IDIM ;
  'SI' (&BOU4 'EGA' 1) ;
    EVEXTT = TEXT.&BOU4 ;EVINTT =TINT.&BOU4 ;EVCINT = TCIN.&BOU4 ;
  'SINON' ;
    EVEXTT = EVEXTT + TEXT.&BOU4  ; EVINTT = EVINTT + TINT.&BOU4 ;
    EVCINT = EVCINT + TCIN.&BOU4 ;
 'FINSI';
'FIN' BOU4 ;

'TITRE' 'BILAN TOTAL';
EVBILAN = EVEXTT - EVINTT - EVCINT ;
EVBILAN = 'COUL' 'ROUG' EVBILAN ;
'DESS'  (EVEXTT ET EVINTT ET EVCINT ET EVBILAN) TTABD 'LEGE' ;

'FINP' ;
$$$$


