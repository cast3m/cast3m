$$$$ MULTIREC
* MULTIREC  PROCEDUR  PICA      92/07/03    21:27:30     619
*----------------------------------------------------------------------*
*                                                                      *
* PROCEDURE MULTIREC                                                   *
*                                                                      *
* CALCUL DE LA RECOMPOSITION D'UN SIGNAL A PARTIR D' UNE SEQUENCE DE   *
*        FILTRE ONDELETTE ORTHOGONALE                                  *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* ENTREE                                                               *
*                                                                      *
* COARSE X/Y CONTENANT LE SIGNAL DE BASE (COARSE SIGNAL). DX DOIT ETRE *
*        CONSTANT DANS LE PROCESSUS DE DECOMPOSITION DIADIC.           *
*                                                                      *
* ONDELL X/Ys CONTENANT LA DECOMPOSITION EN ONDELETTE DANS L'ORDRE DES *
*        BANDES DE FREQUENCE CROISSANTE                                *
*                                                                      *
* H      Y CONTENANT LE FILTRE (NON FORCEMENT SYMETRIQUE).             *
* MMH    POINTS NEGATIFS (MMH<0--->SYMETRIQUE)                         *
*                                                                      *
* G      Y CONTENANT LE FILTRE ORTHOGONAL (NON FORCEMENT SYMETRIQUE).  *
* MMG    POINTS NEGATIFS (MMG<0--->SYMETRIQUE)                         *
*                                                                      *
*                                                                      *
* OPTIO  MOT CONTENANT LE MODE DE CORRELATION (DEFAUT='SYME')          *
*                                                                      *
* N      NOMBRE DE NIVEAU DE RECOMPOSITION DESIRE                      *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* SORTIE                                                               *
*                                                                      *
* M      NOMBRE DE NIVEAU EFFECTIVEMENT ATTEINT                        *
*                                                                      *
* SIGNAL SIGNAL RECOMPOSE                                              *
*----------------------------------------------------------------------*
'DEBPROC' MULTIREC COARSE*'EVOLUTION' ONDELL*'EVOLUTION'
                        H*'LISTREEL'   MMH*'ENTIER'
                        G*'LISTREEL'   MMG*'ENTIER'
                    OPTIO/'MOT'            N/'ENTIER';
*----------------------------------------------------------------------*
YPHI='COPIER' ('EXTRAIRE' COARSE 'ORDO' 1);
*
NPT='DIMENSION' YPHI;
NPTT='DIMENSION' ('EXTRAIRE' ONDELL 'ABSC' 1);
'SI' (NPT 'NEG' NPTT);
  'MESSAGE' '*** nb de point COARSE neg nb de point ONDELL(1)';
  'MESSAGE' '--> pas de calcul';
  M=0;
'SINON';
  M='DIMENSION' ONDELL;
'FINSI';
*
'SI' (MMH '<' 0); MMHT='TEXT' ' ';
                  MMH=('DIMENSION' H) - 1;
                  MPH= MMH;
'SINON';          MMHT='TEXT' NPNE MMH;
                  MPH=('DIMENSION' H) - (MMH+1);
'FINSI';
*
'SI' (MMG '<' 0); MMGT='TEXT' ' ';
                  MMG=('DIMENSION' G) - 1;
                  MPG=MMG;
'SINON';          MMGT='TEXT' NPNE MMG;
                  MPG=('DIMENSION' G) - (MMG+1);
'FINSI';
*
NNPT=2*NPT;
'SI' ((MMG '>' NNPT) 'OU' (MPG '>' NNPT));
  'MESSAGE' '*** nb de point insuffisant pour Phi au niveau 1';
  'MESSAGE' '--> pas de calcul';
  M=0;
'FINSI';
'SI' ((MMH '>' NNPT) 'OU' (MPH '>' NNPT));
  'MESSAGE' '*** nb de point insuffisant pour Psi au niveau 1';
  'MESSAGE' '--> pas de calcul';
  M=0;
'FINSI';
*
'SI' (M 'NEG' 0);
  'SI' (('NON' ('EXISTE' OPTIO)));
               OPTIO='TEXT' SYME; 'FINSI';
  'SI' (('NON' ('EXISTE' N))); N=1000; 'FINSI';
  'SI' (M > N); M=N; 'FINSI';
*
  I=0;
  'REPETER' LAB1 M;
*
    I=I+1;
    YPSI='EXTRAIRE' ONDELL 'ORDO' I;
*
    YYPSI='DIADIQUE' YPSI 'INVE';
    YYPHI='DIADIQUE' YPHI 'INVE';
    YPSI ='CVOLUTION' YYPSI G MMGT OPTIO;
    YPHI ='CVOLUTION' YYPHI H MMHT OPTIO;
    YYPHI=2 * (YPSI + YPHI);
    YPHI=YYPHI;
*
  'FIN' LAB1;
'FINSI';
*
XXXX='EXTRAIRE' ONDELL 'ABSC' M;
XX  ='DIADIQUE' XXXX 'IVIN';
'TITRE' 'Signal recompose sur ' M ' niveaux';
SIGNAL='EVOLUTION' 'MANUEL' 'temps' XX 'Y' YPHI;
'FINPROC' M SIGNAL;
