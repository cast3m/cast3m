$$$$ @PLOTPRI
* @PLOTPRI  PROCEDUR  AM1       92/10/29    21:15:39     734
*-----------------------------------------------------
DEBPROC @PLOTPRI TEN1*MCHAML  MSU1*MMODEL               ;
*-----------------------------------------------------
*-----------------------------------------------------
MESS '                                                ' ;
MESS ' ---------------------------------------------- ' ;
MESS ' PROCEDURA PER GRAFICARE LE TENSIONI PRINCIPALI ' ;
MESS ' E GLI ISOVALORI                                ' ;
MESS ' ---------------------------------------------- ' ;
MESS '                                                ' ;
SU1 = EXTR TEN1 MAIL ;
LTE1= SU1 ELEM TYPE  ;
NSOT= DIME LTE1 ;
TX1 = TABLE;
TM1 = TABLE;
MC1 = TABLE;
TX2 = TABLE;
TM2 = TABLE;
MC2 = TABLE;
VM1 = VMIS TEN1 MSU1   ;
TG1 = CHAN GRAVITE MSU1 TEN1   ;
TP1 = PRIN TG1 MSU1 ; DETR TG1 ;
*-----------------------------------------------------------
NS=0;
REPETER BOOP9 NSOT ;
NS=NS+1 ;
TX1.NS = TABLE;
TM1.NS = TABLE;
MC1.NS = MOTS ;
TX2.NS = TABLE;
TM2.NS = TABLE;
MC2.NS = MOTS ;
MNS  = SU1 ELEM ( EXTR LTE1 NS ) ;
TTT  = REDU TP1 MNS  ;
NNS  = NBEL MNS ;
N = 0 ;
REPETER BOOP1 NNS   ;
N=N + 1 ;
EN   = MNS ELEM N;
GN   = BARY  EN  ; DETR EN;
SN11 = EXTR TTT SI11 1 N 1;
SN22 = EXTR TTT SI22 1 N 1;
CNX1 = EXTR TTT COX1 1 N 1;
CNY1 = EXTR TTT COY1 1 N 1;
CNX2 = EXTR TTT COX2 1 N 1;
CNY2 = EXTR TTT COY2 1 N 1;
TX1.NS.N= FORCE (( SN11 * CNX1 ) ( SN11 * CNY1 )) GN  ;
TM1.NS.N= -1. * TX1.NS.N;
TX2.NS.N= FORCE (( SN22 * CNX2 ) ( SN22 * CNY2 )) GN  ;
TM2.NS.N= -1. * TX2.NS.N;
SI( SN11 > 0. );
M11=MC1.NS;
MC1.NS=INSER M11 N 'ROUG'; DETR M11;
SINON;
M11=MC1.NS;
MC1.NS=INSER MC1.NS N 'VERT'; DETR M11;
FINSI ;
SI( SN22 > 0. );
M22=MC2.NS;
MC2.NS=INSER M22 N 'ROUG'; DETR M22;
SINON;
M22=MC2.NS;
MC2.NS=INSER M22 N 'VERT'; DETR M22;
FINSI ;
FIN BOOP1 ;
DETR TTT ;
FIN BOOP9 ;
*-----------------------------------------------------------
REPETER BOOP2          ;
MESS ' FORNIRE FATTORE DI AMPLIFICAZIONE PER LE  ';
MESS ' TENSIONI PRINCIPALI (<0. PER USCIRE) ?    ';
OBTENIR AX*FLOTTANT ;
SI(AX < 0. ); QUITTER BOOP2 ; FINSI ;
LL=VRAI ;
NS=0 ;
REPETER BOOP8 NSOT ;
NS=NS+1 ;
N=0;
NNS= DIME   MC1.NS     ;
REPETER BOOP3 NNS      ;
N=N+1;
XX1= EXTR MC1.NS N  ; XX2 = EXTR MC2.NS N ;
VX1 = (VECT TX1.NS.N AX FX FY XX1) ET (VECT TM1.NS.N AX FX FY XX1) ET
      (VECT TX2.NS.N AX FX FY XX2) ET (VECT TM2.NS.N AX FX FY XX2)  ;
SI LL     ;
LL=FAUX   ;
VXT = VX1 ;
SINON ;
VXT = VXT ET VX1 ;
FINSI ;
FIN BOOP3;
FIN BOOP8;
CC1= CONT SU1 ;
MESS ' 1) ISOVMISES A LINEE                                     ' ;
MESS ' 2) ISOVMISES A LINEE        + TENS. PRINCIPALI           ' ;
MESS ' 3) ISOVMISES A LINEE        + TENS. PRINCIPALI + MAGLIA  ' ;
MESS ' 4) ISOVMISES A COLORE PIENO                              ' ;
MESS ' 5) ISOVMISES A COLORE PIENO + TENS. PRINCIPALI           ' ;
MESS ' 6) ISOVMISES A COLORE PIENO + TENS. PRINCIPALI + MAGLIA  ' ;
MESS ' 7)                            TENS. PRINCIPALI           ' ;
MESS ' 8)                            TENS. PRINCIPALI + MAGLIA  ' ;
MESS ' ? ' ;
OBTENIR T1*ENTIER ;
SI( T1 EGA 1)               ;
 OPTI ISOV LIGNE            ;
 TRAC SU1 MSU1 VM1 CC1      ;
SINON                       ;
 SI( T1 EGA 2)              ;
  OPTI ISOV LIGNE           ;
   TRAC SU1 MSU1 VM1 VXT CC1;
 SINON                      ;
  SI( T1 EGA 3)             ;
   OPTI ISOV LIGNE          ;
   TRAC SU1 MSU1 VM1 VXT         ;
  SINON                     ;
   SI( T1 EGA 4)            ;
    OPTI ISOV SURFACE       ;
    TRAC SU1 MSU1 VM1 CC1        ;
   SINON                    ;
    SI( T1 EGA 5)           ;
     OPTI ISOV SURFACE      ;
     TRAC SU1 MSU1 VM1 VXT CC1   ;
    SINON                   ;
     SI( T1 EGA 6)          ;
      OPTI ISOV SURFACE     ;
      TRAC SU1 MSU1 VM1 VXT      ;
     SINON                  ;
      SI( T1 EGA 7)         ;
       TRAC CC1     VXT     ;
      SINON                 ;
       TRAC SU1 VXT         ;
      FINSI                 ;
     FINSI                  ;
    FINSI                   ;
   FINSI                    ;
  FINSI                     ;
 FINSI                      ;
FINSI                       ;
FIN BOOP2;
*---------------------------------------------------------------
NS=0 ;
REPETER BOOP7 NSOT ;
NS=NS+1;
NNS=DIME MC1.NS ;
DETR MC1.NS ;
DETR MC2.NS ;
N=0;
REPETER BOOP4 NNS  ;
N=N+1 ;
DETR  TX1.NS.N ;
DETR  TM1.NS.N ;
DETR  TX2.NS.N ;
DETR  TM2.NS.N ;
FIN BOOP4;
FIN BOOP7;
DETR VM1 ;
DETR TP1 ;
FINPROC ;
