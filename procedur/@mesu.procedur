$$$$ @MESU
* @MESU     PROCEDUR  FANDEUR   14/10/10    21:15:01     8178
*-------------------------------------------------
******          PROCEDURE @MESU             ******
*-------------------------------------------------
*
*
*    CETTE PROCEDURE A ETE MISE GRACIEUSEMENT
*   A DISPOSOTION DE LA COMMUNAUTE  CASTEM2000
*    PAR M. LIBEYRE ( CEA/DSM/DRFC )
*
*     TEL : ( 33 1 ) 42 25 46 03
*
*-------------------------------------------------
*
* MESURE D'UN MAILLAGE (LONGUEUR, AIRE OU VOLUME)
*
*--------------------------------------------------------
DEBPROC @MESU GEO*MAILLAGE ;
 LTY = GEO ELEM TYPE ;
 ITCAR=FAUX;
*  mess 'liste des types d elements :' ;
*  list LTY ;
 NTY = DIME LTY ;
 I = 0 ;
 E1 = FAUX ;
 E2 = FAUX ;
 E3 = FAUX ;
 REPETER BOUC NTY ;
 I = I + 1 ;
 ITY = EXTR LTY I ;
 EE1 = (EGA ITY SEG2) OU (EGA ITY SEG3) ;
 EE2 = (EGA ITY TRI3) OU (EGA ITY TRI6) OU (EGA ITY QUA4)
      OU (EGA ITY QUA8) ;
 EE3 = (EGA ITY CU20) OU (EGA ITY PRI6) OU (EGA ITY PR15)
      OU (EGA ITY PYR5) OU (EGA ITY PY13) OU (EGA ITY TET4)
      OU (EGA ITY TE10) ;
 E1 = E1 OU EE1 ;
 E2 = E2 OU EE2 ;
 E3 = E3 OU EE3 ;
 FIN BOUC ;
SI E1;
* MESS 'MAILLAGE DE TYPE LIGNE' ;
FINSI ;
SI E2;
* MESS 'MAILLAGE DE TYPE SURFACE' ;
FINSI ;
SI E3;
* MESS 'MAILLAGE DE TYPE MASSIF' ;
FINSI ;
SI ((E1 ET E2) OU (E1 ET E3) OU (E2 ET E3)) ;
 MESS 'LE MAILLAGE DOIT ETRE D UN TYPE UNIQUE : LIGNE, '
      'SURFACE OU MASSIF' ;
 P1 =0. ;
SINON ;
 SI E1 ;
  IMA = 1 ;
 FINSI ;
 SI E2 ;
  IMA = 2 ;
 FINSI ;
 SI E3 ;
  IMA = 3 ;
 FINSI ;
* MESS 'DIMENSION DE GEO :';
* LIST IMA ;
 SI (EGA IMA 1) ;
*  MESS 'CALCUL DE LA LONGUEUR' ;
  GEO1 = CHANGER GEO SEG2 ;
  MODL1 = MODE GEO1 MECANIQUE  ELASTIQUE BARR ;
  MAT1 = MATE MODL1 'RHO' 1. ;
  CAR1 = CARA MODL1 'SECT' 1.  ;
  ITCAR=VRAI;
 FINSI ;
 SI (EGA IMA 2) ;
*  MESS 'CALCUL DE L AIRE' ;
  I = 0 ;
  REPETER BOUC2 NTY ;
  I = I + 1 ;
  GEOT =  GEO ELEM (EXTR LTY I) ;
  SI (EGA I 1) ;
   GEO1 = CHANGER GEOT TRI3 ;
  SINON ;
   GEO1 = GEO1 ET (CHANGER GEOT TRI3) ;
  FINSI ;
  FIN BOUC2 ;
  IDI = VALEUR DIME ;
  SI (EGA IDI 2) ;
  MODL1 = 'MODE' GEO1 MECANIQUE  ELASTIQUE TRI3 ;
  SINON ;
  MODL1 = 'MODE' GEO1 MECANIQUE  ELASTIQUE COQ3 ;
  CAR1 = 'CARA' MODL1 'EPAI' 1.  ;
  ITCAR=VRAI;
  FINSI ;
  MAT1 = 'MATE' MODL1 'RHO' 1. ;
 FINSI ;
 SI (EGA IMA 3) ;
*  MESS 'CALCUL DU VOLUME' ;
  I = 0 ;
  REPETER BOUC3 NTY ;
  I = I + 1 ;
  GEOT =  GEO ELEM (EXTR LTY I) ;
  SI (EGA I 1) ;
   GEO1 = CHANGER GEOT TET4 ;
  SINON ;
   GEO1 = GEO1 ET (CHANGER GEOT TET4) ;
  FINSI ;
  FIN BOUC3 ;
  MODL1 = 'MODE' GEO1 MECANIQUE  ELASTIQUE 'TET4' ;
  MAT1 = 'MATE' MODL1 'RHO' 1. ;
 FINSI ;
SI (IMA < 3) ;
   SI ITCAR ;
     MASS1 = MASSE MODL1 (MAT1 ET CAR1) ;
   SINON ;
    MASS1 = MASSE MODL1 MAT1  ;
   FINSI ;
SINON ;
 MASS1 = MASSE MODL1 MAT1  ;
FINSI ;
GEO2 = CHANGER GEO POI1 ;
GAM1 = MANU CHPO GEO2 1 UX 1. ;
POIP = MASS1 * GAM1 ;
P1 = @TOTAL POIP GEO2 FX ;
FINSI;
FINPROC P1 ;

