$$$$ CRITLOC
* CRITLOC   PROCEDUR  PV        17/11/16    11:23:51     9622
'DEBPROC' CRITLOC ETAB*'TABLE';
*----------------------------------------------------------------------
*    CRITERES LOCAUX POUR LA MECANIQUE DE LA RUPTURE
*       POUR LES ELEMENTS MASSIFS UNIQUEMENT
*
* ENTREES
*   ETAB.'TNONL'   =  TABLE RESULTAT DU CALCUL NONLINEAIRE
*   ETAB.'OBJMO'   =  OBJET MODEL CORRESPONDANT A LA ZONE GEOMETRIQUE E
*      OU L'ON VEUT APPLIQUER LE CRITERE
*    ** SI CRITERE DE RICE POUR LA RUPTURE DUCTILE
*   ETAB.'RICE'    =  VRAI
*   ETAB.'EPSILON' =  VRAI SI DECOHESION CONTROLEE PAR LA DEFORMATION
*   ETAB.'SIGMA'   =  VRAI SI DECOHESION CONTROLEE PAR LA CONTRAINTE
*         +   DONNEES DES PARAMETRES DU CRITERE
*       SI DECOHESION CONTROLEE PAR LA DEFORMATION
*   ETAB.'EPSC', ETAB.'BETA', ETAB.'ALPHA'
*       SI DECOHESION CONTROLEE PAR LA CONTRAINTE
*   ETAB.'BETA', ETAB.'ALPHA',ETAB.'LAMBDA', ETAB.'SIG0',ETAB.'SIGC'
*     ** SI CRITERE DE WEIBULL POUR LA RUPTURE PAR CLIVAGE
*   ETAB.'WEIBULL' =  VRAI
*         +   DONNEES DES PARAMETRES DU CRITERE
*   ETAB.'M', ETAB.'SIGU',ETAB.'V0',ETAB.'IC'
*         + (SI IC=1) ETAB.'N'
*         + (SI CAS PLAN) ETAB.'EPAI'
*   ETAB.'TEMPER'
*         +(SI TEMPER=0)  SIGU EST UNE CONSTANTE
*         +(SI TEMPER=1)  SIGU DEPEND DE LA TEMPERATURE
*                         + ETAB.'TEREF'
*
* SORTIES
*     TABLE DE TABLES DONNANT POUR CHAQUE PAS DE TEMPS
*     ** SI CRITERE DE RICE POUR LA RUPTURE DUCTILE
*         VALEURS MOYENNEES SUR LES ELEMENTS DE
*   STAB.'RAPPORT'  =  TAUX DE TRIAXIALITE (CHELEM)
*   STAB.'TAUX'     =  TAUX DE CROISSANCE  (CHELEM)
*   STAB.'SIGEQ'    =  CONTRAINTE DE VMIS  (CHELEM)
*   STAB.'EPSEQ'    =  DEFOR. PLAST. CUM.  (CHELEM)
*     ** SI CRITERE DE WEIBULL POUR LA RUPTURE PAR CLIVAGE
*   STAB.'SIGW'     =  CONTRAINTE DE WEIBULL  (FLOTTANT)
*   STAB.'PROB'     =  PROBABILITE DE RUPTURE (FLOTTANT)
*   STAB.'VPLAS'    =  VOLUME PLASTIQUE       (FLOTTANT)
*   STAB.'SIGPMAX'  =  CONTRAINTE PRINCIPALE MAXIMALE  (FLOTTANT)
*   STAB.'ELT'      =  ELEMENT OU LA CONT. PRIN. EST MAXIMALE (MAILLAGE)
*   STAB.'EPSEQ'    =  DEFORM. PLAST CUMULEE DS L'ELEMENT ELT (FLOTTANT)
*   passage aux nouveaux chamelems P DOWLATYARI JAN. 92
*-----------------------------------------------------------------------

IWEIB=0;IREPS=0;IRSIG=0;
TENTR=ETAB.'TNONL';
R=1.D0 / 3.D0;2PI=2.D0 * PI;MOD='VALE' 'MODE';

*- transformation des sorties de l'ancien
*- NONLIN au format de PASAPAS

TEMPS1 = TENTR.'TEMPS';
TCONT1 = TENTR.'CONTRAINTES';
TEPS1 = TENTR.'VARIABLES_INTERNES';
TEPSP1 = TENTR.'DEFORMATIONS_INELASTIQUES';
CHAR1 = TENTR.'CHARGEMENT';
NPAS = 'DIME' TEMPS1;
TCONT = 'TABLE';
TEPS = 'TABLE';
TEPSP = 'TABLE';
CHATOT = 'TABLE';
TIND = PROG;
*
*
'REPETER' BOU_MAN NPAS;
    ITE1 = &BOU_MAN - 1 ;
    TCONT . (TEMPS1.ITE1) = TCONT1.ITE1;
    TEPS . (TEMPS1.ITE1) = TEPS1.ITE1;
    TEPSP . (TEMPS1.ITE1) = TEPSP1.ITE1;
*
    TIND = TIND 'ET' (PROG (TEMPS1.ITE1));
'FIN' BOU_MAN;
*
*
'SI' ( 'NON' ('EXISTE' ETAB 'OBJMO') ) ;
    'ERREUR'  'IL FAUT DONNER L OBJET MODEL ' ;
    'QUITTER' CRITLOC2  ;
'FINSI';
OBJMO=ETAB.'OBJMO';
*
STAB='TABL';
*   LECTURE DES PARAMETRES DU CRITERE ET QUELQUES INITIALISATIONS

LOG='NON' (('EXISTE' ETAB 'RICE') OU  ('EXISTE' ETAB 'WEIBULL'));
'SI' LOG;
    'MESS' 'IL FAUT DEFINIR LE TYPE DE CRITERE : RICE OU WEIBULL';
    'QUITTER' CRITLOC2;
'FINSI';

MAIL1 = 'EXTR' OBJMO 'MAIL'   ;
'SI' ('EXISTE' ETAB 'RICE');
    LOG='NON' (('EXISTE' ETAB 'EPSILON') OU  ('EXISTE' ETAB 'SIGMA'));
    'SI' LOG;
        'MESS'
        'POUR LE CRITERE DE RICE ,IL FAUT DEFINIR L OPTION: EPSILON OU SIGMA';
        'QUITTER' CRITLOC2;
    'FINSI';

    'SI' ('EXISTE' ETAB 'EPSILON');
        IREPS=1;
        VALEPC=ETAB.'EPSC';VALBET=ETAB.'BETA';VALALP=ETAB.'ALPHA';
        EPSCX='MANU' 'CHPO' MAIL1 1 'SCAL' VALEPC;
        EPSC = 'CHAN' 'CHAM' OBJMO EPSCX 'STRESSES' 'SCALAIRE'   ;
        'DETR' EPSCX ;
    'FINSI';

    'SI' ('EXISTE' ETAB 'SIGMA');
        IRSIG=1;
        VALBET=ETAB.'BETA';VALALP=ETAB.'ALPHA';
        VALLAM=ETAB.'LAMBDA';VALSI0=ETAB.'SIG0';VALSIC=ETAB.'SIGC';
        CHSIGX='MANU' 'CHPO' MAIL1 1  'SCAL' VALSI0;
        CHSIG0 = 'CHAN' 'CHAM'  OBJMO CHSIGX 'STRESSES' 'SCALAIRE' ;
        'DETR'  CHSIGX ;
        CHSIGX='MANU' 'CHPO' MAIL1 1  'SCAL' VALSIC;
        CHSIGC = 'CHAN' 'CHAM'  OBJMO CHSIGX 'STRESSES' 'SCALAIRE' ;
        'DETR' CHSIGX ;
        SIGD0='ZERO' OBJMO 'STRESSES';
        STAB.'CONTDECO'='TABL';STAB.'CPRINMAX'='TABL';
    'FINSI';

    STAB.'RAPPORT'='TABL';STAB.'TAUX'='TABL';
    STAB.'SIGEQ'='TABL';STAB.'EPSEQ'='TABL';
    SIG0X='MANU' 'CHPO' MAIL1 1  'SCAL' 1.D-10;
    SIG0 = 'CHAN' 'CHAM'  OBJMO SIG0X 'STRESSES' 'SCALAIRE' ;
    'DETR' SIG0X ;
    EPS00='COPI' SIG0;
    EPS0='ZERO' OBJMO 'STRESSES';SIGS0='COPI' EPS0;SOMR0='COPI' EPS0;
'FINSI';

'SI' ('EXISTE' ETAB 'WEIBULL');
    IWEIB=1;
    VALM=ETAB.'M';VALSU=ETAB.'SIGU';VALV0=ETAB.'V0';
    VALIC=ETAB.'IC';
    VALTP=ETAB.'TEMPER';
*
    'SI' (VALTP 'EGA' 1);
        'REPETER' BOW_MAN NPAS;
            ITE1 = &BOW_MAN - 1 ;
            CHATOT . (TEMPS1.ITE1) =  (tire char1
            (TEMPS1.ITE1)  'T' );
        'FIN' BOW_MAN;
*
*temperature de reference pour le calcul de la contrainte de Weibull
        TEREF = ETAB.'TEREF';
*
*verification que cette temperature figure dans temperature de VALSU
        SAB1 = 'EXTR' VALSU 'ABSC';
        TAL1 = 'MINI' SAB1;
        TAL2 = 'MAXI' SAB1;
*
        'SI'(TEREF <EG TAL2);
            'SI' (TEREF < TAL1);
                'MESS'   'ERREUR:  'TEREF' HORS DES TEMPERATURES DONNEES DANS VALSU';
                'QUITTER' CRITLOC2  ;
            'FINSI';
        'SINON';
            'MESS'   'ERREUR:  'TEREF' HORS DES TEMPERATURES DONNEES DANS VALSU';
            'QUITTER' CRITLOC2  ;
        'FINSI';
*
    'FINSI';
'FINSI';
*
*
'SI' (('EGA' MOD 'PLANCONT') 'OU' ('EGA' MOD 'PLANDEFO'));
    PARA=ETAB.'EPAI';
'FINSI';
'SI' (('EGA' MOD 'AXIS') 'OU' ('EGA' MOD 'FOUR'));
    PARA=1.D0;
'FINSI';
'SI' ('EGA' MOD 'TRID');
    PARA=1.D0;
'FINSI';
FONCPX='MANU' 'CHPO' MAIL1 1  'SCAL' PARA;
FONCP = 'CHAN' 'CHAM' OBJMO FONCPX 'STRESSES' 'SCALAIRE' ;
'DETR' FONCPX ;
STAB.'SIGW'='TABL';
STAB.'PROB'='TABL';
STAB.'VPLAS'='TABL';

STAB.'SIGPMAX'='TABL';
STAB.'EPSEQ'='TABL';
STAB.'ELT'='TABL';

'SI' (IWEIB 'EGA' 1);
    'SI' (VALIC 'EGA' 1);

        VALN=ETAB.'N';
        TMEP='TABL';
        'SI' (('EGA' MOD 'PLANCONT') 'OU' ('EGA' MOD 'PLANDEFO')'OU' ('EGA' MOD 'TRID'));
            TMEP.1='EIXX';TMEP.2='EIYY';TMEP.3='EIZZ';TMEP.4='GIXY';
            'SI' ('EGA' MOD 'TRID');
                TMEP.5='GIXZ';
                TMEP.6='GIYZ';
            'FINSI';
        'FINSI';
        'SI' (('EGA' MOD 'AXIS') 'OU' ('EGA' MOD 'FOUR'));
            TMEP.1='EIRR';TMEP.2='EIZZ';TMEP.3='EITT';TMEP.4='GIRZ';
            'SI' ('EGA' MOD 'FOUR');
                TMEP.5='GIRT';
                TMEP.6='GIZT';
            'FINSI';
        'FINSI';
    'FINSI';
'FINSI';

FONC1X='MANU' 'CHPO' MAIL1 1  'SCAL' 1.;
FONC1 = 'CHAN' 'CHAM' OBJMO FONC1X 'STRESSES' 'SCALAIRE' ;
'DETR' FONC1X ;
FONCEX='MANU' 'CHPO' MAIL1 1  'SCAL' 1.E-20;
FONCE = 'CHAN' 'CHAM' OBJMO FONCEX 'STRESSES' 'SCALAIRE' ;
'DETR' FONCEX ;
FVOLU=(('INTG' 'ELEM' OBJMO FONC1) ** -1)  CHAN  'STRESSES' OBJMO;
TMOT='TABL';
'SI' (('EGA' MOD 'PLANCONT') 'OU' ('EGA' MOD 'PLANDEFO')
    'OU' ('EGA' MOD 'TRID'));
    TMOT.1='SMXX';TMOT.2='SMYY';TMOT.3='SMZZ';TMOT.4='SMXY';
    'SI' ('EGA' MOD 'TRID');
        TMOT.5='SMXZ';TMOT.6='SMYZ';
    'FINSI';
'FINSI';
'SI' (('EGA' MOD 'AXIS') 'OU' ('EGA' MOD 'FOUR'));
    TMOT.1='SMRR';TMOT.2='SMZZ';TMOT.3='SMTT';TMOT.4='SMRZ';
    'SI' ('EGA' MOD 'FOUR');
        TMOT.5='SMRT';TMOT.6='SMZT';
    'FINSI';
'FINSI';
*
*   BOUCLE SUR LES PAS DE TEMPS
*
IPAS=0;
'REPETER' BOUCLE NPAS;
    IPAS=IPAS + 1;
    TEMPS='EXTR' IPAS TIND;
    SIG=TCONT.TEMPS;CEPS=TEPS.TEMPS;EPSP=TEPSP.TEMPS;
    SIG='REDU' SIG OBJMO;CEPS='REDU' CEPS OBJMO;EPSP='REDU' EPSP OBJMO;
    EPS=('EXCO' 'EPSE' CEPS) 'CHAN' 'TYPE' 'SCALAIRE';
*

*   MOYENNATION DES CONTRAINTES SUR LES ELEMENTS

    COEFP=EPS 'MASQ' 'EGSUPE' 1.D-8;
    FVOLUP=((('INTG' 'ELEM' OBJMO (FONC1 * COEFP)) 'CHAN' 'STRESSES' OBJMO)
    + FONCE) ** -1;
*
    'SI' (IWEIB 'EGA' 1);
        SIG=SIG * COEFP;FVOLU=FVOLUP;
    'FINSI';

    SIG1=('INTG' 'ELEM' OBJMO SIG (TMOT.1) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
    SIG1=('EXCO' SIG1  'SCAL' 'NOID'(TMOT.1)) 'CHAN' 'TYPE' 'SCALAIRE' ;
    SIG2=('INTG' 'ELEM' OBJMO SIG (TMOT.2) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
    SIG2=('EXCO' SIG2  'SCAL' 'NOID'(TMOT.2)) 'CHAN' 'TYPE' 'SCALAIRE'  ;
    SIG3=('INTG' 'ELEM' OBJMO SIG (TMOT.3) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
    SIG3=('EXCO' SIG3  'SCAL' 'NOID'(TMOT.3))'CHAN' 'TYPE' 'SCALAIRE' ;
    SIG4=('INTG' 'ELEM' OBJMO SIG (TMOT.4) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
    SIG4=('EXCO' SIG4  'SCAL' 'NOID'(TMOT.4)) 'CHAN' 'TYPE' 'SCALAIRE' ;
    'SI' (('EGA' MOD 'TRID') 'OU' ('EGA' MOD 'FOUR'));
        SIG5=('INTG' 'ELEM' OBJMO SIG (TMOT.5) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
        SIG5=('EXCO' SIG5  'SCAL' 'NOID'(TMOT.5))'CHAN' 'TYPE' 'SCALAIRE' ;
        SIG6=('INTG' 'ELEM' OBJMO SIG (TMOT.6) 'CHAN' 'STRESSES' OBJMO) * FVOLU;
        SIG6=('EXCO' SIG6  'SCAL' 'NOID'(TMOT.6))'CHAN' 'TYPE' 'SCALAIRE' ;
    'FINSI';

*
    'SI' (('EGA' MOD 'PLANCONT') 'OU' ('EGA' MOD 'PLANDEFO')
        'OU' ('EGA' MOD 'AXIS'));
        SIGMOY=SIG1 ET SIG2 ET SIG3 ET SIG4 ;
        SIGMOY='CHAN' 'TYPE' SIGMOY 'CONTRAINTES' ;
    'FINSI';
    'SI' (('EGA' MOD 'TRID') 'OU' ('EGA' MOD 'FOUR'));
        SIGMOY=SIG1 ET SIG2 ET SIG3 ET SIG4 ET SIG5 ET SIG6 ;
        SIGMOY='CHAN' 'TYPE' SIGMOY 'CONTRAINTES' ;
    'FINSI' ;

*   SIGMOY etant au centre de gravite des elements
*   on le change en 'STRESSES' pour avoir compatibilite
*   entre les supports pour le calcul du taux de triaxialite
    SIGMOY = SIGMOY 'CHANG' 'STRESSES' OBJMO;

*   MOYENNATION DES DEFORMATIONS PLASTIQUES CUMULEES
    EPS=('INTG' 'ELEM' OBJMO EPS) 'CHAN' 'STRESSES' OBJMO;
    EPS= EPS * FVOLUP;
*EPS = EPS 'CHANG' 'STRESSES' OBJMO;

*   PLUS GRANDE CONTRAINTE PRINCIPALE  (WEIBULL ET RICE SIGMA)

    'SI' ((IRSIG 'EGA' 1) 'OU' (IWEIB 'EGA' 1));
        PSIG='PRIN' OBJMO SIGMOY;
        PSI11=('EXCO' 'SI11' PSIG 'SCAL')'CHAN' 'TYPE' 'SCALAIRE';
        PSI22=('EXCO' 'SI22' PSIG 'SCAL')'CHAN' 'TYPE' 'SCALAIRE';
        PSI33=('EXCO' 'SI33' PSIG 'SCAL')'CHAN' 'TYPE' 'SCALAIRE';
        COEF1=PSI11 'MASQ' 'EGSUPE' PSI22;
        COEF2=PSI22 'MASQ' 'SUPERIEUR' PSI11;
        PSIAA=(PSI11 * COEF1) + (PSI22 * COEF2);
        COEF3=PSIAA 'MASQ' 'EGSUPE' PSI33;
        COEF4=PSI33 'MASQ' 'SUPERIEUR' PSIAA;
        SCOPR=(PSIAA * COEF3) + (PSI33 * COEF4);
    'FINSI';

*
*   CRITERE DE RICE : CALCUL DU TAUX DE TRIAXIALITE ET R/R0
*

    'SI' ((IREPS 'EGA' 1) 'OU' (IRSIG 'EGA' 1));

        VSIG='VMIS' OBJMO SIGMOY;
        VSIG='CHAN' 'TYPE' VSIG 'SCALAIRE' ;
*   CALCUL DE LA CONTRAINTE DE DECOHESION
        'SI' (IRSIG 'EGA' 1);
            SIGDEC = SCOPR + (VALLAM * (VSIG - CHSIG0));
        'FINSI';

*   CALCUL DU TAUX DE TRIAXIALITE
        CHEL1 CHEL2 CHEL3='INVA' OBJMO SIGMOY;
        SIGMV=R * CHEL1 * ((VSIG + SIG0) ** -1.);

*   CALCUL DE R / R0
*++ ON MET A ZERO LE TAUX DE TRIAXIALITE S IL EST NEGATIF
        SIGSG=SIGMV * (SIGMV 'MASQ' 'SUPERIEUR' 0.);

        T1='EXP' (VALBET * SIGS0);
        T2='EXP' (VALBET * SIGSG);

        'SI' (IRSIG 'EGA' 1);
            TDSIG=(EPS0 * (SIGDEC - CHSIGC)) + (EPS * (CHSIGC - SIGD0));
            IDSIG=((SIGDEC - SIGD0) + SIG0) ** -1;
            EPSC=TDSIG * IDSIG;
        'FINSI';

        TT=(T2 * (EPS - EPSC)) + (T1 * (EPSC - EPS0));
        IDEPS0=((EPS - EPS0) + EPS00) ** -1;
        TC=TT * IDEPS0;

        SS1=(TC + T2) * (EPS - EPSC);
        SS2=(T1 + T2) * (EPS - EPS0);

        'SI' (IREPS 'EGA' 1);
            COEF=EPS 'MASQ' 'SUPERIEUR' VALEPC;
        'FINSI';
        'SI' (IRSIG 'EGA' 1);
            COEF=SIGDEC 'MASQ' 'SUPERIEUR' VALSIC;
        'FINSI';
        'SI' (IREPS 'EGA' 1);
            COEF5=EPS0 'MASQ' 'EGSUPE' VALEPC;
            COEF6=EPS0 'MASQ' 'INFERIEUR' VALEPC;
        'FINSI';
        'SI' (IRSIG 'EGA' 1);
            COEF5=SIGD0 'MASQ' 'EGSUPE' VALSIC;
            COEF6=SIGD0 'MASQ' 'INFERIEUR' VALSIC;
        'FINSI';

        S=((SS1 * COEF6) + (SS2 * COEF5)) * COEF;
        SOMR0=SOMR0 + S;
        RSR0='EXP' ((0.5D0 * VALALP) * SOMR0);
        EPS0=EPS;SIGS0=SIGSG;
        'SI' (IRSIG 'EGA' 1);
            SIGD0=SIGDEC;
            STAB.'CONTDECO'.TEMPS=SIGDEC;
            STAB.'CPRINMAX'.TEMPS=SCOPR;
        'FINSI';

        STAB.'RAPPORT'.TEMPS=SIGMV;
        STAB.'TAUX'.TEMPS=RSR0;
        STAB.'SIGEQ'.TEMPS=VSIG;
        STAB.'EPSEQ'.TEMPS=EPS;
    'FINSI';

*
*   CRITERE DE WEIBULL : CALCUL DU VOLUME PLASTIQUE,
*           DE LA CONTRAINTE DE WEIBULL ET DE LA PROBABILITE DE RUPTURE
*

    'SI' (IWEIB 'EGA' 1);

*  CALCUL DU TERME CORR. SI EFFET DE LA DEFOR. PLAST. SUR LE CLIVAGE

*++DEFORMATION DANS LA DIRECTION DE LA PLUS GRANDE CONTRAINTE PRINCIPALE
        'SI' (VALIC 'EGA' 1);
            COXPR = ((((('EXCO' 'COX1' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF1) + ((('EXCO' 'COX2' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF2))        * COEF3) + ((('EXCO' 'COX3' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF4);
            COYPR = ((((('EXCO' 'COY1' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF1) + ((('EXCO' 'COY2' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF2))        * COEF3) + ((('EXCO' 'COY3' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF4);
            COZPR = ((((('EXCO' 'COZ1' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF1) + ((('EXCO' 'COZ2' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF2))        * COEF3) + ((('EXCO' 'COZ3' PSIG)'CHAN' 'TYPE' 'SCALAIRE') * COEF4);

            DEF11 = ('INTG' 'ELEM' OBJMO EPSP (TMEP.1)'CHAN' 'STRESSES' OBJMO) * FVOLU;
            DEF22 = ('INTG' 'ELEM' OBJMO EPSP (TMEP.2)'CHAN' 'STRESSES' OBJMO ) * FVOLU;
            DEF33 = ('INTG' 'ELEM' OBJMO EPSP (TMEP.3)'CHAN' 'STRESSES' OBJMO) * FVOLU;
            DEF12 = ('INTG' 'ELEM' OBJMO EPSP (TMEP.4)'CHAN' 'STRESSES' OBJMO) * FVOLU;
            DEF12=DEF12 / 2.;
            'SI' (('EGA' MOD 'TRID') 'OU' ('EGA' MOD 'FOUR'));
                DEF13=('INTG' 'ELEM' OBJMO EPSP (TMEP.5)'CHAN' 'STRESSES' OBJMO) * FVOLU;
                DEF13=DEF13 / 2.;
                DEF23=('INTG' 'ELEM' OBJMO EPSP (TMEP.6)'CHAN' 'STRESSES' OBJMO) * FVOLU;
                DEF23=DEF23 / 2.;
            'SINON';
                DEF13=DEF12 * 0.;
                DEF23=DEF12 * 0.;
            'FINSI';

*   DEF11 ... etant au centre de gravite des elements
*   on les change en 'STRESSES' pour avoir compatibilite
*   avec les supports de COXPR et COYPR et COZPR
            DEF11 = DEF11 CHANG 'STRESSES' OBJMO;
            DEF22 = DEF22 CHANG 'STRESSES' OBJMO;
            DEF33 = DEF33 CHANG 'STRESSES' OBJMO;
            DEF12 = DEF12 CHANG 'STRESSES' OBJMO;
            DEF13 = DEF13 CHANG 'STRESSES' OBJMO;
            DEF23 = DEF23 CHANG 'STRESSES' OBJMO;

            DFT1 = ((DEF11 * COXPR) + (DEF12 * COYPR)
            + (DEF13 * COZPR)) * COXPR;
            DFT2 = ((DEF12 * COXPR) + (DEF22 * COYPR)
            + (DEF23 * COZPR)) * COYPR;
            DFT3 = ((DEF13 * COXPR) + (DEF23 * COYPR)
            + (DEF33 * COZPR)) * COZPR;
            DESUSI = DFT1 + DFT2 + DFT3;

            SCOPR =SCOPR * ('EXP' ((-1. * VALIC / VALN) * DESUSI));
        'FINSI';

*   CALCUL DU VOLUME PLASTIQUE
        'SI' ( 'EXISTE'  ETAB 'XMULT');
            XMU1 = ETAB.'XMULT';
        'SINON';
            XMU1 = 1.;
            MESS 'AVEZ-VOUS TENU COMPTE DES SYMETRIES POUR LE CALCUL DU VOLUME PLASTIQUE?';
        'FINSI';
        VOLPLA = 'INTG' OBJMO   (FONCP * COEFP);
        VOLPLA = VOLPLA * XMU1;
*
*   CALCUL DE LA CONTRAINTE DE WEIBULL
*++ ON MET A ZERO LA + GRANDE CONTRAINTE PRINC. SI ELLE EST NEGATIVE
        COEFPR=SCOPR 'MASQ' 'EGSUPE' 0.;
        SCOPR=SCOPR * COEFPR * COEFP;
        SCOMAX='MAXI' SCOPR;
*
*
*
        'SI' (VALTP EGA 1);
*
* determination de la contrainte caracteristique de clivage
* a partir du champ de temperature : SIGU
*VALSU est une evolution
            chtemp = chatot.temps;
            SAB1 = 'EXTR' VALSU 'ABSC';
            SOR1 = 'EXTR' VALSU 'ORDO';
            chsigu = 'IPOL' chtemp SAB1 SOR1;
            chsu0 = 'EXCO' chsigu 'T' 'SCAL';
*determination de la valeur de sigu correspondant
*a la temperature de reference  teref
            VSUREF = 'IPOL' TEREF SAB1 SOR1;
*
        'SINON';
*VALSU est une constante
            VSUREF = VALSU;
            chsu0='MANU' 'CHPO' MAIL1 1  'SCAL' VALSU;
*
        'FINSI';
*
* determination du rapport PRIN_MAX/SIGU soit R(t)
        chsu1 = chsu0 ** (-1);
*chsu1 est un chpo
        chsu2 = 'CHAN' 'CHAM' OBJMO chsu1 'STRESSES' 'CONTRAINTES' ;
*chsu2 est un chaml
*
        XCOPR = SCOPR * chsu2;
*XCOPR est un chaml
*
*++ ON MET A ZERO LE RAPPORT R(t) SI IL EST NEGATIF
        COEFPR=XCOPR 'MASQ' 'EGSUPE' 0.;
        XCOPR=XCOPR * COEFPR * COEFP;
*
        FVOL1=('INTG' 'ELEM' OBJMO FONC1) ** -1;
*
        'SI' (&boucle EGA 1);
            XPS0 = ('EXCO' 'EPSE' CEPS) 'CHAN' 'TYPE' 'SCALAIRE';
*
*moyennation du champ de contraintes
            xcopr = ('INTG' 'ELEM' OBJMO xcopr) * FVOL1 ;
            CHX1 = XCOPR;
*
        'FINSI';
*
        'SI' (&boucle > 1);
* determination si plasticite active
            XPS = ('EXCO' 'EPSE' CEPS)  'CHAN' 'TYPE' 'SCALAIRE' ;
            CPL1 = XPS 'MASQ' 'SUPERIEUR' XPS0 ;
*on ne considere le rapport R(t) que si la plasticite est active
            CHA1 = XCOPR * CPL1;
*
*moyennation du champ de contraintes
            cha1 = ('INTG' 'ELEM' OBJMO cha1) * FVOL1 ;
*
*determination du max du rapport R sur tous les instants
*  [ 0, t ]  ou la plasticite a ete  ou est active
            COEF1 = CHA1 'MASQ' 'SUPERIEUR' CHX1;
            COEF2 = CHX1 'MASQ' 'EGSUPE' CHA1;
            XCOPR = (CHA1 * COEF1) + (CHX1 * COEF2);
*
            CHX1 = XCOPR;
            XPS0 = XPS;
*
        'FINSI';
*
        mcopr = 'MASQ'  xcopr 'EGSUPE' 0.;
        xcopr = ((xcopr * mcopr )'CHAN' 'STRESSES' OBJMO) + fonce;
*
*   CALCUL DE LA PROBABILITE DE RUPTURE
        XGALA = (('INTG' OBJMO (XCOPR ** VALM)) * XMU1 * PARA / VALV0);
        PROB = 1.D0 - ('EXP' ( (-1.D0)*XGALA ));
*
*Contrainte de Weibull a la temperature de reference teref
        SIGW = ( XGALA ** (1.D0 / VALM)) * VSUREF;


*
*   ELEMENT OU LA CONTRAINTE PRINCIPALE EST MAXIMALE
        IEMAX=SCOPR 'ELEM' 'MAXI' ;

*   DEFORMATION EQUIVALENTE ASSOCIEE A SIGPMAX
        SEPMAX='MAXI' ('REDU' EPS IEMAX);
*
        STAB.'SIGW'.TEMPS=SIGW;
        STAB.'VPLAS'.TEMPS=VOLPLA;
        STAB.'PROB'.TEMPS=PROB;
        STAB.'SIGPMAX'.TEMPS=SCOMAX;
        STAB.'ELT'.TEMPS=IEMAX;
        STAB.'EPSEQ'.TEMPS=SEPMAX;
    'FINSI';
    'MENA';
'FIN' BOUCLE;
'FINP' STAB;








