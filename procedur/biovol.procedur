$$$$ BIOVOL
* BIOVOL    PROCEDUR  FANDEUR   14/10/10    21:15:02     8178
 'DEBPROC' BIOVOL  BOBI*'MAILLAGE' PCAL*'MAILLAGE' J*'CHPOINT'
                                            MU0* 'FLOTTANT'  ;
***********************************************************************
*    CALCUL DU CHAMP BS  CREE SUR PCAL PAR L INDUCTEUR VOLUMIQUE BOBI *
*    PARCOURU PAR DES COURANTS DES DENSITES J (COMPOSANTES JX JY JZ)  *
*    MU0  PERMEABILITE DU VIDE                                        *
***********************************************************************
   fact = 1. / (4. * pi) ;
   nni = bobi 'NBNO' ;
   JIX = 'EXCO' J 'JX' ;
   JIY = 'EXCO' J 'JY';
   JIZ  = 'EXCO' J 'JZ' ;
*
   X Y Z = 'COOR' BOBI ;
   NGEO = PCAL 'NBNO' ;IGEO = 0;
   'REPETER' BO_GEO  NGEO ;
     IGEO = IGEO + 1 ;
     PCAL1 = PCAL 'POINT' IGEO ;
     XP YP ZP = 'COOR' PCAL1 ;
     RX = XP - X ;RY = YP - Y ; RZ = ZP  - Z ;
     RM2 = (RX * RX) + ( RY * RY ) + ( RZ * RZ ) ;
     R3 = RM2 ** 1.5 ;
     HX = (((JIY * RZ ) - ( JIZ * RY) )/ R3) ;
     HY = (((JIZ * RX ) - ( JIX * RZ) )/ R3) ;
     HZ  = (((JIX  * RY  ) - ( JIY  * RX ) )/ R3) ;
     HPT =(HX 'NOMC' 'HX')+( HY 'NOMC' 'HY' )+( HZ 'NOMC' 'HZ' ) ;
     OBJM = 'MODE' bobi  'THERMIQUE' 'ISOTROPE' ;
     HEPT = 'CHAN' CHAM HPT  OBJM  'NOEUDS' ;
     hheX = 'EXCO' hept 'HX' ;
     hhey = 'EXCO'hept 'HY' ;
     hhez = 'EXCO'  hept 'HZ' ;
     HTX = 'INTG' HHEX OBJM ;
     HTY = 'INTG' HHEY OBJM ;
     HTZ = 'INTG' HHEZ OBJM ;
*
     HT ='MANU' 'CHPO' pcal1 3 'HX'  HTX 'HY' HTY  'HZ' HTZ ;
     'SI' (IGEO 'EGA' 1) ;
        HGEO = HT ;
     'SINON' ;
        HGEO =HGEO + HT ;
     'FINSI' ;
 'FIN' BO_GEO ;
******************************************************
 HGEO  = HGEO * fact ;
 'FINPROC' HGEO ;

