$$$$ DEADFONC
* DEADFONC  PROCEDUR  GOUNAND   18/10/24    21:15:01     9970
************************************************************************
* NOM         : DEADFONC
* DESCRIPTION : La fonctionnelle de Huang à minimiser
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : gounand@semt2.smts.cea.fr
**********************************************************************
* VERSION    : v1, 21/03/2006, version initiale
* HISTORIQUE : v1, 21/03/2006, création
* HISTORIQUE : 2018/01/22 : chgt nom composante hors diago G21 au lieu
*                           de G12
* HISTORIQUE : 2018/10/10 : NLINP au lieu de NLIN
* HISTORIQUE :
************************************************************************
* Prière de PRENDRE LE TEMPS de compléter les commentaires
* en cas de modification de ce sous-programme afin de faciliter
* la maintenance !
************************************************************************
*
*
'DEBPROC' DEADFONC ;
'ARGUMENT' _mt*'MAILLAGE' ;
'ARGUMENT' gdisc*'MOT' ;
'ARGUMENT' methgau*'MOT' ;
'ARGUMENT' theta*'FLOTTANT' ;
'ARGUMENT' gamma*'FLOTTANT' ;
*
idim  = 'VALEUR' 'DIME' ;
vdim  = DEADUTIL 'DIMM' _mt ;
*
'ARGUMENT' met/'CHPOINT' ;
lmet = 'EXISTE' met ;
'SI' lmet ;
   'ARGUMENT' metdisc*'MOT' ;
'SINON' ;
   metdisc = 'CSTE' ;
'FINSI' ;
debloi = 'CHAINE' 'AHU' ;
ncmet  = '/' ('*' idim ('+' idim 1)) 2 ;
loi = 'CHAINE' debloi 'F' ;
'ARGUMENT' optelem/'MOT' ;
*
'SI' ('EXISTE' optelem) ;
   'SI' ('EGA' optelem 'ELEM') ;
      lelem = VRAI ;
   'SINON' ;
      cherr = 'CHAINE' 'Option ' optelem ' inconnue' ;
      'ERREUR' cherr ;
   'FINSI' ;
'SINON' ;
   lelem = FAUX ;
'FINSI' ;
*
* Calcul de la fonctionnelle
*
numop  = 1 ;
numvar = 1 ;
numder = vdim ;
numdat = 0 ;
numcof = 0 ;
*
   A = ININLIN numop numvar numdat numcof numder ;
   A . 'VAR' . 1 . 'NOMDDL' = 'MOTS' 'DUMM' ;
   A . 'VAR' . 1 . 'DISC'   = 'CSTE' ;
   A . 'VAR' . 1 . 'VALEUR' = 1.D0 ;
*
   numvar = 1 ;
   numdat = '+' ncmet 2 ;
   numcof = 1 ;
   B = ININLIN numop numvar numdat numcof numder ;
   B . 'VAR' . 1 . 'NOMDDL' = 'MOTS' 'DUMM' ;
   B . 'VAR' . 1 . 'DISC'   = 'CSTE' ;
   B . 'VAR' . 1 . 'VALEUR' = 1.D0 ;
*
   idat = 0 ;
   'REPETER' idi idim ;
      nomdat = 'CHAINE' 'G' &idi &idi ;
*         'MESSAGE' ('CHAINE' 'nomdat=' nomdat) ;
      idat = '+' idat 1 ;
      B . 'DAT' . idat . 'NOMDDL' = 'MOTS' nomdat ;
      B . 'DAT' . idat . 'DISC'   = metdisc ;
      'SI' lmet ;
         B . 'DAT' . idat . 'VALEUR' = 'EXCO' nomdat met nomdat ;
      'SINON' ;
         B . 'DAT' . idat . 'VALEUR' = 1.D0 ;
      'FINSI' ;
   'FIN' idi ;
   'REPETER' idi idim ;
      nj = '-' idim &idi ;
*      'MESSAGE' ('CHAINE' 'nj=' nj) ;
      'SI' ('>EG' nj 1) ;
         'REPETER' jdi nj ;
* Mise en cohérence avec Castem
*            nomdat = 'CHAINE' 'G' &idi ('+' &idi &jdi) ;
            nomdat = 'CHAINE' 'G' ('+' &idi &jdi) &idi ;
*               'MESSAGE' ('CHAINE' 'nomdat=' nomdat) ;
            idat = '+' idat 1 ;
            B . 'DAT' . idat . 'NOMDDL' = 'MOTS' nomdat ;
            B . 'DAT' . idat . 'DISC'   = metdisc ;
            'SI' lmet ;
               B . 'DAT' . idat . 'VALEUR' = 'EXCO' nomdat met nomdat ;
            'SINON' ;
               B . 'DAT' . idat . 'VALEUR' = 0.D0 ;
            'FINSI' ;
         'FIN' jdi ;
      'FINSI' ;
   'FIN' idi ;
   lisdat = 'LECT' 1 'PAS' 1 'NPAS' ('-' numdat 1) ;
   idat = '+' idat 1 ;
   B . 'DAT' . idat . 'NOMDDL' = 'MOTS' 'DUMM' ;
   B . 'DAT' . idat . 'DISC'   = 'CSTE' ;
   B . 'DAT' . idat . 'VALEUR' = theta ;
   idat = '+' idat 1 ;
   B . 'DAT' . idat . 'NOMDDL' = 'MOTS' 'DUMM' ;
   B . 'DAT' . idat . 'DISC'   = 'CSTE' ;
   B . 'DAT' . idat . 'VALEUR' = gamma ;
*
   B . 'COF' . 1 . 'COMPOR' = loi ;
   B . 'COF' . 1 . 'LDAT'   = lisdat ;
*
   A . 1 . 1 . 0  = 'LECT' ;
   B . 1 . 1 . 0  = 'LECT' 1 ;
*
vfonc = NLINP gdisc _mt A B 'EREF' methgau ;
*
'SI' ('NON' lelem) ;
   vfonc = 'MAXIMUM' ('RESULT' vfonc) ;
'FINSI' ;
*
'RESPRO' vfonc ;
*
* End of procedure file DEADFONC
*
'FINPROC' ;




