$$$$ @PRCA1
* @PRCA1    PROCEDUR  MB234859  18/11/21    21:15:02     10007
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*                    PROCEDURE DE CALCUL @PRCA1
*
*   Cette procedure recupere les valeurs de glissement et calcule le
*   cisaillement, la pression de contact et l'energie dissipee sur
*   l'increment. Elle realise aussi le cumul de l'energie dissipee sur
*   le cycle et l'energie dissipee totale
*
* Entrees :
*   T1    : Table de PASAPAS
*   ID_BU : Entier donnant le numero de la BOITE D'USURE a considerer
*
* Sortie :
*   T1    : Table de PASAPAS contenant les champs calculesr
*
* Appelee par @EVN_MNG
*
***********************************************************************
DEBP @PRCA1 T1*'TABLE' ID_BU*'ENTIER';

* Booleen pour le trace
  OPCntEvo = FAUX ;
*
*---------------------------------------------------------------------*
*   - CREATION DES MAILLAGES POUR LES TRACES/EVOLS
*---------------------------------------------------------------------*
  WTAB = T1.'WTABLE' ;
* Recuperation des mult associes au CL unil (cont et fro melanges)
  MCAC = 'EXTR' WTAB.'RIBLO_M' 'MAIL' 'MULT' ;
* Recuperation des mult de Lag associe de contact (actif)
  MUCA = MCAC 'INTE' (T1.'MESH-CON') ;
*
* Equivalence mult cont-mult fro a recreer (au cas ou on renumerote)
  MUFRO = 'VIDE' 'MAILLAGE' ;
  LVALF = 'PROG' ;
*
* Traite les difference cas (SYME, MESC, FAIB)
  NODEF = 'NBNO' (T1.'MESH-MFRO' 'ELEM' 1) ;
  'SI' ('EGA' NODEF 5) ;
    NOTOT = 'NBNO' T1.'MESH1' ;
  'SINON' ;
    NOTOT = 'NBEL' T1.'MESH1' ;
  'FINSI' ;
*
  'REPE' BCL NOTOT ;
    'SI' ('EGA' NODEF 5) ;
      ELZ = 'CHAN' (T1.'MESH-MFRO' 'ELEM' &BCL) 'POI1' ;
    'SINON' ;
      INDZ = NOTOT*(&BCL-1) + 1 ;
      ELZ = 'CHAN' (T1.'MESH-MFRO' 'ELEM' INDZ) 'POI1' ;
    'FINSI' ;
    PTZ = ELZ 'POIN' 1 ;
    VAL = 'NOEU' (ELZ 'POIN' NODEF) ;
    MUFRO = MUFRO ET PTZ ;
    LVALF = LVALF ET VAL ;
  'FIN' BCL ;
*
  TESTEL = ('NBEL' T1.'MESH2') * NOTOT ;
  'SI' (('NEG' TESTEL ('NBEL' T1.'MESH-MFRO')) 'ET' ('EGA' NODEF 5));
    NOTOT = 'NBNO' T1.'MESH2' ;
    'REPE' BCL NOTOT ;
      ELZ = 'CHAN' (T1.'MESH-MFRO' 'ELEM' (TESTEL + &BCL)) 'POI1' ;
      PTZ = ELZ 'POIN' 1 ;
      VAL = 'NOEU' (ELZ 'POIN' NODEF) ;
      MUFRO = MUFRO ET PTZ ;
      LVALF = LVALF ET VAL ;
    'FIN' BCL ;
  'FINSI' ;
  CHPZ = 'MANU' 'CHPO' MUFRO 1 'CORR' LVALF ;
  T1.'MESH-CORR' = CHPZ ;
*
* Recuperation des mult de Lag de fro associe a ceux de cont
  MUFR = 'VIDE' 'MAILLAGE' ;
  'REPE' BCL ('NBNO' MUCA) ;
    PZ = MUCA 'POIN' &BCL ;
    VV = EXTR (T1.'MESH-CORR') 'CORR' PZ ;
    MUFR = MUFR ET (NOEU (ENTI VV)) ;
  'FIN' BCL ;

* Recuperation des mult de Lag de fro qui glissent (inactif)
  MUFA = MUFR 'INTE' MCAC ;
  MUGL = 'VIDE' 'MAILLAGE' ;
  LNOE = 'LECT' ;
  'REPE' BCL ('NBNO' MUFR) ;
    PTZ = MUFR 'POIN' &BCL ;
    'SI' ('NON' ('DANS' PTZ MUFA)) ;
      MUGL = MUGL 'ET' PTZ ;
      LNOE = LNOE 'ET' ('NOEU' PTZ) ;
    'FINSI' ;
  'FIN' BCL ;
*
  IGLI = 'NEG' ('DIME' LNOE) 0 ;
  BOITE_US = T1.'BOITES_USURES'. ID_BU ;
  IdIniVCu = BOITE_US.'INDICATEUR_INITIALISATION_VAR_CUMUL' ;
  SUR_APPL = BOITE_US.'SURFACE_APPLICATION' ;
*
*---------------------------------------------------------------------*
*   - RECUPERATION DU GLISSEMENT
*---------------------------------------------------------------------*
  'SI' (IGLI 'ET' ('NEG' IdIniVCu 0)) ;
    GLI  = -1.* (T1.'WTABLE'.'DFLX') ;
    MPO1 = 'VIDE' MAILLAGE ;
    LGLI = 'PROG' ;
    'REPE' BCL ('NBNO' MUGL) ;
      PTSZ = MUGL 'POIN' &BCL ;
      POIZ = SUR_APPL 'POIN' 'PROC' PTSZ ;
      MPO1 = MPO1 'ET' POIZ ;
      VGLI = 'EXTR' GLI 'FLX' PTSZ ;
      LGLI = LGLI 'ET' VGLI ;
    'FIN' BCL;
*   EVOL donnant la valeur du glissement le long de SUR_APPL
    DCSlip = 'MANU' 'CHPO' MPO1 1 'ETA1' LGLI ;
    EvDCSlip = 'EVOL' 'VERT' 'CHPO' DCSlip SUR_APPL ;
    EvDCSlip = 'EXTR' EvDCSlip 'COUR' 1 ;
  'SINON' ;
    DCSlip = 'MANU' 'CHPO' SUR_APPL 1 'ETA1' 0. ;
    EvDCSlip = 'EVOL' 'VERT' 'CHPO' DCSlip SUR_APPL ;
    EvDCSlip = 'EXTR' EvDCSlip 'COUR' 1 ;
  'FINSI' ;
  EvDCSlip = 'CHAN' EvDCSlip 'LEGE' 'Glissement' ;
*
*---------------------------------------------------------------------*
*   - DETERMINATION DU CISAILLEMENT ET DE LA PRESSION
*---------------------------------------------------------------------*
  LXIN = 'EXCO' (T1.'ESTIMATION'.'DEPLACEMENTS') 'LX' 'LX' ;
  LXCO = 'REDU' LXIN MUCA ;
  LXCI = 'REDU' LXIN MUFR ;
*
* CHPOINT donnant la longueur de chaque element de SUR_APPL
  CHLE = 'MANU' 'CHPO' SUR_APPL 1 'LELT' 0. 'NATURE' 'DISCRET' ;
  'REPE' BELT ('NBEL' SUR_APPL) ;
    ELZ = SUR_APPL 'ELEM' &BELT ;
    LEZ = 'MESU' ELZ ;
    CHPZ = 'MANU' 'CHPO' ELZ 1 'LELT' (LEZ/2.) 'NATURE' 'DISCRET' ;
    CHLE = CHLE 'ET' CHPZ ;
  'FIN' BELT ;
*
* Determination du cisaillement et de la pression de contact
  MPO2 = 'VIDE' MAILLAGE ;
  VLPR = 'PROG' ; VALC = 'PROG' ;
  'REPE' BCL ('NBNO' MUCA) ;
    PMUCO = MUCA 'POIN' &BCL ;
    PSURA = SUR_APPL 'POIN' 'PROC' PMUCO ;
    MPO2 = MPO2 ET PSURA ;
    PMUFR = EXTR (T1.'MESH-CORR') 'CORR' PMUCO ;
    VV = 'EXTR' LXCI 'LX  ' ('NOEU' ('ENTI' PMUFR)) ;
    VS = 'EXTR' CHLE 'LELT' PSURA ;
    VP = 'EXTR' LXCO 'LX  ' PMUCO ;
    VALC = VALC 'ET' (VV/VS) ;
    VLPR = VLPR 'ET' (VP/VS) ;
  'FIN' BCL ;
* - Cisaillement
  CShear = 'MANU' 'CHPO' MPO2 'CILI' VALC ;
  EvCShear = 'EVOL' 'ROUG' 'CHPO' CShear SUR_APPL ;
  EvCShear = 'EXTR' EvCShear 'COUR' 1 ;
  EvCShear = 'CHAN' EvCShear 'LEGE' 'Shear' ;
  CShearMu = 'MANU' 'CHPO' MUFR 'CILI' VALC ;
* - Pression de contact
  CPress = 'MANU' 'CHPO' MPO2 'PRES' VLPR ;
  EvCPress = 'EVOL' 'BLEU' 'CHPO' CPress SUR_APPL ;
  EvCPress = 'EXTR' EvCPress 'COUR' 1 ;
  EvCPress = 'CHAN' EvCPress 'LEGE' 'Pressure' ;
*
*---------------------------------------------------------------------*
*   - CALCUL DE L ENERGIE DISSIPEE
*---------------------------------------------------------------------*
* Energie dissipee sur l increment
  NCNECC = BOITE_US . 'NCNECC' ;
  'SI' (IdIniVCu 'EGA' 0) ;
    BOITE_US.'INDICATEUR_INITIALISATION_VAR_CUMUL' = 1 ;
    EnDisInc = MANU 'CHPO' SUR_APPL 1 'ED' 0. 'NATURE' 'DISCRET' ;
    EnDisCyc = MANU 'CHPO' SUR_APPL 1 'ED' 0. 'NATURE' 'DISCRET' ;
    EnDisTot = MANU 'CHPO' SUR_APPL 1 'ED' 0. 'NATURE' 'DISCRET' ;
    BOITE_US.'CISAILL_PREC' = CShearMu ;
  'SINON' ;
    'SI' ('EXIS' (BOITE_US.'ENERGIE_DISSIPEE_SUR_CYCLE') NCNECC) ;
      EnDisCyc = BOITE_US.'ENERGIE_DISSIPEE_SUR_CYCLE'. NCNECC ;
    'SINON' ;
      EnDisCyc = 'MANU' 'CHPO' SUR_APPL 1 'ED' 0. 'NATURE' 'DISCRET' ;
    'FINSI' ;
    EnDisTot = BOITE_US.'ENERGIE_DISSIPEE_TOTALE'                   ;
*   Pour le cisaillement, on realise une moyenne
    CShearPr = BOITE_US.'CISAILL_PREC' ;
    BOITE_US.'CISAILL_PREC' = CShearMu ;
    CShearMu = (CShearMu + CShearPr) / 2. ;
  'FINSI' ;
*
  'SI' (IGLI 'ET' ('NEG' IdIniVCu 0)) ;
    LVA = 'PROG' ;
    'REPE' BCL ('NBNO' MUGL) ;
      MAIZ = MUGL POIN &BCL ;
      VGLI = 'EXTR' GLI 'FLX' MAIZ ;
      VCIS = 'EXTR' CShearMu 'CILI' MAIZ ;
      VEND = 'ABS' (VGLI*VCIS) ;
      LVA = LVA ET VEND ;
    'FIN' BCL ;
    EnDisInc = 'MANU' 'CHPO' MPO1 1 'ED' LVA ;
  'SINON' ;
    EnDisInc = 'MANU' 'CHPO' SUR_APPL 1 'ED' 0. ;
  'FINSI';
*
* Evolution donnant le profil de l'energie dissipee sur le pas de temps
* le long de SUR_APPL
  EvDltEnD = 'EVOL' 'JAUN' 'CHPO' EnDisInc SUR_APPL ;
  EvDltEnD = 'EXTR' EvDltEnD 'COUR' 1 ;
  EvDltEnD = 'CHAN' EvDltEnD 'LEGE' 'Energie dis. Inc.' ;

* Evolution donnant le profil de l'energie dissipee sur le cycle
* le long de SUR_APPL
  EnDisCyc = EnDisCyc + EnDisInc ;
  EvEnDCyc = 'EVOL' 'BLAN' 'CHPO' EnDisCyc SUR_APPL ;
  EvEnDCyc = 'EXTR' EvEnDCyc 'COUR' 1 ;
  EvEnDCyc = 'CHAN' EvEnDCyc 'LEGE' 'Energie dis. Cyc.' ;

* Evolution donnant le profil de l'energie dissipee sur l'ensemble des
* cycles le long de SUR_APPL
  EnDisTot = EnDisTot + EnDisInc ;
  EvEnDTot = 'EVOL' 'GRIS' 'CHPO' (EnDisTot) SUR_APPL ;
  EvEnDTot = 'EXTR' EvEnDTot 'COUR' 1 ;
  EvEnDTot = 'CHAN' EvEnDTot 'LEGE' 'Energie dis. Tot.' ;
*
* Trace
  SI OPCntEvo ;
    DESS (EvCPress ET EvCShear ET (500000. * EvDCSlip)
      ET (100. * EvDltEnD) ET (100. * EvEnDCyc)
      ET (50. * EvEnDTot )) 'LEGE' 'NCLK' ;
  FINSI ;
*
* Stockage des resultats
  Inc = BOITE_US.'INDICE-STOCKAGE' ;
  BOITE_US.'EVO_GLISSEMENT_CONTACT'.           Inc = EvDCSlip ;
  BOITE_US.'CISAILLEMENT_DE_CONTACT'.          Inc = CShear ;
  BOITE_US.'EVO_CISAILLEMENT_CONTACT'.         Inc = EvCShear ;
  BOITE_US.'PRESSION_DE_CONTACT'.              Inc = CPress ;
  BOITE_US.'EVO_PRESSION_CONTACT'.             Inc = EvCPress ;
  BOITE_US.'ENERGIE_DISSIPEE_SUR_INCREMENT'.   Inc = EnDisInc ;
  BOITE_US.'ENERGIE_DISSIPEE_SUR_CYCLE'.    NCNECC = EnDisCyc ;
  BOITE_US.'ENERGIE_DISSIPEE_TOTALE'               = EnDisTot ;
  BOITE_US.'EVO_ENERGIE_DISSIPEE_INCR'.        Inc = EvDltEnD ;
  BOITE_US.'EVO_ENERGIE_DISSIPEE_CYCLE'.    NCNECC = EvEnDCyc ;
  BOITE_US.'EVO_ENERGIE_DISSIPEE_TOTALE'           = EvEnDTot ;

FINP T1 ;
$$$$

