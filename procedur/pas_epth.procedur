$$$$ PAS_EPTH
* PAS_EPTH  PROCEDUR  CB215821  18/11/26    21:15:05     10010
'DEBP' PAS_EPTH   PRECED*TABLE MODEVAL*MMODEL MATEVAL*MCHAML
                               TCHP/CHPOINT   TCHAM/MCHAML             ;
************************************************************************
* Cette procedure calcule la deformation thermique de reference si
* necessaire et calcule de la deformation thermique du pas courant
*
* PRECED : La TABLE donnee en entree de PASAPAS. On a besoin des indices
*           - WTAB.'TEMPERATURE_REFERENCE'
*           - WTAB.'TALPHA_REFERENCE'
*           - WTAB.'MOD_REF'
*           - WTAB.'MAT_REF'
*           - WTAB.'ETREF' (On en a besoin et il peut etre calcule ici)
* MODEVAL: Modele    pour lequel  on veut evaluer EPSILON_THERMIQUE
* MATEVAL: Materiaux pour lequel  on veut evaluer EPSILON_THERMIQUE
*
* TCHP   : Temperature CHPOINT a laquelle est evalué EPSILON_THERMIQUE
*  ou
* TCHAM  : Temperature MCHAML  a laquelle est evalué EPSILON_THERMIQUE
*          Obligatoire pour les JOINTS
*          Necessaire  pour certains éléments linéiques
*
* Les resultats sont :
* ETHER2   : Deformation thermique dilatation
* MSRTHER2 : Deformation thermique poreuse
************************************************************************

WTAB = PRECED. 'WTABLE';

************************************************************************
* Traitement de la reference : 'ETREF'
************************************************************************
'SI' ('EGA' WTAB.'ETAT_REF' 'SUPPRIME');
* Deformation thermique de reference ETREF calculee si elle n'existe pas
  'SI' ('EGA' WTAB.'ETREF' 'INCONNU') ;
     TREF   = WTAB.'TEMPERATURE_REFERENCE';
     TAREF  = WTAB.'TALPHA_REFERENCE'     ;
     TYREF  ='TYPE' TREF                  ;
     TYAREF ='TYPE' TAREF                 ;

*    On fait en sorte que la difference des temperatures soit realisable
     MOREF = WTAB.'MOD_REF';
    'SI'(('EGA' TYREF  'CHPOINT') 'ET'
         ('EGA' TYAREF 'MCHAML' ))                       ;
       TREF = 'CHAN' 'CHAM' TREF MOREF 'TEMPERATURES'    ;
    'SINO';
      'SI'(('EGA' TYREF  'MCHAML' ) 'ET'
           ('EGA' TYAREF 'CHPOINT'))                     ;
         TAREF = 'CHAN' 'CHAM' TAREF MOREF 'TEMPERATURES';
      'FINS';
    'FINS';

     DELTAT = TREF '-' TAREF ;
     TYDT ='TYPE' DELTAT;
*    Protection si Type FLOTTANT
    'SI' ('EGA' TYDT 'FLOTTANT');
       DELTAT = (('ZERO' MOREF 'TEMPERATURES') ** 0) * DELTAT;
    'FINS';

     WTAB.'ETREF'   ='EPTH' MOREF  WTAB.'MAT_REF' DELTAT ;
     WTAB.'MAT_REF' ='MOT' 'SUPPRIME'                    ;
  'FINS';
'SINO';
  'SI' ('NEG' WTAB.'ETAT_REF' 'INCONNU' );
     MESS 'Dans PAS_EPTH, LOGIQUE fausse';
     ERRE 21;
  'FINS';
'FINS';

************************************************************************
* Traitement courant 'ETHER2'
************************************************************************
'SI' ('EGA' ('TYPE' TCHP) 'CHPOINT');
   TEVAL = TCHP;
   TYPT  = 'MOT' 'CHPOINT';
'SINO';
  'SI' ('EGA' ('TYPE' TCHAM) 'MCHAML');
   TEVAL = TCHAM;
   TYPT  = 'MOT' 'MCHAML';
  'SINO';
     ERRE 37 'AVEC' 'CHPOINT';
  'FINS';
'FINS';
 TAREF  =  WTAB.'TALPHA_REFERENCE' ;
 TYAREF = 'TYPE' TAREF             ;

*On fait en sorte que la difference des temperatures soit realisable
'SI'(('EGA' TYPT   'CHPOINT') 'ET'
     ('EGA' TYAREF 'MCHAML' ))                       ;
   TEVAL = 'CHAN' 'CHAM' TEVAL MOREF 'TEMPERATURES'  ;
'SINO';
  'SI'(('EGA' TYPT   'MCHAML' ) 'ET'
       ('EGA' TYAREF 'CHPOINT'))                     ;
     TAREF = 'CHAN' 'CHAM' TAREF MOREF 'TEMPERATURES';
  'FINS';
'FINS';

 ETHER2 = ('EPTH' MODEVAL MATEVAL (TEVAL '-' TAREF))  '-'
          ('REDU' WTAB.'ETREF' MODEVAL ) ;


'SI' ('EGA' WTAB.'ETHER1' 'INCONNU');
   WTAB.'ETHER1'= ETHER2;
'FINS';


************************************************************************
* Traitement de la formulation 'POREUX'
************************************************************************
'SI' WTAB.'POR1';
  MOPOR    =  'EXTR' MODEVAL 'FORM' 'POREUX'                       ;
  MAPOR    =  'REDU' MATEVAL MOPOR                                 ;
  ZALM     = ('EXCO' 'ALPM' MAPOR 'MSR0') 'CHAN' 'TYPE' 'SCALAIRE' ;
  ZBI      = ('EXCO' 'COB'  MAPOR 'MSR0') 'CHAN' 'TYPE' 'SCALAIRE' ;
  ZALPH    = ('EXCO' 'ALPH' MAPOR 'MSR0') 'CHAN' 'TYPE' 'SCALAIRE' ;

  ZFAC = 3.D0 '*' ( ZALM '-' ( ZBI '*' ZALPH ) );
  ZFAC = ZFAC  'CHAN' 'TYPE' 'SCALAIRE';

  ZDT  = 'REDU' (TEVAL '-' WTAB.'TEMPERATURE_REFERENCE') WTAB.'MAI_POR';
  ZDDT = 'CHAN' 'CHAM' ZDT MOPOR 'STRESSES' 'SCALAIRE';
  ZFAC = 'CHAN' 'STRESSES' MOPOR ZFAC                 ;

  MSRTHER2    = ZFAC  '*' ZDDT ;

  'SI' ('EGA' WTAB.'MSRTHER1' 'INCONNU');
     WTAB.'MSRTHER1'= MSRTHER2 ;
  'FINS';
'SINO';
   MSRTHER2 = WTAB.'MSRTHER2';
'FINS';

'FINP' ETHER2 MSRTHER2;
$$$$



