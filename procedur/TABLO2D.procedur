$$$$ TABLO2D
* TABLO2D   PROCEDUR  JC220346  16/05/10    21:15:00     8926
************************************************************************
* NOM         : TABLO2D
* DESCRIPTION : Affiche sous forme graphique 2D (matrice de cases
*               colorees) un tableau de valeurs numeriques.
************************************************************************
* ENTREES : LO  [MOT]      = MOT-CLE VALANT LINE OU LOGA
*           NL  [ENTIER]   = NOMBRE DE LIGNES
*           NC  [ENTIER]   = NOMBRE DE COLONNES
*           LV  [LISTREEL] = LISTE DES NL*NC VALEURS
*           TIT [MOT]      = TITRE DU GRAPHIQUE
* SORTIES : AUCUNE
************************************************************************
* SYNTAXE :
*
*            TABLO2D | ('LINE') | NLIG NCOL LVAL (TIT) ;
*                    |  'LOGA'  |
*
************************************************************************
DEBP TABLO2D LO/'MOT' NL*'ENTIER' NC*'ENTIER' LV*'LISTREEL' TIT/'MOT' ;

*  MODIFICATION DES OPTIONS
   NDIM1 = VALE 'DIME' ;
   ELTY1 = VALE 'ELEM' ;
   SI (NDIM1 EGA 1) ;
      ERRE 709 'AVEC' NDIM1 ;
   FINS ;
   OPTI 'ELEM' 'QUA4' ;

*  ===================
*  PARAMETRES D'ENTREE
*  ===================

*  GESTION DE L'ECHELLE
   KLOGA = FAUX ;
   MCOMP = 'X' ;
   MOECH = MOTS 'LINE' 'LOGA' ;
   SI (EXIS LO) ;
      ILO = POSI LO 'DANS' MOECH ;
      SI (ILO > 0) ;
         SI (ILO EGA 2) ;
            KLOGA = VRAI ;
            MCOMP = '10^X' ;
         FINS ;
      SINON ;
         SI (EXIS TIT) ;
            ERRE 1052 'AVEC' 'LINELOGA' ;
         SINON ;
            TIT = LO ;
         FINS ;
      FINS ;
   FINS ;

*  GESTION DE LA LISTE DES VALEURS
   NV = DIME LV ;
   SI (NV EGA 0) ;
      ERRE 1027 'AVEC' 'LISTREEL' ;
   FINS ;
   SI (NL <EG 0) ;
      ERRE 36 'AVEC' NL ;
   FINS ;
   SI (NC <EG 0) ;
      ERRE 36 'AVEC' NC ;
   FINS ;
   SI (NV NEG (NL*NC)) ;
      ERRE 199 ;
   FINS ;

*  TITRE DU GRAPHIQUE
   SI (NON (EXIS TIT)) ;
      TIT = ' ' ;
   FINS ;


*  =====================
*  CREATION DU GRAPHIQUE
*  =====================

   NDIM1 = VALE 'DIME' ;
   SI (NDIM1 EGA 2) ;
      MAIL1 = (0. NL) DROI NC (NC NL) TRAN NL (0. (-1*NL)) ;
   SINON ;
      MAIL1 = (0. NL 0.) DROI NC (NC NL 0.) TRAN NL (0. (-1*NL) 0.) ;
   FINS ;

*  AFFICHAGE DES DONNEES
   $MAIL1 = MODE MAIL1 'THERMIQUE' 'ISOTROPE' ;
   SI KLOGA ;
      LV = (LOG (ABS LV)) / (LOG 10.) ;
   FINS ;
   CHML1 = MANU 'CHML' $MAIL1 'REPA' 'SCAL' LV 'GRAVITE' ;
   SI (NDIM1 EGA 2) ;
      TRAC $MAIL1 CHML1 'TITR' TIT ;
   SINON ;
      TRAC $MAIL1 CHML1 (0. 0. 1.E20) 'TITR' TIT ;
   FINS ;

*  RETABLISSEMENT DES OPTIONS D'ORIGINE
   SI (NEG ELTY1 '    ') ;
      OPTI 'ELEM' ELTY1 ;
   FINS ;
   SI (NDIM1 EGA 3) ;
      OPTI 'OEIL' (1.E6 -1.2E6 0.9E6) ;
*     (OEIL PAR DEFAUT DANS PRTRAC.ESO)
   FINS ;

FINP ;
$$$$


