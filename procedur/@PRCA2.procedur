$$$$ @PRCA2
* @PRCA2    PROCEDUR  MB234859  18/11/21    21:15:02     10007
***********************************************************************
*
* CETTE PROCEDURE A ETE MISE GRACIEUSEMENT A DISPOSITION DES
* UTILISATEURS PAR LE LTDS - Ecole Centrale Lyon (S. Fouvry et S.Garcin)
*
***********************************************************************
*                    PROCEDURE DE CALCUL @PRCA2
*
*  Calcul du cycle de fretting en recuperant au point 'POINT_REF_DELTA'
*  le deplacement impose (LstDelta) et la force (LstFTang)
*  Ces donnees sont cumulees sur chaque cycle et mise a zero en debut
*  de chaque nouveau cycle.
*
* Entrees :
*   T1    : Table de PASAPAS
*   ID_BU : Entier donnant le numero de la BOITE D'USURE a considerer
*
* Sortie :
*   T1    : Table de PASAPAS avec les booleens mis a jour
*
* Appelee par @EVN_MNG
*
***********************************************************************
DEBP @PRCA2 T1*'TABLE' ID_BU*'ENTIER';

* Booleen pour trace ou non le cycle de freeting
  OPCycFre = FAUX ;

  BOITE_US = T1.'BOITES_USURES'. ID_BU ;
  OuiSauv  = BOITE_US.'TermineC' ;
  NCNECC   = BOITE_US.'NCNECC'   ;
* Indice associe aux resultats du pas courant
  Inc = BOITE_US.'INDICE-STOCKAGE' ;
*
  SI ('EXIS' BOITE_US 'LISTE_DELTAS') ;
    LstDelta = BOITE_US.'LISTE_DELTAS' ;
    LstFTang = BOITE_US.'LISTE_FORCES_TANGENTIELLES' ;
  SINON ;
    LstDelta = PROG ;
    LstFTang = PROG ;
  FINSI ;
*
  EvCShear = BOITE_US.'EVO_CISAILLEMENT_CONTACT'. Inc ;
  PGetDlta = BOITE_US.'POINT_REF_DELTA';
  TU       = T1.'ESTIMATION'.'DEPLACEMENTS' ;

* Evaluation des composantes souhaitees
  FTang_ii = INTG EvCShear ;
  Delta_ii = EXTR TU 'UX' PGetDlta ;
*
* Cumul des valeurs
  LstDelta = LstDelta ET Delta_ii ;
  LstFTang = LstFTang ET FTang_ii ;

* Doit-on sauvegarder les resutlats?
  SI OuiSauv ;
*   - Sauvegarde
    DeltaMin = MINI LstDelta ;
    DeltaMax = MAXI LstDelta ;
    DeltaMoy = (DeltaMin + DeltaMax) / 2. ;
    LstDelta = LstDelta - DeltaMoy ;

*   Cycle de fretting
    CyclFret = EVOL 'ROUG' 'MANU' 'Deplacement (mm)'    LstDelta
                           'Force Tangentielle (N/mm)'  LstFTang ;
    EnteteCF = CHAI 'Cycle de fretting n: ' NCNECC FORMAT '(I2)' ;
    CyclFret = CHAN CyclFret 'LEGE' EnteteCF ;
    BOITE_US.'CYCLE_DE_FRETTING'. NCNECC =  CyclFret ;

*   - Reinitialisation
    LstDelta = PROG Delta_ii ;
    LstFTang = FTang_ii ;

*   - Trace eventuel
    SI OPCycFre ;
      TabDess    = TABL ;
      TabDess. 1 = 'TIRC MARQ CROI' ;
      DESS CyclFret TabDess 'GRIL' 'LEGE' ;
    FINSI ;

  FINSI ;

* MAJ des donnees contenues dans les listes courantes.
  BOITE_US.'LISTE_DELTAS'                = LstDelta ;
  BOITE_US.'LISTE_FORCES_TANGENTIELLES'  = LstFTang ;

FINP T1 ;
$$$$

