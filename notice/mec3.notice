$$$$ MEC3     NOTICE  CHAT      11/09/12    21:17:13     7124
                                             DATE     11/09/12
                                             Voir aussi :

Section : Presentation Presentation

FRAN===========================================================
*--------------------------------------------------------------------*
*                                                                    *
*           Exemple simple de calcul mecanique elastique             *
*    fonctionnant avec les nouvelles structures MMODEL et MCHAML     *
*                                                                    *
*--------------------------------------------------------------------*
* Definition des options
*
           OPTI DIME 3 ELEM CU20 MODEL TRIDIM ; DENS 10 ;
*
* Maillage d'une barre de 5 elements CU20
*
  P1 = 0 0 0 ; P2 = 10 0 0 ; LI= P1 D 1 P2 ; SUB=LI TRAN (0 10 0) ;
   TOTAL = SUB VOLU TRAN (0 0 50) COUL JAUN ; SUH = TOTAL FACE 2 ;
     LIH = SUH COTE 2 ; TRAC TOTAL CACH (1000 -2000 1000) QUAL ;
*
* Formulation, materiau defini a l'aide de
*
   MODL1 = MODL TOTAL MECANIQUE ELASTIQUE CU20 ;
    CARM = MATR MODL1 'YOUN' 2.E11 'NU' 0.3 'RHO' 7800. 'ALPH' 12.E-6 ;
*
* Conditions de blocages
*
                        ENC1 = BLOQ UZ SUB ;
               ENC2 = BLOQ UY LI ; ENC3 = BLOQ UX P1 ;
*
* Blocage pour deplacements imposes
*
                        ENC4 = BLOQ UZ SUH ;
                    ENC = ENC1 ET ENC2 ET ENC3 ;
*
* Raideur totale
*
                 RIG = (RIGI MODL1 CARM) ET ENC ET ENC4 ;
*
* Valeur des deplacements imposes
*
                      FEXT = DEPI ENC4 1E-9  ;
*
* Force ponctuelle et force totale
*
         FNOD = FORCE LIH (0 1000 0) ; FTOT = FEXT ET FNOD ;
*
* Resolution  pour obtenir les deplacements
*
                        DEP = RESOU RIG FTOT ;
*
* Vecteur force et reaction
*
               VEF = VECTEUR FTOT 0.03 FX FY FZ ROUG ;
                   REA = REAC (ENC4 ET ENC) DEP ;
               VER = VECTEUR REA 0.01 FX FY FZ BLEU ;
*
* Contraintes et vonmises
*
               SSI = SIGMA MODL1 DEP CARM ; VM = VMIS MODL1 SSI ;
*
* Deformee et trace
*
             DEF = DEFO TOTAL DEP 50 (VEF ET VER) VERT ;
                   TRAC DEF MODL1 VM (1000 -2000 1000) ;
                                FIN ;

ANGL===========================================================
*--------------------------------------------------------------------*
*                                                                    *
*           Simple example of mechanico-elastic calculation          *
*           applied to the new structures MMODEL and MCHAML          *
*                                                                    *
*--------------------------------------------------------------------*
* Option definitions
*
           OPTI DIME 3 ELEM CU20 MODEL TRIDIM ; DENS 10 ;
*
* Mesh of a 5-element bar CU20
*
  P1 = 0 0 0 ; P2 = 10 0 0 ; LI= P1 D 1 P2 ; SUB=LI TRAN (0 10 0) ;
   TOTAL = SUB VOLU TRAN (0 0 50) COUL JAUN ; SUH = TOTAL FACE 2 ;
     LIH = SUH COTE 2 ; TRAC TOTAL CACH (1000 -2000 1000) QUAL ;
*
* Formulation, material defined by means of
*
   MODL1 = MODL TOTAL MECANIQUE ELASTIQUE CU20 ;
    CARM = MATR MODL1 'YOUN' 2.E11 'NU' 0.3 'RHO' 7800. 'ALPH' 12.E-6 ;
*
* Blockage conditions
*
                        ENC1 = BLOQ UZ SUB ;
               ENC2 = BLOQ UY LI ; ENC3 = BLOQ UX P1 ;
*
* Blockage for imposed displacements
*
                        ENC4 = BLOQ UZ SUH ;
                    ENC = ENC1 ET ENC2 ET ENC3 ;
*
* Total stiffness
*
                 RIG = (RIGI MODL1 CARM) ET ENC ET ENC4 ;
*
* Value of imposed displacements
*
                      FEXT = DEPI ENC4 1E-9  ;
*
* Punctual and total force
*
         FNOD = FORCE LIH (0 1000 0) ; FTOT = FEXT ET FNOD ;
*
* Resolution for getting the displacements
*
                        DEP = RESOU RIG FTOT ;
*
* Force vector and reaction
*
               VEF = VECTEUR FTOT 0.03 FX FY FZ ROUG ;
                   REA = REAC (ENC4 ET ENC) DEP ;
               VER = VECTEUR REA 0.01 FX FY FZ BLEU ;
*
* Stresses and Von Mises
*
               SSI = SIGMA MODL1 DEP CARM ; VM = VMIS MODL1 SSI ;
*
* Deformed shape and plot
*
             DEF = DEFO TOTAL DEP 50 (VEF ET VER) VERT ;
                   TRAC DEF MODL1 VM (1000 -2000 1000) ;
                                FIN ;



