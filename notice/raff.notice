$$$$ RAFF     NOTICE  GG250959  18/01/09    21:15:25     9688
                                             DATE     18/01/09

    Operateur RAFF                           Voir aussi :
    --------------

      GEO2  =  RAFF  GEO1  CHPO1 ;



Section : Maillage Manipulation

FRAN==========================================================

    Objet :
    _______

    L'operateur RAFF part d'un maillage existant (GEO1) pour le raffiner
 en respectant un champ de densite  (CHPO1). Tant que la densite n'est
 pas atteinte un element est divise en sous elements etc...
 Le maillage genere contient le resultat de la division des elements
 plus des elements de types relations (itypel=22) qui permettent de
 realiser au mieux la conformite en deplacement des elements.


      Commentaire :
      _____________

      GEO1  :  Maillage initial

      CHPO1 :  Objet CHPOINT de densite


      GEO2  : Maillage final, contenant les relations a imposer.


      Remarque :
      __________

      Apres avoir raffiné un maillage il est nécéssaire de faire appel à
      l'opérateur RELA pour contruire les relations de conformité
      entre les différentes zones de raffinement.
ANGL==========================================================

    Description :
    ____________

    The RAFF operator refines an existing mesh (GEO1) according to a
 density field (CHPO1). while the density is not reached each element is
 split into several elements etc...
 The generated mesh includes all these element as well as elements of
 type relations(itypel=22) which insure as well as possible the
 displacements conformity between elements.


      Contents :
      _________


      GEO1  :  initial mesh

      CHPO1 :  object CHPOINT giving density field.


      GEO2  :  final mesh including elements for relations on
               displacement

      remark :
      ________

      After the refinement of the mesh it is necessary to use the RELA
      operator to build the conformity relations between the differents
      levels of refinement.

=============================================================

   Exemple :
   _________

    opti elem qua4 mode plan defo dime 2;
    dens 2.;
* mesh 10x6
    pa= 0 0; pb= 10 0;pc= 20 0;
    liab= pa droi pb;libc= pb droi pc;
    su = (liab et libc)  trans  ( 0 12);
    trac su;
* definition of density
    x y = coor su;
    distance = ((x - 10 ) * ( x- 10) + ( y * y)) ** 0.5;
    den = 0.3 + (0.18*distance);
    trac su den;
* new mesh
    su2= raff su den;
    hh = elem su2 SURE ;
    sureal = su2 diff hh ;
* use of this mesh
* definition of model and caracteristic
    mo= mode su2 mecanique elastique isotrope ;
    ma = mate mo  YOUN 2.e5  NU 0.3 ;
* conformity relations
    rel = rela mo;
* loads
    psupe = su2 poin droite ( 0 12) ( 10 12) 0.1;
    lisupe = elem ( contour su2) appu stric psupe;
    ff = pres ( redu mo sureal) massif lisupe  -1.;
* displacements conditions
    py0= point su2 droit pa pc 0.01;
    liy0= elem ( contou su2) appu strict py0;
    li2bc = liy0 elem compris pb pc;
    cl1= bloqu li2bc UY;
    cl2= bloq UX pb;
    cltot= cl1 et cl2;
* compute elastic solution
    ri = rigi mo ma;
    displa =  reso ( ri et cltot et rel) ff;
    stre = sigma displa mo ma;
    vm = vmis stre mo ma;
    trac  su2 vm mo ma;
* compute stress intensity factor
    gt = table;
    gt.'OBJECTIF' = MOT 'J';
    lifis = liy0 elem compris pa pb;
    gt.'LEVRE_SUPERIEURE' = lifis;
    gt.'FRONT_FISSURE' = Pb;
    gt.'CARACTERISTIQUES' = ma;
    gt.'MODELE' = mo;
    gt.'SOLUTION_RESO' = displa;
    rea = reaction  (ri et rel) displa;
    gt.'CHARGEMENTS_MECANIQUES'=rea;
    naa = 5;opti veri 1;
    repe no naa;
      gt.'COUCHE' = &no;
      G_THETA gt;
      si ( &no ega 1) ; g2=prog gt.resultats;sinon;
      g2 = g2 et ( prog gt.resultats); finsi;
    fin no;
    xx = prog 1 pas 1 naa;
    ev= evol manu  'nb of rows' xx 'G ' g2;
    ttt=table;
    ttt.1 = mot 'MARQ CROI';
    tt2=table;
    tt2.1= ' G ';
    ttt.'TITRE'=tt2;
    dess ev lege ttt;
$$$$



