$$$$ MEC2     NOTICE  CHAT      11/09/12    21:17:13     7124
                                             DATE     11/09/12
                                             Voir aussi :

Section : Presentation Presentation

FRAN===========================================================
*--------------------------------------------------------------------*
*                                                                    *
*           Exemple simple de calcul mecanique elastique             *
* fonctionnant avec les anciennes structures AFFECT,MODELE,CHAMELEM  *
*                                                                    *
*--------------------------------------------------------------------*
* Definition des options
*
           OPTI DIME 3 ELEM CU20 MODEL TRIDIM ; DENS 10 ;
*
* Maillage d'une barre de 5 elements CU20
*
  P1 = 0 0 0 ; P2 = 10 0 0 ; LI= P1 D 1 P2 ; SUB=LI TRAN (0 10 0) ;
   TOTAL = SUB VOLU TRAN (0 0 50) COUL JAUN ; SUH = TOTAL FACE 2 ;
     LIH = SUH COTE 2 ; TRAC TOTAL CACH (1000 -2000 1000) QUAL ;
*
* Formulation, materiau defini a l'aide de
* la procedure ACIER (voir operateur MATE)
*
            OBJMOD = MODELE TOTAL MECANIQUE ELASTIQUE  CU20 ;
                     CARM = ACIER A316L OBJMOD ;
*
* Conditions de blocages
*
                        ENC1 = BLOQ UZ SUB ;
               ENC2 = BLOQ UY LI ; ENC3 = BLOQ UX P1 ;
*
* Blocage pour deplacements imposes
*
                        ENC4 = BLOQ UZ SUH ;
                    ENC = ENC1 ET ENC2 ET ENC3 ;
*
* Raideur totale
*
                 RIG = (RIGI CARM OBJMOD) ET ENC ET ENC4 ;
*
* Valeur des deplacements imposes
*
                      FEXT = DEPI ENC4 1E-9  ;
*
* Force ponctuelle et force totale
*
         FNOD = FORCE LIH (0 1000 0) ; FTOT = FEXT ET FNOD ;
*
* Resolution  pour obtenir les deplacements
*
                        DEP = RESOU RIG FTOT ;
*
* Vecteur force et reaction
*
               VEF = VECTEUR FTOT 0.03 FX FY FZ ROUG ;
                   REA = REAC (ENC4 ET ENC) DEP ;
               VER = VECTEUR REA 0.01 FX FY FZ BLEU ;
*
* Contraintes et vonmises
*
               SSI = SIGMA DEP CARM OBJMOD; VM = VMIS SSI ;
*
* Deformee et trace
*
             DEF = DEFO TOTAL DEP 50 (VEF ET VER) VERT ;
                   TRAC DEF VM (1000 -2000 1000) ;
                                FIN ;

ANGL===========================================================
*--------------------------------------------------------------------*
*                                                                    *
*         Simple example of mechanical elastic calculation           *
*     making use of the former structures AFFECT,MODELE,CHAMELEM     *
*                                                                    *
*--------------------------------------------------------------------*
* Options definition
*
           OPTI DIME 3 ELEM CU20 MODEL TRIDIM ; DENS 10 ;
*
* Mesh of a 5-element bar CU20
*
  P1 = 0 0 0 ; P2 = 10 0 0 ; LI= P1 D 1 P2 ; SUB=LI TRAN (0 10 0) ;
   TOTAL = SUB VOLU TRAN (0 0 50) COUL JAUN ; SUH = TOTAL FACE 2 ;
     LIH = SUH COTE 2 ; TRAC TOTAL CACH (1000 -2000 1000) QUAL ;
*
* Formulation, material defined by means of the ACIER
* procedure (see MATE operator)
*
            OBJMOD = MODELE TOTAL MECANIQUE ELASTIQUE CU20 ;
                     CARM = ACIER A316L OBJMOD ;
*
* Boundary conditions
*
                        ENC1 = BLOQ UZ SUB ;
               ENC2 = BLOQ UY LI ; ENC3 = BLOQ UX P1 ;
*
* Restraints for imposed displacements
*
                        ENC4 = BLOQ UZ SUH ;
                    ENC = ENC1 ET ENC2 ET ENC3 ;
*
* Total stiffness
*
                 RIG = (RIGI CARM OBJMOD) ET ENC ET ENC4 ;
*
* Value of imposed displacements
*
                      FEXT = DEPI ENC4 1E-9  ;
*
* Punctual and total force
*
         FNOD = FORCE LIH (0 1000 0) ; FTOT = FEXT ET FNOD ;
*
* Resolution for getting the displacements
*
                        DEP = RESOU RIG FTOT ;
*
* Force vector and reaction
*
               VEF = VECTEUR FTOT 0.03 FX FY FZ ROUG ;
                   REA = REAC (ENC4 ET ENC) DEP ;
               VER = VECTEUR REA 0.01 FX FY FZ BLEU ;
*
* Stresses and Von Mises
*
               SSI = SIGMA DEP CARM OBJMOD; VM = VMIS SSI ;
*
* Deformed shape and plot
*
             DEF = DEFO TOTAL DEP 50 (VEF ET VER) VERT ;
                   TRAC DEF VM (1000 -2000 1000) ;
                                FIN ;




