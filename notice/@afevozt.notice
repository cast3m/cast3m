$$$$ @AFEVOZT NOTICE  GF238795  15/06/29    15:38:38     8564
                                             DATE     15/06/29

    Procedure @AFEVOZT                   Voir aussi : MONTAGNE,TRAC
    ------------------
   CHPO1 MAIL1 = @AFEVOZT TAB1 ('HORIZONTAL' | 'VERTICAL' ) (FLOT1);

Section : Post-traitement Affichage

FRAN==========================================================

La procedure @AFEVOZT permet creer un champ par point pour representer un jeu
d'evolutions sous forme d'iso-valeur. Ceci permet, par exemple, de representer
l'evolution d'un grandeur le long d'un axe et au cours du temps sur un unique
graphique.

Les evolutions doivent etre donnees dans une table contenant les entrees:
   -TAB1. i : evolutions a afficher (i est un entier);
   -TAB1.'TEMPS' : liste des temps (ou autre parametre) associes à chaque
   evolution.

Avec l'option 'HORIZONTAL', l'abscisse des evolutions est representee dans la
direction X, le temps dans la direction Y.
Avec l'option 'VERTICAL', l'abscisse des evolutions est representee dans la
direction Y, le temps dans la direction X.
Sans precision, l'option 'VERTICAL' est assumee.
Dans tous les cas, la couleur des iso-valeurs est basees sur l'ordonnees des
evolutions.

Le reel FLOT1 permet de preciser le rapport d'aspect entre l'abscisse des
evolutions et le temps.
Sans precision, le rapport d'aspect est de 1.0, ce qui signifie que sur
l'affichage, le temps est mis à l'echelle afin que le graphique soit un carre.
Avec un rapport d'aspect negatif, on peut changer le sens d'orientation de l'axe
des temps.
Sens d'augmentation du temps :
         |   HORIZONTAL   |  VERTICAL
FLOT1>0  |   vers le haut |  vers la droite
FLOT1<0  |   vers le bas  |  vers la gauche

La procedure renvoie un champ par point et son maillage support pour pouvoir
l'afficher avec l'operateur 'TRAC'.
Le maillage resultat contient le maillage support et les lignes marquant les
temps des evolutions.
Les lignes correspondants aux differents instants peuvent être obtenues par la
commande :
('ELEM' mail1 'SEG2').
Pour tracer le graphique en visualisant les differents instants et en cachant le
maillage support, on pourra utiliser la commande :
'TRAC' CHPO1 ('ELEM' MAIL1 'TRI3') ('ELEM' MAIL1 'SEG2');

Exemple :
'OPTI' 'DIME' 2;
*Creation de la table
TATEST = 'TABLE';
LITEMP = 'PROG' 0. 'PAS' 0.5 5 5.1 5.2 5.3 5.4 5.5 6 'PAS' 1 10;

TATEST . 'TEMPS' = LITEMP;
LIZ = 'PROG' 0. 'PAS' 0.05 1.0;
*Remplissage des evolutions
'REPE' ITP ('DIME' LITEMP);
   TPCOUR = 'EXTR' LITEMP &ITP;
   LIVAL = 'PROG';
   'REPE' itz ('DIME' liz);;
      ZCOUR = 'EXTR' LIZ &ITZ;
      VALCOUR = ('SIN' ( (360. * ZCOUR - 90) + ( 50 * ('COS' (20. *
      TPCOUR))))) * (1 + (TPCOUR / 10.));
      LIVAL = LIVAL 'ET' VALCOUR;
   'FIN'ITZ;
   TATEST . &ITP = 'EVOL' 'MANU' 'ABSC' LIZ 'ORDO' LIVAL;
'FIN' ITP;

LIST TATEST;
*Trace sous forme d'evolution
EVOTOT = 'VIDE' 'EVOLUTION';
'REPE' ITP ('DIME' LITEMP);
   EVOTOT = EVOTOT 'ET' TATEST . &ITP ;
'FIN' ITP;
DESS EVOTOT;
*Trace sous forme d'iso-valeur
MATEST CHTEST = @AFEVOZT TATEST 'HORIZONTAL' -0.5;
TRAC CHTEST MATEST ('CONTOUR' MATEST);
TRAC CHTEST MATEST ('ELEM' MATEST 'SEG2');

ANGL==========================================================

The @AFEVOZT procedure creates a field by points to represent a set of
evolutions by a iso-values drawing. This enables, by example, to draw the
evolution of a value along an axis and varying with the time.

The evolutions must be given in a table containing the entries:
   -TAB1. i : evolutions to draw (i is an integer);
   -TAB1.'TEMPS' : time (or other parameter) list associated to each evolution.

With the 'HORIZONTAL' option, the abscissa of the evolutions is represented
along the X axis, the time along the Y axis.
With the 'VERTICAL' option, the abscissa of the evolutions is represented along
the Y axis, the time along the X axis.
Without precision, the 'VERTICAL' option is assumed.
In any case, the iso-values colors are based on the ordinate of the evolutions

The real 'FLOT1' enables to impose the aspect ration between the time and the
abscissa of the evolutions.
Without precision, the aspect ration is 1.0 which means that the time is scaled
in order to obtain a square graphic.
With a negative aspect ratio, the orientation of the time axis can be changed.
Direction of the time axis:
         |   HORIZONTAL   |  VERTICAL
FLOT1>0  |   upward       |  to the right
FLOT1<0  |   downward     |  to the left

The procedure gives back a field by point and it support mesh in order to draw
it with the 'TRAC' operator.
The result mesh contains the support mesh and the line marking the time of the
different evolutions.
The line corresponding to the different time can be obtained by the command:
('ELEM' mail1 'SEG2').
To draw the graph visualizing the time line but without visualizing the support
mesh, the following command can be used:
'TRAC' CHPO1 ('ELEM' MAIL1 'TRI3') ('ELEM' MAIL1 'SEG2');

Example:
'OPTI' 'DIME' 2;
*Creation of the table
TATEST = 'TABLE';
LITEMP = 'PROG' 0. 'PAS' 0.5 5 5.1 5.2 5.3 5.4 5.5 6 'PAS' 1 10;
TATEST . 'TEMPS' = LITEMP;
LIZ = 'PROG' 0. 'PAS' 0.05 1.0;
*Creation of the example evolutions
'REPE' ITP ('DIME' LITEMP);
   TPCOUR = 'EXTR' LITEMP &ITP;
   LIVAL = 'PROG';
   'REPE' itz ('DIME' liz);;
      ZCOUR = 'EXTR' LIZ &ITZ;
      VALCOUR = ('SIN' ( (360. * ZCOUR - 90) + ( 50 * ('COS' (20. *
      TPCOUR))))) * (1 + (TPCOUR / 10.));
      LIVAL = LIVAL 'ET' VALCOUR;
   'FIN'ITZ;
   TATEST . &ITP = 'EVOL' 'MANU' 'ABSC' LIZ 'ORDO' LIVAL;
'FIN' ITP;

LIST TATEST;
*Draw with evolution
EVOTOT = 'VIDE' 'EVOLUTION';
'REPE' ITP ('DIME' LITEMP);
   EVOTOT = EVOTOT 'ET' TATEST . &ITP ;
'FIN' ITP;
DESS EVOTOT;
*Draw with iso-values
MATEST CHTEST = @AFEVOZT TATEST 'HORIZONTAL' -0.5;
TRAC CHTEST MATEST ('CONTOUR' MATEST);
TRAC CHTEST MATEST ('ELEM' MATEST 'SEG2');

$$$$

