C TIFOLO    SOURCE    CHAT      05/01/13    03:39:23     5004
      SUBROUTINE TIFOLO(XL,V,CONT,LHOOK,DDHOOK,IDEUX)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES CONTRAINTES DE LA POUTRE DROITE
C    DE TIMOSHENKO MULTI-FIBRE D APRES TEDEL
C
C  ENTREE
C    XL      LONGUEUR DE LA POUTRE DROITE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    DDHOOK(LHOOK,LHOOK) MATRICE DE HOOK
C    IDEUX   INDICE POUR DEFORMATIONS DU SECOND ORDRE
C  SORTIE
C    CONT    CONTRAINTES AU MILIEU 6 PAR NOEUD
C
C-----------------------------------------------------------------------
C     Pierre Pegon (ISPRA) Juillet/Aout 1993
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),V(*),DDHOOK(LHOOK,LHOOK),EPSI(6)
      DATA P5/0.5D0/
C
      RL=1.D00/XL
C
C  DEFORMATIONS GENERALISEES
C
      EPSI(1) = (V(7)-V(1))*RL
      EPSI(2) = (V(8)-V(2))*RL - (V(6)+V(12))*P5
      EPSI(3) = (V(9)-V(3))*RL + (V(5)+V(11))*P5
      EPSI(4) = (V(10)-V(4))*RL
      EPSI(5) =-(V(5)-V(11))*RL
      EPSI(6) =-(V(6)-V(12))*RL
C
C  TERMES DU SECOND ORDRE
C
      IF(IDEUX.NE.0) THEN
      ENDIF
C
C  CONTRAINTES GENERALISEES
C
      DO I=1,6
        CONT(I)=0.D0
        DO J=1,6
          CONT(I)=CONT(I)+DDHOOK(I,J)*EPSI(J)
        END DO
      END DO
C
      RETURN
      END

