C TSEG3A    SOURCE    CHAT      05/01/13    03:53:47     5004
      SUBROUTINE TSEG3A(POINTS,QSIGAU,ETAGAU,DZEGAU,POIGAU,SHPTOT)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
************************************************************************
*
*                             T S E G 3 A
*                             -----------
*
* FONCTION: ON ACQUIERT:
* ---------    - LES COORDONNEES INTRINSEQUES
*              - LES FONCTIONS DE FORME ET LEURS DERIVEES
*           POUR L'ELEMENT DE THERMIQUE SEG3.
*
* MODULES UTILISES:
* -----------------
*
-INC CCOPTIO
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE   (+ = CONTENU DANS UN COMMUN)
* -----------
*
*     POINTS  (E)  CHAINE DE CARACTERES:
*                  'NOEUDS' SI ON VEUT LES VALEURS AUX NOEUDS
*                  'GAUSS' POUR LES VALEURS AUX POINTS DE GAUSS.
*     QSIGAU  (S) |
*     ETAGAU  (S) | COORDONNEES INTRINSEQUES.
*     DZEGAU  (S) |
*     POIGAU  (S)  POIDS D'INTEGRATION,SI ON A 'GAUSS'.
*     SHPTOT  (S)  FONCTIONS DE FORME ET LEURS DERIVEES.
*
      REAL*8 QSIGAU(3),ETAGAU(3),DZEGAU(3),POIGAU(3)
      REAL*8 SHPTOT(6,3,3)
      CHARACTER*(*) POINTS
*
* CONSTANTES:
* -----------
*
*
      PARAMETER ( O0=0.D0 )
      PARAMETER ( O1=1.D0 )
      PARAMETER ( O5=0.5D0 )
      PARAMETER ( O555=0.555555555555556D0 )
      PARAMETER ( O774=0.774596669241483D0 )
      PARAMETER ( O888=0.888888888888889D0 )
*
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     DENIS ROBERT,LE 04 FEVRIER 1988.
*
* LANGAGE:
* --------
*
*             FORTRAN77
*
************************************************************************
*
      IF (POINTS(1:5).EQ.'NOEUD') GOTO 66
      IF (POINTS(1:5).NE.'GAUSS') GOTO 100
*
*     COORDONNEES DES 3 POINTS DE GAUSS
*
      NBPGAU=3
      NBNO=3
      QSIGAU(1)=-O774
      QSIGAU(3)=-QSIGAU(1)
      QSIGAU(2)=O0
      DO 10 I=1,3
         ETAGAU(I)=O0
         DZEGAU(I)=O0
 10      CONTINUE
*     END DO
      POIGAU(1)=O555
      POIGAU(3)=POIGAU(1)
      POIGAU(2)=O888
      GOTO 2
*
*     COORDONNEES DES NOEUDS
*
   66 CONTINUE
      NBNO=3
      NBPGAU=NBNO
      QSIGAU(1)=-O1
      QSIGAU(3)=O1
      QSIGAU(2)=O0
      DO 11 I=1,3
         ETAGAU(I)=O0
         DZEGAU(I)=O0
 11      CONTINUE
*     END DO
*
*     FONCTIONS DE FORME ET LEURS DERIVEES
*
    2 DO 1 I=1,NBPGAU
      XI=QSIGAU(I)
      SHPTOT(1,1,I)=O5*XI*(XI-O1)
      SHPTOT(1,2,I)=O1-XI*XI
      SHPTOT(1,3,I)=O5*XI*(XI+O1)
*
      SHPTOT(2,1,I)=XI-O5
      SHPTOT(2,2,I)=-XI-XI
      SHPTOT(2,3,I)=XI+O5
 1    CONTINUE
      RETURN
 100  CONTINUE
      CALL ERREUR(19)
*
      END

