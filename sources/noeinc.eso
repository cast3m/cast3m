C NOEINC    SOURCE    CB215821  19/03/18    21:15:46     10161
      SUBROUTINE NOEINC(IMEL1,CTYP,SREPE1)
C***********************************************************************
C NOM         : noeinc.eso
C DESCRIPTION : Tableau des MELEME / POINTS nommes dont les numéros de
C               noeuds sont strictement inclus dans IMEL1
C***********************************************************************
C HISTORIQUE :  10/10/2018 : BERTHINC : Creation
C HISTORIQUE :
C***********************************************************************
C Prière de PRENDRE LE TEMPS DE COMPLÉTER LES COMMENTAIRES
C en cas de modification de ce sous-programme afin de faciliter
C la maintenance !
C***********************************************************************
C APPELÉ PAR : SORMED.ESO
C***********************************************************************
C ENTRÉES    : IMEL1
C SORTIES    : SREPE1
C***********************************************************************

      IMPLICIT INTEGER(i-n)
      IMPLICIT REAL*8(a-h,o-z)

-INC CCNOYAU
-INC CCASSIS
-INC CCOPTIO
-INC SMELEME
-INC SMCOORD

      CHARACTER*8 CTYP

C     SEGMENT pour repertorier les objets nommes et leur nom
      SEGMENT SREPER
        INTEGER           IREPER(NBENT)
        CHARACTER*(LONOM) CREPER(NBENT)
C       NBENT   : Nombre d'objets
C       IREPER  : OBJETS (Pointeur ou ENTIER pour les 'POINT')
C       CREPER  : Noms des OBJETS
      ENDSEGMENT
      POINTEUR SREPE1.SREPER

C     SegmentS de travail
      SEGMENT ICPR(iiicpr)

C***********************************************************************
C DEBUT DES INSTRUCTIONS
C***********************************************************************
      IF (CTYP.NE.'MAILLAGE' .AND. CTYP.NE.'POINT   ') THEN
        CALL ERREUR(5)
      ENDIF

C     On recupere les entites nommes et leurs noms
      CALL REPER1(CTYP, SREPER)
      IF (IERR.NE.0) RETURN

C***********************************************************************
C     On place les noeuds tests dans ICPR
      SEGACT,MCOORD
      iiicpr=XCOOR(/1) / (IDIM+1)
      SEGINI,ICPR

      IPT1=IMEL1
      SEGACT,IPT1
      NBSOUS=IPT1.LISOUS(/1)
      IF(NBSOUS .EQ. 0)THEN
C       Cas MELEME SIMPLE
        DO iel=1,IPT1.NUM(/2)
          DO inoe=1,IPT1.NUM(/1)
            ICPR(IPT1.NUM(inoe,iel)) = 1
          ENDDO
        ENDDO

      ELSE
C       Cas MELEME COMPLEXE
        DO isous=1,NBSOUS
          IPT2=IPT1.LISOUS(isous)
          SEGACT,IPT2
          DO iel=1,IPT2.NUM(/2)
            DO inoe=1,IPT2.NUM(/1)
              ICPR(IPT2.NUM(inoe,iel)) = 1
            ENDDO
          ENDDO
        ENDDO
      ENDIF


      NBENT1=SREPER.IREPER(/1)
      NBENT=NBENT1
      SEGINI,SREPE1
      NBENT2=0

      IF    (CTYP .EQ. 'MAILLAGE') THEN
C       ****************************************************************
C       * CAS des objets de type 'MAILLAGES'
C       ****************************************************************
C       On repere quel MELEME nommes est inclu dans IMEL1
        DO 10 iii=1,NBENT1
          IPT1=SREPER.IREPER(iii)
          SEGACT,IPT1
          NBSOUS=IPT1.LISOUS(/1)
          IF(NBSOUS .EQ. 0)THEN
C           Cas MELEME SIMPLE
            DO iel=1,IPT1.NUM(/2)
              DO inoe=1,IPT1.NUM(/1)
                IF(ICPR(IPT1.NUM(inoe,iel)) .NE. 1) GOTO 10
              ENDDO
            ENDDO

          ELSE
C           Cas MELEME COMPLEXE
            DO isous=1,NBSOUS
              IPT2=IPT1.LISOUS(isous)
              SEGACT,IPT2
              DO iel=1,IPT2.NUM(/2)
                DO inoe=1,IPT2.NUM(/1)
                  IF(ICPR(IPT2.NUM(inoe,iel)) .NE. 1) GOTO 10
                ENDDO
              ENDDO
            ENDDO
          ENDIF

          NBENT2=NBENT2+1
          SREPE1.IREPER(NBENT2)=SREPER.IREPER(iii)
          SREPE1.CREPER(NBENT2)=SREPER.CREPER(iii)
 10     CONTINUE

      ELSEIF(CTYP .EQ. 'POINT   ')THEN
C       ****************************************************************
C       * CAS des objets de type 'POINT'
C       ****************************************************************
C       On repere quel POINT nommes est inclu dans IMEL1
        DO 20 iii=1,NBENT1
          inoe=SREPER.IREPER(iii)
          IF(ICPR(inoe) .NE. 1) GOTO 20

          NBENT2=NBENT2+1
          SREPE1.IREPER(NBENT2)=SREPER.IREPER(iii)
          SREPE1.CREPER(NBENT2)=SREPER.CREPER(iii)
 20     CONTINUE

      ELSE
        CALL ERREUR(5)
      ENDIF

C     Ajustement final
      IF (NBENT2 .NE. NBENT) THEN
        NBENT=NBENT2
        SEGADJ,SREPE1
      ENDIF
      SEGSUP,ICPR

      END

