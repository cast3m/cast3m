C UCHIDA    SOURCE    SERRE     16/12/13    21:15:13     9254
      SUBROUTINE UCHIDA(ICOR,IPT1,IPT2,IPT3,IPT4,
     &                  MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,
     &                  MPOVA6,MPOVA7,MPOVA8,MPOVA9,MPOVAA,
     &                  MPOVAB,HMAX,BETA,GAMA,TTAG,TIME2,TAGA,
     &                  MPOVAC,MPOVAD,MPOVAE,MPOUT4)
C--------------------------------------------------------------------
C Evaluation du coef. d'échange thermique en paroi et du débit de
C condensation lorsque la corrélation est UCHIDA ou TAGAMI-UCHIDA
C--------------------------------------------------------------------
C
C  E/   ICOR   : Corrélation à considérer (1=Tagami , 2=Uchida)
C  E/   IPT1   : Maillage des points CELL
C  E/   IPT2   : Maillage des points LAYERW
C  E/   IPT3   : Maillage des points WALL
C  E/   IPT4   : Maillage de SEG2 CELL/LAYERW
C  E/   MPOVA1 : Densité partielle de vapeur (en kg/m3)
C  E/   MPOVA2 : Densité du mélange - (en kg/m3)
C  E/   MPOVA3 : Densité H2O - vapeur et brouillard - (en kg/m3)
C  E/   MPOVA4 : Coefficient d'échange par conduction
C  E/   MPOVA5 : Pression totale
C  E/   MPOVA6 : Pression partielle de vapeur
C  E/   MPOVA7 : Température de saturation
C  E/   MPOVA8 : Température du mélange gazeux
C  E/   MPOVA9 : Température des murs
C  E/   MPOVAA : Surface des parois
C  E/   MPOVAB : Facteur correctif pour les murs en béton
C  E/   HMAX   : Borne sup pour le coefficient de transfert thermique
C  E/   BETA   : Fraction de l'énergie associée à la condensation
C                transférée au mur (défaut 92%)
C  E/   GAMA   : Coef. de lissage au voisinage de Tsat (defaut 0.5)
C  E/   TTAG   : Temps caractéristique pour Tagami
C  E/   TIME2  : Temps physique en fin de pas de temps
C  E/   TAGA   : Partie constante du modèle de Tagami
C   /S  MPOVAC : Coefficient d'échange global en paroi
C   /S  MPOVAD : Débit de condensation / flux thermique
C   /S  MPOVAE : Enthalpie liquide
C   /S  MPOUT4 : Coefficient d'échange convectif en paroi
C
C--------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C Auteurs : F.DABBENE 02/01
C
C--------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      POINTEUR MPOVA7.MPOVAL,MPOVA8.MPOVAL,MPOVA9.MPOVAL,MPOVAA.MPOVAL
      POINTEUR MPOVAB.MPOVAL,MPOVAC.MPOVAL,MPOVAD.MPOVAL,MPOVAE.MPOVAL
      POINTEUR MPOUT4.MPOVAL
C
-INC CCOPTIO
-INC SMCHPOI
-INC SMCOORD
-INC SMELEME
-INC SMTABLE
      SEGMENT REDIR
        INTEGER IND1(NNGOT),IND2(NNGOT),IND3(NNGOT)
      ENDSEGMENT
C
C- Tableau d'adressage indirect
C
      SEGACT IPT1,IPT2,IPT3
      NNGOT  = XCOOR(/1)/(IDIM+1)
      SEGINI REDIR
      NBLAY  = IPT2.NUM(/2)
      DO 10 I=1,NBLAY
         K = IPT2.NUM(1,I)
         IND2(K) = I
 10   CONTINUE
      NBWAL  = IPT3.NUM(/2)
      DO 20 I=1,NBWAL
         K = IPT3.NUM(1,I)
         IND3(K) = I
 20   CONTINUE
      NBCEL  = IPT1.NUM(/2)
      DO 30 I=1,NBCEL
         K = IPT1.NUM(1,I)
         IND1(K) = I
 30   CONTINUE

      SEGINI, MPOVAC=MPOVA4
      N  = MPOVA4.VPOCHA(/1)
      NC = 1
      SEGINI MPOVAD,MPOVAE,MPOUT4
C
C- Pour chaque couple CELL/LAYERW on calcule les grandeurs liees a la
C  condensation
C
      SEGACT IPT4
      N1 = IPT4.NUM(/2)
      DO 70 I=1,N1
C
C Evaluation du coefficient d'échange thermique lié à la condensation
C
       IPOS = IND2(IPT4.NUM(2,I))
       IF (IPOS.NE.0) THEN
         II1  = IND1(IPT4.NUM(1,I))
         JPOS = IND3(IPT4.NUM(2,I))
         ROV  = MPOVA1.VPOCHA(II1,1)
         RO   = MPOVA2.VPOCHA(II1,1)
         ROH  = MPOVA3.VPOCHA(II1,1)
         X1   = ROV / (RO - ROH)
         IF (ICOR.EQ.1) THEN
            H1 = 11.351D0 + 283.77D0*X1
            IF (TIME2.LE.TTAG) THEN
               H2 = ((TIME2/TTAG) ** 0.5) * TAGA
               H1 = H2
            ELSE
               H2 = TAGA
               E1 = EXP((TTAG-TIME2)/20.D0)
               H1 = (H2-H1)*E1 + H1
            ENDIF
            H1 = MIN(H1,HMAX)
         ELSE
            H1 = MIN(11.351D0 + 283.77D0*X1,HMAX)
         ENDIF
C
C Coeff d'échange : correction pour les murs en béton
C Evaluation du débit de condensation
C
         PTOT = MPOVA5.VPOCHA(II1,1)
         PVAP = MPOVA6.VPOCHA(II1,1)
         TSAT = MPOVA7.VPOCHA(II1,1)
         TGAS = MPOVA8.VPOCHA(II1,1)
         TMUR = MPOVA9.VPOCHA(JPOS,1)
         AREA = MPOVAA.VPOCHA(IPOS,1)
         XCON = MPOVAB.VPOCHA(IPOS,1)
         HCON = MPOVA4.VPOCHA(IPOS,1)
C
C---> C'est ici que l'on fixe les échanges d'énergie comp/mur :
C--->   Etat thermodynamique du comp    = PVAP TGAS
C--->   Etat thermodynamique du puisard = PTOT TMUR
C---> La chaleur Latente découle de la difference comp/puisard
C---> pour les états thermo ci-dessus CHOISIS A PRIORI.
C
         HL   = HLS0(PTOT,TMUR)
         HV   = HVS0(PVAP,TGAS)
         HLAT = HV - HL
C
         MPOVAC.VPOCHA(IPOS,1) = MAX(XCON*H1,HCON)
         IF (TMUR.LT.TSAT) THEN
            DT = TSAT - TMUR
            IF (DT.LT.10.D0) THEN
               Z1 = 1.D0 - EXP(-1.D0*DT*DT)
            ELSE
               Z1 = 1.D0
            ENDIF
            DT2 = ABS(TGAS - TSAT)
            B2  = (BETA + (1.D0-BETA)*EXP(-1.D0*GAMA*DT2))*Z1
            MPOVAD.VPOCHA(IPOS,1) = -1.D0 * AREA * B2 / HLAT
            MPOUT4.VPOCHA(IPOS,1) = MPOVAC.VPOCHA(IPOS,1) * (1.D0 - B2)
         ELSE
            MPOVAD.VPOCHA(IPOS,1) = 0.D0
            MPOUT4.VPOCHA(IPOS,1) = MPOVAC.VPOCHA(IPOS,1)
         ENDIF
         MPOVAE.VPOCHA(IPOS,1) = HL
       ENDIF
  70  CONTINUE
C
C- Ménage
C
      SEGSUP REDIR
      SEGDES IPT1,IPT2,IPT3,IPT4
C
      RETURN
      END





