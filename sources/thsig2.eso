C THSIG2    SOURCE    CHAT      05/01/13    03:38:04     5004
      SUBROUTINE THSIG2(SHP,DJAC,XSTRS,NBNN,LRE,REL,N,R)
C=====================================================================
C
C   CALCULE SOMME H,I * SIG I J *H ,J
C   MODE DE FOURIER
C
C  ENTREE
C    SHP(6,NBNO)   = H   H,X   H,Y  H,Z
C    DJAC          = JACOBIEN
C    XSTRS(NSTRS)  = CONTRAINTES
C    NBNN          = NOMBRE DE NOEUDS
C    LRE           = TAILLE DE LA MATRICE DE RIGIDITE
C    REL(LRE,LRE)  = MATRICE DE RIGIDITE    (LRE=3*NBNN)
C    N             = Mode de Fourier
C    R             = rayon
C
C  SORTIES
C    REL(LRE,LRE)=MATRICE DE RIGIDITE
C    JEANVOINE AOUT 95
C=======================================================================

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION SHP(6,*),XSTRS(*),REL(LRE,*)

      DO 100 IA=1,NBNN
        DO 200 IB=1,NBNN
C
          CC1
     1     = SHP(2,IA)*XSTRS(1)*SHP(2,IB) + SHP(3,IA)*XSTRS(2)*SHP(3,IB)
     1     + SHP(2,IA)*XSTRS(4)*SHP(3,IB) + SHP(3,IA)*XSTRS(4)*SHP(2,IB)
     1     + ((1+(N*N))/(R**2))*SHP(1,IA)*XSTRS(3)*SHP(1,IB)
C
          CC2
     1     = SHP(2,IA)*XSTRS(1)*SHP(2,IB) + SHP(3,IA)*XSTRS(2)*SHP(3,IB)
     1     + SHP(2,IA)*XSTRS(4)*SHP(3,IB) + SHP(3,IA)*XSTRS(4)*SHP(2,IB)
     1     + ((N*N)/(R**2))*SHP(1,IA)*XSTRS(3)*SHP(1,IB)
C
          CC3
     1     = SHP(2,IA)*XSTRS(1)*SHP(2,IB) + SHP(3,IA)*XSTRS(2)*SHP(3,IB)
     1     + SHP(2,IA)*XSTRS(4)*SHP(3,IB) + SHP(3,IA)*XSTRS(4)*SHP(2,IB)
     1     + ((1+(N*N))/(R**2))*SHP(1,IA)*XSTRS(3)*SHP(1,IB)
C
          CC4 = 2*N/(R*R)*SHP(1,IA)*XSTRS(3)*SHP(1,IB)
C
           REL(1+3*(IA - 1),1+3*(IB - 1)) = DJAC*CC1 +
     1     REL(1+3*(IA - 1),1+3*(IB - 1))
           REL(2+3*(IA - 1),2+3*(IB - 1)) = DJAC*CC2 +
     1     REL(2+3*(IA - 1),2+3*(IB - 1))
           REL(3+3*(IA - 1),3+3*(IB - 1)) = DJAC*CC3 +
     1     REL(3+3*(IA - 1),3+3*(IB - 1))
           REL(3+3*(IA - 1),1+3*(IB - 1)) = DJAC*CC4 +
     1     REL(3+3*(IA - 1),1+3*(IB - 1))
           REL(1+3*(IA - 1),3+3*(IB - 1)) = DJAC*CC4 +
     1     REL(1+3*(IA - 1),3+3*(IB - 1))
C
  200   CONTINUE
  100 CONTINUE

      RETURN
      END


