C BECALC    SOURCE    DURA      06/07/11    21:17:09     5481
       SUBROUTINE BECALC(PE,PVE,TE,PS,XL,DX,RUG,QINI,XW,NNP,XN,TN,EN,BN,
     $       KIMP,NT,NX,XX,XP,XPV,XT,XY,XU,XHF,XQ,XQW,XQA,XRE,XDH,RECU,
     $       XKUL,XKUT1,XKUT2,XKUT3,XKUT4)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     operateur FUITE
C>>>  parametres d'entree:
C       PE  : pression totale a l'entree
C       PVE : pression de vapeur a l'entree
C       TE  : temperature d'entree
C       PS  : pression de sortie
C       XL  : longueur de la fissure
C       DX  : taille relative maille fluide
C       RUG : rugosite
C       QINI: debit initialisation
C       XW  : parametre de condensation
C       XN TN EN : abscisses temperature et epaisseur de la paroi
C                   (dimension NNP)
C       KIMP: parametre d'impression
C       NT  : dimension max. grille fluide
C     RECU,XKUL,XKUT1,XKUT2,XKUT3,XKUT4 : coef lois de frot utilisateur
C
C>>>  parametres de sortie:
C       NX  : nombre de points grille fluide
C       pour la grille fluide
C       XX  : coordonnees relatives
C       XP XPV XT XU XY XHF XQ XQW XQA XRE
C       pression, pression vapeur, temperature, titre, vitesse
C       coef. echange, debit total, debit eau (liq.+vap.), debit air
C       Reynolds

      DIMENSION XX(NT),XP(NT),XT(NT),XY(NT),XU(NT),XPV(NT)
      DIMENSION XHF(NT),XQ(NT),XQW(NT),XQA(NT),XRE(NT),XDH(NT)
      DIMENSION XN(NNP),TN(NNP),EN(NNP),BN(NNP)
C
      IF(KIMP.GE.1) THEN
        WRITE(6,*) 'entree becalc  '
      ENDIF
      TE = TE + 273.15
      IF (QINI.LT.1.E-5) THEN

C>>>  calcul du debit correspondant a un melange gazeux isotherme
C     a la temperature d'entree
C
      E=(EN(1)+EN(NNP))/2.
      B=(BN(1)+BN(NNP))/2.
      CALL BCALQ0(PE,PVE,TE,PS,XL,E,B,RUG,Q0,RECU,XKUL,XKUT1,XKUT2,
     * XKUT3,XKUT4)
      ELSE
      Q0 = QINI
      ENDIF
C
C>>>  calcul du debit solution

      CALL BCALDP(PE,PVE,TE,PS,XL,DX,RUG,Q0,XW,NNP,XN,TN,EN,BN,
     $           KIMP,NT,NX,XX,XP,XT,XY,XU,XHF,XQ,XQW,
     $               Q,QAE,XRE,XDH,RECU,XKUL,XKUT1,XKUT2,XKUT3,XKUT4)

C>>>  calcul de la pression vapeur et des debits resultats

      DO 15 I=1,NX

         IF (XY(I).GT.0.99) THEN
            PHI=PVE/PE
            XPV(I)=PHI*XP(I)
         ELSE
            T=XT(I)+273.15
            XPV(I)=BPSAT(T)
         ENDIF

C>>   a revoir
         XQA(I) = QAE
         XQW(I) = XQ(I)
         XQ (I) = XQA(I)+XQW(I)

  15  CONTINUE
C
C>>   a revoir
       XHF(1) = XHF(2)
       XRE(1) = XRE(2)
       XDH(1) = XDH(2)

      RETURN
      END







