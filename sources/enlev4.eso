C ENLEV4    SOURCE    CB215821  18/01/29    21:15:07     9715
      SUBROUTINE ENLEV4 (IPOINT,IEME,IPOIN3,IPOS)
C***********************************************************************
C
C                             E N L E V 4
C                             -----------
C
C FONCTION:
C ---------
C
C     ENLEVER LE I-EME "CHPOINT" D'UNE SUITE DE TYPE "LISTCHPO".

C PARAMETRES:   (E)=ENTREE   (S)=SORTIE
C -----------
C
C     IPOINT  ENTIER    (E)  POINTEUR DE LA SUITE DE TYPE "LISTCHPO".
C     IEME    ENTIER    (E)  NUMERO D'ORDRE DU "CHPOINT" A ENLEVER DANS
C                            LA SUITE DE TYPE "LISTCHPO".
C     IPOIN3  ENTIER    (S)  POINTEUR DU 'LISTCHPO' CREE.
C     IPOS    ENTIER    (E)  SI NEGATIF, IEME EST UN POINTEUR "LISTENTI"
C
C SOUS-PROGRAMMES APPELES:
C ------------------------
C
C     ERREUR
C
C AUTEUR, DATE DE CREATION:
C -------------------------
C
C     PASCAL MANIGOT     22 FEVRIER 1985
C
C LANGAGE:
C --------
C
C     ESOPE + FORTRAN77
C
C***********************************************************************
C
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC SMLCHPO
-INC SMLENTI
C
      MLCHPO = IPOINT
      SEGACT,MLCHPO
      LDIM = ICHPOI(/1)

C     Y a-t-il plusieurs remplacements a faire ?
      IF (IPOS.LT.0) THEN
        MLENT1=IEME
        NBENLE=MLENT1.LECT(/1)

        IF (NBENLE .EQ. 0) THEN
C         Cas de la liste VIDE
          SEGINI,MLCHP1=MLCHPO
          IPOIN3=MLCHP1
          RETURN
        ENDIF

C       Le LISTENTI est ordonne donc le min en 1 et le max en NBENLE
        IPOMIN=MLENT1.LECT(1)
        IPOMAX=MLENT1.LECT(NBENLE)

        IF     (IPOMIN.LT.1   ) THEN
C         L'indice %i1 est au dela des bornes de la liste
          INTERR(1) = IPOMIN
          CALL ERREUR(620)
          RETURN
        ELSEIF (IPOMAX.GT.LDIM) THEN
C         L'indice %i1 est au dela des bornes de la liste
          INTERR(1) = IPOMAX
          CALL ERREUR(620)
          RETURN
        ENDIF

      ELSE
        NBENLE=1
        IF (IEME.LT.1 .OR. IEME.GT.LDIM) THEN
C         L'indice %i1 est au dela des bornes de la liste
          INTERR(1) = IEME
          CALL ERREUR(620)
          RETURN
        ENDIF
      ENDIF
C
      N1=LDIM - 1
      SEGINI,MLCHP1
C
      IENLE = 1
      DO II=1,LDIM
        IF (IPOS.LT.0) IEME=MLENT1.LECT(IENLE)
        IF (II.EQ.IEME) THEN
          IENLE = IENLE + 1
          IF (IENLE.GT.NBENLE) GOTO 10
        ELSE
          MLCHP1.ICHPOI(II-IENLE+1) = ICHPOI(II)
        ENDIF
      ENDDO

 10   IF (IEME.LT.LDIM) THEN
        DO II=IEME+1,LDIM
          MLCHP1.ICHPOI(II-NBENLE) = ICHPOI(II)
        ENDDO
      ENDIF
C
      N1=LDIM-NBENLE
      SEGADJ,MLCHP1
      IPOIN3=MLCHP1
      SEGDES,MLCHP1

      RETURN
      END


