C EQUATN    SOURCE    CB215821  16/04/21    21:16:39     8920
       SUBROUTINE EQUATN(INPLAS,SIG,TIME,DT,XMAT,DEPSCP,DEPSCS)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL *8 (A-H,O-Z)
       DIMENSION XMAT(*)
       IF(INPLAS.EQ.20) THEN
C
C   BLACKBURN EQUATION
C
       F1    = XMAT(6)*EXP(XMAT(7)*SIG)
       F2    = XMAT(8)*SIG**XMAT(9)
       FS    = F1 + F2
       R1    = XMAT(10)*EXP(XMAT(11)*SIG)
       R2    = XMAT(12)*SIG**XMAT(13)
       RS    = R1 + R2
       T1    = XMAT(15)*SIG
       SINH  = 0.5D0*(EXP(T1)-EXP(-T1))
       G1    = XMAT(14)*SINH**XMAT(16)
       G2    = XMAT(17)*EXP(XMAT(18)*SIG)
       GS    = G1 + G2
       DEPSCP= FS * RS * EXP(-RS*TIME)
       DEPSCS= GS
       RETURN
C
       ELSE IF(INPLAS.EQ.61) THEN
C
C   BLACKBURN_2 EQUATION
C
       F1    = XMAT(6)*EXP(XMAT(7)*SIG)
       F2    = XMAT(8)*SIG**XMAT(9)
       FS    = F1 + F2
       R1    = XMAT(10)*EXP(XMAT(11)*SIG)
       R2    = XMAT(12)*SIG**XMAT(13)
       RS    = R1 + R2
       T1    = XMAT(15)*SIG
       SINH  = 0.5D0*(EXP(T1)-EXP(-T1))
       G1    = XMAT(14)*SINH**XMAT(16)
       G2    = XMAT(17)*SIG**XMAT(18)
       GS    = G1 + G2
       DEPSCP= FS * RS * EXP(-RS*TIME)
       DEPSCS= GS
       RETURN
C
       ELSE IF(INPLAS.EQ.22) THEN
C
C   RCC-MR EQUATION FOR 1S MATERIAL (SS 316LN)
C
       DEPSCP= 0.0D0
       DEPSCS= 0.0D0
       TFP   = XMAT(9)*SIG**XMAT(10)
       IF(TIME.LT.DT) TIME=ABS(DT)
       IF(TIME.LE.TFP)
     & DEPSCP=(XMAT(6)*XMAT(7))*(TIME**(XMAT(7)-1.0D0))*(SIG**XMAT(8))
       IF(TIME.GT.TFP)
     & DEPSCS=XMAT(11)*SIG**XMAT(12)
       RETURN
       ELSE IF(INPLAS.EQ.23) THEN
C
C   RCC-MR EQUATION FOR 2S MATERIAL (SS 304LN)
C
       CON1  = XMAT(7)/XMAT(8)*SIG
       EPSM  = XMAT(6)*(0.5D0*(EXP(CON1)-EXP(-CON1)))**XMAT(8)
       CON2  = XMAT(10)/XMAT(11)*SIG
       R     = XMAT(9)*(0.5D0*(EXP(CON2)-EXP(-CON2)))**XMAT(11)
       EPSTR = XMAT(12)*EPSM
       S     = XMAT(13)/XMAT(9)*R
       EPSX  = 0.0D0
       IF(SIG.GT.XMAT(16)) EPSX=XMAT(14)+XMAT(15)*SIG
       T1 = R*TIME
       T2 = S*TIME
       F1 = 0.0D0
       F2 = 0.0D0
       IF(ABS(T1).LT.100.0) F1=EXP(-T1)
       IF(ABS(T2).LT.100.0) F2=EXP(-T2)
       DEPSCP= EPSTR*F1+EPSX*S*F2
       DEPSCS= EPSM
C
       ENDIF
       RETURN
       END




