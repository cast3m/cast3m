C VITAPP    SOURCE    CHAT      05/01/13    04:07:33     5004
      SUBROUTINE VITAPP(NDIM,ITY1,ITYG,NOEL1
     $     ,NOUN,DIAM,UELEM,XDEP,UREF,UREEL,IZSH,ITEST)

****************************************************************************
*** SP 'VITAPP' : calcule les vitesses aux pts calcul reel et reference
*** par approximation en utilisant les fctions base ds element de reference.
***
*** APPELES 1 = 'HYSHP', 'MATMAT'
*** APPELES 2 = 'JCBIEN'
***
*** E=  'NDIM' dimension de l'espace
***     'ITY1' entier caracterisant le type de l'element
***     'ITYG' entier caracterisant la geometrie de l'element
***     'NOEL1' nombre de noeuds de l'element considere
***     'NOUN' nombre de flux consideres
***     'DIAM' "longueur caracteristique" de l'element
***     'UELEM' valeurs des flux aux faces
***     'XDEP' coordonnees reference du pt de depart
***
*** S = 'UREF' vitesse dans element reference au pt calcul
***     'UREEL' vitesse dans element reel au pt calcul
***     'ITEST' vaut 0 quand le Jacobien est nul, sinon 1
***
*** E/S = 'IZSH' segment content pour l'elemt considere et au pt depart :
***             'XYZL' coordonnees reelles des noeuds (E)
***             'SHP' fonctions de forme et derivees ds elemt ref (S)
***             'SHY' fonctions de base et derivees ds elemt ref (S)
***
*** Rq : 'XZPREC' (-INC CCREEL) erreur precision calcul machine
***
***             Auteur Cyril Nou
****************************************************************************

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
-INC CCREEL
      SEGMENT IZSH
          REAL*8 SHP(6,MNO9),SHY(12,MNO9),XYZL(3,MNO9)
      ENDSEGMENT
      DIMENSION XDEP(3),UELEM(NOUN),XJAC2(3,3),UREF(3),UREEL(3)

************************************************
*** CALCUL VITESSE REFERENCE AU PT DE CALCUL ***
************************************************

***   initialisation de depart
      ITEST=1
      DO 10 I=1,3
         UREF(I)=0.D0
         UREEL(I)=0.D0
 10   CONTINUE
      ITYH=NUMHYB(ITYG)
***   calcul des fonctions de base au pt de calcul de reference
      CALL HYSHP(XDEP(1),XDEP(2),XDEP(3),ITYH,NOUN,12,SHY,IRET)
      DO 20 I=1,NDIM
         DO 30 K=1,NOUN
***         calcul de la vitesse au pt de calcul de reference
            UREF(I)=UREF(I)+SHY(I,K)*UELEM(K)
 30      CONTINUE
 20   CONTINUE

******************************************************
*** PASSAGE A VITESSE AU PT DE CALCUL REEL ASSOCIE ***
******************************************************

***   calcul matrice jacobienne et jacobien au pt calcul
      CALL JCBIEN(XDEP,ITY1,NDIM,NOEL1,XJAC2,DET2,IZSH)
***   calcul de la variable pour adimensionner 'DET2'
      ADIM=1.D0
      IF (NDIM.EQ.2) THEN
         ADIM=DIAM**2
      ELSEIF (NDIM.EQ.3) THEN
         ADIM=DIAM**3
      ENDIF
***   chgt de variable espace ref -> espace reel
      IF (ABS(DET2/ADIM).GE.XZPREC) THEN
         CALL MATMAT(XJAC2,UREF,3,3,1,UREEL)
         DO 40 I=1,NDIM
            UREEL(I)=UREEL(I)/ABS(DET2)
 40      CONTINUE
      ELSE
         ITEST=0
      ENDIF

      RETURN
      END




