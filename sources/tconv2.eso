C TCONV2    SOURCE    CB215821  16/02/29    21:15:01     8835

C=======================================================================
C=                            T C O N V 2                              =
C=                            -----------                              =
C=                                                                     =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=  Ce sous-programme remplit le segment DESCRipteur de la matrice de  =
C=  CONVECTION (type RIGIDITE) dans le cas d'un modele THERMIQUE       =
C=                                                                     =
C=  Parametres :  (E)=Entree   (S)=Sortie                              =
C=  ------------                                                       =
C=   ICOQ    (E)  = 0 si element MASSIF, sinon element COQUE           =
C=   PEAU    (E)  Definit la PEAU consideree pour la convection dans   =
C=                le cas des COQUEs ('INFERIEURE' ou 'SUPERIEURE')     =
C=   NBNN    (E)  Nombre de NOEUDS dans le type d'element considere    =
C=   IDESCR  (S)  Pointeur sur le segment DESCR                        =
C=                                                                     =
C=  Remarques :                                                        =
C=  -----------                                                        =
C=  - L'inconnue PRIMALE est la temperature 'T   ' pour les elements   =
C=    MASSIFs et 'TINF', 'TSUP' ou 'T   ' dans le cas d'element COQUE. =
C=  - L'inconnue DUALE est la chaleur 'Q   ' pour les elements MASSIFs =
C=    et 'QINF' ou 'QSUP' dans le cas d'elements COQUEs.               =
C=  - Ces noms de composantes pourraient figurer dans BDATA.           =
C=                                                                     =
C=  Denis ROBERT, le 6 janvier 1988.                                   =
C=  Mise a jour pour les coques, P. DOWLATYARI, juin 1990.             =
C=======================================================================

      SUBROUTINE TCONV2 (ICOQ,PEAU,NBNN,IDESCR)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC SMRIGID
-INC CCOPTIO

      CHARACTER*(*) PEAU
      CHARACTER*4 MOTHER,MOCHAL,MOTHR1,MOTHR2,MOCHL1,MOCHL2

      PARAMETER ( MOTHER='T   ' )
      PARAMETER ( MOCHAL='Q   ' )
      PARAMETER ( MOTHR1='TINF',MOTHR2='TSUP' )
      PARAMETER ( MOCHL1='QINF',MOCHL2='QSUP' )

C  1 - Cas des elements COQUES
C =============================
      IF (ICOQ.NE.0) THEN
        NLIGRP = NBNN
        NLIGRD = NBNN
        SEGINI,DESCR
        DO i = 1, NLIGRP
          IF    (PEAU.EQ.'INFERIEURE') THEN
            LISINC(i) = MOTHR1
            LISDUA(i) = MOCHL1
          ELSEIF(PEAU.EQ.'SUPERIEURE') THEN
            LISINC(i) = MOTHR2
            LISDUA(i) = MOCHL2
          ELSE
            MOTERR(1:8) ='MOT     '
            MOTERR(9:16)=PEAU(1:8)
            CALL ERREUR(11)
            RETURN
          ENDIF
          NOELEP(i) = i
          NOELED(i) = i
        ENDDO

C  2 - Cas des elements MASSIFS
C ==============================
      ELSE
        NLIGRP = NBNN
        NLIGRD = NBNN
        SEGINI,DESCR
        DO i = 1, NLIGRP
          LISINC(i) = MOTHER
          LISDUA(i) = MOCHAL
          NOELEP(i) = i
          NOELED(i) = i
        ENDDO
      ENDIF

      SEGDES,DESCR
      IDESCR = DESCR

      RETURN
      END



