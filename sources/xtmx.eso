C XTMX      SOURCE    BP208322  15/06/22    21:23:39     8543
      SUBROUTINE XTMX(IRE1,IRE2,VA)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 VA,COER,VB,XTMXMU
C
C  ****  MULTIPLICATION D'UNE MATRICE(IRE2) PAR UN CHAMPPOINT (IRE1) A
C  ****  DROITE ET A GAUCHE.LE RESULTAT EST UN FLOTTANT.
C  ****  VA=    IRE1 *IRE2 *IRE1
C  ****  POUR EFFECTUER L OPERATION ON ELIMINE LES COMPOSANTES LX
C  ****  DU CHPOINT ET DE LA MATRICE. ON TESTE QUE LES AUTRES INCONNUES
C  ****  DU CHPOINT SONT INCLUSES DANS CELLES DE L OBJET RIGIDITE.
C  ON SUPPOSE QUE :
C   1. YT ET X SONT DE MEME TYPE
C   2. LA MATRICE EST CARREE
C   3. LA MATRICE POSSEDE DES CORRESPONDANCES SUR LES INCONNUES
C      (C'EST A DIRE QUE LA IEME LIGNE EST LA DUALE DE LA IEME COLONNE)
C
C   BP , avril 2010 : on supprime l hypothese 3.
C  (pour permettre l utilisation de matrices crees par imped par ex.)
C
-INC CCOPTIO
-INC CCREEL
-INC SMELEME
-INC SMCHPOI
-INC SMRIGID
-INC SMCOORD
-INC CCHAMP
c
      CHARACTER*4 NI,INCOM
      SEGMENT,IHAR(0)
      SEGMENT,SIINC
       CHARACTER*4 IINC(0)
      ENDSEGMENT
      SEGMENT/ICCPR/(ICPR(NNGOT))
      SEGMENT ITRAV
       CHARACTER*4 IINCO(NNIN)
       REAL*8 CC(NLIGMA),VAA(NNIN,ITES),DD(NLIGMA)
       INTEGER IPOS(NLIGMA)
       INTEGER IPOS2(NLIGMA)
      ENDSEGMENT
      LOGICAL   NEEDDD

C ITES = NONBRE DE NOEUD DU CHPOINT
C NLIGMA = TAILLE MAX D'UNE LIGNE DE MATRICE DE RIGIDITE ELEMENTAIRE

C **** INITIALISATION DU RESULTAT
      VA=0.D0
      MRIGID=IRE2
      MCHPOI=IRE1
      NNGOT=XCOOR(/1)/(IDIM+1)
C
C  ****  ON CREE LES TABLEAUX :
C
C  ****  ICPR(I)=J VEUT DIRE QUE LE NOEUD I A LE NUMERO LOCAL J.
C
      SEGINI,ICCPR
      SEGACT,MCHPOI
      NSOUPO=IPCHP(/1)
      IK=0
      DO 1 ISOU=1,NSOUPO
      MSOUPO=IPCHP(ISOU)
      SEGACT,MSOUPO
      MELEME=IGEOC
      SEGACT,MELEME
      N1=NUM(/1)
      N2=NUM(/2)
      DO 2 I2=1,N2
      K=NUM(1,I2)
*     on ajoute le noeud K a ICPR(K) si pas deja vu
      IF(ICPR(K).NE.0)  GO TO 2
      IK=IK+1
      ICPR(K)=IK
   2  CONTINUE
C     SEGDES,MELEME
C     SEGDES,MSOUPO
   1  CONTINUE
C     SEGDES,MCHPOI

      ITES=IK
      NLIGMA=0

C  **** REMPLISSAGE DE IINC et IHAR
C       = couple(inconnue primale + harmonique) de la matrice MRIGID
      SEGINI,SIINC
      SEGINI,IHAR
*
      SEGACT,MRIGID
      NRIGE=IRIGEL(/1)
      NRIGEL=IRIGEL(/2)
      DESCR=IRIGEL(3,1)
      SEGACT,DESCR
*     Initialisation de la 1ere valeur
      IINC(**)=LISINC(1)
      IHAR(**)=IRIGEL(5,1)
      ININC=1
*     boucle sur les rigidites elementaires
      DO 3 IRI=1,NRIGEL
        MELEME=IRIGEL(1,IRI)
        SEGACT,MELEME
        DESCR=IRIGEL(3,IRI)
        NOHAR=IRIGEL(5,IRI)
        SEGACT,DESCR
        NLIGRE=LISINC(/2)
        IF(NLIGRE.GT.NLIGMA) NLIGMA=NLIGRE
        DO 7 I2=1,NLIGRE
        DO 8 I1=1,ININC
        IF(LISINC(I2).NE.IINC(I1)) GO TO 8
        IF(NOHAR.EQ.IHAR(I1)) GO TO 7
   8    CONTINUE
        IINC(**)=LISINC(I2)
        IHAR(**)=NOHAR
        ININC=ININC+1
   7    CONTINUE
C     SEGDES,DESCR
C     SEGDES,MELEME
   3  CONTINUE
C     SEGDES,MRIGID
C
C  ****  ON INITIALISE LE SEGMENT ITRAV
C
      NNIN=ININC
      SEGINI,ITRAV
      DO 10 I=1,NNIN
   10 IINCO(I)=IINC(I)
C
C  ****  ON INITIALISE VAA (= chpoint)
C     SEGACT,MCHPOI
      NSOUPO=IPCHP(/1)
c --- boucle sur les zones ---
      DO 15 ISOU=1,NSOUPO
        MSOUPO=IPCHP(ISOU)
C       SEGACT,MSOUPO
        MELEME=IGEOC
C       SEGACT,MELEME
        N2=NUM(/2)
        NC=NOCOMP(/2)
        MPOVAL=IPOVAL
        SEGACT,MPOVAL
c    -- boucle sur les composantes --
        DO 16 INC=1,NC
          INCOM=NOCOMP(INC)
          NOHA=NOHARM(INC)
          DO 17 IH=1,NNIN
            IF(INCOM.NE.IINCO(IH)) GO TO 17
            IF(IHAR(IH).EQ.NOHA)  GO TO 18
   17     CONTINUE
          GO TO 16
c         on a trouvé le bon couple inconnue primale+harmonique : IH
   18   CONTINUE
        DO 19 I2=1,N2
          IK=ICPR(NUM(1,I2))
          VAA(IH,IK)= VPOCHA(I2,INC)
   19   CONTINUE
   16   CONTINUE
        SEGDES,MSOUPO
        SEGDES,MPOVAL
        SEGDES,MELEME
   15 CONTINUE
      SEGDES,MCHPOI
C
C  ****  BOUCLE 20 SUR LES OBJETS RIGIDITES ELEMENTAIRES
C
C     SEGACT,MRIGID
      DO 20 IRI=1,NRIGEL
         IANTI=0
         IF (NRIGE.GE.7) THEN
            IANTI=IRIGEL(7,IRI)
         ENDIF
         MELEME=IRIGEL(1,IRI)
         NOHA=IRIGEL(5,IRI)
         SEGACT,MELEME
         DESCR=IRIGEL(3,IRI)
         SEGACT,DESCR
C
C    ** ON VERIFIE QUE:
C      -LA MATRICE EST CARREE
        LISI=LISINC(/2)
        LISD=LISDUA(/2)
        IF ( LISI.NE.LISD) THEN
          CALL ERREUR(21)
          RETURN
        ENDIF
C      -NOELED ET NOELEP SONT LES MEMES
        DO ITEFR=1,LISI
          IF( NOELED(ITEFR).NE.NOELEP(ITEFR) ) THEN
            CALL ERREUR(21)
            RETURN
          ENDIF
        ENDDO
C
C    ** ON REMPLIT IPOS(I)=J QUI DIT QUE LA IEME INCONNUE PRIMALE
C       DE LA MATRICE ELEMENTAIRE EST LA JEME DE IINC
         DO 21 IN=1,LISI
            NI=LISINC(IN)
            DO 22 IJ=1,ININC
               IF(NI.NE.IINC(IJ))  GO TO 22
               IF(NOHA.EQ.IHAR(IJ))  GO TO 23
 22         CONTINUE
 23         CONTINUE
            IPOS(IN)=IJ
 21      CONTINUE
C
C    ** ON ETABLIT LA CORRESPONDANCE INCONNUES PRIMALES ET DUALES
C       (important si hypothèse 3 non vérifiée)
C    ** ON REMPLIT IPOS2(I)=J QUI DIT QUE LA IEME INCONNUE DUALE
C       DE LA MATRICE ELEMENTAIRE EST "NATURELLEMENT" ASSOCIEE A LA
C       JEME INCONNUE PRIMALE DE IINC
C    ** NEEDD nous dit si on a besoin de DD et de IPOS2
        NEEDDD=.false.
        if(IIMPI.ge.5) write(6,*) 'Pour la rigidite elementaire ',IRI
        DO IN=1,LISI
           NI=LISDUA(IN)
           if(IIMPI.ge.5)
     &     write(6,*) 'l inconnue primale ',LISINC(IN),
     &                ' produit le dual ',NI
           do idu=1,LNOMDU
              if(NOMDU(idu).eq.NI) goto 25
           enddo
           write(6,*) 'ERREUR : NOM D INCONNUE DUALE INCONNUE',NI
           CALL ERREUR(21)
           return
 25        continue
c          on a trouve le numero du dual -> on en deduit le primal
C          "naturellement" associé pour le produit scalaire
c          il faut le chercher dans le chpoint VBB cad dans IINC
           NI=NOMDD(idu)
           DO 26 IJ=1,ININC
              IF(NI.NE.IINC(IJ))  GO TO 26
              IF(NOHA.EQ.IHAR(IJ))  GO TO 27
 26        CONTINUE
           write(6,*) 'ERREUR : NOM D INCONNUE PRIMALE INCONNUE',NI
           CALL ERREUR(21)
           return
 27        CONTINUE
           IPOS2(IN)=IJ
           if(IJ.ne.IPOS(IN)) NEEDDD=.true.
        ENDDO

C
C  ****  BOUCLE 30 SUR LES PETITES MATRICES D'UNE RIGIDITE ELEMENTAIRE
C
         N1=NUM(/1)
         N2=NUM(/2)
         xMATRI=IRIGEL(4,IRI)
         COER=COERIG(IRI)
         SEGACT,xMATRI
         DO 30 I2=1,N2
C
C       **  AVANT D'EFFECTUER LE PRODUIT ON VERIFIE QU'IL EST A FAIRE
            DO 31 I1=1,N1
               IF(ICPR(NUM(I1,I2)).NE.0) GO TO 32
 31         CONTINUE
            GO TO 30
 32         CONTINUE
C
C       **  FABRICATION D'UN (ou de 2) VECTEUR(s) ISSU(s) DU CHPOINT
            CALL XTMXMP(LISI,CC,ICPR,NUM,I2,NOELEP,IPOS,VAA,NNIN,N1)
            if(NEEDDD)
     &      CALL XTMXMP(LISI,DD,ICPR,NUM,I2,NOELEP,IPOS2,VAA,NNIN,N1)
C     DO 34 IN=1,LISI
C 34  CC(IN)=0.D0
C     DO 33 IN=1,LISI
C     J2=ICPR(NUM(NOELEP(IN),I2))
C     IF(J2.EQ.0) GO TO 33
C     J1=IPOS(IN)
C     CC(IN)=VAA(J1,J2)
C  33 CONTINUE
C
C  **** BOUCLE 35 SUR LES LIGNES D'UNE MATRICE ELEMENTAIRE
*            XMATRI=IMATTT(I2)
*            SEGACT,XMATRI
            if (NEEDDD) then
              DO 35 IN=1,LISI
*               IF (ABS(DD(IN)).GT.1.D-10) THEN
                IF (ABS(DD(IN)).GT.XPETIT) THEN
                     VB=0.D0
C                 ** BOUCLE 38 SUR LES COLONNES D'UNE MATRICE ELEMENTAIRE
                     DO 38 JN=1,LISI
                           VB=VB+CC(JN)*RE(IN,JN,i2)
 38                  CONTINUE
                     VA=VA+ DD(IN)*VB*COER
                ENDIF
 35           CONTINUE
            else
              VA = VA + XTMXMU(CC,RE(1,1,i2),LISI,IANTI)*COER
            endif
*           SEGDES,XMATRI
 30      CONTINUE
         SEGDES,xMATRI
         SEGDES,DESCR
         SEGDES MELEME
 20   CONTINUE
      SEGDES,MRIGID
      SEGSUP,ITRAV
      SEGSUP,SIINC
      SEGSUP,IHAR
      SEGSUP,ICCPR
*
      END













