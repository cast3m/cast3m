C BARPAR    SOURCE    CHAT      05/01/12    21:34:56     5004
C BARPAR    SOURCE    DC        98/01/26
      SUBROUTINE BARPAR(XMAT,XCAR,DEPST,SIG0,VAR0,SIGF,VARF,DEFP)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(6),VAR0(2),VARF(2)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE
C      MODELE BETON PLASTIQUE DU BAEL
C     ELEMENT DE BARRE
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - Janvier 1998
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C      XMAT( 3) RHO
C      XMAT( 4) ALPHA
C      XMAT( 5) FY
C      XMAT( 6) HH
C
C      SIGF STRESS
C      DEPST,DEFP
C
C                      VAR0(1) VARF(1) EPSE DEFORMATION PLASTIQUE
C
C----------------------------------------------------------------------
       DEPS = DEPST
C
       XSECT = XCAR
C
       XSIG0 = SIG0/XSECT
C
       GAMMA=XMAT(1)/2/(XMAT(2)+1)
       EYOUN = XMAT( 1)
       FYP = XMAT(5)
       FYN = (-1.)*XMAT(5)
       HHH = XMAT(6)
       FYPE = FYP + ( HHH * VAR0(1))
       FYNE = FYN + ( HHH * VAR0(1))
C
       XEPSE =  VAR0(1)
C
       XSIGT = XSIG0 + EYOUN * DEPST
C
       IF (XSIGT . GT . FYPE) THEN
C
         XDEP = ((EYOUN/(EYOUN+HHH))*DEPST)
     &         + ((XSIG0 - FYPE)/(EYOUN+HHH))
         XSIGF =  XSIG0 + (EYOUN*(DEPST - XDEP))
         XTAN = (EYOUN*HHH)/(EYOUN+HHH)
C
       ELSEIF (XSIGT . LT . FYNE) THEN
C
         XDEP = ((EYOUN/(EYOUN+HHH))*DEPST)
     &         + ((XSIG0 - FYNE)/(EYOUN+HHH))
         XSIGF =  XSIG0 + (EYOUN*(DEPST - XDEP))
         XTAN = (EYOUN*HHH)/(EYOUN+HHH)
C
       ELSE
C
         XSIGF = XSIGT
         XDEP = 0.D0
         XTAN = EYOUN
C
       ENDIF
C
C
C
       SIGF = XSIGF * XSECT
C
       VARF(1) = XEPSE + XDEP
       VARF(2) = XTAN
C
       DEFP    = XEPSE
C
       RETURN
       END





