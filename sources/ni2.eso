C NI2       SOURCE    GOUNAND   06/05/09    21:15:17     5435
      SUBROUTINE NI2(NDIM,NBNOD,QSI,ETA,DZE,
     $     NBMMON,NBMON,COEMON,EXPMON,PNM,XCO1,ORDDER,VECTP
     $     ,SHP)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C***********************************************************************
C NOM         : NI2
C DESCRIPTION : Calcul fonctions de forme et dérivées premières
C               Particularisation de ni.eso
C
C
C LANGAGE     : ESOPE
C AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
C               mél : gounand@semt2.smts.cea.fr
C***********************************************************************
C APPELES          :
C APPELE PAR       : SHAPEQ
C***********************************************************************
C ENTREES            : NDIM,NBNOD,QSI,ETA,DZE,EXPMON,PNM
C TRAVAIL            : XCO1,ORDDER,VECTP
C ENTREES/SORTIES    :
C SORTIES            : SHP
C***********************************************************************
C VERSION    : v1, 04/05/2006, version initiale
C HISTORIQUE : v1, 04/05/2006, création
C HISTORIQUE :
C HISTORIQUE :
C***********************************************************************
      INTEGER NBMON(NBNOD)
      REAL*8 COEMON(NBMMON,NBNOD)
      INTEGER EXPMON(NDIM,NBMMON,NBNOD)
      REAL*8 PNM(NBNOD,NBNOD)
      REAL*8 XCO1(NDIM)
      INTEGER ORDDER(NDIM)
      REAL*8 VECTP(NBNOD)
      REAL*8 SHP(6,NBNOD)
*
* Executable statements
*
* Calcul des fonctions de forme et des dérivées premières
* au point QSI,ETA,DZE
* 1) fonctions de forme
* Calcul de < P (p) > = < P1(p) ... Pnbfn(p) > où p est le point
* (qsi, eta, dze)
      XCO1(1)=QSI
      XCO1(2)=ETA
      XCO1(3)=DZE
      DO INOD=1,NBNOD
         ORDDER(1)=0
         ORDDER(2)=0
         ORDDER(3)=0
         CALL VALPO2(NDIM,XCO1,NBMON(INOD),COEMON(1,INOD),
     $        EXPMON(1,1,INOD),ORDDER,
     $        VECTP(INOD))
      ENDDO
* On calcule : < N (p) > = < P (p) > [Pn]^{-1}
      DO INOD=1,NBNOD
         SHP(1,INOD)=DDOT(NBNOD,VECTP,1,PNM(1,INOD),1)
      ENDDO
* 2) dérivées des fonctions de forme
      DO IDIM=1,NDIM
         DO JDIM=1,NDIM
            IF (JDIM.EQ.IDIM) THEN
               ORDDER(JDIM)=1
            ELSE
               ORDDER(JDIM)=0
            ENDIF
         ENDDO
         DO INOD=1,NBNOD
            CALL VALPO2(NDIM,XCO1,NBMON(INOD),COEMON(1,INOD),
     $           EXPMON(1,1,INOD),ORDDER,
     $           VECTP(INOD))
         ENDDO
         DO INOD=1,NBNOD
            SHP(1+IDIM,INOD)=DDOT(NBNOD,VECTP,1,PNM(1,INOD),1)
         ENDDO
      ENDDO
C      WRITE(6,*) 'On a créé SHP(1,*) :'
C      WRITE(6,*) (SHP(1,J),J=1,NBNOD)
C      SOM=0.D0
C      DO J=1,NBNOD
C         SOM=SOM+SHP(1,J)
C      ENDDO
C      WRITE(6,*) 'SOMME : ',SOM
C      WRITE(6,*) 'On a créé SHP(2,*) :'
C      WRITE(6,*) (SHP(2,J),J=1,NBNOD)
C      SOM=0.D0
C      DO J=1,NBNOD
C         SOM=SOM+SHP(2,J)
C      ENDDO
C      WRITE(6,*) 'SOMME : ',SOM
*
* Normal termination
*
      RETURN
*
* End of subroutine NI2
*
      END


