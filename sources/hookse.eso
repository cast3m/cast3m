C HOOKSE    SOURCE    BP208322  15/06/22    21:19:17     8543
      SUBROUTINE HOOKSE(VALMAT,IB,IGAU,MFR,CRIGI,IFOU,
     +            NBGMAT,NELMAT,SECT,LHOOK,DDHOOK,IRET)
C
C----------------------------------------------------------------------
C
C   Calcul de la matrice de HOOKE dans le cas d'un
C   matériau homogeneise
C
C   Entrees:
C   --------
C   VALMAT   tableau de materiau
C   IB       numero de l'element
C   IGAU     numero du point de Gauss
C   MFR      numero de formulation
C   CRIGI    ELEMENT DE REDUCTION DE LA RIGIDITE
C   IFOU     numero d'harmonique de Fourier
C   NBGMAT, NELMAT tailles des tableaux
C   SECT     SECTION DE L'ELEMENT IB (<> 0 SI MFR.EQ.27)
C   LHOOK    taille de la matrice de HOOKE
C
C   Sorties:
C   --------
C   DDHOOK   matrice de HOOKE
C   IRET     = 1 si option existante, 0 sinon
C
C---------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
-INC CCHAMP
-INC CCOPTIO
C
      DIMENSION VALMAT(*)
      DIMENSION DDHOOK(LHOOK,*)
      DIMENSION CRIGI(12)
C
C     INITIALISATION
C
        CALL ZERO(DDHOOK,LHOOK,LHOOK)
C
        IF (IGAU.LE.NBGMAT.AND.(IB.LE.NELMAT.OR.NBGMAT.GT.1)) THEN
C
C  Cas des poutres et des tuyaux
C
         IF (MFR.EQ.7.OR.MFR.EQ.13) THEN
C
            IF(IFOU.EQ.2) THEN
C
C  LA MATRICE EST PLEINE
C
            DDHOOK(1,1)=CRIGI(1)
            DDHOOK(1,5)=CRIGI(3)
            DDHOOK(5,1)=CRIGI(3)
            DDHOOK(1,6)=-CRIGI(2)
            DDHOOK(6,1)=-CRIGI(2)
C
            DDHOOK(5,5)=CRIGI(6)
            DDHOOK(5,6)=-CRIGI(5)
            DDHOOK(6,5)=-CRIGI(5)
C
            DDHOOK(6,6)=CRIGI(4)
C
            DDHOOK(2,2)=CRIGI(8)
            DDHOOK(2,4)=-CRIGI(10)
            DDHOOK(4,2)=-CRIGI(10)
C
            DDHOOK(3,3)=CRIGI(7)
            DDHOOK(3,4)=CRIGI(9)
            DDHOOK(4,3)=CRIGI(9)
C
            DDHOOK(4,4)=CRIGI(11)+CRIGI(12)
C
           ELSEIF(IFOU.EQ.-2.OR.IFOU.EQ.-1.OR.IFOU.EQ.-3) THEN
C
C  LA MATRICE EST PLEINE
C
            DDHOOK(1,1)=CRIGI(1)
            DDHOOK(1,3)=-CRIGI(2)
            DDHOOK(3,1)=-CRIGI(2)
C
            DDHOOK(3,3)=CRIGI(3)
C
            DDHOOK(2,2)=CRIGI(4)
C
           ELSE
            IRET=0
           ENDIF
C
C  Cas des barres
C
         ELSEIF (MFR.EQ.27) THEN
C
          YOU=VALMAT(1)
          DDHOOK(1,1)=YOU*SECT
C
         ELSE
C
C  Option non definie
C
          IRET=0
         ENDIF
        ENDIF
C
      RETURN
      END









