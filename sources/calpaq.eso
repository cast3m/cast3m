C CALPAQ    SOURCE    PASCAL    17/08/02    21:15:01     9500

      SUBROUTINE CALPAQ(IPCHE1,IPCHE2,K,TITC,NUMCHA,IRET)

*______________________________________________________________________
*
*     GESTION DE LA MULTIPLICATION DES CHAMELEMS
*     __________________________________________
*
*   ENTREES :
*   ---------
*
*      IPCHE1     POINTEUR SUR UN 1IER MCHAML
*      IPCHE2     POINTEUR SUR UN 2IEM MCHAML
*
*      Ces segments sont suposes ACTIFs en ENTREE et en SORTIE
*      Ils ne sont en rien modifies dans le present sous-programme.
*
*   SORTIES :
*   ---------
*      IPCHE1     <-|
*                   |-- POINTEURS EVENTUELLEMENT PERMUTES
*      IPCHE2     <-|
*
*      K          TYPE DE MULTIPLICATION 1 SCALAIRE PAR SCALAIRE
*                                        2 COMPOSANTE PAR SCALAIRE
*                                        3 COMPOSANTE PAR COMPOSANTE
*                                        4 MATRICE PAR COMPOSANTE
*                                        5 GRADIEN PAR GRADIEN
*
*      TITC       contient le TITCHE DU MCHAML A CREER
*                 Par defaut TITC = ' '
*      NUMCHA     LONGUEUR DU TITCHE DU CHAMP (=TITC(1:NUMCHA))
*                 Par defaut NUMCHA = 1
*
*      IRET       = 1 SI OK
*                 = 0 SINON AVEC GESTION ERREUR
*
*       EBERSOLT JANVIER 87
*
*       PASSAGE AUX NOUVEAU CHAMELEM PAR JM CAMPENON LE 30 10 90
*_______________________________________________________________________
*
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)

-INC CCOPTIO
-INC SMCHAML

      PARAMETER (NTIT=22)

      CHARACTER*72 TIT1,TIT2,TITC
      CHARACTER*72 TITNOU(NTIT)
*
      DATA TITNOU / 'NOEUD', 'GRAVITE', 'RIGIDITE', 'MASSE',
     1              'STRESSES', 'DEPLACEMENTS',   'FORCES',
     1              'REACTUALISATION',      'FORCES VOLUMIQUES',
     1              'GRADIENT', 'CONTRAINTES', 'DEFORMATIONS',
     1              'CARACTERISTIQUES', 'BIDON',
     1              'TEMPERATURES', 'CONTRAINTES PRINCIPALES',
     1              'MATRICE DE HOOKE', 'MATRICE DE HOOKE TANGENTE',
     1              'DILATATIONS', 'VARIABLES INTERNES',
     1              'GRADIENT DE FLEXION','VON MISES'/
*
      MCHEL1 = IPCHE1
      MCHEL2 = IPCHE2
*      segact,MCHEL1,MCHEL2    <- supposes ACTIFs en E/S

* Analyse du champ 1 (IPCHE1)
      TIT1 = MCHEL1.TITCHE
      INU1 =  0
      INUK1 = 0
C*      write(ioimp,*) 'TIT1' , tit1
      IF (TIT1.EQ.'SCALAIRE') THEN
        INU1 = 1
        IF (MCHEL1.INFCHE(/2).GE.6) THEN
          INU1 = MCHEL1.INFCHE(1,6)
          IF (INU1.EQ.0) INU1=1
        ENDIF
        GOTO 1
      ENDIF
*
      CALL PLACE(TITNOU,NTIT,INU1,TIT1)
      IF (INU1.EQ.0) THEN
* y a t il une unique composante scalaire?
        nkcha = MCHEL1.ICHAML(/1)
        DO lkcha = 1, nkcha
          MCHAM1 = MCHEL1.ICHAML(lkcha)
          SEGACT,MCHAM1
          IF (MCHAM1.NOMCHE(/2).NE.1) THEN
            INUK1 = 2
          ELSE
            IF (MCHAM1.NOMCHE(1).NE.'SCAL') INUK1 = 2
          ENDIF
          SEGDES,MCHAM1
        ENDDO
      ENDIF
 1    CONTINUE
*
* Analyse du champ 2 (IPCHE2)
      TIT2 = MCHEL2.TITCHE
      INU2  = 0
      INUK2 = 0
C*      write(ioimp,*) 'TIT2' , tit2
      IF (TIT2.EQ.'SCALAIRE') THEN
        INU2 = 1
        IF (MCHEL2.INFCHE(/2).GE.6) THEN
          INU2 = MCHEL2.INFCHE(1,6)
          IF (INU2.EQ.0) INU2 = 1
        ENDIF
        GOTO 2
      ENDIF
*
      CALL PLACE(TITNOU,NTIT,INU2,TIT2)
      IF (INU2.EQ.0) THEN
        nkcha = MCHEL2.ICHAML(/1)
* y a t il une unique composante scalaire?
        DO lkcha = 1, nkcha
          MCHAM2 = MCHEL2.ICHAML(lkcha)
          SEGACT,MCHAM2
          IF (MCHAM2.NOMCHE(/2).NE.1) THEN
            INUK2 = 2
          ELSE
            IF (MCHAM2.NOMCHE(1).NE.'SCAL') INUK2 = 2
          ENDIF
          SEGDES,MCHAM2
        ENDDO
      ENDIF
 2    CONTINUE

*     write(ioimp,*) ' inu1,inuk1, inu2,inuk2',inu1,inuk1,inu2,inuk2
*
* S. PASCAL
* Traitement particulier dans le cas d'un produit de 2 MCHAMLs
* a plusieurs composantes dont certaines sont de type EVOLUTIOn.
* Je cherche dans le MCHEL1 les composantes de type EVOL.
*    Si aucune EVOL, INUJ1=0
*    Si toute EVOL,  INUJ1>0
*    Sinon,          INUJ1<0
* Idem pour MCHEL2 avec INUJ2
* Je me place dans le cas d'un produit composante par composante :
      INUJ1 = 0
      INUJ2 = 0
      IF (INU1.EQ.INU2 .AND. INU1.GT.5) THEN
        nkcha = MCHEL1.ICHAML(/1)
        NJ1 = 0
        DO lkcha =1, nkcha
          MCHAM1 = MCHEL1.ICHAML(lkcha)
          SEGACT,MCHAM1
          NJ = MCHAM1.IELVAL(/1)
          DO JELV = 1, NJ
            IF (MCHAM1.TYPCHE(JELV).EQ.'POINTEUREVOLUTIO') THEN
              INUJ1 = INUJ1 + 1
            ENDIF
          ENDDO
          NJ1 = NJ1 + NJ
        ENDDO
*       IF (INUJ1.GT.0) THEN
          IF (INUJ1.NE.NJ1) INUJ1 = 0 - INUJ1
*       ENDIF
        nkcha = MCHEL2.ICHAML(/1)
        NJ2 = 0
        DO lkcha =1, nkcha
          MCHAM2 = MCHEL2.ICHAML(lkcha)
          SEGACT,MCHAM2
          NJ = MCHAM2.IELVAL(/1)
          DO JELV = 1, NJ
            IF (MCHAM2.TYPCHE(JELV).EQ.'POINTEUREVOLUTIO') THEN
              INUJ2 = INUJ2 + 1
            ENDIF
          ENDDO
          NJ2 = NJ2 + NJ
        ENDDO
*       IF (INUJ2.GT.0) THEN
          IF (INUJ2.NE.NJ2) INUJ2 = 0 - INUJ2
*       ENDIF
      ENDIF
c*      write(ioimp,*) ' inuj1,inuj2 ',inuj1,inuj2
*
      IRET = 1
*
      IF (INU1.EQ.0.AND.INUK1.EQ.0) THEN
         NUMCHA=1
         TITC = ' '
*
* AM  20/03/17
*
         IF(INU2.LE.1.AND.INUK2.EQ.0) THEN
           K=1
         ELSE
           K=2
           iii    = IPCHE2
           IPCHE2 = IPCHE1
           IPCHE1 = iii
         ENDIF
      ELSE IF(INU2.EQ.0.AND.INUK2.EQ.0) THEN
         NUMCHA=1
         TITC=' '
         K=2
      ELSE IF(INU1.EQ.0.OR.INU2.EQ.0) THEN
         NUMCHA=1
         TITC=' '
         K=3
      ELSE IF(INU1.EQ.INU2.AND.INU1.LE.5.AND.INU1.GE.1) THEN
         NUMCHA=1
         TITC=' '
         K=1
      ELSE IF(INU1.EQ.10.AND.INU2.EQ.10) THEN
         NUMCHA=8
         TITC='GRADIENT'
         K=5
      ELSE IF(INU1.EQ.10.AND.INU2.EQ.21) THEN
         NUMCHA=8
         TITC='GRADIENT'
         K=5
      ELSE IF(INU1.EQ.21.AND.INU2.EQ.10) THEN
         NUMCHA = 19
         TITC = 'GRADIENT DE FLEXION'
         K=5
      ELSE IF(INU1.EQ.21.AND.INU2.EQ.21) THEN
         NUMCHA=19
         TITC='GRADIENT DE FLEXION'
         K=5
      ELSE IF(INU1.EQ.INU2.AND.INU1.GT.5) THEN
         NUMCHA=1
         TITC=' '
         K=3
         IF ( (INUJ1.EQ.0.AND.INUJ2.NE.0) .OR.
     &        (INUJ1.LT.0.AND.INUJ2.GT.0) ) THEN
           iii    = IPCHE1
           IPCHE1 = IPCHE2
           IPCHE2 = iii
         ENDIF
      ELSE IF (INU1.EQ.1.AND.(INU2.EQ.6.OR.INU2.EQ.7)) THEN
         NUMCHA=1
         TITC=' '
         K=2
         iii    = IPCHE1
         IPCHE1 = IPCHE2
         IPCHE2 = iii
      ELSE IF (INU2.EQ.1.AND.(INU1.EQ.6.OR.INU1.EQ.7)) THEN
         NUMCHA=1
         TITC=' '
         K=2
      ELSE IF (INU1.EQ.3.AND.
     &        (INU2.EQ.13.OR.INU2.EQ.14.OR.INU2.EQ.17)) THEN
         NUMCHA=1
         TITC=' '
         K=2
         iii    = IPCHE1
         IPCHE1 = IPCHE2
         IPCHE2 = iii
      ELSE IF (INU2.EQ.3.AND.
     &        (INU1.EQ.13.OR.INU1.EQ.14.OR.INU1.EQ.17)) THEN
         NUMCHA=1
         TITC=' '
         K=2
      ELSE IF (INU1.EQ.5.AND.(INU2.EQ.11.OR.INU2.EQ.12.OR.
     &         INU2.EQ.15.OR.INU2.EQ.16.OR.INU2.EQ.18)) THEN
         NUMCHA=1
         TITC=' '
         K=2
         iii    = IPCHE1
         IPCHE1 = IPCHE2
         IPCHE2 = iii
      ELSE IF (INU2.EQ.5.AND.(INU1.EQ.11.OR.INU1.EQ.12.OR.
     &         INU1.EQ.15.OR.INU1.EQ.16.OR.INU1.EQ.18)) THEN
         NUMCHA=1
         TITC=' '
         K=2
      ELSE IF (INU1.EQ.11.AND.(INU2.EQ.17.OR.INU2.EQ.18)) THEN
         NUMCHA=12
         TITC='DEFORMATIONS'
         K=4
      ELSE IF (INU1.EQ.12.AND.(INU2.EQ.17.OR.INU2.EQ.18)) THEN
         NUMCHA=11
         TITC='CONTRAINTES'
         K=4
      ELSE IF (INU2.EQ.11.AND.(INU1.EQ.17.OR.INU1.EQ.18)) THEN
         NUMCHA=12
         TITC='DEFORMATIONS'
         K=4
         iii    = IPCHE1
         IPCHE1 = IPCHE2
         IPCHE2 = iii
      ELSE IF (INU2.EQ.12.AND.(INU1.EQ.17.OR.INU1.EQ.18)) THEN
         NUMCHA=11
         TITC='CONTRAINTES'
         K=4
         iii    = IPCHE1
         IPCHE1 = IPCHE2
         IPCHE2 = iii
      ELSE
         IRET = 0
         NUMCHA=1
         TITC=' '
         K = 0
      ENDIF
*
*     ERREUR LES CHAMELEM QUE L ON TENTE DE MULTIPLIER
*              OU DIVISER SONT INCOMPATIBLES
*
      IF (IRET.EQ.0) THEN
        MOTERR( 1:16) = TIT1(1:16)
        MOTERR(17:32) = TIT2(1:16)
        CALL ERREUR(993)
      ENDIF
*
*      SEGDES MCHEL1,MCHEL2 <- on les laisse ACTIFs comme en entree
*
      RETURN
      END




