C CENTJ0    SOURCE    CHAT      05/01/12    21:51:59     5004
      SUBROUTINE CENTJ0(RHO,UX,UY,P,GAM,CNX,CNY,CTX,CTY,
     &                  DFROC,DFRUNC,DFRUTC,DFRETC)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  CENTJ0
C
C DESCRIPTION       :  Calcul du Jacobien du flux numerique centré
C                      Contribution de l'onde de gauche à droite
C
C LANGAGE           :  FORTRAN 77
C
C AUTEUR            :  A. BECCANTINI DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C APPELES           :  CENTJ1
C                      Appellée par KONJR1, KONJC1
C
C************************************************************************
C
C**** Entrées:
C
C     RHO      =  densité
C
C     UX, UY   =  vitesse
C
C     P        =  pression
C
C     GAM      =  le "gamma"
C
C     CNX, CNY =  normale à la face
C
C     CTX, CTY =  tangente à la face
C
C
C**** Sorties:
C
C     DFROC(4) = derivés partielles de F_RHO+ par raport a RHO, RHOUX,
C                RHOUY, RHOET
C
C     DFRUNC(4)= derivés partielles de F_RHOUN+ par raport a RHO, RHOUX,
C                RHOUY, RHOET
C
C     DFRUNT(4)= derivés partielles de F_RHOUT+ par raport a RHO, RHOUX,
C                RHOUY, RHOET
C
C     DFRETC(4)= derivés partielles de F_RHOET+ par raport a RHO,
C                RHOUX, RHOUY, RHOET
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :
C
C************************************************************************
C
C N.B.: Toutes les variables sont declarées explicitement
C
      IMPLICIT INTEGER(I-N)
      REAL*8 RHO,UX,UY,P,GAM,CNX,CNY,CTX,CTY
     &      ,DFROC(4),DFRUNC(4),DFRUTC(4),DFRETC(4)
     &      ,DFROP(4),DFRUNP(4),DFRUTP(4),DFRETP(4)
     &      ,USRO,UXSRO,UYSRO,GM1,GM1EC,GM1UX,GM1UY
C
C**** Jacobian with respect to the primitive variables
C
      CALL CENTJ1(RHO,UX,UY,P,GAM,CNX,CNY,CTX,CTY,
     &                  DFROP,DFRUNP,DFRUTP,DFRETP)
C
C**** Jacobian with respect to the conservative variables
C
      USRO = 1.0D0 / RHO
      UXSRO = UX * USRO
      UYSRO = UY * USRO
      GM1 = GAM - 1.0D0
      GM1EC = GM1 * 0.5D0 * (UX * UX + UY * UY)
      GM1UX = GM1 * UX
      GM1UY = GM1 * UY
C
      DFROC(1) = DFROP(1) - UXSRO * DFROP(2) -  UYSRO * DFROP(3)
     &           + GM1EC * DFROP(4)
      DFROC(2) =  USRO * DFROP(2) -  GM1UX * DFROP(4)
      DFROC(3) =  USRO * DFROP(3) -  GM1UY * DFROP(4)
      DFROC(4) =  GM1 * DFROP(4)
C
      DFRUNC(1) = DFRUNP(1) - UXSRO * DFRUNP(2) -  UYSRO * DFRUNP(3)
     &           + GM1EC * DFRUNP(4)
      DFRUNC(2) =  USRO * DFRUNP(2) -  GM1UX * DFRUNP(4)
      DFRUNC(3) =  USRO * DFRUNP(3) -  GM1UY * DFRUNP(4)
      DFRUNC(4) =  GM1 * DFRUNP(4)
C
      DFRUTC(1) = DFRUTP(1) - UXSRO * DFRUTP(2) -  UYSRO * DFRUTP(3)
     &           + GM1EC * DFRUTP(4)
      DFRUTC(2) =  USRO * DFRUTP(2) -  GM1UX * DFRUTP(4)
      DFRUTC(3) =  USRO * DFRUTP(3) -  GM1UY * DFRUTP(4)
      DFRUTC(4) =  GM1 * DFRUTP(4)
C
      DFRETC(1) = DFRETP(1) - UXSRO * DFRETP(2) -  UYSRO * DFRETP(3)
     &           + GM1EC * DFRETP(4)
      DFRETC(2) =  USRO * DFRETP(2) -  GM1UX * DFRETP(4)
      DFRETC(3) =  USRO * DFRETP(3) -  GM1UY * DFRETP(4)
      DFRETC(4) =  GM1 * DFRETP(4)
C
      RETURN
      END




