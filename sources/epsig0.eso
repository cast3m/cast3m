C EPSIG0    SOURCE    KICH      18/10/04    21:15:21     9946
      SUBROUTINE EPSIG0(S,EPSI,MFR,IFOUR,YUNG,XNU,CARAC,NSTRS)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION S(*),EPSI(*),CARAC(*)

      DO I=1,NSTRS
        EPSI(I)=0.D0
      ENDDO

      AUX1=1.D0/YUNG
      AUX2=2.D0*(1.D0+XNU)
      AUX3=AUX1*AUX2
      IF(MFR.EQ.3.OR.MFR.EQ.5.OR.MFR.EQ.9) THEN
        EP1=CARAC(1)
        IF(MFR.NE.5) ALFAH=CARAC(2)**2
        AUX4=AUX1/EP1
        AUX5=12.D0*AUX4/(EP1*EP1)
      ENDIF


C---------ELEMENTS MASSIFS MFR=1  / 31----------------------------------

      IF(MFR.EQ.1.or.mfr.eq.31) THEN

        EPSI(1)=(S(1)-XNU*(S(2)+S(3)))*AUX1
        EPSI(2)=(S(2)-XNU*(S(1)+S(3)))*AUX1
        EPSI(3)=(S(3)-XNU*(S(1)+S(2)))*AUX1
        DO I=4,NSTRS
          EPSI(I)=AUX3*S(I)
        ENDDO

C---------ELEMENTS COQUES SANS ET AVEC C.T. MFR=3 ET MFR=9--------------

      ELSE IF(MFR.EQ.3.OR.MFR.EQ.9) THEN

        IF(IFOUR.GE.1) THEN
          EPSI(1)=(S(1)-XNU*S(2))*AUX4
          EPSI(2)=(S(2)-XNU*S(1))*AUX4
          EPSI(3)=AUX2*AUX4*S(3)
          EPSI(4)=(S(4)-XNU*S(5))*AUX5
          EPSI(5)=(S(5)-XNU*S(4))*AUX5
          EPSI(6)=AUX2*AUX5*S(6)
        ELSE
          EPSI(1)=(S(1)-XNU*S(2))*AUX4
          EPSI(2)=(S(2)-XNU*S(1))*AUX4
          EPSI(3)=(S(3)-XNU*S(4))*AUX5
          EPSI(4)=(S(4)-XNU*S(3))*AUX5
        ENDIF

C---------ELEMENTS COQUES EPAISSES MFR=5--------------------------------

      ELSE IF(MFR.EQ.5) THEN

        EPSI(1)=(S(1)-XNU*S(2))*AUX1
        EPSI(2)=(S(2)-XNU*S(1))*AUX1
        EPSI(3)=AUX3*S(3)
        EPSI(4)=AUX3*S(4)
        EPSI(5)=AUX3*S(5)

C---------ELEMENTS POUTRES MFR=7----------------------------------------

      ELSE IF(MFR.EQ.7) THEN

        IF(CARAC(4).NE.0.D0) EPSI(1)=AUX1*S(1)/CARAC(4)
        IF(CARAC(5).NE.0.D0) EPSI(2)=AUX3*S(2)/CARAC(5)
        IF(CARAC(6).NE.0.D0) EPSI(3)=AUX3*S(3)/CARAC(6)
        IF(CARAC(1).NE.0.D0) EPSI(4)=AUX3*S(4)/CARAC(1)
        IF(CARAC(2).NE.0.D0) EPSI(5)=AUX1*S(5)/CARAC(2)
        IF(CARAC(3).NE.0.D0) EPSI(6)=AUX1*S(6)/CARAC(3)

      ENDIF
      END




