C POUMA2    SOURCE    CHAT      05/01/13    02:19:17     5004
C POUMA2    SOURCE    MILL      89/07/06    21:03:24
      SUBROUTINE POUMA2(REL,LRE,CARAC,COORD,WORK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DE LA MATRICE DE MASSE ELEMENTAIRE DE L ELEMENT DE
C  POUTRE DROITE DANS LES AXES GLOBAUX
C
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          CARAC   TABLEAU DE CARACTERISTIQUES GEOMETRIQUES ET
C                  MATERIELLES DE LA POUTRE  ( 10 VALEURS )
C          COORD   COORDONNEES DES 2 NOEUDS
C
C  SORTIE
C          REL     MATRICE DE MASSE ELEMENTAIRE
C          KERRE   INDICE NON NUL SI PROBLEME , NUL SINON
C  TRAVAIL
C          WORK(300)
C    VIENT DE TEDEL AOUT 84
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(LRE,*),CARAC(*),COORD(3,*),P(2,2),
     .          XE(2),YE(2),WORK(*)
C
      KERRE=0
      XINZ=CARAC(2)
C
C  CORRECTION STCAN LE 6/7/89 : ON PREND L'INERTIE POLAIRE EN TORSION
C
      SD=CARAC(1)
      RHO=CARAC(4)
      DO 2 I=1,2
      XE(I)=COORD(1,I)
  2   YE(I)=COORD(2,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C  CALCUL DE LA MASSE EN AXES LOCAUX
C
      CALL POMAL2(WORK,XL,RHO,SD,XINZ)
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPA2(XE,YE,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C  PASSAGE DE LA MATRICE DE MASSE EN AXES GLOBAUX
C
      CALL POURO2(REL,LRE,P,WORK)
      RETURN
999   KERRE=1
      RETURN
      END


