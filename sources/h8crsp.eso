C H8CRSP    SOURCE    CHAT      06/03/29    21:22:55     5360
C
      SUBROUTINE H8CRSP( NB1,NB2,NB3, ITRNOE,
     >                          NBNMAX,NBE,NBN,iarr)
C     **********************************************************************
C     OBJET H8CRSP : CREATION DE LA TOPOLOGIE D'UNE GRILLE 3D (CAS SIMPLE)
C
C     EN ENTREE   :
C           NB1,NB2,NB3 : NOMBRE D'ELEMENTS DE LA GRILLE SUR LA LIGNE,
C                         LA COLONNE, LA HAUTEUR
C           ITRNOE      : TABLEAU DES ELEMENTS (A REMPLIR)
C                         SA TAILLE DOIT ETRE DE (NB1*NB2*NB3*NBNMAX)
C           NBNMAX      : NOMBRE MAXIMUM DE NOEUDS PAR ELEMENTS (8 OU +)
C
C     EN SORTIE   :
C           ITRNOE  : DEFINITION DES ELEMENTS (HEXA)
C           NBE, NBN : NOMBRE D'ELEMENTS ET DE NOEUDS
C           iarr     : CODE D'ERREUR
C     **********************************************************************
      IMPLICIT INTEGER(I-N)
      INTEGER NB1,NB2,NB3
      INTEGER ITRNOE(*),NBNMAX,NBE,NBN,iarr
C
      INTEGER I,J,K,IP,IE
C
      iarr = -1
      NBE = 0
      NBN = 0
      IF( NBNMAX.LT.4 )GOTO 999
      IF((NB1.LE.0).OR.(NB2.LE.0))GOTO 999
      iarr = 0
      NBE = NB1*NB2*NB3
      NBN = (NB1+1)*(NB2+1)*(NB3+1)
      DO 20 K=1,NB3
       DO 10 J=1,NB2
        DO 5  I=1,NB1
         IE = I + (J-1)*NB1+(K-1)*NB1*NB2
         IP = I + (J-1)*(NB1+1)+(K-1)*(NB1+1)*(NB2+1)
C
         ITRNOE((IE-1)*NBNMAX+1)= IP
         ITRNOE((IE-1)*NBNMAX+2)= IP+1
         ITRNOE((IE-1)*NBNMAX+3)= IP+(NB1+1)+1
         ITRNOE((IE-1)*NBNMAX+4)= IP+(NB1+1)
         ITRNOE((IE-1)*NBNMAX+5)= IP+(NB1+1)*(NB2+1)
         ITRNOE((IE-1)*NBNMAX+6)= IP+(NB1+1)*(NB2+1)+ 1
         ITRNOE((IE-1)*NBNMAX+7)= IP+(NB1+1)*(NB2+1)+(NB1+1)+1
         ITRNOE((IE-1)*NBNMAX+8)= IP+(NB1+1)*(NB2+1)+(NB1+1)
    5    CONTINUE
   10   CONTINUE
   20  CONTINUE
  999 END



