C COMRED    SOURCE    PV        17/07/11    21:15:01     9491

      SUBROUTINE COMRED(ich1)

*---------------------------------------------------------------
*  reduit la taille d'un melval s'il est constant
*
*  ich1  segment de type MELVAL (ACTIF en E/S)
*        segment modifie par ajustement si necessaire
*---------------------------------------------------------------

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)

-INC SMCHAML
-INC CCREEL

      melval = ich1
**    segact,melval

* Valeurs reelles :
* -----------------
      n1ptel = velche(/1)
      n1el   = velche(/2)
      if (n1el.gt.1.or.n1ptel.gt.1) then
* ymaxe* min et max generaux
        ymaxe1 = xgrand
        ymaxe2 = -xgrand
* Cas general :
        if (n1ptel.gt.1) then
*   Est-on constant par element ?
          do ipel = 1, n1el
* ymaxd* min et max sur l'element
            ymaxd1 = xgrand
            ymaxd2 = -xgrand
            do igel = 1, n1ptel
            valu1 = velche(igel,ipel)
              ymaxd1 = min(valu1,ymaxd1)
              ymaxd2 = max(valu1,ymaxd2)
            enddo
            ymaxr=max(max(abs(ymaxd1),abs(ymaxd2))*xzprec,xpetit)
*   Est-on constant sur l'element ipel ?
            if ((ymaxd2-ymaxd1).gt.ymaxr) goto 10
            ymaxe1=min(ymaxe1,ymaxd1)
            ymaxe2=max(ymaxe2,ymaxd2)
          enddo
*   Ici, on est a minima constant par element :
          n1ptel = 1
*   Est-on uniforme ?
          ymaxr=max(max(abs(ymaxe1),abs(ymaxe2))*xzprec,xpetit)
          if ((ymaxe2-ymaxe1).le.ymaxr) n1el = 1
* Cas particulier : le champ est deja constant par element (n1ptel=1)
        else
          do ipel = 1, n1el
            valu1 = velche(1,ipel)
            ymaxe1=min(ymaxe1,valu1)
            ymaxe2=max(ymaxe2,valu1)
          enddo
*   Est-on uniforme ?
          ymaxr=max(max(abs(ymaxe1),abs(ymaxe2))*xzprec,xpetit)
          if ((ymaxe2-ymaxe1).gt.ymaxr) goto 10
          n1el = 1
        endif
        n2ptel = 0
        n2el   = 0
        segadj,melval
* On reactive le segment pour enlever le statut *mod suite au segadj
**      segact,melval
  10    continue
      endif

* Valeurs de type pointeur :
* --------------------------
      n2ptel = ielche(/1)
      n2el   = ielche(/2)
      if (n2el.gt.1.or.n2ptel.gt.1) then
        jalu1 = ielche(1,1)
        maxd1 = 0
* Cas general :
        if (n2ptel.gt.1) then
          do ipel = 1, n2el
            jalu2 = ielche(1,ipel)
*   Est-on constant sur l'element ipel ?
            maxd2 = 0
            do igel = 2, n2ptel
              jiff  = abs(ielche(igel,ipel) - jalu2)
              maxd2 = max(jiff,maxd2)
            enddo
            if (maxd2.ne.0) goto 20
            jiff  = abs(jalu2 - jalu1)
            maxd1 = max(jiff,maxd1)
          enddo
*   Ici, on est a minima constant par element :
          n2ptel = 1
*   Est-on uniforme ?
          if (maxd1.eq.0) n2el = 1
* Cas particulier : le champ est deja constant par element (n2ptel=1)
        else
          do ipel = 2, n2el
            jalu2 = ielche(1,ipel)
            jiff  = abs(jalu2 - jalu1)
            maxd1 = max(jiff,maxd1)
          enddo
*   Est-on uniforme ?
          if (maxd1.gt.0) goto 20
          n2el = 1
        endif
        n1ptel = 0
        n1el   = 0
        segadj,melval
* On reactive le segment pour enlever le statut *mod suite au segadj
**      segact,melval
 20     continue
      endif

      ich1 = melval

      RETURN
      END





