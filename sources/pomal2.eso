C POMAL2    SOURCE    PV        18/06/18    21:15:24     9860
C POMAL2    SOURCE    CASTEM    85/06/13    12:33:23
      SUBROUTINE POMAL2(RP,XL,RHO,SD,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE MASSE ELEMENTAIRE D UNE
C    POUTRE DROITE EN AXES LOCAUX
C    D APRES TEDEL
C    DANS CETTE VERSION,ON PREND EN COMPTE LES INERTIES DE ROTATION
C    MAIS PAS LA DEFORMABILITE A L EFFORT TRANCHANT
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    XINZ    INERTIE IZ
C    RHO     MASSE VOLUMIQUE
C  SORTIE
C    RP      MATRICE DE MASSE ELEMENTAIRE 12X12
C
C    VIENT DE TEDEL AOUT 84
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(6,6)
C
C  INITIALISATIONS
C
      DO 120 J=1,6
      DO 120 I=1,6
  120 RP(I,J)=0.D00
      RL=1.D00/XL
      XL2=XL*XL
      RHOSL=RHO*SD*XL
      RSP1= RHOSL*0.333333333333333333
      RSP2= RHOSL*0.166666666666666666
       U 1=    0.3714285714285714D+00 *   RHOSL
       U 2=    0.5238095238095238D-01 *   RHOSL    *XL
       U 3=    0.1285714285714286D+00 *   RHOSL
       U 4=   -0.3095238095238095D-01 *   RHOSL    *XL
       U 5=    0.9523809523809524D-02 *   RHOSL    *XL2
       U 6=    0.3095238095238095D-01 *   RHOSL    *XL
       U 7=   -0.7142857142857143D-02 *   RHOSL    *XL2
       U 8=    0.3714285714285714D+00 *   RHOSL
       U 9=   -0.5238095238095238D-01 *   RHOSL    *XL
       U10=    0.9523809523809524D-02 *   RHOSL    *XL2
      U11=1.2*RHO*RL*XINZ
      U12=0.1*RHO*XINZ
      U18=0.13333333333333333*XINZ*RHO*XL
      U19=0.1*XINZ*RHO
      U20=0.03333333333333333*XINZ*XL*RHO
C
      RP(1,1)=RSP1
      RP(1,4)=RSP2
      RP(2,2)=U1+U11
      RP(2,3)=U2+U12
      RP(2,5)=U3-U11
      RP(2,6)=U4+U12
      RP(3,3)=U5+U18
      RP(3,5)=U6-U19
      RP(3,6)=U7-U20
      RP(4,4)=RSP1
      RP(5,5)=U8+U11
      RP(5,6)=U9-U12
      RP(6,6)=U10+U18
      DO 9 I=1,6
      DO 9 J=I,6
  9   RP(J,I)= RP(I,J)
      RETURN
      END



