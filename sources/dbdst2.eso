C DBDST2    SOURCE    CHAT      05/01/12    22:37:17     5004
      SUBROUTINE DBDST2(XEL,DD,XDDL,IGAU,BGENE,CMATE,COSA,SINA
     1                                  ,XSTRS)
C======================================================================
C
C  AJOUTE DANS LES CONTRAINTES XSTRS LA PARTIE DHO * EPS2
C                  QUE POUR LES ELEMENTS DST
C  ENTREE
C    XEL(3,3)           =COORDONNEES LOCALES  DU DST
C    DD(8,8)            =MATRICE DE HOOKE
C    XDDL(18)          =D D L LOCAUX DE LA FORMULATION
C    IGAU               =NUMERO DU POINT DE GAUSS    DE 1 A 3
C    XSTRS(8)           =CONTRAINTES  DHO*(EPS1        )
C   BGENE(8,18)         =RELIE LES DEFORMATIONS AUX DDL
C  CMATE                ='ISOTROPE' OU 'ORTHOTRO'
C  COSA                 =COSINUS ET SINUS DU L'ANGLE ENTRE l'AXE
C  SINA                  1 DE L'ORTHOTROPIE ET L'AXE X
C  SORTIE
C    XSTRS(6)           =CONTRAINTES DHO*(EPS1 + EPS2 )
C
C   CODE EBERSOLT AOUT  85   +  P DOWLATYARI OCT. 91 POUR ELEMENT DST
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XEL(3,*),DD(8,*),XSTRS(*),XDDL(*),BGENE(8,*)
      DIMENSION EPS2(3),SHP(6,9),ROT(9)
      CHARACTER*8 CMATE
C
C
      DATA XZER,UNDEMI,TRDEMI/0.D0,.5D0,1.5D0/
C
      BETAX=XZER
      BETAY=XZER
      GAMAXZ=XZER
      GAMAYZ=XZER
C
      CALL DKTSHP(IGAU,XEL,SHP,DJAC)
C
C     ON EXTRAIT LES DEGRES W ROT X ROT Y
C
      IACC= 0
      IADD= 2
      DO 100 IA=1,3
      DO 110 IB=1,3
      ROT(IACC+IB)=XDDL(IADD+IB)
 110  CONTINUE
      IF(CMATE.NE.'ISOTROPE')THEN
        RRX=ROT(IACC+2)*COSA+ROT(IACC+3)*SINA
        RRY=ROT(IACC+2)*(-SINA)+ROT(IACC+3)*COSA
        ROT(IACC+2)=RRX
        ROT(IACC+3)=RRY
      ENDIF
      IACC=IACC+3
      IADD=IADD+6
 100  CONTINUE
C
C     LES ROT SONT DES W   -BETAX  -BETAY AUX 3 NOEUDS
C     LES XDDL  SONT LES W RX RY AUX 3 NOEUDS
C
      CCC   = ROT(2)
      ROT(2)=-ROT(3)
      ROT(3)= CCC
C
      CCC   = ROT(5)
      ROT(5)=-ROT(6)
      ROT(6)= CCC
C
      CCC   = ROT(8)
      ROT(8)=-ROT(9)
      ROT(9)= CCC
C
      DO 200 IA=1,9
      BETAX =SHP(2,IA)*ROT(IA) +BETAX
      BETAY =SHP(3,IA)*ROT(IA) +BETAY
  200 CONTINUE
C
      DO 210 IA=1,18
      GAMAXZ=BGENE(7,IA)*XDDL(IA)+GAMAXZ
      GAMAYZ=BGENE(8,IA)*XDDL(IA)+GAMAYZ
 210  CONTINUE
C
C     ON AJOUTE L'EFFET DE CISAILLEMENT
C
      IF(IGAU.EQ.1)THEN
        DX=XEL(1,2)-XEL(1,1)
        DY=XEL(2,2)-XEL(2,1)
      ELSEIF(IGAU.EQ.2)THEN
        DX=XEL(1,3)-XEL(1,1)
        DY=XEL(2,3)-XEL(2,1)
      ELSE
        DX=XEL(1,3)-XEL(1,2)
        DY=XEL(2,3)-XEL(2,2)
      ENDIF
      XLONG= DX*DX + DY*DY
      XLON2= SQRT(XLONG)
      CC=DX/XLON2
      SS=DY/XLON2
      BETAX=BETAX-TRDEMI*CC*CC*GAMAXZ-TRDEMI*SS*CC*GAMAYZ
      BETAY=BETAY-TRDEMI*SS*CC*GAMAXZ-TRDEMI*SS*SS*GAMAYZ
C
C     BETA EST EN REALITE  -BETA DONC:W,X=BETAX+GAMAXZ
C
      DWDX = BETAX + GAMAXZ
      DWDY = BETAY + GAMAYZ
C
      EPS2(1)= DWDX*DWDX*UNDEMI
      EPS2(2)= DWDY*DWDY*UNDEMI
      EPS2(3)= DWDX*DWDY
C
C     MULTIPLICATION PAR LA MATRICE DE HOOKE
C
      DO 300 IB=1,3
      DO 300 IC=1,3
      XSTRS(IB)=XSTRS(IB)+ DD(IB,IC)*EPS2(IC)
  300 CONTINUE
C
      RETURN
      END

