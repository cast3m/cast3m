C TIMSTR    SOURCE    CHAT      05/01/13    03:41:01     5004
      SUBROUTINE TIMSTR(COORD,DEPLA,CARAC,CONT,WORK,IDEUX)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DES CONTRAINTES POUR LA POUTRE DROITE
C
C  ENTREE
C          DEPLA   DEPLACEMENTS
C          CARAC   TABLEAU DE CARACTERISTIQUES GEOMETRIQUES ET
C                  MATERIELLES DE LA POUTRE  ( 11 VALEURS )
C          COORD   COORDONNEES DES 2 NOEUDS
C          IDEUX   INDICE POUR DEFORMATIONS DU SECOND ORDRE
C
C  SORTIE
C          CONT    CONTRAINTES AU MILIEU
C  TRAVAIL
C          WORK(300)
C
C     CLONE DE POUSTR PP 4/9/92
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),CARAC(*),COORD(3,*),VECT(3),P(3,3),
     .          XE(2),YE(2),ZE(2),DEPLA(*)
C
      XINX=CARAC(1)
      XINY=CARAC(2)
      XINZ=CARAC(3)
      SD=CARAC(4)
      SREDY=CARAC(5)
      SREDZ=CARAC(6)
      DO 1 I=1,3
1     VECT(I)=CARAC(6+I)
      YOUN=CARAC(10)
      CISAIL=CARAC(11)
      DO 2 I=1,2
      XE(I)=COORD(1,I)
      YE(I)=COORD(2,I)
  2   ZE(I)=COORD(3,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2+(ZE(2)-ZE(1))**2
      XL=SQRT(XL)
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPAS(XE,YE,ZE,VECT,P,KERRE)
C
C  PASSAGE DES DEPLACEMENTS EN AXES LOCAUX
C
      CALL POUVEC(DEPLA,WORK,P,1)
C
C  CALCUL DES CONTRAINTES LOCALES
C
      CALL TIMOLO(WORK,CONT,XL,YOUN,CISAIL,SD,SREDY,SREDZ,
     .            XINX,XINY,XINZ,IDEUX)
      RETURN
      END

