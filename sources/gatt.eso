C GATT      SOURCE    CB215821  17/11/30    21:16:17     9639
       SUBROUTINE GATT(TAU,SEQUV,SIGM,SS,EPSV,VAR,XMAT,NSTRS,PHI1,
     &                 PHITAU,TTAU,EPS0,VARPT)
C-----------------------------------------------------------------------
C                MODELE GATT_MONERIE (EF MASSIFS SEULEMENT)
C           CALCUL DES VITESSES DES GRANDEURS SUIVANTES:
C  - DEFORMATIONS VISCOPLASTIQUES
C  - DEFORMATIONS DE DENSIFICATION ET DE GONFLEMENT
C  - POROSITE ET DEFORMATION VISCOPLASTIQUE CUMULEE
C-----------------------------------------------------------------------
C
C  ENTREES
C  -------
C TAU          = SOUS PAS
C SEQUV        = CONTRAINTE DE VON MISES            AU DEB. DU SOUS PAS
C SIGM         = (TRACE DU TENS. DES CONTRAINTES)/3 AU DEB. DU SOUS PAS
C SS(NSTRS)    = CONTRAINTES DEVIATORIQUES          AU DEB. DU SOUS PAS
C EPSV(NSTRS)  = DEFORMATIONS VISCOPLASTIQUES       AU DEB. DU SOUS PAS
C VAR(NVARI=6) = VARIABLES INTERNES                 AU DEB. DU SOUS PAS
C XMAT         = CARACTERISTIQUES THERMOMECANIQUES DU MATERIAU
C PHI1         = DENSITE DE FISSION AU DEB. DU SOUS PAS
C PHITAU       = DENSITE DE FISSION INSTANCIEE (A LA FIN DU SOUS PAS)
C TTAU         = TEMPERATURE        INSTANCIEE (A LA FIN DU SOUS PAS)
C
C  SORTIES
C  -------
C EPS0(NSTRS)    = VITESSES DES DEFORMATIONS VISCOPLASTIQUES  +
C                  VITESSES DES DEFORMATIONS DE DENSIFICATION +
C                  VITESSES DES DEFORMATIONS DE GONFLEMENT
C VARPT(NVARI=6) = VITESSES DES VARIABLES INTERNES
C VAR(4)         = FONCTION TETA EN CAS DE COUPLAGE STATIQUE
C-----------------------------------------------------------------------
       IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
C
-INC CCOPTIO
-INC CCREEL
C
       PARAMETER (XZER=0.D0,UNTIER=0.333333333333333D0)
C
       DIMENSION XMAT(*),VARPT(*),EPS0(*),SS(*),EPSV(*),VAR(*)
       DIMENSION EPSPRI(6),EPSPT1(6),EPSPT2(6),EPSSEC(6)
       DIMENSION EPS0V(6),EPSVD(6)
C
C ---------- DEF. VISCOPLASTIQUE HYDROSTATIQUE
C
       EPSVM=UNTIER*(EPSV(1)+EPSV(2)+EPSV(3))
C
C ---------- DEF. VISCOPLASTIQUES DEVIATORIQUES
C
       DO 5 I=1,NSTRS
         A=XZER
         IF(I.LE.3) A=1.0D0
         EPSVD(I)=EPSV(I)-A*EPSVM
 5     CONTINUE
C
C ----------- DEFORMATION VISCOPLASTIQUE EQUIVALENTE
C
       EEQUV=PROCON(EPSVD,EPSVD,NSTRS)
       EEQUV=SQRT(2.D0*UNTIER*EEQUV)
C
C ---------- POROSITE, BURNUP & FCT. DE COUPLAGE AU DEB. DU SOUS PAS
C
       PORO=VAR(2)
       BU=VAR(3)
       TETA0=VAR(4)
C
C ---------- PROPRIETES VISCOPLASTIQUES DU MATERIAU
C
C   Les 4 premieres valeurs de Xmat_OBLIGATOIRES sont YOUN,NU,RHO,ALPH
C   puis R,DG,...(caract. mecaniques non lineaires obligatoires)
C   La premiere valeur de Xmat_FACULTATIF est VISQ et la deuxieme en CP
C   est DIM3
C   puis TYPE,COMP,...(caract. mecaniques non lineaires facultatives)
C
        RHO  = XMAT(3)
        RR   = XMAT(5)
        DG   = XMAT(6)
        XK1  = XMAT(7)
        XM1  = XMAT(8)
        Q1   = XMAT(9)
        XN1  = XMAT(10)
        XK2  = XMAT(11)
        XM2  = XMAT(12)
        Q2   = XMAT(13)
        XN2  = XMAT(14)
        OMEG = XMAT(15)
        QQ   = XMAT(16)
        HH   = XMAT(17)
        BETA = XMAT(18)
        XK   = XMAT(19)
        XA   = XMAT(20)
        Q3   = XMAT(21)
        XN3  = XMAT(22)
        ADEN = XMAT(23)
        XKGON= XMAT(24)
        RHO0 = 1.D0-XMAT(25)
        BUMI = XMAT(26)
        EFIS = XMAT(27)
        DG0  = XMAT(28)
        CR1  = XMAT(29)
        CR2  = XMAT(30)
        CR3  = XMAT(31)
        CR   = XMAT(32)
        XKP  = XMAT(33)
        AP   = XMAT(34)
        BP   = XMAT(35)
        QP   = XMAT(36)
C
C ---------- TYPE = 0. COMBUSTIBLE UO2, SINON COMBUSTIBLE AFA3GLAA
C
        NTYPE=38
        IF (IFOUR.EQ.-2) NTYPE=39
        TYPE= XMAT(NTYPE)
C
C ---------- DYN = 0. COUPLAGE STATIQUE, SINON COUPLAGE DYNAMIQUE
C
        DYN = XMAT(NTYPE+2)
C
        IF (DYN.NE.XZER) THEN
           DYN1= XMAT(NTYPE+3)
           TO=DYN1
           IF (TYPE.NE.XZER) THEN
              DYN2= XMAT(NTYPE+4)
              DYN3= XMAT(NTYPE+5)
              TO1 = (DYN2 - TTAU) / DYN3
              TO  = ( DYN1*(1.D0+TANH(TO1)) ) + 1.D0
           ENDIF
        ENDIF
C
C ---------- COMP = 0. COMBUSTIBLE COMPRESSIBLE, SINON INCOMPRESSIBLE
C
        COMP= XMAT(NTYPE+1)
C
C ---------- CARACTERISTIQUES VISCOPLASTIQUES DU MATERIAU
C
        AKP=XKP*EXP(-1.D0*QP/RR/TTAU)
        WC1=1.D0+TANH((CR-CR2)/CR3)
        WC1=1.D0+0.5D0*CR1*WC1
        AK1=XK1*WC1*(DG**XM1)*EXP(-1.D0*Q1/RR/TTAU)
        WC2=2.D0*(DG0**XM2)*(1.D0-(COS(DG/DG0)))
        AK2=XK2*WC2*EXP(-1.D0*Q2/RR/TTAU)
        AK3=XA*EXP(-1.D0*Q3/RR/TTAU)
C
C--------------------------------------------------------------------
C  Fluage therm. primaire: definition de la vitesse de déform. EPSPRI
C--------------------------------------------------------------------
        IF (EEQUV.LT.1D-10) EEQUV=1D-10
        AP1=AP-1.D0
        PRIM = AKP*(SEQUV**AP1)/(EEQUV**BP)
        DO 8 I=1,NSTRS
           EPSPRI(I)=1.5D0*PRIM*SS(I)
  8     CONTINUE
C
C----------------------------------------------------------------------
C  Fluage therm. secondaire: definition de Ai et Bi fct. de la porosite
C----------------------------------------------------------------------
        EXPA1 = -1.D0/XN1
        EXPAB1= 2.D0*XN1/(XN1+1.D0)
        A1    = (XN1*( (PORO**EXPA1) - 1.D0 ))**(-1.D0*EXPAB1)
        B1    = (1.D0+((2.D0/3.D0)*PORO))/((1.D0-PORO)**EXPAB1)
C
        EXPA2 = -1.D0/XN2
        EXPAB2= 2.D0*XN2/(XN2+1.D0)
        A2    = (XN2*( (PORO**EXPA2) - 1.D0 ))**(-1.D0*EXPAB2)
        B2    = (1.D0+((2.D0/3.D0)*PORO))/((1.D0-PORO)**EXPAB2)
C
        IF (COMP.NE.XZER) THEN
           B1 = B1 + A1/4.D0
           B2 = B2 + A2/4.D0
           A1 = XZER
           A2 = XZER
        ENDIF
C-----------------------------------------------------------------------
C  Fluage therm. secondaire: def. des potentiels thermodyn. PSI1 et PSI2
C-----------------------------------------------------------------------
        EXP1=XN1 + 1.D0
        EX1 = 0.5D0*EXP1
        ZZ1 =A1*(1.5D0*SIGM)**2+B1*SEQUV**2
        PSI1=(AK1/EXP1)*(ZZ1**EX1)
C
        EXP2=XN2 + 1.D0
        EX2 = 0.5D0*EXP2
        ZZ2 =A2*(1.5D0*SIGM)**2+B2*SEQUV**2
        zz2=min(zz2,xgrand**(1/ex2))
        PSI2=(AK2/EXP2)*(ZZ2**EX2)
C--------------------------------------------------------------------
C  Fluage therm. secondaire: vit. de def. / mecanismes EPSPT1, EPSPT2
C--------------------------------------------------------------------
        EXPM1 = (XN1 - 1.D0)/2.D0
        DO 10 I=1,NSTRS
          IF(I.LE.3) THEN
            SIG1 = 1.5D0*A1*SIGM + 3.D0*B1*SS(I)
          ELSE
            SIG1 = 3.D0*B1*SS(I)
          ENDIF
          EPSPT1(I) =0.5D0*AK1*(ZZ1**EXPM1)*SIG1
 10    CONTINUE
C
        EXPM2 = (XN2 - 1.D0)/2.D0
        DO 20 I=1,NSTRS
          IF(I.LE.3) THEN
            SIG2 = 1.5D0*A2*SIGM + 3.D0*B2*SS(I)
          ELSE
            SIG2 = 3.D0*B2*SS(I)
          ENDIF
          EPSPT2(I) =0.5D0*AK2*(ZZ2**EXPM2)*SIG2
 20     CONTINUE
C---------------------------------------------------------------------
C Def. de la fonction de couplage statique entre les 2 mecanismes TETA
C---------------------------------------------------------------------
        GSEQ1 = B1/(B1+A1/4.D0)
        GSEQ2 = 9.D0*A1/(4.D0*B1+A1)
        GSEQ  = (GSEQ1*SEQUV**2.D0)+(GSEQ2*SIGM**2.D0)
        GSEQUV=SQRT(GSEQ)
        IF (GSEQUV.LT.1D-10) THEN
          TETA  = XZER
        ELSE
          THERB = OMEG*(GSEQUV**(-QQ))
          TT    = (TTAU-THERB)/HH
          TETA  = 0.5D0*BETA*(1.D0+TANH(TT))
        ENDIF
C--------------------------------------------------------------------
C Vit. de la fct. de couplage dynamique entre les 2 mecanismes TETAPT
C--------------------------------------------------------------------
C ---------- DYN = 0. couplage statique, sinon couplage dynamique
C
        IF (DYN.NE.XZER) THEN
           IF (TETA.LT.1D-10) THEN
              TETAPT = XZER
           ELSE
              TETAPT = ((TETA-TETA0)**2.D0) / (TO * TETA)
              TETAPT =SIGN(TETAPT,TETA-TETA0)
           ENDIF
           TETA   = TETA0
        ELSE
           TETAPT = XZER
        ENDIF
C-----------------------------------------------------------------------
C Fluage thermique secondaire: vitesse de deformation viscoplast. EPSSEC
C-----------------------------------------------------------------------
        IF (GSEQUV.LT.1D-10.OR.DYN.NE.XZER) THEN
          DERTET2 = XZER
        ELSE
          DERTET1 =
     &      0.5D0*BETA*QQ*OMEG*(GSEQUV**(-2.D0-QQ))/HH
          DERTET2 = DERTET1*(1.D0 - (TANH(TT))**2)
        ENDIF
        DO 30 I=1,NSTRS
          A=XZER
          IF(I.LE.3) A=1.0D0
          VIT = (1.D0 - TETA)*EPSPT1(I) + TETA*EPSPT2(I)
          EPSSEC(I) = VIT +
     &    DERTET2*(PSI2-PSI1)*(A*GSEQ2*UNTIER*SIGM + 1.5D0*GSEQ1*SS(I))
 30     CONTINUE
C------------------------------------------------------------------------
C Viscoplast. due a l irradiation EPS0IR (en vit.)
C Prise en compte de l acceleration de la viscoplast. thermique (en vit.)
C EPS0 = (1+k*fit)(EPSPRI+EPSSEC) + EPS0IR
C------------------------------------------------------------------------
        IF (SEQUV.LT.1D-10) THEN
          SIR=XZER
        ELSE
          SIR=SEQUV**(XN3-1.D0)
        ENDIF
        DO 40 I=1,NSTRS
          EPS0IR = AK3*PHITAU*SIR*(1.5D0*SS(I))
          EPS0V(I)=(1.D0 + XK*PHITAU)*(EPSPRI(I)+EPSSEC(I)) + EPS0IR
          EPS0(I)=EPS0V(I)
 40     CONTINUE
C-------------------------------------------------------------------------
C Vit. de def. de gonflement EPS0G et vit. de def. de densification EPS0D.
C-------------------------------------------------------------------------
        AMU238=238.D0
        AMUO2=238.D0 + 16.D0 + 16.D0
        AKBU=(AMUO2/AMU238)*EFIS/(RHO*RHO0)
        BUTAU=BU+(0.5D0*AKBU*TAU*(PHI1+PHITAU))
        IF(BU.GE.BUMI.OR.BUTAU.GE.BUMI) THEN
          BUMAX0=60.D0*BUMI
          CRIT=1.D-10
          DO 50 I=1,100
            BUMAX = BUMI*EXP(1.D0-(ADEN/(XKGON*BUMAX0)))
            BUMAX = (0.2D0*BUMAX) + (0.8D0*BUMAX0)
            TEST=ABS((BUMAX-BUMAX0)/BUMAX0)
            IF (TEST.LT.CRIT) GOTO 60
            BUMAX0=BUMAX
 50       CONTINUE
 60       CONTINUE
          AA = (-1.D0*RHO0*(ADEN-(XKGON*BUMAX))) /
     &           ((1.D0+ADEN)*(LOG(BUMAX/BUMI)))
        ENDIF
        IF(BU.LT.BUMI) EPS0D1=0.D0
        IF(BU.GE.BUMI)
     &    EPS0D1=-1.D0*(1.D0+(XKGON*BU))*AA*LOG(BU/BUMI)/
     &      (RHO0+(AA*LOG(BU/BUMI)))
        IF(BUTAU.LT.BUMI) EPS0DT=0.D0
        IF(BUTAU.GE.BUMI)
     &    EPS0DT=-1.D0*(1.D0+(XKGON*BUTAU))*AA*LOG(BUTAU/BUMI)/
     &        (RHO0+(AA*LOG(BUTAU/BUMI)))
        EPS0D=(EPS0DT-EPS0D1)/3.D0/TAU
        EPS0G=XKGON*0.5D0*AKBU*(PHI1+PHITAU)/3.D0
        DO 70 I=1,3
          EPS0(I)=EPS0V(I)+EPS0D+EPS0G
 70     CONTINUE
C------------------------------------------------------------------
C Vit. des var. int.: déformation viscoplast. cumulée + la porosité
C                     + le taux de combustion + fct. de couplage
C                     + def de densification  + def de gonflement
C------------------------------------------------------------------
        EPSC = PROCON(EPS0,EPS0,NSTRS)
        VARPT(1) = SQRT(2.D0*EPSC/3.D0)
        IF (COMP.NE.XZER) THEN
           VARPT(2) = XZER
        ELSE
           VARPT(2) = (1.D0-PORO)*(EPS0V(1) + EPS0V(2) + EPS0V(3))
           VARPT(2) = VARPT(2) + (3.D0*EPS0D)
        ENDIF
        VARPT(3) = 0.5D0*AKBU*(PHI1+PHITAU)
        VARPT(4) = TETAPT
        IF (DYN.EQ.XZER) VAR(4)=TETA
        VARPT(5) = EPS0D
        VARPT(6) = EPS0G
C
        RETURN
        END







