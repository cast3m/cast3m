C DSDX3D    SOURCE    CHAT      07/10/22    21:15:46     5921
          SUBROUTINE DSDX3D(LOOP,B,U,DEPS,D,NBN)
       IMPLICIT REAL*8(A-H,O-Z)
       IMPLICIT INTEGER (I-N)
*
*----------------------------------------------------------------------
*
*      CALCUL DES DERIVEES DUX/DX DVX/DY ETC....     H.  BUNG   FEV 83
*          (ELEMENTS CUB6 )
*----------------------------------------------------------------------
*
* ENTREES :
*      B     :  MATRICE (B)
*      U     :  TABLEAU DES VALEURS NODALES
*    LOOP    =  2 ON CALCULE LES DEFORMATIONS JUSQU'AU 2E ORDRE
*               SUR LA CONFIGURATION (N+1)
*
* SORTIES :
*      D       : D(1)=DU1/DX1 ;  D(2)=DU2/DX1 ; D(3)=DU3/DX1 ;
*              : D(4)=DU1/DX2 ;  D(5)=DU2/DX2 ; D(6)=DU3/DX2 ;
*              : D(7)=DU1/DX3 ;  D(8)=DU2/DX3 ; D(9)=DU3/DX3
*      DEPS  : TENSEUR DE DEFORMATION ASSOCIE A   <U>
*               DEPS(1)=D(1)     ; DEPS(2)=D(5)    ; DEPS(3)=D(9)
*               DEPS(4)=D(2)+D(4); DEPS(5)=D(6)+D(8); DEPS(6)=D(3)+D(7)
*
*      AVEC EVENTUELLEMENT LES TERMES DU 2EME  ORDRE
*
*
*      IMPLICIT NONE
*
*      REAL *8 B(3,*),U(3,8),D(*),DEPS(*)
*      REAL *8 B1I,B2I,B3I,U1I,U2I,U3I
*      REAL *8 D1,D2,D3,D4,D5,D6,D7,D8,D9
      DIMENSION B(3,*),U(3,8),D(*),DEPS(*)
*
*      INTEGER NBN,LOOP
*
*      INTEGER I
*
**OP_SN
*
*
      DO I=1,9
        D(I) = 0D0
      END DO
*
      D1 = 0.D0
      D2 = 0.D0
      D3 = 0.D0
      D4 = 0.D0
      D5 = 0.D0
      D6 = 0.D0
      D7 = 0.D0
      D8 = 0.D0
      D9 = 0.D0
*
      DO I=1,NBN
        B1I = B(1,I)
        B2I = B(2,I)
        B3I = B(3,I)
        U1I = U(1,I)
        U2I = U(2,I)
        U3I = U(3,I)
        D1 = D1 + B1I*U1I
        D2 = D2 + B1I*U2I
        D3 = D3 + B1I*U3I
        D4 = D4 + B2I*U1I
        D5 = D5 + B2I*U2I
        D6 = D6 + B2I*U3I
        D7 = D7 + B3I*U1I
        D8 = D8 + B3I*U2I
        D9 = D9 + B3I*U3I
      END DO
      D(1) = D1
      D(2) = D2
      D(3) = D3
      D(4) = D4
      D(5) = D5
      D(6) = D6
      D(7) = D7
      D(8) = D8
      D(9) = D9
*
      DEPS(1) = D1
      DEPS(2) = D5
      DEPS(3) = D9
      DEPS(4) = D2 + D4
      DEPS(5) = D6 + D8
      DEPS(6) = D3 + D7
*
      IF(LOOP.EQ.2) THEN
        DEPS(1) = DEPS(1) - 0.5*(D1*D1 + D2*D2 + D3*D3)
        DEPS(2) = DEPS(2) - 0.5*(D4*D4 + D5*D5 + D6*D6)
        DEPS(3) = DEPS(3) - 0.5*(D7*D7 + D8*D8 + D9*D9)
*
        DEPS(4) = DEPS(4) - D1*D4 - D2*D5 - D3*D6
        DEPS(5) = DEPS(5) - D4*D7 - D5*D8 - D6*D9
        DEPS(6) = DEPS(6) - D1*D7 - D2*D8 - D3*D9
      ENDIF
*
      END


