C TRJULO    SOURCE    CHAT      05/01/13    03:51:40     5004
      SUBROUTINE TRJULO(XNOEU,UNOEU,UELEM,XY,ITYP,IDIM,NOEL1,NOUN,
     * IFORMU,SHP,SHY)
C*******************************************************************
C      ISSU DE TRIOEF  sp ULOTAR
C         calcul de la vitesse dans l'element de reference
C********************************************************************
C   ENTREES
C     XNOEU COORDONNEES REELLES DES NOEUDS DE L ELEMENT
C     UNOEU VITESSES AUX NOEUDS DE L ELEMENT(OU FLUX AUX FACES)
C     XY COORDONNEES DE REFERENCES DU POINT DE CALCUL
C     ITYP TYPE DE L ELEMENT
C     NOEL1  NOMBRE DE NOEUDS DE L ELEMENT POSITION
C     NOUN NOMBRE DE VITESSES OU FLUX PAR ELEMENT. Pour les EFMH ce
C        ce nombre est different de NOEL1 pour le cube et le prisme
C   SORTIES
C     UELEM VITESSE DANS L ELEMENT DE REFERENCE EXPRIMEE AVEC LES
C           COORDONNEES DE REFERENCE AU POINT DE CALCUL
C
C     SHP(1,I) FONCTIONS DE FORMES AU POINT DE CALCUL
C     SHP(2,I) DERIVEE DES FONCTIONS DE FORME / XSI AU POINT DE CALCUL
C     SHP(3,I)    "     "      "     "    "   / ETA "    "   "    "
C     SHP(4,I)    "     "      "      "   "   /DZETA "   "    "   "
C ********************************************************************
C
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C
C
      DIMENSION UNOEU(*),XY(*),UELEM(*),UE(3)
      DIMENSION XNOEU(3,*),SHP(6,*),SHY(12,*)
C
C
C***  CALCUL DES XI ET DES DXI
C
C     write(6,*)' ulotar' ,noel1,((unoeu(i,j),i=1,idim),j=1,noel1)
C     write(6,*) xy(1),xy(2),((xnoeu(i,j),i=1,idim),j=1,noel1)
      CALL SHAPE(XY(1),XY(2),XY(3),ITYP,SHP,IRET)
C
C***  VITESSE
C
C
C
C***  COEFFICIENTS DE DERIVATION
C
      IF(IDIM.EQ.2)THEN
C
C***  BIDIM
C
            A=0.D0
            B=0.D0
            C=0.D0
            D=0.D0
            DO 63 I=1,NOEL1
                 A=A+SHP(2,I)*XNOEU(1,I)
                 B=B+SHP(3,I)*XNOEU(1,I)
                 C=C+SHP(2,I)*XNOEU(2,I)
                 D=D+SHP(3,I)*XNOEU(2,I)
  63        CONTINUE
            DEL=A*D-B*C
      IF(IFORMU.EQ.1)THEN
C                            FORMULATION ELEMENTS FINIS
      CALL TRJVPO(UNOEU,UE,XY,ITYP,IDIM,NOEL1,SHP)
            UELEM(1)=(D*UE(1)-B*UE(2))/DEL
            UELEM(2)=(A*UE(2)-C*UE(1))/DEL
C     write(6,*) 'vitesse',(ue(i),i=1,idim)
C                            FORMULATION HYBRIDE
      ELSEIF(IFORMU.EQ.2)THEN
      ITYH=NUMHYB(ITYP)
      CALL HYSHP(XY(1),XY(2),XY(3),ITYH,NOUN,12,SHY,IRET)
      DO 50 I=1,IDIM
      UE(I)=0.D0
      DO 40 K=1,NOUN
      UE(I)=UE(I)+SHY(I,K)*UNOEU(K)
   40 CONTINUE
C     write(6,*)'shy ',i,(shy(i,k),k=1,noel1)
      UELEM(I)=UE(I)/ABS(DEL)
   50 CONTINUE
      ENDIF
C           write(6,*)' uelem ',uelem(1),uelem(2)
C
C
C***  TRIDIM
C
      ELSE
            A1=0.D0
            A2=0.D0
            A3=0.D0
            B1=0.D0
            B2=0.D0
            B3=0.D0
            C1=0.D0
            C2=0.D0
            C3=0.D0
            DO 75 I=1,NOEL1
                 A1=A1+SHP(2,I)*XNOEU(1,I)
                 A2=A2+SHP(3,I)*XNOEU(1,I)
                 A3=A3+SHP(4,I)*XNOEU(1,I)
                 B1=B1+SHP(2,I)*XNOEU(2,I)
                 B2=B2+SHP(3,I)*XNOEU(2,I)
                 B3=B3+SHP(4,I)*XNOEU(2,I)
                 C1=C1+SHP(2,I)*XNOEU(3,I)
                 C2=C2+SHP(3,I)*XNOEU(3,I)
                 C3=C3+SHP(4,I)*XNOEU(3,I)
  75        CONTINUE
            DET=A1*(B2*C3-C2*B3)+A2*(C1*B3-B1*C3)+A3*(B1*C2-B2*C1)
      IF(IFORMU.EQ.1)THEN
C                            FORMULATION ELEMENTS FINIS
      CALL TRJVPO(UNOEU,UE,XY,ITYP,IDIM,NOEL1,SHP)
            UELEM(1)=((B2*C3-C2*B3)*UE(1)+(A3*C2-A2*C3)*UE(2)
     *               +(A2*B3-A3*B2)*UE(3))/DET
            UELEM(2)=((C1*B3-C3*B1)*UE(1)+(A1*C3-A3*C1)*UE(2)
     *               +(A3*B1-A1*B3)*UE(3))/DET
            UELEM(3)=((B1*C2-B2*C1)*UE(1)+(A2*C1-A1*C2)*UE(2)
     *               +(A1*B2-A2*B1)*UE(3))/DET
C     write(6,*) 'vitesse',(ue(i),i=1,idim)
C                            FORMULATION HYBRIDE
      ELSEIF(IFORMU.EQ.2)THEN
      ITYH=NUMHYB(ITYP)
      CALL HYSHP(XY(1),XY(2),XY(3),ITYH,NOUN,12,SHY,IRET)
      DO 70 I=1,IDIM
      UE(I)=0.D0
      DO 60 K=1,NOUN
      UE(I)=UE(I)+SHY(I,K)*UNOEU(K)
   60 CONTINUE
      UELEM(I)=UE(I)/ABS(DET)
   70 CONTINUE
      ENDIF
C
      ENDIF
      RETURN
      END



