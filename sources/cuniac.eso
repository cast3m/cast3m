C CUNIAC    SOURCE    PV        17/12/08    21:17:09     9660
C UNIACI    SOURCE    AM        96/12/24    21:33:08     2448
      SUBROUTINE CUNIAC(wrk52,wrk53,wrk54,NSTRS1,MFR1)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C----------------------------------------------------------------------
C
C     COMPORTEMENT UNIDIRECTIONNEL ACIER_UNI (2D ou coque)
C     (D'APRES FIBSTE DU MODELE A FIBRE)
C
C----------------------------------------------------------------------
C     Pierre Pegon (ISPRA) Juillet 1994
C----------------------------------------------------------------------
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C      XMAT( 3) V1X
C      XMAT( 4) V1Y
C      XMAT( 5) MAX. ELAST. STRESS
C      XMAT( 6) ULTIMATE STRESS
C      XMAT( 7) HARDENING STRAIN
C      XMAT( 8) ULTIMATE STRAIN
C      XMAT( 9) Ro FACTOR
C      XMAT(10) b  FACTOR
C      XMAT(11) a1 FACTOR
C      XMAT(12) a2 FACTOR
C      XMAT(13) FACTOR L/D
C      XMAT(14) a6 FACTOR
C      XMAT(15) c FACTOR
C      XMAT(16) a FACTOR
C----------------------------------------------------------------------
C      VAR0( 1) VARF( 1) FLAG
C      VAR0( 2) VARF( 2) EPSO
C      VAR0( 3) VARF( 3) R FACTOR
C      VAR0( 4) VARF( 4) TANGENT MODULUS
C      VAR0( 5) VARF( 5) LAST REV. STRAIN
C      VAR0( 6) VARF( 6) INTERSEPTION POINT
C      VAR0( 7) VARF( 7) MAX. REV. POST. STRAIN
C      VAR0( 8) VARF( 8) MIN. REV. POST. STRAIN
C      VAR0( 9) VARF( 9) LAST REV. STRESS
C      VAR0(10) VARF(10) INTERSEPTION POINT
C      VAR0(11) VARF(11) MAX. REV. POST. STRESS
C      VAR0(12) VARF(12) MIN. REV. POST. STRESS
C----------------------------------------------------------------------
C      xcarb( 1) EPAIS (if any)
C----------------------------------------------------------------------
-INC CCOPTIO
-INC DECHE

      DIMENSION EPSON(5),SIGMA(5)
      DIMENSION COBMA(10)
*
      KERRE=0
*
C
C Calcul des contraintes unidirectionnelles effectives
C
      YOUN=XMAT(1)
      V1X=XMAT(3)
      V1Y=XMAT(4)
      IF (IFOUR.EQ.2.AND.(MFR1.EQ.3.OR.MFR1.EQ.9))THEN
        EPAI=xcarb(1)
        S0IUNI=(SIG0(1)*V1X**2+SIG0(2)*V1Y**2+V1X*V1Y*SIG0(3))/EPAI
        DSIUNI=(DEPST(1)*V1X**2+DEPST(2)*V1Y**2+V1X*V1Y*DEPST(3))
        DSIUNI=YOUN*DSIUNI
      ELSEIF (IFOUR.LE.0.AND.MFR1.EQ.1)THEN
        S0IUNI=SIG0(1)*V1X**2+SIG0(2)*V1Y**2+V1X*V1Y*SIG0(4)
        DSIUNI=DEPST(1)*V1X**2+DEPST(2)*V1Y**2+V1X*V1Y*DEPST(4)
        DSIUNI=YOUN*DSIUNI
      ELSE
        KERRE=99
        RETURN
      ENDIF
C
      EYOUN = YOUN
      FSY__ = XMAT( 5)
      FSU__ = XMAT( 6)
      EPSH_ = XMAT( 7)
      EPSU_ = XMAT( 8)
      ROFAC = XMAT( 9)
      BFACT = XMAT(10)
      A1FAC = XMAT(11)
      A2FAC = XMAT(12)
      FACLD = XMAT(13)
      A6FAC = XMAT(14)
      CFACT = XMAT(15)
      AFACT = XMAT(16)
C
      IFLAG = NINT(VAR0(1))
      STRAI = VAR0(2) + DSIUNI/EYOUN
      RFACT = VAR0(3)
      TANGE = VAR0(4)
C
      DO IFACT = 1, 2
         EPSON(IFACT) = VAR0(4+IFACT)
         SIGMA(IFACT) = VAR0(8+IFACT)
      END DO
C
      EPSON(3) = VAR0(2)
      SIGMA(3) = S0IUNI
C
      DO IFACT = 4, 5
         EPSON(IFACT) = VAR0(3+IFACT)
         SIGMA(IFACT) = VAR0(7+IFACT)
      END DO
C
      CALL STEEL1
     .       ( EYOUN, FSY__, FSU__, EPSH_, EPSU_, ROFAC, BFACT,
     .         A1FAC, A2FAC, FACLD, A6FAC, CFACT, AFACT, IFLAG,
     .         STRAI, RFACT, EPSON, SIGMA, TANGE, STRES )
C
      VARF(1) = DBLE(IFLAG)
      VARF(2) = STRAI
      VARF(3) = RFACT
      VARF(4) = TANGE
C
      DO IFACT = 1, 2
         VARF(4+IFACT) = EPSON(IFACT)
         VARF(8+IFACT) = SIGMA(IFACT)
      END DO
C
      DO IFACT = 4, 5
         VARF(3+IFACT) = EPSON(IFACT)
         VARF(7+IFACT) = SIGMA(IFACT)
      END DO
C
      IF (IFOUR.EQ.2.AND.(MFR1.EQ.3.OR.MFR1.EQ.9))THEN
        EPAI=xcarb(1)
        SIGF(1)=EPAI*STRES*V1X**2
        SIGF(2)=EPAI*STRES*V1Y**2
        SIGF(3)=EPAI*STRES*V1X*V1Y
        DO ISTRS=4,NSTRS1
C PPDC
C          SIGF(ISTRS)=SIG0(ISTRS)+DSIGT(ISTRS)
          SIGF(ISTRS)=SIG0(ISTRS)
C PPDC
        ENDDO
      ELSEIF (IFOUR.LE.0.AND.MFR1.EQ.1)THEN
        SIGF(1)=STRES*V1X**2
        SIGF(2)=STRES*V1Y**2
        SIGF(3)=0.D0
        SIGF(4)=STRES*V1X*V1Y
      ENDIF
C
      DO ISTRS=1,NSTRS1
        DEFP(ISTRS)=DEPST(ISTRS)-((SIGF(ISTRS)-SIG0(ISTRS)))/EYOUN
      END DO
C
 1000 RETURN
      END










