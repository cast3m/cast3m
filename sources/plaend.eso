C PLAEND    SOURCE    CB215821  16/04/21    21:18:01     8920
C PLAEND     SOURCE    INSL       24/10/96
      SUBROUTINE PLAEND(EPSRX,SIGRX,STRNX,S1X,IFISU,IPLA,EQSTR,EPSEQ,
     1 JFISU,TANG,EPST,EPSC,EDC,EDT,RTM,DEFR,SIGMRX,PENT,EX,RB,ALPHA,
     2 EMAX,EPSU,EPO1,ICAL,IBB1,IGAU1,IDIR)
C
C      MODELE 2 : BETON PLASTIQUE-ENDOMMAGEABLE (OMAR)
C          ******  VERSION 8 JUILLET 1991  ******
C          ******  DERNIERE ACTUALISATION  19 JUILLET 1993  ******
C          ******  DERNIERE ACTUALISATION  20 JUILLET 1995 ******
C
C ENTREES : EPSRX : DEFORMATION TOTALE DERNIER POINT CONVERGE
C           SIGRX : CONTRAINTE DERNIER POINT CONVERGE
C           STRNX = DEPS(1): INCREMENT DE DEFORMATION
C
C  E/S : IFISU,IPLA,EQSTR,EPSEQ,JFISU,TANG,EPST,EPSC,EDC,EDT,RTM,DEFR
C       CE SONT TOUTES DES VARIABLES INTERNES : VOIR LEUR SIGNIFICATION
C       DANS LA PROCEDURE APPELANTE : FIBINSA
C
C SORTIES :  S1X : LA CONTRAINTE REELLE OU FINALE
C
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      CHARACTER*8 PASDT,PASDC
C
C-----------------------------------------------------------------------
C           ############################################
C           *      B E T O N    U N I A X I A L        *
C           ############################################
      STRNRX=EPSRX+STRNX
      EPEQ=ABS(STRNRX)
C=================================================================
C      SI LE POINT A DEPASSE LA LIMITE EMAX EN COMPRESSION
C      ===> LE POINT NE REPREND PLUS DE CONTRAINTES ET LA CONTRAINTE
C           TOTALE EST NULLE QUELQUE SOIT L ETAT DE DEFORMATION
C
C
      IF(IPLA .EQ. 4) THEN
       EPSEQ=EPEQ
       S1X=0.D0
       TANG=0.D0
       RETURN
      ENDIF
C=================================================================
      IF(IFISU .EQ. 1) THEN
C=================================================================
C              ***************************
C              *   POINT DEJA FISSURE    *
C              ***************************
C            TEST DE FERMETURE DE LA FISSURE
C
         PASDT= 'PLAEND21'
         CALL DECHAT(EPSRX,STRNX,STRNRX,SIGMRX,TANG,S1X,EDT,EDC,
     1   EPST,DEFR,IFISU,IPLA,EQSTR,RTM,EPSC,IREFE,EX,RB,ALPHA,EPSU,
     2   EPO1,ICAL,IBB1,IGAU1,IDIR,PASDT)
         IF(IFISU.EQ.0.OR.IREFE.EQ.1) RETURN
C
C        TENSION-SOFTENING
C
         CALL FISSU(S1X,STRNX,TANG,RTM,PENT)
         RETURN
      ENDIF
C======================================================================
      IF(IFISU.EQ.0.AND.JFISU.EQ.1) THEN

C       COMPORTEMENT DU BETON FISSURE (FISSURE FERMEE)
C
         CALL BETFIS(EPSRX,STRNX,STRNRX,SIGMRX,SIGRX,TANG,S1X,EDT,EDC,
     1   EPST,EPSC,DEFR,RTM,IFISU,JFISU,IPLA,EQSTR,PENT,EX,RB,ALPHA,
     2   EMAX,EPO1,ICAL,IBB1,IGAU1,IDIR)
        RETURN
      ENDIF
C==========================================================================
C
C       BETON INTEGRE (NON FISSURE)
C
C==========================================================================
      IF(ABS(STRNRX).LT.EPSEQ) THEN
C
C       ************ DOMAINE DES DECHARGES ************
C                    (DECHARGE ENDOMMAGEE)
C   ZONES DE COMPRESSION OU ZONES DE TRACTION APRES DECHARGE EN COMPRESSION
C
        PASDC= 'PLAEND22'
        CALL DECHAC(S1X,EDC,EPST,EDT,RTM,EPSC,DEFR,SIGRX,STRNX,
     1  STRNRX,EPSRX,IFISU,JFISU,SIGMRX,IPLA,TANG,PENT,EX,RB,ALPHA,
     2  EPO1,IBB1,IGAU1,IDIR,PASDC)
      RETURN
      ENDIF
C==========================================================================

      IF(ABS(STRNRX).GE.EPSEQ .AND. SIGMRX .LT. 0.D0) THEN
C
C       ************ ECROUISSAGE EN COMPRESSION ************
C
      CALL CDCY(EPEQ,SEQ,IPLA,TANG,EX,RB,EMAX,EPO1,ICAL)
      EQSTR=SEQ
      EPSEQ=EPEQ
      S1X=-SEQ
      RETURN
      ENDIF
C==========================================================================
      IF(ABS(STRNRX).GE.EPSEQ.AND.SIGMRX.GE.0.D0.AND.EPSRX.GE.0.D0)
     + THEN
C
C       ***********  DOMAINE DES TRACTIONS  ***********
C       ********** POINT INITIALLEMENT TENDU **********
C
       IF(SIGMRX.GE.RTM) THEN
C
C              *************************************
C              *    CE POINT VIENT DE FISSURER     *
C              *************************************
C
          CALL FISSU2(S1X,SIGRX,STRNX,RTM,PENT,TANG)
          IFISU=1
          JFISU=1
C
C       Remarque : … l it‚ration suivante chemin ---> BETFIS
C
       ELSE
C              ****************************************
C              *  POINT ELASTIQUE INTEGRE (TRACTION)  *
C              ****************************************
C
          S1X=SIGMRX
       ENDIF
      RETURN
      ENDIF
C==========================================================================
      IF(ABS(STRNRX).GE.EPSEQ.AND.SIGMRX.GE.0.D0.AND.EPSRX.LT.0.D0)
     + THEN
C
C       *********** DOMAINE DES TRACTIONS  ***********
C       ******** POINT INITIALLEMENT COMPRIME **********
C
        PASDC= 'PLAEND23'
        CALL DECHAC(S1X,EDC,EPST,EDT,RTM,EPSC,DEFR,SIGRX,STRNX,
     1  STRNRX,EPSRX,IFISU,JFISU,SIGMRX,IPLA,TANG,PENT,EX,RB,ALPHA,
     2  EPO1,IBB1,IGAU1,IDIR,PASDC)
      RETURN
      ENDIF
C--------------------------------------------------------------------------
      END





