C AVANP     SOURCE    PV        15/04/10    21:15:01     8474
C
C    PLG93Debut
C
       SUBROUTINE AVANP(TAU,SIG,EPSV,VAR,SIG1,EPSV1,VAR1,DSPT,
     &            EPSVPT,VARPT,XMAT,NSTRS,NVARI,IFOUR,NCOMAT)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),EPSV(*),VAR(*),SIG1(*),EPSV1(*),VAR1(*)
       DIMENSION DSPT(*),EPSVPT(*),VARPT(*),XMAT(*)
C ____________________________________________________________________
C
C    THIS ROUTINE UPDATES THE STRAINS ,STRESSES & INTERNAL VARIABLES
C    Version modifiee de la procedure advan1 pour le modele
C    viscoplastique poudre.
C ____________________________________________________________________
C
C    connaissant l'etat c'est a dire SIG, EPSV et VAR ainsi que les
C    derivees EPSVPT et DSPT on calcule l'etat SIG1, EPSV1 et VAR1
C    au temps T + TAU
C ____________________________________________________________________
C
-INC CCREEL

C  STEP-1 : UPDATING STRAINS
C ----------------------------
       YOU = XMAT(1)
       XNU = XMAT(2)
       DO 10 I=1,NSTRS
         EPSV1(I)=EPSV(I)+TAU*EPSVPT(I)
10     CONTINUE
C
C  STEP-2 : UPDATING STESSES
C ----------------------------
       IF(IFOUR.EQ.-2) THEN
         SIG(3)=0.0D0
         X2MU= YOU/(1.0D0+XNU)
         XCO = X2MU/(1.0D0-XNU)
         SIG1(1)=SIG(1)+TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(2)))
         SIG1(2)=SIG(2)+TAU*(DSPT(2)-XCO*(EPSVPT(2)+XNU*EPSVPT(1)))
         SIG1(4)=SIG(4)+TAU*(DSPT(4)-X2MU*EPSVPT(4))
         SIG1(3)=0.0D0
       ELSEIF (IFOUR.EQ.2.OR.IFOUR.EQ.0.OR.IFOUR.EQ.-1.OR.
     &         IFOUR.EQ.1.OR.IFOUR.EQ.-3) THEN
         TRACE=EPSVPT(1)+EPSVPT(2)+EPSVPT(3)
         X2MU = YOU / (1.0D0+XNU)
         ALMB = X2MU*XNU/(1.0D0-2.0D0*XNU)
         DO 20 I=1,NSTRS,1
           A = 0.0D0
           IF (I.LE.3) A=1.0D0
           SIG1(I) = SIG(I) + TAU*(DSPT(I)-ALMB*A*TRACE-X2MU*EPSVPT(I))
20       CONTINUE
C= Modes de calcul 1D contraintes planes suivant z (DYCZ et GYCZ)
       ELSE IF (IFOUR.EQ.4.OR.IFOUR.EQ.8) THEN
         SIG(3) = 0.D0
         XCO = YOU/(1.D0-XNU*XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(2)))
         SIG1(2) = SIG(2) + TAU*(DSPT(2)-XCO*(EPSVPT(2)+XNU*EPSVPT(1)))
         SIG1(3) = 0.D0
C= Modes de calcul 1D contraintes planes suivant y (CYDZ et CYGZ) et
C=                 1D axisymetrique contraintes axiales nulles (AXCZ)
       ELSE IF (IFOUR.EQ.5.OR.IFOUR.EQ.10.OR.IFOUR.EQ.13) THEN
         SIG(2) = 0.D0
         XCO = YOU/(1.D0-XNU*XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(3)))
         SIG1(2) = 0.D0
         SIG1(3) = SIG(3) + TAU*(DSPT(3)-XCO*(EPSVPT(3)+XNU*EPSVPT(1)))
C= Modes de calcul 1D contraintes planes suivant y et z (CYCZ)
       ELSE IF (IFOUR.EQ.6) THEN
         SIG(2) = 0.D0
         SIG(3) = 0.D0
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-YOU*EPSVPT(1))
         SIG1(2) = 0.D0
         SIG1(3) = 0.D0
C= Autres modes de calcul 1D deformations planes (DYDZ GYDZ DYGZ GYGZ)
C=                        1D axisymetrique (AXDZ AXGZ) et 1D spherique
       ELSE IF (IFOUR.EQ. 3.OR.IFOUR.EQ. 7.OR.IFOUR.EQ. 9.OR.IFOUR.EQ.11
     .      .OR.IFOUR.EQ.12.OR.IFOUR.EQ.14.OR.IFOUR.EQ.15) THEN
         X2MU = YOU/(1.D0+XNU)
         TRACE = EPSVPT(1)+EPSVPT(2)+EPSVPT(3)
         XCO = X2MU*XNU*TRACE/(1.D0-XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-X2MU*EPSVPT(1)-XCO)
         SIG1(2) = SIG(2) + TAU*(DSPT(2)-X2MU*EPSVPT(2)-XCO)
         SIG1(3) = SIG(3) + TAU*(DSPT(3)-X2MU*EPSVPT(3)-XCO)
       ENDIF
C
C  STEP-3 : UPDATING THE INTERNAL VARIABLES
C------------------------------------------
       DO 30 I=1,NSTRS
         VAR1(1+I)=EPSV1(I)
30     CONTINUE
C
C      majoration de la densite relative : RHOR=RHOR-TraceEPSV*RHOR
C
       TRE = EPSVPT(1)+EPSVPT(2)+EPSVPT(3)
       taux=-tre*tau
       taux=max(min(log(xgrand),taux),log(xpetit))
       VAR1(NVARI) = VAR(NVARI) * EXP(taux)
       IF (VAR1(NVARI).GT.1D0) VAR1(NVARI) = 1.D0
       IF (VAR1(NVARI).ne.var1(nvari)) VAR1(NVARI) = 1.D0
C
C      majoration de EPSE :
C
       VAR1(1) = VAR(1)+TAU*VARPT(1)
C
C _____________________________________________________________________
       RETURN
       END
C









