C COBAR3    SOURCE    CHAT      05/01/12    22:12:14     5004
      SUBROUTINE COBAR3(X,XELE,TLI)
C
C**********************************************************************
C
C   OBJET  : CE SOUS-PROGRAMME CALCULE LES COORDONNEES BARYCENTRIQUES
C   -----    DANS UN TRI3 AU POINT DONT LES COORDONNEES SONT DANS X
C
C   ARGUMENTS:
C   ---------
C
C   ENTREE  :
C              X      = TABLEAU DES COORDONNEES DU POINT
C              XELE   = TABLEAU DES COORDONNEES DU TRI3
C
C   SORTIE  :
C              TLI    = TABLEAU DES COORDONNEES BARYCENTRIQUES
C                       DU POINT X
C
C      ISSU DE FON302 DE TRIOEF
C         UTILISE PAR L OPERATEUR TRAJ
C***********************************************************************
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION X(2),XELE(2,*),NUDROI(3),NOEDRO(2,3)
C
C
      DIMENSION TLI(*)
C
      DATA NOEDRO/2,3 ,3,1 ,1,2/
C
C
      DO 10 I=1,3
      NOE1=NOEDRO(1,I)
      NOE2=NOEDRO(2,I)
      COEFX =XELE(2,NOE1)-XELE(2,NOE2)
      COEFY=XELE(1,NOE2)-XELE(1,NOE1)
      CONS=XELE(1,NOE1)*XELE(2,NOE2)-XELE(1,NOE2)*XELE(2,NOE1)
      CONSNI=XELE(1,I)*COEFX +XELE(2,I)*COEFY+CONS
      DROITE=X(1)*COEFX +X(2)*COEFY+CONS
      TLI(I)=DROITE/CONSNI
   10 CONTINUE
C
C
C
C
      RETURN
      END

