C NORMA1    SOURCE    PV        18/06/18    21:15:19     9860
      SUBROUTINE NORMA1 (IPCHP1,IPLMOT,MOTCLE,IPCHP2)
************************************************************************
*
*                             N O R M A 1
*                             -----------
*
* FONCTION:
* ---------
*
*     NORMER UN 'CHPOINT' EN RAMENANT SA PLUS GRANDE VALEUR A 1.
*
* MODE D'APPEL:
* -------------
*
*     CALL NORMA1 (IPCHP1,IPLMOT,MOTCLE,IPCHP2)
*
* ARGUMENTS:   (E)=ENTREE   (S)=SORTIE
* ----------
*
*     IPCHP1  ENTIER    (E)  POINTEUR SUR L'OBJET 'CHPOINT' A NORMER.
*     IPLMOT  ENTIER    (E)  VOIR LE S.P. "MAXIM1".
*     MOTCLE  CHARACTER (E)  VOIR LE S.P. "MAXIM1".
*     IPCHP2  ENTIER    (S)  POINTEUR SUR L'OBJET 'CHPOINT' NORME ISSU
*                            DE L'OBJET DE POINTEUR "IPCHP1".
*
* SOUS-PROGRAMMES APPELES:
* ------------------------
*
*     MAXIM1, ERREUR.
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     PASCAL MANIGOT     16 OCTOBRE 1984
*
* LANGAGE:
* --------
*
*     ESOPE + FORTRAN77
*
************************************************************************
*
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC SMCHPOI
*
      SEGMENT/MTEMP2/ (MAXSOU,MAXN,MAXNC)
*     (REMPLI DANS LE S.P. "MAXIM1")
*
      REAL*8 PGRAND
      CHARACTER*(*) MOTCLE
*
*     -- RECHERCHE DU MAXIMUM --
*
      SEGINI,MTEMP2
      IPLACE = MTEMP2
      CALL MAXIM1 (IPCHP1,IPLMOT,MOTCLE,IPLACE,  PGRAND)
      IF (IERR .NE. 0) RETURN
      IF (PGRAND .EQ. 0.D0) THEN
         NUMERR = 150
         CALL ERREUR (NUMERR)
         RETURN
      END IF
*
*     -- NORMALISATION --
*
      MTEMP2 = IPLACE
      SEGACT,MTEMP2
*
      MCHPO1 = IPCHP1
      SEGINI,MCHPOI=MCHPO1
      IPCHP2 = MCHPOI
      NSOUPO = IPCHP(/1)
*
      DO 100 IB100=1,NSOUPO
*
         MSOUP1 = IPCHP(IB100)
         SEGINI,MSOUPO=MSOUP1
         IPCHP(IB100) = MSOUPO
         MPOVA1 = IPOVAL
         SEGACT MPOVA1
         N=MPOVA1.VPOCHA(/1)
         NC=MPOVA1.VPOCHA(/2)
         SEGINI,MPOVAL
         IPOVAL = MPOVAL
*
         DO 120 IB120=1,NC
            DO 130 IB130=1,N
              VPOCHA(IB130,IB120) = MPOVA1.VPOCHA(IB130,IB120) / PGRAND
  130          CONTINUE
*           END DO
  120       CONTINUE
*        END DO
         IF (IB100.EQ.MAXSOU)
     >            VPOCHA(MAXN,MAXNC) = 1.D0
*
         SEGDES,MPOVAL,MSOUPO
*
  100    CONTINUE
*     END DO
*
      SEGDES,MCHPOI
      SEGSUP,MTEMP2
*
      END



