C FRUSA3    SOURCE    KLOCZKO   05/06/14    21:15:11     5111
C FRUSA3    SOURCE    CHAT      05/01/13    00:09:15     5004
      SUBROUTINE FRUSA3(NESP,
     &                  GAMG,ROG,PG,UNG,UTG,UVG,
     &                  GAMD,ROD,PD,UND,UTD,UVD,
     &                  YG,YD,FLU,
     &                  CELLT)
C
C
C PROJET            :  CASTEM 2000
C
C NOM               :  FRUSA3
C
C DESCRIPTION       :  Formulation Volumes Finis  pour les Equations
C                      d'Euler Multi-Especes relatives à un melange
C                      de gaz parfaits.
C
C                      Calcul du flux aux interfaces avec la methode
C                      de Rusanov
C
C LANGUAGE          :  FORTRAN 77
C
C AUTEUR            :  T. KLOCZKO DM2S/SFME/LTMF
C
C************************************************************************
C
C APPELES           :
C
C************************************************************************
C
C**** Entrées:
C
C     NESP            =  nombre d'especes considérées dans les Equations
C                        d'Euler
C
C     GAMG, GAMD      =  les "gamma" du gaz (gauche et droite)
C
C     ROG, ROD        =  les densités
C
C     PG, PD          =  les pressions
C
C     UNG, UND        =  vitesses normales
C
C     UTG, UTD        =  vitesses tangentielles
C
C     UVG, UVD        =  vitesses tangentielles
C
C**** Sorties:
C
C     FLU             =  table du flux a l'interface dans le repaire
C                        (n,t), i.e.
C                        (rho*un, rho*un*un + p, rho*un*ut,  rho*un*uv,
C                         rho*un*ht, rho*un*y1, ...)
C
C     CELLT           =  condition de stabilité, i.e.
C
C                        dT/diamax < cellt
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  Créé le 02.05.05
C
C
C
C************************************************************************
C
C N.B.: Toutes les variables sont DECLAREES
C
C
      IMPLICIT INTEGER(I-N)
      INTEGER NESP,I1
      REAL*8  GAMG,ROG,PG,UNG,UTG,UVG,RETG
     &       ,GAMD,ROD,PD,UND,UTD,UVD,RETD
     &       ,YG(*),YD(*)
     &       ,FLU(*),CELLT, FLUM
     &       ,GAMF,ROF,PF,UNF,CF
     &       ,VPMAX
     &       ,XP(5),FC(5)
C
C*** Calcul des énergies
C
      RETG = PG / (GAMG - 1.0D0)
     &     + 0.5D0 * ROG * (UNG**2 + UTG**2 + UVG**2)
      RETD = PD / (GAMD - 1.0D0)
     &     + 0.5D0 * ROD * (UND**2 + UTD**2 + UVD**2)
C
C*** Calcul des états moyens
C
      UNF  = 0.5D0 * (UNG  + UND)
      ROF  = 0.5D0 * (ROG  + ROD)
      PF   = 0.5D0 * (PG   + PD)
      GAMF = 0.5D0 * (GAMG + GAMD)
      CF    = (GAMF * PF / ROF)**0.5D0
C
C*** Calcul du rayon spectral et de la condition de stabilité
C
      VPMAX = ABS(UNF) + CF
      CELLT = 1.0D0 / VPMAX
C
C*** Calcul des deltas
C
      XP(1) = ROG - ROD
      XP(2) = (ROG * UNG) - (ROD * UND)
      XP(3) = (ROG * UTG) - (ROD * UTD)
      XP(4) = (ROG * UVG) - (ROD * UVD)
      XP(5) = RETG - RETD
C
C*** Calcul du flux convectif
C
      FC(1) = 0.5D0 * ((ROG * UNG)            + (ROD * UND))
      FC(2) = 0.5D0 * ((ROG * UNG * UNG + PG) + (ROD * UND * UND + PD))
      FC(3) = 0.5D0 * ((ROG * UNG * UTG)      + (ROD * UND * UTD))
      FC(4) = 0.5D0 * ((ROG * UNG * UVG)      + (ROD * UND * UVD))
      FC(5) = 0.5D0 * ((UNG * (RETG + PG))    + (UND * (RETD + PD)))
C
C*** Calcul du flux numérique
C
      DO I1 = 1,5
         FLU(I1) = FC(I1) + 0.5D0 * VPMAX * XP(I1)
      ENDDO
C
C*** Partie multi-espèces
C
      FLUM = FLU(1)
      IF(FLUM .GT. 0)THEN
         DO I1 = 1, NESP, 1
            FLU(5+I1) = FLUM * YG(I1)
         ENDDO
      ELSE
         DO I1 = 1, NESP, 1
            FLU(5+I1) = FLUM * YD(I1)
         ENDDO
      ENDIF
C
      RETURN
      END










