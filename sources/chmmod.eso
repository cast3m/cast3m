C CHMMOD    SOURCE    CHAT      05/01/12    21:59:32     5004
      SUBROUTINE CHMMOD(IDSCHI,XMU,XMUNEW,TMPNEW,GNEW)
C=======================================================================
C            ISSU DE TRIOEF(TRIMOD)
C
C - OBJET: MODIFICATION DES LOGK POUR UNE FORCE IONIQUE DONNEE (DE XMU A
C XMUNEW) A TEMPERATURE FIXEE TMP.
C
C
C - APPELLEE PAR CHMKMD()
C
C - METHODES: LE COEFFICIENT GFO POUR UN ION UNIVALENT EST CELUI DE
C L'APPROXIMATION DE DAVIES PRENANT EN COMPTE LA TEMPERATURE; ON UTILISE
C UNE DEPENDANCE LINEAIRE EN TEMPERATURE POUR LA CONSTANTE DIELECTRIQUE
C DE L'EAU
C
C LES ESPECES DE TYPE III SONT PRISES EN COMPTE DE TELLE SORTE QUE LEUR
C ACTIVITE INITIALE SOIT MAINTENUE.
C
C======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
      SEGMENT IDSCHI
           REAL*8 GK(NYDIM),AA(NYDIM,NXDIM),FF(NZDIM,NPDIM)
           INTEGER IDX(NXDIM),IDY(NYDIM),IDZ(NZDIM),IDP(NPDIM),NN(6)
           INTEGER IDECY(NYDIM),IONZ(NXDIM)
           CHARACTER*32 NAME(NXDIM),NAMESP(NYDIM)
      ENDSEGMENT


C     write(6,*)' XMUNEW    XMU ',XMUNEW,XMU
      IF (XMUNEW.EQ.XMU) RETURN
C
      NC=NN(1)+NN(2)+NN(3)+NN(4)+NN(5)+NN(6)
      I1=NN(1)+NN(2)+1
      I2=NN(1)+NN(2)+NN(3)
      NX=IDX(/1)
C
C
        T1=TMPNEW+273.15D0
      IF(TMPNEW.LE.100.D0) THEN
        TE=TMPNEW-25.D0
        ET=78.54D0*(1.D0-4.579D-3*TE+1.19D-5*TE*TE
     *  -2.8D-8*TE*TE*TE)
      ELSEIF((TMPNEW.GT.100.D0).AND.(TMPNEW.LE.370.D0)) THEN
        ET=5321.D0/T1+233.76D0-0.9297D0*T1+1.417D-3*T1*T1
     *     -8.292D-7*T1*T1*T1
      ENDIF
C
C
C
      AT=1.82D6/SQRT((ET*T1)**3)
      SI=SQRT(XMU)
      GOLD=-AT*(SI/(1.D0+SI)-0.2D0*XMU)
      SI=SQRT(XMUNEW)
      GNEW=-AT*(SI/(1.D0+SI)-0.2D0*XMUNEW)
      IF (ABS(GNEW).EQ.0.D0) GNEW=0.D0
      DGF=GNEW-GOLD
C      WRITE(6,*)'  '
C      WRITE(6,*)' CHMMOD ', ' NC ',NC,' NX ',NX
C      WRITE(6,*)' ET ',ET,' T1 ',T1,' AT ',AT,' SI ',SI
C      WRITE(6,*)' DGF ',DGF,' GNEW ',GNEW
      DO 20 I=1,NC
         VJ=0.D0
         VI=0.D0
         DO 30 J=1,NX
            VJ=VJ+AA(I,J)*IONZ( J)*IONZ( J)
            VI=VI+AA(I,J)*IONZ( J)
30       CONTINUE
         GKT=GK(I)+DGF*(VJ-VI*VI)
C        write(6,*)'idy',idy(i),'gkt',gkt,gk(i),vi,vj,i,idecy(i)
         IF (I.GE.I1.AND.I.LE.I2) GKT=GK(I)+DGF*VJ
         IF ( IDECY(I)   .EQ. 1 ) GKT=GK(I)+DGF*VJ
         GK(I)=GKT
20    CONTINUE
      XMU=XMUNEW
      RETURN
      END
C
* LE COEFFICIENT D'ACTIVITE D'UN COMPLEXE DE SURFACE EST CONSTANT
* ET EGAL A 1/CONCENTRATION DE SITES DE SURFACE
C
C
C
C





