C ELASF1    SOURCE    AM        09/12/08    21:15:12     6582
      SUBROUTINE ELASF1(DEP,AN,BN,CN,DEPST,SIGEF,
     $                   NSTRS,DSIGT,KERRE)
C
C ROUTINE APPELE DANS CAMECA
C_______________________________________________________________________
C
C SOUS PROGRAMME QUI CALCUL LES CONTRAINTES DE TEST FINALES
C  ELASTIQUES CORRIGE PAR LE FLUAGE TOTAL
C_______________________________________________________________________
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION AN(NSTRS),BN(NSTRS,NSTRS),CN(NSTRS,NSTRS)
      DIMENSION DEP(NSTRS,NSTRS),SIGEF(NSTRS),DSIGT(NSTRS)
      DIMENSION DEPST(NSTRS)
*
      DIMENSION EFLU(10,10),TEMP(10,10)
      DIMENSION DEFLUN(10),DEFELA(10)

C
C INITIALISATION DES VARIABLES
C
      NDI = 10
      CALL ZERO(DSIGT,NSTRS,1)
      CALL ZERO(EFLU,NDI,NSTRS)
      CALL ZERO(TEMP,NDI,NSTRS)
      CALL ZERO(DEFELA,NSTRS,1)
      CALL ZERO(DEFLUN,NSTRS,1)
C
      DO 5 I=1,NSTRS
        DO 5 J=1,NSTRS
          DO 5 K=1,NSTRS
            TEMP(I,J) = TEMP(I,J) + DEP(I,K) * CN(K,J)
  5   CONTINUE
C
C CONSTRUCTION DE LA MATRICE D ELASTICITE CORRIGE PAR LE FLUAGE
C
C  EQUATION : (2.4-1)
C
        DO 10 I=1,NSTRS
          TEMP(I,I) = 1.D0 + TEMP(I,I)
  10    CONTINUE
C
        CALL INVMA1(TEMP,NDI,NSTRS,ISING)
        IF (ISING.EQ.1) THEN
          WRITE(*,*) ' '
          WRITE(*,*) 'ERREUR DANS LE CALCUL DE LA MATRICE DE HOOKE'
          WRITE(*,*) 'CORRIGE PAR LE FLUAGE DANS ELASF1'
          WRITE(*,*) 'MATRICE NON INVERSIBLE'
          WRITE(*,*) ' '
          KERRE=99
          RETURN
        ENDIF
C
C CALCUL DE (1 + CN)-1 * E0
C
        DO 20 I=1,NSTRS
          DO 20 J=1,NSTRS
            DO 20 K=1,NSTRS
              EFLU(I,J) = EFLU(I,J) + TEMP(I,K)*DEP(K,J)
  20    CONTINUE
C
C        DO 30 I=1,NSTRS
C          DO 30 J=1,NSTRS
C            DEP(I,J) = EFLU(I,J)
C  30    CONTINUE
C
C CALCUL DES DEFORMATIONS DE FLUAGE PUIS ELASTIQUES
C
      DO 40 I=1,NSTRS
        DEFLUN(I) = DEFLUN(I) + AN(I)
        DO 40 J=1,NSTRS
          DEFLUN(I) = DEFLUN(I) + (BN(I,J)+CN(I,J)) * SIGEF(J)
  40  CONTINUE
C
      DO 50 I=1,NSTRS
        DEFELA(I) = DEPST(I) - DEFLUN(I)
  50  CONTINUE
C
C CALCUL DE L INCREMENT DE CONTRAINTES ELASTIQUES
C
C  EQUATION (2.4-2)
C
      DO 60 I=1,NSTRS
        DO 60 J=1,NSTRS
          DSIGT(I) = DSIGT(I) + EFLU(I,J) * DEFELA(J)
  60  CONTINUE
C
      END


