C INTGRA    SOURCE    CB215821  18/09/21    21:16:02     9930

C---------------------------------------------------------------------
C
C                         OPERATEUR INTG
C
C---------------------------------------------------------------------
C
C  SYNTAXE 1 : RESU1 = INTG ('ELEM') MODE1 CHAM1 ( MOT2 ) ( CHAM2 );
C
C  Integration d'une composante d'un MCHAML sur le domaine ou elle est
C  definie. Le resultat est soit global, soit local (i.e. sur chaque
C  element, option ELEM).
C
C  ELEM  : Option precisant que le resultat est donne elt par elt (MOT)
C  MODE1 : Modele de calcul (MMODEL)
C  CHAM1 : Champ par element (MCHAML)
C  MOT2  : Nom de la composante a integrer (MOT, facultatif si 1 comp.)
C  CHAM2 : Champ par element de CARACTERISTIQUE (MCHAML, facultatif)
C  RESU1 : FLOTTANT si l'option 'ELEM' n'est pas precisee,
C         Champ par element (objet MCHAML) sinon.
C
C---------------------------------------------------------------------
C
C  SYNTAXE 2 : RESU1 = INTG EVOL1 ('ABS') (DEBU FIN);
C
C  Integration d'une evolution composee d'1 ou plusieurs courbes
C  par la methode des trapezes (anciennement realise par SOMM)
C
C  EVOL1 : Evolution en entree
C  ABS   : mot cle pour integrer la valeur absolue de la fonction
C  DEBU/FIN : bornes d'integration
C             si ENTIER ou LISTENTI, indices de debut/fin
C             si REEL ou LISTREEL, abscisses de debut/fin
C
C---------------------------------------------------------------------

      SUBROUTINE INTGRA

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO
-INC SMCHAML

      CHARACTER*4 MACOMP,MOT1
      CHARACTER*8 CHBOR
      LOGICAL     BDARCY
      INTEGER IDARC


C---------------------------------------------------------------------
C     SYNTAXE 1 (par defaut)
C---------------------------------------------------------------------
      IDARC  = 0
      IELEM  = 0
      IRET   = 0
      IPIN   = 0
      IPMODL = 0
      IPCHB1 = 0
      IPCHE2 = 0
      IR     = 0
      KERRE  = 0
      IRT3   = 0
      IPCHE1 = 0
      IPINT  = 0
      XRET   =REAL(0.D0)
      MOT1   ='    '

C- Lecture eventuelle du mot cle 'ELEM'
      CALL LIRMOT('ELEM',1,IELEM,0)
      IF (IERR.NE.0) RETURN

C- Lecture du modele (facultative pour permettre la syntaxe 2)
      CALL LIROBJ('MMODEL',IPMODL,0,IRET)
      IF (IERR.NE.0) RETURN
C---- ABSENCE DU MODELE => ON VA TESTER LA SYNTAXE 2 (INTG evol1 ...)
      IF(IRET.EQ.0) GOTO 100
      CALL ACTOBJ('MMODEL  ',IPMODL,1)

C- Lecture du MCHAML
      CALL LIROBJ('MCHAML',IPIN,1,IRET)
      IF (IERR.NE.0) RETURN

      CALL ACTOBJ('MCHAML  ',IPIN,1)
      CALL REDUAF(IPIN,IPMODL,IPCHB1,0,IR,KERRE)
      IF(IR.NE.1) CALL ERREUR(KERRE)
      IF (IERR.NE.0) RETURN

C- Lecture eventuelle du nom de la composante du MCHAML a considerer
      CALL LIRCHA(MACOMP,0,IRT3)
      IF (IERR.NE.0) RETURN

C- Lecture facultative du MCHAML de CARACTERISTIQUES
      CALL LIROBJ('MCHAML',IPIN,0,IRET)
      IPCHE2=IPIN
      IF((IRET .EQ.1) .AND. (IDARC .EQ. 0))THEN
        CALL ACTOBJ('MCHAML  ',IPIN,1)
        CALL REDUAF(IPIN,IPMODL,IPCHE2,0,IR,KERRE)
        IF(IR.NE.1) CALL ERREUR(KERRE)
        IF (IERR.NE.0) RETURN
      ENDIF
      IF (IERR.NE.0) RETURN

C- Dans le cas ou le MCHAML a plus d'une composante
C- on impose la donnee du nom de la composante a traiter.
      IF (IRT3.EQ.0) THEN
        MCHELM=IPCHB1
        SEGACT,MCHELM
        N1=ICHAML(/1)
        DO i=1,N1
          MCHAML=ICHAML(I)
          SEGACT,MCHAML
          N2=IELVAL(/1)
          SEGDES,MCHAML
          IF (N2.GT.1) THEN
            MOTERR(1:8)=' MCHAML '
            INTERR(1)=N2
            CALL ERREUR(761)
            SEGDES,MCHELM
            RETURN
          ENDIF
        ENDDO
        SEGDES,MCHELM
      ENDIF

C- Dans le cas ou le nom de la composante du MCHAML
C- a considerer est precise, on l'extrait du MCHAML.
      IPCHE1=IPCHB1
      IF (IRT3.NE.0) THEN
        MOT1='SCAL'
        CALL EXCOC1(IPCHB1,MACOMP,IPCHE1,MOT1,0)
        IF (IERR.NE.0) RETURN
      ENDIF

C- Calcul de l'integrale
      CALL INTGCA(IPMODL,IPCHE1,IPCHE2,IELEM,IRET,XRET,IPINT)
      IF (IERR.NE.0) RETURN

C- Ecriture du resultat et menage
      IF (IPINT.EQ.0) THEN
        CALL ECRREE(XRET)
      ELSE
        CALL ECROBJ('MCHAML',IPINT)
      ENDIF

      RETURN


C---------------------------------------------------------------------
C     SYNTAXE 2
C---------------------------------------------------------------------
 100  CONTINUE

C---  INITIALISATIONS ---
      IABSO=0
      IA=0
      IB=0
      XA=REAL(0.D0)
      XB=REAL(0.D0)
      ILENTA=0
      ILENTB=0
      CHBOR(1:8)='        '
      IPINT=0
      XINT=REAL(0.D0)

C---  LECTURE DES OBJETS EN ENTREE  ---

C- Lecture eventuelle du mot cle 'ABS'
      CALL LIRMOT('ABS',1,IABSO,0)
      IF (IERR.NE.0) RETURN

C- Lecture de l'evolution
      CALL LIROBJ('EVOLUTIO',IPEVO,0,IRET)
      IF (IERR.NE.0) RETURN
c---- ABSENCE DE MODELE ET D'EVOLUTION => ERREUR
      IF(IRET.EQ.0) GOTO 998

C- Lecture eventuelle des bornes d'integration (de meme type)
C   - de type ENTIER pour les indices
      CALL LIRENT(IA,0,IRETA)
      IF(IRETA.NE.0) THEN
        CALL LIRENT(IB,1,IRETB)
        CHBOR(1:6)='ENTIER'
        GOTO 101
      ENDIF
C   - de type FLOTTANT pour les valeurs d'abscisses
      CALL LIRREE(XA,0,IRETA)
      IF(IRETA.NE.0) THEN
        CALL LIRREE(XB,1,IRETB)
        CHBOR(1:8)='FLOTTANT'
        GOTO 101
      ENDIF
C   - de type LISTENTI pour une liste d'indices
      CALL LIROBJ('LISTENTI',ILENTA,0,IRETA)
      IF(IRETA.NE.0) THEN
        CALL LIROBJ('LISTENTI',ILENTB,1,IRETB)
        CHBOR(1:8)='LISTENTI'
        GOTO 101
      ENDIF
C   - de type LISTENTI pour une liste d'indices
      CALL LIROBJ('LISTREEL',ILENTA,0,IRETA)
      IF(IRETA.NE.0) THEN
        CALL LIROBJ('LISTREEL',ILENTB,1,IRETB)
        CHBOR(1:8)='LISTREEL'
        GOTO 101
      ENDIF


C---  CALCUL DE L'INTEGRALE ---

 101  CONTINUE
      CALL INTGEV(IPEVO,IABSO,CHBOR,IA,IB,XA,XB,ILENTA,ILENTB,
     &            XINT,IPINT)
      IF (IERR.NE.0) RETURN

C---  ECRITURE DU RESULTAT ET MENAGE  ---

      IF (IPINT.EQ.0) THEN
        CALL ECRREE(XINT)
      ELSE
        CALL ECROBJ('LISTREEL',IPINT)
      ENDIF
      RETURN


C---------------------------------------------------------------------
C     SYNTAXE 3 et ...
C---------------------------------------------------------------------
C     ...


C---------------------------------------------------------------------
C     ERREURS
C---------------------------------------------------------------------

* /!\ ERREUR : AUCUN OBJET COMPATIBLE TROUVE
 998  MOTERR(1:16)='MODELE  EVOLUTIO'
      CALL ERREUR(471)
      RETURN

      END






