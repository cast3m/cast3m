C INCRE2    SOURCE    CB215821  16/04/21    21:17:09     8920
       SUBROUTINE INCRE2(SIG,VAR,EPSVPT,VARPT,XMAT,NSTRS0,
     &                                     MFR,NVARI,NCOMAT)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),VAR(*),EPSVPT(*),VARPT(*),XMAT(*)
       DIMENSION XX(6),XT1(6),XT2(6),AN(6),ANS(6),YY(6)
       DIMENSION SIG0(6),EPS0(6)
       PARAMETER (AMAX = 1.0D20 , AMIN = 1.D-10)
       DETIER = 2.0D0/3.0D0
       ROOT = SQRT(DETIER)
C-------------------------------------------------------------------|
C*******  EVALUATION OF J2 SIGMA - X                                |
C-------------------------------------------------------------------I
       IF (MFR.EQ.5) THEN
         NSTRS=6
         SIG0(1)=SIG(1)
         SIG0(2)=SIG(2)
         SIG0(3)=0.D0
         SIG0(4)=SIG(3)
         SIG0(5)=SIG(4)
         SIG0(6)=SIG(5)
       ELSE
         NSTRS=NSTRS0
         DO 10 I=1,NSTRS
          SIG0(I)=SIG(I)
  10     CONTINUE
       ENDIF
       TRACE =(SIG0(1)+SIG0(2)+SIG0(3))/3.0D0
       DO 70 I=1,NSTRS
       A = 0.0D0
       IF (I.LE.3) A=1.0D0
       XX(I) = SIG0(I)-A*TRACE - VAR(I)-VAR(NSTRS+I)
  70   CONTINUE
       AJ2 = PROCON (XX,XX,NSTRS)
       AJ2 = SQRT(1.5D0*AJ2)
C--------------------------------------------------------------------|
C*******  CALCUL OF SIGV/K                                           |
C -------------------------------------------------------------------I
       RR = VAR (4*NSTRS+2)
       RS = XMAT(10)*RR
       SK = (AJ2 - RS-XMAT(7))/( XMAT(8) + XMAT(9)*RR )
C--------------------------------------------------------------------|
C*******  CALCULATION OF EFFECTIVE INELASTIC STRAIN INCREMENT (P)    |
C -------------------------------------------------------------------I
       IF (SK.GT.0.0D0) THEN
       PPT1= XPUISS(SK,XMAT(6)  ,AMAX)
       PPT2= XPUISS(SK,XMAT(6)+1.D0,AMAX)
       ELSE
       PPT1= 0.0D0
       PPT2= 0.0D0
       ENDIF
       T1  = XMAT(5)*PPT2
       PPT2=1.0D20
       IF(ABS(T1).LT.40.0D0) PPT2= EXP(T1)
       PPT = PPT1 * PPT2
       VARPT(4*NSTRS+1) = PPT
C---------------------------------------------------------------------|
C*******  CALCULATION OF INELASTIC STRAIN INCREMENTS  (EPSVPT)        |
C---------------------------------------------------------------------I
       DO 71 I=1,NSTRS,1
       IF (PPT.EQ.0.0) THEN
       EPS0  (I) = 0.0D0
       XX    (I) = 0.0D0
       VARPT(3*NSTRS+I)= 0.0D0
       ELSE
       XX(I)     = 1.5D0*XX(I)/AJ2
       EPS0  (I) = XX(I)*PPT
       VARPT(3*NSTRS+I)=EPS0  (I)
       ENDIF
 71    CONTINUE
C
       P      =  VAR(4*NSTRS+1)
C----------------------------------------------------------------|
C ******* CALCULATION OF PI(P)                                   |
C----------------------------------------------------------------I
       T1 = XMAT(16)*P
       T2 = EXP(-T1)
       PIP= XMAT(13)+(1.0D0-XMAT(13)) * T2
C----------------------------------------------------------------|
C*******  CALCULATION OF XII                                     |
C----------------------------------------------------------------I
       DO 72 I=1,NSTRS
       XT1(I)=VAR(I)
       XT2(I)=VAR(I+NSTRS)
  72   CONTINUE
       X1II =  0.0D0
       X2II =  0.0D0
       IF(XMAT(17).NE.0.0D0)
     . X1II =  SQRT(1.5D0*PROCON(XT1,XT1,NSTRS))/XMAT(17)
       IF(XMAT(18).NE.0.0D0)
     . X2II =  SQRT(1.5D0*PROCON(XT2,XT2,NSTRS))/XMAT(18)
       COX1 =  0.0D0
       COX2 =  0.0D0
       IF(X1II.GT.0.0D0) COX1 =  X1II**(XMAT(19)-1.0D0)
       IF(X2II.GT.0.0D0) COX2 =  X2II**(XMAT(20)-1.0D0)
C ----------------------------------------------------------------|
C*******  CALCULATION OF DX1 AND DX2                              |
C-----------------------------------------------------------------I
       DO 73 I=1,NSTRS
       VARPT(      I)=0.0D0
  73   VARPT(NSTRS+I)=0.0D0
       DO 74 I=1,NSTRS
       T1  =  DETIER  * XMAT(11) * XMAT(12) *  EPS0(I)
       T2  =  XMAT(11) * PIP *  XT1(I) * PPT
       T3  =  COX1 *  XT1(I)
       VARPT(I)= T1 - T2 -T3
       T1  =  DETIER * XMAT(14) * XMAT(15) *  EPS0(I)
       T2  =  XMAT(14) * PIP *  XT2(I) * PPT
       T3  =  COX2 *  XT2(I)
  74   VARPT(NSTRS+I)= T1 - T2 - T3
C-----------------------------------------------------------------|
C*******  CALCULATION OF DR                                       |
C-----------------------------------------------------------------I
       T1 = 1.0D0-(1.0D0-VAR(4*NSTRS+3)/XMAT(25))**2
       QR = VAR(4*NSTRS+3)-XMAT(26)*T1
       CO1= QR-VAR(4*NSTRS+2)
       CO = ABS(CO1)
       IF(CO.GT.0.0D0) CO = CO**(XMAT(22)-1.0D0)
       T1= XMAT(16)*(VAR(4*NSTRS+3)-VAR(4*NSTRS+2))*PPT
       T2= XMAT(21)*CO*CO1
       DR= T1 + T2
       VARPT(4*NSTRS+2)=DR
C----------------------------------------------------------------|
C*******  CALCULATION OF PROD (N X N*)                           |
C----------------------------------------------------------------I
       DO 75 I=1,NSTRS
*       A=1.0D0
*       IF(I.GT.3) A = 2.0D0
       YY(I)=VAR(3*NSTRS+I) - VAR(2*NSTRS+I)
 75    CONTINUE
       AJ2=  SQRT(1.5D0*PROCON(YY,YY,NSTRS))
       IF(AJ2.LT.AMIN) THEN
         IF (AJ2.EQ.0.D0) AJ2=AMIN
         AJ20=AJ2*1.D20
         DO 15 I=1,NSTRS
         YY(I)=YY(I)*1.D20
 15      CONTINUE
       ELSE
         AJ20=AJ2
       ENDIF
*       IF (AJ2.LT.AMIN) AJ20=AMIN
       DO 76 I= 1,NSTRS
       ANS(I) = SQRT(1.5D0)*YY(I)/AJ20
       AN (I) = ROOT*XX(I)
 76    CONTINUE
       FF = DETIER*AJ2-VAR(4*NSTRS+4)
       HF=0.0D0
       IF(FF.GT.0.0D0) HF=1.0D0
       ANAN1=PROCON(AN,ANS,NSTRS)
       ANANS=0.0D0
       IF(ANAN1.GT.0.0) ANANS=ANAN1
C----------------------------------------------------------------|
C*******  CALCULATION OF DQ                                      |
C----------------------------------------------------------------I
       DQ = XMAT(23)*HF*ANANS*PPT
       VARPT(4*NSTRS+4)=DQ
C----------------------------------------------------------------|
C*******  CALCULATION OF DGETA                                   |
C----------------------------------------------------------------I
       T1  = SQRT(1.5D0)*(1.0D0-XMAT(23))*HF*ANANS*PPT
       DO 78 I=1,NSTRS
       VARPT(2*NSTRS+I)= T1*ANS(I)
  78   CONTINUE
C----------------------------------------------------------------|
C*******  CALCULATION OF DQQ                                     |
C----------------------------------------------------------------I
       VARPT(4*NSTRS+3)=2.0D0*XMAT(24)*(XMAT(25)-VAR(4*NSTRS+3))*DQ
C
       IF (MFR.EQ.5) THEN
         EPSVPT(1)=EPS0(1)
         EPSVPT(2)=EPS0(2)
         EPSVPT(3)=EPS0(4)
         EPSVPT(4)=EPS0(5)
         EPSVPT(5)=EPS0(6)
       ELSE
         DO 11 I=1,NSTRS
          EPSVPT(I)=EPS0(I)
  11     CONTINUE
       ENDIF
C
       RETURN
       END





