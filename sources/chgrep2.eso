C CHGREP2   SOURCE    CHAT      05/01/12    21:56:48     5004
      SUBROUTINE CHGREP2(COSA,SINA,XSTRS,ISENS,IND)
*---------------------------------------------------------------------*
*                                                                     *
*  PASSAGE DU REPERE D'ORTHOTROPIE AU REPERE LOCAL DES ELEMENTS       *
* (OU LE CONTRAIRE) POUR LES DEFORMATIONS OU LES CONTRAINTES DANS     *
*  LE CAS DE L'ELEMENT DST                                            *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   ENTREES :                                                         *
*   ________                                                          *
*                                                                     *
*        COSA, SINA = A angle de rotation entre le repère local et le *
*                     repère d'orthotropie                            *
*        XSTRS      = tableau de contraintes ou de déformations       *
*        ISENS      = 0 ->  passage rep. ortho.-> rep. local          *
*                     1 ->  passage rep. local -> rep. ortho.
*        IND        = 0 ->  tableau de déformations                   *
*                     1 ->  tableau de contraintes                    *
*                                                                     *
*   SORTIES :                                                         *
*   ________                                                          *
*                                                                     *
*        XSTRS      = tableau de contraintes ou de déformations après *
*                     le changement de repère                         *
*                                                                     *
*---------------------------------------------------------------------*
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*
      DIMENSION TRAV(3,3),A(3,3),R(3,3),RT(3,3)
      DIMENSION XSTRS(*)
*
*              Matrice de passage entre le repere local de la coque
*              et le repere d'orthotropie
*
               CALL ZERO(R,3,3)
               CALL ZERO(RT,3,3)
               R(1,1)=COSA
               IF (ISENS.EQ.0) THEN
                  R(2,1)=-SINA
               ELSE
                  R(2,1)=SINA
               ENDIF
               R(1,2)=-R(2,1)
               R(2,2)= R(1,1)
               R(3,3)= 1.D0
               SIGFLX= 1.D0
               CALL TRSPOD(R,3,3,RT)
*
*           Rotation du tenseur pour les termes de membrane
*
               CALL ZERO(A,3,3)
               CALL ZERO(TRAV,3,3)
               A(1,1) = XSTRS(1)
               A(2,2) = XSTRS(2)
               A(1,2) = XSTRS(3)
               A(1,3) = XSTRS(7)
               A(2,3) = XSTRS(8)
               IF (IND.EQ.0) THEN
                 A(1,2) = A(1,2)/2.D0
                 A(1,3) = A(1,3)/2.D0
                 A(2,3) = A(2,3)/2.D0
               ENDIF
               AUX4=XSTRS(4)
               AUX5=XSTRS(5)
               AUX6=XSTRS(6)
               A(2,1)=A(1,2)
               A(3,1)=A(1,3)
               A(3,2)=A(2,3)
*                                              t
*              >>> Changement de repere : A = R  A  R  <<<
*
               CALL MULMAT(TRAV,A,R,3,3,3)
               CALL MULMAT(A,RT,TRAV,3,3,3)
               IF (IND.EQ.0) THEN
                 A(1,2) = A(1,2)*2.D0
                 A(1,3) = A(1,3)*2.D0
                 A(2,3) = A(2,3)*2.D0
               ENDIF
               XSTRS(1)=A(1,1)
               XSTRS(2)=A(2,2)
               XSTRS(3)=A(1,2)
               XSTRS(7)=A(1,3)
               XSTRS(8)=A(2,3)
*
*              Rotation du tenseur pour les termes de flexion
*

               CALL ZERO(A,3,3)
               CALL ZERO(TRAV,3,3)
               A(1,1)=AUX4
               A(2,2)=AUX5
               A(1,2)=AUX6
               IF (IND.EQ.0)    A(1,2) = A(1,2)/2.D0
               A(2,1)=A(1,2)
*                                              t
*              >>> Changement de repere : A = R  A  R  <<<
*
               CALL MULMAT(TRAV,A,R,3,3,3)
               CALL MULMAT(A,RT,TRAV,3,3,3)
               IF (IND.EQ.0)    A(1,2) = A(1,2)*2.D0
               XSTRS(4)= A(1,1)
               XSTRS(5)= A(2,2)
               XSTRS(6)= A(1,2)
*
      RETURN
      END







