C ELPDDR    SOURCE    CHAT      05/01/12    23:36:21     5004
      SUBROUTINE ELPDDR(CG,R,XR,XA,XB,XC,XDD,IDER,CVA)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-B,D-H,O-Z)
      IMPLICIT COMPLEX*16(C)
************************************************************************
*
*    DERIVATION D'UNE FONCTION complexe DANS 1,2,3,4   DIRECTION FIXES
*
*       CG(1 A  5) CF(R) ET SES 4 PREMIERES DERIVEES
*       XR LA DIRECTION UNITAIRE DE R
*       XA LA DIRECTION UNITAIRE DE DERIVATION 1
*       XB LA DIRECTION UNITAIRE DE DERIVATION 2
*       XC LA DIRECTION UNITAIRE DE DERIVATION 3
*       XDD LA DIRECTION UNITAIRE DE DERIVATION 4
*
*
*
*    F,AB= A.RO B.RO CF21 + A.B CF22
*
*
*
*     F,ABC= A.RO B.RO C.R0 CF31 +( A.B C.RO + A.C B.RO + B.C A.RO)CF32
*
*         AVEC CF31 = V''' - 3* V'' / R + 3*V' / R**2
*              CF32 =           V'' / R -   V' / R**2
*
*
*        F,ABCD= A.RO B.RO C.R0  DRO CF41
*              +( AD BRO CRO  +  5 PERMUTATIONS ABCD )CF42
*              +( AC BD       +  2 PERMUTATIONS ABCD )CF43
*
*         AVEC CF41 = V'''' - 6* V''' / R + 15*V''/ R**2-15 V' / R
*              CF42 = CF31 / R
*              CF43 = CF32 / R
*
*               RO VECTEUR  XR
************************************************************************
          DIMENSION CG (5)
          DIMENSION XR (2)
          DIMENSION XA (2)
          DIMENSION XB (2)
          DIMENSION XC (2)
          DIMENSION XDD (2)
*
       IF (IDER .EQ. 1 ) THEN
         CF1 = CG(2)
         AR = XR(1)*XA(1) +XR(2)*XA(2)
         CVA = AR * CF1
       ELSEIF( IDER .EQ. 2 ) THEN
         CF21= CG(3) - CG(2) / R
         CF22= CG(2)/R
         AR = XR(1)*XA(1) +XR(2)*XA(2)
         BR = XR(1)*XB(1) +XR(2)*XB(2)
         AB = XB(1)*XA(1) +XB(2)*XA(2)
         CVA = AR *BR* CF21+ AB*CF22
       ELSEIF( IDER .EQ. 3 ) THEN
         CF31= CG(4) - (3 * CG(3) / R) + (3 * CG(2) / (R*R))
         CF32=              CG(3) / R  - (  CG(2) / (R*R))
         AR = XR(1)*XA(1) +XR(2)*XA(2)
         BR = XR(1)*XB(1) +XR(2)*XB(2)
         CR = XR(1)*XC(1) +XR(2)*XC(2)
         AB = XA(1)*XB(1) +XA(2)*XB(2)
         AC = XA(1)*XC(1) +XA(2)*XC(2)
         BC = XB(1)*XC(1) +XB(2)*XC(2)
         CVA = (AR *BR* CR)            * CF31 +
     &         (AC*BR + BC*AR + AB*CR) * CF32
       ELSE
         CF41= CG(5)  - (6 * CG(4) / R) + (15* CG(3) / (R*R))
     &                               - (15* CG(2) / (R*R*R))
         CF31= CG(4)  - (3 * CG(3) / R) + (3 * CG(2) / (R*R))
         CF42 = CF31 / R
         CF32=              CG(3) / R  - (  CG(2) / (R*R))
         CF43 = CF32 / R

         AR = XR(1)*XA(1) +XR(2)*XA(2)
         BR = XR(1)*XB(1) +XR(2)*XB(2)
         CR = XR(1)*XC(1) +XR(2)*XC(2)
         DR = XR(1)*XDD(1) +XR(2)*XDD(2)

         AB = XA(1)*XB(1) +XA(2)*XB(2)
         AC = XA(1)*XC(1) +XA(2)*XC(2)
         AD = XA(1)*XDD(1) +XA(2)*XDD(2)
         BC = XB(1)*XC(1) +XB(2)*XC(2)
         BD = XB(1)*XDD(1) +XB(2)*XDD(2)
         CD = XC(1)*XDD(1) +XC(2)*XDD(2)
*
         CVA = (AR *BR* CR * DR)                 * CF41 +
     &         (AB*CR*DR + AC*BR*DR + AD*BR*CR
     &        + BC*AR*DR + BD*AR*CR + CD*AR*BR ) * CF42 +
     &         (AC*BD    + BC*AD    + AB*CD    ) * CF43
       ENDIF

       RETURN
      END

