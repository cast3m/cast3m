C DYCHE3    SOURCE    CHAT      05/01/12    23:04:09     5004
      SUBROUTINE DYCHE3(XDEP,IDIM,IGP,XPALB,NLIAB,I,INTER
     &                 ,XFN,XFT,XPUS,iannul)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Op{rateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul de la force de choc pour un choc {l{mentaire.           *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* e   XDEP    Valeur du d{placement sur la normale                   *
* e   IGP     Indice de glissement au pas pr{c{dent                  *
* es  XPALB   Tableau contenant les param}tres de la liaison         *
* e   NLIAB   Nombre de liaison sur la base B                        *
* e   I       Num{ro de la liaison                                   *
* e   INTER   Indice de liaison interieure / exterieure              *
*  s  XFN     Valeur de la force de choc suivant la normale.         *
*  s  XFT     Module de la force de choc projetee sur le plan tangent*
*  s  XPUS    Valeur de la puissance d'usure instantanee             *
*                                                                    *
*                                                                    *
*     Auteur, date de cr{ation:                                      *
*                                                                    *
*     Bertrand BEAUFILS, le 30 mai 1990.                             *
*                                                                    *
*--------------------------------------------------------------------*
      DIMENSION XPALB(NLIAB,*)
      XFN = 0.D0
      XFT = 0.D0
      XPUS = 0.D0
      XJEU  = XPALB(I,2)
      XAD = ABS(XDEP)
      XAJ = ABS(XJEU)
*
      IF ( (((XAD.LE.XAJ).AND.(INTER.EQ.0)).OR.
     &      ((XAD.GE.XAJ).AND.(INTER.EQ.1))).AND.
     &      (iannul.EQ.0)) THEN
         ID4 = 6 + 3*IDIM
         ID5 = 6 + 4*IDIM
         ID6 = 6 + 5*IDIM
         ID8 = 6 + 7*IDIM
         ID9 = 6 + 8*IDIM
         XRAIN = XPALB(I,1)
         XGLIS = XPALB(I,3)
         XADHE = XPALB(I,4)
         XRAIT = XPALB(I,5)
         XAMOT = XPALB(I,6)
         XVAL = XDEP - XJEU
         XFN = -XRAIN * XVAL
         CALL DYCHFR(IDIM,IGP,XPALB,NLIAB,I,ID4,ID5,ID6,ID8,ID9,XGLIS,
     &               XADHE,XRAIT,XAMOT,XFN,XFT,XPUS)
      ELSE
         ID6 = 6 + 5*IDIM
         IGP = -1
         DO 10 ID=1,IDIM
            XPALB(I,ID6+ID) = 0.D0
 10         CONTINUE
*        end do
      ENDIF
*
      RETURN
      END



