C CHREP2    SOURCE    CHAT      05/01/12    22:01:50     5004
      SUBROUTINE CHREP2(ENT,SOR,ANG,NSTRS,MFR,ICLE,KERRE)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
      DIMENSION ENT(*),SOR(*)
C
C----------------------------------------------
C     CETTE ROUTINE PERMET DE CHANGER DE REPERE
C     UN CHAMP DE CONTRAINTE   (ICLE=1)
C     OU UN CHAMP DE DEFORMATION  (ICLE=2)
C     d'apres CHREP
C
C
C   EN PRATIQUE, IL FAUDRAIT PLUTOT APPELER
C   LES SOUS PROGRAMMES UTILISES DANS RTENS
C----------------------------------------------
C
      KERRE=0
      S=SIN(ANG)
      C=COS(ANG)
      CC=C*C
      SS=S*S
      CS=S*C
      IF(ICLE.EQ.1) THEN
        FAC1=1.D0
        FAC2=2.D0
      ELSE
        FAC1=2.D0
        FAC2=1.D0
      ENDIF
*
*     CAS COQUES MINCES
*
      IF(MFR.EQ.3) THEN
        NN=0
        DO 1 I=1,2
          IF(IFOUR.LE.0) THEN
            SOR(NN+1)=CC*ENT(NN+1)+SS*ENT(NN+2)
            SOR(NN+2)=CC*ENT(NN+2)+SS*ENT(NN+1)
          ELSE IF(IFOUR.EQ.1) THEN
            SOR(NN+1)=CC*ENT(NN+1)+SS*ENT(NN+2)+FAC2*CS*ENT(NN+3)
            SOR(NN+2)=CC*ENT(NN+2)+SS*ENT(NN+1)-FAC2*CS*ENT(NN+3)
            SOR(NN+3)=FAC1*CS*(ENT(NN+2)-ENT(NN+1))+(CC-SS)*ENT(NN+3)
          ENDIF
          NN=NN+NSTRS/2
 1      CONTINUE
*
*     CAS MASSIF 2D
*
      ELSE IF(MFR.EQ.1.AND.IFOUR.LE.0) THEN
          SOR(1)=CC*ENT(1)+SS*ENT(2)+FAC2*CS*ENT(4)
          SOR(2)=CC*ENT(2)+SS*ENT(1)-FAC2*CS*ENT(4)
          SOR(4)=FAC1*CS*(ENT(2)-ENT(1))+(CC-SS)*ENT(4)
          SOR(3)=ENT(3)
      ELSE
        KERRE=99
      ENDIF
      RETURN
      END


