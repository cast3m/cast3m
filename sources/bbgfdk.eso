C BBGFDK    SOURCE    CHAT      05/01/12    21:36:26     5004
      SUBROUTINE BBGFDK(QSI,ETA,GEOM,BB)
C|===============================================================|
C|                                                               |
C|    EVALUATION DE LA MATRICE BB(4,9) AU POINT QSI ETA          |
C|    POUR L'ELEMENT DKT                                         |
C|                                                               |
C|    CODE SUO X.Z. MARS  86                                     |
C|===============================================================|
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BXP(9),BYP(9),BXE(9),BYE(9)
      DIMENSION GEOM(*),BB(4,*)
C
      DATA UN/1.0D0/,DEUX/2.0D0/,QUATRE/4.0D0/,SIX/6.0D0/
C
      CL4=GEOM(1)
      CL5=GEOM(2)
      CL6=GEOM(3)
      SL4=GEOM(4)
      SL5=GEOM(5)
      SL6=GEOM(6)
      D4= GEOM(7)
      D5= GEOM(8)
      D6= GEOM(9)
      SU4=GEOM(10)
      SU5=GEOM(11)
      SU6=GEOM(12)
      VJ11=GEOM(13)
      VJ12=GEOM(14)
      VJ21=GEOM(15)
      VJ22=GEOM(16)
      DETJ=GEOM(17)
      PS2=UN-DEUX*QSI
      ET2=UN-DEUX*ETA
      SU5E=SU5*ET2
C---- DERIVEE DE BETAX(I) /QSI
      BXP(1)=CL6*PS2+ETA*(CL5-CL6)
      BXP(2)=-ETA*D5
      BXP(3)=-QUATRE+SIX*(QSI+ETA)-ETA*SU5
      BXP(4)=-CL6*PS2+ETA*(CL4+CL6)
      BXP(5)=ETA*D4
      BXP(6)=-DEUX+SIX*QSI+ETA*SU4
      BXP(7)=-ETA*(CL5+CL4)
      BXP(8)=ETA*(D4-D5)
      BXP(9)=-ETA*(SU5-SU4)
C---- DERIVEE DE BETAY(I) /QSI
      BYP(1)=ETA*SL5
      BYP(2)=UN-ETA*SU5
      BYP(3)=-BXP(2)
      BYP(4)=ETA*SL4
      BYP(5)=-UN+ETA*SU4
      BYP(6)=-BXP(5)
      BYP(7)=-ETA*(SL5+SL4)
      BYP(8)=BXP(9)
      BYP(9)=-BXP(8)
C---- DERIVEE DE BETAX(I) /ETA
      BXE(1)=-CL5*ET2-QSI*(CL6-CL5)
      BXE(2)=D5*ET2-QSI*D5
      BXE(3)=-QUATRE+SIX*(QSI+ETA)+SU5E-QSI*SU5
      BXE(4)=QSI*(CL4+CL6)
      BXE(5)=QSI*D4
      BXE(6)=QSI*SU4
      BXE(7)=CL5*ET2-QSI*(CL4+CL5)
      BXE(8)=D5*ET2+QSI*(D4-D5)
      BXE(9)=-DEUX+SIX*ETA+SU5E+QSI*(SU4-SU5)
C---- DERIVEE DE BETAY(I) /ETA
      BYE(1)=-SL5*ET2+QSI*SL5
      BYE(2)=UN+SU5E-QSI*SU5
      BYE(3)=-BXE(2)
      BYE(4)=QSI*SL4
      BYE(5)=BXE(6)
      BYE(6)=-BXE(5)
      BYE(7)=SL5*ET2-QSI*(SL4+SL5)
      BYE(8)=-UN+SU5E+QSI*(SU4-SU5)
      BYE(9)=-BXE(8)
C---- CALCUL DE BB(4,9)
      DO 10 I=1,9
      BB(1,I)=(VJ11*BXP(I)+VJ12*BXE(I))
      BB(4,I)=(VJ21*BYP(I)+VJ22*BYE(I))
      BB(2,I)=(VJ21*BXP(I)+VJ22*BXE(I))
      BB(3,I)=(VJ11*BYP(I)+VJ12*BYE(I))
10    CONTINUE
      RETURN
      END

