C FVALPH    SOURCE    CHAT      07/04/20    21:15:13     5723
      FUNCTION FVALPH(VALPAR,NPARA,IERUT)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C-----------------------------------------------------------------------
C     DESCRIPTION FONCTIONNELLE :
C     -------------------------
C     Fonction externe d'evaluation de la composante 'ALPH' du modele
C     'FLUAGE' 'POLYNOMIAL' externalise pour TOUTATIS
C
C     INTERFACE :
C     ---------
C     ENTREE : VALPAR : REAL*8(NPARA), valeurs des parametres
C     ENTREE : NPARA  : INTEGER, nombre de parametres
C     SORTIE : IERUT  : INTEGER, code retour
C
C     CODIFICATION DES ERREURS :
C     ------------------------
C     IERUT=0   : OK
C     IERUT=231 : temp. inferieure au zero absolu
C     IERUT=232 : valeur illicite de 'ALPC'
C-----------------------------------------------------------------------
C     Type de la fonction
C
      REAL*8       FVALPH
C
C     Arguments de l'interface
C
      INTEGER      NPARA, IERUT
      REAL*8       VALPAR(NPARA)
C
C     Variables locales
C
      REAL*8       T
      INTEGER      IALPC
C
C---------------------- Debut du code executable -----------------------
C
C     Identification des parametres : affectation de variables locales
C
      T=VALPAR(1)
      IF(VALPAR(2).GE.0.99999 . and. VALPAR(2).le.1.00001) IALPC=1
      IF(VALPAR(2).GE.1.99999 . and. VALPAR(2).le.2.00001) IALPC=2


C
C     Tests d'erreur sur les parametres
C
      IERUT=0
      FVALPH=0.D0
C
      IF (T.LT.0.0D0) THEN
C........Temperature en Kelvin
         IERUT = 231
         RETURN
      ELSE IF ((IALPC.LT.1).OR.(IALPC.GT.2)) THEN
         IERUT = 232
         RETURN
      ENDIF
C
C     Calcul de la fonction
C
C.....DG MARTIN
C
      IF (IALPC.EQ.1) THEN
         IF (T.LT.923.D0) THEN
            FVALPH = 9.828D-6 - (6.390D-10 * T) + (1.330D-12 * T * T)
     &               - (1.757D-17 * T * T * T)
         ELSE
            FVALPH = 1.1833D-5 - (5.013D-9 * T) + (3.756D-12 * T * T)
     &               - (6.125D-17 * T * T * T)
         ENDIF
C
C.....METEOR/TU
C
      ELSE
         FVALPH = 5.193D-6 + (5.876D-9 * T) - (1.169D-13 * T * T)
      ENDIF
C
      RETURN
      END



