C ELTYPE    SOURCE    CHAT      05/01/12    23:38:29     5004
C
      SUBROUTINE ELTYPE(ITYPE,IORDRE,IDE,NBNO,ISENS,ICODE)
C     **********************************************************************
C     OBJET ELTYPE : RENVOI DES INFO SUR LE TYPE D'UN ELEMENT
C     SI ISENS = 1
C       EN ENTREE :
C         ITYPE   : TYPE DE L'ELEMENT (1..8) (SEGMENT,TRIANGLE...)
C         IORDRE  : ORDRE DE L'ELEMENT (1,2) (LINEAIRE,QUADRATIQUE...)
C       EN SORTIE :
C         ICODE   : LE CODE DE L'ELEMENT
C         IDE     : DIMENSION DE L'ELEMENT
C         NBNO    : NOMBRE TOTAL DE NOEUDS DE L'ELEMENT
C     SI ISENS = -1
C       EN ENTREE :
C         ICODE   : LE CODE DE L'ELEMENT
C       EN SORTIE :
C         ITYPE   : TYPE DE L'ELEMENT (1..8) (SEGMENT,TRIANGLE...)
C         IORDRE  : ORDRE DE L'ELEMENT (1,2) (LINEAIRE,QUADRATIQUE...)
C         IDE     : DIMENSION DE L'ELEMENT
C         NBNO    : NOMBRE TOTAL DE NOEUDS DE L'ELEMENT
C     **********************************************************************
      IMPLICIT INTEGER(I-N)
      INTEGER ITYPE,IORDRE,ISENS,ICODE,IDE,NBNO
C
      INTEGER   MAXEL,MAXTYP,MAXORD
      PARAMETER   (MAXEL = 21, MAXTYP = 8, MAXORD = 2)
      INTEGER ISTYO,ISTY,ISTO,ISNBNO,ISIDE,ISCODE
      COMMON /ELCODE/ ISCODE(MAXEL)
      COMMON /ELTYPO/ ISTYO(MAXTYP,MAXORD)
      COMMON /ELTYPC/ ISTY(MAXEL)
      COMMON /ELORDR/ ISTO(MAXEL)
      COMMON /ELNBNO/ ISNBNO(MAXEL)
      COMMON /ELIDE/  ISIDE(MAXEL)
      INTEGER INUMS
C
      IF(ISENS.EQ.1)THEN
        IF(( IORDRE.LT.1 ).OR.( IORDRE.GT.MAXORD ))THEN
          CALL ESERRO(1,-1,'ELTYPE ',' ORDRE DES ELEMENTS')
          ICODE = -1
          GOTO 9999
        ENDIF
        IF(( ITYPE.LT.1 ).OR.( ITYPE.GT.MAXTYP ))THEN
          CALL ESERRO(1,-1,'ELTYPE ',' TYPE DES ELEMENTS')
          ICODE = -1
          GOTO 9999
        ENDIF
        INUMS = ISTYO(ITYPE,IORDRE)
        ICODE = ISCODE(INUMS)
        IDE   = ISIDE(INUMS)
        NBNO  = ISNBNO(INUMS)
C
      ELSE
        CALL ELNUMS(ICODE,INUMS)
        IF(( INUMS.LT.1 ).OR.( INUMS.GT.MAXEL ))THEN
          CALL ESERRO(1,-1,'ELTYPE ',' CODE DES ELEMENTS')
          ITYPE = -1
          IORDRE = -1
          GOTO 9999
        ENDIF
        ITYPE  = ISTY(INUMS)
        IORDRE = ISTO(INUMS)
        IDE    = ISIDE(INUMS)
        NBNO   = ISNBNO(INUMS)
      ENDIF
C
 9999 END


