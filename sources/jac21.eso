C JAC21     SOURCE    CHAT      05/01/13    00:48:02     5004
CCC
C **********************************************************************
CCC
      SUBROUTINE JAC21 (KMAT,NDIMK,X,NDIMX,LAM,NDIMS,
     .                nmodel,nescri,ues,nnumer,deltax,kerre)
      IMPLICIT INTEGER(I-N)
      integer ndims,nmodel,nescri,nnumer,ues,ndimv,ndimx
      integer i,j,k,kerre,ndimk
      real*8    x(ndimx),lam,void,Kmat(ndimk,ndimk),g,deltax
      real*8    Amat(64),Gmat(64),vecn(8),vecm(8),Avecm(8),vecntA(8),
     .          vecEm(8),Hmat(64)
      kerre=0
      ndimv=2
      void =0.D0
      do i=1,8
          vecntA(i)=0.D0
          Avecm(i)=0.D0
          vecn(i)=0.D0
          vecm(i)=0.D0
          vecEm(i)=0.D0
        enddo
      do i=1,64
          Amat(i)=0.D0
          Gmat(i)=0.D0
          Hmat(i)=0.D0
        enddo
c m
      call VecFlMAC(x,ndimx,vecm,nmodel)
c n
      call VecYiMAC(x,ndimx,vecn,nmodel)
c H
      call HessMAC(x,ndimx,Hmat,ndimx,nnumer,deltax,nmodel)
c G=E
      call MatGenHook(Gmat,ndimx,ndims)
c Em = E*m
      do i=1,ndimx
       do j=1,ndimx
         vecEm(i)=vecEm(i)+Gmat(ndimx*(j-1)+i)*vecm(j)
       enddo
      enddo
c A = I+l*E*H
      do i=1,ndimx
       do j=1,ndimx
        do k=1,ndimx
          Amat(ndimx*(j-1)+i)=Amat(ndimx*(j-1)+i)+
     .                        Gmat(ndimx*(k-1)+i)*Hmat(ndimx*(j-1)+k)
        enddo
        Amat(ndimx*(j-1)+i)=lam*Amat(ndimx*(j-1)+i)
       enddo
       Amat(ndimx*(i-1)+i)=1.D0+Amat(ndimx*(i-1)+i)
      enddo
c G = A-1
      call DescLU(Amat,ndimx)
      call LUinv(Amat,Gmat,ndimx)
c g = nt*A-1*Em
      g=0.D0
      do i=1,ndimx
       do j=1,ndimx
          g=g+vecn(i)*Gmat(i+ndimx*(j-1))*vecEm(j)
       enddo
      enddo
c G*Em; nt*G
      do i=1,ndimx
       do j=1,ndimx
         Avecm(i)=Avecm(i)+Gmat(i+ndimx*(j-1))*vecEm(j)
         vecntA(i)=vecntA(i)+vecn(j)*Gmat(j+ndimx*(i-1))
       enddo
      enddo
c K = G-(G*m * nt*G)/g
      do i=1,ndimx
       do j=1,ndimx
          Kmat(i,j)=Gmat((j-1)*ndimx+i)-Avecm(i)*vecntA(j)/g
       enddo
      enddo
      return
      end


