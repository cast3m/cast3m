C COUMAL    SOURCE    CB215821  18/06/19    21:15:04     9862
      SUBROUTINE COUMAL(RP,XL,RAY,RC,RHO,LCAR,RHOR)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE MASSE ELEMENTAIRE D'UN
C    ELEMENT DE RACCORD LITU EN AXES LOCAUX
C
C  ENTREE
C    XL      LONGUEUR
C    RAY     RAYON DU TUYAU FLUIDE
C    RC      RAYON DE COURBURE
C    RHO     MASSE VOLUMIQUE DU FLUIDE
C    LCAR    LONGUEUR CARACTERISTIQUE
C    RHOR    MASSE VOLUMIQUE DE REFERENCE
C  SORTIE
C    RP      MATRICE DE MASSE ELEMENTAIRE 10x10
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)

-INC CCREEL

      DIMENSION RP(10,10)
      REAL*8 LCAR
C
      DO  J=1,10
        DO  I=J,10
          RP(I,J)=0.D0
        ENDDO
      ENDDO
C
      SE=XPI*RAY*RAY
      A=RHO*SE*XL*0.3714285714285714D0
      B=-RHOR*LCAR*LCAR*SE
      C=RHO*SE*XL*0.1285714285714286D0
C
      RP(4,1) =-B
      RP(2,2) = A
      RP(3,3) = A
      RP(7,2) = C
      RP(8,3) = C
      RP(9,6) = B
      RP(7,7) = A
      RP(8,8) = A
C
      IF (RC.NE.0) THEN
        C  =(RHOR*LCAR*LCAR*SE*XL)/(3.D0*RC)
        CS2=C/2.D0
        RP(4,3) = C
        RP(9,3) = CS2
        RP(8,4) = CS2
        RP(9,8) = C
      ENDIF
C
C SYMETRISATION
C
      DO I=1,10-1
        DO J=I+1,10
          RP(I,J)=RP(J,I)
        ENDDO
      ENDDO
      RETURN
      END

