C ARPSHI    SOURCE    BP208322  15/10/21    21:15:10     8690
      SUBROUTINE ARPSHI (FSHIFT,VSHIFT,QUAD,INVER,SENS)




***********************************************************************
*
*                          A R P S H I
*
* FONCTION:
* ---------
*
*     PASSAGE D'UNE FREQUENCE DE SHIFT A UNE VALEUR PROPRE DE SHIFT
*     ET VICE VERSA
*
*
* PARAMETRES:  (E)=ENTREE   (S)=SORTIE
* -----------
*
*
*     FSHIFT  COMPLEXE DP    (E/S)  FREQUENCE DE SHIFT
*
*     VSHIFT  COMPLEXE DP    (E/S)  VALEUR PROPRE DE SHIFT
*
*     QUAD    LOGIQUE        (E)    PROBLEME QUADRATIQUE OU NON
*
*     INVER   LOGIQUE        (E)   .TRUE. -> PRODUIT SCALAIRE X'KX
*                                  .FALSE. -> PRODUIT SCALAIRE X'MX
*
*     SENS    ENTIER         (E)    SENS DE CONVERSION
*                                       1 : FSHIFT -> VSHIFT
*                                       2 : VSHIFT -> FSHIFT
*
* SOUS-PROGRAMMES APPELES:
* ------------------------
*
*     NEANT
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     PASCAL BOUDA     17 JUIN 2015
*
* LANGAGE:
* --------
*
*     FORTRAN 77 & 90
*
************************************************************************

-INC CCOPTIO
-INC CCREEL

      COMPLEX*16 FSHIFT
      COMPLEX*16 VSHIFT
      LOGICAL QUAD
      LOGICAL INVER
* Note sur INVER : Variable devenue inutile dans ce programme
      INTEGER SENS

      REAL*8 MODV,MODF,THETAV,THETAF
      REAL*8 X,Y
      COMPLEX*16 ZERO,J

      ZERO=CMPLX(0.D0,0.D0)
      J=CMPLX(0.D0,1.D0)


*on utilise la notation exponentielle pour faciliter les calculs
*l'objectif est de calculer
*
*       - lambda=(2*pi*f)^2 si le problemeest lineaire*
*       - lambda=jw=j*(2*pi*f) si le probleme est quadratique
*
*le module et l'argument de l'inconnue sont calcules separement.
*la solution est unique grace a la fonction atan2 qui renvoie
*l'argument dans le bon cadran

****************************
*** PROBLEME QUADRATIQUE ***
****************************

      IF (QUAD) THEN
*frequence de shift connue: on calcule la valeur propre de shift
        IF (SENS .EQ. 1) THEN

           VSHIFT=2.D0*XPI*J*FSHIFT

*valeur propre de shift connue: on calcule la frequence de shift
        ELSEIF (SENS .EQ. 2) THEN

           FSHIFT=VSHIFT/(2.D0*XPI*J)


        ENDIF

*************************
*** PROBLEME LINEAIRE ***
*************************
      ELSE
*frequence de shift connue: on calcule la valeur propre de shift
        IF (SENS .EQ. 1) THEN

          MODF=ABS(FSHIFT)
          THETAF=ATAN2(AIMAG(FSHIFT),REAL(FSHIFT))

          MODV=(2.D0*XPI*MODF)**2
          THETAV=2.D0*THETAF

          X=MODV*COS(THETAV)
          Y=MODV*SIN(THETAV)

          VSHIFT=CMPLX(X,Y)


*valeur propre de shift connue: on calcule la frequence de shift
        ELSEIF (SENS .EQ. 2) THEN

          MODV=ABS(VSHIFT)
          THETAV=ATAN2(AIMAG(VSHIFT),REAL(VSHIFT))

          MODF=SQRT(MODV)/(2.D0*XPI)
          THETAF=THETAV/2.D0

          X=MODF*COS(THETAF)
          Y=MODF*SIN(THETAF)

          FSHIFT=CMPLX(X,Y)

        ENDIF

      ENDIF

      END














