C VLHJ2     SOURCE    CHAT      05/01/13    04:07:50     5004
      SUBROUTINE VLHJ2(RHO,UX,UY,P,GAM,CNX,CNY,
     &                  DFRUNC)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  VLHJ2
C
C DESCRIPTION       :  Calcul du Jacobien du flux numerique VLH
C                      Contribution des faces sur les murs
C
C LANGAGE           :  FORTRAN 77
C
C AUTEUR            :  A. BECCANTINI DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C APPELES           :  VLHJ3
C
C************************************************************************
C
C**** Entrées:
C
C     RHO      =  densité
C
C     UX, UY   =  vitesse
C
C     P        =  pression
C
C     GAM      =  le "gamma"
C
C     CNX, CNY =  normale à la face
C
C
C**** Sorties:
C
C     DFRUNC(4)= derivés partielles de F_RHOUN par raport a RHO, RHOUX,
C                RHOUY, RHOET
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  Créé le 04.09.00
C
C************************************************************************
C
C N.B.: Toutes les variables sont declarées explicitement
C
C
      IMPLICIT INTEGER(I-N)
      REAL*8 RHO,UX,UY,P,GAM,CNX,CNY
     &      ,DFRUNC(4),DFRUNP(4)
     &      ,USRO,UXSRO,UYSRO,GM1,GM1EC,GM1UX,GM1UY
C
C**** Jacobian with respect to the primitive variables
C
      CALL VLHJ3(RHO,UX,UY,P,GAM,CNX,CNY,DFRUNP)
C
C**** Jacobian with respect to the conservative variables
C
      USRO = 1.0D0 / RHO
      UXSRO = UX * USRO
      UYSRO = UY * USRO
      GM1 = GAM - 1.0D0
      GM1EC = GM1 * 0.5D0 * (UX * UX + UY * UY)
      GM1UX = GM1 * UX
      GM1UY = GM1 * UY
C
      DFRUNC(1) = DFRUNP(1) - UXSRO * DFRUNP(2) -  UYSRO * DFRUNP(3)
     &           + GM1EC * DFRUNP(4)
      DFRUNC(2) =  USRO * DFRUNP(2) -  GM1UX * DFRUNP(4)
      DFRUNC(3) =  USRO * DFRUNP(3) -  GM1UY * DFRUNP(4)
      DFRUNC(4) =  GM1 * DFRUNP(4)
C
      RETURN
      END



