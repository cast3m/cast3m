C BABAEL    SOURCE    CHAT      05/01/12    21:31:17     5004
C BABAEL    SOURCE    DC        98/04/28
      SUBROUTINE BABAEL(XMAT,XCAR,DEPST,VAR0,SIGF,VARF,DEFP)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(5),VAR0(3),VARF(3)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE
C      MODELE BETON PLASTIQUE DU BAEL
C     ELEMENT DE BARRE
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - Avril 1999
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C      XMAT( 3) RHO
C      XMAT( 4) ALPHA
C      XMAT( 5) FC
C
C      SIGF STRESS
C      DEPST,DEFP
C
C                      VAR0(1) VARF(1) EPSE DEFORMATION PLASTIQUE
C                      VAR0(2) VARF(2) EPSO DEFORMATION TOTALE
C
C----------------------------------------------------------------------
C
       GAMMA=XMAT(1)/2/(XMAT(2)+1)
       EYOUN = XMAT( 1)
       FC = (-1.D0)*XMAT(5)
       XEPSLI = (2.D0)*(FC/EYOUN)
C
       XEPSE =  VAR0(1)
       XEPSO =  VAR0(2)
C
       XEPSOF = XEPSO + DEPST
C
       IF (XEPSOF. GE . XEPSE) THEN
C
         XSIGF = 0.D0
         XTAN = 0.D0
C
       ELSE
C
         XSIGF1 = EYOUN * (XEPSOF - XEPSE)
C
         IF (XEPSOF . LT . XEPSLI) THEN
            XSIGF2 = FC
            XTAN = 0.D0
         ELSE
            XSIGF2 = FC*( ( (2.D0)*(XEPSOF/XEPSLI))
     &                   - ((XEPSOF/XEPSLI)**2) )
            XTAN =  FC*(  (2.D0/XEPSLI)
     &                   - (( 2.D0 * XEPSOF) /(XEPSLI**2) ) )
         ENDIF
C
         IF (XSIGF2 . GE . XSIGF1) THEN
           XSIGF = XSIGF2
           XEPSE = XEPSOF - (XSIGF/EYOUN)
         ELSE
           XSIGF = XSIGF1
           XTAN = EYOUN
         ENDIF
C
       ENDIF
C
C
       SIGF = XSIGF * XCAR
C
       VARF(1) = XEPSE
       VARF(2) = XEPSOF
       VARF(3) = XTAN * XCAR
C
       DEFP    = XEPSE
C
       RETURN
       END



