C CERKSG    SOURCE    CHAT      05/01/12    21:52:23     5004
      SUBROUTINE CERKSG(REL,LRE,EFFORT,COORD,KERRE)
C-----------------------------------------------------------------------
C       ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE GEOMETRIQUE
C                      DE L ELEMENT CERCE
C-----------------------------------------------------------------------
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          EFFORT  EFFORT DANS LA CERCE ( SUPPOSE TOUKOURS AXI )
C          COORD   COORDONNEES DU NOEUD
C
C  SORTIE
C          REL     MATRICE DE RIGIDITE GEOMETRIQUE ELEMENTAIRE
C          KERRE   0 SI PAS DE PB
C                  1 SI RAYON NUL
C                  2 CAS IMPOSSIBLE
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
-INC CCREEL
      DIMENSION REL(LRE,*),COORD(3,*)
*
      KERRE=0
      R = COORD(1,1)
      IF(ABS(R).LE.XPETIT) THEN
         KERRE=1
         RETURN
      ENDIF
*
      IF(IFOUR.EQ.0.OR.(IFOUR.EQ.1.AND.NIFOUR.EQ.0)) THEN
         FAC=2*XPI*EFFORT/R
      ELSE
         FAC=XPI*EFFORT/R
      ENDIF
      IF(IFOUR.EQ.0) THEN
         REL(1,1)=FAC
      ELSE IF(IFOUR.EQ.1) THEN
         AN= DBLE(NIFOUR)
         AN2=AN*AN
         REL(1,1)=FAC *(1.D0+AN2)
         REL(3,3)=REL(1,1)
         REL(1,3)=FAC*AN*2.D0
         REL(3,1)=REL(1,3)
      ELSE
         KERRE=2
         RETURN
      ENDIF
      RETURN
      END







