C POUDRB    SOURCE    PV        09/12/22    21:15:17     6589
C
C  PLGDebut : implantation des equations relatives au modele
C  viscoplastique poudre.
C
C  Modification ChD division par des rÅels et non pas des entiers
C  COEC doit etre plus grand que 1.0 et non pas 0.0
C
       SUBROUTINE POUDRB(SIG,EPSV,VAR,EPSVPT,VARPT,XMAT,NSTRS,
     +                  NVARI,NCOMAT,KERRE)
C
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
       DIMENSION SIG(*),EPSV(*),VAR(*),EPSVPT(*),VARPT(*)
       DIMENSION XMAT(*)
       DIMENSION XX(6)
C----------------------------------------------------------------
C  ====> Connaissant l'etat, c'est a dire SIG, EPSV et VAR on
C    calcule les derivees EPSVPT et VARPT(1) en utilisant les
C    constantes XMAT
C----------------------------------------------------------------
C
C-------calcul des invariants S1=Tr(sigma)
C                             S2C=S2*S2=Tr(Dev(sigma):Dev(sigma))
C                        avec Dev(sigma)=sigma-1/3Tr(sigma)
       S1 = SIG(1)+SIG(2)+SIG(3)
C Modif Ch.D du 20/10/94 : Cf DEA Canel page A18 assure tr(XX) = 0
       DO 30 I=1,2
       XX(I) = SIG(I)-S1/3.0D0
 30    CONTINUE
       XX(3) = -XX(1)-XX(2)
C Fin de modification Ch.D du 20/10/94
       DO 40 I=4,NSTRS,1
       XX(I) = SIG(I)
 40    CONTINUE
       S2C = PROCON(XX,XX,NSTRS)
C
       RHOR = VAR(NVARI)
       A    = XMAT(6)
       RN   = XMAT(5)
       QSRT = XMAT(7)
       F0 = XMAT(8)
       F1 = XMAT(9)
       F2 = XMAT(10)
       F3 = XMAT(11)
       F4 = XMAT(12)
       F5 = XMAT(13)
       C0 = XMAT(14)
       C1 = XMAT(15)
       C2 = XMAT(16)
       C3 = XMAT(17)
       C4 = XMAT(18)
       C5 = XMAT(19)
       G0 = XMAT(20)
       G1 = XMAT(21)
       G2 = XMAT(22)
       G3 = XMAT(23)
       G4 = XMAT(24)
       G5 = XMAT(25)
       G6 = XMAT(26)
C
       RMIN = F5+1.0D-03
       IF (RHOR.LT.RMIN) THEN
        RHOR1 = RMIN
       ELSE
        RHOR1 = RHOR
       ENDIF
C
C
C-------calcul de EPSVPT en fonction du parametre F4 :
C------- F4 = 0      : loi ABOUAF avec taille de grain
C------- F4 = 1      : loi de Stutz
C
*pv  pour être sur d'avoir la bonne valeur
       NF4=INT(F4+0.1)+1
       GOTO(10,20),NF4
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        Loi Abouaf avec calcul de la taille de grain
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
10     CONTINUE
C
C-------calcul de F et C : fonctions de rho
C
       IF ((1.0D0-RHOR1).LT.(1.D-06)) THEN
          COEF = 0.0
          COEC = 1.0
       ELSE
          COEF = F0*(((1-RHOR1)/(RHOR1-F5))**(F1*RHOR1+F2))
          COEC = 1.0D0+C0*(((1-RHOR1)/(RHOR1-F5))**(C1*RHOR1+C2))
       ENDIF
       IF (COEF.GT.(C5)) COEF = C5
       IF (COEC.GT.(C5)) COEC = C5
C
C-------calcul de sigma equivalent :
C
       SIGEQ = SQRT(COEF*S1*S1+1.5D0*COEC*S2C)
       DO 500 I=1,NSTRS,1
        B = 0.D0
        IF (I.LE.3) B=1.D0
        EPSVPT(I) = 1.D0*(10**A)*(SIGEQ**(RN-1.D0))*RHOR
     &              *(COEF*S1*B+1.5D0*COEC*XX(I))
500    CONTINUE
       GOTO 1000
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        Loi de Stutz
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
 20    CONTINUE
       COEH = F0*(((1.-RHOR)/(RHOR-F5))**F1)
       COEE = C0*(RHOR-RHOR**2)
       COES = G0-(G1*(1.-RHOR))+(G2*((1.-RHOR)**2))
       COF1 = (2./3.)**((RN+1)/2)*COEH/3.
       COF2 = (1.-COEE)*(COES**(-RN))+2.*((3./2.)**((RN+1)/2))*COF1
       COF3 = COEE*(COES**(-RN))*((2./3.)**((RN+5)/4))
     &        -2.*COF1*((3./2.)**((RN+1)/4))
C
C-------calcul de EPSVPT :
C
C
       DO 600 I=1,NSTRS,1
       B = 0.0
         IF (I.LE.3) B=1.0
         SIGEQ = (S1**2*S2C)**((RN-3)/4)
         EPSVPT(I) = (3./2.)**((RN+1)/2)*A*EXP(-QSRT)*RHOR
     &   *(3.*S1*(COF1*S1**(RN-1)+COF3/2.*S2C*SIGEQ)*B
     &   +(COF2*S2C**((RN-1)/2)+COF3/2.*S1**2*SIGEQ)*XX(I))
 600    CONTINUE
C
C-------calcul de VARPT(1) derivee de EPSE                                         C
1000   CONTINUE
       PPT = SQRT(2.0/3.0*PROCON(EPSVPT,EPSVPT,NSTRS))
       VARPT(1) = PPT
C
C--------on rentre
C
       RETURN
       END
C




