C CENTJ1    SOURCE    CHAT      05/01/12    21:52:02     5004
      SUBROUTINE CENTJ1(RHO,UX,UY,P,GAM,CNX,CNY,CTX,CTY,
     &                  DFRO,DFRUN,DFRUT,DFRET)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  CENTJ1
C
C DESCRIPTION       :  Calcul du Jacobien du flux numerique centré
C                      Contribution de l'onde de gauche à droite
C                      Voir CENTJ0
C
C LANGAGE           :  FORTRAN 77
C
C AUTEUR            :  A. BECCANTINI DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C APPELES           :  MPFUNT
C
C************************************************************************
C
C**** Entrées:
C
C     RHO      =  densité
C
C     UX, UY   =  vitesse
C
C     P        =  pression
C
C     GAM      =  le "gamma"
C
C     CNX, CNY =  normale à la face
C
C     CTX, CTY =  tangente à la face
C
C
C**** Sorties:
C
C     DFRO(4)  = derivés partielles de F_RHO+ par raport a RHO, UX,
C                UY, P
C
C     DFRUN(4) = derivés partielles de F_RHOUN+ par raport a RHO, UX,
C                UY, P
C
C     DFRUT(4) = derivés partielles de F_RHOUT+ par raport a RHO, UX,
C                UY, P
C
C     DFRET(4) = derivés partielles de F_RHOET+ par raport a RHO,
C                UX, UY, P
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  Créé le 04.09.00
C
C************************************************************************
C
C N.B.: Toutes les variables sont declarées explicitement
C
C
      IMPLICIT INTEGER(I-N)
      REAL*8 RHO,UX,UY,P,GAM,CNX,CNY,CTX,CTY
     &      ,DFRO(4),DFRUN(4),DFRUT(4),DFRET(4)
     &      ,UN,UT,CELL,GSGM1P,ECIN
C
      UN = UX * CNX + UY * CNY
      UT = UX * CTX + UY * CTY
      CELL = RHO * UN
C
      DFRO(1) = 0.5D0 * UN
      DFRO(2) = 0.5D0 * RHO * CNX
      DFRO(3) = 0.5D0 * RHO * CNY
      DFRO(4) = 0.0D0
C
      DFRUN(1) = DFRO(1) * UN
      DFRUN(2) = CELL * CNX
      DFRUN(3) = CELL * CNY
      DFRUN(4) = 0.5D0
C
      DFRUT(1) = DFRO(1) * UT
      DFRUT(2) = (DFRO(2) * UT)  + (0.5D0 * CELL * CTX)
      DFRUT(3) = (DFRO(3) * UT)  + (0.5D0 * CELL * CTY)
      DFRUT(4) = 0.0D0
C
      GSGM1P=(GAM/(GAM-1.0D0))*P
      ECIN=0.5D0*((UX*UX)+(UY*UY))
      DFRET(1) = 0.5D0*ECIN*UN
      DFRET(2) = 0.5D0*((CNX*(GSGM1P+(RHO*ECIN)))+
     &     (CELL*UX))
      DFRET(3) = 0.5D0*((CNY*(GSGM1P+(RHO*ECIN)))+
     &     (CELL*UY))
      DFRET(4) = 0.5D0*UN*(GAM/(GAM-1.D0))
C
      RETURN
      END




