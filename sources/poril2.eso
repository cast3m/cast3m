C PORIL2    SOURCE    PV        18/06/18    21:15:25     9860
      SUBROUTINE PORIL2(RP,XL,YOUN,CISAIL,SD,SREDY,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE EN AXES LOCAUX EN 2D
C    D APRES TEDEL
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 12X12
C
C     VIENT DE TEDEL AOUT 84
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(6,6)
C
C  INITIALISATIONS
C
      DO 120 J=1,6
      DO 120 I=1,6
  120 RP(I,J)=0.D00
      RL=1.D00/XL
      RL2=RL*RL
      RL3=RL2*RL
      X2=0.D00
      X3=0.D00

           IF( SREDY. NE.0.) X3= YOUN/CISAIL*XINZ*RL2/  SREDY
           ESL=YOUN*SD*RL
           EIZ=YOUN*XINZ

        X12= 1./(1.+12.*X2)
        X13= 1./(1.+12.*X3)

C
C  REMPLISSAGE DE LA MATRICE
C
      RP(1,1)=ESL
      RP(1,4)=-ESL
       RP(2,2)= 12.*EIZ*RL3*X13
      RP(2,3)=6.*EIZ*RL2*X13
        RP(2,5)=  -12. *EIZ*RL3*X13
      RP(2,6)= 6.*EIZ*RL2*X13
      RP(3,3)= 4.*EIZ*RL
     . *(1.+3.*X3)*X13
      RP(3,5)=-6.*EIZ*RL2*X13
      RP(3,6)=2.*EIZ*RL
     .      *(1.-6.*X3)*X13
      RP(4,4)=ESL
       RP(5,5)= 12.*EIZ*RL3*X13
      RP(5,6)=-6.*EIZ*RL2*X13
      RP(6,6)= 4.*EIZ*RL
     . *(1.+3.*X3)*X13
      DO 9 I=1,6
      DO 9 J=I,6
   9  RP(J,I)=RP(I,J)
      RETURN
      END





