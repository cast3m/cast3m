C LUMP6     SOURCE    CHAT      05/01/13    01:27:17     5004
      SUBROUTINE LUMP6(REL,LRE,RE)
c====================================================================
c
c     utilisé par l'opérateur lump pour les éléments pout timo et tuya
c
c     remplissage de re à partir de
c     rel (lre,lre) matrice de rigidite triangulaire
c     inferieure.
c
c     on diagonalise la matrice en somme les termes de la
c     meme lignes, somme que l'on met sur la diagonale
c     pour les dd de translation
c
c     pour les ddl de rotation on prend la trace divisée par le
c     nombre de termes
c
c     ce travail est realisé dans le repere global
c
c     on suppsoe que re est initialisée à 0
c
c====================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(LRE,*),RE(LRE,*)
*
*
*
c      on traite ux uy uz
c       boucle sur les noeuds
      NTRA = 3
      IF (LRE .NE. 12 ) THEN
        WRITE(*,*) 'Erreur dans lump6.eso'
        CALL ERREUR(5)
      ENDIF

      DO 430 I=1,2
c         boucle sur les ddl ux uy uz locaux
          DO 420 J=1,NTRA
              IDERIJ = (NTRA+3)*(I-1) + J
              SUM = 0.D0
              DO 410 K=1,2
                  DO 400 L=1,NTRA
                      IDERKL= (NTRA+3)*(K-1)+L
                      SUM = SUM + REL(IDERIJ,IDERKL)
 400              CONTINUE
 410          CONTINUE
              RE(IDERIJ,IDERIJ) = SUM
 420      CONTINUE
 430  CONTINUE
c
c        on traite maintenant rx ry rz
c
      NTRA = 3
      TRAC = 0.D0
c       boucle sur les noeuds
      DO 530 I=1,2
c         boucle sur les ddl rx ry rz
          DO 520 J=1,NTRA
              IDERIJ = (NTRA+3)*(I-1) + 3 + J
              TRAC = TRAC + REL(IDERIJ,IDERIJ)
 520      CONTINUE
 530  CONTINUE
c
      DO 630 I=1,2
c         boucle sur les ddl rx ry rz  remplir re()
          DO 620 J=1,NTRA
              IDERIJ = (NTRA+3)*(I-1) + 3 + J
              RE(IDERIJ,IDERIJ) = TRAC / 6.D0
 620      CONTINUE
 630  CONTINUE
*
      RETURN
      END


