C DERL      SOURCE    BECC      11/05/26    21:15:20     6981
      SUBROUTINE DERL(
     &     PC, GAM,
     &     P, RHO_L, P_L, U_L,
     &     DER,
     &     LOGDEB, LOGAN)
*
*************************************************************************
*
* project           :  CAST3M, EUROPLEXUS...
*
* name              :  derl
*
* description       :  euler equations for a mixture of stiffened gases
*                      flux in the non-reactive case.
*
*                      left entropy-respecing curve.
*                      derivative of w with respect to p
*
* language          :  fortran 77
*
* author            :  a. beccantini den/dm2s/sfme/ltmf
*
*************************************************************************
*
* called by         :
*
*
*************************************************************************
*
***** input
*
*     pc, gam         = properties of the gas
*
*     p               = pressure in the right side of the left
*                       entropy respecing curve
*
*     rho_l, p_l, u_l
*                     = density, pressure, velocity on the left
*
*     logdeb          = debugging ?
*
***** output
*
*     der             = derivative
*
*     logan           = anomaly ?
*
*************************************************************************
*
*     12/11/2009      created
*     25/05/2011      evolution in CAST3M
*
*************************************************************************
*
* n.b.: all variables are declared
*
C      IMPLICIT NONE
      IMPLICIT INTEGER(I-N)
      REAL*8
     &     PC, GAM,
     &     P, RHO_L, P_L, U_L,
     &     DER, CSON_L, VAR, DER2
*
      LOGICAL LOGDEB, LOGAN
*
      IF (P .LE. (-1.0D0 * PC)) THEN
C
C******* Stop
C
         WRITE(*,*) 'P  = ', P
         WRITE(*,*) 'PC = ', PC
         LOGAN = .TRUE.
         WRITE(*,*) 'SUBROUTINE DERL'
         WRITE(*,*) 'ANOMALY DETECTED.'
         GOTO 9999
      ENDIF
      IF (P .LE. P_L) THEN
C
C******* Rarefaction
C
         cson_L = gam * (P_L + PC) / RHO_L
         cson_L = SQRT (cson_L)
         var = P + PC
         var = var / (P_L + PC)
         var = var ** ((GAM - 1.0D0) / (2.0D0 * GAM))
         DER = ((-1.0D0 * cson_L / GAM) * (var / (P + PC)))
      ELSE
         var = RHO_L * (GAM + 1.0D0) * (P + PC)
         var = var + (RHO_L * (GAM - 1.0D0) * (P_L + PC))
         DER = SQRT (2.0D0 / var)
         DER2 = 0.5D0 * SQRT (2.0D0) * (var**(-1.5D0)) * RHO_L *
     &        (gam + 1.0D0)
         DER2 = DER2 * (P - P_L)
         DER = DER2 - DER
      ENDIF
*
      IF (LOGAN) THEN
         WRITE(*,*) 'SUBROUTINE FSTERL.F'
         WRITE(*,*) 'ANOMALY DETECTED.'
         GOTO 9999
      ENDIF
*
 9999 RETURN
      END

