C FIBET2    SOURCE    CHAT      12/04/06    21:15:10     7348
C FIBET2    SOURCE    AM        94/02/28    21:24:59     1061
      SUBROUTINE FIBET2(XMAT,DEPS,SIG0,VAR0,SIGF,VARF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(18),DEPS(3),SIG0(3),VAR0(6),SIGF(3),VARF(6)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE DE BETON_LIBRE ET DE BETON_CONFINE
C
C----------------------------------------------------------------------
C     Joao Guedes et Pierre Pegon (ISPRA) Juillet/Decembre 1993
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C
C      XMAT( 5) MAXIMUM COMPRESSION STRESS
C      XMAT( 6) STRAIN OF THE MAXIMUM COMPRESSION STRESS
C      XMAT( 7) MAXIMUM TENSION STRESS
C      XMAT( 8) ALPHA 1 FACTOR
C      XMAT( 9) OMEGA 1 FACTOR
C      XMAT(10) MODULUS OF THE DESCEND. PART OF THE CURVE
C      XMAT(11) STRESS VALUE DEFINING THE COMPRESSION PLATEAU
C      XMAT(12) DESCENDING TRACTION CURVE FACTOR
C      XMAT(13) F2 (FOR LOADING/UNLOADING)
C      XMAT(14) F1 (FOR LOADING/UNLOADING)
C      XMAT(15) RESIDUAL STRESS (TENSION)
C      XMAT(16) F1' (FOR LOADING/UNLOADING)
C      XMAT(17) F2' (FOR LOADING/UNLOADING)
C
C      DEPS(1) EPXX    SIG0(1) SIGF(1) SMXX
C      DEPS(2) GAXY    SIG0(2) SIGF(2) SMXY
C      DEPS(3) GAXZ    SIG0(3) SIGF(3) SMXZ
C
C                      VAR0(1) VARF(1) EPSO
C                      VAR0(2) VARF(2) MAX. STRAIN EVER REACHED
C                      VAR0(3) VARF(3) MIN. STRAIN EVER REACHED
C                      VAR0(4) VARF(4) MIN. STRESS EVER REACHED
C                      VAR0(5) VARF(5) TANGENT MODULUS
C                      VAR0(6) VARF(6) DISCHARGE TANGENT MODULUS
C
C----------------------------------------------------------------------
C
C
C
       GAMMA = XMAT( 1) / 2.0D0 / (XMAT(2)+1.D0)
       FC___ = XMAT( 5)
       EZERO = XMAT( 6)
       FLT__ = XMAT( 7)
       ALFA1 = XMAT( 8)
       OMEG1 = XMAT( 9)
       ZETA_ = XMAT(10)
       STR85 = XMAT(11)
       TRAFA = XMAT(12)
       FTPL_ = XMAT(15)
C       FACT1 = 20.0
C       FACT2 = 20.0
       FACLO = XMAT(13)
       FACX2 = XMAT(14)
       FACT1 = XMAT(16)
       FACT2 = XMAT(17)
C
       IF (TRAFA .LT. 1.0d0) THEN
          WRITE (*,*) ' ERROR - TRAFA < 1.0 '
          STOP
       ENDIF
C
       ALFOM = ALFA1 * OMEG1
C
       IF (ZETA_ .LE. 0.D0) THEN
C
C ------- COMPUTING PROPERTIES OF CONFINED CONCRETE -------
C
          IF (ALFOM .LT. 1.0D-1) THEN
             BETAC = 1.000D0 + 2.50D0 * ALFOM
          ELSE
             BETAC = 1.125D0 + 1.25D0 * ALFOM
          ENDIF
C
          FC___ = BETAC * FC___
          EZERO = BETAC * BETAC * EZERO
          ZETA_ = (BETAC-0.85D0) / (BETAC*(0.1D0*ALFOM+0.0035D0-EZERO))
       ENDIF
C
       EPS85 = (1.0D0-STR85/FC___ ) / ZETA_ + EZERO
       EPS85 = -1.0D0 * EPS85 / EZERO
C
       ET___ = FLT__ / (2.0D0 * FC___ / EZERO)
C
       EPMXM = 0.0D0
C
       EPSON = (VAR0(1) + DEPS(1)) / EZERO
       EPSO1 = VAR0(1) / EZERO
       EPMAX = VAR0(2) / EZERO
       EPMIN = VAR0(3) / EZERO
       STMIN = VAR0(4) / FC___
       TANGE = VAR0(5)
       TANGD = VAR0(6)
       STRE1 = SIG0(1) / FC___
C
       CALL NEWBE2
     .       ( FC___, EZERO, EPS85, ZETA_, FLT__, FTPL_,
     .         TRAFA, EPSON, EPSO1, EPMAX, EPMIN, STRES,
     .         STRE1, STMIN, TANGE, TANGD, FACT1, FACT2,
     .         FACX2, FACLO, EPLA1, ET___ )
C
       IF (EPSON .LT. EPLA1 .OR. STMIN .GT. 0.0D0) DEPS(1) = 999D9
C
       VARF(1) = EPSON * EZERO
       VARF(2) = EPMAX * EZERO
       VARF(3) = EPMIN * EZERO
       VARF(4) = STMIN * FC___
       VARF(5) = TANGE
       VARF(6) = TANGD
C
       SIGF(1) = STRES * FC___
       SIGF(2) = SIG0(2) + GAMMA * DEPS(2)
       SIGF(3) = SIG0(3) + GAMMA * DEPS(3)
C
       RETURN
       END





