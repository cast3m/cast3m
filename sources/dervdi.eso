C DERVDI    SOURCE    STRU      08/12/22    21:15:08     6246
c     Sous-Programme du module MISTRAL0
C     --------------------------------------------------------------------------
      SUBROUTINE DERVDI (FI,TT,PSI,SEQ,EPSEQ,VEPSE, NDPI,NDVP,NXX,
     &                   PDVDI, PSIP,PSIP0)
C     --------------------------------------------------------------------------
C     Calcul des termes PSIP et PSIP0 de l'expression suivante de la derivee
C     par rapport au temps de la variable PSI de durcissement d'irradiation :
C                        dPSI/dt = PSIP + PSIP0*VEPSE(0)
C     en fonction :
C     - du flux de neutrons rapides FI,
C     - de la temperature TT,
C     - de la variable PSI,
C       - des contraintes equivalentes SEQ(IDP),
C       - des deformations equivalentes EPSEQ(IDP),
C     - des vitesse de deformations equivalentes VEPSE(IDP),
C     pour toutes les natures de deformations plastiques IDP,
C     - des parametres transmis dans PVDI,
C     NXX etant le nombre de natures de contraintes internes.
C     --------------------------------------------------------------------------
      IMPLICIT INTEGER (I-N)
      IMPLICIT REAL*8 (A-H, O-Z)
      PARAMETER ( NPTTM = 5 , NPPSIM = 6 )
      PARAMETER ( NPEDIM = 13+6*NPTTM+NPPSIM )
c     (NPEDIM = 49)
      PARAMETER ( NVM=2 , NPATDM = 14 , NPFIM = 9 )
      PARAMETER ( NPERVM = 11+NVM*(NPEDIM+2*NPATDM+NPFIM+11) )
c     (NPERVM = 205)
      DIMENSION SEQ(0:*),EPSEQ(0:*),VEPSE(0:*),PDVDI(1:*)
      DIMENSION PATDI(1:NPATDM),PFI(1:NPFIM),PERV(1:NPERVM)
C     ------------------------------------------------------------------
      NP = NDPI+NDVP+NXX+1
      KDI = NINT(PDVDI(NP))
      IF (KDI.EQ.0) THEN
       PSIP = FI
      ELSE
       PSIP = MAX(PDVDI(NP+1)+PDVDI(NP+2)*TT,0.D0)*FI
       NP = NP+2
      END IF
C     ------------------------------------------------------------------
      NP = NP+1
      LFATDI = NINT(PDVDI(NP+1))
      IF (LFATDI.EQ.0) THEN
       NP = NP+1
      ELSE
       NPATDI = NINT(PDVDI(NP))
       DO IP = 1,NPATDI
        PATDI(IP) = PDVDI(NP+IP)
       END DO
       NP = NP+NPATDI
       CALL FATDI (TT,0.D0,PSI, PATDI, PSIPTP)
       NP = NP+1
       LFFI = NINT(PDVDI(NP+1))
       IF (LFFI.EQ.0) THEN
        NP = NP+1
       ELSE
        NPFI = NINT(PDVDI(NP))
        DO IP = 1,NPFI
         PFI(IP) = PDVDI(NP+IP)
        END DO
        NP = NP+NPFI
        CALL FFLUX (FI, PFI, PSIPFI)
        PSIP = PSIP-PSIPTP*PSIPFI
       END IF
      END IF
C     ------------------------------------------------------------------
      PSIP0 = 0.
      NP = NP+1
      KRD = NINT(PDVDI(NP))
      IF (KRD.EQ.0) RETURN
      DO IDP = 1-NDPI,NDVP
       NP = NP+1
       LERV = NINT(PDVDI(NP+1))
       IF (LERV.EQ.0) THEN
        NP = NP+1
       ELSE
        NPERV = NINT(PDVDI(NP))
        DO IP = 1,NPERV
         PERV(IP) = PDVDI(NP+IP)
        END DO
        NP = NP+NPERV
        CALL ECOREV (SEQ(IDP),0.D0,EPSEQ(IDP),TT,FI,PSI, PERV, PSIPRD)
        IF (IDP.EQ.0) THEN
         PSIP = PSIP-PSIPRD*PDVDI(NP+1)
         PSIP0 = -PSIPRD*PDVDI(NP+2)
        ELSE
         PSIP = PSIP-PSIPRD*(PDVDI(NP+1)+PDVDI(NP+2)*VEPSE(IDP))
        END IF
        NP = NP+2
       END IF
      END DO
C     ------------------------------------------------------------------
        RETURN
        END

