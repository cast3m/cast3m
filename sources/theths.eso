C THETHS    SOURCE    BP208322  15/06/22    21:23:23     8543
      SUBROUTINE THETHS(MELE,VALMAT,NSTRS,TEMP,SD,RES,KERRE)
*******************************************************************
*  APPELE PAR THETAP :CONTRAINTES THERMIQUES POUR LES MATERIAUX
*  HOMOGENEISES ET SECTION
*
*   ENTREES :
*    MELE   = NUMERO DE L'ELEMENT FINI
*    VALMAT = TABLEAU DE MATERIAU
*    TEMP   = TEMPERATURES
*    SD     =  SECTION
*   SORTIES :
*    RES =  TABLEAU DE CONTRAINTES
*    KERRE= INDICATEUR D'ERREUR
*
*   P DOWLATYARI MARS 92
***********************************************************************
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*
-INC CCOPTIO
-INC CCHAMP
*
      DIMENSION RES(NSTRS)
      DIMENSION VALMAT(*)
*
      KERRE=0
*
*
*     TEST SUR LA FORMULATION
*
*____________________________________________________________________
*
*     SECTEUR DE CALCUL POUR LES ELEMENTS JOINTS 2D JOI2 ET JOI3
*____________________________________________________________________
*
      IF (MELE.EQ.85.OR.MELE.EQ.86) THEN
*
         ZKN = VALMAT(2)
         ALPN= VALMAT(3)
C
         IF (IFOUR.EQ.-2.OR.IFOUR.EQ.-1.OR.IFOUR.EQ.0) THEN
            RES(1)=0.D0
            RES(2)=ALPN * ZKN * TEMP
         ELSE
          KERRE=19
          GOTO 99
         ENDIF
*____________________________________________________________________
*
*     SECTEUR DE CALCUL POUR LES ELEMENTS JOINTS 3D JOI4
*____________________________________________________________________
*
      ELSE IF (MELE.EQ.88) THEN
          ZKN = VALMAT(3)
          ALPN= VALMAT(4)
         IF (IFOUR.EQ.2) THEN
            RES(1)=0.D0
            RES(2)=0.D0
            RES(3)=ALPN * ZKN * TEMP
         ELSE
          KERRE=19
          GOTO 99
         ENDIF
*____________________________________________________________________
*
*               ELEMENTS POUTRES ET TUYAUX
*____________________________________________________________________
*
      ELSEIF(MELE.EQ.29.OR.MELE.EQ.42)THEN
            YOU=VALMAT(1)
            ALPH=VALMAT(2)
            AUX = YOU*ALPH*SD
*
            RES(1)= AUX*TEMP
*
*____________________________________________________________________
*
*               ELEMENTS BARRES CERCES ET POINT
*____________________________________________________________________
*
      ELSEIF(MELE.EQ.46.OR.MELE.EQ.95.OR.MELE.EQ.45)THEN
*
       IF(MELE.EQ.45.AND.IFOUR.NE.-3) THEN
          KERRE=86
          GO TO 99
       ENDIF
       IF(MELE.EQ.95.AND.IFOUR.NE.0) THEN
          KERRE=86
          GO TO 99
       ENDIF
*
       YOU=VALMAT(1)
       ALPH=VALMAT(2)
       AUX = YOU*ALPH*SD
       RES(1)= AUX*TEMP
*
*____________________________________________________________________
*
*  CAS DE LA POUTRE TIMO - MODELE SECTION
*____________________________________________________________________
*
      ELSE IF(MELE.EQ.84) THEN
         RES(1)=SD * TEMP
         RES(2)=0.D0
         RES(3)=0.D0
         RES(4)=0.D0
         RES(5)=0.D0
         RES(6)=0.D0
      ELSE
*
*   CAS NON TRAITES
*
       KERRE=19
      ENDIF
*
 99   CONTINUE
      RETURN
      END





