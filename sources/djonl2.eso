C DJONL2    SOURCE    CHAT      05/01/12    22:52:31     5004
      SUBROUTINE DJONL2(SIGI,DEPST,VARI,XMAT,SIGF,VARF,DEFP,KERRE)
C-----------------------------------------------------------------------
C
C     ROUTINE DE CALCUL DE L'ECOULEMENT DANS LE JOINT
C
C     "DILATANT JOINT WITH COULOMBS FRICTION"
C     MODELE PROPOSED BY MF SNYMANN, WW BIRD AND JB MARTIN
C
C     WORK ONLY IN 2-D PLANE (STRAIN) STATE
C
C  INPUT
C     SIGI    INITIAL STRESS
C     DEPST   TRIAL INCREMENT OF ELASTIC STRAIN
C     XMAT    MATERIAL PROPERTIES
C     VARI    INITIAL INTERNAL VARIABLES
C
C  OUTPUT
C     SIGF    FINAL STRESS
C     VARF    FINAL INTERNAL VARIABLES
C     DEFP    INCREMENT OF PLASTIQUE STRAIN
C
C-----------------------------------------------------------------------
C  P.PEGON   OCTOBER 93
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
-INC CCREEL
*
      DIMENSION SIGI(*),DEPST(*),VARI(*),XMAT(*),
     .          SIGF(*),VARF(*) ,DEFP(*)
      REAL*8 KS,KN,MU
C
C  QUICK QUIT IN CASE OF DIMENSION ERROR
C
      IF (IFOUR.NE.-3.AND.IFOUR.NE.-2.AND.IFOUR.NE.-1)THEN
        KERRE=99
        RETURN
      ELSE
        KERRE=0
      ENDIF
C
C-----------------------------------------------------------------------
C  MATERIAL PARAMETERS
C  ===================
C
C      KS    = XMAT(1)   Shear modulus
C      KN    = XMAT(2)   Traction modulus
C      PHI   = XMAT(5)   Friction angle
C      MU    = XMAT(6)   Dilatation angle
C      FRTC  = XMAT(7)   Traction maximum (tan(PHI)*FRTC = cohesion)
C-----------------------------------------------------------------------
C  STRESS
C  ======
C
C      SMSN.  = SIG.(1)
C      SMN.   = SIG.(2)
C-----------------------------------------------------------------------
C  STRAIN
C  ======
C
C      DRSN   = EPS(1)
C      DRN    = EPS(2)
C-----------------------------------------------------------------------
C  INTERNAL VARIABLES
C  ==================
C
C      DS0.   = VAR.(1) POSITION OF THE VERTEX      (RELATIVE SHEAR)
C      DN0.   = VAR.(2) POSITION OF THE VERTEX      (EXTENSION)
C      DRSN.  = VAR.(3) CURRENT DEFORMATION         (RELATIVE SHEAR)
C      DRN.   = VAR.(4) CURRENT DEFORMATION         (EXTENSION)
C      DPSN.  = VAR.(5) CURRENT PLASTIC DEFORMATION (RELATIVE SHEAR)
C      DPN.   = VAR.(6) CURRENT PLASTIC DEFORMATION (EXTENSION)
C
C      HC11   = VAR.(7)
C      HC12   = VAR.(8) CURRENT VALUES OF THE
C      HC21   = VAR.(9) CONSISTENT HOOK MATRIX
C      HC22   = VAR.(10)
C-----------------------------------------------------------------------
       KS     = XMAT(1)
       KN     = XMAT(2)
       PSI    = ATAN(KN/KS*TAN(XMAT(5)*XPI/180))
       MU     = XMAT(6)*XPI/180
       FTRAC  = XMAT(7)
       DTRAC  = XMAT(7)/KN
C
       TANPSI = TAN(PSI)
       TANMU  = TAN(MU)
C
C      TOTAL "STRAIN" FROM THE STRESS INCREMENT
C
       DS0I   = VARI(1)
       DN0I   = VARI(2)
       DRSNF  = VARI(3) + DEPST(1)
       DRNF   = VARI(4) + DEPST(2)
       VARF(3)= DRSNF
       VARF(4)= DRNF
C
C      FIRST REGION
C
       IF ((DRNF-DTRAC).GE.(DN0I+ABS(DRSNF-DS0I)*TANMU))THEN
         VARF(5) = DRSNF
         VARF(6) = DRNF
         VARF(1) = DRSNF
         VARF(2) = DN0I+ABS(DRSNF-DS0I)*TANMU
         SIGF(1) = 0.D0
         SIGF(2) = FTRAC
C
         VARF(7) = 0.D0
         VARF(8) = 0.D0
         VARF(9) = 0.D0
         VARF(10)= 0.D0
C
C      SECOND REGION (ELASTIC CASE)
C
       ELSEIF((DRNF-DTRAC).LE.(DN0I-ABS(DRSNF-DS0I)/TANPSI))THEN
         VARF(5) = DS0I
         VARF(6) = DN0I
         VARF(1) = DS0I
         VARF(2) = DN0I
         SIGF(1) = KS * (DRSNF-DS0I)
         SIGF(2) = KN * (DRNF -DN0I)
C
         VARF(7) = KS
         VARF(8) = 0.D0
         VARF(9) = 0.D0
         VARF(10)= KN
C
C      THIRD REGION
C
       ELSE
         A=( (DRNF-DTRAC-DN0I)*SIN(PSI)+ABS(DRSNF-DS0I)*COS(PSI))
     >                                 /COS(PSI-MU)
         B=(-(DRNF-DTRAC-DN0I)*COS(MU )+ABS(DRSNF-DS0I)*SIN(MU ))
     >                                 /COS(PSI-MU)
         VARF(5) = DS0I + A * COS(MU) * SIGN(1.D0,DRSNF-DS0I)
         VARF(6) = DN0I + A * SIN(MU)
         VARF(1) = VARF(5)
         VARF(2) = VARF(6)
         SIGF(1) = KS * B * SIN(PSI) * SIGN(1.D0,DRSNF-DS0I)
         SIGF(2) = KN * B * COS(PSI) * (-1) + FTRAC
C
         COHOOK  = KN/(1+TANPSI*TANMU)
         TANPHI  = TANPSI*KS/KN
         VARF(7) = COHOOK*TANPHI*TANMU
         VARF(8) =-COHOOK*SIGN(1.D0,DRSNF-DS0I)*TANPHI
         VARF(9) =-COHOOK*SIGN(1.D0,DRSNF-DS0I)*TANMU
         VARF(10)= COHOOK
C
       ENDIF
C
C      INCREMENT OF PLASTIC STRAIN
C
       DEFP(1) = VARF(5) - VARI(5)
       DEFP(2) = VARF(6) - VARI(6)
C
       RETURN
       END




