C MASBA3    SOURCE    CHAT      05/01/13    01:30:56     5004
C MASBA3    SOURCE    DJER      93/03/02    22:53:00
      SUBROUTINE MASBA3(REL,LRE,ROS,XE,TGL,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DE LA MATRICE DE MASSE ELEMENTAIRE DE L ELEMENT DE
C  BARRE A 3 NOEUDS
C
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          ROS     RHO * SECT
C          XE      COORDONNEES DES 3 NOEUDS
C
C  SORTIE
C          REL     MATRICE DE MASSE ELEMENTAIRE
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
      DIMENSION REL(LRE,LRE),TGL(3,LRE),XE(3,3)
      dimension xma(3,3),xga(3),pga(3),pn(3,3),dn(3,3),xj(3)
      pga(1)=0.5555555555555555D0 *ros
      pga(3)=pga(1)
      pga(2)=0.8888888888888888d0*ros
      xga(1)=-0.774596669241483D0
      xga(3)=-xga(1)
      xga(2)=0.d0
      do 100 i=1,3
      xx = xga(i)
      pn(i,1)= 0.5d0 * xx * ( xx -1.d0)
      pn(i,2)= -(xx -1.d0)*(xx+1.d0)
      pn(i,3)= 0.5d0 *xx*(xx+1.d0)
      dn(i,1)=xx-0.5d0
      dn(i,2)= -2.d0* xx
      dn(i,3)=xx +0.5d0
  100 continue
      do 101 i=1,3
      dx = dn(i,1)*xe(1,1)+dn(i,2)*xe(1,2)+dn(i,3)*xe(1,3)
      dy = dn(i,1)*xe(2,1)+dn(i,2)*xe(2,2)+dn(i,3)*xe(2,3)
      dz = dn(i,1)*xe(3,1)+dn(i,2)*xe(3,2)+dn(i,3)*xe(3,3)
      ds2 = dx*dx + dy * dy + dz*dz
      ds = sqrt(ds2)
      xj(i)=ds
  101 continue
      do 103 i=1,3
      do 103 j=1,3
  103 xma(i,j)=0.d0
      do 102 ipg=1,3
          xjpg=xj(ipg)*pga(ipg)
        do 102 i=1,3
         do 102 j=1,3
       xma(i,j)=xma(i,j)+ xjpg*pn(ipg,i)*pn(ipg,j)
 102  continue
C
      KERRE=0
C
      do 60 I=1,3
       i1=(i-1)*3
      do 60 j=1,3
         j1=(j-1)*3
       do 60 k=1,3
       ii= i1 +k
       jj = j1 + k
       rel(ii,jj)=xma(i,j)
   60  continue
      RETURN
 999  KERRE=1
      RETURN
      END


