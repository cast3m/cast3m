C KDOM4     SOURCE    KK2000    14/04/10    21:15:13     8032
      SUBROUTINE KDOM4(IP1,IP2,IP3,IP4,XCEN,VOL)
C
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  KDOM4
C
C DESCRIPTION       :  Subroutine called by KDOM3
C                      Given a tetrahedra with base (IP1,IP2,IP3,IP4)
C                      this subroutine compute its volume and its mass
C                      center.
C                      N.B.: VOL is positive if and only if (IP1-IP3),
C                            (IP2 - IP3), (IP4 - IP3) are "right-hand"
C                            oriented vectors (positive mixed product)
C
C
C LANGAGE           :  FORTRAN 77 + ESOPE 2000 (avec estensions CISI)
C
C AUTEUR            :  A. BECCANTINI, DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
      INTEGER IP1,IP2,IP3,IP4, I1
      REAL*8 XCEN(3), VOL, DHX,DHY,DHZ,DAX,DAY,DAZ,DBX,DBY,DBZ
-INC CCOPTIO
-INC SMCOORD
C
      DHX=XCOOR((IP4-1)*(IDIM+1)+1)-XCOOR((IP3-1)*(IDIM+1)+1)
      DHY=XCOOR((IP4-1)*(IDIM+1)+2)-XCOOR((IP3-1)*(IDIM+1)+2)
      DHZ=XCOOR((IP4-1)*(IDIM+1)+3)-XCOOR((IP3-1)*(IDIM+1)+3)
C
      DAX=XCOOR((IP1-1)*(IDIM+1)+1)-XCOOR((IP3-1)*(IDIM+1)+1)
      DAY=XCOOR((IP1-1)*(IDIM+1)+2)-XCOOR((IP3-1)*(IDIM+1)+2)
      DAZ=XCOOR((IP1-1)*(IDIM+1)+3)-XCOOR((IP3-1)*(IDIM+1)+3)
      DBX=XCOOR((IP2-1)*(IDIM+1)+1)-XCOOR((IP3-1)*(IDIM+1)+1)
      DBY=XCOOR((IP2-1)*(IDIM+1)+2)-XCOOR((IP3-1)*(IDIM+1)+2)
      DBZ=XCOOR((IP2-1)*(IDIM+1)+3)-XCOOR((IP3-1)*(IDIM+1)+3)
      VOL=(DAX*DBY*DHZ+DAY*DBZ*DHX+DAZ*DBX*DHY-
     &     (DAZ*DBY*DHX+DAX*DBZ*DHY+DAY*DBX*DHZ))/6.0D0
      DO I1=1,3,1
         XCEN(I1)=(XCOOR((IP4-1)*(IDIM+1)+I1)+
     &        XCOOR((IP3-1)*(IDIM+1)+I1)+
     &        XCOOR((IP1-1)*(IDIM+1)+I1)+
     &        XCOOR((IP2-1)*(IDIM+1)+I1))/4.0D0
      ENDDO
C
      RETURN
C
      END



