C PLAQP     SOURCE    CB215821  16/04/21    21:18:02     8920
      SUBROUTINE PLAQP(EC,QT,FNE,SNNP10,EPCO,XLAM3,SNNP11,EPCOF,XLAM3F)
C-----------------------------------------------------------------------
C
C      DANS LE CADRE DU MODELE 3D MOHR COULOMB ELEMENTS JOINTS JOI1
C       PLASTICITE EN COMPRESSION ECROUISSAGE ISOTROPE LINEAIRE
C
C-----------------------------------------------------------------------
C ENTREES
C     EC      = RAIDEUR A L'ORIGINE ELASTIQUE   °      °
C     QT      = RAIDEUR TANGENTE RELIANT        F A EPS_TOTAL
C     FNE     = LIMITE D'ELASTICITE
C
C     SNNP10  = ESTIMATION ELASTIQUE DE LA CONTRAINTE FINALE
C     EPCO    = DEFORMATION PLASTIQUE EN COMPRESSION AU DEBUT DU PAS
C     XLAM3   = MULTIPLICATEUR PLASTIQUE AU DEBUT DU PAS
C
C SORTIES
C     SNNP11  = CONTRAINTE FINALE A LA FIN DU PAS
C     EPCOF   = DEFORMATION PLASTIQUE EN COMPRESSION A LA FIN DU PAS
C     XLAM3F  = MULTIPLICATEUR PLASTIQUE A LA FIN DU PAS
C-----------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
      PARAMETER(PRECIS=1D-6,XZER=0D0,IMAX=200)
C
C
      H=EC*QT/(EC-QT)
C
      SNNP11=SNNP10
      EPCOF=EPCO
      XLAM3F=XLAM3
C
      F3=ABS(SNNP10)-H*XLAM3-FNE

      IF (F3.LE.PRECIS) GOTO 900
C
C
      I=0
   10 CONTINUE
      I=I+1
      IF (I.GT.IMAX) THEN
         PRINT *,' LE CRITERE PLASTIQUE EN COMPRESSION EST VIOLE '
         PRINT *,' DIVERGENCE APRES ',IMAX,' ITERATIONS INTERNES'
         STOP
      ENDIF
C
      DF3DF=SNNP11/(ABS(SNNP11))
      DLAM3=F3/(EC+QT)
      XLAM3F=XLAM3F+DLAM3
      DEPCO=DLAM3*DF3DF
      EPCOF=EPCOF+DEPCO
      SNNP11=SNNP11-EC*DLAM3*DF3DF
      F3=ABS(SNNP11)-H*XLAM3F-FNE
      IF (ABS(F3).GT.PRECIS) GOTO 10
C
  900 CONTINUE
C
      RETURN
      END



