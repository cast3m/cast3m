C ELPDOP    SOURCE    KK2000    14/04/09    21:15:25     8027
      SUBROUTINE ELPDOP(P0,XN0,QI,XN,XT,XD,XNU,COP,CB,ISTAT)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-B,D-H,O-Z)
      IMPLICIT COMPLEX*16(C)
************************************************************************
*
*    PLAQUES PAR EQUATION INTEGRALE:
*
*       CALCUL DES OPERATEURS PLAQUE ET DE LEURS DERIVEES
*       POUR UN COUPLE DE PTS P0 , QI
*
************************************************************************
*
*
          DIMENSION P0(2)
          DIMENSION QI(2)
          DIMENSION COP(8)
*
          DIMENSION XN (2)
          DIMENSION XN0(2)
*
          DIMENSION XT (2)
C          DIMENSION XT0(2)
          DIMENSION CG (5)
          DIMENSION XG (5)
          DIMENSION XR (2)
*
* 1- CALCUL DE R ET DU VECTEUR UNITAIRE SUIVANT R
*
         R= ( ( QI(1) - P0(1) ) ** 2 + (QI(2) - P0(2) ) ** 2 ) **.5
         XR(1) = (QI(1) - P0(1)) / R
         XR(2) = (QI(2) - P0(2)) / R
*
*
*
*
* 2- ON CALCULE LES FONCTIONS DE GREEN ET LEURS DERIVEES,R
*
       IF ( ISTAT.EQ.1) THEN
         CALL ELPGFS(R,XG)
         DO  10 I=1,5
           CG(I) = XG(I)*(1d0,0d0)
 10      CONTINUE
       ELSE
         CALL ELPDGF(R,CB,CG)
       ENDIF
*
* 3-   CALCUL DE V
*
       COP(1) = CG(1)
*
* 4-   CALCUL DE V,N
*
       CALL ELPDDR(CG,R,XR,XN,XN,XN,XN,1,CAUX1)
       COP(2) = CAUX1
*
* 5-   CALCUL DE MN= -D * (V,NN + NU* V,TT)
*
       CALL ELPDDR(CG,R,XR,XN,XN,XN,XN,2,CAUX1)
       CALL ELPDDR(CG,R,XR,XT,XT,XN,XN,2,CAUX2)
       COP(3) = CMPLX(-1d0)* XD* ( CAUX1 +  XNU*CAUX2)
*
* 6-   CALCUL DE KN= -D * (V,NNN + (2 - NU)* V,NTT)
*
       CALL ELPDDR(CG,R,XR,XN,XN,XN,XN,3,CAUX1)
       CALL ELPDDR(CG,R,XR,XN,XT,XT,XN,3,CAUX2)
       COP(4) = CMPLX(-1d0)* XD* ( CAUX1 +  (CMPLX(2d0) - XNU) *CAUX2)
*****
*      TERMES DERIVES
*****
* 7-   CALCUL DE V,N0
*
*
*
*
       CALL ELPDDR(CG,R,XR,XN0,XN,XN,XN,1,CAUX1)
       COP(5) = - CAUX1
*
* 8-   CALCUL DE V,NN0
*
       CALL ELPDDR(CG,R,XR,XN,XN0,XN,XN,2,CAUX1)
       COP(6) =  - CAUX1
*
* 9-   CALCUL DE MN,N0
*
       CALL ELPDDR(CG,R,XR,XN,XN,XN0,XN,3,CAUX1)
       CALL ELPDDR(CG,R,XR,XT,XT,XN0,XN,3,CAUX2)
       COP(7) = CMPLX(-1d0)* XD* ( - CAUX1 -  XNU*CAUX2)
*
* 9-   CALCUL DE KN,N0
*
       CALL ELPDDR(CG,R,XR,XN,XN,XN,XN0,4,CAUX1)
       CALL ELPDDR(CG,R,XR,XN,XT,XT,XN0,4,CAUX2)
       COP(8) = CMPLX(-1d0)* XD* (-  CAUX1 -  (CMPLX(2d0) - XNU) *CAUX2)

       RETURN
      END


