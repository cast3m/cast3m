C TIFCOR    SOURCE    CHAT      05/01/13    03:38:45     5004
      SUBROUTINE TIFCOR(REL,LRE,CARA,COORD,VROT,
     >           WORK,LHOOK,DDHOOK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DE LA MATRICE DE COUPLAGE GYROSCOPIQUE DE L ELEMENT DE
C  POUTRE DE TIMOSCHENKO EN BIFLEXION DANS LES AXES GLOBAUX
C
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          VECT    DIRECTION DE L'OY LOCAL
C          OMEG    VITESSE DE ROTATION
C          COORD   COORDONNEES DES 2 NOEUDS
C          DDHOOK(LHOOK,LHOOK) MATRICE DE "HOOK"
C
C  SORTIE
C          REL     MATRICE DE RAIDEUR ELEMENTAIRE
C          KERRE   INDICE NON NUL SI PROBLEME , NUL SINON
C  TRAVAIL
C          WORK(300)
C
C-----------------------------------------------------------------------
C     Didier Combescure Mars 2003
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(LRE,*),COORD(3,*),CARA(*),VECT(3),P(3,3),
     .          XE(2),YE(2),ZE(2),WORK(*),VROT(*),VROTL(3)
C
      VECT(1) = CARA(2)
      VECT(2) = CARA(3)
      VECT(3) = CARA(4)
C
      KERRE=0
      DO 2 I=1,2
      XE(I)=COORD(1,I)
      YE(I)=COORD(2,I)
  2   ZE(I)=COORD(3,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2+(ZE(2)-ZE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPAS(XE,YE,ZE,VECT,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C   CALCUL DU VECTEUR ROTATION DANS LE REPERE LOCAL
C
      CALL VECROT(VROT,VROTL,P)
C
C  CALCUL DE LA MATRICE DE COUPLAGE GYROSCOPIQUE EN AXES LOCAUX
C
      CALL TIFCOL(WORK,XL,LHOOK,DDHOOK,VROTL)
C
C  PASSAGE DE LA MATRICE DE MASSE EN AXES GLOBAUX
C
      CALL POUROG(REL,LRE,P,WORK)
C
C
C
      RETURN
999   KERRE=1
      RETURN
      END


