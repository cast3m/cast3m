C FIBFRA    SOURCE    CHAT      12/04/06    21:15:11     7348
C FIBFRA    SOURCE    DC        98/01/26
      SUBROUTINE FIBFRA(XMAT,DEPS,SIG0,VAR0,SIGF,VARF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(*),DEPS(3),SIG0(3),VAR0(4),SIGF(3),VARF(4)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE FRAGILE EN COMPRESSION ET TRACTION
C     MODELE PLASTIQUE POUR LE RENFORCEMENT TFC
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - Janvier 1998
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C      XMAT( 3) RHO
C      XMAT( 4) ALPHA
C      XMAT( 5) FC
C      XMAT( 6) FC_R
C      XMAT( 7) STC
C      XMAT( 8) FT
C      XMAT( 9) FT_R
C      XMAT(10) STT
C
C      DEPS(1) EPXX    SIG0(1) SIGF(1) SMXX
C      DEPS(2) GAXY    SIG0(2) SIGF(2) SMXY
C      DEPS(3) GAXZ    SIG0(3) SIGF(3) SMXZ
C
C                      VAR0(1) VARF(1) EPSO ALLONGEMENT TOTAL
C                      VAR0(2) VARF(2) DAMC ENDOMMAGEMENT EN COMP.
C                      VAR0(3) VARF(3) DAMT ENDOMMAGEMENT EN TRACT.
C
C----------------------------------------------------------------------
       GAMMA=XMAT(1)/2.D0/(XMAT(2)+1.D0)
       EYOUN = XMAT( 1)
       FC = (-1.D0)*XMAT(5)
       FC_R = (-1.D0)*XMAT(6)
       STC = (-1.D0)*XMAT(7)
       XEPSOC = FC/EYOUN
       FT = XMAT(8)
       FT_R = XMAT(9)
       STT = XMAT(10)
       XEPSOT = FT/EYOUN
C
       XDAMC =  VAR0(2)
       XDAMT =  VAR0(3)
C
       XEPSO = VAR0(1) + DEPS(1)
C
C        CALCUL DE LA VARIABLE D'ENDOMMAGEMENT
C
       IF (XEPSO.GT. 0.D0) THEN
C
C         CAS DE LA TRACTION
C
          XDAMT0 = 1.0D0  - ( (XEPSOT/XEPSO)* ( (FT_R / FT) +
     & ((1.D0 - (FT_R / FT))*EXP((XEPSOT - XEPSO)/ STT) )) )
C
         IF (XDAMT0. GT . XDAMT)  THEN
           XDAMT = XDAMT0
         ENDIF
C
         XDAM = XDAMT
C
       ELSEIF (XEPSO.LT. 0.D0) THEN
C
C        CAS DE LA COMPRESSION
C
          XDAMC0 = 1.0D0  - ( (XEPSOC/XEPSO)* ( (FC_R /FC) +
     & ((1.D0 - (FC_R /FC))*EXP((XEPSOC - XEPSO)/STC) )) )
C
         IF (XDAMC0. GT . XDAMC)  THEN
           XDAMC = XDAMC0
         ENDIF
C
         XDAM = XDAMC
C
       ELSE
         XDAM= 0.D0
       ENDIF
C
C
       SIGF(1)= EYOUN* (1.D0 - XDAM) * XEPSO
C
       SIGF(2) = SIG0(2) + GAMMA   * DEPS(2)
       SIGF(3) = SIG0(3) + GAMMA   * DEPS(3)
C
       VARF(1) = XEPSO
       VARF(2) = XDAMC
       VARF(3) = XDAMT
       VARF(4) = (1.D0 - XDAM) * EYOUN
C
       RETURN
       END




