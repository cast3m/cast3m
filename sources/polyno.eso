C POLYNO    SOURCE    CHAT      05/01/13    02:17:21     5004
      FUNCTION POLYNO (COEFP,LCOEFP,ICOEFP,VAR)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
************************************************************************
*
*                             P O L Y N O
*                             -----------
*
* FONCTION:
* ---------
*
*     DONNER LA VALEUR D'UN POLYNOME POUR UN NOMBRE DONNE.
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE   (+ = CONTENU DANS UN COMMUN)
* -----------
*
*     COEFP   (E)  COEFFICIENTS DU POLYNOME (EN COMMENCANT PAR LA
*                  PUISSANCE "0").
*     LCOEFP  (E)  NOMBRE DE COEFFICIENTS UTILES DU TABLEAU "COEFP".
*     ICOEFP  (E)  INCREMENT ENTRE 2 COEFFICIENTS UTILES DU TABLEAU
*                  "COEFP". LE 1ER COEFFICIENT SE TROUVE EN POSITION 1.
*     VAR     (E)  NOMBRE EN LEQUEL ON CHERCHE LA VALEUR DU POLYNOME.
*     POLYNO  (S)  VALEUR DU POLYNOME.
*                  = 0  SI LE TABLEAU "COEFP" EST VIDE.
*
      INTEGER LCOEFP,ICOEFP
      DIMENSION COEFP(*)
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     PASCAL MANIGOT     31 JUILLET 1986
*     P.M. 19/02/87: AJOUT DE LA VARIABLE "ICOEFP".
*
* LANGAGE:
* --------
*
*     FORTRAN77 + EXTENSION: DECLARATION "REAL*8".
*
************************************************************************
*
      ZZ = 0.D0
      INIT = (LCOEFP-1) * ICOEFP + 1
      IPAS = - ICOEFP
      DO 100 IB=INIT,1,IPAS
         ZZ = ZZ * VAR + COEFP(IB)
  100    CONTINUE
*     END DO
      POLYNO = ZZ
*
      END

