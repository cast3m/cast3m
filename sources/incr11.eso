C INCR11    SOURCE    PV        11/03/07    21:16:53     6885
       SUBROUTINE  INCR11(DT,SIG,EPSV,VAR,EPSVPT,VARPT,XMAT,NSTRS0,
     &                    NVARI,MFR)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
       DIMENSION SIG(*),EPSV(*),VAR(*),XMAT(*),EPSVPT(*),VARPT(*)
       DIMENSION SS(6),EPLU(6),EMIN(6),EPSCRP(6)
       DIMENSION SIG0(6),EPSV0(6),EPS0(6)
C
C ICI : le modele est obligatoirement CERAMIQUE
C _________________________________________________________________
C
C  THIS ROUTINE CALCULATES INCREMENTAL STRAINS & INTERNAL VARIABLES
C _________________________________________________________________
C
C STEP-1 : EXTRACT EPLUS , EMINUS & EPSCRP
C ----------------------------------------
       IF (MFR.EQ.5) THEN
         NSTRS=6
         SIG0(1)=SIG(1)
         SIG0(2)=SIG(2)
         SIG0(3)=0.D0
         SIG0(4)=SIG(3)
         SIG0(5)=SIG(4)
         SIG0(6)=SIG(5)
         EPSV0(1)=EPSV(1)
         EPSV0(2)=EPSV(2)
         EPSV0(3)=0.D0
         EPSV0(4)=EPSV(3)
         EPSV0(5)=EPSV(4)
         EPSV0(6)=EPSV(5)
       ELSE
         NSTRS=NSTRS0
         DO 10 I=1,NSTRS
           SIG0(I)=SIG(I)
           EPSV0(I)=EPSV(I)
  10     CONTINUE
       ENDIF
C
CCC        IF ((IFOUR.EQ.2.AND.MFR.EQ.3).OR.IFOUR.EQ.-2) THEN
CCC        IBID = 11
CCC        ELSE IF(IFOUR.EQ.-3.OR.IFOUR.EQ.-1
CCC     1 .OR.IFOUR.EQ.0) THEN
CCC        IBID = 14
CCC        ELSE IF(IFOUR.EQ.2.AND.MFR.EQ.1) THEN
CCC        IBID = 19
CCC        ENDIF
CCC Eloi : correction de IBID
CCC        de plus : ici on a obligatoirement MFR different de 3
       IBID=NVARI-(2*NSTRS)-5
C
       DO 20 I=1,NSTRS
         EPLU(I)=VAR(I+IBID)
         EMIN(I)=VAR(NSTRS+I+IBID)
  20   CONTINUE
       EEFF = VAR(2*NSTRS+1+IBID)
       LL = nint(VAR(2*NSTRS+3+IBID))
C
       DO 30 I=1,NSTRS
         EPS0(I)=0.0D0
         EPSCRP(I)=EPSV0(I)
  30   CONTINUE
C
       DO 40 I=1,NVARI
         VARPT(I)=0.D0
  40   CONTINUE
C_____________________________________________________________________
C
C STEP-2 : COMPUTATION OF SEQUV
C ------------------------------
       SIGM=0.333333333333333D0*(SIG0(1)+SIG0(2)+SIG0(3))
       DO 50 I=1,NSTRS
         A=0.D0
         IF(I.LE.3) A=1.D0
         SS(I)=SIG0(I)-A*SIGM
  50   CONTINUE
       SEQUV = PROCON(SS,SS,NSTRS)
       SEQUV = SQRT(1.5D0*SEQUV)
C_____________________________________________________________________
C
C STEP-3 : UPDATING INTERNAL VARIABLES
C ------------------------------------
       CALL UPVAR1(SIG0,EPSCRP,EPLU,EMIN,EEFF,EPSH,LL,NSTRS)
       DO 60 I=1,NSTRS
         VARPT(      I+IBID)=EPLU(I)
         VARPT(NSTRS+I+IBID)=EMIN(I)
  60   CONTINUE
       VARPT(2*NSTRS+1+IBID)=EEFF
       VARPT(2*NSTRS+2+IBID)=EPSH
       VARPT(2*NSTRS+3+IBID)=LL
C_____________________________________________________________________
C
C STEP-4 : COMPUTATION OF INELASTIC STRAIN RATE
C ---------------------------------------------
       IF(SEQUV.NE.0.D0) THEN
         IF(MFR.EQ.1.AND.IFOMOD.EQ.2) THEN
           IBIDI = 15
         ELSE
           IBIDI = 10
         ENDIF
         IBID1 = IBIDI + 1
         IBID2 = IBIDI + 2
         IBID3 = IBIDI + 3
         CALL NORTON(SEQUV,EPSH,DT,XMAT(IBID1),XMAT(IBID2),XMAT(IBID3),
     &               DECT)
         VARPT(1)=DECT
         VARPT(NVARI) = DECT
         CONS=1.5D0*DECT/SEQUV
         DO 70 I=1,NSTRS
           EPS0(I)=CONS*SS(I)
  70     CONTINUE
C
C_______________________________________________________________________
       ENDIF
C
       IF (MFR.EQ.5) THEN
         EPSVPT(1)=EPS0(1)
         EPSVPT(2)=EPS0(2)
         EPSVPT(3)=EPS0(4)
         EPSVPT(4)=EPS0(5)
         EPSVPT(5)=EPS0(6)
       ELSE
         DO 80 I=1,NSTRS
           EPSVPT(I)=EPS0(I)
  80     CONTINUE
       ENDIF
C
       RETURN
       END



