C TOMIXTC   SOURCE    SERRE     16/12/13    21:15:12     9254
      SUBROUTINE TOMIXTC(MTAB2,MTAB3,MTAB5,IPT1,MPOVA1,
     &                  MPOVAA,MPOVAB,MPOVAC,MPOVAD,MPOVAE,
     &                  MPOVAH,MPOVAG,MPOVAI,MPOVAJ)
C--------------------------------------------------------------------
C Evaluation des propriétés du mélange pour une composition donnée
C--------------------------------------------------------------------
C
C E/  MTAB2  : Table INCO
C E/  MTAB3  : Table COMPONENT
C E/  MTAB5  : Table PHYSICAL
C E/  IPT1   : Maillage POI1 des CELLs
C E/  MPOVA1 : Densité du mélange
C  /S MPOVAA : Lambda du mélange
C  /S MPOVAB : Mu du mélange
C  /S MPOVAC : Nu du mélange
C  /S MPOVAD : Constante des gaz parfait des incondensables
C  /S MPOVAE : Densité des incondensables
C  /S MPOVAH : Capacité thermique du mélange
C  /S MPOVAG : Constante des gaz parfait du mélange
C  /S MPOVAI : Masse molaire des incondensables
C  /S MPOVAJ : Masse molaire de la vapeur d'eau
C
C---------------------------------------------------------------------
C MPOVA2 : Densité du constituant i (table INCO, indice Ri)
C MPOVAF : Capacité thermique des incondensables (calculé ici)
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C Auteurs : F.DABBENE 01/00
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*4  NOMTOT(1)
      CHARACTER*8  TYPE,TYPS,MOT1,MOT2,MOT5,MOT7
      POINTEUR MPOVAA.MPOVAL,MPOVAB.MPOVAL,MPOVAC.MPOVAL,
     &         MPOVAD.MPOVAL,MPOVAE.MPOVAL,MPOVAF.MPOVAL,
     &         MPOVAG.MPOVAL,MPOVAH.MPOVAL,MPOVAI.MPOVAL,
     &         MPOVAJ.MPOVAL
C
-INC CCOPTIO
-INC SMCHPOI
-INC SMELEME
-INC SMTABLE
C
      SEGACT IPT1
      NBCELL = IPT1.NUM(/2)
      SEGACT MTAB3
      IDIM3 = MTAB3.MLOTAB
      N    = NBCELL
      NC   = 1
      SEGINI MPOVAA
      SEGINI MPOVAB
      SEGINI MPOVAC
      SEGINI MPOVAD
      SEGINI MPOVAE
      SEGINI MPOVAF
      SEGINI MPOVAH
      SEGINI MPOVAG
      SEGINI MPOVAI
      SEGINI MPOVAJ
C MCHPO5 : TGAS
      TYPE = 'CHPOINT '
      MOT2 = 'TGAS' // '    '
      CALL ACMO(MTAB2,MOT2,TYPE,MCHPO5)
      IF (IERR.NE.0) RETURN
      CALL LRCHT(MCHPO5,MPOVA5,TYPS,IGEO1)
      IF (IGEO1.NE.IPT1) THEN
         INDIC  =  1
         NBCOMP = -1
         CALL QUEPOI(MCHPO5,IPT1,INDIC,NBCOMP,NOMTOT)
         IF (IERR.NE.0) RETURN
         CALL LRCHT(MCHPO5,MPOVA5,TYPS,IGEO1)
      ENDIF
C MCHPO2 : Densité du constituant i
C MCHCP  : CPH2O = vari dhvdt pvap1 tgas
      DO 40 I=1,IDIM3
         CALL ACEM(MTAB3,I,MOT1)
C
         MOT2 = 'R'   // MOT1(1:7)
         TYPE = 'CHPOINT '
         CALL ACMO(MTAB2,MOT2,TYPE,MCHPO2)
         IF (IERR.NE.0) RETURN
         CALL LRCHT(MCHPO2,MPOVA2,TYPS,IGEO1)
         IF (IGEO1.NE.IPT1) THEN
            INDIC  =  1
            NBCOMP = -1
            CALL QUEPOI(MCHPO2,IPT1,INDIC,NBCOMP,NOMTOT)
            IF (IERR.NE.0) RETURN
            CALL LRCHT(MCHPO2,MPOVA2,TYPS,IGEO1)
         ENDIF
C
         CALL ACMO(MTAB2,'CPH2O',TYPE,MCHPO3)
         IF (IERR.NE.0) RETURN
         CALL LRCHT(MCHPO3,MPOVA3,TYPS,IGEO1)
         IF (IGEO1.NE.IPT1) THEN
            INDIC  =  1
            NBCOMP = -1
            CALL QUEPOI(MCHPO3,IPT1,INDIC,NBCOMP,NOMTOT)
            IF (IERR.NE.0) RETURN
            CALL LRCHT(MCHPO3,MPOVA3,TYPS,IGEO1)
         ENDIF
C
         MOT5 = 'R_'  // MOT1(1:6)
         CALL ACMF(MTAB5,MOT5,RI)
         IF (IERR.NE.0) RETURN
C
         MOT7 = 'W'   // MOT1(1:7)
         CALL ACMF(MTAB5,MOT7,WI)
         IF (IERR.NE.0) RETURN
         CALL ACMF(MTAB5,'WH2O',WH2O)
         IF (IERR.NE.0) RETURN
C
         DO 30 J=1,NBCELL
            TGAS1  = MPOVA5 . VPOCHA(J,1)
            TMOIN1 = TGAS1 - 273.15D0
C
C FLO3 : Lambda pour l'espece i au point j a TGAS(j)
C FLO4 : Mu pour l'espece i au point j a TGAS(j)
C CPI  : Cp pour l'espece i au point j a TGAS(j)
           IF (MOT1(1:2) . EQ . 'H2') THEN
            FLO3 = (1.65D-3 * (TGAS1 ** 1.8D0)) / (TGAS1 - 40.13D0)
            FLO4 = (0.1541D-6 * (TGAS1 ** 1.703D0)) / (TGAS1 - 17.856D0)
            CPI  = 1000.D0 * (14.246D0 + (8.160D-4 * TMOIN1) +
     &             (4.350D-7 * (TMOIN1 * TMOIN1))) ;
           ENDIF
           IF (MOT1(1:2) . EQ . 'HE') THEN
            FLO3 = (5.464D-3 * (TGAS1 ** 1.616D0)) / (TGAS1 + 76.6D0)
            FLO4 = (0.5865D-6 * (TGAS1 ** 1.630D0)) / (TGAS1 + 27.183D0)
            CPI  = 5193.D0 ;
           ENDIF
           IF (MOT1(1:3) . EQ . 'CO2') THEN
            FLO3 = (25.438D-3 * (TGAS1 ** 1.319D0)) /(TGAS1 + 2432.95D0)
            FLO4 = (0.136D-6 * (TGAS1 ** 1.810D0)) / (TGAS1 - 59.30)
            CPI  = 1000.D0 * (0.836D0 + (7.432D-4 * TMOIN1) +
     &             (-2.511D-7 * (TMOIN1 * TMOIN1))) ;
           ENDIF
           IF (MOT1(1:2) . EQ . 'CO') THEN
            FLO3 = (1.931D-3 * (TGAS1 ** 1.541D0)) / (TGAS1 + 218.17D0)
            FLO4 = (0.573D-6 * (TGAS1 ** 1.620D0)) / (TGAS1 + 46.45D0)
            CPI  = 1000.D0 * (1.0302D0 + (2.396D-4 * TMOIN1) +
     &             (-4.950D-8 * (TMOIN1 * TMOIN1))) ;
           ENDIF
           IF (MOT1(1:2) . EQ . 'O2') THEN
            FLO3 = (0.429D-3 * (TGAS1 ** 1.746D0)) / (TGAS1 + 41.27D0)
            FLO4 = (0.7406D-6 * (TGAS1 ** 1.610D0)) / (TGAS1 + 48.16D0)
            CPI  = 1000.D0 * (0.917D0 + (1.404D-4 * TMOIN1) +
     &             (3.000D-8 * (TMOIN1 * TMOIN1)))
           ENDIF
           IF (MOT1(1:2) . EQ . 'N2') THEN
            FLO3 = (0.0417D-3 * (TGAS1 ** 2.042D0)) / (TGAS1 - 133.D0)
            FLO4 = (1.791D-6 * (TGAS1 ** 1.469D0)) / (TGAS1 + 140.812D0)
            CPI  = 1000.D0 * (1.0309D0 + (2.044D-4 * TMOIN1) +
     &             (-3.300D-8 * (TMOIN1 * TMOIN1)))
           ENDIF
           IF (MOT1(1:3) . EQ . 'H2O') THEN
            FLO3 = (-84.442D-3 * (TGAS1 ** 1.251D0)) /(TGAS1 - 5827.6D0)
            FLO4 = (2.142D-6 * (TGAS1 ** 1.5D0)) / (TGAS1 + 890.D0)
            CPI  = 1000.D0 * (1.748383D0 + (0.277768D-3 * TMOIN1) +
     &             (0.275D-6 * (TMOIN1 * TMOIN1)))
           ENDIF
C
            MPOVAA . VPOCHA(J,1) = MPOVAA . VPOCHA(J,1)
     &                           + MPOVA2 . VPOCHA(J,1) *  FLO3  * RI
            MPOVAB . VPOCHA(J,1) = MPOVAB . VPOCHA(J,1)
     &                           + MPOVA2 . VPOCHA(J,1) *  FLO4 * RI
            MPOVAG . VPOCHA(J,1) = MPOVAG . VPOCHA(J,1)
     &                           + RI * MPOVA2 . VPOCHA(J,1)
            MPOVAJ . VPOCHA(J,1) =  WH2O
            IF (MOT1(1:3) . NE . 'H2O') THEN
               MPOVAD . VPOCHA(J,1) = MPOVAD . VPOCHA(J,1)
     &                              + RI * MPOVA2 . VPOCHA(J,1)
               MPOVAE . VPOCHA(J,1) = MPOVAE . VPOCHA(J,1)
     &                              + MPOVA2 . VPOCHA(J,1)
               MPOVAF . VPOCHA(J,1) = MPOVAF . VPOCHA(J,1)
     &         + (MPOVA2 . VPOCHA(J,1) / MPOVA1 . VPOCHA(J,1)) * CPI
               MPOVAI . VPOCHA(J,1) = MPOVAI . VPOCHA(J,1)
     &                              + MPOVA2 . VPOCHA(J,1) / WI
            ENDIF
 30      CONTINUE
         SEGDES MPOVA2

 40   CONTINUE
      DO 50 J=1,NBCELL
         MPOVAA . VPOCHA(J,1) = MPOVAA . VPOCHA(J,1)
     &                        / MPOVAG . VPOCHA(J,1)
         MPOVAB . VPOCHA(J,1) = MPOVAB . VPOCHA(J,1)
     &                        / MPOVAG . VPOCHA(J,1)
         MPOVAC . VPOCHA(J,1) = MPOVAB . VPOCHA(J,1)
     &                        / MPOVA1 . VPOCHA(J,1)
         MPOVAD . VPOCHA(J,1) = MPOVAD . VPOCHA(J,1)
     &                        / MPOVAE . VPOCHA(J,1)
         MPOVAG . VPOCHA(J,1) = MPOVAG . VPOCHA(J,1)
     &                        / MPOVA1 . VPOCHA(J,1)
         MPOVAH . VPOCHA(J,1) = (1.D0 -  (MPOVAE .VPOCHA(J,1)
     &                        / MPOVA1 . VPOCHA(J,1)))
     &                        * MPOVA3 . VPOCHA(J,1)
     &                        + MPOVAF . VPOCHA(J,1)
         MPOVAI . VPOCHA(J,1) = MPOVAE . VPOCHA(J,1)
     &                        /  MPOVAI . VPOCHA(J,1)
 50   CONTINUE
C
      RETURN
      END

