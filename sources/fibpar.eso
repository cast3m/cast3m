C FIBPAR    SOURCE    CHAT      12/04/06    21:15:12     7348
C FIBFRA    SOURCE    DC        98/01/26
      SUBROUTINE FIBPAR(XMAT,DEPS,SIG0,VAR0,SIGF,VARF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(*),DEPS(3),SIG0(3),VAR0(2),SIGF(3),VARF(2)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE
C      MODELE BETON PLASTIQUE DU BAEL
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - Janvier 1998
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C
C      XMAT( 5) FY
C      XMAT( 6) HH
C
C      DEPS(1) EPXX    SIG0(1) SIGF(1) SMXX
C      DEPS(2) GAXY    SIG0(2) SIGF(2) SMXY
C      DEPS(3) GAXZ    SIG0(3) SIGF(3) SMXZ
C
C                      VAR0(1) VARF(1) EPSE DEFORMATION PLASTIQUE
C
C----------------------------------------------------------------------
       GAMMA=XMAT(1)/2.D0/(XMAT(2)+1.D0)
       EYOUN = XMAT( 1)
       FYP = XMAT(5)
       FYN = (-1.D0)*XMAT(5)
       HHH = XMAT(6)
       FYPE = FYP + ( HHH * VAR0(1))
       FYNE = FYN + ( HHH * VAR0(1))
C
       XEPSE =  VAR0(1)
C
       XSIGT = SIG0(1) + EYOUN * DEPS(1)
C
       IF (XSIGT . GT . FYPE) THEN
C
         XDEP = ((EYOUN/(EYOUN+HHH))*DEPS(1))
     &         + ((SIG0(1) - FYPE)/(EYOUN+HHH))
         XSIGF =  SIG0(1) + (EYOUN*(DEPS(1) - XDEP))
         XTAN = (EYOUN*HHH)/(EYOUN+HHH)
C
       ELSEIF (XSIGT . LT . FYNE) THEN
C
         XDEP = ((EYOUN/(EYOUN+HHH))*DEPS(1))
     &         + ((SIG0(1) - FYNE)/(EYOUN+HHH))
         XSIGF =  SIG0(1) + (EYOUN*(DEPS(1) - XDEP))
         XTAN = (EYOUN*HHH)/(EYOUN+HHH)
C
       ELSE
C
         XSIGF = XSIGT
         XDEP = 0.D0
         XTAN = EYOUN
C
       ENDIF
C
C
       SIGF(1) = XSIGF
C
       SIGF(2) = SIG0(2) + GAMMA   * DEPS(2)
       SIGF(3) = SIG0(3) + GAMMA   * DEPS(3)
C
       VARF(1) = XEPSE + XDEP
       VARF(2) = XTAN
C
       RETURN
       END





