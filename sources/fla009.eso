C FLA009    SOURCE    SERRE     12/07/10    21:15:14     7431
      FUNCTION FLA009(P,T,TC,TPROP,X,TRAV)
C---------------------------------------------------------------------
C Calcul du coefficient d'échange thermique entre les plaques du PAR
C et le gaz via la corrrélation d'Elenbaas (en W/m2/K)
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  P       : flottant : Pression de référence (Pa)
C E/  T       : flottant : Température du gaz à l'entrée du PAR (K)
C E/  TC      : flottant : Température des plaques (K)
C E/  TPROP   : flottant : Température de référence (K)
C E/  X(7)    : flottant : Fraction molaire de N2, O2, H2 et H2O
C  /S FLA009  : flottant : Coefficient d'échange thermique (W/m2/K)
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C E/  E         : Distance entre deux plaques
C E/  L         : Hauteur des plaques
C E/  G         : Gravité (=9.81 m/s2)
C E/  R         : Constante des gaz (= 8.314 J/mol/K)
C E/  IKALP     : Flag permettant d'activer la correction RALOC
C E/  M(7)      : Masse molaire des constituants du mélange (kg/mol)
C E/  IMODEL    : Type de model
C E/  CSTMOD(3) : Coeff. correctif du coefficient d'échange (HEATER)
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 FLA009
      REAL*8 X(7)
      REAL*8 LAMBDA,NU
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
C Conductivité thermique du mélange (en W/m/K)
      LAMBDA = FLA006(TPROP,X,TRAV)
C
C Calcul du Rayleigh modifié
      RA  = G*(TC-T)*E*E*E*FLA001(P,TPROP,X,TRAV)*P*FLA002(X,TRAV)
      RA  = RA / (R*TPROP*TPROP*FLA004(TPROP,X,TRAV)*LAMBDA)
      RAB = RA * E / L
C
C Corrélation de Elenbaas
      IF (RAB .LT. 1D-3) THEN
         NU = 0.D0
      ELSE
         NU = RAB/24.D0 * (1.D0 -EXP(-35.D0/RAB)) ** 0.75D0
      ENDIF
      VAL009 = LAMBDA * NU / E
C
C Correction RALOC éventuelle
      IF (IKALP .EQ. 1) THEN
         DT = TC - T
         IF (DT .GE. 75.D0) THEN
            VAL009 = VAL009 + 40.D0
         ELSEIF (DT .GE. 25.D0) THEN
            VAL009 = VAL009 + 30.D0
         ENDIF
      ENDIF
C
      IF (IMODEL .NE. 1) THEN
         val009 = val009 * cstmod(3)
      ENDIF
C
      FLA009 = VAL009
C
      RETURN
      END








