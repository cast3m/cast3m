C DEVEQ7    SOURCE    BP208322  18/12/20    21:15:28     10048
C
c       SUBROUTINE DEVEQ7(Q1,Q2,NA1,XK,XASM,XM,PDT,NPAS,FTOTA,NB1)
      SUBROUTINE DEVEQ7(Q1,Q2,NA1,XM,PDT,NPAS,FTOTA,FAMOR,
     &                  XOPM1,NB1,NB1M)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Operateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul des deplacements generalises pour le second demi-pas    *
*     de temps.                                                      *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* es  Q1(.,.) Vecteur des deplacements generalises                   *
* e   Q2(.,.) Vecteur des vitesses generalisees                      *
* e   NA1     Nombre total d'inconnues dans la base A                *
* e   XK      Vecteur des raideurs generalisees                      *
* e   XASM    Vecteur des amortissements generalises                 *
* e   XM      Vecteur des masses generalisees                        *
* e   PDT     pas de temps                               *
* e   NPAS    Numero du pas de temps                                 *
* e   FTOTA   Forces exterieures totalisees, sur la base A           *
*                                                                    *
*     Auteur, date de creation:                                      *
*                                                                    *
*     Denis ROBERT-MOUGIN, le 19 mai 1989.                           *
*                                                                    *
*--------------------------------------------------------------------*
      REAL*8 Q1(NA1,*),Q2(NA1,*)
      REAL*8 XM(NA1,*),FTOTA(NA1,*)
      REAL*8 XOPM1(NB1,NB1,*),FAMOR(NA1,*)
*
      AUX4 = PDT * PDT / 6.D0
*
*                       .       2
*     Q      = Q    + h Q    + h /6 M^-1 ( F    + 2 F
*      i,m+1    i,m      i,m                i,m      i,m+1/2
*                      .        .
*              - C   ( Q    + 2 Q        ) )
*                 ij    j,m      j,m+1/2
*
*
C     CALCUL DU DEPLACEMENT

C     MATRICE M PLEINE
      IF(NB1M.GT.1) THEN

        DO 21 IA = 1,NA1
          AUX2 = 0.D0
          DO 22 IB = 1,NB1
            AUX2 = AUX2 + XOPM1(IA,IB,3) *
     &      ((FTOTA(IB,3)-FAMOR(IB,3))+2.D0*(FTOTA(IB,2)-FAMOR(IB,2)))
 22       CONTINUE
          Q1(IA,1) = Q1(IA,3) + (PDT * Q2(IA,3)) + (AUX4 * AUX2)
 21     CONTINUE

C     MATRICE M DIAGO
      ELSE

        DO 20 I = 1,NA1
           AUX2 = ( FTOTA(I,3)-FAMOR(I,3)
     &      + 2.D0*(FTOTA(I,2)-FAMOR(I,2)) ) / XM(I,1)
           Q1(I,1) = Q1(I,3) + (PDT * Q2(I,3)) + ( AUX4 * AUX2)
 20     CONTINUE

      ENDIF
*
      END





