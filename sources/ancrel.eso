C ANCREL    SOURCE    CHAT      05/01/12    21:22:18     5004
C
      SUBROUTINE ANCREL(SIGI,DEPST,VARI,XMAT,
     .                       SIGF,VARF,DEFP,KERRE)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(*),VARI(*),VARF(*),SIGI(*)
     . ,DEPST(*),DEFP(*),SIGF(*)
C----------------------------------------------------------------------
C
C     LOI D'ANCRAGE ELIGEHAUSEN SUR UN ELEMENT DE JOINT 2D
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - MAI 2000
C----------------------------------------------------------------------
C
C      XMAT( 1) XKS
C      XMAT( 2) XKN
C      XMAT( 3) RHO
C      XMAT( 4) ALPHA
C      XMAT( 5) S1T
C      XMAT( 6) S2T
C      XMAT( 7) S3T
C      XMAT( 8) T1T
C      XMAT( 9) T3T
C      XMAT(10) ALFA
C      XMAT(11) PERI
C
C  STRESS
C  ======
C
C      SMSN.  = SIG.(1)
C      SMN.   = SIG.(2)
C-----------------------------------------------------------------------
C  STRAIN
C  ======
C
C      DRSN   = EPS(1)
C      DRN    = EPS(2)
C-----------------------------------------------------------------------
C  INTERNAL VARIABLES
C  ==================
C
C      EPPP = VAR0(1) VARF(1) EPPP GLISSEMENT PLASTIQUE POSITIF
C      EPPM = VAR0(2) VARF(2) EPPM GLISSEMENT PLASTIQUE NEGATIF
C      EPSO = VAR0(3) VARF(3) EPSO GLISSEMENT TOTAL
C
C  MATRICE TANGENTE
C
C      KSTAN   = VARF(4)
C----------------------------------------------------------------------
C
       XKS  = XMAT(1)
       XKN  = XMAT(2)
       S1T = XMAT(5)
       S2T = XMAT(6)
       S3T = XMAT(7)
       T1T = XMAT(8)
       T3T = XMAT(9)
       ALFA = XMAT(10)
       PERI = XMAT(11)
C
       G12 = XKS/PERI
C
C  EFFORT NORMAL (ELASTIQUE)
C
        SIGF(2) = SIGI(2)+(XKN*DEPST(2))
C
C  CISAILLEMENT - GLISSEMENT
C
       EPPP   = VARI(1)
       EPPM   = VARI(2)
C
       EPSO = VARI(3) + DEPST(1)
C
       CALL ELIGEH(EPSO,G12,S1T,S2T,S3T,T1T,T3T,ALFA,
     .            EPPP,EPPM,EPPPF,EPPMF,XKTAN,TAUF)
C
       SIGF(1) = PERI * TAUF
C
       VARF(1) = EPPPF
       VARF(2) = EPPMF
       VARF(3) = EPSO
C
       VARF(4) = PERI * XKTAN
       VARF(5) = XKN
C
       RETURN
       END





