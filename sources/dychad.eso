C DYCHAD    SOURCE    CHAT      05/01/12    23:02:39     5004
      SUBROUTINE DYCHAD(IDIM,IGP,XPALB,NLIAB,I,I0,I1,I2,I3,XADHE,XRAIT,
     &                  XAMOT,XFN,XFT,XPUS)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Op{rateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul de la force d'adh{rence et test de sa validit{          *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* es  XPALB   Tableau contenant les param}tres de la liaison         *
* e   NLIAB   Nombre de liaisons sur la base B                       *
* e   I       Num{ro de la liaison                                   *
* e   I0      Pointeur de la vitesse tangentielle dans XPALB         *
* e   I1      Pointeur de la variation de d{placement par rapport a  *
*             la position origine d'adh{rence, projet{e sur le plan  *
*             tangent, dans XPALB                                    *
* e   I2      Pointeur de la force tangentielle dans XPALB           *
* e   I3      Pointeur de la force tangentielle @ la fin du glis-    *
*             -sement dans XPALB                                     *
* e   XFN     Valeur de la force de choc suivant la normale.         *
* e   XADHE   Valeur du coefficient d'adh{rence                      *
* e   XRAIT   Valeur de la raideur tangentielle                      *
* e   XAMOT   Valeur de l'amortissement tangentiel                   *
* es  IGP     Indice de glissement au pas pr{c{dent                  *
*  s  XFT     Module de la force de choc projet{e sur le plan tangent*
*  s  XPUS    Valeur de la puissance d'usure instantan{e             *
*                                                                    *
*                                                                    *
*     Auteur, date de cr{ation:                                      *
*                                                                    *
*     Bertrand BEAUFILS, le 31 mai 1990.                             *
*                                                                    *
*--------------------------------------------------------------------*
      DIMENSION XPALB(NLIAB,*)
*  Calcul de la force tangentielle introduite par l'oscillateur
*  {quivalent
      PSXFT = 0.D0
      DO 10 ID = 1,IDIM
          XPALB(I,I2+ID) = XPALB(I,I3+ID) - XRAIT * XPALB(I,I1+ID)
     &                   - XAMOT * XPALB(I,I0+ID)
          PSXFT = PSXFT + XPALB(I,I2+ID) * XPALB(I,I2+ID)
 10       CONTINUE
*     end do
      XFT = SQRT(PSXFT)
*
      IF (XFT.GT.( XADHE * ABS(XFN) ) ) THEN
*  L'adh{rence ne tient pas
         IGP = 1
      ELSE
*  Ca colle
         XPUS = 0.D0
         IGP = 0
      ENDIF
*
      RETURN
      END

