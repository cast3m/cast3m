C TIMBSG    SOURCE    CHAT      05/01/13    03:40:04     5004
      SUBROUTINE TIMBSG(FORCE,VECT,COORD,CONT,WORK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DES FORCES INTERNES B*SIGMA
C             POUR LA POUTRE DROITE DE TIMOSHENKO SIMPLE ET MULTI-FIBRE
C
C  ENTREE
C          CONT    CONTRAINTES
C          VECT    ORIENTATION DE L'OY LOCAL
C          COORD   COORDONNEES DES 2 NOEUDS
C
C  SORTIE
C          FORCE   FORCES INTERNES
C          KERRE   INDICE D'ERREUR ( 0 SI PAS DE PB )
C  TRAVAIL
C          WORK(300)
C
C-----------------------------------------------------------------------
C     CLONE DE POUBSG PP 4/9/92
C-----------------------------------------------------------------------
C     modifiee par Pierre Pegon (ISPRA) Juillet/Aout 1993
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),COORD(3,*),VECT(3),P(3,3),
     .          XE(2),YE(2),ZE(2),FORCE(*),WORK(*)
      DATA P5/0.5D0/
C
      KERRE=0
      DO 2 I=1,2
      XE(I)=COORD(1,I)
      YE(I)=COORD(2,I)
  2   ZE(I)=COORD(3,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2+(ZE(2)-ZE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C  VALEURS MOYENNES DANS L ELEMENT
C
C FX
      WORK(1) =-CONT(1)
      WORK(7) = CONT(1)
C FY
      WORK(2) =-CONT(2)
      WORK(8) = CONT(2)
C FZ
      WORK(3) =-CONT(3)
      WORK(9) = CONT(3)
C MX
      WORK(4) =-CONT(4)
      WORK(10)= CONT(4)
C MY
      WORK(5) =-CONT(5)  + CONT(3)*P5*XL
      WORK(11)= CONT(5)  + CONT(3)*P5*XL
C MZ
      WORK(6) =-CONT(6)  - CONT(2)*P5*XL
      WORK(12)= CONT(6)  - CONT(2)*P5*XL
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPAS(XE,YE,ZE,VECT,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C  PASSAGE DES FORCES EN AXES GLOBAUX
C
      CALL POUVEC(WORK,FORCE,P,2)
      RETURN
999   KERRE=1
      RETURN
      END

