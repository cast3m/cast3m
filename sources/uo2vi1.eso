C UO2VI1    SOURCE    PV        19/02/04    21:15:10     10103
C   responsable STRUB
       SUBROUTINE UO2VI1(SIG,EPSV,VAR,EPSVPT,VARPT,EPSDPT,EPSGPT,
     &                   XMAT,AA,NGAT,NCOMAT,NSIMP,PHI,TEMPER,BU,BUPT)
C-----------------------------------------------------------------------
C                MODELE UO2 (OTTOSEN+GATT_MONERIE)
C           CALCUL DES VITESSES DES GRANDEURS SUIVANTES:
C  - DEFORMATIONS VISCOPLASTIQUES
C  - DEFORMATIONS DE DENSIFICATION ET DE GONFLEMENT
C  - POROSITE ET DEFORMATION VISCOPLASTIQUE CUMULEE
C-----------------------------------------------------------------------
C
C  ENTREES
C  -------
C SIG(6)       = CONTRAINTES                     AU DEB. DU SOUS PAS
C EPSV(6)      = DEFORMATIONS VISCOPLASTIQUES    AU DEB. DU SOUS PAS
C VAR(NGAT)    = VAR. INT. SCAL. DE GATT_MONERIE AU DEB. DU SOUS PAS
C XMAT(NCOMAT) = CARACTERISTIQUES THERMOMECANIQUES DU MATERIAU
C NSIMP        = POINTE SUR LA CARACTERISTIQUE FACULT. 'SIMP' DE XMAT
C AA           = COEFFICIENT INTERVENANT DS LE CALCUL DE LA VITESSE
C                DE LA DEFORMATION DE DENSIFICATION
C PHI          = DENSITE DE FISSION INSTANCIEE
C TEMPER       = TEMPERATURE        INSTANCIEE
C BU           = TAUX DE COMBUSTION INSTANCIE
C BUPT         = VITESSE DU TAUX DE COMBUSTION
C
C  SORTIES
C  -------
C EPSVPT(6)    = VITESSES DES DEFORMATIONS VISCOPLASTIQUES
C VARPT(NGAT)  = VITESSES DES VARIABLES INTERNES SCAL. DE GATT_MONERIE
C EPSDPT(3)    = VITESSE DE LA DEFORMATION DE DENSIFICATION
C EPSGPT(3)    = VITESSE DE LA DEFORMATION DE GONFLEMENT
C VAR(4)       = FONCTION TETA EN CAS DE COUPLAGE STATIQUE
C-----------------------------------------------------------------------
       IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
C
       PARAMETER (XZER=0.D0,UNTIER=0.333333333333333D0)
C
       DIMENSION SIG(*),EPSV(*),VAR(*),XMAT(*)
       DIMENSION EPSVPT(*),VARPT(*),EPSDPT(*),EPSGPT(*)
       DIMENSION SS(6),EPSPRI(6),EPSPT1(6),EPSPT2(6),EPSSEC(6),EPSVD(6)
-INC CCREEL
C
C ---------- CONTRAINTE ET DEF. VISCOPLASTIQUE HYDROSTATIQUES
C
       SIGM=UNTIER*(SIG(1)+SIG(2)+SIG(3))
       EPSVM=UNTIER*(EPSV(1)+EPSV(2)+EPSV(3))
C
C ---------- CONTRAINTES ET DEF. VISCOPLASTIQUES DEVIATORIQUES
C
       DO 5 I=1,6
         A=XZER
         IF(I.LE.3) A=1.0D0
         SS(I)=SIG(I)-A*SIGM
         EPSVD(I)=EPSV(I)-A*EPSVM
 5     CONTINUE
C
C ---------- CONTRAINTE DE VON MISES
C
       SEQUV=PROCON(SS,SS,6)
       SEQUV=SQRT(1.5D0*SEQUV)
C
C ----------- DEFORMATION VISCOPLASTIQUE EQUIVALENTE
C
       EEQUV=PROCON(EPSVD,EPSVD,6)
       EEQUV=SQRT(2.D0*UNTIER*EEQUV)
C
C ---------- POROSITE ET FONCTION DE COUPLAGE AU DEB. DU SOUS PAS
C
       PORO=VAR(1)
       TETA0=VAR(4)
C
C ---------- PROPRIETES VISCOPLASTIQUES DU MATERIAU
C
        RR  = XMAT(5)
        DG  = XMAT(6)
        XK1 = XMAT(7)
        XM1 = XMAT(8)
        Q1  = XMAT(9)
        XN1 = XMAT(10)
        XK2 = XMAT(11)
        XM2 = XMAT(12)
        Q2  = XMAT(13)
        XN2 = XMAT(14)
        OMEG= XMAT(15)
        QQ  = XMAT(16)
        HH  = XMAT(17)
        BETA= XMAT(18)
        XK  = XMAT(19)
        XA  = XMAT(20)
        Q3  = XMAT(21)
        XN3 = XMAT(22)
        DG0 = XMAT(28)
        CR1 = XMAT(29)
        CR2 = XMAT(30)
        CR3 = XMAT(31)
        CR  = XMAT(32)
        XKP = XMAT(33)
        AP  = XMAT(34)
        BP  = XMAT(35)
        QP  = XMAT(36)
C
C ---------- TYPE = 0. COMBUSTIBLE UO2, SINON COMBUSTIBLE AFA3GLAA
C
        TYPE= XMAT(NSIMP+1)
C
C ---------- DYN = 0. COUPLAGE STATIQUE, SINON COUPLAGE DYNAMIQUE
C
        DYN = XMAT(NSIMP+3)
C
        IF (DYN.NE.XZER) THEN
           DYN1=XMAT(NSIMP+4)
           TO=DYN1
           IF (TYPE.NE.XZER) THEN
              DYN2= XMAT(NSIMP+5)
              DYN3= XMAT(NSIMP+6)
              TO1 = (DYN2 - TEMPER) / DYN3
              TO  = ( DYN1*(1.D0+TANH(TO1)) ) + 1.D0
           ENDIF
        ENDIF
C
C ---------- COMP = 0. COMBUSTIBLE COMPRESSIBLE, SINON INCOMPRESSIBLE
C
        COMP= XMAT(NSIMP+2)
C
C ---------- CARACTERISTIQUES VISCOPLASTIQUES DU MATERIAU
C
        AKP=XKP*EXP(-1.D0*QP/RR/TEMPER)
        WC1=1.D0+TANH((CR-CR2)/CR3)
        WC1=1.D0+0.5D0*CR1*WC1
        AK1=XK1*WC1*(DG**XM1)*EXP(-1.D0*Q1/RR/TEMPER)
        WC2=2.D0*(DG0**XM2)*(1.D0-(COS(DG/DG0)))
        AK2=XK2*WC2*EXP(-1.D0*Q2/RR/TEMPER)
        AK3=XA*EXP(-1.D0*Q3/RR/TEMPER)
C
C--------------------------------------------------------------------
C  Fluage therm. primaire: definition de la vitesse de déform. EPSPRI
C--------------------------------------------------------------------
        IF (EEQUV.LT.1D-10) EEQUV=1D-10
        AP1=AP-1.D0
        PRIM = AKP*(SEQUV**AP1)/(EEQUV**BP)
        DO 8 I=1,6
           EPSPRI(I)=1.5D0*PRIM*SS(I)
  8     CONTINUE
C
C----------------------------------------------------------------------
C  Fluage therm. secondaire: definition de Ai et Bi fct. de la porosite
C----------------------------------------------------------------------
        EXPA1 = -1.D0/XN1
        EXPAB1= 2.D0*XN1/(XN1+1.D0)
        A1  = max(xpetit,(XN1*( (max(xpetit,PORO)**EXPA1) - 1.D0 )))**
     >          (-1.D0*EXPAB1)
        B1  = (1.D0+((2.D0/3.D0)*PORO))/
     >       (max(1.D0-PORO,xpetit/xzprec)**EXPAB1)
C
        EXPA2 = -1.D0/XN2
        EXPAB2= 2.D0*XN2/(XN2+1.D0)
        A2    = max((XN2*( (max(xpetit,PORO)**EXPA2) - 1.D0 )),xpetit)**
     >        (-1.D0*EXPAB2)
        B2    = (1.D0+((2.D0/3.D0)*PORO))/
     >        (max((1.D0-PORO),xpetit)**EXPAB2)
C
        IF (COMP.NE.XZER) THEN
           B1 = B1 + A1/4.D0
           B2 = B2 + A2/4.D0
           A1 = XZER
           A2 = XZER
        ENDIF
C-----------------------------------------------------------------------
C  Fluage therm. secondaire: def. des potentiels thermodyn. PSI1 et PSI2
C-----------------------------------------------------------------------
        EXP1=XN1 + 1.D0
        EX1 = 0.5D0*EXP1
        ZZ1 =A1*(1.5D0*SIGM)**2 + B1*SEQUV**2
        PSI1=(AK1/EXP1)*(ZZ1**EX1)
C
        EXP2=XN2 + 1.D0
        EX2 = 0.5D0*EXP2
        ZZ2 =A2*(1.5D0*SIGM)**2 + B2*SEQUV**2
        PSI2=(AK2/EXP2)*(ZZ2**EX2)
C--------------------------------------------------------------------
C  Fluage therm. secondaire: vit. de def. / mecanismes EPSPT1, EPSPT2
C--------------------------------------------------------------------
        EXPM1 = (XN1 - 1.D0)/2.D0
        DO 10 I=1,6
          IF(I.LE.3) THEN
            SIG1 = 1.5D0*A1*SIGM + 3.D0*B1*SS(I)
          ELSE
            SIG1 = 3.D0*B1*SS(I)
          ENDIF
          EPSPT1(I) =0.5D0*AK1*(ZZ1**EXPM1)*SIG1
 10    CONTINUE
C
        EXPM2 = (XN2 - 1.D0)/2.D0
        DO 20 I=1,6
          IF(I.LE.3) THEN
            SIG2 = 1.5D0*A2*SIGM + 3.D0*B2*SS(I)
          ELSE
            SIG2 = 3.D0*B2*SS(I)
          ENDIF
          EPSPT2(I) =0.5D0*AK2*(ZZ2**EXPM2)*SIG2
 20     CONTINUE
C---------------------------------------------------------------------
C Def. de la fonction de couplage statique entre les 2 mecanismes TETA
C---------------------------------------------------------------------
        GSEQ1 = B1/(B1+A1/4.D0)
        GSEQ2 = 9.D0*A1/(4.D0*B1+A1)
        GSEQ  = (GSEQ1*SEQUV**2.D0)+(GSEQ2*SIGM**2.D0)
        GSEQUV=SQRT(GSEQ)
        IF (GSEQUV.LT.1D-10) THEN
          TETA  = XZER
        ELSE
          THERB = OMEG*(GSEQUV**(-QQ))
          TT    = (TEMPER-THERB)/HH
          TETA  = 0.5D0*BETA*(1.D0+TANH(TT))
        ENDIF
C--------------------------------------------------------------------
C Vit. de la fct. de couplage dynamique entre les 2 mecanismes TETAPT
C--------------------------------------------------------------------
C ---------- DYN = 0. couplage statique, sinon couplage dynamique
C
        IF (DYN.NE.XZER) THEN
           IF (TETA.LT.1D-5) THEN
              TETAPT = XZER
           ELSE
              TETAPT = ((TETA-TETA0)**2.D0) / (TO * TETA)
              TETAPT =SIGN(TETAPT,TETA-TETA0)
           ENDIF
           TETA   = TETA0
        ELSE
           TETAPT = XZER
        ENDIF
C-----------------------------------------------------------------------
C Fluage thermique secondaire: vitesse de deformation viscoplast. EPSSEC
C-----------------------------------------------------------------------
        IF (GSEQUV.LT.1D-10.OR.DYN.NE.XZER) THEN
          DERTET2 = XZER
        ELSE
          DERTET1 =
     &      0.5D0*BETA*QQ*OMEG*(GSEQUV**(-2.D0-QQ))/HH
          DERTET2 = DERTET1*(1.D0 - (TANH(TT))**2)
        ENDIF
        DO 30 I=1,6
          A=XZER
          IF(I.LE.3) A=1.0D0
          VIT = (1.D0 - TETA)*EPSPT1(I) + TETA*EPSPT2(I)
          EPSSEC(I) = VIT +
     &    DERTET2*(PSI2-PSI1)*(A*GSEQ2*UNTIER*SIGM + 1.5D0*GSEQ1*SS(I))
 30     CONTINUE
C------------------------------------------------------------------------
C Viscoplast. due a l irradiation EPS0IR (en vit.)
C Prise en compte de l acceleration de la viscoplast. thermique (en vit.)
C EPSVPT = (1+k*fit)(EPSPRI+EPSSEC) + EPS0IR
C------------------------------------------------------------------------
        IF (SEQUV.LT.1D-10) THEN
          SIR=XZER
        ELSE
          SIR=SEQUV**(XN3-1.D0)
        ENDIF
        DO 40 I=1,6
          EPS0IR = AK3*PHI*SIR*(1.5D0*SS(I))
          EPSVPT(I)=(1.D0 + XK*PHI)*(EPSPRI(I)+EPSSEC(I)) + EPS0IR
 40     CONTINUE
C-----------------------------------------------------
C Prise en compte du gonflement et de la densification
C-----------------------------------------------------
        CALL DENSGF(BU,BUPT,XMAT,NCOMAT,AA,EPSDPT,EPSGPT)
C--------------------------------------------------------------------
C Vit. des var. int.: porosite, taux de combustion,
C                     def. viscoplastique cumulee et fct. de couplage
C--------------------------------------------------------------------
        IF (COMP.NE.XZER) THEN
           VARPT(1) = XZER
        ELSE
           VARPT(1) = (1.D0-PORO)*(EPSVPT(1) + EPSVPT(2) + EPSVPT(3))
           VARPT(1) = VARPT(1) +  (EPSDPT(1) + EPSDPT(2) + EPSDPT(3))
        ENDIF
        VARPT(2) = BUPT
        EPSC = PROCON(EPSVPT,EPSVPT,6)
        VARPT(3) = SQRT(2.D0*EPSC/3.D0)
        VARPT(4) = TETAPT
        IF (DYN.EQ.XZER) VAR(4)=TETA
C
        RETURN
        END







