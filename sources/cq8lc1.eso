C CQ8LC1    SOURCE    CHAT      05/01/12    22:27:19     5004
      SUBROUTINE CQ8LC1(NOBG,NBNN,E,XX,TH,SHPCOQ,TXR,T,IRR)
C=======================================================================
C     CALCULE REPERE LOCAL EN UN POINT DE GAUSS  POUR COQ8 ET COQ6
C  ENTREES
C  -------
C     NOBG             :  NUMERO DU POINT DE GAUSS
C     NBNN             :  NOMBRE DE NOEUDS
C     E                :  COORDONNEES REDUITES DU PT DE GAUSS DS L EPAI
C  XX(3,NBNN)          :    TABLEAU DES COORDONNEES DES NOEUDS
C   TH(NBNN)           :    LES EPAISSEURS NODALES
C SHPCOQ(6,NBNN,NBPGAU):      VALEURS DES FONCTIONS DE FORME ET
C                  DE LEURS DERIVEES AUX POINTS DE GAUSS
C TXR(3,3,NBNN)        :NBNN MATRICES 3 3 CONTENANT REPERES NOEUDS
C SORTIES
C -------
C     T(9)    :  TABLEAU CONTENANT LE REPERE LOCAL
C     IRR     :  INDICATEUR D ERREUR
C-----------------------------------------------------------------------
C   EBERSOLT     RECUPERATION BILBO   JUILLET 86
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION XX(3,*),T(*),SHPCOQ(6,NBNN,*),TXR(3,3,*),TH(*)
      DIMENSION XJ(3,3)
C
      DATA XZER/0.D0/
C
C     DETERMINATION DU JACOBIEN ET DE SON DETERMINANT AU POINT (R,S,T)
C
      CALL COQ8JC(NOBG,NBNN,E,XX,TH,TXR,SHPCOQ,XJ,DET,IRR)
C
      IF(IRR.EQ.-1) RETURN
C*
C*    DETERMINATION DES COSINUS DIRECTEURS DES AXES LOCAUX EN CE POINT
C*
      DO 10 I=1,3
      DO 10 J=1,2
      K=3*(J-1)+I
      T(K)= XJ(J,I)
   10 CONTINUE
C*
C*    PRODUITS VECTORIELS ET NORMALISATIONS
C*
      CALL CROSS2(T(1),T(4),T(7),IRR)
      CALL CROSS2(T(7),T(1),T(4),IRR)
      CALL CROSS2(T(4),T(7),T(1),IRR)
C
      RETURN
      END

