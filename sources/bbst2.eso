C BBST2     SOURCE    KICH      18/01/11    21:15:11     9690
      SUBROUTINE BBST2(TRACE,NBPGAU,IFOUR,MELE,POIGAU,QSIGAU,
     &                 ETAGAU,DZEGAU,SHPTOT,NBNO,SHP,XE,PP)
c
c
c     projection de la trace de epsi2 sur la base reduite des elements icq
c
c entree
c   trace  : tableau contenant la valeur de trace de epsi2 en
c            chaque point de gauss
c   nbpgau : nombre de point de gauss
c   ifour  : variable du ccoptio
c   mele   : numero de l'element
c   poigau : poids d'integration
c   qsigau : 1ere composante sur l'element de reference
c   etagau : 2eme composante sur l'element de reference
c   dzegau : 3eme composante sur l'element de reference
c   shptot : valeurs des fonctions de forme
c
c sortie
c   trac   : contient la correction à apporter à chaque composante diagonale de
c            la deformation
c
c
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
c
       DIMENSION TRACE(*),POIGAU(*),QSIGAU(*),ETAGAU(*),DZEGAU(*)
     &          ,SHPTOT(6,NBNO,*),SHP(6,*),XE(3,*)
       DIMENSION P(3,3),T(4),PP(4,4)

c
c      elements icq4 et ict3 et ict6 et lineaires 3D
c
       IF (MELE.EQ.69.OR.MELE.EQ.70.OR.MELE.EQ.71.or.
     &MELE.EQ.73.or.MELE.EQ.74.or.MELE.EQ.75.or.MELE.EQ.273) THEN
        TRINTG = 0.D0
        aire = 0.d0
        DO 10 I=1,NBPGAU
           DO 5 J=1,NBNO
           DO 5 K =1,4
             SHP(K,J)=SHPTOT(K,J,I)
  5        CONTINUE
          if(mele.ge.73) then
           CALL JACOBI(XE,SHP,3,NBNO,DJAC)
          else
           CALL JACOBI(XE,SHP,2,NBNO,DJAC)
          endif
           TRINTG = TRINTG + POIGAU(I)*TRACE(I)*DJAC
           AIRE = AIRE +POIGAU(I)*DJAC
 10     CONTINUE
        trintg = TRINTG / AIRE
c
        COEF = 2.D0
        IF (IFOUR .EQ. 0 ) COEF = 3.D0
        DO 20 I=1,NBPGAU
           TRACE(I) = (TRINTG - TRACE(I))/COEF
 20     CONTINUE
        RETURN
c
c   élement ICQ8 et quadratiques 3D
c
       ELSE IF ( MELE.EQ.72.or.MELE.GE.76) THEN
c
c    mele 72 ICQ8 matrice P calculée analytiquement voir routine BBCAL2
c
      P(1,1) = 2.D0/3.D0*(XE(1,1)*(XE(2,2)-XE(2,8))
     1         +XE(1,2)*(XE(2,3)-XE(2,1))
     1         +XE(1,3)*(XE(2,4)-XE(2,2))+XE(1,4)*(XE(2,5)-XE(2,3))
     2         +XE(1,5)*(XE(2,6)-XE(2,4))+XE(1,6)*(XE(2,7)-XE(2,5))
     3         +XE(1,7)*(XE(2,8)-XE(2,6))+XE(1,8)*(XE(2,1)-XE(2,7)))
     4         +1.D0/6.D0*((XE(1,1)-XE(1,5))*(XE(2,7)-XE(2,3))
     5         +(XE(1,3)-XE(1,7))*(XE(2,1)-XE(2,5)))

      P(1,2) = 5.D0/9.D0*(-XE(2,2)*(XE(1,1)+XE(1,3))
     1         +XE(1,2)*(XE(2,1)+XE(2,3))+XE(1,6)*(-XE(2,5)-XE(2,7))
     1         +XE(2,6)*(XE(1,5)+XE(1,7)))
     2         +4.D0/9.D0*(XE(1,8)*(XE(2,7)+XE(2,2)-XE(2,6)-XE(2,1))
     2         +XE(2,8)*(-XE(1,7)-XE(1,2)+XE(1,6)+XE(1,1))
     2         +XE(1,4)*(XE(2,2)-XE(2,6)+XE(2,5)-XE(2,3))
     2         +XE(2,4)*(-XE(1,2)+XE(1,6)-XE(1,5)+XE(1,3)))
     3         +7.D0/45.D0*(XE(1,2)*(XE(2,5)+XE(2,7))
     3         -XE(2,2)*(XE(1,5)+XE(1,7))-XE(1,6)*(XE(2,1)+XE(2,3))
     3         +XE(2,6)*(XE(1,1)+XE(1,3)))
     4         +8.D0/15.D0*(XE(1,6)*XE(2,2)-XE(1,2)*XE(2,6))
     5         +7.D0/90.D0*(XE(1,7)*XE(2,1)-XE(1,3)*XE(2,5)
     5         +XE(1,5)*XE(2,3)-XE(1,1)*XE(2,7))
     6         +1.D0/30.D0*(-XE(1,7)*XE(2,3)+XE(1,3)*XE(2,7)
     6         -XE(1,5)*XE(2,1)+XE(1,1)*XE(2,5))


      P(1,3) = 5.D0/9.D0*(-XE(1,8)*(XE(2,7)+XE(2,1))
     1         +XE(2,8)*(XE(1,7)+XE(1,1))
     1         +XE(1,4)*(XE(2,3)+XE(2,5))
     1         -XE(2,4)*(XE(1,3)+XE(1,5)))
     2        +4.D0/9.D0*((XE(1,8)-XE(1,4))*(XE(2,2)+XE(2,6))
     2         +(-XE(2,8)+XE(2,4))*(XE(1,2)+XE(1,6))
     2         +XE(1,2)*(XE(2,1)-XE(2,3))+XE(2,2)*(-XE(1,1)+XE(1,3))
     2         +XE(1,6)*(XE(2,7)-XE(2,5))+XE(2,6)*(-XE(1,7)+XE(1,5)))
     3         +7.D0/45.D0*(-XE(1,8)*(XE(2,3)+XE(2,5))
     3         +XE(2,8)*(XE(1,3)+XE(1,5))
     3         +XE(1,4)*(XE(2,1)+XE(2,7))
     3         -XE(2,4)*(XE(1,1)+XE(1,7)))
     4         +1.D0/30.D0*(-XE(1,7)*XE(2,3)+XE(1,3)*XE(2,7)
     4         +XE(1,5)*XE(2,1)-XE(1,1)*XE(2,5))
     5         +7.D0/90.D0*(XE(1,7)*XE(2,5)-XE(1,3)*XE(2,1)
     5         -XE(1,5)*XE(2,7)+XE(1,1)*XE(2,3))
     6         +8.D0/15.D0*(XE(1,8)*XE(2,4)-XE(1,4)*XE(2,8))

      P(2,1) = P(1,2)

      P(2,2) = 16.D0/45.D0*(XE(2,6)*(XE(1,5)-XE(1,7))
     1         +XE(1,6)*(XE(2,7)-XE(2,5))+XE(1,2)*(XE(2,3)-XE(2,1))
     1         +XE(2,2)*(XE(1,1)-XE(1,3)))
     2         +14.D0/45.D0*(XE(2,4)*(XE(1,3)-XE(1,5))
     2         +XE(1,4)*(XE(2,5)-XE(2,3))+XE(1,8)*(XE(2,1)-XE(2,7))
     2         +XE(2,8)*(XE(1,7)-XE(1,1)))
     3         +8.D0/45.D0*(XE(1,4)*(XE(2,2)-XE(2,6))
     3         +XE(1,8)*(XE(2,6)-XE(2,2))+XE(1,2)*(XE(2,8)-XE(2,4))
     3         +XE(1,6)*(XE(2,4)-XE(2,8)))
     4         +2.D0/45.D0*(XE(2,2)*(XE(1,5)-XE(1,7))
     4         +XE(1,2)*(XE(2,7)-XE(2,5)) +XE(1,6)*(XE(2,3)-XE(2,1))
     4         +XE(2,6)*(XE(1,1)-XE(1,3)))
     5         +4.D0/45.D0*(XE(2,4)*(XE(1,1)-XE(1,7))
     5         +XE(1,4)*(XE(2,7)-XE(2,1)) +XE(1,8)*(XE(2,3)-XE(2,5))
     5         +XE(2,8)*(XE(1,5)-XE(1,3)))
     6         +17.D0/90.D0*(XE(1,7)*XE(2,5)+XE(1,3)*XE(2,1)
     6         -XE(1,5)*XE(2,7)-XE(1,1)*XE(2,3))
     7         +1.D0/90.D0*(-XE(1,7)*XE(2,1)-XE(1,3)*XE(2,5)
     7         +XE(1,5)*XE(2,3)+XE(1,1)*XE(2,7))


      P(2,3) = 1.D0/3.D0*(XE(1,5)*(XE(2,6)-XE(2,4))
     1         +XE(1,8)*(XE(2,7)+XE(2,1))+XE(1,7)*(XE(2,6)-XE(2,8))
     1         -XE(1,2)*(XE(2,1)+XE(2,3))+XE(1,1)*(XE(2,2)-XE(2,8))
     1         +XE(1,4)*(XE(2,5)+XE(2,3))-XE(1,6)*(XE(2,7)+XE(2,5))
     1         +XE(1,3)*(XE(2,2)-XE(2,4)))
     2         +4.D0/9.D0*(-XE(1,4)*(XE(2,2)+XE(2,6))
     2         -XE(1,8)*(XE(2,6)+XE(2,2))+XE(1,2)*(XE(2,8)+XE(2,4))
     2         +XE(1,6)*(XE(2,4)+XE(2,8)))
     3         +1.D0/9.D0*(XE(1,7)*(XE(2,2)-XE(2,4))
     3         +XE(1,8)*(XE(2,3)+XE(2,5))
     3         +XE(1,5)*(XE(2,2)-XE(2,8))+XE(1,3)*(XE(2,6)-XE(2,8))
     3         -XE(1,2)*(XE(2,5)+XE(2,7))+XE(1,1)*(XE(2,6)-XE(2,4))
     3         +XE(1,4)*(XE(2,1)+XE(2,7))-XE(1,6)*(XE(2,1)+XE(2,3)))


      P(3,1) = P(1,3)

      P(3,2) = P(2,3)

      P(3,3) =  14.D0/45.D0*(XE(1,6)*(XE(2,7)-XE(2,5))
     1         +XE(2,6)*(XE(1,5)-XE(1,7))+XE(1,2)*(XE(2,3)-XE(2,1))
     1         +XE(2,2)*(XE(1,1)-XE(1,3)))
     2         +16.D0/45.D0*(XE(1,8)*(XE(2,1)-XE(2,7))
     2         +XE(2,8)*(XE(1,7)-XE(1,1))+XE(1,4)*(XE(2,5)-XE(2,3))
     2         +XE(2,4)*(XE(1,3)-XE(1,5)))
     3         +8.D0/45.D0*(XE(1,4)*(XE(2,2)-XE(2,6))
     3         +XE(1,8)*(XE(2,6)-XE(2,2))+XE(1,2)*(XE(2,8)-XE(2,4))
     3         +XE(1,6)*(XE(2,4)-XE(2,8)))
     4         +2.D0/45.D0*(XE(2,4)*(XE(1,7)-XE(1,1))
     4         +XE(1,8)*(XE(2,5)-XE(2,3))+XE(2,8)*(-XE(1,5)+XE(1,3))
     4         +XE(1,4)*(XE(2,1)-XE(2,7)))
     5         +4.D0/45.D0*(XE(2,2)*(XE(1,7)-XE(1,5))
     5         +XE(1,2)*(XE(2,5)-XE(2,7))+XE(1,6)*(XE(2,1)-XE(2,3))
     5         +XE(2,6)*(XE(1,3)-XE(1,1)))
     6         +1.D0/90.D0*(-XE(1,5)*XE(2,7)+XE(1,3)*XE(2,1)
     6         +XE(1,7)*XE(2,5)-XE(1,1)*XE(2,3))
     7         +17.D0/90.D0*(-XE(1,7)*XE(2,1)-XE(1,3)*XE(2,5)
     7         +XE(1,5)*XE(2,3)+XE(1,1)*XE(2,7))

c
c  calcul des produits <tr eII . 1 > , <tr eII . eta > , <tr eII . xsi >
c
        T(1) = 0.D0
        T(2) = 0.D0
        T(3) = 0.D0
        T(4) = 0.D0
        DO 40 I=1,NBPGAU
           DO 30 J=1,NBNO
           DO 30 K =1,4
             SHP(K,J)=SHPTOT(K,J,I)
 30        CONTINUE
          if(mele.ge.76) then
           CALL JACOBI(XE,SHP,3,NBNO,DJAC)
          else
           CALL JACOBI(XE,SHP,2,NBNO,DJAC)
          endif
           T(1) = T(1) + POIGAU(I)*TRACE(I)*DJAC
           T(2) = T(2) + POIGAU(I)*TRACE(I)*DJAC*QSIGAU(I)
           T(3) = T(3) + POIGAU(I)*TRACE(I)*DJAC*ETAGAU(I)
           if(mele.ge.76)
     &      T(4) = T(4) + POIGAU(I)*TRACE(I)*DJAC*DZEGAU(I)
 40     CONTINUE
*
c    resolution du systme P.X=T
c
       if(mele.ge.76) then

        CALL GAUSSJ(PP,4,4,T,1,1)
          COEF = 3.D0
        DO 55 I=1,NBPGAU
          TRACE(I)=(T(1)+QSIGAU(I)*T(2)+ETAGAU(I)*T(3)+DZEGAU(I)*T(4)
     &-TRACE(I))/COEF
 55     CONTINUE
       else

        CALL GAUSSJ(P,3,3,T,1,1)
*
c    calcul du terme correctif
c
        COEF = 2.D0
        IF (IFOUR .EQ. 0 ) COEF = 3.D0
        DO 50 I=1,NBPGAU
          TRACE(I)=(T(1)+QSIGAU(I)*T(2)+ETAGAU(I)*T(3)-TRACE(I))/COEF
 50     CONTINUE
       endif
c
        RETURN
c
       ENDIF
c
c     pas de  correction pour les autres éléments
c
       DO 70 I=1,NBPGAU
          TRACE(I)=0.D0
 70    CONTINUE
c
c
       RETURN
       END




