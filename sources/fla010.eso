C FLA010    SOURCE    TTMF3     12/07/05    21:15:31     7425
      FUNCTION FLA010(T,TC,TRAV)
C---------------------------------------------------------------------
C Calcul du coefficient 'ki' de RK4 (voir fla011)
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  T       : flottant : Temps courant au pas du RK4 considéré (s)
C E/  TC      : flottant : Température des plaques au pas du RK4
C                          considéré (K)
C  /S FLA010  : flottant : Calcul de dTc/dt (K/s)
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C E/  PRESSION : Pression à l'entrée du PAR (Pa)
C E/  TEMPENT  : Température à l'entrée du PAR (K)
C E/  XiMOY    : Fraction molaire à l'entrée du PAR
C E/  M(7)     : Masse molaire des constituants du mélange (kg/mol)
C E/  EPS_MH2  : Seuil en deça duquel on suppose que la consommation
C                d'H2 max est mise à 0. (kg/s)
C E/  EPS_DT   : Ecart entre la température des plaques et celle du
C                gaz (Tc-T) en deça duquel on suppose qu'il n'y a pas
C                d'échange convectif (K)
C E/  U        : Vitesse minimale dans le PAR (= 0.01 m/s)
C E/  S        : Surface des plaques (m2)
C E/  SP       : Section de passage entrée/sortie fluide du PAR (m2)
C E/  MC       : Masse des plaques (kg)
C E/  CPC      : Chaleur spécifique des plaques (J/kg/K)
C
C-------------------
C Autres variables :
C-------------------
C
C MP      : Débit total traversant le mélange (kg/s)
C MH2MAX  : Consommation maximale par les plaques catalytique (kg/s)
C MH2     : Consommation d'H2 par les plaques pour le débit mp au
C           travers du PAR (kg/s)
C MH2EMAX : Débit d'hydrogène disponible à l'entrée du PAR (kg/s)
C MO2EMAX : Débit d'hydrogène disponible à l'entrée du PAR limité par
C           le débit d'O2 insuffisant (kg/s)
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 FLA010
      REAL*8 XE(7),OME(7)
      REAL*8 MH2MAX,MH2,MP,MH2EMAX,MO2EMAX
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
C OME(7) : Fraction massique
      PE = PRESSION
      TE = TEMPENT
      XE(1) = XN2MOY
      XE(2) = XO2MOY
      XE(3) = XH2MOY
      XE(4) = XH2OMOY
      XE(5) = XHEMOY
      XE(6) = XCO2MOY
      XE(7) = XCOMOY
      CALL FLA012(XE,OME,TRAV)
      ROE = FLA001(PE,TE,XE,TRAV)
C
C Calcul de dmh2/dt sans limitation par le débit d'H2
C et/ou d'O2 à l'entrée du PAR (kg/s)
      IF (IMODEL .EQ. 1) THEN
         MH2MAX = FLA008(PE,XE,CSTMOD(1),CSTMOD(2),CSTMOD(4),CSTMOD(5))
      ELSEIF (IMODEL .EQ. 2) THEN
         MH2MAX = FLA008(PE,XE,CSTMOD(1),CSTMOD(2),CSTMOD(4),CSTMOD(5))
      ENDIF
C
C 1. Cas sans comsommation d'hydrogène
      IF (MH2MAX .LT. EPS_MH2) THEN
         IF ((TC-TE) .LT. EPS_DT) THEN
C 1.1 et sans echange convectif
            FLA010 = 0.D0
         ELSE
C 1.2 et avec echange convectif (bilan d'énergie des plaques)
            FLA010 = -(S*FLA009(PE,TE,TC,TE,XE,TRAV)*(TC-TE))/(MC*CPC)
         ENDIF
      ELSE
C 2. Cas avec comsommation d'hydrogène
         IF ((TC-TE) .LT. EPS_DT) THEN
C 2.1 et sans echange convectif mais prise en compte d'un déficit
C     éventuel en H2/O2 et débit minimum correspondant à la vitesse U
            BOBO = MH2MAX
            MH2EMAX = U * ROE * SP * OME(3)
            MO2EMAX = U * ROE * SP * 2.D0*M(3)*OME(2)/M(2)
            IF (MH2EMAX .LT. BOBO) THEN
               BOBO = MH2EMAX
            ENDIF
            IF (MO2EMAX .LT. BOBO) THEN
               BOBO = MO2EMAX
            ENDIF
            MH2 = BOBO
            FLA010 = DELTAH * MH2 / (MC*CPC)
         ELSE
C 2.2 et avec echange convectif (bilan d'énergie des plaques)
C     et prise en compte d'un déficit éventuel en H2/O2 par rapport
C     au débit mp traversant le PAR et correspondant à MH2MAX
            MP  = FLA015(T,TC,MH2MAX,TRAV)
C 2.2.1 Déficit en O2 (indic=2) ou déficit en H2 (indic=3)
            BNERF = OME(3)
            INDIC = 3
            IF (BNERF .GT. 2.D0*M(3)*OME(2)/M(2)) THEN
               INDIC = 2
               BNERF = 2.D0*M(3)*OME(2)/M(2)
            ENDIF
            IF (MP*BNERF .LT. MH2MAX) THEN
               MP  = FLA014(T,TC,INDIC,TRAV)
               MH2 = MP*BNERF
            ELSE
C 2.2.2 Pas de déficit en O2/H2
               MH2 = MH2MAX
            ENDIF
            FLA010=(DELTAH*MH2 - S*FLA009(PE,TE,TC,TE,XE,TRAV)*(TC-TE))
     &            / (MC*CPC)
         ENDIF
      ENDIF
C
      RETURN
      END









