C PODEF2    SOURCE    PV        08/09/11    21:16:16     6150
C PODEF2    SOURCE    TC1       92/03/16    21:03:26     501
      SUBROUTINE PODEF2(V,DEFO,XL,IDEUX,xinz,sredy,you,xnu)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES DEFORMATIONS DE LA POUTRE DROITE EN 2D
C
C  ENTREE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    XL      LONGUEUR
C    IDEUX   INDICE POUR DEFORMATIONS DU 2-EME ORDRE
C
C  SORTIE
C    DEFO    DEFORMATIONS AUX 2 NOEUDS    6 PAR NOEUD
C            ( PAS D'EFFET D'EFFORT TRANCHANT || )
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DEFO(*),V(*)
C
      RL=1.D00/XL
      RL2=RL*RL
          X2=0.
          X3=0.
           YOUN=YOU
           CISAIL=YOU/(2.D0*(1.D0+XNU))

           IF( SREDY. NE.0.) X3= YOUN/CISAIL*RL2*XINZ/SREDY
             X13=1./(1.+12.*X3)
             X33=(1.+3.*X3)*X13
             X63=(1.-6.*X3)*X13
             X12=1./(1.+12.*X2)
             X32=(1.+3.*X2)*X12
             X62=(1.-6.*X2)*X12
C
C  DEFORMATIONS GENERALISEES
C
      EPS  =(V(4)-V(1))*RL
      XSIZ1=
     .RL2*(-6.*V(2)*X13-4.*XL*X33*V(3)+6.*V(5)*X13-2.*XL*X63*V(6))
      XSIZ2=
     .RL2*( 6.*V(2)*X13+2.*XL*X63*V(3)-6.*V(5)*X13+4.*XL*X33*V(6))
C
C  REMPLISSAGE DU TABLEAU DEFO
C
      DEFO(1)=EPS
      DEFO(2)=0.D0
      DEFO(3)=XSIZ1
      DEFO(4)=EPS
      DEFO(5)=0.D0
      DEFO(6)=XSIZ2
      IF(IDEUX.EQ.0) RETURN
C
C  TERMES DU SECOND ORDRE
C
      AUX1=0.5D0*(EPS**2+V(3)**2)
      AUX2=0.5D0*(EPS**2+V(6)**2)
      DEFO(1)=DEFO(1)+0.5D0*(AUX1+AUX2)
      DEFO(4)=DEFO(4)+0.5D0*(AUX1+AUX2)
      RETURN
      END




