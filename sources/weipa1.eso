C WEIPA1    SOURCE    GF238795  18/02/01    21:16:33     9724
c***********************************************************************
c Metodo della  Massima  Verosimiglianza . Soluzione iterativa  della  *
c equazione seguente con il metodo delle bisezioni :                   *
c                                                                      *
c m=N/(N*SUM(sig(i)**m*log(sig(i)))/SUM(sig(i)**m)-SUM(log(sig(i)))) *
c                                                                      *
c***********************************************************************
c
      SUBROUTINE WEIPA1(n,sig,volut,rappca,rml,sigzer)
c
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      dimension sig(n)
c
c precisione di calcolo della soluzione
      eps=1.0e-6
c incremento di 'm' per la ricerca dell'intervallo contenente la soluz.
      rincr=0.1e+1
      rn=float(n)
c valore di partenza di 'm'
      rml=1.0e-6
c valore di partenza della differenza tra i membri dell'equazione
      diffa=0.
      do 11 j=1,1000
        call emme(n,rml,sig,sum1,diff)
        diffb=diff
        if(diffa*diffb.lt.0.)then
          a1=rml1
          b2=rml
          c2=log((b2-a1)/eps)/log(2.)+1.
          l=INT(c2)
          do 3 k=1,l
            rml=(b2+a1)/2.
            call emme(n,rml,sig,sum1,diff)
            if(diff*diffa.lt.0.)then
              b2=rml
            else
              a1=rml
              diffa=diff
            endif
   3      continue
          go to 999
        else
          rml1=rml
          diffa=diffb
          rml=rml+rincr
        endif
  11  continue
C
 999  continue
C
      b1=rn/sum1
C
c calcolo del volume effettivo
      rkv=(rappca*rml+1.)/(2.*(rml+1.)**2)
      volef=volut*rkv
C
C sigma zero
      sigzer = (volef / b1 ) ** (1./rml)
C
C funzioni gamma
      uno  = 1.+ 1./rml
      rdue = 1.+ 2./rml
      gam1 = gamma(uno)
      gam2 = gamma(rdue)
C
C valore medio
      sigmed=b1**(-1./rml)*gam1
C
C varianza e deviazione standard
      var   = b1**(-2./rml)*(gam2-gam1*gam1)
      devst = sqrt(var)
C
*      write(6,13)volut
*  13  format(/,1x,'VOLUME UTILE                 :',e15.8)
*      write(6,991)volef
*  991 format(1x,'VOLUME EFFETTIVO DEL PROVINO :',e15.8)
      write(6,400)
 400  format(1x,'Mean stress    ',1x,'  Variance     ')
      write(6,500)sigmed,var
 500  format(2(1x,e15.8),/)
c
      return
      end





