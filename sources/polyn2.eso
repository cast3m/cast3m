C POLYN2    SOURCE    PV        07/11/23    21:18:38     5978
      FUNCTION POLYN2 (COEFP,LLIG,LCOL,V,U)
************************************************************************
*
*                             P O L Y N 2
*                             -----------
*
* FONCTION:
* ---------
*
*     DONNER LA VALEUR D'UN POLYNOME A 2 VARIABLES "U ET "V", TEL QUE:
*
*                            | P00 P01 P02 ... |   | 1  |
*               2  3         | P10 P11 P12 ... |   | U  |
*         (1 V V  V  ... ) X | P20 P21 P22 ... | X | U2 |
*                            | P30 P31 P32 ... |   | .. |
*                            |       ...       |
*
*     POUR UN COUPLE (U,V) DONNE.
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE   (+ = CONTENU DANS UN COMMUN)
* -----------
*
*     COEFP   (E)  COEFFICIENTS DU POLYNOME (MATRICE RECTANGULAIRE).
*     LLIG    (E)  NOMBRE DE LIGNES DE LA MATRICE.
*     LCOL    (E)  NOMBRE DE COLONNES DE LA MATRICE.
*     V,U     (E)  COUPLE EN LEQUEL ON CHERCHE LA VALEUR DU POLYNOME.
*     POLYN2  (S)  VALEUR DU POLYNOME.
*                  = 0  SI LE TABLEAU "COEFP" EST VIDE.
*
      IMPLICIT INTEGER(I-N)
      IMPLICIT real*8 (a-h,o-z)
      INTEGER LLIG,LCOL
      REAL*8 COEFP(LLIG,LCOL),V,U
      REAL*8 POLYN2
*
* VARIABLES:
* ----------
*
      REAL*8 ZZ
*
* FONCTIONS:
* ----------
*
      REAL*8 POLYNO
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     PASCAL MANIGOT     24 FEVRIER 1987
*
* LANGAGE:
* --------
*
*     FORTRAN77 + EXTENSION: DECLARATION "REAL*8".
*
************************************************************************
*
*     ON FAIT D'ABORD LA MULTIPLICATION A GAUCHE, POUR EVITER LES GRANDS
*     DEPLACEMENTS DE POINTEUR.
*
      ZZ = 0.D0
      DO 100 IB=LCOL,1,-1
         ZZ = ZZ * U + POLYNO (COEFP(1,IB),LLIG,1,V)
  100    CONTINUE
*     END DO
      POLYN2 = ZZ
*
      END


