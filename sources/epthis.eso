C EPTHIS    SOURCE    FD218221  15/09/15    21:15:00     8626

C=======================================================================
C=                            E P T H I S                              =
C=                            -----------                              =
C=                                                                     =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=   Calcul des deformations d'origine THERMIQUE dans le cas des       =
C=   materiaux ISOTROPES. Sousprogramme appele par EPTHP (epthp.eso).  =
C=                                                                     =
C=  Parametres :  (E)=Entree  (S)=Sortie                               =
C=  ------------                                                       =
C=   MFR     (E)   Numero de la FORMULATION utilisee                   =
C=   MELE    (E)   Numero de l'element fini dans NOMTP (cf. CCHAMP)    =
C=   VALMAT  (E)   Tableau des caracteristiques du materiau            =
C=   NDEFS   (E)   Nombre de composantes de deformations               =
C=   TEMP    (E)   Temperature au point de Gauss considere             =
C=   THIF    (E)   | Temperatures des differentes couches              =
C=   THM     (E)   | dans les cas des elements coques                  =
C=   THSU    (E)   | (couches INFErieure, MOYEnne, SUPErieure)         =
C=   E3      (E)   Excentrement dans la cas d'elements coques          =
C=   EPAIST  (E)   Epaisseur dans le cas d'elements coques             =
C=   RES     (S)   Tableau des valeurs des deformation thermiques      =
C=   KERRE   (S)   Indicateur d'erreur si non nul                      =
C=======================================================================

      SUBROUTINE EPTHIS (MFR,MELE,VALMAT,NDEFS,TEMP,THIF,THM,THSU,
     .                   E3,EPAIST,RES,NPINT,KERRE)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO
-INC CCREEL

      DIMENSION VALMAT(*),RES(*)

      KERRE=0

C  1 - Elements MASSIFS et POREUX
C ================================
      IF (MFR.EQ.1.OR.MFR.EQ.33.OR.MFR.EQ.31.OR.MFR.EQ.63) THEN
        ALPDT=VALMAT(1)*TEMP
C       write(6,*) 'EPTHIS',MFR,ALPDT,VALMAT(1),TEMP
C= 1.1 - Bidimensionnel PLAN Contraintes Planes
        IF (IFOUR.EQ.-2) THEN
          RES(1)=ALPDT
          RES(2)=ALPDT
          RES(3)=XZero
          RES(4)=XZero
C= 1.2 - Bidimensionnel PLAN (DP/DPGE) et Axisymetrie
        ELSE IF (IFOUR.EQ.-3.OR.IFOUR.EQ.-1.OR.IFOUR.EQ.0) THEN
          RES(1)=ALPDT
          RES(2)=ALPDT
          RES(3)=ALPDT
          RES(4)=XZero
C= 1.3 - Fourier et Tridimensionnel
        ELSE IF (IFOUR.EQ.1.OR.IFOUR.EQ.2) THEN
          RES(1)=ALPDT
          RES(2)=ALPDT
          RES(3)=ALPDT
          RES(4)=XZero
          RES(5)=XZero
          RES(6)=XZero
C= 1.4 - Unidimensionnel (1D)
        ELSE IF (IFOUR.GE.3.AND.IFOUR.LE.15.AND.MFR.EQ.1) THEN
          RES(1)=ALPDT
          IF (IFOUR.EQ.6) THEN
            RES(2)=XZero
            RES(3)=XZero
          ELSE IF (IFOUR.EQ.4.OR.IFOUR.EQ.8) THEN
            RES(2)=ALPDT
            RES(3)=XZero
          ELSE IF (IFOUR.EQ.5.OR.IFOUR.EQ.10.OR.IFOUR.EQ.13) THEN
            RES(2)=XZero
            RES(3)=ALPDT
          ELSE
            RES(2)=ALPDT
            RES(3)=ALPDT
          ENDIF
C= 1.5 - Cas non prevus
        ELSE
          KERRE=19
        ENDIF

C  2 - Elements JOINTS 2D PLAN : JOI2 et JOI3
C ============================================
      ELSE IF (MELE.EQ.85.OR.MELE.EQ.86) THEN
        ALPN=VALMAT(1)
        IF (IFOUR.EQ.-3.OR.IFOUR.EQ.-2.OR.IFOUR.EQ.-1
     &                 .OR.IFOUR.EQ.0) THEN
          RES(1)=XZero
          RES(2)=ALPN*TEMP
        ELSE
          KERRE=19
        ENDIF

C  3 - Element JOINT 3D : JOI4 ET JOT3
C ===================================
      ELSE IF (MELE.EQ.88.OR.MELE.EQ.87) THEN
        ALPN=VALMAT(1)
        IF (IFOUR.EQ.2) THEN
          RES(1)=XZero
          RES(2)=XZero
          RES(3)=ALPN*TEMP
        ELSE
          KERRE=19
        ENDIF

C  4 - Elements COQUES MINCES sauf COQ2
C ======================================
      ELSE IF (MELE.EQ.27.OR.MELE.EQ.28.OR.MELE.EQ.49.OR.
     .         MELE.EQ.93) THEN
        ALPH=VALMAT(1)
        IF (NPINT.EQ.0) THEN
          THMO=(THSU+THIF+4.D0*THM)/6.D0
          THDI=THSU-THIF
          RES(1)=ALPH*THMO
          RES(2)=RES(1)
          IF (IFOUR.GT.0) THEN
            RES(3)=XZero
            RES(4)=ALPH*THDI/EPAIST
            RES(5)=RES(4)
            RES(6)=XZero
          ELSE
            RES(3)=ALPH*THDI/EPAIST
            RES(4)=RES(3)
          ENDIF
        ELSE
          IF (IFOUR.EQ.2) THEN
            RES(1)=ALPH*TEMP
            RES(2)=RES(1)
            RES(3)=XZero
            RES(4)=XZero
          ELSE
            KERRE=19
          ENDIF
        ENDIF

C  5 - Element COQ2
C ==================
      ELSE IF (MELE.EQ.44) THEN
        ALPH=VALMAT(1)
        THMO=(THSU+THIF+4.D0*THM)/6.D0
        THDI=THSU-THIF
        IF (IFOUR.GT.0) THEN
          RES(1)=ALPH*THMO
          RES(2)=RES(1)
          RES(3)=XZero
          RES(4)=ALPH*THDI/EPAIST
          RES(5)=RES(4)
          RES(6)=XZero
        ELSE IF (IFOUR.EQ.-2) THEN
          RES(1)=ALPH*THMO
          RES(2)=XZero
          RES(3)=ALPH*THDI/EPAIST
          RES(4)=XZero
        ELSE
          RES(1)=ALPH*THMO
          RES(2)=RES(1)
          RES(3)=ALPH*THDI/EPAIST
          RES(4)=RES(3)
        ENDIF

C  6 - Elements COQUES EPAISSES
C ==============================
      ELSE IF (MELE.EQ.41.OR.MELE.EQ.56) THEN
        ALPH=VALMAT(1)
        THG=(0.5*E3)*((E3+1.D0)*THSU+(E3-1.D0)*THIF)+
     .      (1.D0-E3*E3)*THM
        RES(1)=ALPH*THG
        RES(2)=ALPH*THG
        RES(3)=XZero
        RES(4)=XZero
        RES(5)=XZero

C  7 - Elements POUTRES, TUYAUX et TIMO
C ======================================
      ELSE IF (MELE.EQ.29.OR.MELE.EQ.42.OR.MELE.EQ.84) THEN
        RES(1)=VALMAT(1)*TEMP

C  8 - Elements BARRES, CERCES et POI1
C =====================================
      ELSE IF (MELE.EQ.46.OR.MELE.EQ.95.OR.MELE.EQ.45.OR.
     .         MELE.EQ.123.OR.MELE.EQ.124) THEN
        IF ((MELE.EQ.45.AND.IFOUR.NE.-3).OR.
     .      (MELE.EQ.95.AND.IFOUR.NE.0)) THEN
          KERRE=86
        ELSE
          RES(1)=VALMAT(1)*TEMP
        ENDIF

C  9 - Autres cas non prevus
C ===========================
      ELSE
        KERRE=19
      ENDIF

      RETURN
      END










