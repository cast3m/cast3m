C JACOBF    SOURCE    CHAT      05/01/13    00:48:23     5004
C
      SUBROUTINE JACOBF (POLY,PDER,POLYM1,PDERM1,POLYM2,PDERM2,
     $                   N,ALP,BET,X)
C--------------------------------------------------------------------
C
C     Computes the Jacobi polynomial (POLY) and its derivative (PDER)
C     of degree N at X.
C
C--------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      APB  = ALP+BET
      POLY = 1.D0
      PDER = 0.D0
      IF (N .EQ. 0) RETURN
      POLYL = POLY
      PDERL = PDER
      POLY  = (ALP-BET+(APB+2.D0)*X)/2.D0
      PDER  = (APB+2.D0)/2.D0
      IF (N .EQ. 1) RETURN
      DO 20 K=2,N
         DK =K
         A1 = 2.D0*DK*(DK+APB)*(2.D0*DK+APB-2.D0)
         A2 = (2.D0*DK+APB-1.D0)*(ALP**2-BET**2)
         B3 = (2.D0*DK+APB-2.D0)
         A3 = B3*(B3+1.D0)*(B3+2.D0)
         A4 = 2.D0*(DK+ALP-1.D0)*(DK+BET-1.D0)*(2.D0*DK+APB)
         POLYN  = ((A2+A3*X)*POLY-A4*POLYL)/A1
         PDERN  = ((A2+A3*X)*PDER-A4*PDERL+A3*POLY)/A1
         PSAVE  = POLYL
         PDSAVE = PDERL
         POLYL  = POLY
         POLY   = POLYN
         PDERL  = PDER
         PDER   = PDERN
 20   CONTINUE
      POLYM1 = POLYL
      PDERM1 = PDERL
      POLYM2 = PSAVE
      PDERM2 = PDSAVE
      RETURN
      END


