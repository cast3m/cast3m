C DEVEQ2    SOURCE    BP208322  18/12/20    21:15:24     10048
      SUBROUTINE DEVEQ2(Q2,NA1,XASM,XM,PDT,NPAS,FTOTA,FINERT)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Operateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul des vitesses generalisees pour le premier demi-pas      *
*     de temps.                                                      *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* es  Q2(.,.) Vecteur des vitesses generalisees                      *
* e   NA1     Nombre total d'inconnues dans la base A                *
* e   XASM    Vecteur des amortissements generalises                 *
* e   XM      Vecteur des masses generalisees                        *
* e   PDT     pas de temps                               *
* e   NPAS    Numero du pas de temps                                 *
* e   FTOTA   Forces exterieures totalisees, sur la base A           *
*                                                                    *
*     Auteur, date de creation:                                      *
*                                                                    *
*     Denis ROBERT-MOUGIN, le 19 mai 1989.                           *
*                                                                    *
*--------------------------------------------------------------------*
      REAL*8 XASM(NA1,*),XM(NA1,*),FTOTA(NA1,*),Q2(NA1,*)
      REAL*8 FINERT(NA1,*)
*
      PDTS4 = PDT / 4.D0
*
*     .             4       .                                 .
*     Q        = ________ ( Q    + h/4 ( G    + G        - A  Q   ) )
*      i,m+1/2   4 + A  h    i,m          i,m    i,m+1/2    i  i,m
*                     I
*
*     Boucle sur les inconnues de la base A
*
      DO 10 IA = 1,NA1
         UNSM2 = 1.D0 / ( XM(IA,1) - FINERT(IA,2) )
         UNSM3 = 1.D0 / ( XM(IA,1) - FINERT(IA,3) )
         AUX1 = 4.D0 / ( 4.D0 + ( XASM(IA,1) * UNSM2 * PDT ) )
         AUX2 = ( FTOTA(IA,3) * UNSM3 ) + ( FTOTA(IA,2) * UNSM2 )
     &                        - ( XASM(IA,1) * UNSM3 * Q2(IA,3) )
         AUX3 = Q2(IA,3) + ( PDTS4 * AUX2 )
         Q2(IA,2) = AUX1 * AUX3
 10      CONTINUE
*     end do
*
      END





