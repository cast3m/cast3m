C UO2MAJ    SOURCE    STRU      07/05/31    21:15:44     5744
       SUBROUTINE UO2MAJ(IFOUR,NC,NCA,NDIM,NN,NGAT,XINVL,
     &                   TAU,SIG,EPSV,VAR,DX,SIG1,EPSV1,VAR1,DX1,W1,
     &                   SIGPT,EPSVPT,VARPT,DDX)
C-----------------------------------------------------------------------
C                MODELE UO2 (OTTOSEN+GATT_MONERIE)
C              MISE A JOUR DES GRANDEURS SUIVANTES:
C  - CONTRAINTES
C  - DEFORMATIONS VISCOPLASTIQUES
C  - VARIABLES INTERNES SCALAIRES DE GATT_MONERIE (SAUF BURNUP)
C  - DEFORMATIONS DUES A LA FISSURATION (OUVERTURES)
C-----------------------------------------------------------------------
C
C  ENTREES
C  -------
C IFOUR        = INDIQUE L HYPOTHESE DU CALCUL
C NC           = 3 NBR. TOTAL DE DIRECTIONS DE FISS. POSSIBLES
C NCA          = NBR. DE DIRECTIONS DE FISS. OU UN CRITERE EST ATTEINT
C NDIM         = NCA+1 SI CP, NCA SINON
C NN(NC)       = NUMEROS DES DIRECTIONS DE FISS. OU UN CRIT. EST ATTEINT
C XINVL(3)     = PARAMETRES DE TAILLE
C TAU          = SOUS PAS D INTEGRATION
C SIG(6)       = CONTRAINTES                     AU DEB. DU SOUS PAS
C EPSV(6)      = DEFORMATIONS VISCOPLASTIQUES    AU DEB. DU SOUS PAS
C VAR(NGAT)    = VAR. INT. SCAL. DE GATT_MONERIE AU DEB. DU SOUS PAS
C DX(NC)       = DEF. DE FISSURATION (OUV.)      AU DEB. DU SOUS PAS
C SIGPT(6)     = VIT. DES CONTRAINTES
C EPSVPT(6)    = VIT. DES DEFORMATIONS VISCOPLASTIQUES
C VARPT(NGAT)  = VIT. DES VAR. INT. SCAL. DE GATT_MONERIE
C DDX(NC1)     = VIT. DES DEF. DE FISSURATION (OUV.)
C                (+ PREMIERE COMPOSANTE DE LA VIT. DE DEF. TOT. SI CP)
C
C  SORTIES
C  -------
C SIG1(6)      = CONTRAINTES                     A LA FIN DU SOUS PAS
C EPSV1(6)     = DEFORMATIONS VISCOPLASTIQUES    A LA FIN DU SOUS PAS
C VAR1(NGAT)   = VAR. INT. SCAL. DE GATT_MONERIE A LA FIN DU SOUS PAS
C                (SAUF TAUX DE COMBUSTION)
C DX1(NC)      = DEF. DE FISSURATION (OUV.)      A LA FIN DU SOUS PAS
C W1(3)        = OUVERTURES DE FISSURATION       A LA FIN DU SOUS PAS
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
C
       PARAMETER (XZER=0.D0)
C
       DIMENSION NN(*),XINVL(*),SIG(*),EPSV(*),VAR(*),DX(*)
       DIMENSION SIG1(*),EPSV1(*),VAR1(*),DX1(*),W1(*)
       DIMENSION SIGPT(*),EPSVPT(*),VARPT(*),DDX(*)
C
       CALL SHIFTD(DX,DX1,NC)
       CALL ZDANUL(W1,3)
C
       DO 10 I=1,6
         SIG1(I)=SIG(I)+TAU*SIGPT(I)
         EPSV1(I)=EPSV(I)+TAU*EPSVPT(I)
  10   CONTINUE
       IF(IFOUR.EQ.-2) SIG1(1)=XZER
C
       VAR1(1)=VAR(1)+TAU*VARPT(1)
       VAR1(3)=VAR(3)+TAU*VARPT(3)
       VAR1(4)=VAR(4)+TAU*VARPT(4)

C ------ Porosite
       IF (VAR1(1).LT.0.D0)  VAR1(1) = 0.D0
       IF (VAR1(1).GT.0.3D0) VAR1(1) = 0.3D0
C
      IF (NCA.NE.0) THEN
        DO 30 IJ=1,NCA
           JJ=NN(IJ)
           DX1(JJ)=DX(JJ)+TAU*DDX(IJ)
  30    CONTINUE
      ENDIF
C
      DO 40 I=1,3
        IF (XINVL(I).NE.XZER) W1(I)=DX1(I)/XINVL(I)
  40   CONTINUE
C
      RETURN
      END



