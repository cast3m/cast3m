C INCRE8    SOURCE    CHAT      05/01/13    00:35:34     5004

       SUBROUTINE INCRE8(SIG,VAR,T0,TF,EPSVPT,VARPT,XMAT,NSTRS0,
     &     MFR,NVARI,NCOMAT)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),VAR(*),EPSVPT(*)
       DIMENSION VARPT(*),XMAT(*)
       DIMENSION XX(6)
       DIMENSION SIG0(6),EPS0(6)
       PARAMETER (AMAX = 1.0D20 , AMIN = 1.D-10 ,ZER=0.D0 )
       DETIER = 2.0D0/3.0D0
       ROOT = SQRT(3.0D0/2.0D0)
       CALL ZDANUL(VARPT,NVARI)
       IF (MFR.EQ.5) THEN
         NSTRS=6
         SIG0(1)=SIG(1)
         SIG0(2)=SIG(2)
         SIG0(3)=0.D0
         SIG0(4)=SIG(3)
         SIG0(5)=SIG(4)
         SIG0(6)=SIG(5)
       ELSE
         NSTRS=NSTRS0
         DO 10 I=1,NSTRS
          SIG0(I)=SIG(I)
  10     CONTINUE
       ENDIF
          DO 20 I=1,NSTRS
          EPS0(I) = ZER
  20     CONTINUE
C-------------------------------------------------------------------|
C                      COEFFICIENTS MATERIAU                        |
C-------------------------------------------------------------------|
       YOU =XMAT( 1)
       XNU =XMAT( 2)
       AA=XMAT( 5)
       B  =XMAT( 6)
       RM  =XMAT( 7)
       Q =XMAT( 8)
       R =XMAT( 9)
       H0 =XMAT(10)
       AP =XMAT(11)
       SB =XMAT(12)
       RN =XMAT(13)
       S0 =XMAT(14)
C
C ------------------------------------------------------------------|
C                        PARTIE VISCOPLASTIQUE                      |
C-------------------------------------------------------------------I
C
C
       TRACE =(SIG0(1)+SIG0(2)+SIG0(3))/3.0D0
       DO 71 I=1,NSTRS
       A = 0.0D0
       IF (I.LE.3) A=1.0D0
       XX(I) = SIG0(I)-A*TRACE
  71   CONTINUE
C
C *** CRITERE VISCOPLASTIQUE
C
       AJ2 = PROCON(XX,XX,NSTRS)
       AJ2 = SQRT(1.5D0*AJ2)
       IF (VAR(2).LT.AMIN) VAR(2)= S0
       RR =  SINH (B * AJ2/VAR(2))
C
C   temperature au debut du pas
C
       QSRT = Q/(R*T0)
       Z =  AA*XPUISS(RR,1.D0/RM,AMAX)
       VARPT(1) = Z*EXP(-QSRT)
       SSAT = SB * XPUISS(Z/AA,RN,AMAX)
       DSAT =  SSAT - S0
       SSIG =  ABS(DSAT )/DSAT
       RK =   ABS((SSAT - VAR(2))/ DSAT)
       VARPT(2)= H0*XPUISS(RK,AP,AMAX)*SSIG*VARPT(1)
       IF (AJ2.LT.AMIN)  GOTO 100
       DO  70 I=1,NSTRS
       EPS0(I) =XX(I)/AJ2*VARPT(1)/DETIER
70     CONTINUE
C
100    CONTINUE
C
        IF (MFR.EQ.5) THEN
         EPSVPT(1)=EPS0(1)
         EPSVPT(2)=EPS0(2)
         EPSVPT(3)=EPS0(4)
         EPSVPT(4)=EPS0(5)
         EPSVPT(5)=EPS0(6)
       ELSE
         DO 11 I=1,NSTRS
         EPSVPT(I)=EPS0(I)
 11      CONTINUE
       ENDIF
       RETURN
       END






