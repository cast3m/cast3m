C CRIDOM    SOURCE    CB215821  16/04/21    21:16:09     8920
      SUBROUTINE CRIDOM(SIGG,NSTRS,VARR,XMAT,NCOMAT,XCAR,ICARA,
     1INPLAS,NNVARI,MFR,XG,KERRE,SIGGD,Y)
C
C ===================================================================
C CE SOUS-PROGRAMME EST APPELE PAR "ENDOM".
C IL CALCULE LA VALEUR DU CRITERE D'ENDOMMAGEMENT POUR CHAQUE
C MODELE DE MATERIAU ENDOMMAGEABLE.
C
C ENTREES:
C -------
C NSTRS        = NBR. DE COMPOSANTES DES CONTR. OU DES DEFORM.
C SIGG(NSTRS)  = CONTR.
C NNVARI       = NBR. DE VARIABLES INTERNES PILOTANT LES EQUATIONS
C                DU MODELE
C VARR(2)...   = VARIABLES INTERNES PILOTANT LES EQUATIONS DU MODELE
C ...VARR(1+NNVARI)
C NCOMAT       = NBR. DE CARACTERISTIQUES MECANIQUES DU MATERIAU
C XMAT(NCOMAT) = CARACTERISTIQUES MECANIQUES DU MATERIAU
C MFR          = INDICE DE LA FORMULATION MECANIQUE; SEULEMENT
C                MASSIF OU COQUE POUR LES MATERIAUX ENDOMMAGEABLES
C ICARA        = NBR. DE CARACT. GEOMETRIQUES DES ELEMENTS FINIS
C XCAR(ICARA)  = CARACT. GEOMETRIQUES DES ELEMENTS FINIS
C INPLAS       = INDICE DU MODELE DECRIVANT LE MATERIAU ENDOMMAGEABLE
C
C SORTIES:
C -------
C XG= VALEUR DU CRITERE
C SIGGD(NSTRS)= DEVIATEUR DES CONTR.
C Y= SEUIL D'ENDOMMAGEMENT
C KERRE= INDICE QUI REGIT LES ERREURS
C      = 99 SI LA FORMULATION MECANIQUE N'EST PAS DISPONIBLE POUR
C           LE MODELE CONSIDERE
C
C ==================================================================
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SIGG(*),VARR(*),XMAT(*),XCAR(*),SIGGD(*)
C
      KERRE=0
      IPLAS=INPLAS-25
C
C -------------------------------------------------------------
C LE CALCUL DU CRITERE DEPEND DU MODELE DONC DE INPLAS OU IPLAS
C -------------------------------------------------------------
      GOTO (10,1000,1000) IPLAS
C
C
C ==================================================
C ECROUISSAGE ET ENDOMMAGEMENT ISOTROPES - VON MISES
C ==================================================
   10 XP=VARR(1)
      XNU=XMAT(2)
      Y=XMAT(6)
C
      IF (MFR.NE.1) GOTO 50
C
C -------
C MASSIFS
C -------
C
      TRACE=SIGG(1)+SIGG(2)+SIGG(3)
C
C ..............................
C CALCUL DU DEVIATEUR DES CONTR.
C ..............................
      CALL ZDANUL(SIGGD,NSTRS)
      DO 30 I=1,NSTRS
                       A=0.D0
                       IF (I.LE.3) A=1.D0/3.D0
                       SIGGD(I)=SIGG(I)-A*TRACE
   30 CONTINUE
C
C ............................................
C CALCUL DE LA CONTR. EQUIVALENTE DE VON MISES
C ............................................
      SIGEQI=PROCON(SIGGD,SIGGD,NSTRS)
      SIGEQI=SQRT((3.D0/2.D0)*SIGEQI)
C
C .............................................................
C                     CALCUL DU CRITERE
C LORSQUE LE PREMIER PAS NON NUL DE L'UTILISATEUR EST PLASTIQUE
C LE VON MISES RISQUE D'ETRE NUL POUR LE PAS PRECEDENT : POUR
C EVITER LA DIVISION PAR ZERO ON RAJOUTE 1.D-10
C .............................................................
      SIGEQI=SIGEQI+1.D-10
      TRIAXI=TRACE/(3.D0*SIGEQI)
      TRIAXI=TRIAXI*TRIAXI
      TRIAXI=3.D0*(1.D0-2.D0*XNU)*TRIAXI+(2.D0*(1.D0+XNU))/3.D0
      XG=(XP*TRIAXI)-Y
      GOTO 1000
C
   50 IF (MFR.NE.5) GOTO 998
C
C ---------------------------------------
C COQUES EPAISSES - NON ENCORE DISPONIBLE
C ---------------------------------------
C
  998 KERRE=99
 1000 CONTINUE
      RETURN
      END



