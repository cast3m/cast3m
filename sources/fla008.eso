C FLA008    SOURCE    SERRE     12/07/10    21:15:13     7431
        FUNCTION FLA008(P,X,A1,B1,XH2OFF,BETA)
C----------------------------------------------------------------------
C This subroutine evaluate the consumption hydrogen rate (in kg/s)
C for a passive autocatalytic recombiner; a Siemens law type is used.
C----------------------------------------------------------------------
C
C---------------------------
C Paramètres Entrée/Sortie :
C---------------------------
C
C E/    P      : Averaged containment pressure (Pa)
C E/    X(7)   : Molar fraction (1=N2 ,2=O2 ,3=H2 ,4=H2O)
C E/    A1     : First Siemens like law coefficient
C E/    B1     : Secund Siemens-like law coefficient
C E/    XH2OFF : H2 molar fraction to switch off the PAR
C E/    BETA   : Link from the low Xh2 to the high Xh2
C  /S   FLA008 : Consumption hydrogen rate (in kg/s)
C
C---------------------------------------------------------------------
C
C Langage : FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C 99 : Siemens law modification : relaxation to zero with tanh
C      (old one: fla008=bobo*(0.308D-8*p+0.406D-3))
C 03 : Modification of the linear pressure evolution and
C      coefficients are now user data. Default values change to
C      (0.48D-8*p+0.58D-3) for a Siemens FR90/1-150 PAR.
C 05 : Positivity is assumed even we test it in fla010 and fla019
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 X(7)
C
C Threshold value for the Siemens Law (bobo=min(Xh2,2Xo2,0.08))
      BOBO = 2.D0 * X(2)
      IF (X(3) .LT. BOBO) THEN
         BOBO = X(3)
      ENDIF
      IF (0.08D0 .LT. BOBO) THEN
         BOBO = 0.08D0
      ENDIF
C
C Siemens Law
      SIE008 = (A1*P+B1) * BOBO * TANH((20.D0/BETA)*(BOBO-XH2OFF))
      IF (SIE008 .LT. 0.D0) THEN
          FLA008 = 0.D0
      ELSE
          FLA008 = SIE008
      ENDIF
C
      RETURN
      END











