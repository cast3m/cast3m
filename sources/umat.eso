C UMAT      SOURCE    CHAT      11/03/16    21:32:49     6902
      SUBROUTINE UMAT ( STRESS, STATEV, DDSDDE, SSE, SPD, SCD,
     &                  RPL, DDSDDT, DRPLDE, DRPLDT,
     &                  STRAN, DSTRAN, TIME, DTIME,
     &                  TEMP, DTEMP, PREDEF, DPRED,
     &                  CMNAME, NDI, NSHR, NTENS, NSTATV,
     &                  PROPS, NPROPS, COORDS,
     &                  DROT, PNEWDT, CELENT, DFGRD0, DFGRD1,
     &                  NOEL, NPT, LAYER, KSPT, KSTEP, KINC )

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)

C-----------------------------------------------------------------------
C
C     DESCRIPTION FONCTIONNELLE :
C     -------------------------
C
C     Version du module externe UMAT integree au code pour eviter tout
C     probleme d'edition de lien et pour permettre le passage de tests
C     de non regression.
C
C     En utilisation normale, ce module doit etre surcharge par celui
C     de meme nom fourni par l'utilisateur.
C
C     Si cette version de UMAT est appelee, cela signifie qu'il n'y a
C     pas eu surcharge par le module utilisateur
C     ATTENTION : si l'utilisateur oublie de surcharger et affecte a sa
C     loi le meme numero que l'un de ceux pris en charge ci-apres, il y
C     a risque d'erreur non controlee a l'execution.
C
C     CONVENTION : on autorise la programmation de l'utilisateur a se
C     servir de KINC comme code retour, de la meme maniere que l'on
C     s'en sert ci-dessous. Regles a respecter :
C     1 - Pas d'initialisation superflue de KINC en entrant dans UMAT.
C         KINC est initialise a 1 par WKUMA0, avant l'appel a UMAT.
C     2 - En cas d'erreur, KINC est affecte d'une valeur differente de 1
C
C      Dans ce dernier cas, l'utilisateur a la possibilite de completer
C      le message d'erreur affiche (de numero 965) en surchargeant le
C      sousprogramme UMAERR (umaerr.eso).
C
C-----------------------------------------------------------------------
C
C     DESCRIPTION DE L'INTERFACE :
C     --------------------------
C
C     IN/OUT : STRESS : REAL*8(NTENS), tenseur des contraintes
C                       En entree : tenseur des contraintes a t0
C                       En sortie : tenseur des contraintes a t0+dt
C                       Dans le cas des grandes transformations, il
C                       s'agit des contraintes de Cauchy ("actuelles").
C     IN/OUT : STATEV : REAL*8(*), variables internes
C                       En entree : variables internes a t0
C                       En sortie : variables internes a t0+dt
C
C
C     OUT    : DDSDDE : REAL*8(NTENS,NTENS), matrice jacobienne du
C                       modele (matrice de Hooke tangente) a t0+dt
C                       Sortie facultative, non exploitee par CAST3M
C                       pour l'instant
C
C     IN/OUT : SSE    : REAL*8, energie de deformation elastique
C              SPD    : REAL*8, dissipation plastique
C              SCD    : REAL*8, dissipation visqueuse
C                       Valeurs a t0 en entree, a t0+dt en sortie
C                       Entrees/sorties facultatives, non exploitees par
C                       CAST3M pour l'instant
C
C
C     OUT    : RPL    : REAL*8, puissance calorifique volumique degagee
C                       par le travail mecanique, a t0+dt
C              DDSDDT : REAL*8(NTENS),derivee du tenseur des contraintes
C                       par rapport a la temperature, a t0+dt
C              DRPLDE : REAL*8(NTENS), derivees de RPL par rapport aux
C                       composantes du tenseur des deformations, a t0+dt
C              DRPLDT : REAL*8, derivee de RPL par rapport a la
C                       temperature, a t0+dt
C                       Sorties facultatives, non exploitees par CAST3M
C                       pour l'instant
C                       Ces sorties sont prevues pour un couplage fort
C                       entre thermique et mecanique
C
C
C     IN     : STRAN  : REAL*8(NTENS), tenseur des deformations
C                       totales a t0
C              DSTRAN : REAL*8(NTENS), tenseur des increments de
C                       deformation totale par rapport a l'etat
C                       de reference a t0
C
C
C     IN     : TIME   : REAL*8(2), TIME(1) = 0
C                                  TIME(2) = t0
C              DTIME  : REAL*8,    DTIME   = dt
C                       t0 : precedent instant d'equilibre atteint
C                       dt : nouveau pas de temps propose par PASAPAS
C                       pour atteindre l'equilibre avec l'increment de
C                       deformation totale impose (DSTRAN)
C                       Remarque : les notions attachees a ces entrees
C                       sont differentes lorsque l'on adhere a ABAQUS
C                       (dans ce cas la variable TIME(1) a son utilite)
C
C
C     IN     : TEMP   : REAL*8, temperature a t0
C              DTEMP  : REAL*8, increment de temperature a t0+dt
C              PREDEF : REAL*8(*), vecteur des parametres externes de
C                       la loi de comportement, valeurs a t0
C              DPRED  : REAL*8(*), increments des parametres externes
C                       a t0+dt
C
C
C     IN     : CMNAME : CHARACTER*16, identifiant de la loi de
C                       comportement
C                       On conserve le type 'chaine de caracteres' pour
C                       l'identifiant de la loi, afin de preserver la
C                       compatibilite avec ABAQUS.
C                       Dans le cas d'une adherence a CAST3M, la loi est
C                       identifiee par le numero qui lui a ete attribue:
C                       argument 'NUME_LOI' de l'operateur MODE.
C                       Par convention, ce numero est encode dans les 4
C                       derniers caracteres de la chaine, et doit etre
C                       recupere dans UMAT par une instruction du type
C                       K4ILOI = CMNAME(13:16)
C                       avec K4ILOI variable locale de type CHARACTER*4
C
C
C     IN     : NDI    : INTEGER definissant le type de calcul CAST3M
C                       =  2 : tridimensionnel (3D)
C                       =  0 : axisymetrie (2D)
C                       = -1 : deformations planes (2D)
C                       = -2 : contraintes planes (2D)
C                       = -3 : deformations planes generalisees (2D)
C                       =  3 a 11 : unidimensionnel plan (massif 1D)
C                       = 12 a 14 : unidimensionnel axisymetrique (massif 1D)
C                       = 15 : unidimensionnel spherique (massif 1D)
C
C
C     IN     : NSHR   : INTEGER, nombre de composantes extradiagonales
C                       du tenseur des contraintes
C                       Entree non active dans le cas d'une adherence
C                       a CAST3M
C
C
C     IN     : NTENS  : INTEGER, nombre de composantes du tenseur des
C                       contraintes
C              NSTATV : INTEGER, nombre de variables internes
C
C
C     IN     : PROPS  : REAL*8(NPROPS), vecteur des constantes
C                       de materiau
C                       N.B. Il peut s'agir de coefficients constants
C                       intervenant dans des formules analytiques
C                       decrivant les evolutions des proprietes du
C                       materiau en fonction de la temperature et/ou
C                       d'autres parametres externes.
C                       OU il peut s'agir plus simplement de proprietes
C                       de materiau constantes.
C              NPROPS : INTEGER, nombre de constantes de materiau
C
C
C     IN     : COORDS : REAL*8(3), coordonnees cartesiennes du point
C                       d'integration courant
C
C
C     IN     : DROT   : REAL*8(3,3)
C                       Dans le cas d'une adherence a CAST3M
C                       ------------------------------------
C                       Matrice de passage du repere local de l'element
C                       fini massif au repere general du maillage
C                       DROT(l,c) : composante l du vecteur c de la base
C                       locale dans la base generale
C                       Entree non active pour les elements finis non
C                       massifs
C                       Dans le cas d'une adherence a ABAQUS
C                       ------------------------------------
C                       Matrice d'increments de rotation
C                       Cette matrice decrit la rotation sur le pas de
C                       temps de la base dans laquelle sont exprimes
C                       les tenseurs de contraintes et de deformations
C
C
C     OUT    : PNEWDT : REAL*8, rapport entre le nouveau pas de temps
C                       suggere et le pas de temps donne en entree
C                       (NEWDT = PNEWDT * DTIME)
C
C
C     IN     : CELENT : REAL*8, longueur caracteristique de l'element
C                       Dans le cas d'une adherence a CAST3M, cette
C                       longueur caracteristique est determinee par
C                       LOCARA comme la distance maximale entre deux
C                       noeuds de l'element.
C
C
C     IN     : DFGRD0 : REAL*8(3,3), tenseur gradient de deplacement
C                       a t0
C              DFGRD1 : REAL*8(3,3), tenseur gradient de deplacement
C                       a t0+dt
C
C
C     IN     : NOEL   : INTEGER, numero de l'element courant
C              NPT    : INTEGER, numero du point d'integration courant
C
C
C     IN     : LAYER  : INTEGER, numero de couche pour des coques
C                       composites ou des solides multi-couches
C              KSPT   : INTEGER, numero de section dans la couche
C                       courante
C                       Entrees non actives dans le cas d'une adherence
C                       a CAST3M
C
C
C     IN     : KSTEP  : INTEGER
C     IN     : KINC   : INTEGER
C                       Entrees n'ayant pas de sens dans le cas
C                       d'une adherence a CAST3M
C
C-----------------------------------------------------------------------
C
C     MODELES PRIS EN CHARGE par le module UMAT 'bouchon' :
C     ---------------------------------------------------
C
C     ILOI = 10 : ELASTICITE ISOTROPE ISOTHERME
C                 FORMULATION MASSIVE
C
C     ILOI = 11 : ELASTICITE ISOTROPE NON ISOTHERME
C                 FORMULATION MASSIVE
C                 La temperature 'TETA' est definie en tant que
C                 parametre externe
C              => Instanciation du materiau et prise en charge de la
C                 dilatation thermique dans UMAT
C
C     ILOI = 12 : ELASTICITE ORTHOTROPE ISOTHERME
C                 FORMULATION MASSIVE
C
C     ILOI = 21 : Modele 'FLUAGE' 'POLYNOMIAL' de CAST3M, INPLAS=21
C                 RESTRICTION A LA FORMULATION MASSIVE
C
C     ILOI = 22 : Modele 'FLUAGE' 'POLYNOMIAL' de CAST3M, INPLAS=21
C                 RESTRICTION A LA FORMULATION MASSIVE
C                 Instanciation des composantes de materiau au fur et a
C                 mesure des iterations internes.
C                 Le modele comprend cette fois des parametres externes.
C                 Les composantes de materiau sont evaluees par les
C                 memes fonctions externes que celles utilisees par
C                 l'operateur VARI.
C                 MODELE 'MAQUETTE'
C
C     ILOI =  7 : Modele 'VISCOPLASTIQUE' 'GATT_MONERIE' de CAST3M,
C                 INPLAS=107
C                 RESTRICTION A LA FORMULATION MASSIVE
C
C     ILOI =  5 : Modele 'PLASTIQUE' 'ISOTROPE' de CAST3M, INPLAS=5
C                 RESTRICTION A LA FORMULATION MASSIVE
C
C     ILOI = 31 : Modele MOONEY-RIVLIN en GRANDES TRANSFORMATIONS
C                 Cas des CONTRAINTES PLANES dans la direction 3 (2D/3D)
C                 Restriction a la formulation MASSIVE
C
C     ILOI = 32 : Modele NEO-HOOKIEN en GRANDES TRANSFORMATIONS
C                 Restriction a la formulation MASSIVE
C
C-----------------------------------------------------------------------
C     Arguments de l'interface
C
      CHARACTER*16  CMNAME
C
      INTEGER       NDI, NSHR, NTENS, NSTATV, NPROPS,
     &              NOEL, NPT, LAYER, KSPT, KSTEP, KINC
C
      REAL*8        STRESS(NTENS), STATEV(*),
     &              DDSDDE(NTENS,NTENS),
     &              SSE, SPD, SCD,
     &              RPL, DDSDDT(NTENS), DRPLDE(NTENS), DRPLDT,
     &              STRAN(NTENS), DSTRAN(NTENS),
     &              TIME(2), DTIME,
     &              TEMP, DTEMP, PREDEF(*), DPRED(*),
     &              PROPS(NPROPS),
     &              COORDS(3),
     &              DROT(3,3),
     &              PNEWDT,
     &              CELENT,
     &              DFGRD0(3,3), DFGRD1(3,3)
C
C     Variables locales
C
      CHARACTER*4   K4ILOI
C
      PARAMETER(UN=1.D0,DEUX=2.D0,UNDEMI=.5D0)
C
      REAL*8        YOUN, NU, BULK3, G2, LAM, EPST0(6), DEPST(6),
     &              TETA0, DTETA, TETAF, ALPH, TREF, DYDT, DNDT,
     &              YOUN0, NU0, BULK30, G20, LAM0,
     &              YOUNF, NUF, BULK3F, G2F, LAMF,
     &              DELDSE(6,6), ETHERM(6), DTHERM(6),
     &              YREF, NREF, EPSE0(6), DEPSE(6),DHOOK(6,6),
     &              DSIG2(3,3),DEPST2(3,3),TENS(3,3),DSIG(6),
     &              V1(3),V2(3),V3(3),V(3,3),P(3,3)
C
C
C---------------------- Debut du code executable -----------------------
C
C
      K4ILOI = CMNAME(13:16)
C
C=======================================================================
C     ELASTICITE ISOTROPE ISOTHERME
C=======================================================================
C
      IF (K4ILOI.EQ.'  10') THEN
C
C-----------------------------------------------------------------------
C        Constantes de materiau
C        ----------------------
C        PROPS(1)  : YOUN  Module d'Young
C        PROPS(2)  : NU    Coefficient de Poisson
C-----------------------------------------------------------------------
C
C        Recuperation des constantes de materiau
C
         YOUN = PROPS(1)
         NU   = PROPS(2)
C
C        Calcul de la matrice de Hooke (constante)
C        Hypotheses : NTENS=6
C                     les composantes diagonales sont les 3 premieres
C
         BULK3 = YOUN/(1.0D0-2.0D0*NU)
         G2 = YOUN/(1.0D0+NU)
         LAM = (BULK3-G2)/3.0D0
C
         DO 100 J=1,NTENS
            DO 101 I=1,NTENS
               DDSDDE(I,J)=0.0D0
 101        CONTINUE
 100     CONTINUE
C
         DO 110 J=2,3
            DO 111 I=1,J-1
               DDSDDE(I,J)=LAM
               DDSDDE(J,I)=LAM
 111        CONTINUE
 110     CONTINUE
         DO 112 J=1,3
            DDSDDE(J,J)=G2+LAM
 112     CONTINUE
         DO 113 J=4,6
            DDSDDE(J,J)=G2
 113     CONTINUE
C
C        Deformations : gamma -> epsilon pour les termes extradiagonaux
C        Uniquement sur les increments car on n'a pas besoin des cumuls
C
         DO 120 J=1,3
            DEPST(J)=DSTRAN(J)
 120     CONTINUE
         DO 121 J=4,6
            DEPST(J)=DSTRAN(J)/2.0D0
 121     CONTINUE
C
C        Calcul des contraintes a la fin du pas
C
         DO 130 J=1,NTENS
            DO 131 I=1,NTENS
               STRESS(I)=STRESS(I)+DDSDDE(I,J)*DEPST(J)
 131        CONTINUE
 130     CONTINUE
C
C=======================================================================
C     ELASTICITE ISOTROPE NON ISOTHERME, premier modele
C     La temperature 'TETA' est definie en tant que parametre externe
C  => Instanciation du materiau et prise en charge de la dilatation
C     thermique dans UMAT
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  11') THEN
C
C-----------------------------------------------------------------------
C        Variables internes du modele
C        ----------------------------
C
C        STATEV( 1): EEXX  Deformation elastique XX
C        STATEV( 2): EEYY  Deformation elastique YY
C        STATEV( 3): EEZZ  Deformation elastique ZZ
C        STATEV( 4): EEXY  Deformation elastique XY
C        STATEV( 5): EEXZ  Deformation elastique XZ
C        STATEV( 6): EEYZ  Deformation elastique YZ
C
C        STATEV( 7): ETXX  Deformation thermique XX
C        STATEV( 8): ETYY  Deformation thermique YY
C        STATEV( 9): ETZZ  Deformation thermique ZZ
C        STATEV(10): ETXY  Deformation thermique XY
C        STATEV(11): ETXZ  Deformation thermique XZ
C        STATEV(12): ETYZ  Deformation thermique YZ
C
C        Parametres externes du modele
C        -----------------------------
C
C        PREDEF(1) : TETA  Temperature
C        DPRED(1)  :       Variation de temperature sur le pas de temps
C
C        N.B. On nomme 'TETA' la temperature afin de pouvoir prendre en
C             charge au niveau le plus bas l'instanciation du materiau
C             et la dilatation thermique
C
C        Constantes de materiau
C        ----------------------
C
C        PROPS(1)  : YOUN  Module d'Young a la temperature
C                          de reference TREF
C        PROPS(2)  : NU    Coefficient de Poisson a la temperature
C                          de reference TREF
C        PROPS(3)  : ALPH  Coefficient de dilatation thermique
C                          (constant)
C        PROPS(4)  : TREF  Temperature de reference
C        PROPS(5)  : DYDT  Derivee du module d'Young par rapport
C                          a la temperature
C        PROPS(6)  : DNDT  Derivee du coefficient de Poisson
C                          par rapport a la temperature
C-----------------------------------------------------------------------
C
C        Recuperation des parametres externes du modele
C
         TETA0 = PREDEF(1)
         DTETA = DPRED(1)
         TETAF = TETA0 + DTETA
C
C        Recuperation des constantes de materiau
C
         YOUN = PROPS(1)
         NU   = PROPS(2)
         ALPH = PROPS(3)
         TREF = PROPS(4)
         DYDT = PROPS(5)
         DNDT = PROPS(6)
C
C        Evaluation des proprietes de materiau au debut du pas
C
         YOUN0 = YOUN + DYDT * (TETA0-TREF)
         NU0   = NU   + DNDT * (TETA0-TREF)
C
         BULK30 = YOUN0/(1.0D0-2.0D0*NU0)
         G20 = YOUN0/(1.0D0+NU0)
         LAM0 = (BULK30-G20)/3.0D0
C
C        Evaluation des proprietes de materiau a la fin du pas
C
         YOUNF = YOUN + DYDT * (TETAF-TREF)
         NUF   = NU   + DNDT * (TETAF-TREF)
C
         BULK3F = YOUNF/(1.0D0-2.0D0*NUF)
         G2F = YOUNF/(1.0D0+NUF)
         LAMF = (BULK3F-G2F)/3.0D0
C
C        Calcul de la matrice de Hooke a la fin du pas
C        Hypotheses : NTENS=6
C                     les composantes diagonales sont les 3 premieres
C
         DO 200 J=1,NTENS
            DO 201 I=1,NTENS
               DDSDDE(I,J)=0.0D0
 201        CONTINUE
 200     CONTINUE
C
         DO 210 J=2,3
            DO 211 I=1,J-1
               DDSDDE(I,J)=LAMF
               DDSDDE(J,I)=LAMF
 211        CONTINUE
 210     CONTINUE
         DO 212 J=1,3
            DDSDDE(J,J)=G2F+LAMF
 212     CONTINUE
         DO 213 J=4,6
            DDSDDE(J,J)=G2F
 213     CONTINUE
C
C        Calcul de la variation de la matrice de Hooke au cours du pas
C
         DO 220 J=1,NTENS
            DO 221 I=1,NTENS
               DELDSE(I,J)=0.0D0
 221        CONTINUE
 220     CONTINUE
C
         DO 230 J=2,3
            DO 231 I=1,J-1
               DELDSE(I,J)=LAMF-LAM0
               DELDSE(J,I)=LAMF-LAM0
 231        CONTINUE
 230     CONTINUE
         DO 232 J=1,3
            DELDSE(J,J)=(G2F+LAMF)-(G20+LAM0)
 232     CONTINUE
         DO 233 J=4,6
            DELDSE(J,J)=G2F-G20
 233     CONTINUE
C
C        Calcul de la dilatation thermique au debut du pas et de
C        l'increment de dilatation thermique au cours du pas
C
         DO 240 J=1,3
            ETHERM(J)=ALPH*(TETA0-TREF)
 240     CONTINUE
         DO 241 J=4,6
            ETHERM(J)=0.0D0
 241     CONTINUE
C
         DO 242 J=1,3
            DTHERM(J)=ALPH*DTETA
 242     CONTINUE
         DO 243 J=4,6
            DTHERM(J)=0.0D0
 243     CONTINUE
C
C        Deformations : gamma -> epsilon pour les termes extradiagonaux
C
         DO 250 J=1,3
            EPST0(J)=STRAN(J)
  250    CONTINUE
         DO 251 J=4,6
            EPST0(J)=STRAN(J)/2.0D0
  251    CONTINUE
         DO 252 J=1,3
            DEPST(J)=DSTRAN(J)
  252    CONTINUE
         DO 253 J=4,6
            DEPST(J)=DSTRAN(J)/2.0D0
  253    CONTINUE
C
C        Calcul des contraintes a la fin du pas
C
         DO 260 J=1,NTENS
            DO 261 I=1,NTENS
               STRESS(I)=STRESS(I)+DDSDDE(I,J)*(DEPST(J)-DTHERM(J))
     &                            +DELDSE(I,J)*(EPST0(J)-ETHERM(J))
 261        CONTINUE
 260     CONTINUE
C
C        Calcul de la dilatation thermique a la fin du pas
C
         DO 270 J=1,NTENS
            STATEV(NTENS+J)=ETHERM(J)+DTHERM(J)
 270     CONTINUE
C
C        Calcul de la deformation elastique a la fin du pas
C
         DO 280 J=1,NTENS
            STATEV(J)=EPST0(J)+DEPST(J)-STATEV(NTENS+J)
 280     CONTINUE
C
C=======================================================================
C     ELASTICITE ORTHOTROPE ISOTHERME
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  12') THEN
C-----------------------------------------------------------------------
C        Constantes de materiau
C        ----------------------
C        PROPS(1-3)   : Modules d'Young
C        PROPS(4-6)   : Coefficients de Poisson
C        PROPS(7-9)   : Modules de cisaillement
C        PROPS(10-18) : repere d'orthotropie / repere local
C-----------------------------------------------------------------------
C
C        Recuperation des constantes du materiau
C
          YG1=PROPS(1)
          YG2=PROPS(2)
          YG3=PROPS(3)
          XNU12=PROPS(4)
          XNU23=PROPS(5)
          XNU13=PROPS(6)
          G12=PROPS(7)
          G23=PROPS(8)
          G13=PROPS(9)
          XNU21=(YG2/YG1)*XNU12
          XNU32=(YG3/YG2)*XNU23
          XNU31=(YG3/YG1)*XNU13
          AUX=(UN-XNU12*XNU21-XNU23*XNU32-XNU13*XNU31
     .       -DEUX*XNU21*XNU32*XNU13)
          AUX1=AUX/YG1
          AUX2=AUX/YG2
          AUX3=AUX/YG3
C-----------------------------------------------------------------------
C
C        Construction de la matrice de Hooke dans repere d'orthotropie
C
          CALL ZERO(DHOOK,6,6)
          DHOOK(1,1)=(UN-XNU23*XNU32)/AUX1
          DHOOK(1,2)=(XNU21+XNU31*XNU23)/AUX1
          DHOOK(2,1)=DHOOK(1,2)
          DHOOK(1,3)=(XNU31+XNU21*XNU32)/AUX1
          DHOOK(3,1)=DHOOK(1,3)
          DHOOK(2,2)=(UN-XNU13*XNU31)/AUX2
          DHOOK(2,3)=(XNU32+XNU12*XNU31)/AUX2
          DHOOK(3,2)=DHOOK(2,3)
          DHOOK(3,3)=(UN-XNU12*XNU21)/AUX3
          DHOOK(4,4)=DEUX * G12
          DHOOK(5,5)=DEUX * G13
          DHOOK(6,6)=DEUX * G23
C-----------------------------------------------------------------------
C
C        Recuperation des vecteurs d'orthotropie / repere local
C
          V1(1)=PROPS(10)
          V1(2)=PROPS(11)
          V1(3)=PROPS(12)
          V2(1)=PROPS(13)
          V2(2)=PROPS(14)
          V2(3)=PROPS(15)
          CALL CROSS2(V1,V2,V3,IER)
          V(1,1)=V1(1)
          V(2,1)=V1(2)
          V(3,1)=V1(3)
          V(1,2)=V2(1)
          V(2,2)=V2(2)
          V(3,2)=V2(3)
          V(1,3)=V3(1)
          V(2,3)=V3(2)
          V(3,3)=V3(3)
C----------------------------------------------------------------------------
C
C        Expression des increments des deformations dans repere d'orthotropie
C        P  : matrice de passage du repere global au repere d'orthotropie
C
          CALL MULMAT(P,DROT,V,3,3,3)
C
          DEPST2(1,1)=DSTRAN(1)
          DEPST2(2,2)=DSTRAN(2)
          DEPST2(3,3)=DSTRAN(3)
          DEPST2(1,2)=DSTRAN(4)/DEUX
          DEPST2(1,3)=DSTRAN(5)/DEUX
          DEPST2(2,3)=DSTRAN(6)/DEUX
          DEPST2(2,1)=DEPST2(1,2)
          DEPST2(3,1)=DEPST2(1,3)
          DEPST2(3,2)=DEPST2(2,3)
          CALL PRODT(TENS,DEPST2,P,3,3)
C----------------------------------------------------------------------------
C
C        Calcul des increments de contrainte dans le repere d'orthotropie
C
          DEPST(1)=TENS(1,1)
          DEPST(2)=TENS(2,2)
          DEPST(3)=TENS(3,3)
          DEPST(4)=TENS(1,2)
          DEPST(5)=TENS(1,3)
          DEPST(6)=TENS(2,3)
C
          DO 5500 I=1,6
             DSIG(I)=0.D0
             DO 55001 J=1,6
                DSIG(I)=DSIG(I)+DHOOK(I,J)*DEPST(J)
55001        CONTINUE
 5500     CONTINUE
C----------------------------------------------------------------------------
C
C        Expression des increments de contrainte dans le repere global
C
          DSIG2(1,1)=DSIG(1)
          DSIG2(2,2)=DSIG(2)
          DSIG2(3,3)=DSIG(3)
          DSIG2(1,2)=DSIG(4)
          DSIG2(1,3)=DSIG(5)
          DSIG2(2,3)=DSIG(6)
          DSIG2(2,1)=DSIG2(1,2)
          DSIG2(3,1)=DSIG2(1,3)
          DSIG2(3,2)=DSIG2(2,3)
          CALL TRPOSE(P)
          CALL PRODT(TENS,DSIG2,P,3,3)
C----------------------------------------------------------------------------
C
C        Calcul des contraintes a la fin du pas dans le repere global
C
          STRESS(1)=STRESS(1)+TENS(1,1)
          STRESS(2)=STRESS(2)+TENS(2,2)
          STRESS(3)=STRESS(3)+TENS(3,3)
          STRESS(4)=STRESS(4)+TENS(1,2)
          STRESS(5)=STRESS(5)+TENS(1,3)
          STRESS(6)=STRESS(6)+TENS(2,3)
C
C=======================================================================
C     Modele 'FLUAGE' 'POLYNOMIAL' de CAST3M, INPLAS=21
C     RESTRICTION A LA FORMULATION MASSIVE
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  21') THEN
C
         CALL UMAPOL ( STRESS, STATEV, DDSDDE,
     &                 STRAN, DSTRAN, DTIME,
     &                 TEMP, DTEMP, PREDEF, DPRED,
     &                 NTENS, NSTATV,
     &                 PROPS, NPROPS, COORDS,
     &                 NOEL, NPT, KINC )
C
C=======================================================================
C     Modele 'FLUAGE' 'POLYNOMIAL' de CAST3M, INPLAS=21
C     RESTRICTION A LA FORMULATION MASSIVE
C     Instanciation des composantes de materiau au fur et a mesure des
C     iterations internes.
C     Le modele comprend cette fois des parametres externes.
C     Les composantes de materiau sont evaluees par les memes fonctions
C     externes que celles utilisees par l'operateur VARI.
C     MODELE 'MAQUETTE'
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  22') THEN
C
         CALL UMAPO2 ( STRESS, STATEV, DDSDDE,
     &                 STRAN, DSTRAN, DTIME,
     &                 TEMP, DTEMP, PREDEF, DPRED,
     &                 NTENS, NSTATV,
     &                 PROPS, NPROPS, COORDS,
     &                 NOEL, NPT, KINC )
C
C=======================================================================
C     Modele 'VISCOPLASTIQUE' 'GATT_MONERIE' de CAST3M, INPLAS=107
C     RESTRICTION A LA FORMULATION MASSIVE
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'   7') THEN
C
         CALL UMAGAT ( STRESS, STATEV, DDSDDE,
     &                 STRAN, DSTRAN, DTIME,
     &                 TEMP, DTEMP, PREDEF, DPRED,
     &                 NTENS, NSTATV,
     &                 PROPS, NPROPS, COORDS,
     &                 NOEL, NPT, KINC )
C
C=======================================================================
C     Modele 'PLASTIQUE' 'ISOTROPE' de CAST3M, INPLAS=5
C     RESTRICTION A LA FORMULATION MASSIVE
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'   5') THEN
C
         CALL UMAPLI ( STRESS, STATEV, DDSDDE,
     &                 STRAN, DSTRAN, DTIME,
     &                 TEMP, DTEMP, PREDEF, DPRED,
     &                 NTENS, NSTATV,
     &                 PROPS, NPROPS, COORDS,
     &                 NOEL, NPT, KINC )
C
C=======================================================================
C     Modele HYPERELASTIQUE MOONEY-RIVLIN
C            CONTRAINTES PLANES INCOMPRESSIBLE (direction 3)
C            DEFORMATIONS PLANES  Quasi INCOMPRESSIBLE
C            TRIDIMENSIONNEL      Quasi INCOMPRESSIBLE
C     Contribution de Laurent Gornet - Ecole Centrale de Nantes (2006)
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  31') THEN
C
        CALL MOONEY ( STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                DFGRD0, DFGRD1, KSTEP, KINC,SSE,spd,scd,rpl,
     &                DDSDDT,DRPLDE,DRPLDT,cmname,coords,drot, PNEWDT,
     &                celent,NOEL, NPT, LAYER, KSPT )
C
C=======================================================================
C     Modele HYPERELASTIQUE NEO-HOOKEEN COMPRESSIBLE (2006/2010)
C     en GRANDES TRANSFORMATIONS
C     Contribution de Laurent Gornet - Ecole Centrale de Nantes (2010)
C=======================================================================
C
      ELSE IF (K4ILOI.EQ.'  32') THEN
C
        CALL NEOHOO ( STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                DFGRD0, DFGRD1, KSTEP, KINC )

C
      ELSE IF (K4ILOI.EQ.'  33') THEN
C
        CALL GDGD (STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                   TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                   NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                   DFGRD0, DFGRD1, KSTEP, KINC,SSE,spd,scd,rpl,
     &                   DDSDDT,DRPLDE,DRPLDT,cmname,coords,drot,PNEWDT,
     &                   celent,NOEL, NPT, LAYER, KSPT )
      ELSE  IF (K4ILOI.EQ.'  34') THEN
       CALL HARTS (STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                   TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                   NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                   DFGRD0, DFGRD1, KSTEP, KINC,SSE,spd,scd,rpl,
     &                   DDSDDT,DRPLDE,DRPLDT,cmname,coords,drot,PNEWDT,
     &                   celent,NOEL, NPT, LAYER, KSPT )
C
      ELSE  IF (K4ILOI.EQ.'  35') THEN
C
       CALL BIDERM (STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                   TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                   NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                   DFGRD0, DFGRD1, KSTEP, KINC,SSE,spd,scd,rpl,
     &                   DDSDDT,DRPLDE,DRPLDT,cmname,coords,drot,PNEWDT,
     &                   celent,NOEL, NPT, LAYER, KSPT )
      ELSE IF (K4ILOI.EQ.'  36') THEN
C
      CALL HUITCH (STRESS, STATEV, DDSDDE, STRAN, DSTRAN,
     &                   TIME, DTIME, TEMP, DTEMP, PREDEF, DPRED,
     &                   NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS,
     &                   DFGRD0, DFGRD1, KSTEP, KINC,SSE,spd,scd,rpl,
     &                   DDSDDT,DRPLDE,DRPLDT,cmname,coords,drot,PNEWDT,
     &                   celent,NOEL, NPT, LAYER, KSPT )
C=======================================================================
C     Modele externe non reconnu
C=======================================================================
C
      ELSE
C
         KINC = -2
C
      ENDIF
C
      RETURN
      END



