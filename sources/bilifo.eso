C BILIFO    SOURCE    CHAT      05/01/12    21:40:04     5004
      SUBROUTINE BILIFO(SIG0,DEPST,VAR0,XMAT,XCAR,SIGF,VARF,DEFP)
C-----------------------------------------------------------------------
C
C    DRIVER OF THE BILINEAR PLASTIC TRANSVERSE SHEAR MODEL
C
C  INPUT:
C
C    SIG0             INITIAL GENERALIZED STRESS
C    DEPST            INCREMENT OF GENERALIZED STRAIN
C    XMAT             MATERIAL CHARACTERISTICS
C    XCAR             GEOMETRIC CHARACTERISTICS
C    VAR0             INITIAL INTERNAL VARIABLE
C
C  OUTPUT:
C
C    SIGF             FINAL GENERALIZED STRESS
C    VARF             FINAL INTERNAL VARIABLE
C    DEFP             INCREMENT OF PLASTIC DEFORMATION
C
C  GENERALIZED STRESS (.=0 OR F):
C
C    EFFZ = SIG.(3)
C
C  MATERIAL CHARACTERISTICS:
C
C    E    = XMAT(1)   ELASTIC YOUNG MODULUS
C    NU   = XMAT(2)   POISSON'S RATIO
C    EAYI = XMAT(5)   PLASTIC MODULUS
C    YEFF = XMAT(6)   YIELDING TRANSVERSE SHEAR FORCE
C
C  GEOMETRIC CHARACTERISTICS:
C
C    INRX = XCAR(1)   X-INERTIAL TORSION
C    INRY = XCAR(2)   Y-INERTIAL BENDING
C    INRZ = XCAR(3)   Z-INERTIAL BENDING
C    SECT = XCAR(4)   SECTION
C    SRDY = XCAR(5)   TRANSVERSE SHEAR'S REDUCED SECTION
C    SECZ = XCAR(6)   Z-TRANSVERSE SHEAR SECTION  <------
C
C  INTERNAL VARIABLE (.=0 OR F):
C
C    EPSE = VAR.(1)   CUMULATED PLASTIC TRANSVERSE SHEAR STRAIN
C    GATA = VAR.(2)   TANGENT Z-SHEAR STIFFNESS
C    EFEZ = VAR.(3)   ELASTIC SHEAR FORCE
C    CISZ = VAR.(4)   TRANSVERSE SHEAR STRAIN
C
C-----------------------------------------------------------------------
C  P. PEGON      26/11/92 renewed new ECOU 24/11/95
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
C
      DIMENSION SIG0(*),DEPST(*),VAR0(*),XMAT(*),XCAR(*),
     .          SIGF(*),VARF(*) ,DEFP(*)
C
C-----------------------------------------------------------------------
C
C  ELASTIC STRESS INCREMENT FOR ALL STRESS COMPONENT EXCEPT MOMY
C
      CISA=0.5D0*XMAT(1)/(1.D0+XMAT(2))
C     SIGF(1)=SIG0(1) + DSIGT(1)
      SIGF(1)=SIG0(1) + XMAT(1)*XCAR(4)*DEPST(1)
C     SIGF(2)=SIG0(2) + DSIGT(2)
      SIGF(2)=SIG0(2) + CISA*XCAR(5)*DEPST(2)
C     SIGF(4)=SIG0(4) + DSIGT(4)
      SIGF(4)=SIG0(4) + CISA*XCAR(1)*DEPST(4)
C     SIGF(5)=SIG0(5) + DSIGT(5)
      SIGF(5)=SIG0(5) + XMAT(1)*XCAR(2)*DEPST(5)
C     SIGF(6)=SIG0(6) + DSIGT(6)
      SIGF(6)=SIG0(6) + XMAT(1)*XCAR(3)*DEPST(6)
C
C  ELASTIC AND PLASTIC SHEAR STIFNESS
C
      XMODG=0.5D0 / (1.D0 + XMAT(2))
      GAZ    = XMAT(1)*XCAR(6)*XMODG
      GYAZ   = XMAT(5)*XCAR(6)*XMODG
C
C  Z-TRANSVERSE SHEAR INCREMENT
C
C     DCISAZ = DSIGT(3) / GAZ
      DCISAZ = DEPST(3)
C
C  CALL TO THE SCALAR BILINEAR PLASTIC MODEL COMPUTING MOMY
C
      FEP0 = SIG0(3) - VAR0(3)
      FE0  = VAR0(3)
      CALL BILINE(DCISAZ,XMAT(6),GAZ, GYAZ, VARF(2),
     >          FE0,FEP0,SIGF(3),DEPSE)
C
C  FINAL INTERNAL VARIABLE
C
      VARF(1)=VAR0(1)+DEPSE
      VARF(3)=FE0
      VARF(4)=VAR0(4)+DCISAZ
C
C  PLASTIC DEFORMATION INCREMENT
C
      DEFP(1) = 0.D0
      DEFP(2) = 0.D0
      DEFP(3) = DEPSE
      DEFP(4) = 0.D0
      DEFP(5) = 0.D0
      DEFP(6) = 0.D0
      RETURN
      END


