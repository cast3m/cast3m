C TUFK1A    SOURCE    CHAT      05/01/13    03:56:36     5004
      SUBROUTINE TUFK1A(XM,XP,YOU,RAYMO,EPAI,TETA,XK1,AIRE)
C=======================================================================
C   CALCULE LE FACTEUR D INTENSITE DES CONTRAINTES
C                ET L AIRE DE BRECHE
C    ENTREES
C     XM    = MOMENT
C     XP    = EFFORT NORMAL
C     YOU   = MODULE D YOUNG
C     RAYMO = RAYON MOYEN
C     EPAI  = EPAISSEUR
C     TETA  = 1/2 ANGLE DE LA FISSURE EN RADIANS
C    SORTIES
C     XK1   = FACTEUR D INTENSITE DES CONTRAINTES
C     AIRE  = AIRE DE BRECHE
C
C    APPELE PAR TUFPLA
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C    Include contenant quelques constantes dont XPI :
-INC CCREEL
      PARAMETER(DEUX=2.D0)
C
C     CALCUL DE A   COEFIICIENT ZAHOR
C
      RSURT=RAYMO / EPAI
      IF(RSURT.LE.10.D0) THEN
         AXX = ( .125D0*RSURT - .25D0 ) **.25D0
      ELSE
         AXX = ( .4D0*RSURT - 3.D0 ) **.25D0
      ENDIF
C
      TETA1= (TETA * 180.D0) / XPI
      TESPI = TETA/XPI
      CALL TUFIFP(TESPI,AXX,FP,FM,FMP,FOP,FOM)
C
C     FACTEUR D INTENSITE DES CONTRAINTES
C
      IF(TETA1.LE.(0.5D0))THEN
         XK1=XZERO
         GOTO 666
      ENDIF
      SQQ= XPI * RAYMO * TETA
      SQQ= SQRT(SQQ)
      XEX= SQQ * FOP/(DEUX * XPI * RAYMO *EPAI)
      XFL= SQQ * FOM/(XPI * RAYMO * RAYMO *EPAI)
      XK1=XEX * XP + XFL * XM
C
C     CALCUL DES AIRES DE BRECHE NOTE TECHNIQUE DRE/STRE/LMA 85/695
C
      SIGM = XP /( DEUX * XPI * RAYMO * EPAI )
      SIGF = XM /( XPI * RAYMO * RAYMO * EPAI )
      XIM =  XPI * RAYMO * RAYMO * DEUX * TETA * TETA * FP /YOU
      XIF =  XIM * ( .75D0 +(.25D0 * COS ( TETA )))
      AIRE=XIM * SIGM  + XIF * SIGF
C
 666  CONTINUE
      RETURN
      END






