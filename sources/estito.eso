C ESTITO    SOURCE    CB215821  17/11/30    21:16:05     9639
      SUBROUTINE ESTITO(SIG,NSTRS,VAR,NVARI,FKX,NKX,NPTEUR,NC,
     &               XMAT,NCOMAT,T,TAUX,TRUC,NCOURB)
C
C----------------------------------------------------------------------
C Objet: Estimation du pas de temps par integration de la loi
C           d'evolution du dommage a contr. cte
C----------------------------------------------------------------------
C Entree: SIG(NSTRS) tenseur des contraintes
C         VAR(NVARI) tableau contenant les variables internes
C           pilotant les equations
C           VAR(1)=p ; VAR(2)=r ;VAR(3)=D
C         XMAT(NCOMAT) tableau des parametres scalaires du materiau
C           pouvant dependre de la temperature
C           XMAT(1)=YOUNG ;XMAT(2)=XNU ;XMAT(3)=n
C           XMAT(4)=m ;XMAT(5)=KK; XMAT(6)=ALPHA
C           XMAT(7)=BETA ;XMAT(8)=r; XMAT(9)=A ;
C           XMAT(10)=EVOL ;XMAT(11)=ALPHAT
C           XMAT(12)=RHO; XMAT(13)=SIGY
C         FKX(NKX) tableau de la fonction k(X,T) contenant
C           les courbes de k a differentes temperatures T1,T2,T3,...
C         NPTEUR(NC) tableau de pointeurs sur les differentes courbes
C           de la fonction k
C         T temperature
C------------------------------------------------------------------------
C
C------------------------------------------------------------------------
C Sortie: TAUX  pas de temps estime
C----------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),VAR(*),XMAT(*)
       DIMENSION FKX(*),NPTEUR(*),TRUC(*)
       DIMENSION XX(6),A(3,3),D(3),S(3,3)
C----------------------------------------------------------------------
C CALCULATE  J0,J1,J2
C   J0: THE BIGEST PRINCIPAL STRESS
C   J1:  MEAN STRESS
C   J2: CONTR. DE VON MISES
C----------------------------------------------------------------------
       AJ1  = SIG(1)+SIG(2)+SIG(3)
       AJ1  = AJ1/3.0D0
       DO 30 I=1,3
30       XX(I) = SIG(I)-AJ1
       DO 40 I=4,NSTRS
40       XX(I) = SIG(I)
       AJ2 =  PROCON(XX,XX,NSTRS)
       AJ2 =  SQRT(1.5D0*AJ2)

       CALL ZERO(A,3,3)
       DO 50 I=1,3
50       A(I,I) = SIG(I)
       A(1,2) = SIG(4)
       IF(NSTRS.NE.6) GOTO 54
       A(1,3) = SIG(5)
       A(2,3) = SIG(6)
54     CONTINUE
       A(2,1) = A(1,2)
       A(3,1) = A(1,3)
       A(3,2) = A(2,3)
       IF (NSTRS.EQ.4) CALL JACOB3(A,2,D,S)
       IF (NSTRS.EQ.6) CALL JACOB3(A,3,D,S)
C
C---------------------------------------------------------------------
C CALCULATE PRINCIPLE STRESS : D(*)
C---------------------------------------------------------------------
       AJ0 =  MAX(D(1),D(2),D(3))
C
C---------------------------------------------------------------------
C CALCULATE VARIABLE X
C---------------------------------------------------------------------
C
       ALFA = XMAT(6)
       BETA = XMAT(7)
       X    = (ALFA*AJ0)+(BETA*AJ1)+((1.0D0-ALFA-BETA)*AJ2)
C
C---------------------------------------------------------------------
C CALCULATE PARAMETER k(X,T)=AK
C---------------------------------------------------------------------
C
       CALL VALPAR(FKX,NPTEUR,NKX,NC,X,AK,AKV,T,TIN,TSU,TRUC,NCOURB)
C--------------------------------------------------------------------
C Estimation du pas de temps par integration de la loi
C d'evolution du dommage a contr. cte
C---------------------------------------------------------------------
       EA=XMAT(9)
       ER=XMAT(8)
       TAUX = 1.0D10
       IF(VAR(3).GE.0.3.AND.X.GT.0.0D0) THEN
         AK1 = AK + 1
         TAUX = (1.0D0-VAR(3))**AK1/AK1/(X/EA)**ER
       ENDIF
C
       RETURN
       END



