C HYSHPT    SOURCE    BP208322  15/06/22    21:19:21     8543
      SUBROUTINE HYSHPT(NEF,NBDDL,IPINTE,IPTHYB)
C-----------------------------------------------------------------------
C Calcul les fonctions de bases de la formulation hybride pour l'ef NEF
C aux points contenus dans le segment d'integration IPINTE.
C-----------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  NEF     : Numero de l'element fini dans NOMTP (cf common CCHAMP)
C E/  NBDDL   : Nombre de noeuds portant des degres de liberte
C E/  IPINTE  : Pointeur vers le segment d'integration des fonctions
C               de forme de l'element fini NEF
C  /S IPTHYB  : Pointeur du segment HYBSTO contenant les fonctions de
C               bases en formulation hybride
C
C----------------------
C Variables en COMMON :
C----------------------
C
C E/  IDIM    : Dimension de l'espace   (cf CCOPTIO)
C E/  NOMTP   : Noms des elements finis (cf CCHAMP)
C  /S MOTERR  : Chaine de char pour erreur (cf CCOPTIO)
C
C----------------------
C Tableaux de travail :
C----------------------
C
C     SHP     : Contient les fonctions de base en un point
C
C-----------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C
C Auteurs : F.DABBENE 08/93
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
-INC CCHAMP
-INC SMINTE
-INC CCOPTIO
C
      SEGMENT HYBSTO
         REAL*8 HYBASE(NDIM,NBDDL,NBPP)
      ENDSEGMENT
C
      SEGMENT SHXX
         REAL*8 SHY(NDIM,NBDDL)
      ENDSEGMENT
C
C= Initialisations
C
      MINTE = IPINTE
      SEGACT MINTE
      NBPP  = SHPTOT(/3)
      NDIM  = IDIM * (IDIM+1)
      SEGINI HYBSTO , SHXX
*
*= Calcul des fonctions de base en chaque point du segment d'integration
*
      DO 20 II=1,NBPP
         XX = QSIGAU(II)
         YY = ETAGAU(II)
         ZZ = DZEGAU(II)
         CALL HYSHP(XX,YY,ZZ,NEF,NBDDL,NDIM,SHY,IRET)
         IF (IRET.NE.0) GOTO 100
         DO 10 ID=1,NDIM
            DO 10 NO=1,NBDDL
               HYBASE(ID,NO,II) = SHY(ID,NO)
  10     CONTINUE
  20  CONTINUE
C
C= Desactivation et menage
C
      IPTHYB = HYBSTO
      SEGDES MINTE , HYBSTO
      SEGSUP SHXX
      RETURN
*
*= ERREUR : Fonctions de forme non encore implementees
*
 100  CONTINUE
      MOTERR(1:4) = NOMTP(NEF)
      CALL ERREUR(68)
      SEGSUP SHXX , HYBSTO
      SEGDES MINTE
      IPTHYB = 0
      RETURN
      END




