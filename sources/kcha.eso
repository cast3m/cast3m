C KCHA      SOURCE    CB215821  16/12/05    21:39:53     9237
      SUBROUTINE KCHA
C-----------------------------------------------------------------------
C Transforme un CHPO de support CENTRE en un MCHAML constant par élément
C Transforme un MCHAML constant par élément en un CHPO de support CENTRE
C   de nature DIFFUSE
C-----------------------------------------------------------------------
C
C---------------------------
C Phrase d'appel (GIBIANE) :
C---------------------------
C
C RES1 = 'KCHA' MODL1 MOT1 DONN1 ('QUAF')
C               TABL1
C
C------------------------
C Opérandes et résultat :
C------------------------
C
C RES1    : Contient le CHPO ou le MCHAML résultat selon l'option MOT1
C           Le support de RES1 est soit table.centre, soit table.maillage
C MOT1    : Mot de quatre lettre indiquant le type d'objet à créer
C           Si MOT1='CHPO' un MCHAML constant devient un CHPO centre
C           Si MOT1='CHAM' un CHPO centre devient un MCHAML constant
C MODL1   : Objet MMODEL
C TABL1   : TABLE Domaine
C DONN1   : Objet à transformer (CHPO OU MCHAML)
C 'QUAF'  : mot de 4 lettres indiquant le travail avec maillage QUAF
C           plutôt que simple
C
C-----------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C
C Auteurs : F.DABBENE 07/94
C Révision : 01/99 P.Maugis
C Révision : 11/06 PM : utilisation maillages QUAF
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER (NBOPT=2)
C
-INC CCOPTIO
-INC SMCHAML
-INC SMCHPOI
-INC SMELEME
-INC SMTABLE
C
      CHARACTER*4 MOTOPT(NBOPT),NOMTOT(1),MOQUAF(1)
      DATA        MOTOPT / 'CHAM' , 'CHPO' /
      DATA        MOQUAF / 'QUAF' /
      LOGICAL     LQUAF
C
C-    Lecture de l'option et traitement
C
*     on construit un CHAMELEM ou un CHPOINT ?
      ICOND = 1
      CALL LIRMOT(MOTOPT,NBOPT,IKCHA,ICOND)
      IF (IKCHA.EQ.0) RETURN

*     maillage simple ou QUAF ?
      ICOND = 0
      CALL LIRMOT(MOQUAF,1,IQUAF,ICOND)
      LQUAF = (IQUAF.EQ.1)
C
C     Lecture du modèle
C
      CALL LIROBJ('MMODEL',IPMODE,0,IRET)
      IF (IERR.NE.0) RETURN
C
C     Récupération de la TABLE domaine et des maillages
C
      IPTABL = 0
      IF(IRET.EQ.0)THEN
        CALL LIRTAB('DOMAINE',IPTABL,1,IRET)
        IF (IERR.NE.0) RETURN
      ELSE
        CALL LEKMOD(IPMODE,IPTABL,IRET)
        IF (IERR.NE.0) RETURN
      ENDIF

C     Maillage des points centres
      CALL LEKTAB(IPTABL,'CENTRE  ',IPCENT)
      IF (IERR.NE.0) RETURN

C-    Maillage support voulu
      IF (LQUAF) THEN
         CALL LEKTAB(IPTABL,'QUAF    ',IPGEOM)
         IF (IERR.NE.0) RETURN
      ELSE
         CALL LEKTAB(IPTABL,'MAILLAGE',IPGEOM)
         IF (IERR.NE.0) RETURN
      ENDIF
C
C-    Transformation ...
C
      IF (IKCHA.EQ.1) THEN
C
C-       d'un CHPO CENTRE en MCHAML constant par élément
C
C        Lecture objet à transformer
         CALL LIROBJ('CHPOINT',ICHP1,1,IRET1)
         IF (IERR.NE.0) RETURN
C        Construction du segment de travail
         NBCOMP    = 0
         NOMTOT(1) = '    '
         CALL KCHA0(ICHP1,IPCENT,NBCOMP,NOMTOT,MTRAV,IREDIR)
C        Appel de la procédure de métamorphose :
         CALL KCHA1(MTRAV,IPGEOM,IPRESU)
      ELSE
C
C-       d'un MCHAML constant par élément en CHPO CENTRE
C
C        Lecture objet à transformer
         CALL LIROBJ('MCHAML',IPIN,1,IRET1)
         IF (IERR.NE.0) RETURN
         CALL REDUAF(IPIN,IPMODE,IPCHE1,0,IR,KER)
         IF(IR   .NE. 1) CALL ERREUR(KER)
         IF(IERR .NE. 0) RETURN
C        Appel de la procédure de métamorphose :
         CALL KCHA2(IPCHE1,IPGEOM,IPCENT,IPRESU)
         IF(IERR .NE. 0) RETURN
      ENDIF
C
C-    Ecriture du résultat
C
      IF (IPRESU .EQ. 0) then
        CALL ERREUR(5)
      ELSE
        IF (IKCHA .EQ. 1) THEN
          CALL ECROBJ('MCHAML',IPRESU)
        ELSE
          CALL ECROBJ('CHPOINT',IPRESU)
        ENDIF
      ENDIF
C
      RETURN
      END


