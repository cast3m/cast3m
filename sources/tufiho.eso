C TUFIHO    SOURCE    CHAT      05/01/13    03:55:44     5004
      SUBROUTINE TUFIHO(THETA,DTUFI,CTUFI,YOUN,RAYOM,EPAI)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C    Include contenant quelques constantes dont XPI :
-INC CCREEL
      DIMENSION DTUFI(2,*),CTUFI(2,*)
C=======================================================================
C       CE SOUS-PROGRAMME CALCULE LA MATRICE DE HOOKE
C           POUR L ELEMENT TUFI
C        IL EST APPELE PAR TUFIEC
C
C         ENTREE : THETA : ANGLE DE LA FISSURE
C         SORTIE : DTUFI : MATRICE DE HOOKE
C                  CTUFI : MATRICE DE COMPLAISANCE
C=======================================================================
      CALL TUFIZA(A,RAYOM,EPAI)
      TSP=THETA/XPI
      TSP1=TSP**1.5D0
      TSP2=TSP**4.24D0
      TSP3=TSP**3.D0
      TSP4=TSP**5.74D0
      TSP5=TSP**8.48D0
C
C   CALCUL DES COEF DE LA MATRICE DE COMPLAISANCE
C
      FP=A*(6.09D0*TSP1+12.03D0*TSP2)
      FP=FP+1.D0
      FP=FP+A*A*(11.36D0*TSP3+51.70D0*TSP4+67.24D0*TSP5)
      FM=A*(5.26D0*TSP1+1.69D0*TSP2)
      FM=FM+1.D0
      FM=FM+A*A*(8.46D0*TSP3+6.28D0*TSP4+1.33D0*TSP5)
      FMP=A*(5.67D0*TSP1+6.86D0*TSP2)
      FMP=FMP+1.D0
      FMP=FMP+A*A*(9.81D0*TSP3+25.95D0*TSP4+9.46D0*TSP5)
C
      CP=THETA*THETA*FP
      CP=CP/2.D0/YOUN/XPI/EPAI
      CM=2.D0*THETA*THETA*FM
      CM=CM/YOUN/XPI/RAYOM/RAYOM/EPAI
      CMP=THETA*THETA*FMP
      CMP=CMP/YOUN/XPI/RAYOM/EPAI
C
C   REMPLISSAGE DE LA MATRICE DE COMPLAISANCE
C
      CTUFI(1,1)=CM
      CTUFI(1,2)=CMP
      CTUFI(2,1)=CMP
      CTUFI(2,2)=CP
C
C   INVERSION ET REMPLISSAGE DE LA MATRICE DE HOOKE
C
      DET=CM*CP-CMP*CMP
      DTUFI(1,1)=CP/DET
      DTUFI(1,2)=-CMP/DET
      DTUFI(2,1)=DTUFI(1,2)
      DTUFI(2,2)=CM/DET
      RETURN
      END





