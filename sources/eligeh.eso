C ELIGEH    SOURCE    PV        15/04/10    21:15:08     8474
C
      SUBROUTINE  ELIGEH(EPSO,G12,S1T,S2T,S3T,T1T,T3T,ALFA,EPPP,EPPM
     .            ,EPPPF,EPPMF,XKTAN,TAUF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C----------------------------------------------------------------------
C
C     LOI ELIGEHAUSEN 1D EXPRIMEE EN CONTRAINTE - GLISSEMENT
C      UTILISEE SUR DES ELEMENTS JOINTS 2D
C      ET POUR LES LOIS D'ANCRAGE (BARRE et FIBRE)
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - MAI 2000
C----------------------------------------------------------------------
C
C      EPSO: Glissement total
C      G12: Module elastique (charge et décharge)
C      EPPP: Glissement plastique positif
C      EPPM: Glissement plastique négatif
C      EPPPF: Glissement plastique positif final
C      EPPMF: Glissement plastique négatif final
C      TAUF: Contrainte de cisaillement finale
C      KTAN: Module tangent
C
C      S1T, S2T et S3T: Glissements caractéristiques
C      T1T et T3T: Contraintes de cisaillement caractéristiques
C      ALFA: Paramètre définissant la première partie de la courbe
C
C -----------------------------------
C
C       VERIFICATION DES PARAMETRES
C
C
-INC CCREEL

       IF ((S1T.LT.0.D0).OR.(S2T.LT.0.D0).OR.(S3T.LT.0.D0).OR.
     .   (T1T.LT.0.D0).OR.(T3T.LT.0.D0).OR.(S2T.LT.S1T).OR.
     .   (S3T.LT.S2T).OR.(T1T.LT.T3T).OR.(ALFA.GT.1.D0).OR.
     .   (ALFA.LT.0.D0)) THEN
          WRITE (*,*) 'Vos paramètres Eligehausen ne sont pas corrects'
       ENDIF
C
C
C
       IF (EPSO.GE.EPPP) THEN
C
C  CAS EPSO>EPPP : ELASTIQUE OU PLASTIQUE
C
C  PREDICTION ELASTIQUE
C
         TAUFELA = G12*(EPSO - EPPP)
C
C  COURBE NON LINEAIRE
C
        IF (EPSO.LT.S1T) THEN
         TAUFPLA = T1T*((EPSO/S1T)**ALFA)
         epso=max(xpetit*s1t,epso)
         XKTANP =  ALFA*(T1T/S1T)*((EPSO/S1T)**(ALFA - 1.D0))
        ELSEIF (EPSO.LT.S2T) THEN
         TAUFPLA = T1T
         XKTANP = 0.D0
        ELSEIF (EPSO.LT.S3T) THEN
          TAUFPLA = T1T - ((T1T - T3T)*(EPSO - S2T)/(S3T - S2T))
          XKTANP = (T3T - T1T)/(S3T - S2T)
        ELSE
          TAUFPLA = T3T
          XKTANP = 0.D0
        ENDIF
C
C
C
        IF (TAUFELA.GT.TAUFPLA) THEN
         TAUF = TAUFPLA
         EPPPF = EPSO - (TAUFPLA/G12)
         EPPMF = EPPM
         XKTAN = XKTANP
        ELSE
         TAUF = TAUFELA
         EPPPF = EPPP
         EPPMF = EPPM
         XKTAN = G12
        ENDIF

C
       ELSEIF (EPSO.GT.EPPM) THEN
C
C  CAS EPPM<EPSO<EPPP : PHASE GLISSEMENT : TAUF + 0
C
         TAUF = 0.D0
         XKTAN = 0.D0
         EPPPF = EPPP
         EPPMF = EPPM
C
       ELSEIF (EPSO.LE.EPPM) THEN
C
C CAS EPSO<EPPM : ELASTIQUE OU PLASTIQUE
C
C  PREDICTION ELASTIQUE
C
         TAUFELA = G12*(EPSO - EPPM)
C
C  COURBE NON LINEAIRE
C
        IF (EPSO.GT.((-1.D0)*S1T)) THEN
         TAUFPLA = (-1.D0)*T1T*(((-1.D0)*EPSO/S1T)**ALFA)
         XKTANP =  ALFA*(T1T/S1T)*(((-1.D0)*EPSO/S1T)**(ALFA - 1.D0))
        ELSEIF (EPSO.GT.((-1.D0)*S2T)) THEN
         TAUFPLA = (-1.D0)*T1T
         XKTANP = 0.D0
        ELSEIF (EPSO.GT.((-1.D0)*S3T)) THEN
          TAUFPLA = (-1.D0)*(T1T - ((T1T - T3T)*
     .(((-1.D0)*EPSO) - S2T)/(S3T - S2T)))
          XKTANP = (T3T - T1T)/(S3T - S2T)
        ELSE
          TAUFPLA = (-1.D0)*T3T
          XKTANP = 0.D0
        ENDIF
C
C
C
        IF (TAUFELA.LT.TAUFPLA) THEN
         TAUF = TAUFPLA
         EPPMF = EPSO - (TAUFPLA/G12)
         EPPPF = EPPP
         XKTAN = XKTANP
        ELSE
         TAUF = TAUFELA
         EPPPF = EPPP
         EPPMF = EPPM
         XKTAN = G12
        ENDIF

       ENDIF
C
C      WRITE(*,*) XKTAN
C
C
       RETURN
       END






