C UO2FL     SOURCE    CHAT      05/01/13    03:59:06     5004
      SUBROUTINE UO2FL(NCA,NN,XINVL,PENTE,SIGMA,GS,
     &                 DFDS,DGDS,HDFDQ,KERRE)
C-----------------------------------------------------------------------
C                MODELE UO2 (OTTOSEN+GATT_MONERIE)
C-----------------------------------------------------------------------
C
C  ENTREES
C  -------
C NCA          = NBR. DE CRITERES ATTEINTS
C NN(NC)       = DIRECTIONS CORRESPONDANTES AUX CRITERES ATTEINTS
C XINVL(3)     = INVERSES DES PARAMETRES DE TAILLE
C PENTE(NC)    = PENTES DES CRITERES
C                (DERIVEE DES CRIT. / DEF. DE FISS. EN OUV.)
C SIGMA(6)     = CONTRAINTES AU DEB. DU SOUS PAS
C GS(3)        = RESISTANCES AU CISAILLEMENT
C
C  SORTIES
C  -------
C DFDS(6,NC)   = DERIVEES DES CRITERES DE FISSURATION / CONTRAINTES
C DGDS(6,NC)   = MATRICE DE PASSAGE DU VECTEUR DES DEF. DE FISSURATION
C                EN OUVERTURE AU VECTEUR DE FISSURATION COMPLET
C                (OUVERTURES + GLISSEMENTS)
C HDFDQ(NC)    = DERIVEES DES CRITERES DE FISSURATION /
C                VECTEUR DES DEF. DE FISSURATION EN OUVERTURE
C KERRE        = GESTION DES ERREURS
C-----------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
C
      PARAMETER (XZER=0.D0)
C
      DIMENSION NN(*),DFDS(6,*),DGDS(6,*),HDFDQ(*)
      DIMENSION XINVL(*),PENTE(*),SIGMA(*),GS(*)
      DIMENSION IV1(3),IV2(3)
C
      DATA IV1/4,4,5/
      DATA IV2/5,6,6/
C
      KERRE=0
C
      IF (NCA.EQ.0) THEN
        RETURN
      ENDIF
C
C
      DO IC=1,NCA
        JC=NN(IC)
        DO I=1,6
           DFDS(I,JC)=XZER
           DGDS(I,JC)=XZER
        ENDDO
        HDFDQ(JC)=XZER
C
        GO TO (1,1,1),JC
C
        KERRE=99
        PRINT *,'&&&&&&&& UO2FL  JC = ',JC
        RETURN
C
 1      CONTINUE
C
        IC1=IV1(JC)
        IC2=IV2(JC)
C
        IF (XINVL(JC).NE.XZER) THEN
           IF(IIMPI.EQ.42) THEN
             WRITE(IOIMP,77005) XINVL(JC),PENTE(JC)
77005        FORMAT(2X,'UO2FL - XINVL=',1PE12.5,2X,'PENTE=',1PE12.5/)
           ENDIF
C
C
           H =PENTE(JC)/XINVL(JC)
           IF(IIMPI.EQ.42) THEN
              WRITE(IOIMP,77009)  H
77009         FORMAT(2X,'UO2FL - MODULE D ECROUISSAGE H= ',1PE12.5/)
           ENDIF
           HDFDQ(JC)=-H
           DFDS(JC,JC)=1.D0
           DGDS(JC,JC)=1.D0
           DGDS(IC1,JC)=SIGMA(IC1)/GS(JC)/2.D0
           DGDS(IC2,JC)=SIGMA(IC2)/GS(JC)/2.D0
        ENDIF
        GO TO 100
C
100     CONTINUE
      ENDDO
C
      RETURN
      END


