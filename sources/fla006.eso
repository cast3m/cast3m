C FLA006    SOURCE    SERRE     12/07/10    21:15:12     7431
      FUNCTION FLA006(T,X,TRAV)
C---------------------------------------------------------------------
C Calcul de la conductivité thermique du mélange (en W/m/K) en fonction
C de la composition du mélange pour une température donnée par la
C formule de Wilke
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  T      : flottant : Température de référence
C E/  X(7)   : flottant : Fraction molaire de N2, O2, H2 et H2O
C  /S FLA006 : flottant : Conductivité thermique du mélange (W/m/K)
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C E/  M(7)   : Masse molaire des constituants du mélange (J/kg/K)
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 FLA006
      REAL*8 X(7),MUI(7),LAMI(7),FIJ(7,7)
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
C Viscosité dynamique en fonction de T de chaque constituant
      CALL FLA005(T,MUI)
C
C Conductivité thermique en fonction de T de chaque constituant
      CALL FLA007(T,LAMI)
C
C Formule de Wilke
      IBOU = CPI(/1)
      COEF = 1.D0 / SQRT(8.D0)
      DO 20 J=1,IBOU
         DO 10 I=1,IBOU
            FIJ(I,J) = COEF / SQRT(1.D0+M(I)/M(J))
     S      * ((1.D0 + SQRT(MUI(I)/MUI(J))*(M(J)/M(I))**0.25D0)**2.D0)
 10      CONTINUE
 20   CONTINUE
      VAL006 = 0.D0
      DO 40 I=1,IBOU
         COEF = 0.D0
         DO 30 J=1,IBOU
            COEF = COEF + X(J)*FIJ(I,J)
 30      CONTINUE
         VAL006 = VAL006 + X(I)*LAMI(I)/COEF
 40   CONTINUE
C
      FLA006 = VAL006
C
      RETURN
      END









