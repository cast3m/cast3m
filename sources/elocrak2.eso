C ELOCRAK2  SOURCE    PV        19/02/05    21:15:06     10105
      SUBROUTINE ELOCRAK2 (WRK52,WRK53,WRK54,WRKK2,NSTRS1,NVARI,
     1                   ICARA,JDIM,IFOUR2)
C
C====&===1=========2=========3=========4=========5=========6=========7==
C Commentaires :   Subroutine permettant de mettre en oeuvre le
C                          modele ELOCRACK (CEA) pour representer
C                  le comportement cyclique du b√©ton sous
C                  sollicitations sismiques
C
C Traits       : -
C                -
C                -
C                -
C                -
C
C Auteur       : B. Richard (Dr - Ing.) - CEA/DEN/DANS/DM2S/SEMT/EMSI
C====&===1=========2=========3=========4=========5=========6=========7==
C
C----DECLARATION GENERALES----------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
C----LES INCLUDES-------------------------------------------------------
C
-INC CCOPTIO
-INC DECHE
-INC CCREEL
C
C----DECLARATION PARAMETRES---------------------------------------------
C
      SEGMENT WRKK2
         REAL*8 EPSILI(NSTRS1)
      ENDSEGMENT

      SEGMENT WRK3
        REAL*8 EPSILO(NSTRS1)
      ENDSEGMENT

      INTEGER NSTRS1,NVARI,ISTRS,JSTRS,KCAS,IRTD

      REAL*8 EPS33(3,3),EPSIPP(3),VALP33(3,3)
      REAL*8 YOUN,XNU,XYDT,XDELTA,EPSIP1(3)
      REAL*8 UN,DEUX

      PARAMETER ( UN=1.D0 , DEUX=2.D0)
C
C----MISE EN DONNEE-----------------------------------------------------
C
      N=NSTRS1

      CMATE = 'ISOTROPE'

C     Module d Young et coefficient de Poisson
      YOUN = XMAT(1)
      XNU = XMAT(2)

C     Resistance et parametre endommagement traction
      XFT = XMAT(5)
      XEPSUT = XMAT(6)
      XY0T = XFT/YOUN

C     Resistance et parametre endommagement compression
      XFC = XMAT(7)
      XEPSUC = XMAT(8)
      XY0C = XFC/YOUN
C
C----VARIABLES INTERNES-------------------------------------------------
C
C     Deformation totale
      EPSILI(1) = VAR0(1)
      EPSILI(2) = VAR0(2)
      EPSILI(3) = VAR0(3)
      EPSILI(4) = VAR0(4)

C     Endommagement et ecrouissage traction
      XDT = VAR0(7)
      XZT = VAR0(8)

C     Endommagement et ecrouissage traction
      XDC = VAR0(9)
      XZC = VAR0(10)

C
C----ACTUALISATION DEFORMATION------------------------------------------
C
      SEGINI WRK3
      DO 100 ISTRS=1,NSTRS1
         EPSILO(ISTRS)=EPSILI(ISTRS)+DEPST(ISTRS)
100   CONTINUE

C
C----PASSAGE EN MATRICIEL-----------------------------------------------
C
      CALL ENDOCB (EPSILO,EPS33,2,IFOUR2)
C
C----CALCUL DE L ENDOMMAGEMENT------------------------------------------
C

C     Indicateur pour le sens du chargement
      XINDQ = SIG0(1)+SIG0(2)

C     Diagonalisation des deformations de membrane
C     ! La diagonalisation est analytique car absence de !
C     ! convergence ... voire resultats faux ...         !
C     ! en cas de distorsion pure                        !
      XDELTA = (EPS33(1,1)-EPS33(2,2))**2.0D0+EPS33(1,2)**2.0D0
      XDELTA = SQRT(XDELTA)

      EPSIPP(1) = (EPS33(1,1) + EPS33(2,2) + XDELTA)/2.0D0
      EPSIPP(2) = (EPS33(1,1) + EPS33(2,2) - XDELTA)/2.0D0
      EPSIPP(3) = -(EPSIPP(1) + EPSIPP(2))*XNU / (UN-XNU)

C     Energie due a l endommagement traction
      DO I = 1,3
         EPSIP1(I) = 0.5D0*(ABS(EPSIPP(I))+EPSIPP(I))
      ENDDO

      XYDT = 0.0D0
      DO I = 1,3
         XYDT = XYDT + (EPSIP1(I))**2.0D0
      ENDDO

      XYDT = SQRT(XYDT)

C     Seuil d'endommagement traction
      IF (XINDQ.GE.0.0D0) THEN
         XINDQT = 1.0D0
      ELSE
         XINDQT = 0.0D0
      ENDIF

      XFDT = XYDT*XINDQT -(XY0T + XZT)
      xrf=abs(xydt*xindqt)*xzprec

C     Calcul de l'endommagement en traction
      IF (XFDT.GT.-xrf) THEN
         XDT = 1.0D0-(XY0T/XYDT)*EXP(XEPSUT*(XY0T-XYDT)*XFT)
         XZT = XYDT - XY0T
      ENDIF

      IF (XDT.GE.0.999D0) THEN
         XDT = 0.999D0
      ENDIF

C     Seuil d'endommagement compression
      IF (XINDQ.LT.0.0D0) THEN
         XINDQC = 1.0D0
      ELSE
         XINDQC = 0.0D0
      ENDIF

      XFDC = XYDT*XINDQC -(XY0C + XZC)
      xrf=abs(xydt*xindqc)*xzprec

      IF (XFDC.GT.-xrf) THEN
         XDC = 1.0D0-(XY0C/XYDT)*EXP(XEPSUC*(XY0C-XYDT)*XFC)
         XZC = XYDT -XY0C
      ENDIF
C
C----MATRICE DE HOOKE---------------------------------------------------
C
         CMATE = 'ISOTROPE'
         KCAS=1

         CALL DOHMAS(XMAT,CMATE,IFOUR2,NSTRS1,KCAS,DDHOOK,IRTD)

         CALL MATVE1 (DDHOOK,EPSILO,NSTRS1,NSTRS1,SIGF,2)
C
C----SORTIE DES CONTRAINTES---------------------------------------------
C
         DO 500 ISTRS=1,NSTRS1
         SIGF(ISTRS)=SIGF(ISTRS)*(1.0D0-XDT*XINDQT)*(1.0D0-XDC*XINDQC)
500      CONTINUE
C
C----SORTIE DES VARIABLES INTERNES--------------------------------------
C

C     Deformation totale
      VARF(1) = EPSILO(1)
      VARF(2) = EPSILO(2)
      VARF(3) = EPSILO(3)
      VARF(4) = EPSILO(4)

C     Endommagement et ecrouissage traction
      VARF(7) = XDT
      VARF(8) = XZT

C     Endommagement et ecrouissage traction
      VARF(9) = XDC
      VARF(10) = XZC

      SEGSUP WRK3
      RETURN
      END














