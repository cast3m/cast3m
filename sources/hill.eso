C HILL      SOURCE    KK2000    05/05/25    21:15:17     5088
c     Sous-Programme du module MISTRAL0
C     --------------------------------------------------------------------------
      SUBROUTINE HILL (TT,PSI,S, ID, PCOHI, NP, SEQ,DSESDS,
     &                 DSEDTT,DSDPSI)
C     --------------------------------------------------------------------------
C     Calcul de :
C     - la contrainte equivalente SEQ,
C     - ses derivees partielles DSESDS(IJ) par rapport aux S(IJ),
C     puis, si ID = 1, DSEDTT et DSDPSI par rapport a TT et PSI,
C     en fonction :
C     - de la temperature TT,
C     - de la variable de durcissement du a l'irradiation PSI,
C     - des contraintes S(IJ),
C     - des parametres transmis dans PCOHI,
C     NP (incremente dans COHILL) etant en entree le nombre
C     de ceux correspondant aux deformations de natures IDPP < IDP.
C     --------------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H, O-Z)
      DIMENSION S(1:*)
      DIMENSION PCOHI(1:*)
      DIMENSION H(1:6),DHDTT(1:6),DHDPSI(1:6)
        DIMENSION ISU(1:3)
      DIMENSION DSESDS(1:*)
C     ------------------------------------------------------------------
      CALL COHILL (TT,PSI, ID, PCOHI, NP, H,DHDTT,DHDPSI)
      SEQ = SQRT(
     &      H(1)*(S(2)-S(3))**2+H(2)*(S(3)-S(1))**2+H(3)*(S(1)-S(2))**2
     &     +2.*H(4)*S(4)**2+2.*H(5)*S(5)**2+2.*H(6)*S(6)**2 )
        IF (SEQ.NE.0.) THEN
         CALL PROMAH (H,S, DSESDS)
       DO I = 1,6
          DSESDS(I) = DSESDS(I)/SEQ
         END DO
        END IF
C     ------------------------------------------------------------------
      IF (ID.EQ.1) THEN
       ISU(1) = 2
       ISU(2) = 3
       ISU(3) = 1
       DSEDTT = 0.
       DSDPSI = 0.
       DO I = 1,6
        IF ((DHDTT(I).NE.0.D0).OR.(DHDPSI(I).NE.0.D0)) THEN
         IF (I.LE.3) THEN
          J = ISU(I)
            K = ISU(J)
            SS2 = (S(J)-S(K))**2
           ELSE
            SS2 = 2.*S(I)**2
         END IF
           DSEDTT = DSEDTT+SS2*DHDTT(I)
           DSDPSI = DSDPSI+SS2*DHDPSI(I)
        END IF
         END DO
       DSEDTT = DSEDTT/2./SEQ
       DSDPSI = DSDPSI/2./SEQ
      END IF
C     ------------------------------------------------------------------
        RETURN
        END



