C DETOWP    SOURCE    CB215821  16/11/21    21:15:01     9196
      SUBROUTINE DETOWP(P,T,X,AH2,AH2O,AO2,TINT,W,IINT,NBC,NBT)
C---------------------------------------------------------------------
C Calcul des taux de production de chaque espece, N2 étant neutre.
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  P         : Pression du mélange (Pa)
C E/  T         : Température du mélange (K)
C E/  X(NBC)    : Fraction molaire (-)
C E/  AH2(NBT)  : Coeff du devt en T des propriétés de h2
C E/  AH2O(NBT) : Coeff du devt en T des propriétés de h2o
C E/  AO2(NBT)  : Coeff du devt en T des propriétés de o2
C E/  TINT      : Température seuil (K)
C  /S W(NBC)    : Taux de production de chacune des espèces
C E/  IINT      : Nombre de coeff nécessaire à T donnée
C E/  NBC       : Nombre de constituants du mélange
C E/  NBT       : Nombre de coeff total
C
C---------------------
C Variables internes :
C---------------------
C
C AKP : Constante d'équilibre de la réaction
C AKF : Vitesse de la réaction dans le sens gauche->droite
C AKT : Vitesse de la réaction dans le sens droite->gauche
C AKC : Constante cinétique (AKF/AKT)
C
C---------------------------------------------------------------------
C
C Langage : FORTRAN
C
C Auteurs : F.DABBENE et E.STUDER 02/99
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION X(*),W(*),AH2(*),AH2O(*),AO2(*)
C
C- Décalage dans la table suivant la borne TINT
C
      IINTM2 = IINT - 2
      IINTM1 = IINT - 1
      II = 0
      IF (T.LT.TINT) II = IINT
C
C- Description du modele chimique de combustion
C-> Correspondance indice/espece : 1=H2 2=O2 3=H2O 4=N2
C-> Cinétique à une réaction     : 2.H2 + 1.O2 = 2.H2O
C-> Vitesse réaction (Arrhenius) : AKF = A * T**B * exp(-E/RT)
C
      ANP1 = REAL(2.D0)
      ANP2 = REAL(1.D0)
      ANP3 = REAL(0.D0)
      ANS1 = REAL(0.D0)
      ANS2 = REAL(0.D0)
      ANS3 = REAL(2.D0)
      ANU1 = ANS1 - ANP1
      ANU2 = ANS2 - ANP2
      ANU3 = ANS3 - ANP3
      DANU = ANU1 + ANU2 + ANU3
C
C- Constantes de la cinétique à une réaction (loi d'Arrhenius)
C- A et E correspondent à des énergies en Joules
C
      A = REAL(1.2D14)
      B = REAL(-0.9D0)
      E = REAL(4.184D0*16511.D0)
C
C- Calcul de l'enthalpie/RT et de l'entropie/R des constituants
C- H2, O2 et H2O en fonction de la température (en mol/gramme)
C
      XLOGT= LOG(T)
      SH2  = AH2 (1+II) * XLOGT
      SO2  = AO 2(1+II) * XLOGT
      SH2O = AH2O(1+II) * XLOGT

      HH2  = AH2 (1+II)
      HO2  = AO2 (1+II)
      HH2O = AH2O(1+II)

      IF (IINTM2 .GE. 2) THEN
        DO 10 I = 2,IINTM2
           TPOW = T**(I-1)
           HH2  = HH2  + AH2 (I+II) * TPOW / REAL(I)
           HO2  = HO2  + AO2 (I+II) * TPOW / REAL(I)
           HH2O = HH2O + AH2O(I+II) * TPOW / REAL(I)
           SH2  = SH2  + AH2 (I+II) * TPOW / REAL(I-1)
           SO2  = SO2  + AO2 (I+II) * TPOW / REAL(I-1)
           SH2O = SH2O + AH2O(I+II) * TPOW / REAL(I-1)
 10     CONTINUE
      ENDIF
      HH2  = HH2  + AH2(IINTM1+II)  / T
      HO2  = HO2  + AO2(IINTM1+II)  / T
      HH2O = HH2O + AH2O(IINTM1+II) / T
      SH2  = SH2  + AH2(IINT+II)
      SO2  = SO2  + AO2(IINT+II)
      SH2O = SH2O + AH2O(IINT+II)
C
C- Calcul de la constante d'équilibre de la réaction
C- AKP = EXP(-DG/RT) avec G = h - Ts
C
      AKP = EXP(ANU1*(SH2-HH2)+ANU2*(SO2-HO2)+ANU3*(SH2O-HH2O))
C
C- Calcul de la constante cinétique de la réaction (AKC) et des vitesses
C- de réaction dans le sens gauche->droite AKF et droite->gauche AKT
C
      AKC = AKP*(1.01325D6/(8.314D0*1.D7*T))**DANU
      AKF = A*(T**B)*EXP(-E/8.314D0/T)
      AKT = AKF / AKC
C
C- Calcul des taux de production/destruction de chaque espèce
C- (X(i)*(P/(8.314D0*T)*1.D-7) = concentration molaire de i)
C
C       Q = (AKF * X(1)**ANP1 * X(2)**ANP2 * X(3)**ANP3 )
C     %    * (P/8.314D0/T*1.D-7)**(ANP1+ANP2+ANP3)
C    %    - (AKT * X(1)**ANS1 * X(2)**ANS2 * X(3)**ANS3 )
C     %    * (P/8.314D0/T*1.D-7)**(ANS1+ANS2+ANS3)
C
C ANS1=ANS2=ANP3=0 =>
        Q = (AKF * X(1)**ANP1 * X(2)**ANP2 )
     %    * (P/8.314D0/T*1.D-7)**(ANP1+ANP2)
     %    - (AKT * X(3)**ANS3 )
     %    * (P/8.314D0/T*1.D-7)**ANS3
C
      W(1) = ANU1*Q
      W(2) = ANU2*Q
      W(3) = ANU3*Q
      W(4) = 0.D0
C
      RETURN
      END





