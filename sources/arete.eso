C ARETE     SOURCE    PV        09/01/08    21:15:18     6254
      SUBROUTINE ARETE

*
*       OPERATEUR TRANSFORMANT UN MAILLAGE 3D DE TYPE COMPLEXE
*       EN UN MAILLAGE 3D COMPOSE UNIQUEMENT DE SEG2 REPRESENTANT
*       LES ARETES VIVES DU VOLUME.
*
*-------------------------------------------------------------------
*
*   ARBRE D'APPEL :
*   --------------
*         ENVVEL : ELIMINATION PARTIE MASSIVE DU MAILLAGE
*
*         ARETE1 : CALCUL DE PREPARATION
*                  (CALCUL DE NORMALE,TRANSFORMATION EN SEG2)
*
*         ARETE3 : ELIMINATION SEGMENTS INUTILES PAR ESTIMATION
*                  DE LA DIFFERENCE DES NORMALES DE 2 FACETTES ADJACENTE
*
*-------------------------------------------------------------------
*
*   VARIABLES PRINCIPALES :
*   ---------------------
*
*      TSEG : SEGMENT 2 DIMENSIONS  CONTENANT
*     | A TRACER (= 0  NON,   =1 OUI)    |
*     | COULEUR                          |
*     | N› NOEUD MIN                     |
*     | N› NOEUD MAX                     |
*     | ENTREES DANS TSEG                |
*
*      TNOR : SEGMENT 2 DIMENSIONS CONTENANT LES NORMALES A COMPARER
*             POUR CHAQUE FACE ELEMENTAIRE
*
*      TINDIC : SEGMENT DONNANT LA POSITION DANS TNOR EN FONCTION
*               DES 2 NOEUDS
*
*      ISEG : TAILLE REELLE DU SEGMENT TSEG
*      INOR : TAILLE REELLE DU SEGMENT TNOR
*
*--------------------------------------------------------------------
*      AUTEUR : J.BRUN (JUIN 90)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Y)
      IMPLICIT LOGICAL (Z)
-INC CCREEL
-INC SMELEME
-INC CCOPTIO
-INC SMCOORD
      SEGMENT TN
      REAL*8 TNOR(N1,3)
      ENDSEGMENT
*
      SEGMENT TS
      INTEGER TSEG(M1,M2)
      ENDSEGMENT
*
      SEGMENT TI
      INTEGER TINDIC(MI1,MI2,2)
      ENDSEGMENT
*
      SEGMENT ICPR(XCOOR(/1)/(IDIM+1))
*
C  Operateur disponible en dimension 3
      IF (IDIM.NE.3) THEN
        INTERR(1)=IDIM
        CALL ERREUR(709)
        RETURN
      ENDIF

      CANGLE=COS(XPI/9.D0)
      CALL LIROBJ ('MAILLAGE',MELEME,1,IRETOU)
      IF (IERR.NE.0) RETURN
      CALL LIRREE (ANGLE,0,IOK)
* UN ANGLE ALPHA > 90›  <==> XPI-ALPHA
      IF (IOK.NE.0) CANGLE=ABS(COS(ANGLE*XPI/180.D0))
*--------------------------------------------------------------------
*
*     ELIMINATION PARTIE MASSIVE
*
      CALL ECROBJ('MAILLAGE',MELEME)
      CALL ENVVOL
      CALL LIROBJ('MAILLAGE',MELEME,1,IRETOU)
*--------------------------------------------------------------------
*
*     CREATION STRUCTURE ESOPE
*
      N1=200
      SEGINI TN
      INOR=0
      M1=200
      M2=10
      SEGINI TS
      ISEG=0
      MI1=200
      MI2=3
      SEGINI TI
      SEGINI ICPR
      LCPR=0
*------------------------------------------------------------------
*     PREPARATION MAILLAGE
*
      CALL ARETE1(MELEME,TS,ISEG,TN,INOR,TI,ICPR,LCPR)
*------------------------------------------------------------------
*     ELIMINATION SEG2 INUTILE
*

      CALL ARETE3(TS,ISEG,TN,INOR,CANGLE,NBSEG)

      NBNN=2
      NBELEM=NBSEG
      NBSOUS=0
      NBREF=0
      SEGINI MELEME
      ITYPEL=2
      J=1
      DO 100 I=1,ISEG
         IF (TSEG(I,1).EQ.1) THEN
            ICOLOR(J)=TSEG(I,2)
            NUM(1,J)=TSEG(I,3)
            NUM(2,J)=TSEG(I,4)
            J=J+1
         ENDIF
 100  CONTINUE
      SEGDES MELEME
      SEGSUP TN,TS,TI,ICPR
      CALL ECROBJ('MAILLAGE',MELEME)

      RETURN

      END





