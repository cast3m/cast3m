C DKTHSH    SOURCE    CHAT      05/02/25    21:15:10     5035
      SUBROUTINE DKTHSH(tabuv,tabw,tabrot,DJAC,XSTRS,REL,epai)
C---------------------------------------------------------------------
C   CALCULE LA MATRICE  KSIGMA   POUR L ELEMENT DKT
C
C      ENTREES
C         tabuv(6,3)    = FONCTIONS U U,X U,Y V V,X V,Y
C         tabw(6,9)   = FONCTIONS W W,X  W,Y
C         tabrot(4,9) = FONCTIONS BETAx,x BETAx,y BETA y,x BETAy,y
C         DJAC        = JACOBIEN
C         XSTRS(6)    = CONTRAINTES AU POINT DE GAUSS
C         REL (18,18) = MATRICE KSIGMA
C         epai        = Epaisseur
C      SORTIE
C         REL (18,18) = MATRICE KSIGMA
C
C   RE-ECRIT  LE 2/4/90
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(18,*),XSTRS(*),tabuv(6,*),tabw(6,9),tabrot(4,9)
      DATA UNTIER/.33333333333333333D0/
      DATA UNDOUZ/.08333333333333333D0/
      xNXX=XSTRS(1)
      xNYY=XSTRS(2)
      xNXY=XSTRS(3)
      xNXX2=XSTRS(1) * UNDOUZ * epai * epai
      xNYY2=XSTRS(2) * UNDOUZ * epai * epai
      xNXY2=XSTRS(3) * UNDOUZ * epai * epai

      DO 16  J=1,3
      JJ=(J-1)*6+1
      DO 17  I=1,3
      II=(I-1)*6+1
        S= xNXX*tabuv(2,I)*tabuv(2,J) +
     .     xNYY*tabuv(3,I)*tabuv(3,J) +
     .     xNXY*(tabuv(2,I)*tabuv(3,J)+tabuv(3,I)*tabuv(2,J))
      S=S*DJAC
      REL(II,JJ)    =REL(II,JJ)    +S
      REL(II+1,JJ+1)=REL(II+1,JJ+1)+S
   17 CONTINUE
   16 CONTINUE

      DO 20 L=1,3
      DO 21 M=1,3
      DO 22 I=1,3
      DO 23 K=1,3
         S= xNXX*tabw(2,3*L-3+I)*tabw(2,3*M-3+K) +
     .      xNYY*tabw(3,3*L-3+I)*tabw(3,3*M-3+K) +
     .      xNXY*tabw(2,3*L-3+I)*tabw(3,3*M-3+K) +
     .      xNXY*tabw(3,3*L-3+I)*tabw(2,3*M-3+K) +
     .      xNXX2*tabrot(1,3*L-3+I)*tabrot(1,3*M-3+K) +
     .      xNXX2*tabrot(3,3*L-3+I)*tabrot(3,3*M-3+K) +
     .      xNYY2*tabrot(2,3*L-3+I)*tabrot(2,3*M-3+K) +
     .      xNYY2*tabrot(4,3*L-3+I)*tabrot(4,3*M-3+K) +
     .      xNXY2*tabrot(1,3*L-3+I)*tabrot(2,3*M-3+K) +
     .      xNXY2*tabrot(2,3*L-3+I)*tabrot(1,3*M-3+K) +
     .      xNXY2*tabrot(3,3*L-3+I)*tabrot(4,3*M-3+K) +
     .      xNXY2*tabrot(4,3*L-3+I)*tabrot(3,3*M-3+K)
         S=S*DJAC
         REL(6*L-4+I,6*M-4+K) = REL(6*L-4+I,6*M-4+K) + S
   23 CONTINUE
   22 CONTINUE
   21 CONTINUE
   20 CONTINUE

      RETURN
      END

