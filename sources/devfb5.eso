C DEVFB5    SOURCE    CHAT      05/01/12    22:45:35     5004
      SUBROUTINE  DEVFB5(ITYP,FTOTB,XPTB,IPALB,IPLIB,XPALB,XVALB,
     &        NLIAB,NPLB,IND,IND1,INDM1,NPA,NPAM1,IND2,PDT,PDTS2,
     &        FEXPSM,NPC1,I,XABSCI,XORDON,NIP,IANNUL)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Operateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul des forces de choc sur base B pour les liaisons de      *
*     type POINT_POINT_DEPLACEMENT_PLASTIQUE.                        *
*                                                                    *
*     Parametres:                                                    *
*                                                                    *
* e   ITYP    type de la liaison.                                    *
* es  FTOTB   Forces exterieures totalisees sur la base B.           *
* e   XPTB    Tableau des deplacements des points                    *
* e   IPALB   Renseigne sur la liaison.                              *
* e   IPLIB   Tableau contenant les numeros "DYNE" de la liaison.    *
* e   XPALB   Tableau contenant les parametres de la liaison.        *
* es  XVALB   Tableau contenant les variables internes de liaisons.  *
* e   NLIAB   Nombre de liaisons sur la base B.                      *
* e   NPLB    Nombre total de points intervenant dans les liaisons.  *
* e   IND     Indice du pas.                                         *
* e   I       numero de la liaison.                                  *
* e   XABSCI  Tableau contenant les abscisses de la loi plastique    *
*             pour la liaison point-point-plastique                  *
* e   XORDON  Tableau contenant les ordonnees de la loi plastique    *
*             pour la liaison point-point-plastique                  *
* e   NIP     Nombre de points dans l'evolution de la loi            *
*                                                                    *
*     Auteur, date de creation:                                      *
*                                                                    *
*     Nicolas WECXSTEEN 04/96 point-point- ... -plastique            *
*                                                                    *
*--------------------------------------------------------------------*
*
      INTEGER IPALB(NLIAB,*),IPLIB(NLIAB,*)
      REAL*8  XPALB(NLIAB,*),XPTB(NPLB,4,*),FTOTB(NPLB,*)
      REAL*8  XVALB(NLIAB,4,*),FEXPSM(NPLB,NPC1,2,*)
      REAL*8  XABSCI(NLIAB,*),XORDON(NLIAB,*)

*
* --- choc elementaire POINT_POINT_DEPLACEMENT_PLASTIQUE sans amortissement
*
      IF (ITYP.EQ.16) THEN
         NPOA   = IPLIB(I,1)
         NPOB   = IPLIB(I,2)
         IDIM   = IPALB(I,3)
         IPERM  = IPALB(I,5)
         XJEU   = XPALB(I,1)
         XDPLAS = XPALB(I,2+IDIM)
         XELA   = XPALB(I,3+IDIM)
         XDPLAC = XPALB(I,4+IDIM)
         XDEP = 0.D0
         XAMO = 0.d0
         XVIT = 0.d0
         DO 10 ID = 1,IDIM
            IDA = 3 + ID
            IDB = 3 + IDIM + ID
            XDEA = XPTB(NPOA,IND,ID)
            XDEB = XPTB(NPOB,IND,ID)
            XVALB(I,IND,IDA) = XDEA
            XVALB(I,IND,IDB) = XDEB
            XDEA = XDEA + FEXPSM(NPOA,NPA,IND1,ID)
            XDEB = XDEB + FEXPSM(NPOB,NPA,IND1,ID)
*
*  Dans le tableau XPALB(I,1+ID) se trouve les coord de la normale
*
            XDEP = XDEP + (XDEA - XDEB) * XPALB(I,1+ID)
 10         CONTINUE
*        end do
* On appel DYCHEC, SP qui calcul ,avec le deplacement xdep,
* la force xfla et le dep plastique xdplas sur l'evolution xabsci/xordon
* (loi de comportement)
*
        call DYCHEC(XDEP,XDPLAS,XDPLAC,XJEU,IPERM,XABSCI,
     &                  XORDON,I,XFLA,NLIAB,NIP,XVIT,XAMO,iannul)

*
       XFLB = -1.D0 * XFLA
       XVALB(I,IND,1) = XFLA
       XVALB(I,IND,2) = XFLB
       XPALB(I,2+IDIM) = XDPLAS
       XPALB(I,3+IDIM) = XELA
       XPALB(I,4+IDIM) = XDPLAC
       XVALB(I,IND,13) = XDPLAS
       XVALB(I,IND,14) = XELA
       XVALB(I,IND,15) = XDPLAC
*
         DO 12 ID = 1,IDIM
            FTOTB(NPOA,ID) = FTOTB(NPOA,ID) + XFLA * XPALB(I,1+ID)
            FTOTB(NPOB,ID) = FTOTB(NPOB,ID) + XFLB * XPALB(I,1+ID)
 12         CONTINUE
*        end do
*
* --- choc elementaire POINT_POINT_DEPLACEMENT_PLASTIQUE avec amortissement
*
      ELSE IF (ITYP.EQ.17) THEN
         NPOA   = IPLIB(I,1)
         NPOB   = IPLIB(I,2)
         IDIM   = IPALB(I,3)
         IPERM  = IPALB(I,5)
         XJEU   = XPALB(I,1)
         XAMO   = XPALB(I,2)
         XDPLAS = XPALB(I,3+IDIM)
         XELA   = XPALB(I,4+IDIM)
         XDPLAC = XPALB(I,5+IDIM)
         XDEP = 0.D0
         XDEPM1 = 0.D0
         DO 20 ID = 1,IDIM
            IDA = 3 + ID
            IDB = 3 + IDIM + ID
*
*  Ici, l'indice IND2 sert a calculer les vitesses
*
            XDEA = XPTB(NPOA,IND,ID)
            XDMA = XPTB(NPOA,IND2,ID)
            XDEB = XPTB(NPOB,IND,ID)
            XDMB = XPTB(NPOB,IND2,ID)
            XVALB(I,IND,IDA) = XDEA
            XVALB(I,IND,IDB) = XDEB
            XDEA = XDEA + FEXPSM(NPOA,NPA,IND1,ID)
            XDEB = XDEB + FEXPSM(NPOB,NPA,IND1,ID)
            XDMA = XDMA + FEXPSM(NPOA,NPAM1,INDM1,ID)
            XDMB = XDMB + FEXPSM(NPOB,NPAM1,INDM1,ID)
            XDEB = XDEB + FEXPSM(NPOB,NPA,IND1,ID)
*
*  Dans le tableau XPALB(I,2+ID) on trouve les coord de la normale
*
            XDEP = XDEP + (XDEA - XDEB) * XPALB(I,2+ID)
            XDEPM1 = XDEPM1 + (XDMA - XDMB) * XPALB(I,2+ID)
 20         CONTINUE
*        end do
         XVIT = (XDEP - XDEPM1) / PDTS2
         XVALB(I,IND,3) = XVIT
*
* Appel a DYCHEC pour calcul de la force et depla plastique
*
        call DYCHEC(XDEP,XDPLAS,XDPLAC,XJEU,IPERM,XABSCI,
     &                  XORDON,I,XFLA,NLIAB,NIP,XVIT,XAMO,iannul)

         XFLB = -1.D0 * XFLA
         XVALB(I,IND,1) = XFLA
         XVALB(I,IND,2) = XFLB
         XPALB(I,3+IDIM) = XDPLAS
         XPALB(I,4+IDIM) = XELA
         XPALB(I,5+IDIM) = XDPLAC
         XVALB(I,IND,13) = XDPLAS
         XVALB(I,IND,14) = XELA
         XVALB(I,IND,15) = XDPLAC
*
         DO 22 ID = 1,IDIM
            FTOTB(NPOA,ID) = FTOTB(NPOA,ID) + XFLA * XPALB(I,2+ID)
            FTOTB(NPOB,ID) = FTOTB(NPOB,ID) + XFLB * XPALB(I,2+ID)
 22         CONTINUE
*
C   Rotule [
*
* --- choc elementaire POINT_POINT_ROTATION_PLASTIQUE sans amortissement
*
      ELSE IF (ITYP.EQ.50) THEN
         NPOA   = IPLIB(I,1)
         NPOB   = IPLIB(I,2)
         IDIM   = IPALB(I,3)
         IPERM  = IPALB(I,5)
         XJEU   = XPALB(I,1)
         XDPLAS = XPALB (I,2+IDIM)
         XELA   = XPALB (I,3+IDIM)
         XDPLAC = XPALB(I,4+IDIM)
         XROT = 0.D0
         xamo = 0.d0
         xvit = 0.d0
         DO 30 ID = 1,IDIM
            IDA = 3 + ID
            IDB = 3 + IDIM + ID
*
* On recupere les rotations des points A et B
* XROA = rotation point A
* XROB = rotation point B
*
            XROA = XPTB(NPOA,IND,ID+3)
            XROB = XPTB(NPOB,IND,ID+3)
            XVALB(I,IND,IDA) = XROA
            XVALB(I,IND,IDB) = XROB
            XROA = XROA + FEXPSM(NPOA,NPA,IND1,ID+3)
           XROB = XROB + FEXPSM(NPOB,NPA,IND1,ID+3)
*
*  Dans le tableau XPALB(I,1+ID) se trouve les coord de l'axe de rotation
*
            XROT = XROT + (XROA - XROB) * XPALB(I,1+ID)
 30         CONTINUE
*        end do
* On appel DYCHEC, SP qui calcul ,avec la rotation xrot,
* le moment xmla et la rota plastique xdplas sur l'evolution xabsci/xordon
* (loi de comportement)
*
        call DYCHEC(XROT,XDPLAS,XDPLAC,XJEU,IPERM,XABSCI,
     &                  XORDON,I,XMLA,NLIAB,NIP,XVIT,XAMO,iannul)

         XMLB = -1.D0 * XMLA
         XVALB(I,IND,1) = XMLA
         XVALB(I,IND,2) = XMLB
         XPALB(I,2+IDIM) = XDPLAS
         XPALB(I,3+IDIM) = XELA
         XPALB(I,4+IDIM) = XDPLAC
         XVALB(I,IND,13) = XDPLAS
         XVALB(I,IND,14) = XELA
         XVALB(I,IND,15) = XDPLAC
*
         DO 32 ID = 1,IDIM
            FTOTB(NPOA,ID+3) = FTOTB(NPOA,ID+3) + XMLA * XPALB(I,1+ID)
            FTOTB(NPOB,ID+3) = FTOTB(NPOB,ID+3) + XMLB * XPALB(I,1+ID)
 32         CONTINUE
*        end do
*
* --- choc elementaire POINT_POINT_ROTATION_PLASTIQUE avec amortissement
*
      ELSE IF (ITYP.EQ.51) THEN
         NPOA   = IPLIB(I,1)
         NPOB   = IPLIB(I,2)
         IDIM   = IPALB(I,3)
         IPERM  = IPALB(I,5)
         XJEU   = XPALB(I,1)
         XAMO   = XPALB(I,2)
         XDPLAS = XPALB(I,3+IDIM)
         XELA   = XPALB(I,4+IDIM)
         XDPLAC = XPALB(I,5+IDIM)
         XROT   = 0.D0
         XDRPM1 = 0.D0
         DO 40 ID = 1,IDIM
            IDA = 3 + ID
            IDB = 3 + IDIM + ID
*  Ici, l'indice IND2 sert a calculer les vitesses
*
            XROA = XPTB(NPOA,IND,ID+3)
            XDMA = XPTB(NPOA,IND2,ID+3)
            XROB = XPTB(NPOB,IND,ID+3)
            XDMB = XPTB(NPOB,IND2,ID+3)
            XVALB(I,IND,IDA) = XROA
            XVALB(I,IND,IDB) = XROB
** Verifier indice pseudo-modes***
            XROA = XROA + FEXPSM(NPOA,NPA,IND1,ID+3)
            XROB = XROB + FEXPSM(NPOB,NPA,IND1,ID+3)
            XDMA = XDMA + FEXPSM(NPOA,NPAM1,INDM1,ID+3)
            XDMB = XDMB + FEXPSM(NPOB,NPAM1,INDM1,ID+3)
C
*  Dans le tableau XPALB(I,2+ID) on trouve les coord de l'axe de rotation
*
            XROT = XROT + (XROA - XROB) * XPALB(I,2+ID)
            XDRPM1 = XDRPM1 + (XDMA - XDMB) * XPALB(I,2+ID)
 40         CONTINUE
*        end do
         XVIT = (XROT - XDRPM1) / PDTS2
         XVALB(I,IND,3) = XVIT
*
* Appel a DYCHEC pour calcul du moment et rotation plastique XDPLAS
*
        call DYCHEC(XROT,XDPLAS,XDPLAC,XJEU,IPERM,XABSCI,
     &                  XORDON,I,XMLA,NLIAB,NIP,XVIT,XAMO,iannul)

         XMLB = -1.D0 * XMLA
         XVALB(I,IND,1) = XMLA
         XVALB(I,IND,2) = XMLB
         XPALB(I,3+IDIM) = XDPLAS
         XPALB(I,4+IDIM) = XELA
         XPALB(I,5+IDIM) = XDPLAC
         XVALB(I,IND,13) = XDPLAS
         XVALB(I,IND,14) = XELA
         XVALB(I,IND,15) = XDPLAC
*
         DO 42 ID = 1,IDIM
            FTOTB(NPOA,ID+3) = FTOTB(NPOA,ID+3) + XMLA * XPALB(I,2+ID)
            FTOTB(NPOB,ID+3) = FTOTB(NPOB,ID+3) + XMLB * XPALB(I,2+ID)
 42         CONTINUE
*
C   Rotule ]
C
*        end do
*
* --- choc ...........
*
*     ELSE IF (ITYP.EQ.  ) THEN
*        .......
*        .......
*
      ENDIF
*
      END







