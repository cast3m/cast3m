C BMFDST    SOURCE    CHAT      05/01/12    21:41:26     5004
      SUBROUTINE BMFDST(IGAU,XE,NSTRS1,QSI,ETA,SHPTOT,SHP,HS4,
     1                  HS5,HS6,BGENE,DJAC)
*
*            CALCUL LA MATRICE B  RELATIVE AUX EFFETS DE
*            MEMBRANE ET DE FLEXION
*
*
* ENTREES :
*
*   IGAU :NUMERO DU POINT DE GAUSS
*    XE  :COORDONNEES DES NOEUDS
*  NSTRS1:NOMBRE DE COMPOSANTES DE CONTRAINTES
*  QSI   :COORDONNEES QSI DES POINTS DE GAUSS
*  ETA    COORDONNEES ETA DES POINTS DE GAUSS
* SHPTOT :FONCTIONS DE FORME ET LEURS DERIVEE ( EFFET DE MEMBRANE)
*   SHP  :TABLEAU DE TRAVAIL
* HS4,HS5:FONCTIONS QUI RELIENT LES ROTATIONS (BETA)SK A {U}
* HS6
*
*  SORTIES :
*
*   BGENE :MATRICE B
*   DJAC  :JACOBIEN
*
*
* AUTEUR : P.DOWLATYARI  JUILLET 91
*
************************************************************************
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION XE(3,*),QSI(*),ETA(*),SHPTOT(6,3,*),SHP(6,*)
      DIMENSION HS4(*),HS5(*),HS6(*),BGENE(NSTRS1,*)
      DIMENSION SX(3),SY(3),RL(3),EXX(3),EYY(3)
      DIMENSION HXABS(9),HXORD(9),HYABS(9)
      DIMENSION HYORD(9),B(3,9)
*
*      MATRICE B RELATIVE A L'EFFET DE MEMBRANE
*
      DO 10 NPOI=1,3
      SHP(1,NPOI)=SHPTOT(1,NPOI,IGAU)
      SHP(2,NPOI)=SHPTOT(2,NPOI,IGAU)
      SHP(3,NPOI)=SHPTOT(3,NPOI,IGAU)
  10  CONTINUE
      CALL JACOBI(XE,SHP,2,3,DJAC)
      K=1
      DO 30 NPOI=1,3
      BGENE(1,K  )=SHP(2,NPOI)
      BGENE(1,K+1)=0.D0
      BGENE(2,K  )=0.D0
      BGENE(2,K+1)=SHP(3,NPOI)
      BGENE(3,K  )=SHP(3,NPOI)
      BGENE(3,K+1)=SHP(2,NPOI)
  30  K=K+6
*
*     MATRICE B RELATIVE A L'EFFET DE FLEXION
*
      DO 40 K=4,6
       IF(K.EQ.4)THEN
         IJ=1
         I=2
         J=3
       ELSEIF(K.EQ.5)THEN
         IJ=2
          I=3
          J=1
        ELSE
         IJ=3
          I=1
          J=2
        ENDIF
       SX(IJ)=XE(1,I)-XE(1,J)
       SY(IJ)=XE(2,I)-XE(2,J)
       RL(IJ)=SQRT(SX(IJ)*SX(IJ)+SY(IJ)*SY(IJ))
       EXX(IJ)=-SX(IJ)/RL(IJ)
       EYY(IJ)=-SY(IJ)/RL(IJ)
  40   CONTINUE
       AIR=ABS(0.5D0*(SX(1)*SY(2)-SX(2)*SY(1)))
*
       CALL DERIVO(SX,SY,RL,QSI(IGAU),ETA(IGAU),HS4,HS5
     1              ,HS6,EXX,EYY,HXABS,HXORD,HYABS,HYORD)
       CALL BMATO(SX,SY,HXABS,HYABS,HXORD,HYORD,B)
      K=2
*
      KK=0
      DO 60 NPOI=1,3
      DO 70 IX=1,3
      DO 80 IY=1,3
      BGENE(3+IX,K+IY)=B(IX,IY+KK)
  80  CONTINUE
  70  CONTINUE
      KK=KK+3
  60  K=K+6
*
      RETURN
      END


