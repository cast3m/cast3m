C DEVEQ5    SOURCE    BP208322  18/12/20    21:15:27     10048
C
c       SUBROUTINE DEVEQ5(Q1,Q2,NA1,XK,XASM,XM,PDT,NPAS,FTOTA,
      SUBROUTINE DEVEQ5(Q1,Q2,NA1,XM,PDT,NPAS,FTOTA,FAMOR,
     &                  XOPM1,NB1,NB1M)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Operateur DYNE : algorithme de FU - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul des deplacements generalises pour le premier demi-pas   *
*     de temps.                                                      *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* es  Q1(.,.) Vecteur des deplacements generalises                   *
* es  Q2(.,.) Vecteur des vitesses generalisees                      *
* e   NA1     Nombre total d'inconnues dans la base A                *
* e   XK      Vecteur des raideurs generalisees                      *
* e   XM      Vecteur des masses generalisees                        *
* e   PDT     pas de temps                               *
* e   NPAS    Numero du pas de temps considere                       *
* e   FTOTA   Forces exterieures totalisees, sur la base A           *
*                                                                    *
*     Auteur, date de creation:                                      *
*     Benoit PRABEL, 16/02/2015                                      *
*                                                                    *
*--------------------------------------------------------------------*
      REAL*8 Q1(NA1,*),Q2(NA1,*)
      REAL*8 XM(NA1,*),FTOTA(NA1,*)
      REAL*8 XOPM1(NB1,NB1,*),FAMOR(NA1,*)
*
      PDTS2 = PDT * 0.5D0
      AUX4 = PDT * PDT/ 24.D0
*
*                           .       2
*     Q        = Q    + h/2 Q    + h /24 M^-1 ( 4 F    - F
*      i,m+1/2    i,m        i,m                   i,m    i,m-1/2
*                          .      .
*                 - C   ( 4 Q    - Q        ) )
*                    ij      j,m    j,m-1/2
*
*
C     CALCUL DU DEPLACEMENT

C     MATRICE M PLEINE
      IF(NB1M.GT.1) THEN

        DO 21 IA = 1,NA1
          AUX2 = 0.D0
          DO 22 IB = 1,NB1
            AUX2 = AUX2 + XOPM1(IA,IB,3) *
     &      (4.D0*(FTOTA(IB,3)-FAMOR(IB,3))-(FTOTA(IB,4)-FAMOR(IB,4)))
 22       CONTINUE
          Q1(IA,2) = Q1(IA,3) + (PDTS2 * Q2(IA,3)) + (AUX4 * AUX2)
 21     CONTINUE

C     MATRICE M DIAGO
      ELSE

        DO 20 I = 1,NA1
          AUX2 = (4.D0*(FTOTA(I,3)-FAMOR(I,3))
     &                -(FTOTA(I,4)-FAMOR(I,4))) / XM(I,1)
          Q1(I,2) = Q1(I,3) + (PDTS2 * Q2(I,3)) + (AUX4 * AUX2)
 20     CONTINUE

      ENDIF
*
      END





