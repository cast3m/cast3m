C VONMIS    SOURCE    CHAT      05/01/13    04:09:37     5004
      FUNCTION VONMIS(SIGMA,ICLE,ALFAH,COMIS)
C
C CALCUL DE LA CONTRAINTE DE VON MISES
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SIGMA(*)
      DIMENSION COMIS(*)
C****
C
C       ICLE=1   CAS DES ELEMENTS MASSIFS
C       ICLE=2   CAS DES COQUES
C       ICLE=3   CAS DES MEMBRANES
C       ICLE=4   CAS DES CABLES
C       ICLE=5   CAS QUELCONQUE
C       ICLE=6   CAS DES CONTRAINTES PLANES
C       ICLE=7   CAS DES COQUES A NU=0. OU CONTRAINTES PLANES
C       ICLE=8   CAS DES MEMBRANES A NU=0. OU CONTRAINTES PLANES
C       ICLE=9   CAS DES COQUES MASSIFS
C       ICLE=10  CAS DES JOINTS
C       ICLE=11  CAS DES POUTRES
C       ICLE=12  CAS DES TUYAUX
C       ICLE=13  CAS DES COQUES AVEC CISAILLEMENT
C       ICLE=14  CAS DES ELEMENTS MASSIFS EN 1D (UNID)
C
C****
C
C       ALFAH  COEFFICIENT MULTIPLICATEUR DE LA FLEXION DANS LES COQUES
C       COMIS  COEFFICIENT DES CONTRAINTES DANS LE VON MISES
C
C****
      GO TO (1,2,3,4,5,6,7,8,9,10,11,12,13,14),ICLE
    1 CONTINUE
      VONMIS=
     1SQRT(ABS(SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)+SIGMA(3)*SIGMA(3)
     1-SIGMA(1)*SIGMA(2)-SIGMA(2)*SIGMA(3)-SIGMA(3)*SIGMA(1)+3.D0*
     2(SIGMA(4)*SIGMA(4)+SIGMA(5)*SIGMA(5)+SIGMA(6)*SIGMA(6)))
     3  )
      RETURN
    2 VONMIS=
     . SQRT((SIGMA(4)*SIGMA(4)+SIGMA(5)*SIGMA(5)-SIGMA(4)*SIGMA(5)+
     1 3.*SIGMA(6)*SIGMA(6))*ALFAH+SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)
     2 -SIGMA(1)*SIGMA(2)+3.*SIGMA(3)*SIGMA(3))
      RETURN
    3 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)-SIGMA(1)*SIGMA(2)
     1 +3.D0*SIGMA(3)*SIGMA(3))
      RETURN
    4 VONMIS=ABS(SIGMA(3))
      RETURN
    5 VONMIS=SQRT(0.5*((COMIS(2)+COMIS(3))*SIGMA(1)*SIGMA(1)
     1+(COMIS(1)+COMIS(3))*SIGMA(2)*SIGMA(2)+(COMIS(1)+COMIS(2))*
     1 SIGMA(3)*SIGMA(3))
     1 -COMIS(1)*SIGMA(2)*SIGMA(3)-COMIS(2)*SIGMA(1)*SIGMA(3)-COMIS(3)*
     1 SIGMA(1)*SIGMA(2)+0.5*COMIS(4)*SIGMA(4)*SIGMA(4)+COMIS(5)*0.5*
     1 SIGMA(5)*SIGMA(5)+0.5*COMIS(6)*SIGMA(6)*SIGMA(6)   )
      RETURN
    6 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)-SIGMA(1)*SIGMA(2)
     1+3.*SIGMA(4)*SIGMA(4))
      GO TO 999
    7 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+ALFAH*SIGMA(4)*SIGMA(4))
      GO TO 999
    8 VONMIS=ABS(SIGMA(1))
      GO TO 999
C
C***  ELEMENT COQUE EPAISSE
C
    9 VONMIS=SQRT((SIGMA(2)*SIGMA(2)+SIGMA(3)*SIGMA(3)-SIGMA(2)*
     1             SIGMA(3)+3.D0*(SIGMA(4)*SIGMA(4)+SIGMA(5)*SIGMA(5)+
     2             SIGMA(6)*SIGMA(6)))*ALFAH+SIGMA(8)*SIGMA(8)+
     3             SIGMA(9)*SIGMA(9)-SIGMA(8)*SIGMA(9)+3.D0*(SIGMA(10)*
     4             SIGMA(10)+SIGMA(11)*SIGMA(11)+SIGMA(12)*SIGMA(12)))
      GO TO 999
   10 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+SIGMA(3)*SIGMA(3))
     *      +COMIS(1)*SIGMA(2) + COMIS(2)
      GO TO 999
C
C  POUTRES  FORMULATION GLOBALE
C
   11 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+
     .             SIGMA(5)*SIGMA(5)+
     .             SIGMA(6)*SIGMA(6)+
     .             SIGMA(4)*SIGMA(4))
      GO TO 999
C
C  TUYAUX  ( MEME CONVENTION QUE LES POUTRES  ET
C            PAS DE PRESSION POUR LE MOMENT )
C
   12 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+
     .             SIGMA(5)*SIGMA(5)+
     .             SIGMA(6)*SIGMA(6)+
     .             SIGMA(4)*SIGMA(4))
      GO TO 999
C
C   COQUES AVEC CISAILLEMENT TRANSVERSE
C
   13 VONMIS=SQRT(SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)-SIGMA(1)*SIGMA(2)
     . +3.*SIGMA(4)*SIGMA(4)+3.*SIGMA(5)*SIGMA(5)+3.*SIGMA(6)*SIGMA(6))
      GO TO 999
C
C= Modes de calcul UNIDIMENSIONNELs (1D)
 14   VONMIS=SQRT(ABS( SIGMA(1)*SIGMA(1)+SIGMA(2)*SIGMA(2)
     .                +SIGMA(3)*SIGMA(3)-SIGMA(1)*SIGMA(2)
     .                -SIGMA(2)*SIGMA(3)-SIGMA(3)*SIGMA(1) ))
C     GOTO 999
  999 RETURN
      END




