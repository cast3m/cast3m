C TIRILO    SOURCE    PV        18/06/18    21:15:41     9860
      SUBROUTINE TIRILO(RP,XL,YOUN,CISAIL,SD,SREDY,SREDZ,
     .                  XINX,XINY,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE DE TIMOSHENKO EN AXES LOCAUX
C    "D APRES TEDEL"
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    SREDZ   SECTION REDUITE A L EFFORT TRANCHANT TZ
C    XINX    INERTIE IX
C    XINY    INERTIE IY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 12X12
C
C     PP 3/9/92
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(12,12)
      DATA P5,P25/.5D0,.25D0/
C
C  INITIALISATIONS
C
      DO 120 J=1,12
      DO 120 I=1,12
  120 RP(I,J)=0.D00
      RL=1.D00/XL
      RL2=RL*RL
C
C  SECTION "REDUITE"
C
      IF ( SREDZ. NE.0. ) THEN
           SRZ=SREDZ
      ELSE
           SRZ=SD
      ENDIF
      IF ( SREDY. NE.0. ) THEN
           SRY=SREDY
      ELSE
           SRY=SD
      ENDIF
C
      ESL=YOUN*SD*RL
      EIZ=YOUN*XINZ
      GAZ=CISAIL*SRZ
      EIY=YOUN*XINY
      GAY=CISAIL*SRY
      GJL=CISAIL*XINX*RL
C
C  REMPLISSAGE DE LA MATRICE
C  FX1
       RP(1,1)=ESL
      RP(1,7)=-ESL
C  FY1
       RP(2,2)= GAY*RL
      RP(2,6)=  P5*GAY
       RP(2,8)= -GAY*RL
      RP(2,12)= P5*GAY
C  FZ1
       RP(3,3)= GAZ*RL
      RP(3,5)=  -P5*GAZ
       RP(3,9)= -GAZ*RL
      RP(3,11)= -P5*GAZ
C  MX1
       RP(4,4)= GJL
      RP(4,10)=-GJL
C  MY1
      RP(5,5)=  P25*GAZ*XL + EIY*RL
       RP(5,9)= P5*GAZ
      RP(5,11)= P25*GAZ*XL - EIY*RL
C  MZ1
      RP(6,6)=  P25*GAY*XL + EIZ*RL
       RP(6,8)= -P5*GAY
      RP(6,12)= P25*GAY*XL - EIZ*RL
C  FX2
      RP(7,7)=  ESL
C  FY2
       RP(8,8)= GAY*RL
      RP(8,12)= -P5*GAY
C  FZ2
       RP(9,9)= GAZ*RL
      RP(9,11)= P5*GAZ
C  MX2
      RP(10,10)= GJL
C  MY2
      RP(11,11)= P25*GAZ*XL + EIY*RL
C  MZ2
      RP(12,12)= P25*GAY*XL + EIZ*RL
C
      DO 9 I=1,12
      DO 9 J=I,12
   9  RP(J,I)=RP(I,J)
      RETURN
      END


