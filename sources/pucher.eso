C PUCHER    SOURCE    CB215821  16/06/15    21:15:12     8967
      SUBROUTINE PUCHER(IPCHE1,XPU,IPCHPU,IRET)
*_______________________________________________________________________
*
*        ELEVATION A UNE PUISSANCE REEL D'UN CHAMELEM
*
*  ENTREES :
*  ---------
*
*     IPCHE1  POINTEUR SUR LE CHAMPS PAR ELEMENT A ELEVER A LA PUISSANCE
*             XPU
*     XPU     PUISSANCE ( C EST UN REEL POSITIF OU NEGATIF )
*
*
*  SORTIES :
*  ---------
*
*     IPCHPU  POINTEUR SUR LE CHAMELEM RESULTANT
*     IPCHPU  =0 SI PB
*     IRET=1
*         =0  SI UNE COMPOSANTE EST NEGATIVE
*
*     CODE EBERSOLT AVRIL 85
*
*     PASSAGE AUX NOUVEAUX CHAMELEM PAR JM CAMPENON LE 12/90
*
*_______________________________________________________________________
*
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*
-INC SMCHAML
-INC CCOPTIO
-INC SMLREEL
-INC CCREEL
*
      IRET=1
      MCHEL1=IPCHE1
*
*     INITIALISATION DU CHAPEAU DU SEGMENT
*
      SEGINI,MCHELM=MCHEL1
      IPCHPU=MCHELM
      NSOUS=ICHAML(/1)

C     ON ORIENTE SELON LA VALEUR DE XPU (PERFORMANCES)
      IF (ABS(XPU-REAL(0.5D0)).LE.(XZPREC*ABS(XPU)*REAL(2.D0))) goto 100


c----------------------------------------------------------------------
c     Cas général Y**XPU
*
*     BOUCLE SUR LES SOUS ZONES
*
      DO 30 ISOUS=1,NSOUS
         MCHAM1=ICHAML(ISOUS)
         SEGINI,MCHAML=MCHAM1
         ICHAML(ISOUS)=MCHAML
         DO 40 ICOMP=1,IELVAL(/1)
            MELVA1=IELVAL(ICOMP)
            SEGINI,MELVAL=MELVA1
            IELVAL(ICOMP)=MELVAL
            IF (TYPCHE(ICOMP).EQ.'REAL*8') THEN
               N1PTEL=VELCHE(/1)
               N1EL  =VELCHE(/2)
               DO 20 IB=1,N1EL
                  DO 20 IGAU=1,N1PTEL
                    XTRA=VELCHE(IGAU,IB)
                    IF(ABS(XTRA).LT.XPETIT .AND. XPU.LT.REAL(0.D0))THEN
                      IRET = 0
                      RETURN
                    ELSE
C                     Verification si puissance ENTIERE possible
                      I2    = NINT(XPU)
                      XFLOT = ABS(XPU - REAL(I2))
                      IF ( XFLOT .LE. (XZPREC*ABS(XPU)*REAL(2.D0))) THEN
                        VELCHE(IGAU,IB) = XTRA**I2
                      ELSEIF(XTRA .LT. REAL(0.D0))THEN
                        IRET = 0
                        RETURN
                      ELSE
                        VELCHE(IGAU,IB)= XTRA ** XPU
                      ENDIF
                    ENDIF
 20            CONTINUE
            ELSE IF (TYPCHE(ICOMP).EQ.'POINTEURLISTREEL') THEN
               N2PTEL=IELCHE(/1)
               N2EL  =IELCHE(/2)
               DO 10 IB=1,N2EL
                  DO 10 IGAU=1,N2PTEL
                     MLREE1=IELCHE(IGAU,IB)
                     SEGACT MLREE1
                     JG=MLREE1.PROG(/1)
                     SEGINI MLREEL
                     DO 11 IPROG=1,JG
                        XTRA=MLREE1.PROG(IPROG)
                        IF(ABS(XTRA).LT.XPETIT .AND.
     &                   XPU.LT.REAL(0.D0))THEN
                          IRET = 0
                          RETURN
                        ELSE
C                         Verification si puissance ENTIERE possible
                          I2    = NINT(XPU)
                          XFLOT = ABS(XPU - REAL(I2))
                          IF ( XFLOT .LE.
     &                                (XZPREC*ABS(XPU)*REAL(2.D0))) THEN
                            PROG(IPROG) = XTRA**I2
                          ELSEIF(XTRA .LT. REAL(0.D0))THEN
                            IRET = 0
                            RETURN
                          ELSE
                            PROG(IPROG)= XTRA ** XPU
                          ENDIF
                        ENDIF
 11                  CONTINUE
                     IELCHE(IGAU,IB)=MLREEL
                     SEGDES MLREE1,MLREEL
 10            CONTINUE
            ELSE IF (TYPCHE(ICOMP).EQ.'POINTEUREVOLUTIO') THEN
               N2PTEL=IELCHE(/1)
               N2EL  =IELCHE(/2)
               DO 12 IB=1,N2EL
                  DO 12 IGAU=1,N2PTEL
                     MEVOL1=IELCHE(IGAU,IB)
                     IRET=2
                     CALL PUEVOL(MEVOL1,XPU,0,MEVOL2,IRET)
                     IELCHE(IGAU,IB)=MEVOL2
 12            CONTINUE
            ELSE
*
*              NOM DE COMPOSANTE NON RECONNU
*
               MOTERR(1:4)='**  '
               MOTERR(5:8)=NOMCHE(ICOMP)
               CALL ERREUR(335)
               SEGSUP MELVAL,MCHAML,MCHELM
               IPCHPU=0
               RETURN
            ENDIF
            SEGDES MELVAL
 40      CONTINUE
         SEGDES MCHAML
 30   CONTINUE
      GOTO 999


c----------------------------------------------------------------------
c     Cas : Y**0.5
 100  CONTINUE
*
*     BOUCLE SUR LES SOUS ZONES
*
      DO 130 ISOUS=1,NSOUS
         MCHAM1=ICHAML(ISOUS)
         SEGINI,MCHAML=MCHAM1
         ICHAML(ISOUS)=MCHAML
         DO 140 ICOMP=1,IELVAL(/1)
            MELVA1=IELVAL(ICOMP)
            SEGINI,MELVAL=MELVA1
            IELVAL(ICOMP)=MELVAL
            IF (TYPCHE(ICOMP).EQ.'REAL*8') THEN
               N1PTEL=VELCHE(/1)
               N1EL  =VELCHE(/2)
               DO 120 IB=1,N1EL
                  DO 120 IGAU=1,N1PTEL
                    XTRA=VELCHE(IGAU,IB)
                    IF(XTRA .LT. REAL(0.D0))THEN
                      IRET = 0
                      RETURN
                    ELSE
                      VELCHE(IGAU,IB)= SQRT(XTRA)
                    ENDIF
 120           CONTINUE
            ELSE IF (TYPCHE(ICOMP).EQ.'POINTEURLISTREEL') THEN
               N2PTEL=IELCHE(/1)
               N2EL  =IELCHE(/2)
               DO 110 IB=1,N2EL
                  DO 110 IGAU=1,N2PTEL
                     MLREE1=IELCHE(IGAU,IB)
                     SEGACT MLREE1
                     JG=MLREE1.PROG(/1)
                     SEGINI MLREEL
                     DO 111 IPROG=1,JG
                        XTRA=MLREE1.PROG(IPROG)
                        IF(XTRA .LT. REAL(0.D0))THEN
                          IRET = 0
                          RETURN
                        ELSE
                          PROG(IPROG)= SQRT(XTRA)
                        ENDIF
 111                 CONTINUE
                     IELCHE(IGAU,IB)=MLREEL
                     SEGDES MLREE1,MLREEL
 110           CONTINUE
            ELSE IF (TYPCHE(ICOMP).EQ.'POINTEUREVOLUTIO') THEN
               N2PTEL=IELCHE(/1)
               N2EL  =IELCHE(/2)
               DO 112 IB=1,N2EL
                  DO 112 IGAU=1,N2PTEL
                     MEVOL1=IELCHE(IGAU,IB)
                     IRET=2
                     CALL PUEVOL(MEVOL1,XPU,0,MEVOL2,IRET)
                     IELCHE(IGAU,IB)=MEVOL2
 112           CONTINUE
            ELSE
*
*              NOM DE COMPOSANTE NON RECONNU
*
               MOTERR(1:4)='**  '
               MOTERR(5:8)=NOMCHE(ICOMP)
               CALL ERREUR(335)
               SEGSUP MELVAL,MCHAML,MCHELM
               IPCHPU=0
               RETURN
            ENDIF
            SEGDES MELVAL
 140     CONTINUE
         SEGDES MCHAML
 130  CONTINUE
      GOTO 999



c----------------------------------------------------------------------
c     FIN DU PROGRAMME
 999  CONTINUE
      SEGDES MCHELM
      RETURN
      END


