C TCONV4    SOURCE    CHAT      05/01/13    03:34:29     5004

C=======================================================================
C=                            T C O N V 4                              =
C=                            -----------                              =
C=                                                                     =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=   Calcul du jacobien et des derivees des fonctions de forme dans la =
C=   geometrie reelle pour les elements SEG2, SEG3, LIA3 et LIA4.      =
C=                                                                     =
C=  Parametres :  (E)=Entree  (S)=Sortie                               =
C=  ------------                                                       =
C=   XEL    (E)   COORDONNEES des noeuds de l'element (repere GLOBAL)  =
C=   SHP   (E/S)  DERIVEES des fonctions de FORME de l'element         =
C=   IDIM   (E)   DIMENSION du probleme                                =
C=   NBNN   (E)   Nombre de NOEUDS de l'element fini considere         =
C=   DJAC   (S)   JACOBIEN                                             =
C=                                                                     =
C=  Denis ROBERT, le 3 mai 1988.                                       =
C=======================================================================

      SUBROUTINE TCONV4 (XEL,SHP,IDIM,NBNN,DJAC)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCREEL

      DIMENSION XEL(3,*),SHP(6,*)

C ===========================
C  1 - Cas de la DIMENSION 2
C ===========================
      IF (IDIM.EQ.2) THEN
        A1=XZERO
        A2=XZERO
        DO i=1,NBNN
          A1=A1+SHP(2,i)*XEL(1,i)
          A2=A2+SHP(2,i)*XEL(2,i)
        ENDDO
        DJAC=SQRT(A1*A1+A2*A2)
        IF (NBNN.EQ.2) THEN
          A1=XEL(2,2)-XEL(2,1)
          A2=XEL(1,2)-XEL(1,1)
          DLON=SQRT(A1*A1+A2*A2)
          COSANG=A2/DLON
          SINANG=A1/DLON
C** Ces 4 lignes restent a verifier
          SHP(3,1)=SHP(2,1)*SINANG
          SHP(2,1)=SHP(2,1)*COSANG
          SHP(3,2)=SHP(2,2)*SINANG
          SHP(2,2)=SHP(2,2)*COSANG
        ENDIF
C ===========================
C  2 - Cas de la DIMENSION 3
C ===========================
      ELSE IF (IDIM.EQ.3) THEN
        A1=XZERO
        A2=XZERO
        A3=XZERO
        DO i=1,NBNN
          A1=A1+SHP(2,i)*XEL(1,i)
          A2=A2+SHP(2,i)*XEL(2,i)
          A3=A3+SHP(2,i)*XEL(3,i)
        ENDDO
        DJAC=SQRT(A1*A1+A2*A2+A3*A3)
      ENDIF

      RETURN
      END



