C XPXTRA    SOURCE    BP208322  15/06/22    21:23:38     8543
      SUBROUTINE XPXTRA
C
C     CREATION DE LA RIGIDITE OBTENU PAR MULTIPLICATION TENSORIELLE
C              (PONDEREE) DE DEUX CHPOINT ELEMENTAIRES
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
-INC CCOPTIO
-INC SMRIGID
-INC SMCHPOI
-INC SMELEME
-INC CCHAMP
C
      SEGMENT,ITRAV1
        CHARACTER*4 NDCOMP(NC)
      ENDSEGMENT
C
C     LECTURE D'UN CHPOINT
C
      CALL LIROBJ('CHPOINT',MCHPOI,1,IRETOU)
      IF(IERR.NE.0)RETURN
C
C     LECTURE EVENTUELLE D'UN REEL
C
      CALL LIRREE(FLO,0,IRETOU)
      IF(IRETOU.EQ.0)FLO=1.D0
C
C     VERIFICATION DU CARACTERE ELEMENTAIRE DU CHPOINT,
C     SANS SERIE DE FOURIER
C
      SEGACT,MCHPOI
      IF(IPCHP(/1).NE.1)THEN
        WRITE(IOIMP,*)'XXT: the CHPO should be elementar'
        GOTO 9999
      ENDIF
      IF(IFOPOI.EQ.1)THEN
        WRITE(IOIMP,*)'XXT: the CHPO should not be FOURIER'
        GOTO 9999
      ENDIF
C
C     ACTIVATIONS DIVERSES
C
      MSOUPO=IPCHP(1)
      SEGACT,MSOUPO
      IPT1=IGEOC
      MPOVAL=IPOVAL
      SEGACT,IPT1,MPOVAL
C
C     NOM DES COMPOSANTES DE DEPLACEMENTS
C
      NC=NOHARM(/1)
      SEGINI,ITRAV1
      DO IE1=1,NC
        CALL PLACE(NOMDU,LNOMDD,IMOT,NOCOMP(IE1))
        IF(IMOT.EQ.0)THEN
          WRITE(IOIMP,*)'XXT: one component of the CHPOIN is not'
          WRITE(IOIMP,*)'        a force'
          GOTO 9998
        ENDIF
        NDCOMP(IE1)=NOMDD(IMOT)
      ENDDO
C
C     CREATION DU SUPERELEMENT ET DESACTIVATION DU MAILLAGE
C
      NBSOUS=0
      NBELEM=1
      NBNN=IPT1.ICOLOR(/1)
      NBREF=0
      SEGINI,MELEME
      ITYPEL=28
      DO IE1=1,NBNN
        NUM(IE1,1)=IPT1.NUM(1,IE1)
      ENDDO
      ICOLOR(1)=IPT1.ICOLOR(1)
      SEGDES,IPT1,MELEME
C
C     DECRIPTEUR POUR LA RIGIDITE
C
      NLIGRP=NC*NBNN
      NLIGRD=NLIGRP
      SEGINI,DESCR
      DO IE1=1,NBNN
        IDUM=(IE1-1)*NC
        DO IE2=1,NC
          LISINC(IDUM+IE2)=NDCOMP(IE2)
          LISDUA(IDUM+IE2)=NOCOMP(IE2)
          NOELEP(IDUM+IE2)=IE1
          NOELED(IDUM+IE2)=IE1
        ENDDO
      ENDDO
      SEGDES,DESCR,MSOUPO
      SEGSUP,ITRAV1
C
C     CONTENU DE LA RIGIDITE
C
      nelrig=1
      SEGINI,XMATRI
      DO IE1=1,NLIGRP
        DO IE2=1,NLIGRP
          RE(IE1,IE2,1)=VPOCHA((IE1+NC-1)/NC,MOD(IE1+NC-1,NC)+1)
     >               *VPOCHA((IE2+NC-1)/NC,MOD(IE2+NC-1,NC)+1)
        ENDDO
      ENDDO
      SEGDES,XMATRI,MPOVAL
*      NELRIG=1
*      SEGINI,IMATRI
*      IMATTT(1)=XMATRI
*      SEGDES,IMATRI
C
C     CHAPEAU MRIGID DE LA RIGIDITE
C
      NRIGEL=1
      NRIGE=7
      SEGINI,MRIGID
      MTYMAT='RIGIDITE'
      COERIG(1)=FLO
      IRIGEL(1,1)=MELEME
      IRIGEL(2,1)=0
      IRIGEL(3,1)=DESCR
      IRIGEL(4,1)=xMATRI
      IRIGEL(5,1)=0
      IRIGEL(6,1)=0
      IRIGEL(7,1)=0
      ICHOLE=0
      IMGEO1=0
      IMGEO2=0
      IFORIG=IFOPOI
      ISUPEG=0
      SEGDES,MRIGID,MCHPOI
C
C     RETOUR A GIBIANE
C
      CALL ECROBJ('RIGIDITE',MRIGID)
      RETURN
C
C     ERREURS
C
 9998 SEGSUP,ITRAV1
      SEGDES,IPT1,MPOVAL,MSOUPO
 9999 SEGDES,MCHPOI
      RETURN
      END











