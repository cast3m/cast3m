C GCFS2     SOURCE    GOUNAND   05/12/21    21:24:27     5281
      SUBROUTINE GCFS2(PGCOUR,NOPG,DIMSRF,XCOR,POIDS,
     $     IMPR,IRET)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C***********************************************************************
C NOM         : GCFS2
C PROJET      : Noyau linéaire NLIN
C DESCRIPTION : Rajoute des points à une méthode d'intégration type
C               Gauss (PGCOUR).
C               Domaine de type [ Fully symmetric region ]
C                       i.e. segment, carré et cube pour nous.
C               Générateur de type [ Fully symmetric ].
C               Rule structure of index 2 i.e.
C               XCOR=(a,0,...,0)
C
C               le nombre de points générés est 2*DIMSRF :
C               (a,0,...,0), (-a,0,...,0)
C               (0,a,...,0), (0,-a,...,0) et les autres permutations
C
C LANGAGE     : ESOPE
C AUTEUR      : Stéphane GOUNAND (CEA/DRN/DMT/SEMT/LTMF)
C               mél : gounand@semt2.smts.cea.fr
C***********************************************************************
C APPELES          : -
C APPELE PAR       : INGACU
C***********************************************************************
C ENTREES            : DIMSRF, XCOR, POIDS
C ENTREES/SORTIES    : PGCOUR (actif en *MOD), NOPG
C SORTIES            : MYPGS
C CODE RETOUR (IRET) : = 0 si tout s'est bien passé
C***********************************************************************
C VERSION    : v1, 20/10/99, version initiale
C HISTORIQUE : v1, 20/10/99, création
C HISTORIQUE :
C HISTORIQUE :
C***********************************************************************
C Prière de PRENDRE LE TEMPS de compléter les commentaires
C en cas de modification de ce sous-programme afin de faciliter
C la maintenance !
C***********************************************************************
-INC CCOPTIO
CBEGININCLUDE SPOGAU
      SEGMENT POGAU
      CHARACTER*(LNNPG)  NOMPG
      CHARACTER*(LNTPG)  TYPMPG
      CHARACTER*(LNFPG)  FORLPG
      INTEGER NORDPG
      REAL*8 XCOPG(NDLPG,NBPG)
      REAL*8 XPOPG(NBPG)
      ENDSEGMENT
      SEGMENT POGAUS
      POINTEUR LISPG(0).POGAU
      ENDSEGMENT
CENDINCLUDE SPOGAU
      POINTEUR PGCOUR.POGAU
*
      INTEGER NOPG,DIMSRF
      REAL*8 XCOR(DIMSRF)
      REAL*8 POIDS
      INTEGER IMPR,IRET
*
      INTEGER IPERM,ISIGN,ICOOP
*
* Executable statements
*
      IF (IMPR.GT.6) WRITE(IOIMP,*) 'Entrée dans gcfs2'
*
* Boucle sur les permutations
      DO 1 IPERM=1,DIMSRF
* Boucle sur le signe
         DO 12 ISIGN=1,2
            NOPG=NOPG+1
* Boucle sur les coordonnées du point
            DO 122 ICOOP=1,DIMSRF
               IF (ICOOP.EQ.IPERM) THEN
                  PGCOUR.XCOPG(ICOOP,NOPG)=
     $                 DBLE((-1)**ISIGN)
     $                 *XCOR(1)
               ELSE
                  PGCOUR.XCOPG(ICOOP,NOPG)=0.D0
               ENDIF
 122        CONTINUE
            PGCOUR.XPOPG(NOPG)=POIDS
 12      CONTINUE
 1    CONTINUE
*
* Normal termination
*
      IRET=0
      RETURN
*
* Format handling
*
*
* Error handling
*
 9999 CONTINUE
      IRET=1
      WRITE(IOIMP,*) 'An error was detected in subroutine gcfs2'
      RETURN
*
* End of subroutine GCFS2
*
      END



