C TIRIL2    SOURCE    PV        18/06/18    21:15:40     9860
C TIRILO    SOURCE    AM        93/01/12    21:36:57     781
      SUBROUTINE TIRIL2(RP,XL,YOUN,CISAIL,SD,SREDY,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE DE TIMOSHENKO EN AXES LOCAUX
C    "D APRES TEDEL"
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 6X6
C
C     D'PRES TIRILO DC 98
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(6,6)
      DATA P5,P25/.5D0,.25D0/
C
C  INITIALISATIONS
C
      DO 120 J=1,6
      DO 120 I=1,6
  120 RP(I,J)=0.D00
      RL=1.D00/XL
      RL2=RL*RL
C
C  SECTION "REDUITE"
C
      IF ( SREDY. NE.0. ) THEN
           SRY=SREDY
      ELSE
           SRY=SD
      ENDIF
C
      ESL=YOUN*SD*RL
      EIZ=YOUN*XINZ
      GAY=CISAIL*SRY
C
C  REMPLISSAGE DE LA MATRICE
C  FX1
       RP(1,1)=ESL
       RP(1,4)=-ESL
C  FY1
       RP(2,2)= GAY*RL
       RP(2,3)=  P5*GAY
       RP(2,5)= -GAY*RL
       RP(2,6)= P5*GAY
C  MZ1
      RP(3,3)=  P25*GAY*XL + EIZ*RL
       RP(3,5)= -P5*GAY
      RP(3,6)= P25*GAY*XL - EIZ*RL
C  FX2
      RP(4,4)=  ESL
C  FY2
       RP(5,5)= GAY*RL
      RP(5,6)= -P5*GAY
C  MZ2
      RP(6,6)= P25*GAY*XL + EIZ*RL
C
      DO 9 I=1,6
      DO 9 J=I,6
   9  RP(J,I)=RP(I,J)
      RETURN
      END



