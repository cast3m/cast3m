C FLPVL2    SOURCE    CHAT      05/01/13    00:04:01     5004
C FLPVLH    SOURCE    BECC      97/12/23    22:13:54     3017
      SUBROUTINE FLPVL2(NESP,GAMG,ROG,MG,AG,UTG,UVG,HTG,Y,FLUX)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  FLPVLH
C
C DESCRIPTION       :  Voir FLUVLH
C
C                      Onde de Gauche à Droite
C
C LANGUAGE          :  FORTRAN 77
C
C AUTEUR            :  A. BECCANTINI DRN/DMT/SEMT/TTMF
C
C************************************************************************
C
C APPELES           :  aucun
C
C************************************************************************
C
C**** Entrées:
C
C     NESP     =  nombre d'especes in FRAMAS
C
C     GAMG     =  le "gamma" (à gauche)
C
C     ROG      =  la densité
C
C     MG       =  nombre de Mach (normale)
C
C     AG       =  vitesse du son
C
C     UTG      =  vitesse tangentielle
C
C     Y        =  table des fractiones massiques
C
C**** Sorties:
C
C      FLUX    =  table du flux "gauche -> droite" a
C                 l'interface
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  Créé le 6.1.98
C
C************************************************************************
C
C N.B.: Toutes les variables sont declarées explicitement
C
C
C
      IMPLICIT INTEGER(I-N)
      INTEGER I1, NESP
      REAL*8 GAMG,ROG,MG,AG,UTG,UVG,HTG
     &       ,GM1G,MPLUS, CELL, CELLM
     &       ,Y(*),FLUX(*)
C

C**** N.B. :
C
C     FLUX(2) = MPLUS * AG * ROG * UNG + PPLUS * PG
C
C     PG = ROG * AG * AG / GAMG
C
C     ABS(MG) < 1  ->  PPLUS = MPLUS * (2 - MG)
C
C     ABS(MG) > 1  ->  PPLUS = MPLUS / MG
C

C
      GM1G = GAMG - 1.0D0
      IF(ABS(MG) .LE. 1.0D0)THEN
         MPLUS = 0.5D0 * (MG + 1.0D0)
         MPLUS = MPLUS * MPLUS
         CELL = (GM1G * MG+2.0D0)*AG/GAMG
      ELSE
         MPLUS = 0.5D0 * (MG + ABS(MG))
         CELL = (MG + 1.0D0/GAMG/MG)*AG
      ENDIF
      CELLM = MPLUS * AG * ROG
C
      FLUX(1) = CELLM
      FLUX(2) = CELLM * CELL
      FLUX(3) = CELLM * UTG
      FLUX(4) = CELLM * UVG
      FLUX(5) = CELLM * HTG
C
C**** N.B. NESP = 0  -> no boucle
C
      DO I1 = 1, NESP
         FLUX(5+I1) = CELLM * Y(I1)
      ENDDO
C
C
      RETURN
      END



