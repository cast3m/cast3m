C FIBETO    SOURCE    CHAT      12/04/06    21:15:10     7348
      SUBROUTINE FIBETO(XMAT,DEPS,SIG0,VAR0,SIGF,VARF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(18),DEPS(3),SIG0(3),VAR0(6),SIGF(3),VARF(6)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE DE BETON_LIBRE ET DE BETON_CONFINE
C
C----------------------------------------------------------------------
C     Joao Guedes et Pierre Pegon (ISPRA) Juillet/Decembre 1993
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C      XMAT( 3) RHO
C      XMAT( 4) ALPHA
C      XMAT( 5) MAXIMUM COMPRESSION STRESS
C      XMAT( 6) STRAIN OF THE MAXIMUM COMPRESSION STRESS
C      XMAT( 7) MAXIMUM TENSION STRESS
C      XMAT( 8) ALPHA 1 FACTOR
C      XMAT( 9) OMEGA 1 FACTOR
C      XMAT(10) MODULUS OF THE DESCEND. PART OF THE CURVE
C      XMAT(11) STRESS VALUE DEFINING THE COMPRESSION PLATEAU
C      XMAT(12) DESCENDING TRACTION CURVE FACTOR
C
C Paramètres "inactifs"
C      XMAT(13) F2 (FOR LOADING/UNLOADING)
C      XMAT(14) F1 (FOR LOADING/UNLOADING)
C      XMAT(15) RESIDUAL STRESS (TENSION)
C      XMAT(16) F1' (FOR LOADING/UNLOADING)
C      XMAT(17) F2' (FOR LOADING/UNLOADING)
C
C      DEPS(1) EPXX    SIG0(1) SIGF(1) SMXX
C      DEPS(2) GAXY    SIG0(2) SIGF(2) SMXY
C      DEPS(3) GAXZ    SIG0(3) SIGF(3) SMXZ
C
C                      VAR0(1) VARF(1) FLAG
C                      VAR0(2) VARF(2) EPSO
C                      VAR0(3) VARF(3) MAX. STRAIN EVER REACHED
C                      VAR0(4) VARF(4) PLASTIC STRAIN
C                      VAR0(5) VARF(5) MOD. OF THE UNLOAD. CURVE
C                      VAR0(6) VARF(6) TANGENT MODULUS
C
C----------------------------------------------------------------------
C
       GAMMA = XMAT( 1) / 2.0D0 / (XMAT(2)+1.D0)
       FC___ = XMAT( 5)
       EZERO = XMAT( 6)
       FLT__ = XMAT( 7)
       ALFA1 = XMAT( 8)
       OMEG1 = XMAT( 9)
       ZETA_ = XMAT(10)
       STR85 = XMAT(11)
       TRAFA = XMAT(12)
C
       ALFOM = ALFA1 * OMEG1
C
       IF (ZETA_ .LE. 0.D0) THEN
C
C ------- COMPUTING PROPERTIES OF CONFINED CONCRETE -------
C
          IF (ALFOM .LT. 1.0D-1) THEN
             BETAC = 1.000D0 + 2.50D0 * ALFOM
          ELSE
             BETAC = 1.125D0 + 1.25D0 * ALFOM
          ENDIF
C
          FC___ = BETAC * FC___
          EZERO = BETAC * BETAC * EZERO
          ZETA_ = (BETAC-0.85D0) / (BETAC*(0.1D0*ALFOM+0.0035D0-EZERO))
       ENDIF
C
       EPS85 = (1.0D0-STR85/FC___ ) / ZETA_ + EZERO
       EPS85 = -1.0D0 * EPS85 / EZERO
C
       IFLAG = NINT(VAR0(1))
       EPSON = (VAR0(2) + DEPS(1)) / EZERO
       EPSO1 = VAR0(2) / EZERO
       EPMAX = VAR0(3) / EZERO
       EPLAS = VAR0(4) / EZERO
       SLOPE = VAR0(5)
       TANGE = VAR0(6)
       STRE1 = SIG0(1) / FC___
C
       CALL NEWBET
     .       ( FC___, EZERO, FLT__, ZETA_, IFLAG, EPSON,
     .         EPSO1, EPMAX, EPLAS, EPS85, SLOPE, TANGE,
     .         STRES, STRE1, TRAFA )
C
       VARF(1) = DBLE(IFLAG)
       VARF(2) = EPSON * EZERO
       VARF(3) = EPMAX * EZERO
       VARF(4) = EPLAS * EZERO
       VARF(5) = SLOPE
       VARF(6) = TANGE
C
       SIGF(1) = STRES * FC___
       SIGF(2) = SIG0(2) + GAMMA * DEPS(2)
       SIGF(3) = SIG0(3) + GAMMA * DEPS(3)
C
       RETURN
       END




