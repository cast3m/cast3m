C INCRE7    SOURCE    CHAT      05/01/13    00:35:30     5004
       SUBROUTINE INCRE7(SIG,VAR,DSPT,EPSVPT,VARPT,XMAT,NSTRS0,
     &     MFR,NVARI,NCOMAT,IFOUR)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),VAR(*),DSPT(*)
       DIMENSION EPSVPT(*),VARPT(*),XMAT(*)
       DIMENSION XX(6),YY(6),EVPT(6),EPPT(6),SIGPT(6)
       DIMENSION SIG0(6),EPS0(6)
       PARAMETER (AMAX = 1.0D20 , AMIN = 1.D-10)
       DETIER = 2.0D0/3.0D0
       ROOT = SQRT(3.0D0/2.0D0)
       CALL ZDANUL(VARPT,NVARI)
       IF (MFR.EQ.5) THEN
         NSTRS=6
         SIG0(1)=SIG(1)
         SIG0(2)=SIG(2)
         SIG0(3)=0.D0
         SIG0(4)=SIG(3)
         SIG0(5)=SIG(4)
         SIG0(6)=SIG(5)
       ELSE
         NSTRS=NSTRS0
         DO 10 I=1,NSTRS
          SIG0(I)=SIG(I)
  10     CONTINUE
       ENDIF
       TRACE =(SIG0(1)+SIG0(2)+SIG0(3))/3.0D0
C-------------------------------------------------------------------|
C                      COEFFICIENTS MATERIAU                        |
C-------------------------------------------------------------------|
       YOU =XMAT( 1)
       XNU =XMAT( 2)
       RP0 =XMAT( 5)
       QP  =XMAT( 6)
       BP  =XMAT( 7)
       CP1 =XMAT( 8)
       CP2 =XMAT( 9)
       DP1 =XMAT(10)
       DP2 =XMAT(11)
       XK  =XMAT(12)
       XN  =XMAT(13)
       RV0 =XMAT(14)
       QV  =XMAT(15)
       BV  =XMAT(16)
       CV1 =XMAT(17)
       CV2 =XMAT(18)
       DV1 =XMAT(19)
       DV2 =XMAT(20)
       CVP1=XMAT(21)
       CVP2=XMAT(22)

       IF (CP1.EQ.0.D0) THEN
         DCP1 = 0.D0
       ELSE
         DCP1 = DP1 / CP1
       ENDIF

       IF (CP2.EQ.0.D0) THEN
         DCP2 = 0.D0
       ELSE
         DCP2 = DP2 / CP2
       ENDIF

       IF (CV1.EQ.0.D0) THEN
         DCV1 = 0.D0
       ELSE
         DCV1 = DV1 / CV1
       ENDIF

       IF (CV2.EQ.0.D0) THEN
         DCV2 = 0.D0
       ELSE
         DCV2 = DV2 / CV2
       ENDIF

C-------------------------------------------------------------------|
C                        PARTIE VISCOPLASTIQUE                      |
C-------------------------------------------------------------------I
C
C *** CALCUL DE XX=DEVIATEUR(SIGMA-XP)
C
       TRACE =(SIG0(1)+SIG0(2)+SIG0(3))/3.0D0
       DO 71 I=1,NSTRS
       A = 0.0D0
       IF (I.LE.3) A=1.0D0
       XX(I) = SIG0(I)-A*TRACE - VAR(5*NSTRS+I)-VAR(6*NSTRS+I)
  71   CONTINUE
C
C *** CRITERE VISCOPLASTIQUE
C
       AJ2 = PROCON(XX,XX,NSTRS)
       AJ2 = SQRT(1.5D0*AJ2)
       CRIT= AJ2-VAR(10*NSTRS+4)-RV0
C
C *** ECOULEMENT
C
       IF(CRIT.GT.0.D0) THEN
         VPT=(CRIT/XK)**XN
       ELSE
         VPT=0.D0
       ENDIF
       VARPT(10*NSTRS+3)=VPT
       DO 80 I=1,NSTRS
       IF(VPT.EQ.0.D0) THEN
         EVPT(I)=0.D0
         VARPT(7*NSTRS+I)=0.D0
         VARPT(8*NSTRS+I)=0.D0
         VARPT(9*NSTRS+I)=0.D0
       ELSE
         EVPT(I)=1.5*XX(I)*VPT/AJ2
         VARPT(9*NSTRS+I)=EVPT(I)
         VARPT(7*NSTRS+I)=EVPT(I)-(1.5*DCV1*VAR(5*NSTRS+I)*VPT)
         VARPT(8*NSTRS+I)=EVPT(I)-(1.5*DCV2*VAR(6*NSTRS+I)*VPT)
       ENDIF
  80   CONTINUE
C-------------------------------------------------------------------|
C                         PARTIE PLASTIQUE                          |
C-------------------------------------------------------------------I
C
C *** CALCUL DE XX=DEVIATEUR(SIGMA-XP)/J2(SIGMA-X)
C
       DO 70 I=1,NSTRS
       A = 0.0D0
       IF (I.LE.3) A=1.0D0
       XX(I) = SIG0(I)-A*TRACE - VAR(I)-VAR(NSTRS+I)
  70   CONTINUE
       AJ2 = PROCON (XX,XX,NSTRS)
       AJ2 = SQRT(1.5D0*AJ2)
       IF(AJ2.NE.0.D0) THEN
         DO 81 I=1,NSTRS
         XX(I)=1.5*XX(I)/AJ2
  81     CONTINUE
       ENDIF
C
C *** CRITERE PLASTIQUE
C
       CRIT=AJ2-VAR(10*NSTRS+2)-RP0
C
C *** CALCUL DE PPT1=XX:(E:(EPSPT-EVPT)-2/3*SIGVPPT)
C
       IF(CRIT.LE.0.D0) THEN
         PPT=0.D0
         VARPT(10*NSTRS+1)=PPT
         DO 85 I=1,NSTRS
         VARPT(2*NSTRS+I)=0.D0
         VARPT(3*NSTRS+I)=0.D0
         VARPT(4*NSTRS+I)=0.D0
         EPPT(I)=0.D0
   85    CONTINUE
       ELSE
       IF(IFOUR.EQ.-2) THEN
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       SIGPT(1)=DSPT(1)-XCO*(EVPT(1)+XNU*EVPT(2))
       SIGPT(2)=DSPT(2)-XCO*(EVPT(2)+XNU*EVPT(1))
       SIGPT(4)=DSPT(4)-X2MU*EVPT(4)
       SIGPT(3)=0.0D0
       ELSEIF (IFOUR.EQ.2.OR.IFOUR.EQ.0.OR.IFOUR.EQ.1.OR.
     &         IFOUR.EQ.-1.OR.IFOUR.EQ.-3) THEN
       X2MU = YOU/(1+XNU)
       ALMB = X2MU*XNU/(1-2*XNU)
       IF (MFR.EQ.5) THEN
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       SIGPT(1)=DSPT(1)-XCO*(EVPT(1)+XNU*EVPT(2))
       SIGPT(2)=DSPT(2)-XCO*(EVPT(2)+XNU*EVPT(1))
       SIGPT(3)=DSPT(3)-X2MU*EVPT(3)
       SIGPT(4)=DSPT(4)-X2MU*EVPT(4)
       SIGPT(5)=DSPT(5)-X2MU*EVPT(5)
       ELSE
        TRACE=EVPT(1)+EVPT(2)+EVPT(3)
        DO 20 I=1,NSTRS,1
        A = 0.0
        IF (I.LE.3) A=1.0
        SIGPT(I) = DSPT(I)-ALMB*A*TRACE-X2MU*EVPT(I)
   20   CONTINUE
       ENDIF
C= Modes de calcul 1D contraintes planes suivant z (DYCZ et GYCZ)
       ELSE IF (IFOUR.EQ.4.OR.IFOUR.EQ.8) THEN
         XCO = YOU/(1.-XNU*XNU)
         SIGPT(1) = DSPT(1)-XCO*(EVPT(1)+XNU*EVPT(2))
         SIGPT(2) = DSPT(2)-XCO*(EVPT(2)+XNU*EVPT(1))
         SIGPT(3) = 0.D0
C= Modes de calcul 1D contraintes planes suivant y (CYDZ et CYGZ) et
C=                 1D axisymetrique contraintes axiales nulles (AXCZ)
       ELSE IF (IFOUR.EQ.5.OR.IFOUR.EQ.10.OR.IFOUR.EQ.13) THEN
         XCO = YOU/(1.-XNU*XNU)
         SIGPT(1) = DSPT(1)-XCO*(EVPT(1)+XNU*EVPT(3))
         SIGPT(2) = 0.D0
         SIGPT(3) = DSPT(3)-XCO*(EVPT(3)+XNU*EVPT(1))
C= Modes de calcul 1D contraintes planes suivant y et z (CYCZ)
       ELSE IF (IFOUR.EQ.6) THEN
         SIGPT(1) = DSPT(1)-YOU*EVPT(1)
         SIGPT(2) = 0.D0
         SIGPT(3) = 0.D0
C= Autres modes de calcul 1D deformations planes (DYDZ GYDZ DYGZ GYGZ)
C=                        1D axisymetrique (AXDZ AXGZ) et 1D spherique
       ELSE IF (IFOUR.EQ. 3.OR.IFOUR.EQ. 7.OR.IFOUR.EQ. 9.OR.IFOUR.EQ.11
     .      .OR.IFOUR.EQ.12.OR.IFOUR.EQ.14.OR.IFOUR.EQ.15) THEN
         X2MU = YOU/(1.+XNU)
         TRACE = EVPT(1)+EVPT(2)+EVPT(3)
         XCO = X2MU*XNU*TRACE/(1.-XNU)
         SIGPT(1) = DSPT(1)-X2MU*EVPT(1)-XCO
         SIGPT(2) = DSPT(2)-X2MU*EVPT(2)-XCO
         SIGPT(3) = DSPT(3)-X2MU*EVPT(3)-XCO
       ENDIF
       DO 30 I=1,NSTRS
       SIGPT(I)=SIGPT(I)-DETIER*(
     &         CVP1*VARPT(7*NSTRS+I)-CVP2*VARPT(8*NSTRS+I))
   30  CONTINUE
       PPT1=PROCON(SIGPT,XX,NSTRS)
C
C *** CALCUL DE
C     H1=2/3*XX:(C1P(NP-3/2*(D1P/C1P)*X1P)+C2P(NP-3/2*(D2P/C2P)*X2P))
C
       DO 40 I=1,NSTRS
       YY(I)=CP1*(XX(I)-1.5*DCP1*VAR(I))+
     &       CP2*(XX(I)-1.5*DCP2*VAR(NSTRS+I))
 40    CONTINUE
       H1=DETIER*PROCON(XX,YY,NSTRS)
C
C *** CALCUL DE H2=XX:(E:XX)
C
       IF(IFOUR.EQ.-2) THEN
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       YY(1)=XCO*(XX(1)+XNU*XX(2))
       YY(2)=XCO*(XX(2)+XNU*XX(1))
       YY(4)=X2MU*XX(4)
       YY(3)=0.0D0
       ELSEIF (IFOUR.EQ.2.OR.IFOUR.EQ.0.OR.IFOUR.EQ.1.OR.
     &         IFOUR.EQ.-1.OR.IFOUR.EQ.-3) THEN
       X2MU = YOU/(1+XNU)
       ALMB = X2MU*XNU/(1-2*XNU)
       IF (MFR.EQ.5) THEN
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       YY(1)=XCO*(XX(1)+XNU*XX(2))
       YY(2)=XCO*(XX(2)+XNU*XX(1))
       YY(3)=X2MU*XX(3)
       YY(4)=X2MU*XX(4)
       YY(5)=X2MU*XX(5)
       ELSE
        TRACE=XX(1)+XX(2)+XX(3)
        DO 50 I=1,NSTRS,1
        A = 0.0
        IF (I.LE.3) A=1.0
        YY(I) = ALMB*A*TRACE+X2MU*XX(I)
   50   CONTINUE
       ENDIF
C= Modes de calcul 1D contraintes planes suivant z (DYCZ et GYCZ)
       ELSE IF (IFOUR.EQ.4.OR.IFOUR.EQ.8) THEN
         XCO = YOU/(1.-XNU*XNU)
         YY(1) = XCO*(XX(1)+XNU*XX(2))
         YY(2) = XCO*(XX(2)+XNU*XX(1))
         YY(3) = 0.D0
C= Modes de calcul 1D contraintes planes suivant y (CYDZ et CYGZ) et
C=                 1D axisymetrique contraintes axiales nulles (AXCZ)
       ELSE IF (IFOUR.EQ.5.OR.IFOUR.EQ.10.OR.IFOUR.EQ.13) THEN
         XCO = YOU/(1.-XNU*XNU)
         YY(1) = XCO*(XX(1)+XNU*XX(3))
         YY(2) = 0.D0
         YY(3) = XCO*(XX(3)+XNU*XX(1))
C= Modes de calcul 1D contraintes planes suivant y et z (CYCZ)
       ELSE IF (IFOUR.EQ.6) THEN
         YY(1) = YOU*XX(1)
         YY(2) = 0.D0
         YY(3)=0.D0
C= Autres modes de calcul 1D deformations planes (DYDZ GYDZ DYGZ GYGZ)
C=                        1D axisymetrique (AXDZ AXGZ) et 1D spherique
       ELSE IF (IFOUR.EQ. 3.OR.IFOUR.EQ. 7.OR.IFOUR.EQ. 9.OR.IFOUR.EQ.11
     .      .OR.IFOUR.EQ.12.OR.IFOUR.EQ.14.OR.IFOUR.EQ.15) THEN
         X2MU = YOU/(1.+XNU)
         TRACE = XX(1)+XX(2)+XX(3)
         XCO = X2MU*XNU*TRACE/(1.-XNU)
         YY(1) = X2MU*XX(1)+XCO
         YY(2) = X2MU*XX(2)+XCO
         YY(3) = X2MU*XX(3)+XCO
       ENDIF
       H2=PROCON(XX,YY,NSTRS)
C
C *** CALCUL DE H3=BP*(RP-RP0-QP)
C
      H3=BP*(VAR(10*NSTRS+2)-QP)
C
C *** MODULE PLASTIQUE
C
      H=H1+H2-H3
C
C *** CALCUL DE PPT,EPPT,ALPHAPT
C
      PPT=PPT1/H
      IF(PPT.LT.0.D0) PPT=0.D0
      VARPT(10*NSTRS+1)=PPT
      DO 60 I=1,NSTRS
      EPPT(I)=XX(I)*PPT
      VARPT(2*NSTRS+I)=EPPT(I)-(1.5*DCP1*VAR(I)*PPT)
      VARPT(3*NSTRS+I)=EPPT(I)-(1.5*DCP2*VAR(NSTRS+I)*PPT)
      VARPT(4*NSTRS+I)=EPPT(I)
   60 CONTINUE
      ENDIF
C
       IF (MFR.EQ.5) THEN
         EPSVPT(1)=EVPT(1)+EPPT(1)
         EPSVPT(2)=EVPT(2)+EPPT(2)
         EPSVPT(3)=EVPT(4)+EPPT(4)
         EPSVPT(4)=EVPT(5)+EPPT(5)
         EPSVPT(5)=EVPT(6)+EPPT(6)
       ELSE
         DO 11 I=1,NSTRS
          EPSVPT(I)=EVPT(I)+EPPT(I)
  11     CONTINUE
       ENDIF
C
       VARPT(10*NSTRS+5)=VPT+PPT
       RETURN
       END












