C AVCREE    SOURCE    CHAT      05/01/12    21:30:15     5004
      SUBROUTINE AVCREE(TAU,SIG,EPSV,EC0,ESW0,
     &                  SIG1,EPSV1,EC01,ESW01,P1,QTLD1,
     &                  DSPT,EPSVPT,EC0PT,ESW0PT,XMAT,
     &                  NSTRS1,IVIEX,MFR1,IFOURB)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C-----------------------------------------------------------------------
C
C     DESCRIPTION FONCTIONNELLE :
C     -------------------------
C
C     Lois 'VISCO_EXTERNE' : mise a jour des deformations inelastiques,
C     des contraintes, des variables internes pre-definies
C
C     Entrees : TAU,
C               SIG,EPSV,EC0,ESW0 (etat initial),
C               DSPT,EPSVPT,EC0PT,ESW0PT,XMAT
C     Sorties : SIG1,EPSV1,EC01,ESW01,P1,QTLD1 (etat final)
C
C     Parametres : NSTRS1,IVIEX,MFR1,IFOURB
C                  Les indicateurs IVIEX,MFR1,IFOURB ne servent pas
C                  pour l'instant mais seront utiles si extensions :
C                  autres modeles que 'GENERAL', autres formulations EF,
C                  autres options de calcul
C
C-----------------------------------------------------------------------
C     Arguments de l'interface
C
      REAL*8     SIG(*), EPSV(*), SIG1(*), EPSV1(*),
     &           DSPT(*), EPSVPT(*), XMAT(*)
C
C     Variables locales
C
C     Tableaux de travail
C
      REAL*8     SS(8)
C
C------------------- Debut du code executable --------------------------
C
C 1 - Mise a jour des deformations inelastiques
C
      DO 10 I=1,NSTRS1
         EPSV1(I) = EPSV(I) + TAU*EPSVPT(I)
  10  CONTINUE
C
C 2 - Mise a jour des contraintes
C
      YOU = XMAT(1)
      XNU = XMAT(2)
      X2MU = YOU/(1.0D0+XNU)
      ALMB = X2MU*XNU/(1.0D0-2.0D0*XNU)
C
      TRACE = EPSVPT(1) + EPSVPT(2) + EPSVPT(3)
      DO 20 I=1,NSTRS1
         A = 0.0D0
         IF (I.LE.3) A = 1.0D0
         SIG1(I) = SIG(I) + TAU*(DSPT(I)-ALMB*A*TRACE-X2MU*EPSVPT(I))
  20  CONTINUE
C
C 3 - Mise a jour des variables internes pre-definies
C
      EC01 = EC0 + TAU*EC0PT
      ESW01 = ESW0 + TAU*ESW0PT
C
      P1 = 0.333333333333333D0*(SIG1(1)+SIG1(2)+SIG1(3))
      DO 30 I=1,NSTRS1
         A = 0.0D0
         IF (I.LE.3) A = 1.0D0
         SS(I) = SIG1(I)-A*P1
  30  CONTINUE
      QTLD1 = PROCON(SS,SS,NSTRS1)
      QTLD1 = SQRT(1.5D0*QTLD1)
C
      RETURN
      END


