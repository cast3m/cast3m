C DEVEQ1    SOURCE    BP208322  18/12/20    21:15:23     10048
      SUBROUTINE DEVEQ1(Q1,Q2,NA1,XASM,XM,PDT,NPAS,FTOTA,FINERT)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Operateur DYNE : algorithme de FU - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul des deplacements generalises pour le premier demi-pas   *
*     de temps.                                                      *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* es  Q1(.,.) Vecteur des deplacements generalises                   *
* es  Q2(.,.) Vecteur des vitesses generalisees                      *
* e   NA1     Nombre total d'inconnues dans la base A                *
* e   XASM    Vecteur des amortissements generalises                 *
* e   XM      Vecteur des masses generalisees                        *
* e   PDT     pas de temps                               *
* e   NPAS    Numero du pas de temps considere                       *
* e   FTOTA   Forces exterieures totalisees, sur la base A           *
*                                                                    *
*     Auteur, date de creation:                                      *
*                                                                    *
*     Denis ROBERT-MOUGIN, le 19 mai 1989.                           *
*                                                                    *
*--------------------------------------------------------------------*
      REAL*8 Q1(NA1,*),Q2(NA1,*),FINERT(NA1,*)
      REAL*8 XASM(NA1,*),XM(NA1,*),FTOTA(NA1,*)
*
      PDTS2 = PDT * 0.5D0
      AUX4 = PDT * PDT / 24.D0
*
*                           .       2
*     Q        = Q    + h/2 Q    + h /24 ( 4 G    - G
*      i,m+1/2    i,m        i,m              i,m    i,m-1/2
*                          .      .
*                 - A  ( 4 Q    - Q        ) )
*                    i      i,m    i,m-1/2
*
*
*     Boucle sur les inconnues de la base A
*
      DO 10 IA = 1,NA1
         UNSM3 = 1.D0 / ( XM(IA,1) - FINERT(IA,3) )
         UNSM4 = 1.D0 / ( XM(IA,1) - FINERT(IA,4) )
         AUX1 = XASM(IA,1) * ( ( 4.D0 * Q2(IA,3) * UNSM3 )
     &                       - ( Q2(IA,4) * UNSM4 ) )
         AUX2 = (4.D0*FTOTA(IA,3)*UNSM3) - (FTOTA(IA,4)*UNSM4)
         AUX3 = PDTS2 * Q2(IA,3)
         Q1(IA,2) = Q1(IA,3) + AUX3 + ( AUX4 * ( AUX2 - AUX1) )
 10      CONTINUE
*     end do
*
      END





