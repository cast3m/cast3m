C FLA011    SOURCE    SERRE     12/07/10    21:15:16     7431
      SUBROUTINE FLA011(TCPDT,T,DT,TC,TRAV)
C---------------------------------------------------------------------
C Calcul de la température des plaques par la méthode RK4
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C  /S TCPDT   : flottant : Température des plaques au temps t+dt (K)
C E/  T       : flottant : Temps au début du pas de temps (s)
C E/  DT      : flottant : Pas d'intégration (s)
C E/  TC      : flottant : Température des plaques au temps t (K)
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C Aucune variable n'est utilisée.
C Le pointeur du segment de travail est simplement transmis.
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 K0,K1,K2,K3
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
      K0  = FLA010(T,TC,TRAV) * DT
      T1  = T  + 0.5D0 * DT
      TC1 = TC + 0.5D0 * K0
      K1  = FLA010(T1,TC1,TRAV) * DT
      T2  = T  + 0.5D0 * DT
      TC2 = TC + 0.5D0 * K1
      K2  = FLA010(T2,TC2,TRAV) * DT
      T3  = T  + DT
      TC3 = TC + K2
      K3  = FLA010(T3,TC3,TRAV) * DT
C
      TCPDT = TC + 1.D0/6.D0 * (K0 + 2.D0*K1 + 2.D0*K2 +K3)
C
      RETURN
      END








