C ESTIT1    SOURCE    CB215821  17/11/30    21:16:04     9639
      SUBROUTINE ESTIT1(SIG,NSTRS,VAR,NVARI,XMAT,NCOMAT,T,TAUX)
C
C----------------------------------------------------------------------
C Objet: Estimation du pas de temps par integration de la loi
C           d'evolution du dommage a contr. cte
C----------------------------------------------------------------------
C Entree: SIG(NSTRS) tenseur des contraintes
C         VAR(NVARI) tableau contenant les variables internes
C           pilotant les equations
C           VAR(1)=p ; VAR(2)=D11 ; VAR(3)=D22;  VAR(4)=D33;
C           VAR(5)=D12;  VAR(6)=D13;  VAR(7)=D23;    VAR(8)=DMAX
C         XMAT(NCOMAT) tableau des parametres scalaires du materiau
C           pouvant dependre de la temperature
C           XMAT(1)=YOUNG ;XMAT(2)=XNU ;XMAT(3)=N
C           XMAT(4)=M ;XMAT(5)=KK; XMAT(6)=ALPHA
C           XMAT(7)=BETA ;XMAT(8)=r; XMAT(9)=A ;
C           XMAT(10)=q ;XMAT(11)=ALPHAT
C           XMAT(12)=RHO; XMAT(13)=SIGY
C         T temperature
C------------------------------------------------------------------------
C
C------------------------------------------------------------------------
C Sortie: TAUX  pas de temps estime
C----------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION SIG(*),VAR(*),XMAT(*)
      DIMENSION XX(6),A(3,3),D(3),S(3,3)
C
C----------------------------------------------------------------------
C CALCUL DE J1,J2
C   J1: CONTRAINTE MOYENNE
C   J2: CONTRAINTE DE VON MISES
C----------------------------------------------------------------------
C
       AJ1  = SIG(1)+SIG(2)+SIG(3)
       AJ1  = AJ1/3.0D0
       DO I=1,3
         XX(I) = SIG(I)-AJ1
       ENDDO
       DO I=4,NSTRS
         XX(I) = SIG(I)
       ENDDO
       AJ2 =  PROCON(XX,XX,NSTRS)
       AJ2 =  SQRT(1.5D0*AJ2)
C
C---------------------------------------------------------------------
C CALCUL DE LA VALEUR PROPRE D'ENDOMMAGEMENT MAX. : D(*)
C---------------------------------------------------------------------
C
       DMAX = VAR(8)
C
C---------------------------------------------------------------------
C CALCUL DE LA VARIABLE X
C---------------------------------------------------------------------
C
       ALF2 = XMAT(6)
       X    = (ALF2*AJ1)+(AJ2)
C
C--------------------------------------------------------------------
C Estimation du pas de temps par integration de la loi
C d'evolution du dommage a contr. cte
C---------------------------------------------------------------------
C
       EA=XMAT(9)
       ER=XMAT(8)
       EQ=XMAT(10)
       TAUX = 1.0D10
       IF(DMAX.GE.0.3.AND.X.GT.0.0D0) THEN
         EQ1 = EQ + 1
         TAUX = (1.0D0-DMAX)**EQ1/EQ1/(X/EA)**ER
       ENDIF
C
       RETURN
       END




