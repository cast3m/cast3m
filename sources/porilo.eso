C PORILO    SOURCE    PV        18/06/18    21:15:26     9860
      SUBROUTINE PORILO(RP,XL,YOUN,CISAIL,SD,SREDY,SREDZ,
     .                  XINX,XINY,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE EN AXES LOCAUX
C    D APRES TEDEL
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    SREDZ   SECTION REDUITE A L EFFORT TRANCHANT TZ
C    XINX    INERTIE IX
C    XINY    INERTIE IY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 12X12
C
C     VIENT DE TEDEL AOUT 84
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(12,12)
C
C  INITIALISATIONS
C
      DO 120 J=1,12
      DO 120 I=1,12
  120 RP(I,J)=0.D00
      RL=1.D00/XL
      RL2=RL*RL
      RL3=RL2*RL
      X2=0.D00
      X3=0.D00

           IF( SREDZ. NE.0.) X2= YOUN/CISAIL*XINY*RL2/  SREDZ
           IF( SREDY. NE.0.) X3= YOUN/CISAIL*XINZ*RL2/  SREDY
           ESL=YOUN*SD*RL
           EIZ=YOUN*XINZ
           EIY=YOUN*XINY
           GJL=CISAIL*XINX*RL

      X12= 1./(1.+12.*X2)
      X13= 1./(1.+12.*X3)

C
C  REMPLISSAGE DE LA MATRICE
C
      RP(1,1)=ESL
      RP(1,7)=-ESL
       RP(2,2)= 12.*EIZ*RL3*X13
      RP(2,6)=6.*EIZ*RL2*X13
        RP(2,8)=  -12. *EIZ*RL3*X13
      RP(2,12)= 6.*EIZ*RL2*X13
       RP(3,3)=12.*EIY*RL3*X12
       RP(3,5)= -6.*EIY*RL2*X12
      RP(3,9)=  -12.*EIY*RL3*X12
      RP(3,11)=  -6.*EIY*RL2*X12
       RP(4,4)= GJL
      RP(4,10)=-GJL
       RP(5,5)= 4.*EIY*RL
     . *(1.+3.*X2)*X12
      RP(5,9)= 6.*EIY*RL2*X12
        RP(5,11)=2.*EIY*RL
     .*(1.-6.*X2)*X12
      RP(6,6)= 4.*EIZ*RL
     . *(1.+3.*X3)*X13
      RP(6,8)=-6.*EIZ*RL2*X13
      RP(6,12)=2.*EIZ*RL
     .      *(1.-6.*X3)*X13
      RP(7,7)=ESL
       RP(8,8)= 12.*EIZ*RL3*X13
      RP(8,12)=-6.*EIZ*RL2*X13
       RP(9,9)=   12.*EIY*RL3*X12
       RP(9,11)= 6.*EIY*RL2*X12
      RP(10,10)= GJL
      RP(11,11)= 4.*EIY*RL
     .  *(1.+3.*X2)*X12
      RP(12,12)= 4.*EIZ*RL
     . *(1.+3.*X3)*X13
      DO 9 I=1,12
      DO 9 J=I,12
   9  RP(J,I)=RP(I,J)
      RETURN
      END




