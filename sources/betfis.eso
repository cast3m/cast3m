C BETFIS    SOURCE    CB215821  16/04/21    21:15:20     8920
C BETDJE     SOURCE    INSL       24/10/96
      SUBROUTINE BETFIS(EPSRX,STRNX,STRNRX,SIGMRX,SIGRX,TANG,S1X,EDT,
     1 EDC,EPST,EPSC,DEFR,RTM,IFISU,JFISU,IPLA,EQSTR,PENT,EX,RB,ALPHA,
     2 EMAX,EPO1,ICAL,IBB1,IGAU1,IDIR)
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      CHARACTER*8 PASDC
C
C       CE SOUS-PROGRAMME GERE LE COMPORTEMENT DU BETON EN UN POINT
C       OU LA FISSURE EST REFERMEE (IFISU=0 ET JFISU=1)
C
      RBT=ALPHA*RB
      IF(ABS(EDC).LT.0.1D-05) EDC=0.D0
C==================================================================
      IF(EDC.EQ.0.D0.OR.(EQSTR.LE.RBT.AND.IPLA.EQ.0)) THEN
C-------------------------------------------------------------
C       POINT INITIALLEMENT TENDU OU PAS COMPRIME PLUS LOIN
C       QUE -RBT
C
        IF(STRNRX.LT.EPSRX) THEN
C
C        COMPRESSION DE LA FISSURE (CHARGE)
C
         IF(STRNRX.GT.EPST) THEN
          S1X=SIGRX+EDT*STRNX
          TANG=EDT
          EQSTR=ABS(S1X)
          IF(S1X .GE.0.D0) EQSTR=0.D0
         ELSE
          EPEQ=ABS(STRNRX)
          CALL CDCY(EPEQ,SEQ,IPLA,TANG,EX,RB,EMAX,EPO1,ICAL)
          EQSTR=SEQ
          S1X=-SEQ
         ENDIF
        RETURN
        ENDIF
C==================================================================
         IF(EPSRX.GT.EPST) THEN
C
C          PENTE DE DECHARGE ---> EDT
C
          S1X=SIGRX+EDT*STRNX
          TANG=EDT
C
C       REOUVERTURE DE LA FISSURE
C
           IF(S1X.GT.RTM) THEN
            CALL FISSU2(S1X,SIGRX,STRNX,RTM,PENT,TANG)
            IFISU=1
           ENDIF
         RETURN
C
         ELSE
C
C          PENTE DE DECHARGE ---> EDC
C
         PASDC= 'BETFIS1'
         CALL DECHAC(S1X,EDC,EPST,EDT,RTM,EPSC,DEFR,SIGRX,STRNX,
     1   STRNRX,EPSRX,IFISU,JFISU,SIGMRX,IPLA,TANG,PENT,EX,RB,ALPHA,
     2   EPO1,IBB1,IGAU1,IDIR,PASDC)
         RETURN
         ENDIF
C        ENDIF
      ENDIF
C======================================================================
C
C       POINT AYANT SUBIT UNE DECHARGE EN COMPRESSION (EDC .NE. 0)
C
       IF(ABS(SIGMRX).GT.EQSTR.AND.SIGMRX.LE.0.D0) THEN
C======================================================================
C    ************************
C    * DOMAINES DES CHARGES *
C    ************************
C
C       ECROUISSAGE ?
C
        IF(STRNRX.LT.EPSC) THEN
C
C        ON EST SUR LA COURBE DE COMPRESSION (CHARGE)
C
          EPEQ=ABS(STRNRX)
          CALL CDCY(EPEQ,SEQ,IPLA,TANG,EX,RB,EMAX,EPO1,ICAL)
          EQSTR=SEQ
          S1X=-SEQ
        ELSE
C
C        ON EST SUR LA COURBE DE COMPRESSION ---> EDC
C                                         ou ---> EDT
            IF(STRNRX.LT.EPST) THEN
             S1X=EDC*(STRNRX-DEFR)
             TANG=EDC
            ELSE
             S1X=SIGRX+EDT*STRNX
             TANG=EDT
            ENDIF
        ENDIF
C
      ELSE
C======================================================================
C    **************************
C    * DOMAINES DES DECHARGES *
C    **************************
       IF(EPSRX.LT.EPSC) THEN
C
C       DECHARGE A PARTIR DE LA COURBE DE COMPRESSION
C       LE CALCUL DES NOUVELLES VALEURS EPSC/EPST/EDC sera effectu‚
C                                                     dans DECHARC
C
         PASDC= 'BETFIS2'
         CALL DECHAC(S1X,EDC,EPST,EDT,RTM,EPSC,DEFR,SIGRX,STRNX,
     1   STRNRX,EPSRX,IFISU,JFISU,SIGMRX,IPLA,TANG,PENT,EX,RB,ALPHA,
     2   EPO1,IBB1,IGAU1,IDIR,PASDC)
         RETURN
       ELSE
        IF(STRNRX.LT.EPST) THEN
C
C       ON EST SUR LA COURBE DE DECHARGE EDC
C
         IF(EPSRX.GT.EPST) THEN
C
C         PASSAGE DE LA COURBE EDT A LA COURBE EDC
C
         S1X=EDC*(STRNRX-DEFR)
         TANG=EDC
         ELSE
C
C         ON RESTE SUR LA COURBE EDC
C
         S1X=SIGRX+EDC*STRNX
         TANG=EDC
         ENDIF
        ELSE
C
C         ON EST SUR LA COURBE DE DECHARGE EDT
C
         TANG=EDT
         IF(EPSRX.GT.EPST) THEN
C
C         ON RESTE SUR LA COURBE EDT
C
          S1X=SIGRX+EDT*STRNX
         ELSE
C
C         PASSAGE DE LA COURBE EDC A LA COURBE EDT
C
          S1X=-RBT+EDT*(STRNRX-EPST)
         ENDIF
        ENDIF
       ENDIF
      ENDIF
C==================================================================
      IF(S1X.GT.RTM) THEN
C
C       REOUVERTURE DE LA FISSURE
C
       CALL FISSU2(S1X,SIGRX,STRNX,RTM,PENT,TANG)
       IFISU=1
      RETURN
      ENDIF
C==================================================================
      RETURN
      END




