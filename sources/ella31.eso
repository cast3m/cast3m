C ELLA31    SOURCE    KK2000    14/04/09    21:15:18     8027
      SUBROUTINE ELLA31 (R,ZALFAX,ZALFAR,ZALFAF,ZALFAA,
     *                   ZG01,ZG11,ZG02,ZG12,
     *                   ZG03,ZG13,ZG23,ZG33,ZG43,ZZ3,
     *                   ZG04,ZG14)
C
      IMPLICIT INTEGER(I-N)
        IMPLICIT REAL*8 (A-H,O-Y)
        IMPLICIT COMPLEX*16 (Z)
C
C   OPERATEUR ELFE LAPLACE ACOU
C
C   CALCUL POUR LA POUTRE N› INP DES FONCTIONS DE GREEN CORRESPONDAN-
C   TES EN  - TC                   (Num 1)
C           - TORSION              (Num 2)
C           - FLEXION / OY ET / OZ (Num 3)  --> MODELE DE TIMOSCHENKO
C           - PRESSION ACOUSTIQUE  (Num 4)
C   AU POINT DE COORDONNEES LOCALES R.
C
C   PARAMETRES :
C
C   CARACT : TABLEAU DES CARACTERISTIQUE DES POUTRES (10 ,NP)
C   R      : POINT DE CALCUL DE G ( 0 < R < L)
C
C   SORTIES :
C
C   ZG'N''M' : VALEUR DE LA DERIVEE 'N'IEME DE LA FONCTION DE GREEN
C   NUMERO M.
C
C
C   AUTEURS : SAINT-DIZIER ET GORCY
C   DATE    : 24 JANVIER 1991
C
C -- CALCUL DE GREEN 1 : TRACTION COMPRESSION
C                        --------------------
        ZG01  = CMPLX(.5D0) *EXP(-ZALFAX*R)
        ZG11  = CMPLX(-.5D0)*EXP(-ZALFAX*R)
C
C -- CALCUL DE GREEN 2 : TORSION
C                        -------
        ZG02  = CMPLX(.5D0) *EXP(-ZALFAR*R)
        ZG12  = CMPLX(-.5D0)*EXP(-ZALFAR*R)
C
C -- CALCUL DE GREEN 3 : FLEXION (/ OY ET OZ)
C                        --------------------
        ZDELTA= ZZ3*ZZ3 - (4.D0,0.D0)
        ZDEL  = SQRT(ZDELTA)
C
        Z1    = (ZZ3+ZDEL)/CMPLX(2.D0)
        Z2    = (ZZ3-ZDEL)/CMPLX(2.D0)
        ZL1   = SQRT(Z1)
        ZL2   = SQRT(Z2)
        PR1   = ZL1*ZALFAF
        PR2   = ZL2*ZALFAF
C
C -- SELECTION DES PARTIES REELLES POSITIVES
C
        IF (PR1.LT.0.D0) ZL1 = -ZL1
        IF (PR2.LT.0.D0) ZL2 = -ZL2
C
        ZZA = CMPLX(1.D0)/(CMPLX(2.D0)*ZL1*(ZL2*ZL2-ZL1*ZL1))
        ZZB = CMPLX(1.D0)/(CMPLX(2.D0)*ZL2*(ZL1*ZL1-ZL2*ZL2))
C
        ZE1 = EXP(-ZL1*ZALFAF*R)
        ZE2 = EXP(-ZL2*ZALFAF*R)
C
        ZG03  = ZZA*ZE1 + ZZB*ZE2
        ZG13  = -ZL1*ZZA*ZE1 - ZL2*ZZB*ZE2
        ZG23  = ZL1*ZL1*ZZA*ZE1 + ZL2*ZL2*ZZB*ZE2
        ZG33  = -ZL1*ZL1*ZL1*ZZA*ZE1 - ZL2*ZL2*ZL2*ZZB*ZE2
        ZG43  = ZL1*ZL1*ZL1*ZL1*ZZA*ZE1 + ZL2*ZL2*ZL2*ZL2*ZZB*ZE2
C
C -- CALCUL DE GREEN 4 : PRESSION ACOUSTIQUE
C                        -------------------
        ZG04  = CMPLX(.5D0) *EXP(-ZALFAA*R)
        ZG14  = CMPLX(-.5D0)*EXP(-ZALFAA*R)
C
        END




