C CQ8GRA    SOURCE    CHAT      05/01/12    22:27:12     5004
      SUBROUTINE CQ8GRA(XX,NBNN,NBPGAU,LRE,EPAIST,EXCEN,
     1    DZEGAU,SHPCOQ,SHPELE,XDDL,GRADI,IRR)
C |=====================================================================
C |== ENTREES
C |     XX(3,NBNN):  TABLEAU DES COORDONNEES DES NOEUDS
C |     NBNN    : NOMBRE DE NOUDS
C |     NBPGAU  : NOMBRE DE POINTS DE GAUSS
C |     LRE     :    NOMBRE DE DDL
C |     EPAIST  :    EPAISSEUR DE LA COQUE
C |     EXCEN   :    EXCENTREMENT DE LA COQUE
C |     DZEGAU(NBPGAU): COORDONNEES REDUITES DES POIN
C |                     DE GAUSS DANS L EPAISSEUR
C |     SHPCOQ(6,NBNN,NBPGAU) :FONCTIONS DE FORME ET DERIVEES
C |                            AUX POINTS DE  GAUSS
C |     SHPELE(6,NBNN,NBNN) :FONCTIONS DE FORME ET DERIVEES AUX NOEUDS
C |     XDDL(LRE):   DEPLACEMENTS AU NOEUDS
C |== SORTIES
C |     GRADI(9*NBPGAU):9 TERMS DE GRADIAN AUX NBPGAU POINTS DE GAUSS
C |     IRR     :    INDICATEUR DE SUCCES ( 1 ) , D ECHEC (0 OU-1)
C |
C |       CODE SUO X.Z.
C |=====================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(XZER=0.D0)
      DIMENSION DZEGAU(*),SHPCOQ(6,NBNN,*),SHPELE(6,NBNN,*)
      DIMENSION XX(3,*),GRADI(*),XDDL(*)
      DIMENSION TXR(3,3,8),BGR(9,48),TH(8),EXC(8),T(9),XSTRS(9)
C
      NGRADI=9*NBPGAU
      DO 2 J=1,NGRADI
      GRADI(J)=XZER
    2 CONTINUE
C
C   ---   DETERMINATION DES AXES LOCAUX AU NOEUD I   ---
C
      CALL CQ8LOC(XX,NBNN,SHPELE,TXR,IRR)
C
      DO 70 K=1,NBNN
      TH(K)=EPAIST
      EXC(K)=EXCEN
   70 CONTINUE
C
C-----------------------------------
C     BOUCLE SUR LES POINTS DE GAUSS
C-----------------------------------
C
C
      DO 100 IA=1,NBPGAU
      E3 = DZEGAU(IA)
C----------------------------------------
C     CALCUL DE LA MATRICE BGR DE GRADIAN
C----------------------------------------
      DO 3 I=1,9
      DO 3 J=1,LRE
      BGR(I,J)=XZER
    3 CONTINUE
      CALL BGRCQ8(IA,XX,NBNN,TH,EXC,BGR,DET,E3,SHPCOQ,TXR,IRR)
C
      IF(IRR.NE.1) RETURN
C
C     CALCUL DES GRADIENTS POUR LE POINT DE GAUSS
C
      CALL BGRDEP(BGR,9,XDDL,LRE,XSTRS)
C
C     RANGEMENT DANS GRADI(9*NBPGAU)
C
      DO 80 K=1,9
      JE=9*(IA-1)+K
      GRADI(JE)=XSTRS(K)
   80 CONTINUE
C
  100 CONTINUE
      RETURN
      END


