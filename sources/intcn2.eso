C INTCN2    SOURCE    PV        09/03/12    21:25:20     6325
      SUBROUTINE INTCN2(IPVANO,IPGEOM,IPINTE,IPVAEQ)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
************************************************************************
*
*                             I N T C N 2
*                             -----------
*
* FONCTION:
* ---------
*     INTEGRATION NUMERIQUE DANS UN DOMAINE BIDIMENSIONNEL DU PRODUIT:
*                     COEF. * TRANSPOSEE( N )
*     COEF. : GRANDEUR PHYSIQUE REPRESENTEE PAR UN CHAMELEM
*     N     : FONCTIONS DE FORME DE L'ELEMENT MAILLANT LE DOMAINE
*             CONSIDERE
*
* MODULES UTILISES:
* -----------------
*
-INC CCOPTIO
-INC CCREEL
-INC SMCHAML
-INC SMELEME
-INC SMINTE
-INC SMCOORD
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE   (+ = CONTENU DANS UN COMMUN)
* -----------
*
*     IPVANO  (E)  POINTEUR SUR UN SEGMENT MELVAL CONTENANT LES
*                  VALEURS NODALES DE NOTRE COEFFICIENT
*     IPGEOM  (E)  POINTEUR SUR UN OBJET MAILLAGE ELEMENTAIRE
*                  DU DOMAINE D'INTEGRATION
*     IPINTE  (E)  POINTEUR SUR UN SEGMENT MINTE CONTENANT LES
*                  CARACTERISTIQUES D'INTEGRATION
*    +IDIM    (E)  VOIR CCOPTIO
*    +XPI     (E)  VOIR CCREEL
*     IPVAEQ  (S)  POINTEUR SUR UN SEGMENT MELVAL CONTENANT LES VALEURS
*                  NODALES EQUIVALENTES
*
* VARIABLES:
* ----------
*
*     XE(3,NBPTEL)  =  COORDONNEES DES ELEMENTS DANS LE REPERE GLOBAL
*     A ET S        =  TABLEAUX DE TRAVAIL
*
      REAL*8  S(2,3)
      SEGMENT,MMAT1
         REAL*8 XE(3,NBPTEL),A(NBPTEL,NBPTEL),SHP(6,NBPTEL)
      ENDSEGMENT
*
* CONSTANTES:
* -----------
*
      PARAMETER ( O0=0.D0 )
      PARAMETER ( O1=1.D0 )
      PARAMETER ( O2=2.D0 )
*
* REMARQUES:
* ----------
*
*     L'UTILISATION DE CE S-P PRESUPPOSE UN PRE ET POST-TRAITEMENT
*     DES SEGMENTS MELVAL PASSES EN TANT QUE PARAMETRES
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     DENIS ROBERT,LE 15 AVRIL 1988.
*
* LANGAGE:
* --------
*
*     ESOPE + FORTRAN77
*
************************************************************************
*
*     ON RECUPERE LES VALEURS DU COEFFICIENT
*
      MELVA1=IPVANO
      SEGACT,MELVA1
      NBPTE1=MELVA1.VELCHE(/1)
      NEL1=MELVA1.VELCHE(/2)
*
*     ON RECUPERE LES CARACTERISTIQUES D'INTEGRATION
*
      MINTE=IPINTE
      SEGACT,MINTE
      NBPGAU=POIGAU(/1)
*
*     ON RECUPERE UN DES MAILLAGES ELEMENTAIRES DE L'ENVELOPPE
*
      MELEME=IPGEOM
      SEGACT,MELEME
      NBPTEL=NUM(/1)
      NEL=NUM(/2)
*
*     INITIALISATION DU MELVAL QUI CONTIENDRA LES VALEURS EQUIVALENTES
*
      N1PTEL=NBPTEL
      N1EL=NEL
      N2PTEL=0
      N2EL=0
      SEGINI,MELVAL
      IPVAEQ=MELVAL
      SEGINI,MMAT1
*
*     BOUCLE SUR LES ELEMENTS
*
      DO 10 IEL=1,NEL
*
*        ON CHERCHE LES COORDONNEES DES NOEUDS DANS LE REPERE GLOBAL
*
         CALL DOXE(XCOOR,IDIM,NBPTEL,NUM,IEL,XE)
         CALL ZERO(A,NBPTEL,NBPTEL)
*
*        BOUCLE SUR LES POINTS DE GAUSS
*
         DO 20 IGAU=1,NBPGAU
*
*           CALCUL DU JACOBIEN AU POINT DE GAUSS CONSIDERE
*
            DO 30 IP = 1,2
               IP1 = IP + 1
               DO 30 IQ = 1,3
                  S(IP,IQ) = XZERO
                  DO 40 IR = 1,NBPTEL
                     S(IP,IQ)=S(IP,IQ)+SHPTOT(IP1,IR,IGAU)*XE(IQ,IR)
 40                  CONTINUE
*                 END DO
 30               CONTINUE
*           END DO
            S1 = S(1,2)*S(2,3)-S(1,3)*S(2,2)
            S2 = S(1,3)*S(2,1)-S(1,1)*S(2,3)
            S3 = S(1,1)*S(2,2)-S(1,2)*S(2,1)
            DJAC = SQRT ( S1*S1 + S2*S2 + S3*S3 )
            IF (IFOMOD.EQ.0) THEN
*
*              CAS DES ELEMENTS AXISYMETRIQUES
*
               DO 41 NP=1,NBPTEL
                  SHP(1,NP)=SHPTOT(1,NP,IGAU)
                  SHP(2,NP)=SHPTOT(2,NP,IGAU)
                  SHP(3,NP)=SHPTOT(3,NP,IGAU)
 41               CONTINUE
*              END DO
               CALL DISTRR(XE,SHP,NBPTEL,RR)
               DJAC=O2*XPI*RR*DJAC
            ENDIF
            DO 50 INOE=1,NBPTEL
               DO 60 INO2=1,NBPTEL
                  A(INOE,INO2)=A(INOE,INO2)+SHPTOT(1,INOE,IGAU)*
     &            SHPTOT(1,INO2,IGAU)*POIGAU(IGAU)*DJAC
 60               CONTINUE
*              END DO
 50            CONTINUE
*           END DO
 20         CONTINUE
*        END DO
         IEMIN=MIN(NEL1,IEL)
         DO 70 INOE=1,NBPTEL
            DO 80 INO2=1,NBPTEL
               INMIN=MIN(NBPTE1,INO2)
         VELCHE(INOE,IEL)=VELCHE(INOE,IEL)+MELVA1.VELCHE(INMIN,IEMIN)*
     &                    A(INO2,INOE)
 80            CONTINUE
*           END DO
 70         CONTINUE
*        END DO
 10      CONTINUE
*     END DO
*
      SEGSUP,MMAT1
      SEGDES,MELEME,MELVA1,MINTE
      SEGDES,MELVAL
*
      END





