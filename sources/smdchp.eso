C SMDCHP    SOURCE    CB215821  19/03/18    21:16:24     10161
C***********************************************************************
C NOM         : smdchp.eso
C DESCRIPTION : Sortie des CHPOINT dans un fichier au format MED
C***********************************************************************
C HISTORIQUE :  16/10/2017 : RPAREDES : CREATION
C HISTORIQUE :  07/03/2019 : CB215821 : Compatibilite avec Salome > 9.2
C***********************************************************************
C Prière de PRENDRE LE TEMPS DE COMPLÉTER LES COMMENTAIRES
C en cas de modification de ce sous-programme afin de faciliter
C la maintenance !
C***********************************************************************
C APPELÉ PAR : opérateur (SORT 'MED') sormed.eso
C***********************************************************************
C ENTRÉES :   fid     : Id du fichier
C             name    : Nom du MAILLAGE MED courant
C             IJGROU  : SEGMENT contenant la liste des GROUPES
C             LISCHP  : SEGMENT contenant la liste des MPOVAL
C
C SORTIES : aucune
C***********************************************************************
      SUBROUTINE smdchp(fid, name, IJGROU, LISCHP)

      IMPLICIT INTEGER(i-n)
      IMPLICIT REAL*8(a-h,o-z)

-INC CCMED
-INC CCOPTIO
-INC SMELEME
-INC SMCOORD
-INC CCGEOME
-INC SMCHPOI
-INC SMLENTI

C-----Déclaration des variables
      INTEGER*4 cret
      INTEGER*4 cs
      INTEGER*4 etype
      INTEGER*4 fid
      INTEGER*4 ftype
      INTEGER*4 gtype
      INTEGER*4 n
      INTEGER*4 ncomp
      INTEGER*4 numit
      INTEGER*4 numdt
      INTEGER*4 psize
      INTEGER*4 stm
      INTEGER*4 swm

      CHARACTER*4 cha4F

C-----Définition des réels
      REAL*8    dt

C-----Chaines de Caractere de longueur MED_NAME_SIZE=64
      CHARACTER*(MED_NAME_SIZE) fname
      CHARACTER*(MED_NAME_SIZE) lname
      CHARACTER*(MED_NAME_SIZE) name
      CHARACTER*(MED_NAME_SIZE) mname
      CHARACTER*(MED_NAME_SIZE) pname

C-----Chaines de Caractere de longueur MED_SNAME_SIZE=16
      CHARACTER*(MED_SNAME_SIZE) dtunit

C-----SEGMENT pour stocker les profils des familles (numeros d'elements local)
      SEGMENT IPROFI(nbelp)
      SEGMENT IPROF1(nbelp)

C-----Information sur les GROUPES
      SEGMENT IJGROU
        INTEGER                    ILENTI(nbgrou)
        INTEGER                    IPMAIL(nbgrou)
        CHARACTER*(MED_LNAME_SIZE) CNOMGR(nbgrou)
C       nbgrou : Nombre de groupes
C       ILENTI : pointeur LISTENTI des numeros de famille composant les groupes
C       IPMAIL : pointeur MELEME du groupe en question
C       CNOMGR : Noms des groupes
      ENDSEGMENT

C---- Donnees pour CHPOINT et MCHAML
      SEGMENT IFOCHA
        CHARACTER*(MED_NAME_SIZE) NOCHAP(nbmspo)
        INTEGER                   PNUMDT(nbmspo)
        INTEGER                   LICHAP(nbmspo)
        INTEGER                   LIMAIL(nbmspo)
        INTEGER                   ISUPOR(nbmspo,2)
        REAL*8                    XTEMPS(nbmspo)
C       nbmspo : nombre de champs a sortir
C       NOCHAP : nom du champ a produire
C       PNUMDT : numero de pas de temps (numdt)
C       LICHAP : liste de pointeurs MSOUPO ou MCHAML
C       LIMAIL : liste de pointeurs MELEME
C       ISUPOR : type de support  (0,1) : Non-Defini (CHPOINT)
C                                 (1,1) : NOEUDS
C                                 (2,1) : GRAVITE
C                                 (N,1) : Non-prevu encore
C                                 (.,2) : Pointeur sur un MINTE (SEGMENT d'INTEGRATION)
C       XTEMPS : Valeur du temps pour cet instant
      ENDSEGMENT
      POINTEUR LISCHP.IFOCHA

C-----SCHMED : Proprietes des CHAMPS a sortir
      SEGMENT SCHMED
        CHARACTER*(MED_NAME_SIZE) CCHMED(nchmed)
        CHARACTER*(MED_NAME_SIZE) CPRMED(nchmed)
C       CCHMED : Nom du champ  MED a creer
C       CPRMED : Nom du profil MED a creer
      ENDSEGMENT

      SEGMENT SLISCO
        CHARACTER*(MED_SNAME_SIZE) LISSCP(nbcomp)
        CHARACTER*(MED_SNAME_SIZE) LCUNIT(nbcomp)
      ENDSEGMENT

C-----SEGMENT pour traiter les valeurs en INTEGER*4
      SEGMENT INT4(itaill)

C **********************************************************************
C     Traitement des CHPOINT : Champ, Profil et Valeurs
C **********************************************************************
      INT4    = 0
      nbgrou  = IJGROU.IPMAIL(/1)
      nbmspo  = LISCHP.LICHAP(/1)
      nchmed  = nbmspo
      SEGINI,SCHMED
      nbch = 0
      nbpr = 0
      DO ia = 1,nbmspo
        fname  = LISCHP.NOCHAP(ia)
        MSOUPO = LISCHP.LICHAP(ia)
        nbcomp = MSOUPO.NOHARM(/1)

        CALL PLACE(SCHMED.CCHMED, nbch, iplace, fname)
        IF(iplace .EQ. 0)THEN
C-------  Creation du Champ
          nbch = nbch + 1
          SCHMED.CCHMED(nbch)=fname
          SEGINI,SLISCO
          DO ie = 1,nbcomp
            SLISCO.LISSCP(ie) = MSOUPO.NOCOMP(ie)
            SLISCO.LCUNIT(ie) ='NO_UNIT'
          ENDDO

          ftype  = MED_FLOAT64
          ncomp  = nbcomp
          dtunit ='NO_UNIT'
          mname  = name

          CALL mfdcre(fid, fname, ftype, ncomp,
     &        SLISCO.LISSCP,SLISCO.LCUNIT, dtunit, mname, cret)
          IF (cret .NE. 0) THEN
            moterr    = 'mfdcre'
            interr(1) = cret
            CALL ERREUR(873)
            RETURN
          ENDIF
          SEGSUP,SLISCO
        ENDIF

        IPT1   = LISCHP.LIMAIL(ia)
C       Verification de controle au CAS OU on aurait oublie un MAILLAGE
        CALL PLACE2(IJGROU.IPMAIL,nbgrou,IDANS,IPT1)
        IF(IDANS .EQ. 0)THEN
C         Determination du FORMAT automatique
          IFORMA = INT(LOG10(REAL(IPT1))) + 1
          cha4F='(I )'
          IF    (IFORMA.GE.1 .AND. IFORMA.LT.9)THEN
            WRITE(cha4F(3:3),'(I1)') IFORMA
            WRITE(pname     , cha4F) IPT1
          ELSE
            CALL ERREUR(1094)
            RETURN
          ENDIF
        ELSE
          pname  = IJGROU.CNOMGR(IDANS)
        ENDIF

        itaill = IPT1.NUM(/2)
        psize  = itaill

        CALL PLACE(SCHMED.CPRMED, nbpr, iplace, pname)
        IF(iplace .EQ. 0)THEN
C-------  Creation du Profil
          nbpr = nbpr + 1
          SCHMED.CPRMED(nbpr)=pname
          SEGINI,INT4
          CALL pfprw4(fid, pname, psize, IPT1.NUM, INT4(1), cret)
          IF (cret .NE. 0) THEN
            moterr    ='pfprw4'
            interr(1) = cret
            CALL ERREUR(873)
            RETURN
          ENDIF
          SEGSUP,INT4
        ENDIF

C------ Ecriture des Valeurs du Champ
        MPOVAL = MSOUPO.IPOVAL
        ndt    = LISCHP.PNUMDT(ia)
        numit  = MED_NO_IT
        dt     = LISCHP.XTEMPS(ia)
        numdt  = ndt
        etype  = MED_NODE
        gtype  = MED_NONE
        stm    = MED_COMPACT_STMODE
        lname  = ' '
        swm    = MED_NO_INTERLACE
        cs     = MED_ALL_CONSTITUENT
        n      = psize

        CALL mfdrpw(fid, fname, numdt, numit, dt, etype, gtype, stm,
     &        pname, lname, swm, cs, n, MPOVAL.VPOCHA, cret)
        IF (cret .NE. 0) THEN
          moterr    = 'mfdrpw'
          interr(1) = cret
          CALL ERREUR(873)
          RETURN
        ENDIF
      ENDDO
      SEGSUP,SCHMED

      END

