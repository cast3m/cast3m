C POUBS2    SOURCE    CHAT      05/01/13    02:18:23     5004
C POUBS2    SOURCE    CHAT      89/05/26    21:34:31
      SUBROUTINE POUBS2(FORCE,CARAC,COORD,CONT,WORK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DES FORCES INTERNES B*SIGMA
C             POUR LA POUTRE DROITE
C
C  ENTREE
C          CONT    CONTRAINTES
C          CARAC   TABLEAU DE CARACTERISTIQUES GEOMETRIQUES ET
C                  MATERIELLES DE LA POUTRE  ( 5 VALEURS )
C          COORD   COORDONNEES DES 2 NOEUDS
C
C  SORTIE
C          FORCE   FORCES INTERNES
C          KERRE   INDICE D'ERREUR ( 0 SI PAS DE PB )
C  TRAVAIL
C          WORK(300)
C
C     MAI 85  MILLARD
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),CARAC(*),COORD(3,*),P(2,2),
     .          XE(2),YE(2),FORCE(*),WORK(*)
C
      KERRE=0
      DO 2 I=1,2
      XE(I)=COORD(1,I)
  2   YE(I)=COORD(2,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C  VALEURS MOYENNES DANS L ELEMENT
C
      SIGM=0.5D00*(CONT(1)+CONT(4))
C FX
      WORK(1)=-SIGM
      WORK(4)= SIGM
C FY
      WORK(2)=-CONT(2)
      WORK(5)= CONT(5)
C MZ
      WORK(3)= -CONT(3)
      WORK(6)= CONT(6)
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPA2(XE,YE,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C  PASSAGE DES FORCES EN AXES GLOBAUX
C
      CALL POUVE2(WORK,FORCE,P,2)
      RETURN
999   KERRE=1
      RETURN
      END



