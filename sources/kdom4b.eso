C KDOM4B    SOURCE    CHAT      05/01/13    00:54:16     5004
      SUBROUTINE KDOM4B(X1,Y1,X2,Y2,X3,Y3,VOLU,SURF,XCEN,YCEN,XC2D,YC2D)
C
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  KDOM4B
C
C DESCRIPTION       :  Subroutine called by KDOM4A
C                      Axial-symmetric case
C                      We compute
C                      the surface of the 2D mesh
C                      the 3D volume of the 3D mesh
C                      its center of gravity
C
C LANGAGE           :  FORTRAN 77 + ESOPE 2000 (avec estensions CISI)
C
C AUTEUR            :  A. BECCANTINI, DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C INPUT             : (X1,Y1), (X2,Y2), (X3,Y3) coordinates of the TRI3
C
C OUTPUT            : VOLU: volume
c                     SURF: surface
C                     (XCEN,YCEN) center of gravity
C                     (XC2D,YC2D) center of gravity of the surface
C
C************************************************************************
C
C Created the 24/02/04
C
C
      IMPLICIT INTEGER(I-N)
      REAL*8 X1,Y1,X2,Y2,X3,Y3,VOLU,SURF,XCEN,YCEN
     &     ,RJACO,XC2D,YC2D
C
      RJACO=(X2-X1)*(Y3-Y1)-(X3-X1)*(Y2-Y1)
      RJACO=ABS(RJACO)
      SURF=0.5D0*RJACO
      XC2D=(X1+X2+X3)/3.0D0
      YC2D=(Y1+Y2+Y3)/3.0D0
      VOLU=RJACO*(X1+X2+X3)/6.0D0
C
      XCEN=(X1+X2)**2
      XCEN=XCEN+((X1+X3)**2)
      XCEN=XCEN+((X2+X3)**2)
      XCEN=XCEN*RJACO/24.0D0
      XCEN=XCEN/VOLU
      YCEN=(X1+X2)*(Y1+Y2)
      YCEN=YCEN+((X1+X3)*(Y1+Y3))
      YCEN=YCEN+((X2+X3)*(Y2+Y3))
      YCEN=YCEN*RJACO/24.0D0
      YCEN=YCEN/VOLU
C
      RETURN
      END




