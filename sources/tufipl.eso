C TUFIPL    SOURCE    CHAT      05/01/13    03:56:07     5004
      SUBROUTINE TUFIPL(XM,XP,DELTAM,DELTAP,THETA,XJP,DLAM1,DLAM2,EP,
     &FIP,PRECIS,XM0,XP0,RAYOM,XLAM0,WORK,XJ1C,T,THETA0,EPAI,YOUN,KERRE,
     &MAPL)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C***********************************************************************
C      CE SOUS PROGRAMME EFFECTUE LA PARTIE DE L ECOULEMENT PLASTIQUE
C      PENDANT LAQUELLE IL N Y A PAS PROPAGATION
C      IL EST APPELE PAR TUFPLA
C
C          ENTREES:XM     VALEUR DU MOMENT AU DEBUT DU PAS
C                  XP     VALEUR DE L EFFORT NORMAL AU DEBUT DU PAS
C                  DELTAM  INCREMENT DE MOMENT
C                  DELTAP  INCREMENT D EFFORT NORMAL
C                  THETA   ANGLE DE LA FISSURE AU DEBUT DU PAS
C                  XJP    VALEUR DE LA PARTIE PLASTIQUE DE J
C
C          SORTIES:XM     VALEUR DU MOMENT A LA FIN DU PAS
C                  XP     VALEUR DE L EFFORT NORMAL A LA FIN DU PAS
C                  DELTAM  PARTIE DE L INCREMENT DE MOMENT RESTANT
C                          A ECOULER EN PLASTICITE
C                  DELTAP  PARTIE DE L INCREMENT D EFFORT NORMAL
C                          RESTANT A ECOULER EN PLASTICITE
C=======================================================================
-INC CCOPTIO
      SEGMENT/WORK/(SIG(NCOURB)*D,XLAM(NCOURB)*D)
      XMS=XM
      XPS=XP
      DMS=DELTAM
      DPS=DELTAP
      XJPS=XJP
      IF(IIMPI.EQ.999) WRITE (IOIMP,*) 'ENTREE DANS TUFIPL'
      CALL TUFIPA(XM,XP,DELTAM,DELTAP,THETA0,XJP,EP,FIP,
     $DLAM1,DLAM2,PRECIS,XM0,XP0,RAYOM,XLAM0,WORK,EPAI,YOUN,KERRE,
     $THETA)
      CALL TUFIJE(XM,XP,THETA0,XJE,RAYOM,EPAI,YOUN)
      IF(MAPL.EQ.14) THEN
        CALL TUFIC2(XJP,XJE,THETA,IRE,PRECIS,XJ1C,T,RAYOM,THETA0)
      ELSE
        IRE=0
      ENDIF
      IF(IRE.EQ.0) THEN
          IF(IIMPI.EQ.999) WRITE (IOIMP,*) 'SORTIE DE TUFIPL'
          RETURN
      ENDIF
      XL1=0.75D0
      XM1=XMS
      XP1=XPS
      DM1=XL1*DMS
      DP1=XL1*DPS
      CALL TUFIPA(XM1,XP1,DM1,DP1,THETA,XJPS,EP,FIP,DLAM1,DLAM2,PRECIS,
     &XM0,XP0,RAYOM,XLAM0,WORK,EPAI,YOUN,KERRE,THETA)
      F1=CRIT2(XM1,XP1,THETA,XJPS,XJ1C,T,RAYOM,THETA0,EPAI,YOUN)
      XL2=0.5D0
      XM2=XMS
      XP2=XPS
      DM2=XL2*DMS
      DP2=XL2*DPS
      CALL TUFIPA(XM2,XP2,DM2,DP2,THETA,XJPS,EP,FIP,DLAM1,DLAM2,PRECIS,
     &XM0,XP0,RAYOM,XLAM0,WORK,EPAI,YOUN,KERRE,THETA)
      F2=CRIT2(XM2,XP2,THETA,XJPS,XJ1C,T,RAYOM,THETA0,EPAI,YOUN)
1     CONTINUE
      FP=(F2-F1)/(XL2-XL1)
      XL3=XL2-F2/FP
      XM3=XMS
      XP3=XPS
      DM3=XL3*DMS
      DP3=XL3*DPS
      CALL TUFIPA(XM3,XP3,DM3,DP3,THETA,XJPS,EP,FIP,DLAM1,DLAM2,PRECIS,
     &XM0,XP0,RAYOM,XLAM0,WORK,EPAI,YOUN,KERRE,THETA)
      F3=CRIT2(XM3,XP3,THETA,XJPS,XJ1C,T,RAYOM,THETA0,EPAI,YOUN)
      CRI=ABS((F3-F2)/F2)
      IF(CRI.GE.PRECIS) THEN
           XL1=XL2
           XL2=XL3
           F1=F2
           F2=F3
           GO TO 1
      ELSE
           XJP=XJPS
           XM=XM3
           XP=XP3
           DELTAM=(1.D0-XL3)*DMS
           DELTAP=(1.D0-XL3)*DPS
           IF(IIMPI.EQ.999) WRITE (IOIMP,*) 'SORTIE DE TUFIPL'
           RETURN
      ENDIF
      END

