C COPAIN    SOURCE    SERRE     16/12/13    21:15:04     9254
      SUBROUTINE COPAIN(IPT1,IPT2,IPT3,IPT4,
     &  MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,MPOVA6,
     &  MPOVA7,MPOVA8,MPOVA9,MPOVAA,
     &  MPOVAB,MPOVAC,MPOVAD,MPOVAE,
     &  G1,BETA,HMAX,MPOVACP,MPOVAF,MPOVAI,MPOVAJ,
     &  MPOUT1,MPOUT2,MPOUT3,MPOUT4)
C--------------------------------------------------------------------
C Evaluation du coef. d'echange thermique en paroi et du debit de
C condensation lorsque la correlation retenue est celle de COPAIN
C--------------------------------------------------------------------
C
C  E/   IPT1   : Maillage des points CELL
C  E/   IPT2   : Maillage des points LAYERW
C  E/   IPT3   : Maillage des points WALL
C  E/   IPT4   : Maillage de SEG2 CELL/LAYERW
C  E/   MPOVA1 : Pression totale
C  E/   MPOVA2 : Temperature du melange gazeux
C  E/   MPOVA3 : Densitee du melange - (en kg/m3)
C  E/   MPOVA4 : Densitee partielle de vapeur (en kg/m3)
C  E/   MPOVA5 : Pression partielle de vapeur
C  E/   MPOVA6 : Temperature de saturation
C  E/   MPOVA7 : Coefficient d'echange par conduction
C  E/   MPOVA8 : Temperature des murs
C  E/   MPOVA9 : Surface des parois
C  E/   MPOVAA : Flux de chaleur en paroi
C  E/   MPOVAB : Lambda du melange
C  E/   MPOVAC : Nu du melange
C  E/   MPOVAD : Constante des gaz parfait des incondensables
C  E/   MPOVAE : Coeff de diffusion de la vapeur
C  E/   MPOVAF : Constante des gaz parfait du melange a Tgas
C  E/   MPOVAI : Masse molaire des incondensables
C  E/   MPOVAJ : Masse molaire de la vapeur d'eau
C  E/   G1     : Gravite (aeleration de la pesanteur)
C  E/   BETA   : Fraction de l'energie associee a la condensation
C                transferee au mur (defaut 92%)chilton
C  E/   HMAX   : Borne sup pour le coefficient de transfert thermique
C  E/   MPOVACP: Coefficient calorifique de l'eau
C   /S  MPOUT1 : Coefficient d'echange global en paroi
C   /S  MPOUT2 : Debit de condensation / flux thermique
C   /S  MPOUT3 : Enthalpie liquide
C   /S  MPOUT4 : Coefficient d'échange convectif en paroi
C
C--------------------------------------------------------------------
C Les segments MPOUT... ne sont pas desactives
C--------------------------------------------------------------------
C 03/07/02 : Le debit de condensation est mis a 0. si il est positif
C FD         On se sert de QW2 update afin d'en lisser les variations
C
C
C--------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C Auteurs :
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      POINTEUR MPOVA7.MPOVAL,MPOVA8.MPOVAL,MPOVA9.MPOVAL,MPOVAA.MPOVAL
      POINTEUR MPOVAB.MPOVAL,MPOVAC.MPOVAL,MPOVAD.MPOVAL,MPOVAE.MPOVAL
      POINTEUR MPOVAF.MPOVAL,MPOVAI.MPOVAL,MPOVAJ.MPOVAL,MPOVACP.MPOVAL
      POINTEUR MPOUT1.MPOVAL,MPOUT2.MPOVAL,MPOUT3.MPOVAL,MPOUT4.MPOVAL
C
-INC CCOPTIO
-INC SMCHPOI
-INC SMCOORD
-INC SMELEME
-INC SMTABLE
      SEGMENT REDIR
        INTEGER IND1(NNGOT),IND2(NNGOT),IND3(NNGOT)
      ENDSEGMENT
C
C- Tableau d'adressage indirect
C
      SEGACT IPT1,IPT2,IPT3,IPT4
      SEGACT MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,MPOVA6,MPOVA7,MPOVA8,
     &       MPOVA9,MPOVAA,MPOVAB,MPOVAC,MPOVAD,MPOVAE,MPOVACP,MPOVAF,
     &       MPOVAI,MPOVAJ
      NNGOT  = XCOOR(/1)/(IDIM+1)
      SEGINI REDIR
      NBLAY  = IPT2.NUM(/2)
      DO 10 I=1,NBLAY
         K = IPT2.NUM(1,I)
         IND2(K) = I
 10   CONTINUE
      NBWAL  = IPT3.NUM(/2)
      DO 20 I=1,NBWAL
         K = IPT3.NUM(1,I)
         IND3(K) = I
 20   CONTINUE
      NBCEL  = IPT1.NUM(/2)
      DO 30 I=1,NBCEL
         K = IPT1.NUM(1,I)
         IND1(K) = I
 30   CONTINUE
      UST    = 1.D0/3.D0
      DST    = 2.D0/3.D0
C
C- Pour chaque couple CELL/LAYERW on calcule les grandeurs liees a la
C  condensation
C
      SEGINI, MPOUT1=MPOVA7
      N  = MPOVA7.VPOCHA(/1)
      NC = 1
      SEGINI MPOUT2,MPOUT3,MPOUT4
      N1 = IPT4.NUM(/2)
      DO 70 I=1,N1
C
C Coeff d'echange : correction pour les murs en beton
C Evaluation du debit de condensation
C
       IPOS = IND2(IPT4.NUM(2,I))
       IF (IPOS.NE.0) THEN
          II1    = IND1(IPT4.NUM(1,I))
          JPOS   = IND3(IPT4.NUM(2,I))
          PTOT1  = MPOVA1.VPOCHA(II1,1)
          XTGAS1 = MPOVA2.VPOCHA(II1,1)
          RTOT1  = MPOVA3.VPOCHA(II1,1)
          RVAP3  = MPOVA4.VPOCHA(II1,1)
          PVP1   = MPOVA5.VPOCHA(II1,1)
          XTSAT1 = MPOVA6.VPOCHA(II1,1)
          TMUR1  = MPOVA8.VPOCHA(JPOS,1)
          AREA1  = MPOVA9.VPOCHA(IPOS,1)
          QW2    = MPOVAA.VPOCHA(IPOS,1)
          XLBD1  = MPOVAB.VPOCHA(II1,1)
          XNU1   = MPOVAC.VPOCHA(II1,1)
          RINC1  = MPOVAD.VPOCHA(II1,1)
          DV2    = MPOVAE.VPOCHA(II1,1)
          RMEL   = MPOVAF.VPOCHA(II1,1)
          WNC    = MPOVAI.VPOCHA(II1,1)
          WVAP   = MPOVAJ.VPOCHA(II1,1)
          XCP    = MPOVACP.VPOCHA(II1,1)
C
          XAL    = XLBD1/(RTOT1*XCP)
          XMU    = RTOT1*XNU1
          PSATP1 = PSATT0(TMUR1)
          HL     = HLS0(PTOT1,TMUR1)
          HV0    = HVS0(PVP1,XTGAS1)
          HV     = HVS0(PSATP1,TMUR1)
          HLAT   = HV - HL
          HLAT0  = HV0 - HL
          DT1    = XTSAT1 - TMUR1
          DT2    = TMUR1 - XTGAS1
          ROITP1 =(PTOT1 - PSATP1) / (RINC1 * TMUR1)
          XVAPI  = PSATP1/PTOT1
          XINCI  = (PTOT1-PSATP1)/PTOT1
          WVAP   = 0.01801528D0
          WMEL   = XVAPI*WVAP+XINCI*WNC
          RMEL1  = 8.31446210D0 / WMEL
          rtot22 = rovap0(psatp1,tmur1) + roitp1
          RTOT2  = PTOT1 / (RMEL1 * TMUR1)
          ROITP2 = RTOT1 - RVAP3
          XNC    = ROITP2 / RTOT1
          XNCIT  = ROITP1 / RTOT22
C
          XUN   = (XNCIT - XNC) / 0.01D0
          XUN   = MAX(XUN,0.D0)
          XUN   = MIN(XUN,1.D0)
          XDEU  = (XTSAT1 - TMUR1) / 0.2D0
          XDEU  = MAX(XDEU,0.D0)
          XDEU  = MIN(XDEU,1.D0)
          FXUN  = (XUN**2.D0) * (3.D0 - 2.D0*XUN)
          FXDEU = (XDEU**2.D0) * (3.D0 - 2.D0*XDEU)
C
          XNCI = FXUN*XNCIT + (1.D0 - FXUN) * XNC
          RAPP = (1.D0 - XNC / XNCIT) * FXDEU
C
C Condition implicite de positivité de Hcond (plus d'incondensable en paroi)
          RAPP = MAX(0.D0,RAPP)
          THET = 0.8254D0 + 0.616D0*((PVP1-PSATP1)/(PTOT1-PSATP1))
          FACT1 = (-1.D0*DT2/XTGAS1)
          FACT2 = ((XNCI-XNC)/(((WNC)/(WNC-WVAP))-XNC))
          XBETA = ABS(FACT1 + FACT2)
          HCONV = THET*0.13D0*(((G1*XBETA)/(XNU1*XAL)) ** UST) * XLBD1
          HCONV = MIN(HCONV,HMAX)
C
          IF (DT2 .LT. -0.2D0) THEN
             HCOND  = THET*0.13D0*(((G1*XBETA)/(XNU1)) ** UST)
     &              *((RTOT1*(DV2**DST)*RAPP*HLAT0)/((-1.D0)*DT2))
             HCOND = MAX(HCOND,0.D0)
             DEBIC1 = -1.D0*(AREA1*DT2)*HCOND/HLAT0
             DEBIC1 = MAX(DEBIC1,0.D0)
             MPOUT1.VPOCHA(IPOS,1) = HCONV + HCOND
             QW2 = MPOUT1.VPOCHA(IPOS,1) * DT2
             MPOUT2.VPOCHA(IPOS,1) = DEBIC1 / QW2
          ELSE
             MPOUT1 . VPOCHA(IPOS,1) = HCONV
             MPOUT2 . VPOCHA(IPOS,1) = 0.D0
          ENDIF
          HTOT1 = MPOUT1.VPOCHA(IPOS,1)
          IF (HTOT1 . LT . MPOVA7.VPOCHA(IPOS,1)) THEN
             MPOUT1 . VPOCHA(IPOS,1) = MPOVA7.VPOCHA(IPOS,1)
             HCONV  = HCONV + MPOVA7.VPOCHA(IPOS,1) - HTOT1
          ENDIF
          MPOUT4 . VPOCHA(IPOS,1) = HCONV
          MPOUT3 . VPOCHA(IPOS,1) = HL
       ENDIF
  70  CONTINUE
C
C- Menage
C
      SEGSUP REDIR
      SEGDES IPT1,IPT2,IPT3,IPT4,MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,
     &       MPOVA6,MPOVA7,MPOVA8,MPOVA9,MPOVAA,MPOVAB,MPOVAC,MPOVAD,
     &       MPOVAE,MPOVAF,MPOVAI,MPOVAJ,MPOVACP
C
      RETURN
      END

