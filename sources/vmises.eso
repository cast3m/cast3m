C VMISES    SOURCE    CB215821  18/09/21    21:16:57     9930

C=======================================================================
C=                            V M I S E S                              =
C=                            -----------                              =
C=                                                                     =
C=  OPERATEUR CAST3M "VMISES" :                                        =
C=  ---------------------------                                        =
C=   CHAM1  =  'VMIS'  MODL1  SIG1  ( CAR1 ) ;                         =
C=                                                                     =
C=  Cet operateur sert a calculer les contraintes de Von Mises.        =
C=                                                                     =
C=  ARGUMENTS :                                                        =
C=  -----------                                                        =
C=   MODL1   (MMODEL)   Modele associe a la structure etudiee          =
C=   SIG1    (MCHAML)   Champ de contraintes                           =
C=   CAR1    (MCHAML)   Caracteristiques geometriques     (facultatif) =
C=                      Sous-type 'CARACTERISTIQUES'                   =
C=                                                                     =
C=  RESULTAT :                                                         =
C=  ----------                                                         =
C=   CHAM1   (MCHAML)   Contrainte equivalente de Von Mises            =
C=======================================================================

      SUBROUTINE VMISES

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO

C  1 - LECTURE DES ARGUMENTS DE L'OPERATEUR
C ==========================================
C  1.1 - Lecture OBLIGATOIRE du modele (IPMODL)
C =====
      CALL LIROBJ('MMODEL',IPMODL,1,iOK)
      IF (IERR.NE.0) RETURN
      CALL ACTOBJ('MMODEL',IPMODL,1)

C =====
C  1.2 - Lecture OBLIGATOIRE du champ de contraintes (IPCHA1)
C =====
      CALL LIROBJ('MCHAML',IPIN,1,iOK)
      IF (IERR.NE.0) RETURN
      CALL ACTOBJ('MCHAML',IPIN,1)
      CALL REDUAF(IPIN,IPMODL,IPCHA1,0,IR,KER)
      IF(IR   .NE. 1) CALL ERREUR(KER)
      IF(IERR .NE. 0) RETURN
C =====
C  1.3 - Lecture FACULTATIVE des caracteristiques geometriques (IPCHA2)
C =====
      CALL LIROBJ('MCHAML',IPIN,0,iOK)
      IF (IERR.NE.0) RETURN
      IPCHA2=0
      IF (iOK .EQ. 1) THEN
        CALL ACTOBJ('MCHAML',IPIN,1)
        CALL REDUAF(IPIN,IPMODL,IPCHA2,0,IR,KER)
        IF(IR   .NE. 1) CALL ERREUR(KER)
        IF(IERR .NE. 0) RETURN
      ENDIF
C =====
C  1.4 - Mise en ordre des MCHAML lus
C        1 -> IPCHE1 = Contraintes, 2 -> IPCHE2 = Carac. geom.
C =====
      IPCHE1=0
      IPCHE2=0
      if (ipcha2.ne.0) then
      CALL RNGCHA(IPCHA1,IPCHA2,'CONTRAINTES','CARACTERISTIQUES',
     .            IPCHE1,IPCHE2)
      else
       ipche1 = ipcha1
      endif
      IF (IERR.NE.0) RETURN
*  Si presence d'un champ de caracteristiques, l'autre est de contraintes
      if (ipche2.ne.0) ipche1=ipcha1+ipcha2-ipche2


C  2 - CALCUL DU MCHAML DE CONTRAINTE DE VON MISES
C =================================================
      iConv=0
      CALL VMISPO(IPMODL,IPCHE1,IPCHE2,iConv,IPCHE3,iRet)

C  3 - ECRITURE DU MCHAML RESULTAT
C =================================
      IF (iRet.EQ.1) CALL ECROBJ('MCHAML',IPCHE3)

      RETURN
      END




