C FIBAEL    SOURCE    CHAT      12/04/06    21:15:09     7348
C FIBFRA    SOURCE    DC        98/01/26
      SUBROUTINE FIBAEL(XMAT,DEPS,SIG0,VAR0,SIGF,VARF)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XMAT(*),DEPS(3),SIG0(3),VAR0(3),SIGF(3),VARF(3)
C----------------------------------------------------------------------
C
C     COMPORTEMENT DE FIBRE
C      MODELE BETON PLASTIQUE DU BAEL
C----------------------------------------------------------------------
C     Didier COMBESCURE - EMSI - Janvier 1998
C----------------------------------------------------------------------
C
C      XMAT( 1) YOUNG
C      XMAT( 2) NU
C
C      XMAT( 5) FC
C
C      DEPS(1) EPXX    SIG0(1) SIGF(1) SMXX
C      DEPS(2) GAXY    SIG0(2) SIGF(2) SMXY
C      DEPS(3) GAXZ    SIG0(3) SIGF(3) SMXZ
C
C                      VAR0(1) VARF(1) EPSE DEFORMATION PLASTIQUE
C                      VAR0(2) VARF(2) EPSO DEFORMATION TOTALE
C
C----------------------------------------------------------------------
       GAMMA=XMAT(1)/2.D0/(XMAT(2)+1.D0)
       EYOUN = XMAT( 1)
       FC = (-1.D0)*XMAT(5)
       XEPSLI = (2.D0)*(FC/EYOUN)
C
       XEPSE =  VAR0(1)
       XEPSO =  VAR0(2)
C
       XEPSOF = XEPSO + DEPS(1)
C
       IF (XEPSOF. GE . XEPSE) THEN
C
         XSIGF = 0.D0
         XTAN = 0.D0
C
       ELSE
C
         XSIGF1 = EYOUN * (XEPSOF - XEPSE)
C
         IF (XEPSOF . LT . XEPSLI) THEN
            XSIGF2 = FC
            XTAN = 0.D0
         ELSE
            XSIGF2 = FC*( ( (2.D0)*(XEPSOF/XEPSLI))
     &                   - ((XEPSOF/XEPSLI)**2) )
            XTAN =  FC*(  (2.D0/XEPSLI)
     &                   - (( 2.D0 * XEPSOF) /(XEPSLI**2) ) )
         ENDIF
C
         IF (XSIGF2 . GE . XSIGF1) THEN
           XSIGF = XSIGF2
           XEPSE = XEPSOF - (XSIGF/EYOUN)
         ELSE
           XSIGF = XSIGF1
           XTAN = EYOUN
         ENDIF
C
       ENDIF
C
C
       SIGF(1) = XSIGF
C
       SIGF(2) = SIG0(2) + GAMMA   * DEPS(2)
       SIGF(3) = SIG0(3) + GAMMA   * DEPS(3)
C
       VARF(1) = XEPSE
       VARF(2) = XEPSOF
       VARF(3) = XTAN
C
       RETURN
       END




