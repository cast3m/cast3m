C GORIEN    SOURCE    CHAT      05/01/13    00:19:04     5004
C
C
      INTEGER FUNCTION GORIEN(IT, N, C, IDIMC, ZERO )
C     *****************************************************************
C     OBJET : RENVOI L'ORIENTATION DE L'ELEMENT +1 SI POSITIF
C             -1 SI NEGATIF, 0 SI NUL
C     EN ENTREE :
C       IT      : NUMERO DES NOEUDS DE L'ELEMENT
C       N       : NOMBRE DE NOEUDS DE L'ELEMENT
C       C       : COORDONNEES DES NOEUDS
C       IDIMC    : DIMENSION DE L'ESPACE
C       ZERO    : SURFACE CONSIDEREE COMME NULLE
C     CONDITIONS D'APPLICATIONS : N'A PAS DE SENS SI LA DIMENSION DE
C                L'ELEMENT EST INFERIEURE A LA DIMENSION DE L'ESPACE
C                POUR ORIENTER UN QUADRANGLE IL SUFFIT DE DONNER LE
C                PREMIER TRIANGLE.
C     *****************************************************************
      IMPLICIT INTEGER(I-N)
      INTEGER IT(*),N,IDIMC
      REAL*8    C(*), ZERO
C
      DIMENSION X(12)
      INTEGER   I,J
      REAL*8      X,VX,VY,VZ,TAILLE
C
      TAILLE = 0.0
      GORIEN= 0
      DO 10 I=1,(N-1)
        DO 20 J=1,IDIMC
          X((I-1)*IDIMC+J) = C(((IT(I+1)-1)*IDIMC)+J)
     >                    - C(((IT(I)-1)*IDIMC)+J)
   20   CONTINUE
   10 CONTINUE
      IF( IDIMC .EQ. 3 )THEN
        VX = (X(2) * X(6)) - (X(5) * X(3))
        VY = (X(3) * X(4)) - (X(6) * X(1))
        VZ = (X(1) * X(5)) - (X(4) * X(2))
      ENDIF
C
C     CAS DU SEGMENT (1D, 2D, 3D)
C
      IF( N. EQ. 2 )THEN
        IF( IDIMC .EQ. 1 )THEN
           TAILLE = X(1)
        ENDIF
C
C     CAS DU TRIANGLE (2D, 3D)
C
      ELSE IF( N.EQ.3 ) THEN
        IF( IDIMC .EQ. 2 )THEN
          TAILLE = ((X(1) * X(4)) - (X(2) * X(3))) / 2.
        ENDIF
C
C     CAS DU TETRAEDRE (3D)
C
      ELSE IF( N. EQ. 4 )THEN
        TAILLE = ((VX * X(7)) + (VY * X(8)) + (VZ * X(9))) / 6.
      ENDIF
      IF( TAILLE .GT. ZERO )GORIEN= 1
      IF( TAILLE .LT. ZERO )GORIEN= -1
      END


