C BILINE    SOURCE    CHAT      05/01/12    21:40:08     5004
      SUBROUTINE BILINE(DELTAD,FY,STIFE,STIFP,RIG,FE0,FEP0,F,DELDPL)
C-----------------------------------------------------------------------
C
C SUBROUTINE FOR ELASTIC PLASTIC (WITH HARDENING) RELATIONSHIP.
C
C
C A PERFECT E.P. SCALAR MODEL WITH INITIAL STIFNESS STIFEQ AND
C YIELDING FYEQ IS ADDED IN PARALEL TO AN ELASTIC SCALAR MODEL
C OF STIFNESS STIFP.
C
C INPUT
C
C    DELTAD  DISPLACEMENT INCREMENT
C    FY      YIELDING FORCE (FY=STIFE*FYEQ/STIFEQ)
C    STIFE   ELASTIC STIFNESS (STIFE=STIFEQ+STIFP)
C    STIFP   PLASTIC STIFNESS
C    FE0     INITIAL FORCE IN THE ELASTIC MODEL
C    FEP0    INITIAL FORCE IN THE E.P. MODEL
C
C OUTPUT
C
C    RIG     FINAL STIFFNESS
C    F       FINAL FORCE
C    FE0     FINAL FORCE IN THE ELASTIC MODEL
C    FEP0    FINAL FORCE IN THE ELASTIC MODEL
C    DELDPL  PLASTIC DEFORMATION INCREMENT
C
C WARNING FE0 AND FEP0 SHOULD BE INITIALIZED !!
C
C-----------------------------------------------------------------------
C VERY OLD (CRAZY ?) STUFF SLIGHTLY MODIFIED FOR DELDPL
C AND TAKEN AS AN EXEMPLE OF "EXISTING" MODEL
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C
C  COMPUTATION OF THE STIFNESS AND YIELDING OF THE E.P. MODEL
C
      STIFEQ=STIFE-STIFP
      FYEQ=FY*STIFEQ/STIFE
C
C  ELASTIC PREDICTION OF THE FORCE IN THE E.P. MODEL ...
C
      FEP=FEP0+STIFEQ*DELTAD
C
C  ... AND POSSIBLE RETURN
C
      IF (ABS(FEP).GT.FYEQ)THEN
        FEP=SIGN(FYEQ,FEP)
        RIG=STIFP
        DELDPL=DELTAD-(FEP-FEP0)/STIFEQ
      ELSE
        RIG=STIFE
        DELDPL=0.D0
      ENDIF
      FEP0=FEP
C
C  FORCE IN THE ELASTIC MODEL
C
      FE=FE0+STIFP*DELTAD
      FE0=FE
C
C  TOTAL FORCE
C
      F=FE+FEP
      RETURN
      END


