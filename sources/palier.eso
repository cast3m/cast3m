C PALIER    SOURCE    BP208322  18/07/11    21:15:18     9879

**********************************************************************
*                                                                    *
* Sous-programme de calcul des efforts engendrés par un palier       *
* avec le modèle de palier court                                     *
* Benoit PRABEL, 29/04/2015                                          *
*                                                                    *
**********************************************************************

      SUBROUTINE PALIER(X,Y,VX,VY,XAD,YAD,VXAD,VYAD,FX,FY,PDT,ICAVIT,
     &     XLONG,VISCDY,RARBRE,XJEU,VITROT,NBMAIL,PALM,XPROG)
*
**********************************************************************
*
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
*
-INC CCOPTIO
-INC CCREEL
*
      REAL*8 X,Y,VX,VY,XAD,YAD,VXAD,VYAD,FX,FY,PDT,AMPLIT,XLONG,VISCDY,
     &     RARBRE,XJEU,VITROT,PALM
      REAL*8 EMAX,EMIN
      REAL*8 XPROG(2,NBMAIL)

      PARAMETER (AMPLIT=2.D0*XPI)
*     max d'excentrement admissible  et min pour detecter 0
      PARAMETER (EMAX=0.9999D0)
      PARAMETER (EMIN=1.0D-16)
*
**********************************************************************
*
*     coef de P0
      PCOEF = ((0.5D0*XLONG)**2) * 3.D0 * VISCDY
      PALMAD = PALM / PCOEF

* --- calcul de E, phi(COSPHI et SINPHI), VE et VPHI
*
      E     = SQRT(XAD**2 + YAD**2)
*    -jeu entierement consomme
      IF(E.GE.EMAX) THEN
        write(ioimp,*) 'Excentrement relatif=',E,' trop proche de 1 :',
     &                  'on le limite a ',EMAX
        E=EMAX
      ENDIF
*
* --- Boucle sur les angles et calcul de la pression
*
      DTETA=AMPLIT/DBLE(NBMAIL)
      FXAD = 0.D0
      FYAD = 0.D0

      DO 1 I=1,NBMAIL
*
* -----  Calcul de l'angle et epaisseur du film
c          TETA  = DBLE(I-1)*DTETA
c          COSTE = COS(TETA)
c          SINTE = SIN(TETA)
         COSTE = XPROG(1,I)
         SINTE = XPROG(2,I)
         H     = XJEU - X*COSTE - Y*SINTE

* -----  Calcul de variables auxiliaires
         H3     = H**3
         DHDTET =  X*SINTE -  Y*COSTE
         DHDT   = VX*COSTE + VY*SINTE
*
* -----  Calcul de la pression en Z=0
         P0  = ( 2.D0*DHDT - VITROT*DHDTET) / H3
         IF(PALM.NE.0.D0) P0 = PALMAD + P0
*        si condition de Gumbel on prends partie positive de p
         IF(ICAVIT.EQ.2) P0 = MAX(0.D0,P0)

* ----- On integre la force avec les trapezes
*       simplification car fonction periodique :
*       p0*cos(1)=p0*cos(NBMAIL+1)  et  idem pour p0*sin
        FXAD = FXAD + (P0 * COSTE)
        FYAD = FYAD + (P0 * SINTE)

 1    CONTINUE
*
*
* --- Calcul des efforts (valeur dimensionnee)
*
*     on multiplie par l integrale selon Z du profil parabolique
*     et le R du R dtheta
      ZCOEF = -2.D0*XLONG/3.D0 * DTETA * RARBRE

      FX = ZCOEF * PCOEF * FXAD
      FY = ZCOEF * PCOEF * FYAD
*
      RETURN
      END



