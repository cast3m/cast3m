C SIGPOR    SOURCE    CHAT      05/01/13    03:18:08     5004
      SUBROUTINE SIGPOR(COBMA,UNSRM,XGENE,NSTN,XDDL,IFOUR,NSTRS,
     .              XSTRS,LRB,LRN,LPP,MELE,IRTD,COBB,XKBB,IDECAP)
*----------------------------------------------------------------------
*
*  ON AJOUTE LES CONTRAINTES DUES A LA PRESSION POREUSE
*  N'EST VALABLE QUE POUR LES ELEMENTS POREUX
*  (JOINTS POREUX INCLUS)
*
*  ENTREE
*    COBMA            COEFFICIENTS DE BIOT
*    UNSRM            INVERSE DU MODULE DE BIOT
*    XGENE(NSTN,LRN)  MATRICE DES FONCTIONS DE FORME DE P
*    NSTN             NOMBRE DE LIGNES DE XGENE
*    XDDL(LRE)        D D L DE LA FORMULATION
*    IFOUR            VARIABLE IFOUR
*    NSTRS            NOMBRE DE CONTRAINTES
*    XSTRS(NSTRS)     CONTRAINTES  DHO*EPS1
*    LRB LRN          DIMENSIONS  ( VOIR RIGI1 )
*    MELE             NUMERO DE L ELEMENT FINI DANS NOMTP
*
*  SORTIE
*    XSTRS(NSTRS)     CONTRAINTES  DHO*EPS1  - COBMA * P
*                     ET P
*----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION XGENE(NSTN,*),XSTRS(*),XDDL(*),COBMA(*)
      DIMENSION XNTH(12,12),XNTB(12,12),XNTT(12),VP(3)
      DIMENSION COBB(IDECAP),XKBB(IDECAP,IDECAP)

*
*  CALCUL DE LA PRESSION
*
      P=0.D0
*
      IF(MELE.GE.108.AND.MELE.LE.110)THEN
*
        CALL INTDEL(XNTH,XNTB,XNTT,LRN,MELE)
*
        DO 1 J=1,LRN
          JJ=J+LRB
          P=P+XNTT(J)*XGENE(1,J)*XDDL(JJ)
 1      CONTINUE
*
        LHOOK=NSTRS-1
        SIGP=COBMA(LHOOK)*P
        XSTRS(LHOOK)=XSTRS(LHOOK) - SIGP
        XSTRS(NSTRS)=XSTRS(NSTRS) + P*UNSRM
*
      ELSE IF (MELE.GE.79.AND.MELE.LE.83) THEN
*
        DO 2 J=1,LRN
          JJ=J+LRB
          P=P+XGENE(1,J)*XDDL(JJ)
 2      CONTINUE
*
        LHOOK=NSTRS-1
        DO 20 IA=1,LHOOK
          SIGP=COBMA(IA)*P
          IF(IFOUR.EQ.-2.AND.IA.EQ.3)  SIGP=0.D0
          XSTRS(IA)=XSTRS(IA) - SIGP
 20     CONTINUE
        XSTRS(NSTRS)=XSTRS(NSTRS) + P*UNSRM
*
      ELSE IF (MELE.GE.173.AND.MELE.LE.182) THEN
*
        IE=LRB
        DO 33 IPR=1,IDECAP
          P=0.D0
          IPR1=(IPR-1)*LPP
          DO 3 ID=1,LPP
            IE=IE+1
            P=P+XGENE(IPR,ID+IPR1)*XDDL(IE)
 3        CONTINUE
*
          LHOOK=NSTRS-IDECAP
********* DO 30 IA=1,LHOOK
          DO 30 IA=1,3
            SIGP=COBB(IPR)*P
            IF(IFOUR.EQ.-2.AND.IA.EQ.3)  SIGP=0.D0
            XSTRS(IA)=XSTRS(IA) - SIGP
 30       CONTINUE
*
          DO 32 JPR=1,IDECAP
          XSTRS(NSTRS-IDECAP+JPR)=XSTRS(NSTRS-IDECAP+JPR) +
     &                     XKBB(JPR,IPR)*P
  32     CONTINUE

 33   CONTINUE

*
      END IF
*
*     ON FINIT PAR MSR0
*
      RETURN
*
  666 CONTINUE
      IRTD=1
      RETURN
      END




