C TIFCLO    SOURCE    KK2000    05/05/25    21:15:27     5088
      SUBROUTINE TIFCLO(RP,XL,LHOOK,DDHOOK,VROTL)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RAIDEUR CENTRIFUGE ELEMENTAIRE
C    D UNE POUTRE DE TIMOSHENKO DROITE MULTI-FIBRE EN AXES LOCAUX
C
C
C  ENTREE
C    XL      LONGUEUR
C    DDHOOK(LHOOK,LHOOK) MATRICE DE "HOOK"
C
C  SORTIE
C    RP      MATRICE DE RAIDEUR ELEMENTAIRE 12X12
C
C-----------------------------------------------------------------------
C     Didier Combescure 2003
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCREEL
      DIMENSION RP(12,12),VROTL(3),ROME(6,6)
      DIMENSION DDHOOK(LHOOK,LHOOK)
C
C  INITIALISATIONS
C
      DO 120 I=1,12
      DO 120 J=1,12
  120 RP(I,J)=0.D00
C
      RHOSD = DDHOOK( 1, 1)
      FAC=RHOSD*XL/6.D0
      FAC2=FAC*2.D0
C
      DO 121 I=1,6
      DO 121 J=1,6
  121 ROME(I,J)=0.D00
C
      ROME(1,1) = (-1.)*((VROTL(2)**2) + (VROTL(3)**2))
      ROME(2,2) = (-1.)*((VROTL(1)**2) + (VROTL(3)**2))
      ROME(3,3) = (-1.)*((VROTL(1)**2) + (VROTL(2)**2))
      ROME(1,2) = VROTL(1)*VROTL(2)
      ROME(1,3) = VROTL(1)*VROTL(3)
      ROME(2,3) = VROTL(2)*VROTL(3)
      ROME(2,1) = ROME(1,2)
      ROME(3,1) = ROME(1,3)
      ROME(3,2) = ROME(2,3)
C
      ROME(4,4) = ROME(1,1)
      ROME(4,5) = ROME(1,2)
      ROME(4,6) = ROME(1,3)
      ROME(5,4) = ROME(2,1)
      ROME(5,5) = ROME(2,2)
      ROME(5,6) = ROME(2,3)
      ROME(6,4) = ROME(3,1)
      ROME(6,5) = ROME(3,2)
      ROME(6,6) = ROME(3,3)
C
C
C  REMPLISSAGE DE LA MATRICE
C
      DO 201 I=1,3
        DO 201 J=1,3
        RP(I,J) =  DDHOOK( I, J)*ROME(I,J)*XL/3.D0
        RP(I+6,J) =DDHOOK( I, J)*ROME(I,J)*XL/6.D0
        RP(I,J+6) =DDHOOK( I, J)*ROME(I,J)*XL/6.D0
        RP(I+6,J+6) =DDHOOK( I, J)*ROME(I,J)*XL/3.D0
 201  CONTINUE
C
      RETURN
      END




