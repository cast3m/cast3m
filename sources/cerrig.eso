C CERRIG    SOURCE    CHAT      05/01/12    21:52:29     5004
      SUBROUTINE CERRIG(REL,LRE,ES,COORD,KERRE)
C-----------------------------------------------------------------------
C       ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE
C                      DE L ELEMENT CERCE
C-----------------------------------------------------------------------
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          ES      YOUN * SECT
C          COORD   COORDONNEES DU NOEUD
C
C  SORTIE
C          REL     MATRICE DE RIGIDITE ELEMENTAIRE
C          KERRE   0 SI PAS DE PB
C                  1 SI RAYON NUL
C                  2 CAS IMPOSSIBLE
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
-INC CCREEL
      DIMENSION REL(LRE,*),COORD(3,*)
*
      KERRE=0
      R = COORD(1,1)
      IF(ABS(R).LE.XPETIT) THEN
         KERRE=1
         RETURN
      ENDIF
*
      DO 1 I=1,LRE
         DO 2 J=1,LRE
            REL(I,J)=0.D0
 2       CONTINUE
 1    CONTINUE
*
      IF(IFOUR.EQ.0.OR.(IFOUR.EQ.1.AND.NIFOUR.EQ.0)) THEN
         FAC=2*XPI*ES/R
      ELSE
         FAC=XPI*ES/R
      ENDIF
      IF(IFOUR.EQ.0) THEN
         REL(1,1)=FAC
      ELSE IF(IFOUR.EQ.1) THEN
         AN= DBLE(NIFOUR)
         REL(1,1)=FAC
         REL(1,3)=FAC*AN
         REL(3,1)=REL(1,3)
         REL(3,3)=FAC*AN*AN
      ELSE
         KERRE=2
         RETURN
      ENDIF
      RETURN
      END







