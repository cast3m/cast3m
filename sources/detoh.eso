C DETOH     SOURCE    CHAT      05/01/12    22:44:24     5004
      SUBROUTINE DETOH(T,X,AN,AH2,AH2O,AN2,AO2,TINT,H,IINT,NBC,NBT)
C---------------------------------------------------------------------
C Calcul de l'enthalpie spécifique du mélange à la température T
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  T         : Température du mélange (K)
C E/  X         : Fraction molaire (-)
C E/  AN(NBC)   : Masse molaire des constituants (gramme)
C E/  AH2(NBT)  : Coeff du devt en T des propriétés de h2
C E/  AH2O(NBT) : Coeff du devt en T des propriétés de h2o
C E/  AN2(NBT)  : Coeff du devt en T des propriétés de n2
C E/  AO2(NBT)  : Coeff du devt en T des propriétés de o2
C E/  TINT      : Température seuil (K)
C  /S H         : Enthalpie spécifique du mélange (J/kg)
C E/  IINT      : Nombre de coeff nécessaire à T donnée
C E/  NBC       : Nombre de constituants du mélange
C E/  NBT       : Nombre de coeff total
C
C---------------------------------------------------------------------
C
C Langage : FORTRAN
C
C Auteurs : F.DABBENE et E.STUDER 02/99
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION X(*),AN(*),AH2(*),AH2O(*),AN2(*),AO2(*)
C
C- Décalage dans la table suivant la borne TINT
C
      IINTM2 = IINT - 2
      IINTM1 = IINT - 1
      II = 0
      IF (T.LT.TINT) II = IINT
C
C- Calcul de l'enthalpie spécifique de chaque constituant en J/mole
C
      HH2  = 0.D0
      HO2  = 0.D0
      HN2  = 0.D0
      HH2O = 0.D0
      DO 10 I = 1,IINTM2
        HH2  = HH2  + (AH2 (I+II) * (T**(I-1)) /I)
        HO2  = HO2  + (AO2 (I+II) * (T**(I-1)) /I)
        HN2  = HN2  + (AN2 (I+II) * (T**(I-1)) /I)
        HH2O = HH2O + (AH2O(I+II) * (T**(I-1)) /I)
 10   CONTINUE
      HH2  = HH2  + (AH2 (IINTM1+II) / T)
      HO2  = HO2  + (AO2 (IINTM1+II) / T)
      HN2  = HN2  + (AN2 (IINTM1+II) / T)
      HH2O = HH2O + (AH2O(IINTM1+II) / T)
C
      HH2  = HH2  * 8.314D0 * T
      HO2  = HO2  * 8.314D0 * T
      HN2  = HN2  * 8.314D0 * T
      HH2O = HH2O * 8.314D0 * T
C
C- Calcul de l'enthalpie spécifique du mélange en J/kg
C- (c'est pourquoi on divise par la masse molaire AM)
C
      H  = X(1)*HH2   + X(2)*HO2   + X(3)*HH2O  + X(4)*HN2
      AM =(X(1)*AN(1) + X(2)*AN(2) + X(3)*AN(3) + X(4)*AN(4))*1.D-3
      H  = H / AM
C
      RETURN
      END


