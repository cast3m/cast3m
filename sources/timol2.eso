C TIMOL2    SOURCE    CHAT      05/01/13    03:40:35     5004
C TIMOL2    SOURCE    IANIS     95/09/18    21:20:16     1823
      SUBROUTINE TIMOL2(V,CONT,XL,YOUN,CISAIL,SD,SREDY,XINZ,IDEUX)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES CONTRAINTES DE LA POUTRE DROITE EN 2D
C    D APRES TEDEL
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C    IDEUX   INDICE POUR DEFORMATIONS DU SECOND ORDRE
C  SORTIE
C    CONT    CONTRAINTES AU MILIEU 6 PAR NOEUD
C
C    DC 14/4/98 d'apres TIMOL2
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),V(*)
      DATA P5/0.5D0/
C
      RL=1.D00/XL
      RL2=RL*RL
      RL3= RL2*RL
C
C  SECTION "REDUITE"
C
      IF ( SREDY. NE.0. ) THEN
           SRY=SREDY
      ELSE
           SRY=SD
      ENDIF
C
C  DEFORMATIONS GENERALISEES
C
      EPS  = (V(4)-V(1))*RL
      EPTY = (V(5)-V(2))*RL - (V(3)+V(6))*P5
      XSIZ =-(V(3)-V(6))*RL
C
C  TERMES DU SECOND ORDRE pour la membrane
C
      IF(IDEUX.NE.0) THEN
      AUX1=0.5D0*(EPS**2+((V(5)-V(2))*RL)**2)
      EPS = EPS + AUX1
      ENDIF
C
C  CONTRAINTES GENERALISEES
C
      SIG  =YOUN*SD*    EPS
      TRANY=CISAIL*SRY* EPTY
      XMOMZ=YOUN*XINZ*  XSIZ
C
C  REMPLISSAGE DU TABLEAU CONT
C
      CONT(1)=SIG
      CONT(2)=TRANY
      CONT(3)=XMOMZ
      RETURN
      END



