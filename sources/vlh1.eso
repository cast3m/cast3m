C VLH1      SOURCE    CHAT      05/01/13    04:07:41     5004
      SUBROUTINE VLH1(H1,H1P,X,G)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  VLH1 (subroutine du groupe RIECOM.ESO)
C
C DESCRIPTION       :  Fonction H1 de SMOLLER (voir RIEMAN)
C
C LANGAGE           :  FORTRAN 77
C
C AUTEUR            :  P. GALON DRN/DMT/SEMT/TTMF
C
C************************************************************************
C
C APPELES           : aucun
C
C************************************************************************
C
C Entrées : X, G
C
C Sorties :    H1   FONCTION H1
C              H1P  FONCTION DERIVEE DE H1
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  créé le 1.10.96
C
C HISTORIQUE :  modifié par A. BECCANTINI le 7.1.98 pour
C               ajouter des commentaires et eliminer la
C               partié relative au cas X = 0.
C
C
C************************************************************************
C
C N.B.: Toutes le variables sont DECLAREES
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 H1, H1P, X, G, GM1, UNSGM1, T, ETX, AUX1, AUX2, B, EX
C
      GM1 = G-1.0D0
      UNSGM1 = 1.0D0 / GM1
      T = 0.5D0*GM1/G
      IF(X .GE. 0.D0) THEN
C
C******** X>0
C
         ETX = EXP(-T*X)
         H1 = 2.D0*UNSGM1*(1.D0-ETX)
         H1P = 2.D0*UNSGM1*T*ETX
C
C***** NB Function continue et derivable in X = 0
C
C      X=0
C
C      H1 = 0.0D0
C      H1P = 1.D0/G
C
      ELSE
C
C******** X<0
C
         B = (G+1.0D0)*UNSGM1
         EX = EXP(-X)
         AUX1 = 1.0D0 / SQRT(1.D0+B*EX)
         AUX2 = 2.0D0*SQRT(T)*UNSGM1*AUX1
         H1 = AUX2*(1.0D0-EX)
         H1P = AUX2*EX* ( 1.D0 + 0.5D0*(1.D0-EX)*B*AUX1*AUX1 )
      ENDIF
      RETURN
      END












