C DEVCOI    SOURCE    BP208322  18/01/05    21:15:17     9672
C DEVCOI    SOURCE    IANIS      93/2/22    21:12:00
      SUBROUTINE DEVCOI(Q1,Q2,PDT,NA1,XPTB,NPLB,XPHILB,NSB,NPLSB,
     &                  NA2,IDIMB,IBASB,IPLSB,INMSB,IORSB,IND,
     &                  IAROTA)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Opérateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Recombinaison des déplacements  et des vitesses                *
*     aux points de choc  pour calculer XPTB  pour le 1er pas        *
*                                                                    *
*                                                                    *
*     Paramètres:                                                    *
*                                                                    *
* e   Q1      Tableau des déplacements généralisés.                  *
* e   Q2      Tableau des vitesses generalises                       *
* e   NA1     Nombre total d'inconnues en base A.                    *
* s   XPTB    Tableau des déplacements des points sur base B.        *
* e   NPLB    Nombre total de points intervenant dans les liaisons.  *
* e   XPHILB  Tableau des vecteurs propres aux points de liaisons.   *
* e   NSB     Nombre de sous base.                                   *
* e   NPLSB   Nombre total de points intervenant dans les liaisons   *
*             dans une sous base.                                    *
* e   NA2     Nombre total d'inconnues dans une sous base.           *
* e   IDIMB   Nombre total de ddl retenus.                           *
* e   IBASB   Indique dans quelle sous base appartient le point de   *
*             liaison.                                               *
* e   INMSB   Indique le nombre d'inconnues de la sous base.         *
* e   IORSB   Donne l'indice du premier mode dans l'ensemble des     *
*             modes.                                                 *
* e   IPLSB   Dans une sous base, indique la position du point de    *
*             liaison.                                               *
* e   IAROTA  Indique la position des modes de rotation              *
* e   IND     Indice du pas.                                         *
* e   PDT     pas de temps                                           *
*                                                                    *
*     Auteur, date de création:                                      *
*                                                                    *
*     Ianis Politopoulos  22/02/93                                   *
*                                                                    *
*--------------------------------------------------------------------*
*
      INTEGER IBASB(*),INMSB(*),IORSB(*),IPLSB(*),IAROTA(*)
      REAL*8  XPHILB(NSB,NPLSB,NA2,*),Q1(NA1,*),Q2(NA1,*)
      REAL*8  XPTB(NPLB,4,*)
*
c       PDT = XDT(1)
      PDTS2 = 0.5D0 * PDT
      IND2 = IND + 1
      DO 10 IP = 1,NPLB
         ISB  = IBASB(IP)
         NA3  = INMSB(ISB)
         INA1 = IORSB(ISB) - 1
         IPLB = IPLSB(IP)
         IROT = IAROTA(ISB)
         DO 20 ID = 1,IDIMB
            XRET = 0.D0
            XRETV = 0.D0
            DO 30 IN = 1,NA3
               INN = INA1 + IN
               XRET = XRET + XPHILB(ISB,IPLB,IN,ID) * Q1(INN,IND)
               XRETV = XRETV + XPHILB(ISB,IPLB,IN,ID) * Q2(INN,IND)
 30            CONTINUE
*   Prise en compte des effets de rotation pour les corps rigides
            IF (IROT.NE.0) THEN
                XANGLE=Q1(IROT,IND)
                VITROT=Q2(IROT,IND)
* Déplacements dus à la rotation
                    XRET =XRET + XPHILB(ISB,IPLB,NA3+1,ID)*
     &(COS(XANGLE)-1)
                    XRET =XRET + XPHILB(ISB,IPLB,NA3+2,ID)* SIN(XANGLE)
* Vitesse due à la rotation
                    XRETV=XRETV+VITROT*(COS(XANGLE)*
     &XPHILB(ISB,IPLB,NA3+2,ID)-SIN(XANGLE)*XPHILB(ISB,IPLB,NA3+1,ID))
            ENDIF
*           end do
            XPTB(IP,IND,ID) = XRET
            XPTB(IP,IND2,ID) = XRET - (XRETV * PDTS2)
 20         CONTINUE
*        end do
 10      CONTINUE
*     end do
*
      END



