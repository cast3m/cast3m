C MANUC7    SOURCE    CB215821  18/09/10    21:16:07     9912

*------------------------------------------------------------------
*
*                 CREATION D'UN MCHAML
*
*------------------------------------------------------------------
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE
* -----------
*
*    IPMODL   (E) POINTEUR DE L'OBJET MODELE
*                 MODELE et SOUS-MODELE(S) ACTIFS EN ENTREE/SORTIE
*     MLMOTS  (E) POINTEUR SUR UN LISTMOTS CONTENANT LES NOMS
*                 ACTIF EN ENTREE/SORTIE
*     IPOI    (E) POINTEUR SUR UN LISTENTI OU UN LISTREEL
*     MONMOT  (E) MOT DE 8 CARACTERES
*     MLMOT3  (E) POINTEUR SUR UN LISTMOTS CONTENANT LES TYPES
*     MLMOT2  (E) POINTEUR SUR UN LISTMOTS CONTENANT LES TYPES
*                 DES CONSTITUANTS
*     LETYP   (E) TYPE DU MCHAML A CREER
*     JER1    (E) LONGUEUR DE LA CHAINE DE CARACTERES
*    ISUP1    (E) SUPPORT DEMANDE
*     ICHA    (S) POINTEUR SUR LE MCHAML RESULTAT
*
* LANGAGE:
* --------
*
*    ESOPE + FORTRAN77
*
************************************************************************

      SUBROUTINE MANUC7(IPMODL,MLMOTS,IPOI,MONMOT,MLMOT3,MLMOT2,
     &                  LETYP,JER1,ISUP1,ICHA,itart)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)

-INC CCOPTIO
-INC CCGEOME

-INC SMCHAML
-INC SMELEME
-INC SMLMOTS
-INC SMLREEL
-INC SMLENTI
-INC SMMODEL
-INC SMINTE

      SEGMENT MPTVAL
        INTEGER IPOS(NS)    ,NSOF(NS)
        INTEGER      IVAL(NCOSOU)
        CHARACTER*16 TYVAL(NCOSOU)
      ENDSEGMENT

      SEGMENT NOTYPE
        CHARACTER*16 TYPE(NBTYPE)
      ENDSEGMENT

      SEGMENT INFO
       INTEGER INFELL(JG)
      ENDSEGMENT
*
      PARAMETER ( N3 = 6 , NINF = 3 )

      CHARACTER*(*) MONMOT, LETYP

      CHARACTER*8 CHARIN
      CHARACTER*(NCONCH) CONM
      CHARACTER*4 CAR,CAR2

      DIMENSION INFOS(NINF)

      ICHA = 0
      ITHER= 0
      IDIFF= 0
      IMETA= 0
*
      MMODEL = IPMODL
**      SEGACT,MMODEL  <- Actif en E/S
      NSOUS = mmodel.KMODEL(/1)
*
* Determination du nombre de sous-modeles (sous-zones) a traiter :
      NSZ1 = NSOUS
      DO i = 1, NSOUS
        IMODEL = mmodel.KMODEL(i)
**        SEGACT,IMODEL*NOMOD  <- Actif en E/S
        IF (imodel.NEFMOD.EQ.22) NSZ1 = NSZ1 - 1
        IF (imodel.NEFMOD.EQ.259) NSZ1 = NSZ1 - 1
      ENDDO
*
*     INITIALISATION DU SEGMENT MCHELM
*
      N1 = NSZ1
      L1 = JER1
      SEGINI,MCHELM
      mchelm.TITCHE = LETYP
      mchelm.IFOCHE = IFOUR
*
**      SEGACT,MLMOTS  <- Actif en E/S
      N2 = mlmots.MOTS(/2)

      SEGACT,MLMOT3,MLMOT2
      IF (MONMOT.EQ.'REAL*8  ') THEN
        MLREEL = IPOI
        SEGACT,MLREEL
      ELSE
        MLENTI = IPOI
        SEGACT,MLENTI
      ENDIF

      INFOS(1) = 0
      INFOS(2) = 0
      INFOS(3) = NIFOUR
*
*     BOUCLE SUR LES ZONES ELEMENTAIRES DU MODELE
*
      kch = 0
      DO 20 isous = 1, NSOUS
*
        IMODEL = mmodel.KMODEL(isous)
C
C     ON RECUPERE L INFORMATION GENERALE
C
        IPMAIL =  imodel.IMAMOD
        CONM   =  imodel.CONMOD
C____________________________________________________________________
C
C     INFORMATION SUR L'ELEMENT FINI
C____________________________________________________________________
C
        MELE = imodel.NEFMOD
        IF (MELE.EQ.22) GOTO 20
        IF (MELE.EQ.259) GOTO 20
*
        NFOR = imodel.FORMOD(/2)
        CALL PLACE(imodel.FORMOD,NFOR,ITHER,'THERMIQUE')
        CALL PLACE(imodel.FORMOD,NFOR,IDIFF,'DIFFUSION')
        CALL PLACE(imodel.FORMOD,NFOR,IMETA,'METALLURGIE')
        CALL PLACE(imodel.FORMOD,NFOR,ICONT,'CONTACT')
        CALL PLACE(imodel.FORMOD,NFOR,IDARC,'DARCY')
C
C      CAS DE LA FORMULATION DARCY ON VA EXTRAIRE LE MAILLAGE SOMMET
C
        IF (IDARC.NE.0)THEN
          CALL LEKMOD(MMODEL,IPTABL,INEFMD)
          CHARIN = 'MAILLAGE'
          CALL LEKTAB(IPTABL,CHARIN, IOBRE)
          IF (IERR.NE.0) RETURN
C* Inutile de reactiver le modele suite a LEKMOD :
          IPT1  = IOBRE
          IPMAIL= IOBRE
c???      IF (NSZ1.GT.1) THEN
          IF (NSOUS.GT.1) THEN
            SEGACT IPT1
            IPMAIL = IPT1.LISOUS(isous)
C            SEGDES IPT1
          ENDIF
        ENDIF
C Fin du cas special DARCY
*
        IPPORE = 0
        IF (MELE.GE.79.AND.MELE.LE.83) IPPORE = NBNNE(NUMGEO(MELE))

        ISUP = ISUP1
*
* EN CAS DE FORMULATION CONTACT, SEUL SUPPORT = LES NOEUDS
* AM  25/1/08 ON FORCE LE SUPPORT EN CONSEQUENCE AU LIEU DE SORTIR EN ERREUR
        IF (ICONT.NE.0 .AND. ISUP.NE.1) ISUP = 1
*
        IPMIN = 0
C        MFR   = 0
        info  = 0
        IF (ISUP.NE.1) THEN
          IF (ITHER.EQ.0 .AND. IDIFF.EQ.0 .AND. IMETA.EQ.0) THEN
            IF (2+ISUP.GT.infmod(/1)) THEN
              CALL ELQUOI(MELE,0,ISUP,IPINF,IMODEL)
              IF (IERR.NE.0) GOTO 99
              info  = IPINF
              IPMIN = info.INFELL(11)
C              MFR   = info.INFELL(13)
              SEGSUP,info
            else
              IPMIN = infmod(ISUP+2)
C              MFR   = INFELE(13)
            endif
          ELSE
c en thermique,diffusion ou metallurgie on veut les points de gauss ad hoc
            ISUP = 6
            iplr = 0
            iplc = 0
            nmat = imodel.matmod(/2)
            CALL PLACE(imodel.matmod,nmat,iplr,'RAYONNEMENT')
            CALL PLACE(imodel.matmod,nmat,iplc,'CONVECTION')
            IF (iplr+iplc.eq.0) THEN
              CALL TSHAPE(MELE,'GAUSS',IPMIN)
            ELSE
              NLG = NUMGEO(MELE)
              CALL TSHAPE(NLG,'GAUSS',IPMIN)
            ENDIF
          ENDIF
        ENDIF
        MINTE = IPMIN
        IF (IPMIN.NE.0) SEGACT,MINTE

        kch = kch+1
        IMACHE(kch)   = IPMAIL
        CONCHE(kch)   = CONMOD
        INFCHE(kch,1) = 0
        INFCHE(kch,2) = 0
        INFCHE(kch,3) = NIFOUR
        INFCHE(kch,4) = IPMIN
        INFCHE(kch,5) = 0
        INFCHE(kch,6) = ISUP

        SEGINI,MCHAML
        ICHAML(kch) = MCHAML
*
        N1PTEL = 0
        N1EL   = 0
        N2PTEL = 0
        N2EL   = 0

        IF (MONMOT.EQ.'REAL*8  ') THEN
          N1PTEL = 1
          N1EL   = 1
          DO in = 1, N2
            SEGINI,MELVAL
            melval.VELCHE(N1PTEL,N1EL) = mlreel.PROG(in)
            SEGDES,MELVAL
            mchaml.NOMCHE(in) = mlmots.MOTS(in)
            mchaml.TYPCHE(in) = MONMOT(1:6)
            mchaml.IELVAL(in) = MELVAL
          ENDDO

        ELSE

          DO 10 in = 1, N2
            mchaml.NOMCHE(in) = mlmots.MOTS(in)
            CAR  = MLMOT3.MOTS(in)(1:4)
            CAR2 = MLMOT2.MOTS(in)(1:4)
*
*              TRAITEMENT PARTICULIER POUR LE TYPE 'MCHAML'
*---------------
            IF (CAR.EQ.'MCHA') THEN
*
*              MODIF 02/94 POUR POUTRE A FIBRES
*              TEST SUR LES MAILLAGES POINTES
*
              IPCHE1 = mlenti.LECT(in)
              MCHEL1 = IPCHE1
              SEGACT,MCHEL1
              NSOU1 = MCHEL1.ICHAML(/1)
              IDEM = 0
              DO 294 i = 1, NSOU1
                IF (IPMAIL.EQ.MCHEL1.IMACHE(i)) IDEM = 1
 294          CONTINUE
              SEGDES,MCHEL1
              IF (IDEM.EQ.0) GO TO 295
*
              CALL QUESUP(IPMODL,IPCHE1,ISUP,0,IRET1,IRET2)
              IF (IRET1.GT.1) THEN
                SEGSUP MCHAML
                GOTO 99
              ENDIF
* On reactive le modele suite a APPEL a QUESUP !
              MMODEL = IPMODL
              SEGACT,MMODEL
              DO i = 1, NSOUS
                imode1 = mmodel.KMODEL(i)
                SEGACT,imode1
              ENDDO

              NBROBL = 1
              NBRFAC = 0
              SEGINI NOMID
              MOTAUX = NOMID
              LESOBL(1)= mchaml.NOMCHE(in)
              NBTYPE = 1
              SEGINI,NOTYPE
              MOTYPE = NOTYPE
              TYPE(1) = ' '
              CALL KOMCHA(IPCHE1,IPMAIL,CONM,MOTAUX,MOTYPE,
     $                    2,INFOS,3,IVAAUX)
              SEGSUP,NOTYPE
              IF (IERR.NE.0)THEN
                SEGSUP MCHAML
                GOTO 99
              ENDIF
              IF (IRET1.EQ.1) THEN
                CALL VALCHE(IVAAUX,1,IPMIN,IPPORE,MOTAUX,MELE)
                IF (IERR.NE.0) THEN
                  MPTVAL = IVAAUX
                  MELVA1 = IVAL(1)
                  SEGDES MELVA1
                  SEGSUP MPTVAL
                  SEGSUP MCHAML
                  GOTO 99
                ENDIF
              ENDIF
              SEGSUP,NOMID
              MPTVAL = IVAAUX
              mchaml.TYPCHE(in) = TYVAL(1)
              MELVA1 = IVAL(1)
              SEGINI,MELVAL=MELVA1
              IELVAL(IN) = MELVAL
              IF (IRET1.EQ.1)THEN
                SEGSUP MELVA1
              ELSE
                SEGDES,MELVA1
              ENDIF
              SEGSUP,MPTVAL
              SEGDES,MELVAL
              GOTO 10
 295          CONTINUE
            ENDIF
*
            IF (itart.EQ.1 .AND. CAR.EQ.'LIST'
     $                     .AND. CAR2.EQ.'REEL') THEN
              mchaml.TYPCHE(IN) = 'REAL*8  '
              ipt4 = ipmail
              segact,ipt4
              N1EL = ipt4.num(/2)
              segdes,ipt4
              N1PTEL = 1
              N2PTEL = 0
              N2EL   = 0
              SEGINI,MELVAL
              mlree2 = mlenti.lect(in)
              segact,mlree2
              jg2 = mlree2.prog(/1)
              ia = 0
              do i = 1, n1el
                ia = ia+1
                IF (ia.GT.jg2) ia=1
                melval.velche(N1PTEL,i) = mlree2.prog(ia)
              enddo
              segdes mlree2
            ELSE
              mchaml.TYPCHE(IN) = 'POINTEUR'//car//car2
              N1PTEL = 0
              N1EL   = 0
              mlent2 = mlenti.lect(in)
              if (ITART.EQ.1.AND.car2(1:4).eq.'INT ') then
                ipt4 = ipmail
                SEGACT,ipt4
                N2EL = ipt4.num(/2)
                SEGDES,ipt4
                N2PTEL = 1
                SEGINI,MELVAL
                segact,mlent2
                jg2 = mlent2.lect(/1)
                ia = 0
                do i = 1, n2el
                  ia = ia+1
                  IF (ia.GT.jg2) ia=1
                  melval.ielche(N2PTEL,i) = mlent2.lect(ia)
                enddo
                segdes,mlent2
              else
                N2PTEL = 1
                N2EL   = 1
                SEGINI,MELVAL
                melval.ielche(1,1) = mlent2
              endif
            ENDIF
            SEGDES,MELVAL
            mchaml.IELVAL(IN) = MELVAL

 10       CONTINUE
*         ENDDO
*
        ENDIF
*
        IF (IPMIN.NE.0) SEGDES,MINTE
        SEGDES,MCHAML

 20   CONTINUE
*     ENDDO

 99   CONTINUE
      IF (MONMOT.EQ.'REAL*8  ') THEN
        SEGDES,MLREEL
      ELSE
        SEGDES,MLENTI
      ENDIF
      SEGDES,MLMOT3,MLMOT2
*
      IF (IERR.EQ.0)THEN
        SEGDES,MCHELM
      ELSE
        SEGSUP,MCHELM
        MCHELM = 0
      ENDIF
      ICHA = MCHELM
*
      RETURN
      END

