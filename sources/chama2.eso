C CHAMA2    SOURCE    FANDEUR   10/12/17    21:16:02     6427

C=======================================================================
C=                            C H A M A 2                              =
C=                            -----------                              =
C=                                                                     =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=   Calcul du MCHAML (champ par element aux noeuds) des flux nodaux   =
C=   equivalents a des sources volumiques. Cas BIDIMENSIONNEL MASSIF   =
C=                                                                     =
C=  Parametres :  (E)=Entree  (S)=Sortie                               =
C=  ------------                                                       =
C=   IPSONO   (E)   Pointeur sur le segment MELVAL des SOURCES         =
C=   IPGEOM   (E)   Pointeur sur un objet MAILLAGE elementaire         =
C=   IPINTE   (E)   Pointeur sur un segment MINTE contenant les        =
C=                  caracteristiques d'integration                     =
C=   IPCHEQ   (S)   Pointeur sur un segment MELVAL contenant les       =
C=                  flux de chaleur nodaux equivalents                 =
C=                                                                     =
C=  Variables locales :                                                =
C=  -------------------                                                =
C=   XEL(3,NBPTEL)  Coordonnees GLOBALES des noeuds d'un element       =
C=   Surf           Surface elementaire au point de Gauss              =
C=   AEL            Matrice de travail                                 =
C=                                                                     =
C=  Denis ROBERT, le 16 fevrier 1988.                                  =
C=======================================================================

      SUBROUTINE CHAMA2 (IPSONO,IPGEOM,IPINTE, IPCHEQ)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO
-INC CCREEL

-INC SMCHAML
-INC SMELEME
-INC SMINTE
-INC SMCOORD

      SEGMENT MMAT1
        REAL*8 XEL(3,NBPTEL), AEL(NBPTEL,NBPTEL)
      ENDSEGMENT

      PARAMETER (X2Pi=6.283185307179586476925286766559D0)

C  1 - INITIALISATION
C ====================
C  1.1 - Recuperation d'informations sur le maillage elementaire
C =====
      MELEME=IPGEOM
      SEGACT,MELEME
      NBPTEL=NUM(/1)
      NbElt=NUM(/2)
C =====
C  1.2 - Recuperation d'informations sur l'element fini du maillage
C =====
      MINTE=IPINTE
      SEGACT,MINTE
      NBPGAU=POIGAU(/1)
      NBNO=SHPTOT(/2)
C =====
C  1.3 - Activation du MCHAML contenant les valeurs des sources
C =====
      MELVA1=IPSONO
      SEGACT,MELVA1
      NBPTE1=MELVA1.VELCHE(/1)
      NEL1=MELVA1.VELCHE(/2)
C =====
C  1.4 - Initialisation du segment de travail MMAT1
C =====
      SEGINI,MMAT1
C =====
C  1.5 - Initialisation du segment resultat MELVAL contenant les valeurs
C        des flux nodaux pour chaque element du maillage (IPGEOM)
C =====
      N1PTEL=NBPTEL
      N1EL=NbElt
      N2PTEL=0
      N2EL=0
      SEGINI,MELVAL
      IPCHEQ=MELVAL

C  2 - BOUCLE SUR LES ELEMENTS DU MAILLAGE ELEMENTAIRE IMAMOD
C ============================================================
      DO iElt=1,NbElt
C =====
C  2.1 - Mise a zero de la matrice symetrique AEL(i,j)=Forme(i).Forme(j)
C =====
        CALL ZERO(AEL,NBPTEL,NBPTEL)
C =====
C  2.2 - Recuperation des coordonnees GLOBALES des noeuds de l'element
C =====
        CALL DOXE(XCOOR,IDIM,NBPTEL,NUM,iElt,XEL)
C =====
C  2.3 - Boucle sur les points de Gauss de l'element iElt
C =====
        DO iGau=1,NBPGAU
C =======
C  2.4.1 - Calcul de la surface associee au point de Gauss iGau
C          Pour les elements MASSIFs, traitement du cas axisymetrique
C =======
          S1=XZERO
          S2=XZERO
          S3=XZERO
          S4=XZERO
          S5=XZERO
          S6=XZERO
          DO i=1,NBPTEL
            S1=S1+SHPTOT(2,i,iGau)*XEL(2,i)
            S2=S2+SHPTOT(3,i,iGau)*XEL(3,i)
            S3=S3+SHPTOT(3,i,iGau)*XEL(2,i)
            S4=S4+SHPTOT(2,i,iGau)*XEL(3,i)
            S5=S5+SHPTOT(3,i,iGau)*XEL(1,i)
            S6=S6+SHPTOT(2,i,iGau)*XEL(1,i)
          ENDDO
          SurfX=S1*S2-S3*S4
          SurfY=S4*S5-S2*S6
          SurfZ=S6*S3-S5*S1
          Surf=SQRT(SurfX*SurfX+SurfY*SurfY+SurfZ*SurfZ)
          IF (IFOMOD.EQ.0) THEN
            CALL DISTRR(XEL,SHPTOT(1,1,iGau),NBPTEL,RR)
            Surf=X2Pi*RR*Surf
          ENDIF
          Surf=Surf*POIGAU(iGau)
C =======
C  2.3.2 - Calcul de la contribution du point de Gauss a la matrice AEL
C =======
          DO i=1,NBPTEL
            ZZ=SHPTOT(1,i,iGau)*Surf
            DO j=1,NBPTEL
              AEL(i,j)=AEL(i,j)+SHPTOT(1,j,iGau)*ZZ
            ENDDO
          ENDDO
        ENDDO
C =====
C  2.4 - Calcul des flux equivalents sur cet element (MCHAML aux noeuds)
C =====
        IEMIN=MIN(NEL1,iElt)
        DO i=1,NBPTEL
          ZZ=XZERO
          DO j=1,NBPTEL
            k=MIN(NBPTE1,j)
            ZZ=ZZ+MELVA1.VELCHE(k,IEMIN)*AEL(j,i)
          ENDDO
          VELCHE(i,iElt)=ZZ
        ENDDO
      ENDDO

C  3 - MENAGE : DESACTIVATION/DESTRUCTION DE SEGMENTS
C ====================================================
      SEGSUP,MMAT1
      SEGDES,MELEME,MELVA1,MINTE,MELVAL

      RETURN
      END


