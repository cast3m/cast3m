C VONEPS    SOURCE    CHAT      05/01/13    04:09:30     5004
       FUNCTION  VONEPS(EPSIL,ICLE,ALFAH,COMIS)
C
C CALCUL DE LA DEFORMATION DE VON MISES
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EPSIL(*)
      DIMENSION COMIS(6)
C****
C
C       ICLE=1   CAS DES ELEMENTS MASSIFS
C       ICLE=2   CAS DES COQUES
C       ICLE=3   CAS DES MEMBRANES
C       ICLE=4   CAS DES CABLES
C       ICLE=5   CAS QUELCONQUE
C       ICLE=6   CAS DES CONTRAINTES PLANES
C       ICLE=7   CAS DES COQUES A NU=0. OU CONTRAINTES PLANES
C       ICLE=8   CAS DES MEMBRANES A NU=0. OU CONTRAINTES PLANES
C       ICLE=9   CAS DES COQUES MASSIFS
C       ICLE=10  CAS DES JOINTS
C       ICLE=11  CAS DES POUTRES
C       ICLE=12  CAS DES TUYAUX
C
C****
C
C       ALFAH  COEFFICIENT MULTIPLICATEUR DE LA FLEXION DANS LES COQUES
C       COMIS  COEFFICIENT DES CONTRAINTES DANS LE VON MISES
C
CCCCCCCCCCCCCCCCCCCC
      COEFEP=SQRT(0.666666666666666666666667D0)
      UNALF=0.D0
      IF(ALFAH.GE.1.D-12) UNALF=1.D0/ALFAH
C****
      GO TO (1,2,3,4,5,6,7,8,9,10,11,12),ICLE
    1 CONTINUE
      VONEPS=
     . SQRT(EPSIL(1)*EPSIL(1)+EPSIL(2)*EPSIL(2)+EPSIL(3)*EPSIL(3) +
     . 0.5D0*(EPSIL(4)*EPSIL(4)+EPSIL(5)*EPSIL(5)+EPSIL(6)*EPSIL(6)))
      GO TO 999
    2 VONEPS= SQRT((EPSIL(4)*EPSIL(4)+EPSIL(5)*EPSIL(5) +
     .  0.5D0*EPSIL(6)*EPSIL(6))*UNALF+EPSIL(1)*EPSIL(1)+
     .  EPSIL(2)*EPSIL(2) + 0.5D0*EPSIL(3)*EPSIL(3))
      GO TO 999
    3 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+EPSIL(2)*EPSIL(2)
     . +0.5D0*EPSIL(3)*EPSIL(3))
      GO TO 999
    4 VONEPS=ABS(EPSIL(3))
      GO TO 999
    5 VONEPS=SQRT(0.5*((COMIS(2)+COMIS(3))*EPSIL(1)*EPSIL(1)
     .+(COMIS(1)+COMIS(3))*EPSIL(2)*EPSIL(2)+(COMIS(1)+COMIS(2))*
     . EPSIL(3)*EPSIL(3))
     . -COMIS(1)*EPSIL(2)*EPSIL(3)-COMIS(2)*EPSIL(1)*EPSIL(3)-COMIS(3)*
     . EPSIL(1)*EPSIL(2)+0.5*COMIS(4)*EPSIL(4)*EPSIL(4)+COMIS(5)*0.5*
     . EPSIL(5)*EPSIL(5)+0.5*COMIS(6)*EPSIL(6)*EPSIL(6)   )
      GO TO 999
    6 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+EPSIL(2)*EPSIL(2)+EPSIL(3)*EPSIL(3)
     . +0.5D0*EPSIL(4)*EPSIL(4))
      GO TO 999
    7 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+UNALF*EPSIL(4)*EPSIL(4))
      GO TO 999
    8 VONEPS=ABS(EPSIL(1))
      GO TO 999
C
C***  ELEMENT COQUE EPAISSE
C
    9 VONEPS=SQRT((EPSIL(2)*EPSIL(2)+EPSIL(3)*EPSIL(3)
     .             +0.5D0*(EPSIL(4)*EPSIL(4)+EPSIL(5)*EPSIL(5)+
     .             EPSIL(6)*EPSIL(6)))*UNALF+EPSIL(8)*EPSIL(8)+
     .             EPSIL(9)*EPSIL(9) +0.5D0*(EPSIL(10)*
     .             EPSIL(10)+EPSIL(11)*EPSIL(11)+EPSIL(12)*EPSIL(12)))
      GO TO 999
   10 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+EPSIL(3)*EPSIL(3))
     .      +COMIS(1)*EPSIL(2) + COMIS(2)
      GO TO 999
C
C  POUTRES  FORMULATION GLOBALE
C  ATTENTION  VERIFIER LE COEFEP
C
   11 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+
     .             EPSIL(5)*EPSIL(5)+
     .             EPSIL(6)*EPSIL(6)+
     .             EPSIL(4)+EPSIL(4))
      GO TO 999
C
C  TUYAUX
C
   12 VONEPS=SQRT(EPSIL(1)*EPSIL(1)+
     .             EPSIL(5)*EPSIL(5)+
     .             EPSIL(6)*EPSIL(6)+
     .             EPSIL(4)+EPSIL(4))
  999 VONEPS=VONEPS*COEFEP
      RETURN
      END

