C SHAPE3    SOURCE    CHAT      05/01/13    03:15:24     5004
      SUBROUTINE SHAPE3(QSI,ETA,DZE,IELE,SHP,IRET)
C=======================================================================
C
C     FONCTIONS DE FORME TRIDIMENSIONNELLES
C        EBERSOLT
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION SHP(6,*)
      DATA XZER/0.D0/,UNDEMI/.5D0/,UN/1.D0/,DEUX/2.D0/,QUATRE/4.D0/
      DATA CINQ/5.D0/,HUIT/8.D0/,TROIS/3.D0/
      DATA UNQUA/.25D0/
C=======================================================================
C     QSI ETA DZE COORDONNEES REDUITES DU POINT DE GAUSS
C
C     SHP FONCTIONS DE FORME SHP(1,INO) FONCTIONS DE FORME DU ND INO
C                            SHP(2,INO) DERIVEES PR RPPRT A QSI
C                            SHP(3,INO) DERIVEES PR RPPRT A ETA
C                            SHP(4,INO) DERIVEES PR RPPRT A DZE
C                            SHP(5,INO) DERIVEES PR RPPRT A  4
C                              DERIVEES BARYCENTRIQUE
C     CAS DE DIMENSION 3
C               4 OU 10 NOEUDS  TETRAEDRE
C               5 OU 13 NOEUDS  PYRAMIDE
C               6 OU 15 NOEUDS  PRISME
C               8 OU 20 NOEUDS  CUBE
C=======================================================================
C
      IRET=1
C           P1 S2 S3 T3 T4 T6 T7 Q4 Q5 Q8 Q9 R2 R3 C8 C20 P6 P15
      GOTO (66,66,66,66,66,66,66,66,66,66,66,66,66,14, 15,16, 17,
C        L3 L4 L6 L8 MU T4 T10 P5 P13
     1   66,66,66,66,66,23, 24,25, 30) IELE
C
  66  IRET=0
      GOTO 666
C
  16  CONTINUE
C
C     FONCTIONS DE FORME PRISMATIQUES  A 6 NOEUDS
C
      AUX=UN-QSI-ETA
      AUX1=(UN-DZE)/DEUX
      AUX2=(UN+DZE)/DEUX
      SHP(1,1)=AUX*AUX1
      SHP(1,2)=QSI*AUX1
      SHP(1,3)=ETA*AUX1
      SHP(2,1)=-AUX1
      SHP(2,2)=AUX1
      SHP(2,3)=XZER
      SHP(3,1)=-AUX1
      SHP(3,2)=XZER
      SHP(3,3)=AUX1
      SHP(4,1)=-AUX/DEUX
      SHP(4,2)=-QSI/DEUX
      SHP(4,3)=-ETA/DEUX
      SHP(1,4)=AUX*AUX2
      SHP(1,5)=QSI*AUX2
      SHP(1,6)=ETA*AUX2
      SHP(2,4)=-AUX2
      SHP(2,5)=AUX2
      SHP(2,6)=XZER
      SHP(3,4)=-AUX2
      SHP(3,5)=XZER
      SHP(3,6)=AUX2
      SHP(4,4)=AUX/DEUX
      SHP(4,5)=QSI/DEUX
      SHP(4,6)=ETA/DEUX
      GOTO 666
  17  CONTINUE
C
C     FONCTION DE FORME PRISMATIQUES A 15 NOEUDS
C
      AUX=UN-QSI-ETA
      DZEM=(UN-DZE)
      DZEP=(UN+DZE)
      PAUX =QSI-UNDEMI*DZE-UN
      PAUX1=ETA-UNDEMI*DZE-UN
      PAUX2=QSI+ETA+UNDEMI*DZE
      PAUX3=QSI+UNDEMI*DZE-UN
      PAUX4=ETA+UNDEMI*DZE-UN
      PAUX5=QSI+ETA-UNDEMI*DZE
      SHP(1,1)=-AUX*DZEM*PAUX2
      SHP(1,2)=DEUX*QSI*AUX*DZEM
      SHP(1,3)=QSI*DZEM*PAUX
      SHP(2,1)=(PAUX2-AUX)*DZEM
      SHP(2,2)=DEUX*(AUX-QSI)*DZEM
      SHP(2,3)=(PAUX+QSI)*DZEM
      SHP(3,1)=(PAUX2-AUX)*DZEM
      SHP(3,2)=-DEUX*QSI*DZEM
      SHP(3,3)=XZER
      SHP(4,1)=AUX*(PAUX2-UNDEMI*DZEM)
      SHP(4,2)=-DEUX*QSI*AUX
      SHP(4,3)=-QSI*(UNDEMI*DZEM+PAUX)
      SHP(1,4)=DEUX*QSI*ETA*DZEM
      SHP(1,5)=ETA*DZEM*PAUX1
      SHP(1,6)=DEUX*ETA*AUX*DZEM
      SHP(2,4)=DEUX*ETA*DZEM
      SHP(2,5)=XZER
      SHP(2,6)=-DEUX*ETA*DZEM
      SHP(3,4)=DEUX*QSI*DZEM
      SHP(3,5)=(ETA+PAUX1)*DZEM
      SHP(3,6)=DEUX*(AUX-ETA)*DZEM
      SHP(4,4)=-DEUX*QSI*ETA
      SHP(4,5)=-ETA*(PAUX1+UNDEMI*DZEM)
      SHP(4,6)=-DEUX*ETA*AUX
      SHP(1,7)=AUX*DZEM*DZEP
      SHP(1,8)=QSI*DZEM*DZEP
      SHP(1,9)=ETA*DZEM*DZEP
      SHP(2,7)=-DZEM*DZEP
      SHP(2,8)=DZEM*DZEP
      SHP(2,9)=XZER
      SHP(3,7)=-DZEM*DZEP
      SHP(3,8)=XZER
      SHP(3,9)=DZEM*DZEP
      SHP(4,7)=-DEUX*DZE*AUX
      SHP(4,8)=-DEUX*DZE*QSI
      SHP(4,9)=-DEUX*DZE*ETA
      SHP(1,10)=-AUX*DZEP*PAUX5
      SHP(1,11)=DEUX*QSI*AUX*DZEP
      SHP(1,12)=QSI*DZEP*PAUX3
      SHP(2,10)=(PAUX5-AUX)*DZEP
      SHP(2,11)=DEUX*(AUX-QSI)*DZEP
      SHP(2,12)=(PAUX3+QSI)*DZEP
      SHP(3,10)=(PAUX5-AUX)*DZEP
      SHP(3,11)=-DEUX*QSI*DZEP
      SHP(3,12)=XZER
      SHP(4,10)=AUX*(UNDEMI*DZEP-PAUX5)
      SHP(4,11)=DEUX*QSI*AUX
      SHP(4,12)=QSI*(PAUX3+UNDEMI*DZEP)
      SHP(1,13)=DEUX*QSI*ETA*DZEP
      SHP(1,14)=ETA*DZEP*PAUX4
      SHP(1,15)=DEUX*ETA*AUX*DZEP
      SHP(2,13)=DEUX*ETA*DZEP
      SHP(2,14)=XZER
      SHP(2,15)=-DEUX*ETA*DZEP
      SHP(3,13)=DEUX*QSI*DZEP
      SHP(3,14)=(ETA+PAUX4)*DZEP
      SHP(3,15)=DEUX*(AUX-ETA)*DZEP
      SHP(4,13)=DEUX*QSI*ETA
      SHP(4,14)=ETA*(PAUX4+UNDEMI*DZEP)
      SHP(4,15)=DEUX*ETA*AUX
      GOTO 666
  23  CONTINUE
C
C     FONCTIONS DE FORME TETRAEDRE A 4 NOEUDS
C
      AUX=UN-QSI-ETA-DZE
      SHP(1,1)=AUX
      SHP(1,2)=QSI
      SHP(1,3)=ETA
      SHP(1,4)=DZE
      SHP(2,1)=-UN
      SHP(2,2)= UN
      SHP(2,3)= XZER
      SHP(2,4)= XZER
      SHP(3,1)=-UN
      SHP(3,2)= XZER
      SHP(3,3)= UN
      SHP(3,4)= XZER
      SHP(4,1)=-UN
      SHP(4,2)= XZER
      SHP(4,3)= XZER
      SHP(4,4)= UN
      GOTO 666
  24  CONTINUE
C
C     FONCTIONS DE FORME TETRAEDRE A 10 NOEUDS
C
      AUX=UN-QSI-ETA-DZE
      SHP(1,1)=AUX*(DEUX*AUX-UN)
      SHP(1,2)=QUATRE*QSI*AUX
      SHP(1,3)=QSI*(DEUX*QSI-UN)
      SHP(1,4)=QUATRE*QSI*ETA
      SHP(1,5)=ETA*(DEUX*ETA-UN)
      SHP(1,6)=QUATRE*ETA*AUX
      SHP(1,7)=QUATRE*DZE*AUX
      SHP(1,8)=QUATRE*QSI*DZE
      SHP(1,9)=QUATRE*ETA*DZE
      SHP(1,10)=DZE*(DEUX*DZE-UN)
      SHP(2,1)=UN-QUATRE*AUX
      SHP(2,2)=QUATRE*(AUX-QSI)
      SHP(2,3)=QUATRE*QSI-UN
      SHP(2,4)=QUATRE*ETA
      SHP(2,5)=XZER
      SHP(2,6)=-QUATRE*ETA
      SHP(2,7)=-QUATRE*DZE
      SHP(2,8)= QUATRE*DZE
      SHP(2,9)= XZER
      SHP(2,10)=XZER
      SHP(3,1)= UN-QUATRE*AUX
      SHP(3,2)= -QUATRE*QSI
      SHP(3,3)= XZER
      SHP(3,4)= QUATRE*QSI
      SHP(3,5)= QUATRE*ETA-UN
      SHP(3,6)= QUATRE*(AUX-ETA)
      SHP(3,7)=-QUATRE*DZE
      SHP(3,8)= XZER
      SHP(3,9)= QUATRE*DZE
      SHP(3,10)=XZER
      SHP(4,1)= UN-QUATRE*AUX
      SHP(4,2)= -QUATRE*QSI
      SHP(4,3)= XZER
      SHP(4,4)= XZER
      SHP(4,5)= XZER
      SHP(4,6)= -QUATRE*ETA
      SHP(4,7)=QUATRE*(AUX-DZE)
      SHP(4,8)=QUATRE*QSI
      SHP(4,9)=QUATRE*ETA
      SHP(4,10)=QUATRE*DZE-UN
      GOTO 666
  14  CONTINUE
C
C     FONCTIONS DE FORME CUBE A 8 NOEUDS
C
      QSIM= UN-QSI
      QSIP= UN+QSI
      ETAM= UN-ETA
      ETAP= UN+ETA
      DZEM= UN-DZE
      DZEP= UN+DZE
      SHP(1,1)=QSIM*ETAM*DZEM/HUIT
      SHP(1,2)=QSIP*ETAM*DZEM/HUIT
      SHP(1,3)=QSIP*ETAP*DZEM/HUIT
      SHP(1,4)=QSIM*ETAP*DZEM/HUIT
      SHP(1,5)=QSIM*ETAM*DZEP/HUIT
      SHP(1,6)=QSIP*ETAM*DZEP/HUIT
      SHP(1,7)=QSIP*ETAP*DZEP/HUIT
      SHP(1,8)=QSIM*ETAP*DZEP/HUIT
      SHP(2,1)=    -ETAM*DZEM/HUIT
      SHP(2,2)=     ETAM*DZEM/HUIT
      SHP(2,3)=     ETAP*DZEM/HUIT
      SHP(2,4)=    -ETAP*DZEM/HUIT
      SHP(2,5)=    -ETAM*DZEP/HUIT
      SHP(2,6)=     ETAM*DZEP/HUIT
      SHP(2,7)=     ETAP*DZEP/HUIT
      SHP(2,8)=    -ETAP*DZEP/HUIT
      SHP(3,1)=-QSIM*DZEM/HUIT
      SHP(3,2)=-QSIP*DZEM/HUIT
      SHP(3,3)= QSIP*DZEM/HUIT
      SHP(3,4)= QSIM*DZEM/HUIT
      SHP(3,5)=-QSIM*DZEP/HUIT
      SHP(3,6)=-QSIP*DZEP/HUIT
      SHP(3,7)= QSIP*DZEP/HUIT
      SHP(3,8)= QSIM*DZEP/HUIT
      SHP(4,1)=-QSIM*ETAM/HUIT
      SHP(4,2)=-QSIP*ETAM/HUIT
      SHP(4,3)=-QSIP*ETAP/HUIT
      SHP(4,4)=-QSIM*ETAP/HUIT
      SHP(4,5)= QSIM*ETAM/HUIT
      SHP(4,6)= QSIP*ETAM/HUIT
      SHP(4,7)= QSIP*ETAP/HUIT
      SHP(4,8)= QSIM*ETAP/HUIT
      GOTO 666
  15  CONTINUE
C
C     FONCTIONS DE FORME CUBE A 20 NOEUDS
C
      QSIM=(UN-QSI)
      QSIP=(UN+QSI)
      ETAM=(UN-ETA)
      ETAP=(UN+ETA)
      DZEM=(UN-DZE)
      DZEP=(UN+DZE)
C     NOEUDS AUX SOMMETS 1 3 5 7 13 15 17 19
      SHP(1,1 )=QSIM*ETAM*DZEM*(QSIM+ETAM+DZEM-CINQ)/HUIT
      SHP(1,3 )=QSIP*ETAM*DZEM*(QSIP+ETAM+DZEM-CINQ)/HUIT
      SHP(1,5 )=QSIP*ETAP*DZEM*(QSIP+ETAP+DZEM-CINQ)/HUIT
      SHP(1,7 )=QSIM*ETAP*DZEM*(QSIM+ETAP+DZEM-CINQ)/HUIT
      SHP(1,13)=QSIM*ETAM*DZEP*(QSIM+ETAM+DZEP-CINQ)/HUIT
      SHP(1,15)=QSIP*ETAM*DZEP*(QSIP+ETAM+DZEP-CINQ)/HUIT
      SHP(1,17)=QSIP*ETAP*DZEP*(QSIP+ETAP+DZEP-CINQ)/HUIT
      SHP(1,19)=QSIM*ETAP*DZEP*(QSIM+ETAP+DZEP-CINQ)/HUIT
      SHP(2,1 )=    -ETAM*DZEM*(DEUX*QSIM+ETAM+DZEM-CINQ)/HUIT
      SHP(2,3 )=     ETAM*DZEM*(DEUX*QSIP+ETAM+DZEM-CINQ)/HUIT
      SHP(2,5 )=     ETAP*DZEM*(DEUX*QSIP+ETAP+DZEM-CINQ)/HUIT
      SHP(2,7 )=    -ETAP*DZEM*(DEUX*QSIM+ETAP+DZEM-CINQ)/HUIT
      SHP(2,13)=    -ETAM*DZEP*(DEUX*QSIM+ETAM+DZEP-CINQ)/HUIT
      SHP(2,15)=     ETAM*DZEP*(DEUX*QSIP+ETAM+DZEP-CINQ)/HUIT
      SHP(2,17)=     ETAP*DZEP*(DEUX*QSIP+ETAP+DZEP-CINQ)/HUIT
      SHP(2,19)=    -ETAP*DZEP*(DEUX*QSIM+ETAP+DZEP-CINQ)/HUIT
      SHP(3,1 )=    -QSIM*DZEM*(QSIM+DEUX*ETAM+DZEM-CINQ)/HUIT
      SHP(3,3 )=    -QSIP*DZEM*(QSIP+DEUX*ETAM+DZEM-CINQ)/HUIT
      SHP(3,5 )=     QSIP*DZEM*(QSIP+DEUX*ETAP+DZEM-CINQ)/HUIT
      SHP(3,7 )=     QSIM*DZEM*(QSIM+DEUX*ETAP+DZEM-CINQ)/HUIT
      SHP(3,13)=    -QSIM*DZEP*(QSIM+DEUX*ETAM+DZEP-CINQ)/HUIT
      SHP(3,15)=    -QSIP*DZEP*(QSIP+DEUX*ETAM+DZEP-CINQ)/HUIT
      SHP(3,17)=     QSIP*DZEP*(QSIP+DEUX*ETAP+DZEP-CINQ)/HUIT
      SHP(3,19)=     QSIM*DZEP*(QSIM+DEUX*ETAP+DZEP-CINQ)/HUIT
      SHP(4,1 )=    -QSIM*ETAM*(QSIM+ETAM+DEUX*DZEM-CINQ)/HUIT
      SHP(4,3 )=    -QSIP*ETAM*(QSIP+ETAM+DEUX*DZEM-CINQ)/HUIT
      SHP(4,5 )=    -QSIP*ETAP*(QSIP+ETAP+DEUX*DZEM-CINQ)/HUIT
      SHP(4,7 )=    -QSIM*ETAP*(QSIM+ETAP+DEUX*DZEM-CINQ)/HUIT
      SHP(4,13)=     QSIM*ETAM*(QSIM+ETAM+DEUX*DZEP-CINQ)/HUIT
      SHP(4,15)=     QSIP*ETAM*(QSIP+ETAM+DEUX*DZEP-CINQ)/HUIT
      SHP(4,17)=     QSIP*ETAP*(QSIP+ETAP+DEUX*DZEP-CINQ)/HUIT
      SHP(4,19)=     QSIM*ETAP*(QSIM+ETAP+DEUX*DZEP-CINQ)/HUIT
C     NOEUDS SUR LES COTES PARALLELES A L AXE QSI 2 6 14 18
      SHP(1,2 )=UNQUA*(UN-QSI*QSI)*ETAM*DZEM
      SHP(1,6 )=UNQUA*(UN-QSI*QSI)*ETAP*DZEM
      SHP(1,14)=UNQUA*(UN-QSI*QSI)*ETAM*DZEP
      SHP(1,18)=UNQUA*(UN-QSI*QSI)*ETAP*DZEP
      SHP(2,2 )=-UNDEMI*QSI*ETAM*DZEM
      SHP(2,6 )=-UNDEMI*QSI*ETAP*DZEM
      SHP(2,14)=-UNDEMI*QSI*ETAM*DZEP
      SHP(2,18)=-UNDEMI*QSI*ETAP*DZEP
      SHP(3,2 )=-UNQUA*(UN-QSI*QSI)*DZEM
      SHP(3,6 )= UNQUA*(UN-QSI*QSI)*DZEM
      SHP(3,14)=-UNQUA*(UN-QSI*QSI)*DZEP
      SHP(3,18)= UNQUA*(UN-QSI*QSI)*DZEP
      SHP(4,2 )=-UNQUA*(UN-QSI*QSI)*ETAM
      SHP(4,6 )=-UNQUA*(UN-QSI*QSI)*ETAP
      SHP(4,14)= UNQUA*(UN-QSI*QSI)*ETAM
      SHP(4,18)= UNQUA*(UN-QSI*QSI)*ETAP
C     NOEUDS SUR LES COTES PARALELLES A L AXE ETA 4 8 16 20
      SHP(1,4 )=UNQUA*QSIP*(UN-ETA*ETA)*DZEM
      SHP(1,8 )=UNQUA*QSIM*(UN-ETA*ETA)*DZEM
      SHP(1,16)=UNQUA*QSIP*(UN-ETA*ETA)*DZEP
      SHP(1,20)=UNQUA*QSIM*(UN-ETA*ETA)*DZEP
      SHP(2,4 )= UNQUA*(UN-ETA*ETA)*DZEM
      SHP(2,8 )=-UNQUA*(UN-ETA*ETA)*DZEM
      SHP(2,16)= UNQUA*(UN-ETA*ETA)*DZEP
      SHP(2,20)=-UNQUA*(UN-ETA*ETA)*DZEP
      SHP(3,4 )=-UNDEMI*QSIP*ETA*DZEM
      SHP(3,8 )=-UNDEMI*QSIM*ETA*DZEM
      SHP(3,16)=-UNDEMI*QSIP*ETA*DZEP
      SHP(3,20)=-UNDEMI*QSIM*ETA*DZEP
      SHP(4,4 )=-UNQUA*QSIP*(UN-ETA*ETA)
      SHP(4,8 )=-UNQUA*QSIM*(UN-ETA*ETA)
      SHP(4,16)= UNQUA*QSIP*(UN-ETA*ETA)
      SHP(4,20)= UNQUA*QSIM*(UN-ETA*ETA)
C     NOEUDS SUR LES COTES PARALELLES A L AXE DZE 9 10 11 12
      SHP(1,9 )= UNQUA*QSIM*ETAM*(UN-DZE*DZE)
      SHP(1,10)= UNQUA*QSIP*ETAM*(UN-DZE*DZE)
      SHP(1,11)= UNQUA*QSIP*ETAP*(UN-DZE*DZE)
      SHP(1,12)= UNQUA*QSIM*ETAP*(UN-DZE*DZE)
      SHP(2,9 )=-UNQUA*ETAM*(UN-DZE*DZE)
      SHP(2,10)= UNQUA*ETAM*(UN-DZE*DZE)
      SHP(2,11)= UNQUA*ETAP*(UN-DZE*DZE)
      SHP(2,12)=-UNQUA*ETAP*(UN-DZE*DZE)
      SHP(3,9 )=-UNQUA*QSIM*(UN-DZE*DZE)
      SHP(3,10)=-UNQUA*QSIP*(UN-DZE*DZE)
      SHP(3,11)= UNQUA*QSIP*(UN-DZE*DZE)
      SHP(3,12)= UNQUA*QSIM*(UN-DZE*DZE)
      SHP(4,9 )=-UNDEMI*QSIM*ETAM*DZE
      SHP(4,10)=-UNDEMI*QSIP*ETAM*DZE
      SHP(4,11)=-UNDEMI*QSIP*ETAP*DZE
      SHP(4,12)=-UNDEMI*QSIM*ETAP*DZE
      GOTO 666
  25  CONTINUE
C
C     FONCTIONS DE FORME PYRAMIDE A 5 NOEUDS
C
      DZEM =UN-DZE
      DZEM4=(UN-DZE)*QUATRE
      AUX = QSI+ETA+DZE-UN
      AUX1=-QSI+ETA+DZE-UN
      AUX2=-QSI-ETA+DZE-UN
      AUX3= QSI-ETA+DZE-UN
      IF(ABS(DZE-UN).LT.1.D-6) THEN
      DO 251 IB=1,5
      DO 251 IA=1,3
      SHP(IA,IB)= XZER
  251 CONTINUE
      SHP(1,5)= UN
      SHP(2,1)= UNDEMI
      SHP(2,3)=-UNDEMI
      SHP(3,2)= UNDEMI
      SHP(3,4)=-UNDEMI
      DO 252 IA=1,4
      SHP(4,IA)=-UNDEMI
  252 CONTINUE
      SHP(4,5)= UN
      GO TO 666
      ENDIF
      SHP(1,1)=AUX1*AUX2/DZEM4
      SHP(1,2)=AUX2*AUX3/DZEM4
      SHP(1,3)=AUX *AUX3/DZEM4
      SHP(1,4)=AUX *AUX1/DZEM4
      SHP(1,5)=DZE
      SHP(2,1)=(-AUX1-AUX2)/DZEM4
      SHP(2,2)=( AUX2-AUX3)/DZEM4
      SHP(2,3)=( AUX +AUX3)/DZEM4
      SHP(2,4)=( AUX1-AUX )/DZEM4
      SHP(2,5)=XZER
      SHP(3,1)=( AUX2-AUX1)/DZEM4
      SHP(3,2)=(-AUX2-AUX3)/DZEM4
      SHP(3,3)=( AUX3-AUX )/DZEM4
      SHP(3,4)=( AUX +AUX1)/DZEM4
      SHP(3,5)=XZER
      SHP(4,1)=(AUX1+AUX2+AUX1*AUX2/DZEM)/DZEM4
      SHP(4,2)=(AUX2+AUX3+AUX2*AUX3/DZEM)/DZEM4
      SHP(4,3)=(AUX3+AUX +AUX3*AUX /DZEM)/DZEM4
      SHP(4,4)=( AUX+AUX1+ AUX*AUX1/DZEM)/DZEM4
      SHP(4,5)=UN
      GOTO 666
  30  CONTINUE
C
C     FONCTIONS DE FORME PYRAMIDE A 13 NOEUDS
C
      DZEM=UN-DZE
      DZEM2=(UN-DZE)*DEUX
      PAUX = QSI+ETA+DZE-UN
      PAUX1=-QSI+ETA+DZE-UN
      PAUX2=-QSI-ETA+DZE-UN
      PAUX3= QSI-ETA+DZE-UN
      QAUX = QSI-UNDEMI
      QAUX1= ETA-UNDEMI
      QAUX2=-QSI-UNDEMI
      QAUX3=-ETA-UNDEMI
      IF(ABS(DZE-UN).LT.1.D-6) THEN
      DO 301 IB=1,13
      DO 301 IA=1,3
      SHP(IA,IB)= XZER
  301 CONTINUE
      SHP(1,13)= UN
      SHP(2,1 )=-UNDEMI
      SHP(2,5 )= UNDEMI
      SHP(2,9 )= DEUX
      SHP(2,11)=-DEUX
      SHP(3,3 )=-UNDEMI
      SHP(3,7 )= UNDEMI
      SHP(3,10)= DEUX
      SHP(3,12)=-DEUX
      DO 302 IA=1,7,2
      SHP(4,IA)=   UN/QUATRE
      SHP(4,IA+1)= XZER
  302 CONTINUE
      DO 303 IA=9,12
      SHP(4,IA)=-UN
  303 CONTINUE
      SHP(4,13)= TROIS
      GO TO 666
      ENDIF
      SHP(1,1 )= PAUX1*PAUX2*QAUX /DZEM2
      SHP(1,2 )=-PAUX1*PAUX2*PAUX3/DZEM2
      SHP(1,3 )= PAUX2*PAUX3*QAUX1/DZEM2
      SHP(1,4 )=-PAUX2*PAUX3*PAUX /DZEM2
      SHP(1,5 )= PAUX3*PAUX *QAUX2/DZEM2
      SHP(1,6 )=-PAUX3*PAUX *PAUX1/DZEM2
      SHP(1,7 )= PAUX *PAUX1*QAUX3/DZEM2
      SHP(1,8 )=-PAUX *PAUX1*PAUX2/DZEM2
      SHP(1,9 )= DZE*PAUX1*PAUX2/DZEM
      SHP(1,10)= DZE*PAUX2*PAUX3/DZEM
      SHP(1,11)= DZE*PAUX3*PAUX /DZEM
      SHP(1,12)= DZE*PAUX *PAUX1/DZEM
      SHP(1,13)= DEUX*DZE*(DZE-UNDEMI)
C
      SHP(2,1 )=(PAUX1*PAUX2-(PAUX1+PAUX2)*QAUX)/DZEM2
      SHP(2,2 )=(PAUX2*PAUX3+PAUX1*PAUX3-PAUX1*PAUX2)/DZEM2
      SHP(2,3 )=(PAUX2-PAUX3)*QAUX1/DZEM2
      SHP(2,4 )=(PAUX3*PAUX-PAUX2*PAUX-PAUX2*PAUX3)/DZEM2
      SHP(2,5 )=((PAUX+PAUX3)*QAUX2-PAUX3*PAUX)/DZEM2
      SHP(2,6 )=(PAUX3*PAUX-PAUX*PAUX1-PAUX3*PAUX1)/DZEM2
      SHP(2,7 )=(PAUX1-PAUX)*QAUX3/DZEM2
      SHP(2,8 )=(PAUX*PAUX2+PAUX*PAUX1-PAUX1*PAUX2)/DZEM2
      SHP(2,9 )=(-PAUX2-PAUX1)*DZE/DZEM
      SHP(2,10)=(PAUX2-PAUX3)*DZE/DZEM
      SHP(2,11)=(PAUX+PAUX3)*DZE/DZEM
      SHP(2,12)=(PAUX1-PAUX)*DZE/DZEM
      SHP(2,13)=XZER
C
      SHP(3,1 )=(PAUX2-PAUX1)*QAUX/DZEM2
      SHP(3,2 )=(PAUX1*PAUX3+PAUX1*PAUX2-PAUX2*PAUX3)/DZEM2
      SHP(3,3 )=(PAUX2*PAUX3-(PAUX2+PAUX3)*QAUX1)/DZEM2
      SHP(3,4 )=(PAUX3*PAUX+PAUX2*PAUX-PAUX2*PAUX3)/DZEM2
      SHP(3,5 )=(PAUX3-PAUX)*QAUX2/DZEM2
      SHP(3,6 )=(PAUX*PAUX1-PAUX3*PAUX1-PAUX3*PAUX)/DZEM2
      SHP(3,7 )=((PAUX1+PAUX)*QAUX3-PAUX*PAUX1)/DZEM2
      SHP(3,8 )=(PAUX*PAUX1-PAUX1*PAUX2-PAUX*PAUX2)/DZEM2
      SHP(3,9 )=(PAUX2-PAUX1)*DZE/DZEM
      SHP(3,10)=(-PAUX3-PAUX2)*DZE/DZEM
      SHP(3,11)=(PAUX3-PAUX)*DZE/DZEM
      SHP(3,12)=(PAUX+PAUX1)*DZE/DZEM
      SHP(3,13)=XZER
C
      SHP(4,1 )=(PAUX1+PAUX2+PAUX1*PAUX2/DZEM)*QAUX/DZEM2
      SHP(4,2 )=-UN*(PAUX2*PAUX3+PAUX1*PAUX3+PAUX1*PAUX2+
     +           PAUX1*PAUX2*PAUX3/DZEM)/DZEM2
      SHP(4,3 )=(PAUX2+PAUX3+PAUX2*PAUX3/DZEM)*QAUX1/DZEM2
      SHP(4,4 )=-UN*(PAUX3*PAUX +PAUX2*PAUX+PAUX2*PAUX3+
     +           PAUX2*PAUX3*PAUX/DZEM)/DZEM2
      SHP(4,5 )=(PAUX+PAUX3+PAUX*PAUX3/DZEM)*QAUX2/DZEM2
      SHP(4,6 )=-UN*(PAUX*PAUX1+PAUX3*PAUX1+PAUX3*PAUX+
     +           PAUX3*PAUX*PAUX1/DZEM)/DZEM2
      SHP(4,7 )=(PAUX1+PAUX+PAUX*PAUX1/DZEM)*QAUX3/DZEM2
      SHP(4,8 )=-UN*(PAUX1*PAUX2+PAUX*PAUX2+PAUX*PAUX1+
     +           PAUX*PAUX1*PAUX2/DZEM)/DZEM2
      SHP(4,9 )=PAUX1*PAUX2/DZEM/DZEM+(PAUX2+PAUX1)*DZE/DZEM
      SHP(4,10)=PAUX2*PAUX3/DZEM/DZEM+(PAUX3+PAUX2)*DZE/DZEM
      SHP(4,11)=PAUX3*PAUX /DZEM/DZEM+(PAUX +PAUX3)*DZE/DZEM
      SHP(4,12)=PAUX *PAUX1/DZEM/DZEM+(PAUX +PAUX1)*DZE/DZEM
      SHP(4,13)=QUATRE*DZE-UN
      GO TO 666
C
 666  CONTINUE
      RETURN
      END

