C CQ8BSE    SOURCE    CHAT      05/01/12    22:27:08     5004
      SUBROUTINE CQ8BSE(XX,NBNN,NBPGAU,LRE,EPAIST,EXCEN,DZEGAU,
     +      POIGAU,SHPCOQ,SHPELE,WORK,XFORC,IRR)
C=======================================================================
C
C            (ROUTINE EQUIVALENTE A COQ8BS AVEC EXCENTREMENT.)
C ENTREES
C     XX(3,NBNN):  TABLEAU DES COORDONNEES DES NOEUDS
C     NBNN    :    NOMBRE DE NOEUDS
C     NBPGAU  : NOMBRE DEPOINTS DE GAUSS
C     LRE     : NOMBRE DE DDL
C     EPAIST  :    EPAISSEUR DE LA COQUE
C     EXCEN   :    EXCENTREMENT DE LA COQUE
C DZEGAU(NBPGAU):  COORDONNEES REDUITES DES POINTS DE GAUSS DANS
C                  L EPAISSEUR
C POIGAU(NBPGAU):  POIDS DE GAUSS
C SHPCOQ(6,NBNN,NBPGAU) : FONCTIONS DE FORME ET DERIVEES AUX PTS DE GSS
C SHPELE(6,NBNN,NBNN)   : FONCTIONS DE FORME ET DERIVEES AUX NOEUDS
C WORK(5*NBPGAU):  5XNBPGAU CONTRAINTES AUX POINTS DE GAUSS
C      SORTIES
C XFORC(LRE)  :    6 FORCES AUX NBNN NOEUDS
C
C     IRR     :    INDICATEUR DE SUCCES ( 1 ) , D ECHEC (0 OU-1)
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(XZER=0.D0,UN=1.D0)
      PARAMETER(X577=.577350269189626D0,UNSIX=.166666666666666D0)
      DIMENSION XX(3,*),DZEGAU(*),SHPCOQ(6,8,*),SHPELE(6,8,*),WORK(*)
      DIMENSION POIGAU(*)
      DIMENSION TXR(3,3,8),B(5,48),TH(8),EXC(8),T(9),XFORC(48),XSTRS(5)
C
      USX577=UNSIX/X577
C
      DO 2 J=1,LRE
      XFORC(J)=XZER
    2 CONTINUE
C
C   ---   DETERMINATION DES AXES LOCAUX AU NOEUD I   ---
C
      CALL CQ8LOC(XX,NBNN,SHPELE,TXR,IRR)
C
      DO 70 K=1,NBNN
      TH(K)=EPAIST
      EXC(K)=EXCEN
   70 CONTINUE
C----------------------------------------------------------------------
C     BOUCLE SUR LES POINTS DE GAUSS
C----------------------------------------------------------------------
      DO 100 IA=1,NBPGAU
      E3 = DZEGAU(IA)
C-------------------------------
C CALCUL DE LA MATRICE B ET RECUPERATION DES CONTRAITES DU PT DE GAUSS
C-------------------------------
      IDEB=(IA-1)*5
      DO 3 I=1,5
      XSTRS(I)=WORK(IDEB+I)
      DO 3 J=1,LRE
      B(I,J)=XZER
    3 CONTINUE
      CALL BCOQ8E(IA,XX,NBNN,TH,EXC,B,DET,E3,SHPCOQ,TXR,IRR)
C
      IF(IRR.NE.1) RETURN
C
C     CALCUL DE BSIGMA POUR LE POINT DE GAUSS
C
      DJAC=DET*POIGAU(IA)
      CALL BSIG8(B,NBNN,XSTRS,DJAC,XFORC)
  100 CONTINUE
C
      RETURN
      END

