C COTEST    SOURCE    CB215821  16/04/21    21:16:05     8920
      SUBROUTINE COTEST (PIM,PIMS,EPI,DELTA1,DPMAX,INDIC,ICONV,NL)
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C 22/11/91                                                 C
C     LES TESTS SONT CONDUITS SUR LA PSEUDO-PRESSION       C
C 21/11/91                                                 C
C     ON SORT LA VALEUR DE DPMAX, AVEC UN MESSAGE SI       C
C     DPMAX A ETE AJUSTE A UNE VALEUR RAISONNABLE (1)      C
C                                                          C
C  NEDELEC 16 1 91                                         C
C                                                          C
C             SSPRGM TESTANT LA CONVERGENCE                C
C              DE LA RESOLUTION ITERATIVE                  C
C                                                          C
C  EN ENTREE   PIM     : PRESSION COURANTE                 C
C              PIMS    : PRESSION AU TEST PRECEDENT        C
C              EPI     : EPSILON DE TEST                   C
C              INDIC   : 0 TEST COURANT , 1 TEST EN BUTEE  C
C              NL      : NOMBRE D'ELEMENTS                 C
C                                                          C
C  EN SORTIE   ICONV   : 0 RESOLUTION NON CONVERGEE        C
C                      : 1 RESOLUTION CONVERGEE            C
C              DELTA   : ACCROI DETECTE > EPI TEST COURANT C
C                      : ACCROI MAX SI TEST EN BUTEE       C
C                                                          C
C               (PIM(I)-PIMS(I))                           C
C  ON TESTE    ------------------                          C
C              MAX(PIM) - MIN(PIM)                         C
C                                                          C
C     SOIT LA VARIATION DE PRESSION ENTRE DEUX TESTS       C
C     RAPPORTEE A LA DIFFERENCE DE PRESSION MAXI DANS      C
C     TOUT LE CHAMP. C'EST BEAUCOUP MIEUX QUE TESTER       C
C     DELTA(P)/P CAR LA PRESSION ETANT DEFINIE A UNE       C
C     CONSTANTE PRES, P PEUT PRENDRE DES VALEURS TRES      C
C     PROCHES DE ZERO ET FAIRE EXPLOSER LE TEST.           C
C                                                          C
C     LE FIN DU FIN SERAIT DE TESTER DELPA(P)/GRADIENT(P)  C
C     MAIS BON ...                                         C
C                                                          C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION PIM(1),PIMS(1)
C
C RECHERCHE DE L'ETENDUE DE P SUR TOUT LE DOMAINE : DPMAX
C
      DPMIN=1.E-5
      DPFIXE=1.0
      PMIN=1.E32
      PMAX=-1.E32
      DO 1 I=1,NL
         IF(PIM(I).GT.PMAX) PMAX=PIM(I)
         IF(PIM(I).LT.PMIN) PMIN=PIM(I)
 1    CONTINUE
      DPMAX=PMAX-PMIN
      IF(DPMAX.LT.DPMIN) THEN
         WRITE(6,*) '>>>COTEST: L''ECHELLE DE PRESSION EST FIXEE A :',
     ;             DPFIXE
         DPMAX=DPFIXE
      ENDIF
      IF (INDIC.EQ.0) THEN
C
       ICONV=1
       DO 10 I=1,NL
         DELTA1=ABS(PIM(I)-PIMS(I))
         IF (DELTA1.GT.(EPI*DPMAX)) THEN
C-BON    IF (DELTA1.GT.(EPI*ABS(PIM(I)))) THEN
            ICONV=0
            GOTO 30
         ENDIF
   10  CONTINUE
      ELSE
C
       ICONV=1
       DELTA1=1.D-20
       DO 20 I=1,NL
        DELTA=ABS(PIM(I)-PIMS(I))
        IF (DELTA.GT.DELTA1) DELTA1=DELTA
        IF(DELTA.GT.(EPI*DPMAX)) ICONV=0
C-BON   IF(DELTA.GT.(EPI*ABS(PIM(I)))) ICONV=0
   20  CONTINUE
      ENDIF
   30 CONTINUE
      RETURN
      END



