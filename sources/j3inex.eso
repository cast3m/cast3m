C J3INEX    SOURCE    CHAT      05/01/13    00:46:51     5004
      SUBROUTINE J3INEX(XY,XYC,NPTO,TOL,ICOD,ISIGM,IRET)
C----------------------------------------------------
C     LE POINT XY EST-IL DANS OU HORS DU CONTOURS XYC?
C
C     ESCLAVE DE J3COTO, mais pas seulement... on le blinde
C                        en rajoutant le sur qui ne sert pas
C                        pour J3COTO
C
C                 1 on est sur
C     CODE ICOD : 0 on ne sait pas
C                -1 est a l'interieur
C                -2 est a l'exterieur
C     PP 6/97
C     Pierre Pegon/JRC Ispra
C----------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCREEL
-INC  CCOPTIO
      DIMENSION XY(2),XYC(2,NPTO)
      DIMENSION XY1(2),XY2(2)
      LOGICAL LOK
C
      ICOD=0
      THETA=0.D0
C
      DO IE1=1,NPTO
        I1=IE1
        I2=IE1+1-(IE1/NPTO)*IE1
C
        DO IE2=1,2
          XY1(IE2)=XYC(IE2,I1)-XY(IE2)
          XY2(IE2)=XYC(IE2,I2)-XY(IE2)
        ENDDO
        CALL J3ANGL(XY1,XY2,TOL,THET1,IR)
C
        IF(IR.EQ.1)THEN
          ICOD=1
          RETURN
        ENDIF
C
        THETA=THETA+THET1
C
      ENDDO
C
      IF(ABS(ABS(THETA)-2*XPI).LT.NPTO*TOL)THEN
        ICOD=-1
        ISIGM=INT(SIGN(1.D0,THETA))
      ELSEIF(ABS(THETA).LT.NPTO*TOL)THEN
        ICOD=-2
        ISIGM=0
      ELSE
        WRITE(IOIMP,*)' J3INEX: on ne peut pas etre la'
        IRET=IRET+1
      ENDIF
C
      RETURN
      END



