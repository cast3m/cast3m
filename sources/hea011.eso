C HEA011    SOURCE    SERRE     12/07/10    21:15:19     7431
      SUBROUTINE HEA011(TC1,HEAS,HEAF,T,DT,TC,TRAV)
C---------------------------------------------------------------------
C Calcul de la température des plaques par la méthode RK4 et des
C puissances transmises aux plaques et au fluide
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C  /S TC1     : flottant : Température des plaques au temps t+dt (K)
C  /S HEAS    : flottant : Puissance générée dans le solide (J/s)
C  /S HEAF    : flottant : Puissance transmise au fluide (J/s)
C E/  T       : flottant : Temps au début du pas de temps (s)
C E/  DT      : flottant : Pas d'intégration (s)
C E/  TC      : flottant : Température des plaques au temps t (K)
C E/  TRAV    : entier   : Pointeur du segment de travail
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C Aucune variable n'est utilisée.
C Le pointeur du segment de travail est simplement transmis.
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 K0,K1,K2,K3
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
      call HEA010(T,TC,HEAS0,HEAF0,DTCS0,ZMP0,TRAV)
      K0   = DTCS0 * DT
      T1   = T  + 0.5D0 * DT
      TCI1 = TC + 0.5D0 * K0
      call HEA010(T1,TCI1,HEAS1,HEAF1,DTCS1,ZMP1,TRAV)
      K1   = DTCS1 * DT
      T2   = T  + 0.5D0 * DT
      TCI2 = TC + 0.5D0 * K1
      call HEA010(T2,TCI2,HEAS2,HEAF2,DTCS2,ZMP2,TRAV)
      K2   = DTCS2 * DT
      T3   = T  + DT
      TCI3 = TC + K2
      call HEA010(T3,TCI3,HEAS3,HEAF3,DTCS3,ZMP3,TRAV)
      K3   = DTCS3 * DT
C
      TC1 = TC + 1.D0/6.D0 * (K0 + 2.D0*K1 + 2.D0*K2 + K3)
      HEAS = 1.D0/6.D0 * (HEAS0 + 2.D0*HEAS1 + 2.D0*HEAS2 + HEAS3)
      HEAF = 1.D0/6.D0 * (HEAF0 + 2.D0*HEAF1 + 2.D0*HEAF2 + HEAF3)
C
      RETURN
      END








