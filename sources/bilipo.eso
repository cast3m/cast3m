C BILIPO    SOURCE    CHAT      05/01/12    21:40:11     5004
      SUBROUTINE BILIPO(SIG0,DEPST,VAR0,XMAT,XCAR,SIGF,VARF,DEFP)
C-----------------------------------------------------------------------
C
C    DRIVER OF THE BILINEAR PLASTIC BENDING MODEL
C
C  INPUT:
C
C    SIG0             INITIAL GENERALIZED STRESS
C    DEPST            INCREMENT OF GENERALIZED STRAIN
C    XMAT             MATERIAL CHARACTERISTICS
C    XCAR             GEOMETRIC CHARACTERISTICS
C    VAR0             INITIAL INTERNAL VARIABLE
C
C  OUTPUT:
C
C    SIGF             FINAL GENERALIZED STRESS
C    VARF             FINAL INTERNAL VARIABLE
C    DEFP             INCREMENT OF PLASTIC DEFORMATION
C
C  GENERALIZED STRESS (.=0 OR F):
C
C    MOMY = SIG.(5)
C
C  MATERIAL CHARACTERISTICS:
C
C    E    = XMAT(1)   ELASTIC YOUNG MODULUS
C    NU   = XMAT(2)   POISSON RATIO
C    EAYI = XMAT(5)   PLASTIC MODULUS
C    YMOM = XMAT(7)   YIELDING MOMENT
C
C  GEOMETRIC CHARACTERISTICS:
C
C    INRX = XCAR(1)   X-INERTIAL TORSION
C    INRY = XCAR(2)   Y-INERTIAL BENDING        <-----
C    INRZ = XCAR(3)   Z-INERTIAL BENDING
C    SECT = XCAR(4)   SECTION
C    SRDY = XCAR(5)   TRANSVERSE SHEAR'S REDUCED SECTION
C    SRDZ = XCAR(6)   TRANSVERSE SHEAR'S REDUCED SECTION
C
C  INTERNAL VARIABLE (.=0 OR F):
C
C    EPSE = VAR.(1)   CUMULATED PLASTIC CURVATURE
C    EITA = VAR.(2)   TANGENT Y-BENDING STIFFNESS
C    MOEY = VAR.(3)   ELASTIC BENDING
C    COUY = VAR.(4)   CURVATURE
C
C-----------------------------------------------------------------------
C  P. PEGON      14/9/92 renewed new ECOU 24/11/95
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO
C
      DIMENSION SIG0(*),DEPST(*),VAR0(*),XMAT(*),XCAR(*),
     .          SIGF(*),VARF(*) ,DEFP(*)
C
C-----------------------------------------------------------------------
C
C  ELASTIC STRESS INCREMENT FOR ALL STRESS COMPONENT EXCEPT MOMY
C
      CISA=0.5D0*XMAT(1)/(1.D0+XMAT(2))
C     SIGF(1)=SIG0(1) + DSIGT(1)
      SIGF(1)=SIG0(1) + XMAT(1)*XCAR(4)*DEPST(1)
C     SIGF(2)=SIG0(2) + DSIGT(2)
      SIGF(2)=SIG0(2) + CISA*XCAR(5)*DEPST(2)
C     SIGF(3)=SIG0(3) + DSIGT(3)
      SIGF(3)=SIG0(3) + CISA*XCAR(6)*DEPST(3)
C     SIGF(4)=SIG0(4) + DSIGT(4)
      SIGF(4)=SIG0(4) + CISA*XCAR(1)*DEPST(4)
C     SIGF(6)=SIG0(6) + DSIGT(6)
      SIGF(6)=SIG0(6) + XMAT(1)*XCAR(3)*DEPST(6)
C
C  ELASTIC AND PLASTIC BENDING STIFFNESS
C
      EIY    = XMAT(1)*XCAR(2)
      EYIY   = XMAT(5)*XCAR(2)
C
C
C  Y-CURVATURE INCREMENT
C
C     DCOURY = DSIGT(5) / EIY
      DCOURY = DEPST(5)
C
C  CALL TO THE SCALAR BILINEAR PLASTIC MODEL COMPUTING MOMY
C
      FEP0 = SIG0(5) - VAR0(3)
      FE0  = VAR0(3)
      CALL BILINE(DCOURY,XMAT(6),EIY, EYIY, VARF(2),
     >          FE0,FEP0,SIGF(5),DEPSE)
C
C  FINAL INTERNAL VARIABLE
C
      VARF(1)=VAR0(1)+DEPSE
      VARF(3)=FE0
      VARF(4)=VAR0(4)+DCOURY
C
C  PLASTIC DEFORMATION INCREMENT
C
      DEFP(1) = 0.D0
      DEFP(2) = 0.D0
      DEFP(3) = 0.D0
      DEFP(4) = 0.D0
      DEFP(5) = DEPSE
      DEFP(6) = 0.D0
      RETURN
      END


