C DETJAC    SOURCE    CHAT      05/01/12    22:44:08     5004
C
      SUBROUTINE DETJAC(XNOEU,XY,ITYP,IDIM,NOEL1,IFORMU,SHP,DET)
C*******************************************************************
C     CALCUL DU JACOBIEN POUR LE PASSAGE D'UN SCALAIRE  DE
C            L'ELEMENT DE REFERENCE A L'ELEMENT REEL
C********************************************************************
C   ENTREES
C
C     XY COORDONNEES DE REFERENCES DU POINT DE CALCUL
C     ITYP TYPE DE L ELEMENT
C     XNOEU COORDONNEES REELLES DES NOEUDS DE L ELEMENT
C     NOEL1  NOMBRE DE NOEUDS DE L ELEMENT POSITION
C
C           COORDONNEES DE REFERENCE AU POINT DE CALCUL
C
C     SHP(1,I) FONCTIONS DE FORMES AU POINT DE CALCUL
C     SHP(2,I) DERIVEE DES FONCTIONS DE FORME / XSI AU POINT DE CALCUL
C     SHP(3,I)    "     "      "     "    "   / ETA "    "   "    "
C     SHP(4,I)    "     "      "      "   "   /DZETA "   "    "   "
C
C   SORTIE
C     DET VALEUR DU JACOBIEN
C ********************************************************************
C
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C
C
      DIMENSION XNOEU(3,*),SHP(6,*),XY(*)
C
C
C***  CALCUL DES XI ET DES DXI
C
      CALL SHAPE(XY(1),XY(2),XY(3),ITYP,SHP,IRET)
C
C***  COEFFICIENTS DE DERIVATION
C
      DET=0.D0
      IF(IDIM.EQ.2)THEN
C
C***  BIDIM
C
            A=0.D0
            B=0.D0
            C=0.D0
            D=0.D0
            DO 63 I=1,NOEL1
                 A=A+SHP(2,I)*XNOEU(1,I)
                 B=B+SHP(3,I)*XNOEU(1,I)
                 C=C+SHP(2,I)*XNOEU(2,I)
                 D=D+SHP(3,I)*XNOEU(2,I)
  63        CONTINUE
            DET=A*D-B*C
C
C***  TRIDIM
C
      ELSE
            A1=0.D0
            A2=0.D0
            A3=0.D0
            B1=0.D0
            B2=0.D0
            B3=0.D0
            C1=0.D0
            C2=0.D0
            C3=0.D0
            DO 75 I=1,NOEL1
                 A1=A1+SHP(2,I)*XNOEU(1,I)
                 A2=A2+SHP(3,I)*XNOEU(1,I)
                 A3=A3+SHP(4,I)*XNOEU(1,I)
                 B1=B1+SHP(2,I)*XNOEU(2,I)
                 B2=B2+SHP(3,I)*XNOEU(2,I)
                 B3=B3+SHP(4,I)*XNOEU(2,I)
                 C1=C1+SHP(2,I)*XNOEU(3,I)
                 C2=C2+SHP(3,I)*XNOEU(3,I)
                 C3=C3+SHP(4,I)*XNOEU(3,I)
  75        CONTINUE
            DET=A1*(B2*C3-C2*B3)+A2*(C1*B3-B1*C3)+A3*(B1*C2-B2*C1)
C
      ENDIF
      RETURN
      END




