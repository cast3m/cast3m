C UNIAKT    SOURCE    CHAT      05/01/13    03:58:26     5004
      SUBROUTINE UNIAKT(XMAT,XVAR,INDIC,XKTAT,KERRE)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C----------------------------------------------------------------------
C
C     COMPORTEMENT UNIDIRECTIONNEL ACIER_UNI (2D ou coque)
C     (D'APRES FIBSTE DU MODELE A FIBRE)
C     Matrice tangente
C
C----------------------------------------------------------------------
C     Pierre Pegon (ISPRA) Juillet 1994
C     Didier Combescure (ISPRA) Septembre 1995
C----------------------------------------------------------------------
C      XMAT(1) YOUN
C      XMAT(2) V1X
C      XMAT(3) V1Y
C  EPAI (if any) en deformations planes
C----------------------------------------------------------------------
C      XVAR( 1) VARF( 1) TANGENT MODULUS
C----------------------------------------------------------------------
C      XKTAT(4,4)   TANGENT STIFFNESS MATRIX
C----------------------------------------------------------------------
-INC CCOPTIO
*
      DIMENSION XMAT(*),XVAR(*)
      DIMENSION XKTAT(4,4)
*
      KERRE=0
*
      YOUN=XMAT(1)
      V1X=XMAT(2)
      V1Y=XMAT(3)
      TANGE = XVAR(1)
      EPAI = 1.D0
      CALL ZERO(XKTAT,4,4)
C
      IF (INDIC.EQ.2)THEN
C
C  Cas deformations planes
C
        EPAI=XMAT(17)
        XKTAT(1,1)=EPAI*TANGE*V1X**4
        XKTAT(2,2)=EPAI*TANGE*V1Y**4
        XKTAT(2,1)=EPAI*TANGE*(V1X*V1Y)**2
        XKTAT(1,2)=EPAI*TANGE*(V1X*V1Y)**2
        XKTAT(1,3)=EPAI*TANGE*(V1X**3)*V1Y
        XKTAT(2,3)=EPAI*TANGE*V1X*(V1Y**3)
        XKTAT(3,1)=EPAI*TANGE*(V1X**3)*V1Y
        XKTAT(3,2)=EPAI*TANGE*V1X*(V1Y**3)
        XKTAT(3,3)=EPAI*TANGE*(V1X*V1Y)**2
      ELSEIF (INDIC.LE.0)THEN
C
C  Cas contraintes planes
C
       XKTAT(1,1)=TANGE*V1X**4
       XKTAT(2,2)=TANGE*V1Y**4
       XKTAT(2,1)=TANGE*(V1X*V1Y)**2
       XKTAT(1,2)=TANGE*(V1X*V1Y)**2
       XKTAT(1,4)=TANGE*(V1X**3)*V1Y
       XKTAT(2,4)=TANGE*V1X*(V1Y**3)
       XKTAT(4,1)=TANGE*(V1X**3)*V1Y
       XKTAT(4,2)=TANGE*V1X*(V1Y**3)
       XKTAT(4,4)=TANGE*(V1X*V1Y)**2
C       XKTAT(1,1)=TANGE
C       XKTAT(3,3)=TANGE
      ENDIF
C
      RETURN
      END


