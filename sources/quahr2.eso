C QUAHR2    SOURCE    CHAT      05/01/13    02:41:08     5004
C QUAHR2    SOURCE    LOFT      88/06/23    21:07:05
      SUBROUTINE QUAHR2(XGENE,DJAC,VKL1,VKL2,LRE,NST,NBNO,IFOU,REL)
C=======================================================================
C
C      INTEGRE LE PRODUIT NT KL N
C          POUR LES ELEMENTS HOMOGENEISES TRIH
C
C      ROUTINE FORTRAN PUR
C
C=======================================================================
C     NBDL = NOMBRE DE DDL PAR NOEUD
C INPUT
C     XGENE(NBNO,NST)=MATRICE N
C     DJAC =JACOBIEN
C     VKL1=(COEFPR**2)/(RHO*C**2)*SFLU/SCEL
C     VKL2= EI/((EPS**2)*SCEL)
C     LRE=TAILLE DE LA MATRICE DE RIGIDITE
C     NST=NOMBRE DE COLONNES DE LA MATRICE XGENE
C     NBNO=NOMBRE DE NOEUDS DE L ELEMENT CONSIDERE
C     IFOU=IFOUR DE CCOPTIO
C OUTPUT
C     REL=MATRICE DE RIGIDITE
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION REL(LRE,*),XGENE((NBNO+NBNO),*)
      NBDL=LRE/NBNO
C
C
      DJAC = ABS(DJAC)
C
C     TERMES EN P*P
C
      IX1=0
      IY1=0
      DO   300 IX=1,LRE ,NBDL
      IX1=IX1 + 1
      DO   400 IY=1,IX  ,NBDL
      IY1=IY1 + 1
      REL(IX,IY) = REL(IX,IY) + DJAC*VKL1*XGENE(IX1,1)*XGENE(IY1,1)
      REL(IY,IX) = REL(IX,IY)
  400 CONTINUE
      IY1=0
  300 CONTINUE
C
C     CAS     AXISYMETRIQUE  OU  FOURIER   (  NST  = 2  )
C
C     TERMES EN (UR,RT )*(UR,RT )
C
      IX1=0
      IY1=0
      DO   302 IX=3,LRE ,NBDL
      IX1=IX1 + 1
      DO   402 IY=3,IX  ,NBDL
      IY1=IY1 + 1
      REL(IX,IY) = REL(IX,IY) + DJAC*VKL2*XGENE(IX1,2)*XGENE(IY1,2)
      REL(IX+1,IY) = REL(IX+1,IY) + DJAC*VKL2*XGENE(IX1+4,2)
     #*XGENE(IY1,2)
      REL(IX,IY+1) = REL(IX,IY+1) + DJAC*VKL2*XGENE(IX1,2)
     #*XGENE(IY1+4,2)
      REL(IX+1,IY+1) = REL(IX+1,IY+1) + DJAC*VKL2*XGENE(IX1+4,2)*
     #XGENE(IY1+4,2)
      REL(IY,IX) = REL(IX,IY)
      REL(IY,IX+1) = REL(IX+1,IY)
      REL(IY+1,IX) = REL(IX,IY+1)
      REL(IY+1,IX+1) = REL(IX+1,IY+1)
  402 CONTINUE
      IY1=0
  302 CONTINUE
C
      IF(IFOU.EQ.1)  THEN
C
C     TERMES EN (UT,RR )*(UT,RR )
C     NON  NULS  QUE  DANS LE  CAS  FOURIER
C
      IX1=0
      IY1=0
      DO   303 IX=5,LRE ,NBDL
      IX1=IX1 + 1
      DO   403 IY=5,IX  ,NBDL
      IY1=IY1 + 1

      REL(IX,IY) = REL(IX,IY) + DJAC*VKL2*XGENE(IX1,2)*XGENE(IY1,2)
      REL(IX+1,IY) = REL(IX+1,IY) + DJAC*VKL2*XGENE(IX1+4,2)
     #*XGENE(IY1,2)*(-1.D0)
      REL(IX,IY+1) = REL(IX,IY+1) + DJAC*VKL2*XGENE(IX1,2)
     #*XGENE(IY1+4,2)*(-1.D0)
      REL(IX+1,IY+1) = REL(IX+1,IY+1) + DJAC*VKL2*XGENE(IX1+4,2)*
     #XGENE(IY1+4,2)
      REL(IY,IX) = REL(IX,IY)
      REL(IY,IX+1) = REL(IX+1,IY)
      REL(IY+1,IX) = REL(IX,IY+1)
      REL(IY+1,IX+1) = REL(IX+1,IY+1)
  403 CONTINUE
      IY1=0
  303 CONTINUE
C
      ENDIF
      RETURN
      END


