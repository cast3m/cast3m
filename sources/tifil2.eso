C TIFIL2    SOURCE    PV        16/03/23    21:15:08     8864
C TIFIL2    SOURCE    AM        94/02/25    22:55:47     1061
      SUBROUTINE TIFIL2(RP,XL,LHOOK,DDHOOK)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE DE TIMOSHENKO MULTI-FIBRE EN AXES LOCAUX
C    "D APRES TEDEL"
C
C  ENTREE
C    XL      LONGUEUR
C    DDHOOK(LHOOK,LHOOK) MATRICE DE HOOK
C
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 12X12
C
C D'APRES TIFILO DC 98
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(6,*)
      DIMENSION DDHOOK(LHOOK,*)
C
C  INITIALISATIONS
C
      DO 120 I=1,6
      DO 120 J=1,6
  120 RP(I,J)=0.D00
C
      RL=1.D00/XL
C
C  REMPLISSAGE DE LA MATRICE
C  FX1
      RP( 1, 1)=DDHOOK( 1, 1)*RL
      RP( 1, 3)=DDHOOK( 1, 3)*RL
      RP( 1, 4)=-RP( 1, 1)
      RP( 1, 6)=-RP( 1, 3)
C  FY1
      RP( 2, 2)=DDHOOK( 2, 2)*RL
      RP( 2, 3)=DDHOOK( 2, 2)/2.0D0
      RP( 2, 5)=-RP( 2, 2)
      RP( 2, 6)= RP( 2, 3)
C  MZ1
      RP( 3, 3)=DDHOOK( 3, 3)*RL+DDHOOK( 2, 2)/(4.0D0*RL)
      RP( 3, 4)=-DDHOOK( 3, 1)*RL
      RP( 3, 5)=-DDHOOK( 2, 2)/2.0D0
      RP( 3, 6)=-DDHOOK( 3, 3)*RL+DDHOOK( 2, 2)/(4.0D0*RL)
C  FX2
      RP( 4, 4)=RP( 1, 1)
      RP( 4, 6)=-RP( 1, 6)
C  FY2
      RP( 5, 5)=RP( 2, 2)
      RP( 5, 6)=-RP( 2, 3)
C  MZ2
      RP( 6, 6)=RP( 3, 3)
C
      DO 9 I=1,5
      DO 9 J=I+1,6
   9  RP(J,I)=RP(I,J)
      RETURN

      END



