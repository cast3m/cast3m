C CHAMBO    SOURCE    CHAT      05/01/12    21:54:56     5004
      SUBROUTINE CHAMBO(IUN,IC,NB,O,RI,ALFA,BETA,DENS,ZP,RP,BZP,BRP,BP)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C---------------------------------------------------------------------
CALCUL DU CHAMP DE NB BOBINES RECTANGULAIRES COAXIALES
C     IUN INDIQUE LES UNITES CHOISIES:
C                             IUN=0 OU 1     IUN=2           IUN=3
C     CHAMP EN                   TESLA       TESLA           GAUSS
C     DIMENSIONS EN                M          MM              MM
C     DENSITE DE COURANT EN       A/M2       A/MM2           A/MM2
C     FMUJ=MU*DENS/2*XPI        2E-7*DENS    2E-4*DENS        2*DENS
C
C     IC=1  CALCUL DE LA COMPOSANTE BZP SEULE
C     IC=2  CALCUL DE LA COMPOSANTE BRP SEULE
C     IC#1#2 CALCUL DES 2 COMPOSANTES BZP ET BRP
C---------------------------------------------------------------------
      EXTERNAL FONZ,FONR
      DIMENSION O(NB),RI(NB),ALFA(NB),BETA(NB),DENS(NB)
-INC CCREEL
-INC CCOPTIO
      COMMON/CMCHAM/ D1,D2,R,A,U,DD1,DD2,LSOMD,LD1,LD2
      DIMENSION BZ(45),BR(45)
      LOGICAL LSOMD,LD1,LD2
      LOGICAL LR
      PI=XPI
      DO 1 I=1,NB
         BZ(I)=0.D0
         BR(I)=0.D0
 1    CONTINUE
      BZP=0
      BRP=0
      BP=0.D0
      PREBZ=0.D0
      PREBR=0.D0
      IF((IC.EQ.2).AND.(RP.EQ.0.D0)) RETURN
      UPSI=0.000001D0
      U=1.D0
      UU=1.D0
      FMU=0.0000002D0
      IF(IUN.EQ.2) FMU=0.0002D0
      IF(IUN.EQ.3) FMU=2.D0
      DO 2 I=1,NB
         FMUJ=FMU*DENS(I)
         RMUJ=FMUJ*RI(I)
         Z=(ZP-O(I))/RI(I)
         D1=Z-BETA(I)
         D2=Z+BETA(I)
         R=RP/RI(I)
         A=ALFA(I)
         DD1=D1**2
         DD2=D2**2
         AA=A**2
         LR=R.LE.UPSI
         LSOMD=ABS(D1+D2).LE.UPSI
         LD1=ABS(D1).LE.UPSI
         LD2=ABS(D2).LE.UPSI
CALCUL DE LA COMPOSANTE AXIALE BZP
         IF(IC.EQ.2) GOTO 222
         ACBZ=0.D0
         IF(.NOT.LR) GOTO 11
 10      SOMLG2= LOG((A+SQRT(AA+DD2))/(U+SQRT(UU+DD2)))
         SOMLG1= LOG((A+SQRT(AA+DD1))/(U+SQRT(UU+DD1)))
         SOMBZ=XPI*(D2*SOMLG2-D1*SOMLG1)
         GOTO 12
 11      CONTINUE
         SOMBZ= GQUAD(FONZ,0.D0,PI,80)
         IF(IERR.NE.0) RETURN
 12      CONTINUE
         BZ(I)=RMUJ*SOMBZ
         PREBZ=PREBZ+ABS(ACBZ*BZ(I))
         BZP=BZP+BZ(I)
         IF(IC.EQ.1) GOTO 2
 222     CONTINUE
CALCUL DE LA COMPOSANTE RADIALE BRP
         IF(LR) GOTO 2
         IF(LSOMD) GOTO 2
         ACBR=0.D0
         SOMBR= GQUAD(FONR,0.D0,PI,80)
         IF(IERR.NE.0) RETURN
         BR(I)=RMUJ*SOMBR
         PREBR=PREBR+ABS(ACBR*BR(I))
         BRP=BRP+BR(I)
 2    CONTINUE
      IF(BZP.NE.0.D0) PREBZ=PREBZ/ABS(BZP)
      IF(BRP.NE.0.D0) PREBR=PREBR/ABS(BRP)
CALCUL DU MODULE DU CHAMP BP
      BP=SQRT(BZP**2+BRP**2)
      RETURN
      END







