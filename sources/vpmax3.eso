C VPMAX3    SOURCE    CHAT      05/01/13    04:09:49     5004
      SUBROUTINE VPMAX3 (SIG,SIGMAX,VECP)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
-INC CCOPTIO
C
      DIMENSION SIG(*),VECP(*),A(3,3),D(3),S(3,3)
C
C  CETTE ROUTINE SERT A CALCULER LA VALEUR MAX DE SIG ET LE
C  VECTEUR PROPRE QUI CORRESPOND
C  ENTREES :
C          SIG   : TENSEUR DE CONTRAINTES
C          IIMPI : INDICE D IMPRESSION INTERNE
C  SORTIES :
C          SIGMAX : CONTRAINTE MAX
C          VECP  : VECTEUR PROPRE CORRESPONDANT
C
C     INITIALISATION
C
      A(1,1)=SIG(1)
      A(2,2)=SIG(2)
      A(3,3)=SIG(3)
      A(1,2)=SIG(4)
      A(2,1)=SIG(4)
      A(2,3)=SIG(5)
      A(3,2)=SIG(5)
      A(3,1)=SIG(6)
      A(1,3)=SIG(6)
C
      CALL JACOB3(A,3,D,S)
C
C CALCUL DE LA VALEUR PROPRE MAXI ET SON VECTEUR PROPRE
C
      VECP(1)=S(1,1)
      VECP(2)=S(2,1)
      VECP(3)=S(3,1)
      SIGMAX=D(1)
C
      DO 1 I=2,3
      IF(SIGMAX.GE.D(I)) GO TO 1
      SIGMAX=D(I)
      DO 2 J=1,3
      VECP(J)=S(J,I)
    2 CONTINUE
    1 CONTINUE
C
      DO 3 I=1,3
      IF(ABS(VECP(I)).LT.1.D-9) VECP(I)=0.D0
    3 CONTINUE
C
      IF(IIMPI.EQ.9)
     .  WRITE(IOIMP,1000) (SIG(I),I=1,6),SIGMAX,(VECP(I),I=1,3),
     .                    ((A(I,J),J=1,3),I=1,3),(D(I),I=1,3),
     .                    ((S(I,J),J=1,3),I=1,3)
 1000 FORMAT(1X,'SIG =',6(3X,D12.5),/,1X,'SIGMAX=',D12.5,/,
     .1X,'VECP=',3(3X,D12.5),/,1X,'AAA =',3(3(3X,D12.5),/),
     .1X,'DDD =',3(3X,D12.5),/,1X,'SSS =',3(3(3X,D12.5),/))
C
      RETURN
      END

