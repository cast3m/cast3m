C TIMKCE    SOURCE    CHAT      05/01/13    03:40:24     5004
                SUBROUTINE TIMKCE(REL,LRE,CARAC,COORD,VROT,WORK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DE LA MATRICE DE RAIDEUR CENTRIFUGE
C  ELEMENTAIRE DE L ELEMENT DE
C  POUTRE DE TIMOSCHENKO DANS LES AXES GLOBAUX
C
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          CARAC   TABLEAU DE CARACTERISTIQUES GEOMETRIQUES ET
C                  MATERIELLES DE LA POUTRE  ( 10 VALEURS )
C          COORD   COORDONNEES DES 2 NOEUDS
C
C  SORTIE
C          REL     MATRICE DE MASSE ELEMENTAIRE
C          KERRE   INDICE NON NUL SI PROBLEME , NUL SINON
C  TRAVAIL
C          WORK(300)
C
C      Didier COMBESCURE Mars 2003
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCREEL
      DIMENSION REL(LRE,*),CARAC(*),COORD(3,*),VECT(3),P(3,3),
     .          XE(2),YE(2),ZE(2),WORK(*),VROT(3),VROTL(3)
C
      KERRE=0
      XINY=CARAC(2)
      XINZ=CARAC(3)
C
C
      XINX=XINY+XINZ
      SD=CARAC(4)
      DO 1 I=1,3
1     VECT(I)=CARAC(7+I)
      RHO=CARAC(11)
C      OME=CARAC(7)
C
      DO 2 I=1,2
      XE(I)=COORD(1,I)
      YE(I)=COORD(2,I)
  2   ZE(I)=COORD(3,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2+(ZE(2)-ZE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPAS(XE,YE,ZE,VECT,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C   CALCUL DU VECTEUR ROTATION DANS LE REPERE LOCAL
C
      CALL VECROT(VROT,VROTL,P)
C
C  CALCUL DE LA MATRICE DE RAIDEUR EN AXES LOCAUX
C
      CALL TIKCLO(WORK,XL,RHO,SD,XINX,XINY,XINZ,VROTL)
C
C  PASSAGE DE LA MATRICE DE RAIDEUR EN AXES GLOBAUX
C
      CALL POUROG(REL,LRE,P,WORK)
C
      RETURN
999   KERRE=1
      RETURN
      END


