C CLI162    SOURCE    PV        16/11/17    21:58:37     9180
      SUBROUTINE CLI162(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM,ICHPVO,
     &     ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC,ILIINP,IJAC,IJACO)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  CLI162
C
C DESCRIPTION       :  Subroutine appellée par CLIM11
C
C LANGAGE           :  FORTRAN 77 + ESOPE 2000 (avec estensions CISI)
C
C AUTEUR            :  A. BECCANTINI, DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C APPELES (Calcul)  :
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :
C
C************************************************************************
C
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC SMLMOTS
-INC SMELEME
      POINTEUR MELEFC.MELEME
-INC SMLENTI
      POINTEUR MLEMC.MLENTI, MLEMCB.MLENTI,MLEMF.MLENTI
-INC SMCHPOI
      POINTEUR MPNORM.MPOVAL, MPVOL.MPOVAL, MPSURF.MPOVAL, MPRC.MPOVAL,
     &     MPVC.MPOVAL, MPPC.MPOVAL, MPGAMC.MPOVAL, MPLIM.MPOVAL
-INC SMMATRIK
      POINTEUR RR.IZAFM, RUX.IZAFM, RUY.IZAFM, RRET.IZAFM,
     &         UXR.IZAFM, UXUX.IZAFM, UXUY.IZAFM, UXRET.IZAFM,
     &         UYR.IZAFM, UYUX.IZAFM, UYUY.IZAFM, UYRET.IZAFM,
     &         RETR.IZAFM, RETUX.IZAFM, RETUY.IZAFM, RETRET.IZAFM
C
C**** Variables de COOPTIO
C
C      INTEGER IPLLB, IERPER, IERMAX, IERR, INTERR
C     &        ,IOTER,   IOLEC,  IOIMP,     IOCAR,  IOACQ
C     &        ,IOPER,   IOSGB,  IOGRA,     IOSAU,  IORES
C     &        ,IECHO,   IIMPI,  IOSPI
C     &        ,IDIM,    IFICLE, IPREFI
C     &        ,MCOORD
C     &        ,IFOMOD, NIFOUR, IFOUR, NSDPGE, IONIVE
C     &        ,NGMAXY, IZROSF, ISOTYP, IOSCR,LTEXLU
C     &        ,NORINC,NORVAL,NORIND,NORVAD
C     &        ,NUCROU, IPSAUV
C
      INTEGER MELEMF,MELEMC,MELECB,INORM,ICHPVO,ICHPSU, IROC,IVITC,IPC
     &     ,IGAMC,ICHLIM,ICEL,NFAC,IFAC,MELRES,IJACO
     &     ,NGF,NGC,NLF,NLC,NLCB
     &     ,ILIINC,ILIINP,IJAC
     &     ,MP, NBEL, NBME, NBSOUS, NKID, NKMT, NMATRI, NP, NRIGE
      REAL*8 VOLU,SURF,RC,PC,UXC,UYC,GAMC,CNX,CNY,CTX,CTY
     &     ,HTF,SF,UTF,RF,PF,UXF,UYF,GM1,USGM1
     &     ,UNC,UNF,ECIN,PSRF,DPSRUN,DRDUN,DPDUN,DUNDUX,DUNDUY
C     &     ,CACCA,EPS,CACCA1,CACCA2
     &     ,VECG(4),VECD(4),NVEC(2),TVEC(2),DFDUG(4,4),DFDUD(4,4)
     &     ,DUXDUN,DUYDUN,COEF5,COEF
     &     ,DRORO,DROUX,DROUY,DROP
     &     ,DUXRO,DUXUX,DUXUY,DUXP
     &     ,DUYRO,DUYUX,DUYUY,DUYP
     &     ,DPRO,DPUX,DPUY,DPP
     &     ,COEF1,COEF2,COEF3
      CHARACTER*(8) TYPE
C
C
C**** KRIPAD pour la correspondance global/local
C
      CALL KRIPAD(MELEMC,MLEMC)
C     SEGINI MLEMC
      CALL KRIPAD(MELECB,MLEMCB)
C     SEGINI MLEMCB
      CALL KRIPAD(MELEMF,MLEMF)
C     SEGINI MLEMF
C
C**** CHPOINTs de la table DOMAINE
C
      CALL LICHT(INORM,MPNORM,TYPE,ICEL)
      CALL LICHT(ICHPVO,MPVOL,TYPE,ICEL)
      CALL LICHT(ICHPSU,MPSURF,TYPE,ICEL)
C
C**** LICHT active les MPOVALs en *MOD
C
C     SEGACT MPNORM*MOD
C     SEGACT MPOVSU*MOD
C     SEGACT MPOVOL*MOD
C
C
C**** CHPOINTs des variables
C
      CALL LICHT(IROC,MPRC,TYPE,ICEL)
      CALL LICHT(IVITC,MPVC,TYPE,ICEL)
      CALL LICHT(IPC,MPPC,TYPE,ICEL)
      CALL LICHT(IGAMC,MPGAMC,TYPE,ICEL)
      CALL LICHT(ICHLIM,MPLIM,TYPE,ICEL)
C
C     SEGACT *MOD
C     SEGACT *MOD
C     SEGACT *MOD
C     SEGACT *MOD
C     SEGACT *MOD
C
C
C**** Boucle sur le face pour le calcul des invariants de
C     Riemann et du flux
C
      SEGACT MELEFC
      NFAC=MELEFC.NUM(/2)
C
C**** Objet MATRIK
C
      NRIGE = 7
      NMATRI = 1
      NKID = 9
      NKMT = 7
C
      SEGINI MATRIK
      IJACO = MATRIK
      MATRIK.IRIGEL(1,1) = MELRES
      MATRIK.IRIGEL(2,1) = MELRES
C
C**** Matrice non symetrique
C
      MATRIK.IRIGEL(7,1) = 2
C
      NBME = 16
      NBSOUS = 1
      SEGINI IMATRI
      IF(IJAC.EQ.1)THEN
         MLMOTS=ILIINC
      ELSEIF(IJAC.EQ.2)THEN
         MLMOTS=ILIINP
      ENDIF
      SEGACT MLMOTS
      MATRIK.IRIGEL(4,1) = IMATRI
C
      IMATRI.LISPRI(1) = MLMOTS.MOTS(1)
      IMATRI.LISPRI(2) = MLMOTS.MOTS(2)
      IMATRI.LISPRI(3) = MLMOTS.MOTS(3)
      IMATRI.LISPRI(4) = MLMOTS.MOTS(4)
      IMATRI.LISPRI(5) = MLMOTS.MOTS(1)
      IMATRI.LISPRI(6) = MLMOTS.MOTS(2)
      IMATRI.LISPRI(7) = MLMOTS.MOTS(3)
      IMATRI.LISPRI(8) = MLMOTS.MOTS(4)
      IMATRI.LISPRI(9) = MLMOTS.MOTS(1)
      IMATRI.LISPRI(10) = MLMOTS.MOTS(2)
      IMATRI.LISPRI(11) = MLMOTS.MOTS(3)
      IMATRI.LISPRI(12) = MLMOTS.MOTS(4)
      IMATRI.LISPRI(13) = MLMOTS.MOTS(1)
      IMATRI.LISPRI(14) = MLMOTS.MOTS(2)
      IMATRI.LISPRI(15) = MLMOTS.MOTS(3)
      IMATRI.LISPRI(16) = MLMOTS.MOTS(4)
C
      SEGDES MLMOTS
      MLMOTS=ILIINC
      SEGACT MLMOTS
C
      IMATRI.LISDUA(1) = MLMOTS.MOTS(1)
      IMATRI.LISDUA(2) = MLMOTS.MOTS(1)
      IMATRI.LISDUA(3) = MLMOTS.MOTS(1)
      IMATRI.LISDUA(4) = MLMOTS.MOTS(1)
      IMATRI.LISDUA(5) = MLMOTS.MOTS(2)
      IMATRI.LISDUA(6) = MLMOTS.MOTS(2)
      IMATRI.LISDUA(7) = MLMOTS.MOTS(2)
      IMATRI.LISDUA(8) = MLMOTS.MOTS(2)
      IMATRI.LISDUA(9) = MLMOTS.MOTS(3)
      IMATRI.LISDUA(10) = MLMOTS.MOTS(3)
      IMATRI.LISDUA(11) = MLMOTS.MOTS(3)
      IMATRI.LISDUA(12) = MLMOTS.MOTS(3)
      IMATRI.LISDUA(13) = MLMOTS.MOTS(4)
      IMATRI.LISDUA(14) = MLMOTS.MOTS(4)
      IMATRI.LISDUA(15) = MLMOTS.MOTS(4)
      IMATRI.LISDUA(16) = MLMOTS.MOTS(4)
C
      SEGDES MLMOTS
      NBEL = NFAC
      NBSOUS = 1
      NP = 1
      MP = 1
      SEGINI   RR  , RUX , RUY , RRET ,
     &         UXR , UXUX , UXUY , UXRET ,
     &         UYR , UYUX , UYUY , UYRET ,
     &         RETR , RETUX , RETUY , RETRET
C
C**** Duale = IMATRI.LISDUA(1) = 'RN'
C     Primale = IMATRI.LISPRI(1) = 'RN'
C     -> IMATRI.LIZAFM(1,1) = RR
C
      IMATRI.LIZAFM(1,1) = RR
      IMATRI.LIZAFM(1,2) = RUX
      IMATRI.LIZAFM(1,3) = RUY
      IMATRI.LIZAFM(1,4) = RRET
      IMATRI.LIZAFM(1,5) = UXR
      IMATRI.LIZAFM(1,6) = UXUX
      IMATRI.LIZAFM(1,7) = UXUY
      IMATRI.LIZAFM(1,8) = UXRET
      IMATRI.LIZAFM(1,9) = UYR
      IMATRI.LIZAFM(1,10) = UYUX
      IMATRI.LIZAFM(1,11) = UYUY
      IMATRI.LIZAFM(1,12) = UYRET
      IMATRI.LIZAFM(1,13) = RETR
      IMATRI.LIZAFM(1,14) = RETUX
      IMATRI.LIZAFM(1,15) = RETUY
      IMATRI.LIZAFM(1,16) = RETRET
C
      SEGDES MATRIK
      SEGDES IMATRI
C
C**** Fin definition MATRIK
C
      DO IFAC=1,NFAC,1
         NGF=MELEFC.NUM(1,IFAC)
         NGC=MELEFC.NUM(2,IFAC)
         NLF=MLEMF.LECT(NGF)
         NLC=MLEMC.LECT(NGC)
         NLCB=MLEMCB.LECT(NGF)
         VOLU=MPVOL.VPOCHA(NLC,1)
         SURF=MPSURF.VPOCHA(NLF,1)
C        In CASTEM les normales sont sortantes
         CNX=-1*MPNORM.VPOCHA(NLF,1)
         CNY=-1*MPNORM.VPOCHA(NLF,2)
         CTX=-1.0D0*CNY
         CTY=CNX
C        Variables au centre
         RC=MPRC.VPOCHA(NLC,1)
         PC=MPPC.VPOCHA(NLC,1)
         UXC=MPVC.VPOCHA(NLC,1)
         UYC=MPVC.VPOCHA(NLC,2)
         GAMC=MPGAMC.VPOCHA(NLC,1)
         GM1=GAMC-1.0D0
         USGM1=1.0D0/GM1
C        Variables à la face
         HTF=MPLIM.VPOCHA(NLCB,1)
         SF=MPLIM.VPOCHA(NLCB,2)
         UTF=0.0D0
C
         UNC=(UXC*CNX)+(UYC*CNY)
         UNF=UNC
C
         UXF=UNF*CNX+UTF*CTX
         UYF=UNF*CNY+UTF*CTY
C
         ECIN=0.5D0*((UXF*UXF)+(UYF*UYF))
         PSRF=(GM1/GAMC)*(HTF-ECIN)
         RF=PSRF/SF
         RF=RF**(1.0D0/GM1)
         PF=SF*(RF**GAMC)
C
C******* Derivative of PF/RF,PF,RF,UXF,UYF with respect to UN
C
         DPSRUN=-1*(GM1/GAMC)*UNC
         DRDUN=USGM1*RF/PSRF*DPSRUN
         DPDUN=GAMC*PSRF*DRDUN
         DUXDUN=CNX
         DUYDUN=CNY
C
         DUNDUX=CNX
         DUNDUY=CNY
CC
CC******* Test
CC
C         CACCA=PF/RF
C         CACCA1=RF
C         CACCA2=PF
C         EPS=1.0D-6
C         UNC=UNC*(1+EPS)
CC
C         UNF=UNC
C         UXF=UNF*CNX+UTF*CTX
C         UYF=UNF*CNY+UTF*CTY
CC
C         ECIN=0.5D0*((UXF*UXF)+(UYF*UYF))
C         PSRF=(GM1/GAMC)*(HTF-ECIN)
C         RF=PSRF/SF
C         RF=RF**(1.0D0/GM1)
C         PF=SF*(RF**GAMC)
C         write(*,*) DPSRUN, ((PF/RF) - CACCA)/(EPS*UNC)
C         write(*,*) DRDUN, (RF - CACCA1)/(EPS*UNC)
C         write(*,*) DPDUN, (PF - CACCA2)/(EPS*UNC)
C
C
C******* Jacobian with respect to primitive variables
C
         VECG(1)=RF
         VECG(2)=UXF
         VECG(3)=UYF
         VECG(4)=PF
         VECD(1)=RC
         VECD(2)=UXC
         VECD(3)=UYC
         VECD(4)=PC
         NVEC(1)=CNX
         NVEC(2)=CNY
         TVEC(1)=CTX
         TVEC(2)=CTY
         CALL CONJP2(DFDUG,DFDUD,VECG,VECD,NVEC,TVEC,GAMC)
C
         COEF=SURF/VOLU
         DRORO=DFDUD(1,1)*COEF
         COEF5=(DFDUG(1,1)*DRDUN)+(DFDUG(1,2)*DUXDUN)+
     &        (DFDUG(1,3)*DUYDUN)+(DFDUG(1,4)*DPDUN)
         DROUX=(COEF5*DUNDUX+DFDUD(1,2))*COEF
         DROUY=(COEF5*DUNDUY+DFDUD(1,3))*COEF
         DROP=DFDUD(1,4)*COEF
C
         DPRO=DFDUD(4,1)*COEF
         COEF5=(DFDUG(4,1)*DRDUN)+(DFDUG(4,2)*DUXDUN)+
     &        (DFDUG(4,3)*DUYDUN)+(DFDUG(4,4)*DPDUN)
         DPUX=(COEF5*DUNDUX+DFDUD(4,2))*COEF
         DPUY=(COEF5*DUNDUY+DFDUD(4,3))*COEF
         DPP=DFDUD(4,4)*COEF
C
         DUXRO=DFDUD(2,1)*COEF
         COEF5=(DFDUG(2,1)*DRDUN)+(DFDUG(2,2)*DUXDUN)+
     &        (DFDUG(2,3)*DUYDUN)+(DFDUG(2,4)*DPDUN)
         DUXUX=(COEF5*DUNDUX+DFDUD(2,2))*COEF
         DUXUY=(COEF5*DUNDUY+DFDUD(2,3))*COEF
         DUXP =DFDUD(2,4)*COEF
C
         DUYRO=DFDUD(3,1)*COEF
         COEF5=(DFDUG(3,1)*DRDUN)+(DFDUG(3,2)*DUXDUN)+
     &        (DFDUG(3,3)*DUYDUN)+(DFDUG(3,4)*DPDUN)
         DUYUX=(COEF5*DUNDUX+DFDUD(3,2))*COEF
         DUYUY=(COEF5*DUNDUY+DFDUD(3,3))*COEF
         DUYP =DFDUD(3,4)*COEF
CC
CC******* Jacobian with respect to conservative variables
CC
         IF(IJAC.EQ.1)THEN

            COEF1=-1.0D0*UXC/RC
            COEF2=-1.0D0*UYC/RC
            COEF3=0.5D0*GM1*((UXC*UXC)+(UYC*UYC))
C
            RR.AM(IFAC,1,1)=DRORO+(DROUX*COEF1)+(DROUY*COEF2)+(DROP
     $           *COEF3)
            RUX.AM(IFAC,1,1)=(DROUX/RC)-((UXC*GM1)*DROP)
            RUY.AM(IFAC,1,1)=(DROUY/RC)-((UYC*GM1)*DROP)
            RRET.AM(IFAC,1,1)=GM1*DROP
C
            UXR.AM(IFAC,1,1)=DUXRO+(DUXUX*COEF1)+(DUXUY*COEF2)+(DUXP
     $           *COEF3)
            UXUX.AM(IFAC,1,1)=(DUXUX/RC)-((UXC*GM1)*DUXP)
            UXUY.AM(IFAC,1,1)=(DUXUY/RC)-((UYC*GM1)*DUXP)
            UXRET.AM(IFAC,1,1)=GM1*DUXP
C
            UYR.AM(IFAC,1,1)=DUYRO+(DUYUX*COEF1)+(DUYUY*COEF2)+(DUYP
     $           *COEF3)
            UYUX.AM(IFAC,1,1)=(DUYUX/RC)-((UXC*GM1)*DUYP)
            UYUY.AM(IFAC,1,1)=(DUYUY/RC)-((UYC*GM1)*DUYP)
            UYRET.AM(IFAC,1,1)=GM1*DUYP
C
            RETR.AM(IFAC,1,1)=DPRO+(DPUX*COEF1)+(DPUY*COEF2)+(DPP
     $           *COEF3)
            RETUX.AM(IFAC,1,1)=(DPUX/RC)-((UXC*GM1)*DPP)
            RETUY.AM(IFAC,1,1)=(DPUY/RC)-((UYC*GM1)*DPP)
            RETRET.AM(IFAC,1,1)=GM1*DPP
C
         ELSEIF(IJAC.EQ.2)THEN
            RR.AM(IFAC,1,1)=DRORO
            RUX.AM(IFAC,1,1)=DROUX
            RUY.AM(IFAC,1,1)=DROUY
            RRET.AM(IFAC,1,1)=DROP
C
            UXR.AM(IFAC,1,1)=DUXRO
            UXUX.AM(IFAC,1,1)=DUXUX
            UXUY.AM(IFAC,1,1)=DUXUY
            UXRET.AM(IFAC,1,1)=DUXP
C
            UYR.AM(IFAC,1,1)=DUYRO
            UYUX.AM(IFAC,1,1)=DUYUX
            UYUY.AM(IFAC,1,1)=DUYUY
            UYRET.AM(IFAC,1,1)=DUYP
C
            RETR.AM(IFAC,1,1)=DPRO
            RETUX.AM(IFAC,1,1)=DPUX
            RETUY.AM(IFAC,1,1)=DPUY
            RETRET.AM(IFAC,1,1)=DPP
         ENDIF
      ENDDO
C
      SEGDES MELEFC
C
      SEGSUP MLEMC
      SEGSUP MLEMCB
      SEGSUP MLEMF
C
      SEGDES MPNORM
      SEGDES MPVOL
      SEGDES MPSURF
      SEGDES MPRC
      SEGDES MPPC
      SEGDES MPVC
      SEGDES MPGAMC
      SEGDES MPLIM
C
      SEGDES   RR  , RUX , RUY , RRET ,
     &         UXR , UXUX , UXUY , UXRET ,
     &         UYR , UYUX , UYUY , UYRET ,
     &         RETR , RETUX , RETUY , RETRET
C
 9999 CONTINUE
      RETURN
      END











