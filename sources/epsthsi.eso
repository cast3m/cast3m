C EPSTHSI   SOURCE    CB215821  16/04/21    21:16:39     8920
        FUNCTION EPSTHSI(t,tmax)

c       Thermal elongation of siliceous concrete
c     according to equations A.1 and A.2 of ENV1992-1-2
c                same as     3.3.1 (1) of prEN 1992-1-2

      IMPLICIT REAL*8 (A-B,D-H,O-Z)
      implicit integer (I-K,M,N)
      implicit logical (L)
      implicit character*10 (C)

      precision = 1.0d-3
      r1 = 1.0d0
      r0 = 0.d0

      if (tmax.le.700.) then
        epsth = -1.80184d-4+tmax*(9.d-6+2.3d-11*tmax*tmax)
      else
        epsth = 14.d-3
      endif

c     Attention to the cases where T<20°C and Tmax=20°C
      if ( (t.lt.tmax) .and. (ABS(tmax-20.).ge.precision) ) then
        if (tmax.le.300.) then
          epsres = -.58d-3*(tmax-20.)/280.
        else if (tmax.le.400.) then
          epsres = -.58d-3+0.29d-3*(tmax-300.)/100.
        else if (tmax.le.600.) then
          epsres = -.29d-3+2.00d-3*(tmax-400.)/200.
        else if (tmax.le.800.) then
          epsres = 1.71d-3+1.58d-3*(tmax-600.)/200.
        else if (tmax.le.900.) then
          epsres = 3.29d-3+1.71d-3*(tmax-800.)/100.
        else
          epsres = 5.00d-3
        endif
        epsrevers = epsth-epsres
        dtmax = tmax-20.
        dt = t-20.
        dtsdtmax = dt/dtmax
        epsth = epsres+epsrevers*dtsdtmax*(.6+.4*dtsdtmax)
      endif

      epsthsi = epsth

      return
      end








