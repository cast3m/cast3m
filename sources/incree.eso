C INCREE    SOURCE    CHAT      05/01/13    00:35:37     5004
      SUBROUTINE INCREE (TAU,SIG,P,QTLD,DECRA,DESWA,EPSVPT,EC0PT,ESW0PT,
     &                  NSTRS1,IVIEX,MFR1,IFOURB)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C-----------------------------------------------------------------------
C
C     DESCRIPTION FONCTIONNELLE :
C     -------------------------
C
C     Lois 'VISCO_EXTERNE' : exploitation des sorties de CREEP pour le
C     calcul du tenseur des vitesses de deformation inelastique
C
C     Entrees : TAU,SIG,P,QTLD,DECRA,DESWA
C     Sorties : EPSVPT,EC0PT,ESW0PT
C
C     Parametres : NSTRS1,IVIEX,MFR1,IFOURB
C                  Les indicateurs IVIEX,MFR1,IFOURB ne servent pas
C                  pour l'instant mais seront utiles si extensions :
C                  autres modeles que 'GENERAL', autres formulations EF,
C                  autres options de calcul
C
C-----------------------------------------------------------------------
C     Arguments de l'interface
C
      REAL*8     SIG(*), DECRA(*), DESWA(*), EPSVPT(*)
C
C     Variables locales
C
C     Tableaux de travail
C
      REAL*8     SS(8)
C
C------------------- Debut du code executable --------------------------
C
C 1 - Initialisation du tenseur des vitesses de deformation inelastique
C
      DO 10 I=1,NSTRS1
         EPSVPT(I)=0.0D0
  10  CONTINUE
C
C 2 - Calcul des vitesses de deformation inelastique
C     (grandeurs scalaires)
C
      EC0PT = DECRA(1)/TAU
      ESW0PT = DESWA(1)/TAU
C
C 3 - Premiere contribution au tenseur des vitesses de deformation
C     inelastique : 1/3 * ESW0PT * [Id]
C
      CONS=0.333333333333333D0*ESW0PT
      DO 30 I=1,3
         EPSVPT(I)=EPSVPT(I)+CONS
  30  CONTINUE
C
C 4 - Seconde contribution au tenseur des vitesses de deformation
C     inelastique : (3/2)/QTLD * [SS]
C
      IF ( QTLD.LE.1.D-10 ) GOTO 1000
C
C     Calcul du deviateur des contraintes
C
      DO 40 I=1,NSTRS1
         A=0.0D0
         IF (I.LE.3) A=1.0D0
         SS(I)=SIG(I)-A*P
  40  CONTINUE
C
      CONS=(1.5D0*EC0PT)/QTLD
      DO 41 I=1,NSTRS1
         EPSVPT(I)=EPSVPT(I)+CONS*SS(I)
  41  CONTINUE
C
1000  CONTINUE
      RETURN
      END


