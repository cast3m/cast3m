C EQUA32    SOURCE    CHAT      05/01/12    23:44:25     5004
      SUBROUTINE EQUA32 (AA,B,  X,RESOLU)
************************************************************************
*
*                             E Q U A 3 2
*                             -----------
*
* FONCTION:
* ---------
*
*     RESOLUTION D'UN SYSTEME DE 3 EQUATIONS A 2 INCONNUES, DE RANG 2.
*
*     AA * X = B
*
* MODULES UTILISES:
* -----------------
*
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC CCREEL
*
* PARAMETRES:   (E)=ENTREE   (S)=SORTIE   (+ = CONTENU DANS UN COMMUN)
* -----------
*
*     AA      (E)  MATRICE 3X2 DU SYSTEME.
*     B       (E)  SECOND MEMBRE DU SYSTEME.
*     X       (S)  2 INCONNUES DU SYSTEME.
*     RESOLU  (S)  = .TRUE.  SI LA RESOLUTION A PU SE FAIRE.
*
      REAL*8 AA(3,2),B(3),X(2)
      LOGICAL RESOLU
*
* CONSTANTES:
* -----------
*
*     IND    = BIAIS POUR REPRESENTER UN INDICAGE CIRCULAIRE DE 1 A 3.
*     PRECIS = CRITERE DE PRECISION.
*
      INTEGER IND(5)
      REAL*8 PRECIS
      PARAMETER (PRECIS = 1.D-5)
*
* VARIABLES:
* ----------
*
*     DET    = DETERMINANT 2X2 DE LA MATRICE "AA".
*     DETMAX = PLUS GRAND DETERMINANT EN VALEUR ABSOLUE.
*
      REAL*8 DET,DETMAX,VERIF,B3,A1,A2
*
* AUTEUR, DATE DE CREATION:
* -------------------------
*
*     PASCAL MANIGOT     18 NOVEMBRE 1987
*
* LANGAGE:
* --------
*
*     FORTRAN77
*
************************************************************************
*
      DATA IND/1,2,3,1,2/
*
      I=1
      DETMAX = 0.D0
      DO 100 IB=1,3
         DET = AA(IND(IB),1) * AA(IND(IB+1),2)
     &       - AA(IND(IB+1),1) * AA(IND(IB),2)
         IF (ABS(DET) .GT. ABS(DETMAX) ) THEN
            DETMAX = DET
            I = IB
         END IF
  100    CONTINUE
*     END DO
*
*     ON RESOUT EN PRENANT LES 2 EQUATIONS CORRESPONDANT AU PLUS GRAND
*     DETERMINANT.
*
      X(1) = ( B(IND(I)) * AA(IND(I+1),2)
     &       - B(IND(I+1)) * AA(IND(I),2) ) / DETMAX
      X(2) = ( AA(IND(I),1) * B(IND(I+1))
     &       - AA(IND(I+1),1) * B(IND(I)) ) / DETMAX
*
*     ON VERIFIE LA 3EME EQUATION
*
      A1 = AA(IND(I+2),1) * X(1)
      A2 = AA(IND(I+2),2) * X(2)
      VERIF = A1 + A2
      B3 = B(IND(I+2))
      IF (IIMPI .EQ. 732) THEN
         WRITE (IOIMP,*) 'VERIF,B3,A1,A2'
         WRITE (IOIMP,*) VERIF,B3,A1,A2
      END IF
*
      IF (ABS(B3) .LT. XPETIT) THEN
         A1 = ABS(A1)
         A2 = ABS(A2)
         B3 = MAX(A1,A2) * PRECIS
         RESOLU = ABS(VERIF) .LE. B3
      ELSE
         VERIF = (VERIF - B3) / B3
         RESOLU = ABS(VERIF) .LE. PRECIS
      END IF
      IF (IIMPI .EQ. 732) THEN
         WRITE (IOIMP,*) 'VERIF'
         WRITE (IOIMP,*) VERIF
      END IF
*
      END



