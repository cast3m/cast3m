C ADVDDI    SOURCE    CB215821  16/04/21    21:15:06     8920
       SUBROUTINE ADVDDI(TAU,SIG,EPSV,VAR,SIG1,EPSV1,VAR1,DSPT,
     &            EPSVPT,VARPT,XMAT,NSTRS,NVARI,IFOUR,INPLAS,NCOMAT
     &            ,MFR)
      IMPLICIT INTEGER(I-N)
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION SIG(*),EPSV(*),VAR(*),SIG1(*),EPSV1(*),VAR1(*)
       DIMENSION DSPT(*),EPSVPT(*),VARPT(*),XMAT(*)
C ____________________________________________________________________
C
C    THIS ROUTINE UPDATES THE STRAINS ,STRESSES & INTERNAL VARIABLES
C ____________________________________________________________________
C
       DETIER=2.0D0/3.0D0
C  STEP-1 : UPDATING STRAINS
C ----------------------------
       YOU  =XMAT(1)
       XNU  =XMAT(2)
       DO 10 I=1,NSTRS
       EPSV1(I)=EPSV(I)+TAU*EPSVPT(I)
  10   CONTINUE
C
C  STEP-2 : UPDATING STESSES
C ----------------------------
       IF(IFOUR.EQ.-2) THEN
       SIG(3)=0.0D0
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       SIG1(1)=SIG(1)+TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(2)))
       SIG1(2)=SIG(2)+TAU*(DSPT(2)-XCO*(EPSVPT(2)+XNU*EPSVPT(1)))
       SIG1(4)=SIG(4)+TAU*(DSPT(4)-X2MU*EPSVPT(4))
       SIG1(3)=0.0D0
       ELSEIF (IFOUR.EQ.2.OR.IFOUR.EQ.0.OR.IFOUR.EQ.-1.OR.
     &         IFOUR.EQ.1.OR.IFOUR.EQ.-3) THEN
       X2MU = YOU/(1+XNU)
       ALMB = X2MU*XNU/(1-2*XNU)
       IF (MFR.EQ.5) THEN
       X2MU=YOU/(1.0+XNU)
       XCO = X2MU/(1.0-XNU)
       SIG1(1)=SIG(1)+TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(2)))
       SIG1(2)=SIG(2)+TAU*(DSPT(2)-XCO*(EPSVPT(2)+XNU*EPSVPT(1)))
       SIG1(3)=SIG(3)+TAU*(DSPT(3)-X2MU*EPSVPT(3))
       SIG1(4)=SIG(4)+TAU*(DSPT(4)-X2MU*EPSVPT(4))
       SIG1(5)=SIG(5)+TAU*(DSPT(5)-X2MU*EPSVPT(5))
       ELSE
        TRACE=EPSVPT(1)+EPSVPT(2)+EPSVPT(3)
        DO 20 I=1,NSTRS,1
        A = 0.0
        IF (I.LE.3) A=1.0
        SIG1(I) = SIG(I) +
     &  TAU*(DSPT(I)-ALMB*A*TRACE-X2MU*EPSVPT(I))
   20   CONTINUE
       ENDIF
C= Modes de calcul 1D contraintes planes suivant z (DYCZ et GYCZ)
       ELSE IF (IFOUR.EQ.4.OR.IFOUR.EQ.8) THEN
         SIG(3) = 0.
         XCO = YOU/(1.-XNU*XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(2)))
         SIG1(2) = SIG(2) + TAU*(DSPT(2)-XCO*(EPSVPT(2)+XNU*EPSVPT(1)))
         SIG1(3) = 0.
C= Modes de calcul 1D contraintes planes suivant y (CYDZ et CYGZ) et
C=                 1D axisymetrique contraintes axiales nulles (AXCZ)
       ELSE IF (IFOUR.EQ.5.OR.IFOUR.EQ.10.OR.IFOUR.EQ.13) THEN
         SIG(2) = 0.
         XCO = YOU/(1.-XNU*XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-XCO*(EPSVPT(1)+XNU*EPSVPT(3)))
         SIG1(2) = 0.
         SIG1(3) = SIG(3) + TAU*(DSPT(3)-XCO*(EPSVPT(3)+XNU*EPSVPT(1)))
C= Modes de calcul 1D contraintes planes suivant y et z (CYCZ)
       ELSE IF (IFOUR.EQ.6) THEN
         SIG(2) = 0.
         SIG(3) = 0.
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-YOU*EPSVPT(1))
         SIG1(2) = 0.
         SIG1(3) = 0.
C= Autres modes de calcul 1D deformations planes (DYDZ GYDZ DYGZ GYGZ)
C=                        1D axisymetrique (AXDZ AXGZ) et 1D spherique
       ELSE IF (IFOUR.EQ. 3.OR.IFOUR.EQ. 7.OR.IFOUR.EQ. 9.OR.IFOUR.EQ.11
     .      .OR.IFOUR.EQ.12.OR.IFOUR.EQ.14.OR.IFOUR.EQ.15) THEN
         X2MU = YOU/(1.+XNU)
         TRACE = EPSVPT(1)+EPSVPT(2)+EPSVPT(3)
         XCO = X2MU*XNU*TRACE/(1.-XNU)
         SIG1(1) = SIG(1) + TAU*(DSPT(1)-X2MU*EPSVPT(1)-XCO)
         SIG1(2) = SIG(2) + TAU*(DSPT(2)-X2MU*EPSVPT(2)-XCO)
         SIG1(3) = SIG(3) + TAU*(DSPT(3)-X2MU*EPSVPT(3)-XCO)
       ENDIF
C
C  STEP-3 : UPDATING THE INTERNAL VARIABLES
C------------------------------------------
       IF (MFR.EQ.5) THEN
         NSTRS0=NSTRS+1
       ELSE
         NSTRS0=NSTRS
       ENDIF
       DO 50 I=1,NVARI
       VAR1(I)=VAR(I)+TAU*VARPT(I)
   50  CONTINUE
       DO 60 I=1,NSTRS0
       VAR1(I)=DETIER*(XMAT(8)*VAR1(2*NSTRS+I)+XMAT(21)*VAR1(7*NSTRS+I))
       VAR1(NSTRS+I)=DETIER*(XMAT(9)*VAR1(3*NSTRS+I)+
     &                       XMAT(22)*VAR1(8*NSTRS+I))
       VAR1(5*NSTRS+I)=DETIER*(XMAT(17)*VAR1(7*NSTRS+I)+
     &                       XMAT(21)*VAR1(2*NSTRS+I))
       VAR1(6*NSTRS+I)=DETIER*(XMAT(18)*VAR1(8*NSTRS+I)+
     &                       XMAT(22)*VAR1(3*NSTRS+I))
 60    CONTINUE
       VAR1(10*NSTRS+2)=XMAT(6)*(1.D0-EXP(-XMAT(7)*VAR1(10*NSTRS+1)))
       VAR1(10*NSTRS+4)=XMAT(15)*(1.D0-EXP(-XMAT(16)*VAR1(10*NSTRS+3)))
C
C _____________________________________________________________________
       RETURN
       END





