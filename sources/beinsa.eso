C BEINSA    SOURCE    PV        05/04/25    21:15:01     5077
C BEINSA     SOURCE    INSL       24/10/96
      SUBROUTINE BEINSA(SIG0,NSTRSS,DEPST,VAR0,XMAT,IVAL,NMATT,
     1 SIGF,VARF,KERRE,MELE,IFOUR,NVARI,XCAR,NCARR,MFR,EPIN0,EPINF,
     2 DTEMP,XE,NBNN,CMATE,IBB,IGAU,wrk12)
C---------------------------------------------------------------------
C             PLASTICITE MODELE BETON
C
C ENTREES
C     SIG0(NSTRS)   = CONTRAINTES INITIALES
C     NSTRS         = NOMBRE DE CONTRAINTES
C     DEPST(NSTRS)  = INCREMENT DE DEFORMATION TOTALES
C     VAR0(NVARI)   =  VARIABLES INTERNES DEBUT
C     VAR0(  1  )   = EPPE    :Deformation plastique equivalente
C     VAR0(  2  )   = ALPT1   :Coef. multi. Defo. elas. au pic dir. 1
C     VAR0(  3  )   = ALPT2   :Coef. multi. Defo. elas. au pic dir. 2
C     VAR0(  4  )   = IPLA1   :Indicateur plas dir. 1
C     VAR0(  5  )   = IFISU2  :Indicateur de fissure actuelle  (0 ou 1)
C     VAR0(  6  )   = DTR1    :Resistance en traction dir. 1
C     VAR0(  7  )   = DTR2    :Resistance en traction dir. 2
C     VAR0(  8  )   = IFIS0   :Indicateur de fissuration etat precedent
C     VAR0(  9  )   = JFISU   :Indicateur de fissure  (0 ou 1)
C     VAR0( 10  )   = ANGL    :Angle de fissuration
C     VAR0( 11  )   = IPLA    :Indicateur d'etat en bicompres. (0 2 4)
C     VAR0( 12  )   = EQSTRC1 :Contrainte  equiv. de compres. dir. 1
C     VAR0( 13  )   = EPSEQC1 :Deformation equiv. de compres. dir. 1
C     VAR0( 14  )   = EQSTRT1 :Contrainte  equiv. de traction dir. 1
C     VAR0( 15  )   = EPSEQT1 :Deformation equiv. de traction dir. 1
C     VAR0( 16  )   = EQSTRC2 :Contrainte  equiv. de compres. dir. 2
C     VAR0( 17  )   = EPSEQC2 :Deformation equiv. de compres. dir. 2
C     VAR0( 18  )   = EQSTRT2 :Contrainte  equiv. de traction dir. 2
C     VAR0( 19  )   = EPSEQT2 :Deformation equiv. de traction dir. 2
C     VAR0( 20  )   = IDECH1  :Indicateur de decharge beton intergre
C     VAR0( 21  )   = OUV1    :Indicateur ouverture fissure 1
C     VAR0( 22  )   = EDC1    :Module (d'elasti.) initiale equiva. dir.1
C     VAR0( 23  )   = EDC2    :Module (d'elasti.) initiale equiva. dir.2
C     VAR0( 24  )   = ETS1    :Pente courbe de traction (Post-pic)   dir.1
C     VAR0( 25  )   = ETS2    :Pente courbe de traction (Post-pic)   dir.2
C     VAR0( 26  )   = EPF01   :Deformation au pic courbe de traction dir.1
C     VAR0( 27  )   = EPF02   :Deformation au pic courbe de traction dir.2
C     VAR0( 28  )   = OUV2    :Indicateur ouverture fissure 2
C     VAR0( 29  )   = DEFR1   :defo residuelle en compres.1 (beton fissure)
C     VAR0( 30  )   = DEFR2   :defo residuelle en compres.2 (beton fissure)
C     VAR0( 31  )   = IPAS0   :Initial. variables internes 0=oui/1=non
C     VAR0( 32  )   = EPSR(1) :Deform. initi. EpsX (repere local elem.)
C     VAR0( 33  )   = EPSR(2) :Deform. initi. EpsY      ...
C     VAR0( 34  )   = EPSR(3) :Deform. initi. GamXY     ...
C     VAR0( 35  )   = EPSR(4) :Deform. initi. GamXZ     ...
C     VAR0( 36  )   = EPSR(5) :Deform. initi. GamYZ     ...
C     VAR0( 37  )   = DEP(1,1):Terme de matrice elastoplastique tangente
C     VAR0( 38  )   = DEP(1,2):
C     VAR0( 39  )   = DEP(1,3):
C     VAR0( 40  )   = DEP(1,4):
C     VAR0( 41  )   = DEP(1,5):
C     VAR0( 42  )   = DEP(2,1):
C     VAR0( 43  )   = DEP(2,2):
C     VAR0( 44  )   = DEP(2,3):
C     VAR0( 45  )   = DEP(2,4):
C     VAR0( 46  )   = DEP(2,5):
C     VAR0( 47  )   = DEP(3,1):
C     VAR0( 48  )   = DEP(3,2):
C     VAR0( 49  )   = DEP(3,3):
C     VAR0( 50  )   = DEP(3,4):
C     VAR0( 51  )   = DEP(3,5):
C     VAR0( 52  )   = DEP(4,1):
C     VAR0( 53  )   = DEP(4,2):
C     VAR0( 54  )   = DEP(4,3):
C     VAR0( 55  )   = DEP(4,4):
C     VAR0( 56  )   = DEP(4,5):
C     VAR0( 57  )   = DEP(5,1):
C     VAR0( 58  )   = DEP(5,2):
C     VAR0( 59  )   = DEP(5,3):
C     VAR0( 60  )   = DEP(5,4):
C     VAR0( 61  )   = DEP(5,5):
C     XMAT(NMATT)  =  COMPOSANTES DE MATERIAU
C     IVAL(NMATT)  =  INDICE DES COMPOSANTES DE MATERIAU
C     NMATT        = NOMBRE DE COMPOSANTES DE MATERIAU
C  SORTIES
C     SIGF(NSTRS)   = CONTRAINTES FINALES
C     VARF(NVARI)   = VARIABLES INTERNES FINALES
C     DEFP(NSTRS)   = DEFORMATIONS PLASTIQUES
C     KERRE         = 0    TOUT OK
C---------------------------------------------------------------------
C      VARIABLES PASSEES PAR LES COMMONS COPTIO , ECOU  ET NECOU
C
C  IFOUR    INDICE DU TYPE DE PROBLEME
C            -2  CONTRAINTES PLANES
C            -1  DEFORMATIONS PLANES
C             0  AXISYMETRIQUE
C             1  SERIE DE FOURIER
C             2  TRIDIMENSIONNEL
C---------------------------------------------------------------------
C        COMPOSANTES DE MATERIAU
C=====================================================================
C  EX   : Module d'Young
C  XNU  : Coeficient de Poisson
C  RHO  : Masse volumique (Facultatif)
C  ALPH : Coeficient de dilation thermique (Facultatif)
C  ALPHA: Resis. tract. simple / resis. compr. simple
C  RB   : Resis. compr. simple
C  EMAX : Deformation ultime en compression (sigma = 0)
C  EPUT : Deformation ultime en traction (sigma = 0)
C  FTC  : Facteur de transfert de cisaillement
C
C  ICAL : Choix du comportement Pre-pic
C  ICAL    0 = Comportement elastoplastique ecrouissable
C          1 = Comportement elastique
C
C  ICU  : Choix de la courbe de compression (Calibrage)
C          1 =  Parabole pre-pic et post-pic
C          2 =  Parabole pre-pic et lineaire post-pic
C          3 =  courbe INSA pre-pic et lineaire post-pic
C          5 =  courbe de Krishnan pre-pic et  post-pic
C          4 =  courbe de Krishnan pre-pic et lineaire post-pic
C
C  ILOI : Choix de la loi d'ecoulement
C          0 = Plasticite Associee
C          1 = Plasticite Non Associee
C
C  IOUV : Nombre d'iteration interne
C
C  IMOD : Choix du modele Beton
C          0 = Modele BETON_INSA ( critere d'OTTOSEN)
C          1 = Modele BETON_INSA ( critere de NADAI partout)
C          2 = Modele BETON_INSA ( NADAI + Sigma Max en trac.)
C
C---------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      CHARACTER*8 CMATE
      DIMENSION SIG0(NSTRSS),DEPST(NSTRSS),VAR0(NVARI),XMAT(NMATT)
      DIMENSION IVAL(*),SIGF(NSTRSS),VARF(NVARI),XE(3,NBNN)
      DIMENSION EPIN0(NSTRSS),EPINF(NSTRSS),DEPIN(6),EPFLU(6)
      DIMENSION STRN(6),SIGM(6),D(6,6),D1(6,6),D2(6,6),XCAR(NCARR)
      DIMENSION EPSR(6),SIGR(6),VART(100),VV(36)


C
       segment wrk12
       real*8 AA,BB,DK1,DK2,RB,ALPHA,EX,XNU,EMAX
       real*8 EPUT,FTC,EPO,EPO1,ENGF,RMOY,PHIF,TEMP0
       real*8 DTEMP1,TEMP1,POAR,SCT,TETA,DTR1,DTR2,EDC1
       real*8 EDC2,ETS1,ETS2,EDT1,EDT2,OUV1,OUV2,TANG1
       real*8 TANG2,DEFR1,DEFR2,EPSC1,EPSC2,EPST1,EPST2,EQSTR1
       real*8 EQSTR2,EPSEQ1,EPSEQ2,EQSTR3,EPSEQ3,EPST3,EPSC3,DEFR3
       real*8 RTM3,EDC3,ETS3,EDT3,OUV3,TANG3
       integer ICU,ILOI,IOUV,ICAL,IFLU,IPLA2,IPLA1,IFISU2
       integer IFISU1,JFISU,JFISU2,IPLA3,IFISU3,JFISU3,IBB1,IGAU1
       endsegment
c                     1  2  3   4   5   6    7  8   9    10   11  12
c      COMMON /CINSA/ AA,BB,DK1,DK2,RB,ALPHA,EX,XNU,EMAX,EPUT,FTC,EPO,
c             13    14  15   16    17    18    19    20   21   22
c     1       EPO1,ENGF,RMOY,PHIF,TEMP0,DTEMP1,TEMP1,POAR,SCT,TETA,
c             23   24   25   26   27   28   29   30   31   32   33
c     2       DTR1,DTR2,EDC1,EDC2,ETS1,ETS2,EDT1,EDT2,OUV1,OUV2,TANG1,
c             34     35    36   37    38    39     40    41     42
c     3       TANG2,DEFR1,DEFR2,EPSC1,EPSC2,EPST1,EPST2,EQSTR1,EQSTR2,
C              43     44     45    46     47    48     49    50   51
c     4       EPSEQ1,EPSEQ2,EQSTR3,EPSEQ3,EPST3,EPSC3,DEFR3,RTM3,EDC3,
c             52   53   54    55
c     5       ETS3,EDT3,OUV3,TANG3,
C             1    2   3     4    5    6     7     8      9
c     6       ICU,ILOI,IOUV,ICAL,IFLU,IPLA2,IPLA1,IFISU2,IFISU1,
C             10     11     12     13    14    15    16
c    7       JFISU,JFISU2,IPLA3,IFISU3,JFISU3,IBB1,IGAU1
*
      IRTD =1
      IBB1 =IBB
      IGAU1=IGAU
C
C TEST DE CONSISTANCE DES DONNEES
C
      EX   = XMAT( 1)
      XNU  = XMAT( 2)
      RHO  = XMAT( 3)
      ALPH = XMAT( 4)
      IKA=4
      IF((MFR.EQ.1.OR.MFR.EQ.3.OR.MFR.EQ.31).AND.IFOUR.EQ.-2) IKA=5
*
      IFOU=IFOUR
      IF(MELE.EQ.28.OR.MFR.EQ.3) IFOU=-2
C
      ALPHA=0.1D0
      RB   =EX*1.D-3
      EMAX =10.D0*RB/EX
      EPUT =3.D0*RB*ALPHA/EX
      FTC  =0.1D0
      ICU  =3
      ILOI =0
      IOUV =2
      IFLU =0
      PHIF =2.D0
      TEMP1=0.D0
      POAR =0.D0
      ENGF =0.D0
*
      ICAL  = 0
      DTEMP1=DTEMP
C
      AA=0.D0
      BB=0.D0
      DK1=0.D0
      DK2=0.D0
*
      ALPHA= XMAT(IKA+1)
      RB   = XMAT(IKA+2)
      EMAX = XMAT(IKA+3)
      EPUT = XMAT(IKA+4)
      FTC  = XMAT(IKA+5)
      ICAL = INT(REAL(XMAT(IKA+6)))
      ENGF = XMAT(IKA+7)
*
      IFLU = INT(REAL(XMAT(IKA+8)))
      RMOY = XMAT(IKA+9)
      PHIF = XMAT(IKA+10)
      TEMP1= XMAT(IKA+11)
      POAR = XMAT(IKA+12)
      EPO  = 2.4D0*RB/EX
C
C       VALEURS PAR DEFAUT
C
      IF(MELE.EQ.4.OR.MELE.EQ.8.OR.MELE.EQ.6.OR.MELE.EQ.10) THEN
         IF(IVAL(IKA+7).EQ.0.AND.IVAL(IKA+4).EQ.0) ENGF =.15D0
      ELSE
         ENGF =.0D0
      ENDIF
*
      IF(IVAL(IKA+8).EQ.0) IFLU=0
      IF(IVAL(IKA+10).EQ.0) PHIF=2.D0
      IF(IVAL(IKA+11).EQ.0) TEMP1=0.D0
      IF(IVAL(IKA+12).EQ.0) POAR =.0D0
C---------------------------------------------------------------------
      IECR=0
      IF(IECR.EQ.10) THEN
*      WRITE(*,*) ' ** XMAT='
*      WRITE(*,1991) (XMAT(I),I=1,NMATT)
*      WRITE(*,*) ' ** IVAL='
*      WRITE(*,1992) (IVAL(I),I=1,NMATT)
      WRITE(*,404) EX,XNU,RHO,ALPH,ALPHA,RB,EMAX,EPUT,FTC,ICAL,ENGF
404   FORMAT('EX=',E9.3,' XNU=',E9.3,' RHO=',E9.3,' ALPH=',E9.3,
     *' ALPHA=',E9.3,/,' RB=',E9.3,' EMAX=',E9.3,' EPUT=',E9.3,
     &' FTC=',E9.3,' ICAL=',I1,' ENGF=',E9.3)
      WRITE(*,405) NSTRSS,NMATT,MELE,NVARI,NCARR,MFR,NBNN
405   FORMAT(' NSTRSS=',I1,' NMATT=',I2,' MELE=',I2,/,' NVARI=',I2,
     *' NCARR=',I2,' MFR=',I2,' NBNN=',I2)
      ENDIF
C---------------------------------------------------------------------
C
      IF(IFLU.EQ.20.OR.IFLU.EQ.21) THEN
          IF(IVAL(IKA+10).EQ.0) PHIF= 60.D0
      ENDIF
*
      IF(IFLU.EQ.30.OR.IFLU.EQ.31) THEN
          IF(IVAL(IKA+9).EQ.0) RMOY= 0.159D0
          IF(IVAL(IKA+10).EQ.0) PHIF=-0.88D0
      ENDIF
*
      IF((IFLU.EQ.10.OR.IFLU.EQ.11.OR.IFLU.EQ.20.OR.IFLU.EQ.21).AND.
     &   IVAL(IKA+9).EQ.0) THEN
       WRITE(*,*)'!! ATTENTION POUR FAIRE DU FLUAGE BETON (LOI BPEL91)'
       WRITE(*,*)' IL FAUT DONNER LA VALEUR DU RAYON MOYEN (RMOY) '
       STOP
      ENDIF
C---------------------------------------------------------------------
C
C     CORRESPONDANCE DES VARIABLES POUR BETDJE
C
      CALL ZERO(D,6,6)
      CALL ZERO(D1,6,6)
      CALL ZERO(EPSR,6,1)
      CALL ZERO(DEPIN,6,1)
      CALL ZERO(EPFLU,6,1)
      CALL ZERO(VART,100,1)
      CALL ZERO(SIGR,6,1)
      CALL ZERO(SIGM,6,1)
      CALL ZERO(STRN,6,1)
C
      EPAIST=1.D0
      NSTRS=NSTRSS
*
      IF((MELE.EQ.28.OR.MFR.EQ.3).AND.NSTRSS.EQ.6) THEN
        NSTRS=3
        EPAIST=XCAR(1)
      ENDIF
*
      DO 1 I=1,NSTRS
        SIGR(I)=SIG0(I)/EPAIST
        STRN(I)=DEPST(I)
        EPFLU(I)=EPIN0(I)
    1 CONTINUE
*
      IF((MELE.EQ.28.OR.MFR.EQ.3).AND.NSTRSS.EQ.4) THEN
        NSTRS=3
        SIGR(3) =SIG0(4)
        STRN(3) =DEPST(4)
      ENDIF
C---------------------------------------------------------------------
      IF(IECR.EQ.10) THEN
        WRITE(*,*) ' ** SIGR='
        WRITE(*,1991) (SIGR(I),I=1,NSTRS)
        WRITE(*,*) ' ** STRN='
        WRITE(*,1991) (STRN(I),I=1,NSTRS)
      ENDIF
C------------------------------------------------------
C   INITIALISATION DES VARIABLES INTERNES
C------------------------------------------------------
        DO  II=1,NVARI
          VART(II)=VAR0(II)
        END DO
C
        EPSPL  = VART( 1)
        TANG1  = VART( 2)
        TANG2  = VART( 3)
        IPLA1  = INT(REAL(VART( 4)))
        IFISU2 = INT(REAL(VART( 5)))
        DTR1   = VART( 6)
        DTR2   = VART( 7)
        IFISU1 = INT(REAL(VART( 8)))
        JFISU  = INT(REAL(VART( 9)))
        ANGL   = VART(10)
        JFISU2 = INT(REAL(VART(11)))
        EQSTR1 = VART(12)
        EPSEQ1 = VART(13)
        EPST1  = VART(14)
        EPST2  = VART(15)
        EQSTR2 = VART(16)
        EPSEQ2 = VART(17)
        EPSC1  = VART(18)
        EPSC2  = VART(19)
        IPLA2  = INT(REAL(VART(20)))
        OUV1   = VART(21)
        EDC1   = VART(22)
        EDC2   = VART(23)
        ETS1   = VART(24)
        ETS2   = VART(25)
        EDT1   = VART(26)
        EDT2   = VART(27)
        OUV2   = VART(28)
        DEFR1  = VART(29)
        DEFR2  = VART(30)
        IPAS0  = INT(REAL(VART(31)))
        TEMP0  = VART(32)
C
        KD = 32
        IF(NSTRS.EQ.4.AND.(IFOU.EQ.-1.OR.IFOU.EQ.0)) THEN
          EQSTR3 = VART(KD+1)
          EPSEQ3 = VART(KD+2)
          EPST3  = VART(KD+3)
          EPSC3  = VART(KD+4)
          DEFR3  = VART(KD+5)
          RTM3   = VART(KD+6)
          EDC3   = VART(KD+7)
          ETS3   = VART(KD+8)
          EDT3   = VART(KD+9)
          TANG3  = VART(KD+10)
          OUV3   = VART(KD+11)
          IPLA3  = INT(REAL(VART(KD+12)))
          IFISU3 = INT(REAL(VART(KD+13)))
          JFISU3 = INT(REAL(VART(KD+14)))
          KD = 46
        ENDIF
C
        EPSR(1)= VART(KD+1)
        EPSR(2)= VART(KD+2)
        EPSR(3)= VART(KD+3)
C  -------------------------------------
      IF(IFLU.NE.0.AND.(TEMP0+DTEMP-TEMP1).LE.0.D0) THEN
        WRITE(*,*)'!! ATTENTION DANS LE MODELE DE FLUAGE BETON '
        TEMPF = TEMP0+DTEMP-TEMP1
        WRITE(*,*)' (LOI BPEL91) TEMPS NEGATIF TEMPF=',TEMPF
        WRITE(*,*)' TEMP0=',TEMP0,' TEMP1=',TEMP1,' DTEMP=',DTEMP
        STOP
      ENDIF
C  -------------------------------------
      TETA=ANGL
      IF((NSTRS.EQ.4.OR.NSTRS.EQ.6).AND.IFOU.EQ.-2) THEN
         NSTRS=3
         SIGR(3)=SIG0(4)
         STRN(3)=DEPST(4)
         STRN(4)=DEPST(3)
         SIGR(4)=0.D0
         EPFLU(3)=EPIN0(4)
         EPFLU(4)=EPIN0(3)
      ENDIF
C
      GO TO (10,10,30,40,50,60),NSTRS
   10 CONTINUE
      KERRE=437
      WRITE(*,*) '!! ATTENTION DANS BETON  NSTRS=',NSTRS
      STOP
c+mdj
C      GO TO 1000
c+mdj
C
   60 CONTINUE
C
C     COMPORTEMENT TRIDIMENSIONNEL
C
        EPSR(4)= VART(KD+4)
        EPSR(5)= VART(KD+5)
        EPSR(6)= VART(KD+6)
        GO TO 30
C
   50 CONTINUE
C
C     CONTRAINTES OU DEFORMATIONS PLANES
C       AVEC TOUS LES CISAILLEMENTS
C
        EPSR(4)= VART(KD+4)
        EPSR(5)= VART(KD+5)
        GO TO 30
C
   40 CONTINUE
C
C     COMPORTEMENT AXISYMETRIQUE
C
        EPSR(4)= VART(KD+4)
C
   30 CONTINUE
C
C---------------------------------------------------------------------
       IF(IFLU.EQ.0)  CALL ZERO(EPFLU,6,1)
       IF(ICAL.EQ.1) EPO=RB/EX
       EPO1=EPO
       IF(ICAL.NE.1.AND.ICU.EQ.3) EPO1=0.85D0*EPO
*     *-----------------------*
*     *   MODELE D' OTTOSEN   *
*     *-----------------------*
*
         CALL PAMAOT(ALPHA,AA,BB,DK1,DK2)
         CALL BETDJE(EPSR,SIGR,SIGM,NSTRS,D,D1,IFOU,STRN,EPSPL,
     A   DEPIN,EPFLU,XE,NBNN,MELE,wrk12)
C---------------------------------------------------------------------
      NSTRS1=NSTRSS
      IF((MELE.EQ.28.OR.MFR.EQ.3).AND.NSTRSS.GT.3) NSTRS1=3
C
      IF((NSTRS1.EQ.4.OR.NSTRS1.EQ.6).AND.IFOU.EQ.-2) THEN
         SIGR(4)=SIGR(3)
         SIGR(3)=0.D0
*
         DEPFZ   =DEPIN(4)
         DEPIN(4)=DEPIN(3)
         DEPIN(3)=DEPFZ
      ENDIF
C
      DO 2 I=1,NSTRS1
        SIGF(I) =SIGR(I)*EPAIST
*       EPINF(I)=EPIN0(I)+DEPIN(I)
        EPINF(I)=DEPIN(I)
    2 CONTINUE
*
      IF((MELE.EQ.28.OR.MFR.EQ.3).AND.NSTRSS.EQ.6) THEN
        CALL ZERO(D1,6,6)
        CALL ZERO(D2,6,6)
        CALL ZERO(SIGM,6,1)
*
        CALL DOHCOM(XMAT,NMATT,CMATE,IFOUR,NSTRSS,D1,IRTD)
        CALL HOOKMU(EPAIST,0.D0,NSTRSS,D1,D2)
        CALL BST(D2,DEPST,NSTRSS,NSTRSS,SIGM)
*
        DO I = 4,NSTRSS
           SIGF(I)=SIG0(I)+SIGM(I)
*          EPINF(I)=EPIN0(I)+DEPIN(I)
           EPINF(I)=DEPIN(I)
        END DO
      ENDIF
*
      IF((MELE.EQ.28.OR.MFR.EQ.3).AND.NSTRSS.EQ.4) THEN
         SIGF(3) =0.D0
         SIGF(4) =SIGR(3)*EPAIST
         DEPFZ   =DEPIN(4)
         DEPIN(4)=DEPIN(3)
         DEPIN(3)=DEPFZ
      ENDIF
C---------------------------------------------------------------------
      IF(IECR.EQ.20) THEN
      WRITE(*,*) ' ** IFLU =',IFLU,' RMOY=',RMOY,' PHIF=',PHIF,
     & 'TEMP0= ',TEMP0,' DTEMP =',DTEMP
      WRITE(*,*) ' **  DEPIN  *** '
      WRITE(*,1991) (DEPIN(IC),IC=1,NSTRSS)
      WRITE(*,*) ' ** EPIN0  / EPINF '
      WRITE(*,1991) (EPIN0(IC),IC=1,NSTRSS),(EPINF(IC),IC=1,NSTRSS)
      ENDIF
C-----------------------------------------------------------------------
C
      VART( 1)=EPSPL
      VART( 2)=TANG1
      VART( 3)=TANG2
c+mdj
      VART( 4)=IPLA1
      VART( 5)=IFISU2
c+mdj
      VART( 6)=DTR1
      VART( 7)=DTR2
c+mdj
      VART( 8)=IFISU1
      VART( 9)=JFISU
c+mdj
      VART(10)=TETA
c+mdj
      VART(11)=JFISU2
c+mdj
      VART(12)=EQSTR1
      VART(13)=EPSEQ1
      VART(14)=EPST1
      VART(15)=EPST2
      VART(16)=EQSTR2
      VART(17)=EPSEQ2
      VART(18)=EPSC1
      VART(19)=EPSC2
c+mdj
      VART(20)=IPLA2
c+mdj
      VART(21)=OUV1
      VART(22)=EDC1
      VART(23)=EDC2
      VART(24)=ETS1
      VART(25)=ETS2
      VART(26)=EDT1
      VART(27)=EDT2
      VART(28)=OUV2
      VART(29)=DEFR1
      VART(30)=DEFR2
      VART(31)=1.D0
      VART(32)=TEMP0
C
      KD = 32
      IF(NSTRSS.EQ.4.AND.(IFOU.EQ.-1.OR.IFOU.EQ.0)) THEN
        VART(KD+1) =EQSTR3
        VART(KD+2) =EPSEQ3
        VART(KD+3) =EPST3
        VART(KD+4) =EPSC3
        VART(KD+5) =DEFR3
        VART(KD+6) =RTM3
        VART(KD+7) =EDC3
        VART(KD+8) =ETS3
        VART(KD+9) =EDT3
        VART(KD+10)=TANG3
        VART(KD+11)=OUV3
        VART(KD+12)=IPLA3
        VART(KD+13)=IFISU3
        VART(KD+14)=JFISU3
        KD = 46
      ENDIF
C
      VART(KD+1)=EPSR(1)
      VART(KD+2)=EPSR(2)
      VART(KD+3)=EPSR(3)
      K1=KD+3
C
      GO TO (11,11,31,41,51,61),NSTRS
   11 CONTINUE
      KERRE=437
      WRITE(*,*) '!! ATTENTION DANS BETON  NSTRS=',NSTRS
      STOP
c+mdj
C      GO TO 1000
c+mdj
C
   61 CONTINUE
C
C     COMPORTEMENT TRIDIMENSIONNEL
C
        VART(KD+4)=EPSR(4)
        VART(KD+5)=EPSR(5)
        VART(KD+6)=EPSR(6)
        K1=KD+6
        GO TO 31
C
   51 CONTINUE
C
C     CONTRAINTES OU DEFORMATIONS PLANES
C       AVEC TOUS LES CISAILLEMENTS
C
        VART(KD+4)=EPSR(4)
        VART(KD+5)=EPSR(5)
        K1=KD+5
        GO TO 31
C
   41 CONTINUE
C
C     COMPORTEMENT AXISYMETRIQUE
C
        VART(KD+4)=EPSR(4)
        K1=KD+4
        GO TO 31
   31 CONTINUE
C
C     CONTRAINTES OU DEFORMATIONS PLANES
C       SANS CISAILLEMENTS TRANSVERSAL
C
      L1=0
      DO 66 I1 = 1,6
      DO 67 J1 = 1,6
         L1=L1+1
         VV(L1) = D(J1,I1)
   67 CONTINUE
   66 CONTINUE
*
      L1=0
      DO 62 I1 = 1,NSTRS
      DO 63 J1 = 1,NSTRS
        K=K1+(I1-1)*NSTRS+J1
        L1=L1+1
        VART(K)=VV(L1)
   63 CONTINUE
   62 CONTINUE
C
      DO 23 II=1,NVARI
        VARF(II)=VART(II)
   23 CONTINUE
C---------------------------------------------------------------------
      IF(IECR.EQ.10) THEN
         WRITE(*,66770) IBB,IGAU
66770    format(///'SORTIE DE BEINSA : element  ',i6,2x,'point  ',i3//)
         WRITE(*,66771) MELE,IFOU,MFR,NSTRS
66771    format('0  MELE=',i4,2x,'ifour=',i4,'mfr=',i2,'nstrs=',i2/)
         WRITE(*,66774) (DEPST(I),I=1,NSTRSS)
66774    format(2x,'  depst '/(6(1x,1pe12.5)))
         WRITE(*,66772) (SIGF(I),I=1,NSTRSS)
66772    format(2x,'  sigf '/(6(1x,1pe12.5)))
         WRITE(*,66773) (VARF(I),I=1,NVARI)
66773    format(2x,'  varf '/(6(1x,1pe12.5)))
      ENDIF
C
1991  FORMAT(18(1X,E12.5))
1992  FORMAT(18(1X,I6))
C---------------------------------------------------------------------
C
 1000 CONTINUE
C
      RETURN
      END







