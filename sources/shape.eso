C SHAPE     SOURCE    PV        08/09/11    21:17:04     6150
      SUBROUTINE SHAPE(QSI,ETA,DZE,IELE,SHP,IRET)
C=======================================================================
C
C     FONCTIONS DE FORME ET LEUR DERIVEES
C
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(XZER=0.D0,UNDEMI=.5D0,UN=1.D0,DEUX=2.D0)
      PARAMETER(TROIS=3.D0,QUATRE=4.D0)
      DIMENSION SHP(6,*)
C=======================================================================
C     QSI ETA DZE COORDONNEES REDUITES DU POINT DE GAUSS
C
C     SHP FONCTIONS DE FORME SHP(1,INO) FONCTIONS DE FORME DU ND INO
C                            SHP(2,INO) DERIVEES PR RPPRT A QSI
C                            SHP(3,INO) DERIVEES PR RPPRT A ETA
C                            SHP(4,INO) DERIVEES PR RPPRT A DZE
C                            SHP(5,INO) DERIVEES PR RPPRT A 4
C                               COORDONNEES BARYCENTRIQUES
C     CAS DE DIMENSION 1
C               2 OU 3 NOEUDS LIGNE
C               2 NOEUDS POLYNOME DE HERMITE ET DERIVEES
C     CAS DE DIMENSION 2
C               3 OU 6 NOEUDS TRIANGLE
C               7 NOEUDS TRIANGLE
C               4 OU 8 NOEUDS QUADRILATERE
C               9 NOEUDS QUADRILATERE
C     CAS DE DIMENSION 3 FAIT DANS SHAPE3    ETIQUETTE 50
C               4 OU 10 NOEUDS TETRAEDRE
C               5 OU 13 NOEUDS PYRAMIDE
C               6 OU 15 NOEUDS PRISME
C               8 OU 20 NOEUDS CUBE
C     CAS DE DIMENSION 3 QUAF FAIT DANS SHAPEQ    ETIQUETTE 51
C               15 NOEUDS TETRAEDRE
C               19 NOEUDS PYRAMIDE
C               21 NOEUDS PRISME
C               27 NOEUDS CUBE
C=======================================================================
      IRET=1
C
C    66 INDIQUE QUE L ELEMENT PAS ENCORE IMPLEMENTE
C
C           P1 S2 S3 T3 T4 T6 T7 Q4 Q5 Q8 Q9 R2 R3 C8 C20 P6 P15
      GOTO ( 1, 2, 3, 4,66, 6,7, 8,66,10,10,12,13,50, 50,50, 50,
C        L3 L4 L6 L8 MU T4 T10 P5 P13 ATT SUP RP3 LP6 LP8 POLY
     1   18,19,66,66,66,50, 50,50, 50, 66, 66, 29, 30, 31, 66,
C        CU27 PR21 TE15 PY19
     1     51,51,51,51 ) IELE
      if(iele.eq.260) then
           write(6,*) ' on passe dans shape bizarre'
      endif
C
  66  IRET=0
      GOTO 666
C
C     FONCTIONS DE FORME MONODIMENSIONELLES
C
C
C     FONCTION FORME 1 NOEUD
C
   1  CONTINUE
      SHP(1,1)=UN
      GOTO 666
C     FONCTIONS DE FORME 2 NOEUDS
C
   2  CONTINUE
      SHP(1,1)=(UN-QSI)/DEUX
      SHP(1,2)=(UN+QSI)/DEUX
      SHP(2,1)=-UNDEMI
      SHP(2,2)= UNDEMI
      GOTO 666
C
C     FONCTIONS DE FORME 3 NOEUDS
C
   3  CONTINUE
      SHP(1,1)=(UN-QSI)/DEUX
      SHP(1,3)=(UN+QSI)/DEUX
      SHP(2,1)=-UNDEMI
      SHP(2,3)= UNDEMI
      SHP(1,2)=UN-QSI*QSI
      SHP(2,2)=-DEUX*QSI
      SHP(1,1)=SHP(1,1)-SHP(1,2)/DEUX
      SHP(1,3)=SHP(1,3)-SHP(1,2)/DEUX
      SHP(2,1)=SHP(2,1)-SHP(2,2)/DEUX
      SHP(2,3)=SHP(2,3)-SHP(2,2)/DEUX
      GOTO 666
C
C     POLYNOME DE HERMITE 2 NOEUDS 1 FCTN DE FORME ET SA DERIVEE
C
C104  SHP(1,1)=(UN-QSI)*(UN-QSI)*(DEUX+QSI)/QUATRE
C     SHP(2,1)= TROIS*(QSI*QSI-UN)/QUATRE
C     SHP(1,2)=(UN-QSI*QSI)*(UN-QSI)/QUATRE
C     SHP(2,2)=(QSI-UN)*(UN+TROIS*QSI)/QUATRE
C     SHP(1,3)=(UN+QSI)*(UN+QSI)*(DEUX-QSI)/QUATRE
C     SHP(2,3)=TROIS*(UN-QSI*QSI)/QUATRE
C     SHP(1,4)=(QSI*QSI-UN)*(UN+QSI)/QUATRE
C     SHP(2,4)=(UN+QSI)*(TROIS*QSI-UN)/QUATRE
C     GOTO 666
C
C     FONCTIONS DE FORME BIDIMENSIONNELLES
C
   4  CONTINUE
C
C     FONCTIONS DE FORME TRIANGLE A 3 NOEUDS
C
      AUX=UN-QSI-ETA
      SHP(1,1)=AUX
      SHP(1,2)=QSI
      SHP(1,3)=ETA
      SHP(2,1)=-UN
      SHP(2,2)=UN
      SHP(2,3)=XZER
      SHP(3,1)=-UN
      SHP(3,2)=XZER
      SHP(3,3)=UN
      GOTO 666
   6  CONTINUE
C
C     FONCTIONS DE FORME TRIANGLE A 6 NOEUDS
C
      AUX=UN-QSI-ETA
      SHP(1,1)=AUX
      SHP(1,3)=QSI
      SHP(1,5)=ETA
      SHP(2,1)=-UN
      SHP(2,3)=UN
      SHP(2,5)=XZER
      SHP(3,1)=-UN
      SHP(3,3)=XZER
      SHP(3,5)=UN
C   AJOUTER NOEUD 2
      SHP(1,2)=QUATRE*QSI*AUX
      SHP(2,2)=QUATRE*(AUX-QSI)
      SHP(3,2)=-QUATRE*QSI
      DO 1010 I=1,3
      SHP(I,1)=SHP(I,1)-SHP(I,2)/DEUX
 1010 SHP(I,3)=SHP(I,3)-SHP(I,2)/DEUX
C   AJOUTER NOEUD 4
      SHP(1,4)=QUATRE*QSI*ETA
      SHP(2,4)=QUATRE*ETA
      SHP(3,4)=QUATRE*QSI
      DO 1020 I=1,3
      SHP(I,3)=SHP(I,3)-SHP(I,4)/DEUX
 1020 SHP(I,5)=SHP(I,5)-SHP(I,4)/DEUX
C   AJOUTER NOEUD 6
      SHP(1,6)=QUATRE*ETA*AUX
      SHP(2,6)=-QUATRE*ETA
      SHP(3,6)=QUATRE*(AUX-ETA)
      DO 1030 I=1,3
      SHP(I,5)=SHP(I,5)-SHP(I,6)/DEUX
 1030 SHP(I,1)=SHP(I,1)-SHP(I,6)/DEUX
      GOTO 666
   7  CONTINUE
C
C     FONCTIONS DE FORME TRIANGLE A 7 NOEUDS
C
      AUX=UN-QSI-ETA
      SHP(1,1)=AUX*(DEUX*AUX-UN)+TROIS*AUX*QSI*ETA
      SHP(1,3)=QSI*(DEUX*QSI-UN)+TROIS*AUX*QSI*ETA
      SHP(1,5)=ETA*(DEUX*ETA-UN)+TROIS*AUX*QSI*ETA

      SHP(1,2)=QUATRE*(AUX*QSI-TROIS*AUX*QSI*ETA)
      SHP(1,4)=QUATRE*(QSI*ETA-TROIS*AUX*QSI*ETA)
      SHP(1,6)=QUATRE*(AUX*ETA-TROIS*AUX*QSI*ETA)

      SHP(1,7)=TROIS*TROIS*TROIS*AUX*QSI*ETA

C     GRADIENTS DES FONCTIONS DE FORME A 7 NOEUDS
C     NOEUD 1
      SHP(2,1)=-3.D0*ETA*ETA+4.D0*QSI+7.D0*ETA-
     * 6.D0*QSI*ETA-3.D0
      SHP(3,1)=-3.D0*QSI*QSI+4.D0*ETA+7.D0*QSI-
     * 6.D0*QSI*ETA-3.D0
C     NOEUD 3
      SHP(2,3)=-3.D0*ETA*ETA+4.D0*QSI+3.D0*ETA-
     * 6.D0*QSI*ETA-1.D0
      SHP(3,3)=-3.D0*QSI*QSI+3.D0*QSI-6.D0*QSI*ETA
C     NOEUD 5
      SHP(2,5)=-3.D0*ETA*ETA+3.D0*ETA-6.D0*QSI*ETA
      SHP(3,5)=-3.D0*QSI*QSI+4.D0*ETA+3.D0*QSI-
     * 6.D0*QSI*ETA-1.D0
C     NOEUD 2
      SHP(2,2)=QUATRE*TROIS*ETA*ETA-QUATRE*QUATRE*ETA-DEUX*QUATRE*QSI+
     * DEUX*TROIS*QUATRE*QSI*ETA+QUATRE
      SHP(3,2)=QUATRE*TROIS*QSI*QSI-QUATRE*QUATRE*QSI+
     * DEUX*TROIS*QUATRE*QSI*ETA
C     NOEUD 4
      SHP(2,4)=QUATRE*TROIS*ETA*ETA-DEUX*QUATRE*ETA+
     * DEUX*TROIS*QUATRE*QSI*ETA
      SHP(3,4)=QUATRE*TROIS*QSI*QSI-DEUX*QUATRE*QSI+
     * DEUX*TROIS*QUATRE*QSI*ETA
C     NOEUD 6
      SHP(2,6)=QUATRE*TROIS*ETA*ETA-QUATRE*QUATRE*ETA+
     * DEUX*TROIS*QUATRE*QSI*ETA
      SHP(3,6)=QUATRE*TROIS*QSI*QSI-QUATRE*QUATRE*QSI-DEUX*QUATRE*ETA+
     * DEUX*TROIS*QUATRE*QSI*ETA+QUATRE
C     NOEUD 7
      SHP(2,7)=TROIS*TROIS*TROIS*ETA*(UN-DEUX*QSI-ETA)
      SHP(3,7)=TROIS*TROIS*TROIS*QSI*(UN-QSI-DEUX*ETA)
      GOTO 666
   8  CONTINUE
C
C  FONCTIONS DE FORME QUADRANGLE  A 4 NOEUDS
C
      SHP(1,1)=(UN-QSI)*(UN-ETA)/QUATRE
      SHP(1,2)=(UN+QSI)*(UN-ETA)/QUATRE
      SHP(1,3)=(UN+QSI)*(UN+ETA)/QUATRE
      SHP(1,4)=(UN-QSI)*(UN+ETA)/QUATRE
      SHP(2,1)=(ETA-UN)/QUATRE
      SHP(2,2)=-SHP(2,1)
      SHP(2,3)=(ETA+UN)/QUATRE
      SHP(2,4)=-SHP(2,3)
      SHP(3,1)=(QSI-UN)/QUATRE
      SHP(3,2)=-(UN+QSI)/QUATRE
      SHP(3,3)=-SHP(3,2)
      SHP(3,4)=-SHP(3,1)
      GOTO 666
C
C     FONCTIONS DE FORME QUADRANGLE A 8 NOEUDS
C
  10  CONTINUE
      SHP(1,1)=(UN-QSI)*(UN-ETA)/QUATRE
      SHP(1,3)=(UN+QSI)*(UN-ETA)/QUATRE
      SHP(1,5)=(UN+QSI)*(UN+ETA)/QUATRE
      SHP(1,7)=(UN-QSI)*(UN+ETA)/QUATRE
      SHP(2,1)=(ETA-UN)/QUATRE
      SHP(2,3)=-SHP(2,1)
      SHP(2,5)=(ETA+UN)/QUATRE
      SHP(2,7)=-SHP(2,5)
      SHP(3,1)=(QSI-UN)/QUATRE
      SHP(3,3)=-(UN+QSI)/QUATRE
      SHP(3,5)=-SHP(3,3)
      SHP(3,7)=-SHP(3,1)
C   AJOUTER NOEUD 2
      SHP(1,2)=(UN-QSI*QSI)*(UN-ETA)/DEUX
      SHP(2,2)=(ETA-UN)*QSI
      SHP(3,2)=(QSI*QSI-1)/DEUX
      DO 2010 I=1,3
      SHP(I,1)=SHP(I,1)-SHP(I,2)/DEUX
 2010 SHP(I,3)=SHP(I,3)-SHP(I,2)/DEUX
C   AJOUTER NOEUD 4
      SHP(1,4)=(UN-ETA*ETA)*(UN+QSI)/DEUX
      SHP(2,4)=(UN-ETA*ETA)/DEUX
      SHP(3,4)=-ETA*(UN+QSI)
      DO 2020 I=1,3
      SHP(I,3)=SHP(I,3)-SHP(I,4)/DEUX
 2020 SHP(I,5)=SHP(I,5)-SHP(I,4)/DEUX
C   AJOUTER NOEUD 6
      SHP(1,6)=(UN-QSI*QSI)*(UN+ETA)/DEUX
      SHP(2,6)=-QSI*(UN+ETA)
      SHP(3,6)=(UN-QSI*QSI)/DEUX
      DO 2030 I=1,3
      SHP(I,5)=SHP(I,5)-SHP(I,6)/DEUX
 2030 SHP(I,7)=SHP(I,7)-SHP(I,6)/DEUX
C   AJOUTER NOEUD 8
      SHP(1,8)=(UN-ETA*ETA)*(UN-QSI)/DEUX
      SHP(2,8)=(ETA*ETA-UN)/DEUX
      SHP(3,8)=ETA*(QSI-UN)
      DO 2040 I=1,3
      SHP(I,7)=SHP(I,7)-SHP(I,8)/DEUX
 2040 SHP(I,1)=SHP(I,1)-SHP(I,8)/DEUX
C
C     FONCTIONS DE FORME QUADRANGLE A 9 NOEUDS
C
      IF (IELE.NE.11)  GOTO 666
      SHP(1,9)=(UN-QSI*QSI)*(UN-ETA*ETA)
      SHP(2,9)=DEUX*ETA*(QSI*QSI-UN)
      SHP(3,9)=DEUX*QSI*(ETA*ETA-UN)
      DO 2051 I=1,3
      DO 2051 J=1,7,2
      SHP(I,J)=SHP(I,J)+SHP(I,9)/QUATRE
 2051 SHP(I,J+1)=SHP(I,J+1)-SHP(I,9)/DEUX
      GOTO 666
C
C     FONCTIONS DE FORME ELEMENTS DE RACCORD
C            LIQUIDE - MASSIF LINEAIRE
C
C     FONCTIONS DE FORME RAC2
C
  12  CONTINUE
      SHP(1,1)=(1.0-QSI)/DEUX
      SHP(1,2)=(1.0+QSI)/DEUX
      SHP(2,1)=-1.0/DEUX
      SHP(2,2)= 1.0/DEUX
      SHP(1,3)=SHP(1,2)
      SHP(1,4)=SHP(1,1)
      SHP(2,3)=SHP(2,2)
      SHP(2,4)=SHP(2,1)
      GOTO 666
C
C     FONCTIONS DE FORME RAC3
C
  13  CONTINUE
      SHP(1,1)=(-QSI+QSI*QSI)*(UN-ETA)/QUATRE
      SHP(1,2)=( UN -QSI*QSI)*(UN-ETA)/DEUX
      SHP(1,3)=( QSI+QSI*QSI)*(UN-ETA)/QUATRE
      SHP(1,4)=( QSI+QSI*QSI)*(UN+ETA)/QUATRE
      SHP(1,5)=( UN -QSI*QSI)*(UN+ETA)/DEUX
      SHP(1,6)=(-QSI+QSI*QSI)*(UN+ETA)/QUATRE
C
      SHP(2,1)=(-UN+DEUX*QSI)*(UN-ETA)/QUATRE
      SHP(2,2)=(   -DEUX*QSI)*(UN-ETA)/DEUX
      SHP(2,3)=( UN+DEUX*QSI)*(UN-ETA)/QUATRE
      SHP(2,4)=( UN+DEUX*QSI)*(UN+ETA)/QUATRE
      SHP(2,5)=(   -DEUX*QSI)*(UN+ETA)/DEUX
      SHP(2,6)=(-UN+DEUX*QSI)*(UN+ETA)/QUATRE
C
      SHP(3,1)=-(-QSI+QSI*QSI)/QUATRE
      SHP(3,2)=-( UN -QSI*QSI)/DEUX
      SHP(3,3)=-( QSI+QSI*QSI)/QUATRE
      SHP(3,4)= ( QSI+QSI*QSI)/QUATRE
      SHP(3,5)= ( UN -QSI*QSI)/DEUX
      SHP(3,6)= (-QSI+QSI*QSI)/QUATRE
      GOTO 666
  18  CONTINUE
C
C     FONCTIONS DE FORME LIA3
C
      AUX=UN-QSI-ETA
      SHP(1,1)=AUX
      SHP(1,2)=QSI
      SHP(1,3)=ETA
      SHP(2,1)=-UN
      SHP(2,2)=UN
      SHP(2,3)=XZER
      SHP(3,1)=-UN
      SHP(3,2)=XZER
      SHP(3,3)=UN
      SHP(1,4)=SHP(1,1)
      SHP(1,5)=SHP(1,2)
      SHP(1,6)=SHP(1,3)
      SHP(2,4)=SHP(2,1)
      SHP(2,5)=SHP(2,2)
      SHP(2,6)=SHP(2,3)
      SHP(3,4)=SHP(3,1)
      SHP(3,5)=SHP(3,2)
      SHP(3,6)=SHP(3,3)
      GOTO 666
  19  CONTINUE
C
C  FONCTIONS DE FORME LIA4
C
      SHP(1,1)=(UN-QSI)*(UN-ETA)/QUATRE
      SHP(1,2)=(UN+QSI)*(UN-ETA)/QUATRE
      SHP(1,3)=(UN+QSI)*(UN+ETA)/QUATRE
      SHP(1,4)=(UN-QSI)*(UN+ETA)/QUATRE
      SHP(2,1)=(ETA-UN)/QUATRE
      SHP(2,2)=-SHP(2,1)
      SHP(2,3)=(ETA+UN)/QUATRE
      SHP(2,4)=-SHP(2,3)
      SHP(3,1)=(QSI-UN)/QUATRE
      SHP(3,2)=-(UN+QSI)/QUATRE
      SHP(3,3)=-SHP(3,2)
      SHP(3,4)=-SHP(3,1)
      SHP(1,5)=SHP(1,1)
      SHP(1,6)=SHP(1,2)
      SHP(1,7)=SHP(1,3)
      SHP(1,8)=SHP(1,4)
      SHP(2,5)=SHP(2,1)
      SHP(2,6)=SHP(2,2)
      SHP(2,7)=SHP(2,3)
      SHP(2,8)=SHP(2,4)
      SHP(3,5)=SHP(3,1)
      SHP(3,6)=SHP(3,2)
      SHP(3,7)=SHP(3,3)
      SHP(3,8)=SHP(3,4)
      GOTO 666
  29  CONTINUE
C
C     FONCTIONS DE FORME ELEMENTS DE RACCORD POREUX RAP3
C
      SHP(1,1)=(-QSI+QSI*QSI)/DEUX
      SHP(1,2)=( UN -QSI*QSI)
      SHP(1,3)=( QSI+QSI*QSI)/DEUX
      SHP(1,4)=( QSI+QSI*QSI)/DEUX
      SHP(1,5)=( UN -QSI*QSI)
      SHP(1,6)=(-QSI+QSI*QSI)/DEUX
      SHP(2,1)=(-UN+DEUX*QSI)/DEUX
      SHP(2,2)=(   -DEUX*QSI)
      SHP(2,3)=( UN+DEUX*QSI)/DEUX
      SHP(2,4)=( UN+DEUX*QSI)/DEUX
      SHP(2,5)=(   -DEUX*QSI)
      SHP(2,6)=(-UN+DEUX*QSI)/DEUX
c
      SHP(1,7)= ( UN-QSI)/DEUX
      SHP(1,8)= ( UN+QSI)/DEUX
      SHP(1,9)= ( UN+QSI)/DEUX
      SHP(1,10)=( UN-QSI)/DEUX
      SHP(1,11)=( UN-QSI)/DEUX
      SHP(1,12)=( UN+QSI)/DEUX
      SHP(2,7)= -UN/DEUX
      SHP(2,8)=  UN/DEUX
      SHP(2,9)=  UN/DEUX
      SHP(2,10)=-UN/DEUX
      SHP(2,11)=-UN/DEUX
      SHP(2,12)= UN/DEUX
      GOTO 666
  30  CONTINUE
C
C     FONCTIONS DE FORME ELEMENTS DE LIAISON POREUX RAP6
C
      SHP(1,1)= (UN-QSI-ETA)*(UN-DEUX*QSI-DEUX*ETA)
      SHP(1,2)= QUATRE*QSI*(UN-QSI-ETA)
      SHP(1,3)= QSI*(DEUX*QSI-UN)
      SHP(1,4)= QUATRE*QSI*ETA
      SHP(1,5)= ETA*(DEUX*ETA-UN)
      SHP(1,6)= QUATRE*ETA*(UN-QSI-ETA)
      SHP(2,1)=-(UN-DEUX*QSI-DEUX*ETA)-DEUX*(UN-QSI-ETA)
      SHP(2,2)= QUATRE*(UN-DEUX*QSI-ETA)
      SHP(2,3)= QUATRE*QSI-UN
      SHP(2,4)= QUATRE*ETA
      SHP(2,5)= XZER
      SHP(2,6)=-QUATRE*ETA
      SHP(3,1)=-(UN-DEUX*QSI-DEUX*ETA)-DEUX*(UN-QSI-ETA)
      SHP(3,2)=-QUATRE*QSI
      SHP(3,3)= XZER
      SHP(3,4)= QUATRE*QSI
      SHP(3,5)= QUATRE*ETA-UN
      SHP(3,6)= QUATRE*(UN-QSI-DEUX*ETA)
      DO 111 I=7,12
      SHP(1,I)=SHP(1,I-6)
      SHP(2,I)=SHP(2,I-6)
      SHP(3,I)=SHP(3,I-6)
  111 CONTINUE
c
      SHP(1,13)= UN-QSI-ETA
      SHP(1,14)= QSI
      SHP(1,15)= ETA
      SHP(2,13)=-UN
      SHP(2,14)= UN
      SHP(2,15)= XZER
      SHP(3,13)=-UN
      SHP(3,14)= XZER
      SHP(3,15)= UN
      DO 222 I=16,18
      SHP(1,I)=SHP(1,I-3)
      SHP(2,I)=SHP(2,I-3)
      SHP(3,I)=SHP(3,I-3)
      SHP(1,I+3)=SHP(1,I-3)
      SHP(2,I+3)=SHP(2,I-3)
      SHP(3,I+3)=SHP(3,I-3)
  222 CONTINUE
      GOTO 666
  31  CONTINUE
C
C     FONCTIONS DE FORME ELEMENTS DE LIAISON POREUX RAP8
C
      SHP(1,2)= (UN-QSI*QSI)*(UN-ETA)/DEUX
      SHP(1,4)= (UN+QSI)*(UN-ETA*ETA)/DEUX
      SHP(1,6)= (UN-QSI*QSI)*(UN+ETA)/DEUX
      SHP(1,8)= (UN-QSI)*(UN-ETA*ETA)/DEUX
      SHP(1,1)= (UN-QSI)*(UN-ETA)/QUATRE-UN/DEUX*(SHP(1,8)+SHP(1,2))
      SHP(1,3)= (UN+QSI)*(UN-ETA)/QUATRE-UN/DEUX*(SHP(1,2)+SHP(1,4))
      SHP(1,5)= (UN+QSI)*(UN+ETA)/QUATRE-UN/DEUX*(SHP(1,4)+SHP(1,6))
      SHP(1,7)= (UN-QSI)*(UN+ETA)/QUATRE-UN/DEUX*(SHP(1,6)+SHP(1,8))
      SHP(2,2)=-QSI*(UN-ETA)
      SHP(2,4)= (UN-ETA*ETA)/DEUX
      SHP(2,6)=-QSI*(UN+ETA)
      SHP(2,8)=-(UN-ETA*ETA)/DEUX
      SHP(2,1)=-(UN-ETA)/QUATRE-UN/DEUX*(SHP(2,8)+SHP(2,2))
      SHP(2,3)= (UN-ETA)/QUATRE-UN/DEUX*(SHP(2,2)+SHP(2,4))
      SHP(2,5)= (UN+ETA)/QUATRE-UN/DEUX*(SHP(2,4)+SHP(2,6))
      SHP(2,7)=-(UN+ETA)/QUATRE-UN/DEUX*(SHP(2,6)+SHP(2,8))
      SHP(3,2)=-(UN-QSI*QSI)/DEUX
      SHP(3,4)=-ETA*(UN+QSI)
      SHP(3,6)= (UN-QSI*QSI)/DEUX
      SHP(3,8)=-ETA*(UN-QSI)
      SHP(3,1)=-(UN-QSI)/QUATRE-UN/DEUX*(SHP(3,8)+SHP(3,2))
      SHP(3,3)=-(UN+QSI)/QUATRE-UN/DEUX*(SHP(3,2)+SHP(3,4))
      SHP(3,5)= (UN+QSI)/QUATRE-UN/DEUX*(SHP(3,4)+SHP(3,6))
      SHP(3,7)= (UN-QSI)/QUATRE-UN/DEUX*(SHP(3,6)+SHP(3,8))
      DO 333 I=9,16
      SHP(1,I)=SHP(1,I-8)
      SHP(2,I)=SHP(2,I-8)
      SHP(3,I)=SHP(3,I-8)
  333 CONTINUE
c
      SHP(1,17)= (UN-QSI)*(UN-ETA)/QUATRE
      SHP(1,18)= (UN+QSI)*(UN-ETA)/QUATRE
      SHP(1,19)= (UN+QSI)*(UN+ETA)/QUATRE
      SHP(1,20)= (UN-QSI)*(UN+ETA)/QUATRE
      SHP(2,17)=-(UN-ETA)/QUATRE
      SHP(2,18)= (UN-ETA)/QUATRE
      SHP(2,19)= (UN+ETA)/QUATRE
      SHP(2,20)=-(UN+ETA)/QUATRE
      SHP(3,17)=-(UN-QSI)/QUATRE
      SHP(3,18)=-(UN+QSI)/QUATRE
      SHP(3,19)= (UN+QSI)/QUATRE
      SHP(3,20)= (UN-QSI)/QUATRE
      DO 444 I=21,24
      SHP(1,I)=SHP(1,I-4)
      SHP(2,I)=SHP(2,I-4)
      SHP(3,I)=SHP(3,I-4)
      SHP(1,I+4)=SHP(1,I-4)
      SHP(2,I+4)=SHP(2,I-4)
      SHP(3,I+4)=SHP(3,I-4)
  444 CONTINUE
      GOTO 666
C
C     FONCTIONS DE FORME TRIDIMENSIONNELLES
C
  50  CALL SHAPE3(QSI,ETA,DZE,IELE,SHP,IRT1)
      IRET=IRT1
      GOTO 666
C
C     FONCTIONS DE FORME TRIDIMENSIONNELLES QUAF
C
 51   CALL SHAPEQ(QSI,ETA,DZE,IELE,SHP,IRT1)
      IRET=IRT1
C
 666  CONTINUE
      RETURN
      END









