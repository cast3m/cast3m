C CLIM11    SOURCE    PV        16/11/17    21:58:50     9180
      SUBROUTINE CLIM11(IJAC)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  CLIM11
C
C DESCRIPTION       :  Subroutine appellée par CLIM1
C
C                      Modelisation 2D/3D des equations d'Euler
C                      Calcul de conditions aux bords
C                      Inlet; Riemann invariants
C
C LANGAGE           :  FORTRAN 77 + ESOPE 2000 (avec estensions CISI)
C
C AUTEUR            :  A. BECCANTINI, DRN/DMT/SEMT/LTMF
C
C************************************************************************
C
C APPELES (Calcul)  :
C
C************************************************************************
C
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :
C
C************************************************************************
C
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC SMLMOTS
-INC SMELEME
-INC SMLENTI
-INC SMMATRIK
      POINTEUR MLMVIT.MLMOTS
C
C**** Variables de COOPTIO
C
C      INTEGER IPLLB, IERPER, IERMAX, IERR, INTERR
C     &        ,IOTER,   IOLEC,  IOIMP,     IOCAR,  IOACQ
C     &        ,IOPER,   IOSGB,  IOGRA,     IOSAU,  IORES
C     &        ,IECHO,   IIMPI,  IOSPI
C     &        ,IDIM,    IFICLE, IPREFI
C     &        ,MCOORD
C     &        ,IFOMOD, NIFOUR, IFOUR, NSDPGE, IONIVE
C     &        ,NGMAXY, IZROSF, ISOTYP, IOSCR,LTEXLU
C     &        ,NORINC,NORVAL,NORIND,NORVAD
C     &        ,NUCROU, IPSAUV
C
      INTEGER IJAC, IJACO
     &     ,IDOMA, IDBOR, IRET, MELEMC, MELEFE, MELEMF, ICHPVO, INORM
     &     ,ICHPSU, MELECB, NBCOMP, INDIC, MELEFC, MELRES
     &     ,JGN, JGM, NBELEM, NBNN, NBSOUS, NBREF, NGF, NLC
     &     ,I1, ICEN, N1, ILIINP
     &     ,ILIINC, IROC, IVITC, IPC, IGAMC, ICHLIM, NBOPT, ILIM
     &     ,ICHRES, ICHRLI
     &     ,NKID,NKMT,NMATRI,NRIGE,MMODEL,INEFMD
      PARAMETER (NBOPT=9)
      CHARACTER*8 LOPT(NBOPT)
      CHARACTER*4 MOT
      CHARACTER*8 TYPE
C
      DATA LOPT/'INRI    ','OUTRI   ','INSS    ','OUTSS   ','OUTP    ',
     &          'INSU    ','INJE    ','INJELM  ','INSO    '/
C
C*******************************
C**** La table domaine *********
C*******************************
C
      CALL LIROBJ('MMODEL',MMODEL,1,IRET)
      IF(IERR.NE.0)GOTO 9999
      CALL LEKMOD(MMODEL,IDOMA,INEFMD)
C     INEFMD inutilisé
      IF(IERR .NE. 0)GOTO 9999
C
      CALL LEKTAB(IDOMA,'CENTRE',MELEMC)
      IF(IERR .NE. 0) GOTO 9999
C
      CALL LEKTAB(IDOMA,'FACE',MELEMF)
      IF(IERR .NE. 0) GOTO 9999
C
C**** Lecture du CHPOINT contenant les volumes
C
      CALL LEKTAB(IDOMA,'XXVOLUM',ICHPVO)
      IF(IERR .NE. 0) GOTO 9999
      INDIC = 1
      NBCOMP = 1
      MOT = 'SCAL'
      CALL QUEPOI(ICHPVO, MELEMC, INDIC, NBCOMP, MOT)
      IF(IERR .NE. 0) GOTO 9999
C
      CALL LEKTAB(IDOMA,'XXSURFAC',ICHPSU)
      IF(IERR .NE. 0) GOTO 9999
      INDIC = 1
      NBCOMP = 1
      MOT = 'SCAL'
      CALL QUEPOI(ICHPSU, MELEMF, INDIC, NBCOMP, MOT)
      IF(IERR .NE. 0) GOTO 9999
C
C**** Les normales aux faces
C
      IF(IDIM .EQ. 2)THEN
C        Que les normales
         CALL LEKTAB(IDOMA,'XXNORMAF',INORM)
         IF(IERR .NE. 0) GOTO 9999
         JGN = 4
         JGM = 2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'UX  '
         MLMVIT.MOTS(2) = 'UY  '
         CALL QUEPO1(INORM, MELEMF, MLMVIT)
         SEGSUP MLMVIT
         IF(IERR .NE. 0) GOTO 9999
      ELSE
C
C****    Les normales ('MX  ', ...)
C        Les tangentes ('RX  ', ...)
C
         TYPE = ' '
         CALL ACMO(IDOMA,'MATROT',TYPE,INORM)
         IF (TYPE .NE. 'CHPOINT ') THEN
            CALL MATRAN(IDOMA,INORM)
            IF(IERR .NE. 0) GOTO 9999
         ENDIF
         JGN = 4
         JGM = 9
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'MX  '
         MLMVIT.MOTS(2) = 'MY  '
         MLMVIT.MOTS(3) = 'MZ  '
         MLMVIT.MOTS(4) = 'RX  '
         MLMVIT.MOTS(5) = 'RY  '
         MLMVIT.MOTS(6) = 'RZ  '
         MLMVIT.MOTS(7) = 'UX  '
         MLMVIT.MOTS(8) = 'UY  '
         MLMVIT.MOTS(9) = 'UZ  '
         CALL QUEPO1(INORM, MELEMF, MLMVIT)
         SEGSUP MLMVIT
      ENDIF
C
C**********************************
C**** La table domaine du bord ****
C**********************************
C
      CALL LIROBJ('MMODEL',MMODEL,1,IRET)
      IF(IERR.NE.0)GOTO 9999
      CALL LEKMOD(MMODEL,IDBOR,INEFMD)
C     INEFMD inutilisé
      IF(IERR .NE. 0)GOTO 9999
C
      CALL LEKTAB(IDBOR,'CENTRE',MELECB)
      IF(IERR .NE. 0) GOTO 9999
C
      TYPE = ' '
      CALL ACMO(IDBOR,'FACCEN',TYPE,MELEFC)
      IF (TYPE.NE.'MAILLAGE') THEN
C
         CALL LEKTAB(IDOMA,'FACEL',MELEFE)
         IF(IERR .NE. 0) GOTO 9999
C
C******* On cree la connectivité face-centre
C
         IPT1=MELECB
         IPT2=MELEFE
         SEGACT IPT1
         SEGACT IPT2
         CALL KRIPAD(IPT1,MLENTI)
C        SEGINI MLENTI
         NBELEM=IPT1.NUM(/2)
         NBNN=2
         NBSOUS=0
         NBREF=0
         SEGINI IPT3
         IPT3.ITYPEL=2
         N1=IPT2.NUM(/2)
         ICEN=0
         DO I1=1,N1,1
            NGF=IPT2.NUM(2,I1)
            NLC=MLENTI.LECT(NGF)
            IF(NLC.NE.0)THEN
               ICEN=ICEN+1
               IPT3.NUM(1,ICEN)=NGF
               IPT3.NUM(2,ICEN)=IPT2.NUM(1,I1)
               IF(IPT2.NUM(1,I1) .NE. IPT2.NUM(3,I1))THEN
C                Interior point
C                Donné incompatible
                  WRITE(IOIMP,*) 'Internal boundary condition!!!'
                  CALL ERREUR(21)
               ENDIF
            ENDIF
         ENDDO
C
         IF(ICEN .NE. NBELEM)THEN
            CALL ERREUR(5)
         ENDIF
         SEGDES IPT1
         SEGDES IPT2
         SEGDES IPT3
         SEGSUP MLENTI
C
         MELEFC=IPT3
         CALL ECMO(IDBOR,'FACCEN','MAILLAGE',IPT3)
      ENDIF
C
C**** Le SPG du residu
C
      IPT1=MELEFC
      SEGACT IPT1
      NBELEM=IPT1.NUM(/2)
      NBNN=1
      NBSOUS=0
      NBREF=0
      SEGINI IPT2
      IPT2.ITYPEL=1
      DO I1=1,NBELEM,1
         IPT2.NUM(1,I1)=IPT1.NUM(2,I1)
      ENDDO
      MELRES=IPT2
      SEGDES IPT1
      SEGDES IPT2
C
C**** Noms de variables conservatives
C
      TYPE='LISTMOTS'
      CALL LIROBJ(TYPE,ILIINC,1,IRET)
      IF(IERR .NE. 0) GOTO 9999
      MLMOTS = ILIINC
      SEGACT MLMOTS
      NBCOMP = MLMOTS.MOTS(/2)
      SEGDES MLMOTS
      IF(NBCOMP .NE. (IDIM+2))THEN
         MOTERR(1:40) = 'LISTINCO = ???'
         WRITE(IOIMP,*) MOTERR
C
C******* Message d'erreur standard
C        21 2
C        Données incompatibles
C
         CALL ERREUR(21)
         GOTO 9999
      ENDIF
C
C**** Noms de variables primitives
C
      TYPE='LISTMOTS'
      CALL LIROBJ(TYPE,ILIINP,1,IRET)
      IF(IERR .NE. 0) GOTO 9999
      MLMOTS = ILIINP
      SEGACT MLMOTS
      NBCOMP = MLMOTS.MOTS(/2)
      SEGDES MLMOTS
      IF(NBCOMP .NE. (IDIM+2))THEN
         MOTERR(1:40) = 'LISTPRIM = ???'
         WRITE(IOIMP,*) MOTERR
C
C******* Message d'erreur standard
C        21 2
C        Données incompatibles
C
         CALL ERREUR(21)
         GOTO 9999
      ENDIF
C
C**** Lecture du CHPOINT RN
C
      TYPE='CHPOINT '
      CALL LIROBJ(TYPE,IROC,1,IRET)
      IF (IERR.NE.0) GOTO 9999
C
C**** Control du CHPOINT: QUEPOI
C
C     INDIC = 1   -> on impose le pointeur du support geometrique
C     NBCOMP > 0 -> nombre des composantes
C
      INDIC = 1
      NBCOMP = 1
      MOT = 'SCAL'
      CALL QUEPOI(IROC, MELEMC, INDIC, NBCOMP, MOT)
      IF(IERR .NE. 0)GOTO 9999
C
C**** Lecture du CHPOINT VITC
C
      CALL LIROBJ('CHPOINT',IVITC,1,IRET)
      IF (IERR.NE.0) GOTO 9999
C
C**** Control du CHPOINT
C
      JGN = 4
      JGM = IDIM
      SEGINI MLMVIT
      MLMVIT.MOTS(1) = 'UX  '
      MLMVIT.MOTS(2) = 'UY  '
      IF(IDIM .EQ. 3)  MLMVIT.MOTS(3) = 'UZ  '
      CALL QUEPO1(IVITC, MELEMC, MLMVIT)
      SEGSUP MLMVIT
      IF(IERR .NE. 0)GOTO 9999
C
C**** Lecture du CHPOINT PC
C
      CALL LIROBJ('CHPOINT',IPC,1,IRET)
      IF (IERR.NE.0) GOTO 9999
C
C**** Control du CHPOINT
C
      INDIC = 1
      NBCOMP = 1
      MOT = 'SCAL'
      CALL QUEPOI(IPC, MELEMC, INDIC, NBCOMP, MOT)
      IF(IERR .NE. 0)GOTO 9999
C
C**** Lecture du CHPOINT GAMC
C
      CALL LIROBJ('CHPOINT',IGAMC,1,IRET)
      IF (IERR.NE.0) GOTO 9999
C
C**** Control du CHPOINT
C
      INDIC = 1
      NBCOMP = 1
      MOT = 'SCAL'
      CALL QUEPOI(IGAMC, MELEMC, INDIC, NBCOMP, MOT)
      IF(IERR .NE. 0)GOTO 9999
C
C**** CHPOINT condition limite
C
      CALL LIROBJ('CHPOINT',ICHLIM,1,IRET)
      IF (IERR.NE.0) GOTO 9999
C
C**** Resultats
C
      IF(IJAC .EQ.0)THEN
         TYPE='        '
         CALL KRCHP1(TYPE,MELRES,ICHRES,ILIINC)
C
         TYPE='        '
         CALL KRCHP1(TYPE,MELECB,ICHRLI,ILIINP)
      ELSE
         ICHRES=0
         ICHRLI=0
      ENDIF
C
C**** TYPE DE CONDITION LIMITE
C
      CALL LIRMOT(LOPT,NBOPT,ILIM,1)
      IF(IERR .NE. 0) GOTO 9999
      IF(ILIM .EQ. 1)THEN
C
C********  'INRI    '
C
         JGN = 4
         JGM = IDIM+2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'RN  '
         MLMVIT.MOTS(2) = 'UX  '
         MLMVIT.MOTS(3) = 'UY  '
         IF(IDIM .EQ. 3)  MLMVIT.MOTS(4) = 'UZ  '
         MLMVIT.MOTS(2+IDIM)='PN  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI111(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI112(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI113(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
            IF(IERR.NE.0)GOTO 9999
         ENDIF
      ELSEIF(ILIM .EQ. 2)THEN
C
C********  'OUTRI   '
C
         JGN = 4
         JGM = IDIM+2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'RN  '
         MLMVIT.MOTS(2) = 'UX  '
         MLMVIT.MOTS(3) = 'UY  '
         IF(IDIM .EQ. 3)  MLMVIT.MOTS(4) = 'UZ  '
         MLMVIT.MOTS(2+IDIM)='PN  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI121(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI122(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI123(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
            IF(IERR.NE.0)GOTO 9999
         ENDIF
      ELSEIF(ILIM .EQ. 3)THEN
C
C********  'INSS    '
C
         JGN = 4
         JGM = IDIM+2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'RN  '
         MLMVIT.MOTS(2) = 'UX  '
         MLMVIT.MOTS(3) = 'UY  '
         IF(IDIM .EQ. 3)  MLMVIT.MOTS(4) = 'UZ  '
         MLMVIT.MOTS(2+IDIM)='PN  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI131(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
*           Le Jacobien est une matrik vide
            NRIGE=7
            NMATRI=0
            NKID =9
            NKMT =7
            SEGINI MATRIK
            SEGDES MATRIK
            IJACO=MATRIK
         ENDIF
      ELSEIF(ILIM .EQ. 4)THEN
C
C********  'OUTSS   '
C
C        ICHLIM est un CHPOINT vide
C        Mais on fait pas de controlle
C
         IF(IJAC.EQ.0)THEN
            CALL CLI141(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI142(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI143(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
            IF(IERR.NE.0)GOTO 9999
         ENDIF
      ELSEIF(ILIM .EQ. 5)THEN
C
C********  'OUTP   '
C
         JGN = 4
         JGM = 1
         SEGINI MLMVIT
         MLMVIT.MOTS(1)='PN  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI151(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI152(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI153(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
         ENDIF
      ELSEIF(ILIM .EQ. 6)THEN
C
C********  'INSU    '
C
         JGN = 4
         JGM = 2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'HT  '
         MLMVIT.MOTS(2) = 'S   '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI161(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI162(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     &              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     &              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI163(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
         ENDIF
      ELSEIF(ILIM .EQ. 7)THEN
C
C********  'INJE    '
C
         JGN = 4
         JGM = 2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'MOME'
         MLMVIT.MOTS(2) = 'RT  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI181(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI182(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     &              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     &              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI183(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
         ENDIF
      ELSEIF(ILIM .EQ. 8)THEN
C
C********  'INJELM  '
C
         JGN = 4
         JGM = 2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'MOME'
         MLMVIT.MOTS(2) = 'RT  '
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI171(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ELSE
            IF(IDIM.EQ.2)THEN
               CALL CLI172(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     &              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     &              ,ILIINP,IJAC,IJACO)
            ELSE
               CALL CLI173(MELEMF,MELEMC,MELECB,MELEFC,MELRES,INORM
     $              ,ICHPVO,ICHPSU,IROC,IVITC,IPC,IGAMC,ICHLIM,ILIINC
     $              ,ILIINP,IJAC,IJACO)
            ENDIF
         ENDIF
      ELSEIF(ILIM .EQ. 9)THEN
C
C********  'INSO  '
C
         JGN = 4
         JGM = 2
         SEGINI MLMVIT
         MLMVIT.MOTS(1) = 'PSTA'
         MLMVIT.MOTS(2) = 'RSTA'
         CALL QUEPO1(ICHLIM, MELECB, MLMVIT)
         SEGSUP MLMVIT
         IF (IERR.NE.0) GOTO 9999
C
         IF(IJAC.EQ.0)THEN
            CALL CLI191(MELEMF,MELEMC,MELECB,MELEFC,INORM,ICHPVO,ICHPSU,
     &           IROC,IVITC,IPC,IGAMC,ICHLIM,ICHRES,ICHRLI)
            IF(IERR.NE.0)GOTO 9999
         ENDIF
      ENDIF
C
      IF(IJAC.EQ.0)THEN
         CALL ECROBJ('CHPOINT ',ICHRES)
         CALL ECROBJ('CHPOINT ',ICHRLI)
      ELSE
         CALL ECROBJ('MATRIK  ',IJACO)
      ENDIF
C
 9999 CONTINUE
      RETURN
      END















