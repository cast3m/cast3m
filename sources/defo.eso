C DEFO      SOURCE    BP208322  16/11/18    21:16:14     9177
C  CONSTRUCTION D'UN OBJET DE TYPE DEFORME A PARTIR D'UNE GEOMETRIE
C  D'UN CHPOIN UX UY UZ ET D'UN COEF D'AMPLIFICATION
C  1995 Changement de defaut de couleur P.PEGON JRC-ISPRA
C
      SUBROUTINE DEFO
      IMPLICIT INTEGER(I-N)
-INC CCOPTIO
-INC CCGEOME
-INC SMCHPOI
-INC SMDEFOR
-INC SMCOORD
-INC SMELEME
      CHARACTER*4 NU(3),NV(3),NOC
      REAL*8 AMP,COMPMA
      DATA NU/'UX  ','UY  ','UZ  '/
      DATA NV/'UR  ','UZ  ','UT  '/
      CALL LIROBJ('MAILLAGE',MELEME,1,IRETOU)
      CALL LIROBJ('CHPOINT ',MCHPOI,1,IRETOU)
C  AMPLIFICATION 1 PAR DEFAUT
      AMP=1.
      CALL LIRREE(AMP,0,IRETOA)
      IF (IERR.NE.0) RETURN
      MTVE=0
      CALL LIROBJ('VECTEUR ',MTVE,0,IRETOU)
      CALL LIRMOT(NCOUL,NBCOUL,ICOUL,0)
      IF (ICOUL.EQ.0) ICOUL=IDCOUL+1
      ICOUL=ICOUL-1
C  VERIFICATION QU'IL Y A UX UY UZ PARMI LES INCONNUES DU CHPOINT
      SEGACT MCHPOI
      NSOUPO=IPCHP(/1)
      IRAT=1
      IF (NSOUPO.EQ.0) GOTO 13
      IRAT=1
      DO 10 ISOUPO=1,NSOUPO
      MSOUPO=IPCHP(ISOUPO)
      SEGACT MSOUPO
      NC=NOCOMP(/2)
      DO 11 I=1,NC
      NOC=NOCOMP(I)
      DO 12 J=1,IDIM
      IF (IFOUR.NE.0.AND.IFOUR.NE.1) THEN
      IF (NOC.EQ.NU(J)) IRAT=0
      ELSE
      IF (NOC.EQ.NV(J)) IRAT=0
      ENDIF
  12  CONTINUE
  11  CONTINUE
      SEGDES MSOUPO
  10  CONTINUE
      SEGDES MCHPOI
      IF (IRAT.EQ.1) CALL ERREUR(184)
  13  CONTINUE
      NDEF=1
      SEGINI MDEFOR
      AMPL(1)=AMP
      MTVECT(1)=MTVE
      IELDEF(1)=MELEME
      ICHDEF(1)=MCHPOI
      JCOUL(1)=ICOUL
      CALL ECROBJ('DEFORME ',MDEFOR)
      IF (IRETOA.EQ.1) GOTO 1000
*  CAS DE L'AMPLIFICATION AUTOMATIQUE ON MODIFIE L'OBJET
*  QU'ON VIENT D'ECRIRE. C'EST PAS BIEN |
*  CALCUL DU CADRE
      XGRAND=-1E30
      YGRAND=-1E30
      ZGRAND=-1E30
      XPETIT= 1E30
      YPETIT= 1E30
      ZPETIT= 1E30
      SEGACT MELEME
      NBSOUS=LISOUS(/1)
      IPT1=MELEME
      DO 50 ISOUS=1,MAX0(1,NBSOUS)
      IF (NBSOUS.NE.0) IPT1=LISOUS(ISOUS)
      SEGACT IPT1
       DO 55 I=1,IPT1.NUM(/1)
       DO 55 J=1,IPT1.NUM(/2)
        IPT=IPT1.NUM(I,J)
        IREF=(IDIM+1)*(IPT-1)
        XPT=XCOOR(IREF+1)
        XGRAND=MAX(XPT,XGRAND)
        XPETIT=MIN(XPT,XPETIT)
        YPT=XCOOR(IREF+2)
        YGRAND=MAX(YPT,YGRAND)
        YPETIT=MIN(YPT,YPETIT)
        ZPT=XCOOR(IREF+3)
        ZGRAND=MAX(ZPT,ZGRAND)
        ZPETIT=MIN(ZPT,ZPETIT)
  55  CONTINUE
      IF (NBSOUS.EQ.0) GOTO 60
      SEGDES IPT1
  50  CONTINUE
      SEGDES MELEME
  60  CONTINUE
      CLONG=MAX(XGRAND-XPETIT,YGRAND-YPETIT,ZGRAND-ZPETIT)
*  ON VA ADMETTRE QUE LA DEFORME MAXIMALE EST CLONG/10 ???
      COMPMA=0.D0
*  BOUCLE DANS LE CHAMP POINT
      SEGACT MCHPOI
      NSOUPO=IPCHP(/1)
      IF (NSOUPO.EQ.0) GOTO 113
      DO 100 ISOUPO=1,NSOUPO
      MSOUPO=IPCHP(ISOUPO)
      SEGACT MSOUPO
      MPOVAL=IPOVAL
      SEGACT MPOVAL
      NC=NOCOMP(/2)
      DO 110 I=1,NC
       NOC=NOCOMP(I)
       DO 120 J=1,IDIM
        IF (IFOUR.NE.0.AND.IFOUR.NE.1) THEN
        IF (NOC.EQ.NU(J)) GOTO 130
        ELSE
        IF (NOC.EQ.NV(J)) GOTO 130
        ENDIF
        GOTO 120
 130    CONTINUE
        DO 140 K=1,VPOCHA(/1)
        COMPMA=MAX(COMPMA,ABS(VPOCHA(K,I)))
 140    CONTINUE
 120   CONTINUE
 110  CONTINUE
      SEGDES MSOUPO,MPOVAL
 100  CONTINUE
      SEGDES MCHPOI
      IF (COMPMA.EQ.0.D0) then
        ampl(1)=0
      ELSE
        AMPL(1)=CLONG/(10*COMPMA)
      endif
 1000 CONTINUE
*  lecture eventuelle d'un champoint ou d'un chamelem + model
      CALL LIROBJ('CHPOINT ',MCHPOI,0,IRETOU)
        if (iretou.ne.0) mdchp(1)=mchpoi
      if (iretou.eq.0) then
          CALL LIROBJ('MCHAML   ',MCHEL,0,IRETOU)
            if (iretou.ne.0) then
                CALL LIROBJ('MMODEL  ',MODEL,1,IRETOU)
                if(ierr.ne.0) return
                mdmode(1)=model
*  PASSER LES CHAMELEM AUX noeuds
                CALL CHASUP(MODEL,MCHEL,MCHAM,IRET,1)
                IF (IRET.NE.0) MCHAM=MCHEL
                mdchel(1)=mcham
            endif
      endif
      SEGDES MDEFOR
      RETURN
 113  CONTINUE
      CALL ERREUR(475)
      RETURN
      END







