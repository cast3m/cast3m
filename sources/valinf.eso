C VALINF    SOURCE    CHAT      05/01/13    04:00:36     5004
      SUBROUTINE VALINF(X,XDEGRA,CURVE,NCURV,YOUN,Y,KERRE)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
C============================================================
C 25/1/96  D. C. Modif de la routine de Pierre
C  Routine qui lit la valeur d'une courbe
C  quand on lui donne l'abscisse
C  Adaptation au cas d'une famille de courbe dependant d'un parametre
C
C  ENTREES:
C    X     : Abscisse (deformation totale -E>0- ou plastique-Ereel)
C    XDEGRA: Parametre definissant la famille de courbe
C    CURVE : Courbe
C    NCURV : Nombre de points de la courbe
C    YOUN  : Module de decharge elastique E
C
C SORTIES
C    Y     : Valeur de la fonction en X
C=============================================================
      PARAMETER (XZER = 0.D0, EPSILO=1.D-20)
      DIMENSION CURVE(2,NCURV)
C
      Y00 = XDEGRA * CURVE(2,1)
      IF (ABS(YOUN).GT.EPSILO) THEN
         X00 = (Y00/YOUN)
      ELSE
         X00 = XZER
      ENDIF
      DO IE1=2,NCURV
        YC = XDEGRA * CURVE(2,IE1)
        XC = CURVE(1,IE1)
        IF (ABS(YOUN).GT.EPSILO) THEN
          XBIS = XC + (YC/YOUN)
        ELSE
          XBIS = XC
        ENDIF
        IF (X.LE.XBIS) GOTO 1
        Y00 = YC
        X00 = XBIS
      ENDDO
      Y = Y00
      GOTO 2
 1    Y = Y00 + ((YC - Y00)/(XBIS - X00))*(X - X00)
C
 2    CONTINUE
      RETURN
      END



