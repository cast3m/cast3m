C DHVDT0    SOURCE    SERRE     12/07/10    21:15:06     7431
      FUNCTION DHVDT0(P,T)

C--------------------------------------------------------------------
C Derivee partielle de enthalpie specifique vapeur par rapport a la
C Temperature T
C Issue de la fonction enthalpie specifique vapeur HVS0(P,T)
C--------------------------------------------------------------------
C  E/  P    : Pression partielle de vapeur (en Pa)
C  E/  T    : Température (en K)
C   /S DHVDT0 : Derivee de enthalpie specifique (en J/kg/K)
C             d/dT (Hvap(Pvap,T))
C--------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     COEFFICIENTS

      A1=1.748383D3
      A2=0.277768D0
      A3=0.275D-3


      B0 = -4512.70756D0
      B1 = 4245585.9D0
      B2 = -1584624342.D0
      R  = 461.523D0

C     premier terme de la formule
      VAL0  = A1+(A3*T+A2)*T

C     ECRITURE DE EPSILON(Pv,T)
C     *************************************************
      EPSILON= P*((B0*T+B1)*T+B2)
      EPSILON= (EPSILON/(T*T*T*T))+((R*R)/4)
      EPSILON = SQRT(EPSILON)

C     ECRITURE DE VAL1
C     *************************************************
      VAL1=(-R/2)+EPSILON

C     ECRITURE DE VAL2
C     *************************************************
      S1=2*B0*T*T*T
      S2=3*B1*T*T
      S3=4*B2*T
      S4=S1+S2+S3
      S5= (B0*T+B1)*T+B2
      VAL2=S4/S5

C     ECRITURE DE BETA1
C     *************************************************
      X1=(6*B0*T*T)+(6*B1*T)+4*B2
      X2= (B0*T*T)+(B1*T)+B2
      X3=(2*B0*T*T*T)+(3*B1*T*T)+(4*B2*T)
      X4=(2*B0*T)+B1
      BETA1=((X1*X2)-(X3*X4))/(X2*X2)

C     ECRITURE DE BETA2
C     *************************************************
      Y1=(((2*B0*T+3*B1)*T)+4*B2)*(-P)
      Y2=2*EPSILON*(T*T*T*T*T)
      BETA2=Y1/Y2

C     ECRITURE DE LA FORMULE BRUTE
C     *************************************************
      DHVDT0 =VAL0 + VAL1*BETA1+VAL2*BETA2
      RETURN
      END





