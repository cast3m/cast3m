C TIMOLO    SOURCE    CHAT      05/01/13    03:40:40     5004
      SUBROUTINE TIMOLO(V,CONT,XL,YOUN,CISAIL,SD,SREDY,SREDZ,
     .                  XINX,XINY,XINZ,IDEUX)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES CONTRAINTES DE LA POUTRE DROITE
C    D APRES TEDEL
C
C  ENTREE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    SREDZ   SECTION REDUITE A L EFFORT TRANCHANT TZ
C    XINX    INERTIE IX
C    XINY    INERTIE IY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C    IDEUX   INDICE POUR DEFORMATIONS DU SECOND ORDRE
C  SORTIE
C    CONT    CONTRAINTES AU MILIEU 6 PAR NOEUD
C
C    PP 4/9/92
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),V(*)
      DATA P5/0.5D0/
C
      RL=1.D00/XL
      RL2=RL*RL
      RL3= RL2*RL
C
C  SECTION "REDUITE"
C
      IF ( SREDZ. NE.0. ) THEN
           SRZ=SREDZ
      ELSE
           SRZ=SD
      ENDIF
      IF ( SREDY. NE.0. ) THEN
           SRY=SREDY
      ELSE
           SRY=SD
      ENDIF
C
C  DEFORMATIONS GENERALISEES
C
      EPS  = (V(7)-V(1))*RL
      EPTY = (V(8)-V(2))*RL - (V(6)+V(12))*P5
      EPTZ = (V(9)-V(3))*RL + (V(5)+V(11))*P5
      XSIX = (V(10)-V(4))*RL
      XSIY =-(V(5)-V(11))*RL
      XSIZ =-(V(6)-V(12))*RL
C
C  TERMES DU SECOND ORDRE pour la membrane
C
      IF(IDEUX.NE.0) THEN
      AUX1=0.5D0*(EPS**2+((V(8)-V(2))*RL)**2 +((V(9)-V(3))*RL)**2)
      EPS = EPS + AUX1
      ENDIF
C
C  CONTRAINTES GENERALISEES
C
      SIG  =YOUN*SD*    EPS
      TRANY=CISAIL*SRY* EPTY
      TRANZ=CISAIL*SRZ* EPTZ
      TOR  =CISAIL*XINX*XSIX
      XMOMY=YOUN*XINY*  XSIY
      XMOMZ=YOUN*XINZ*  XSIZ
C
C  REMPLISSAGE DU TABLEAU CONT
C
      CONT(1)=SIG
      CONT(2)=TRANY
      CONT(3)=TRANZ
      CONT(4)=TOR
      CONT(5)=XMOMY
      CONT(6)=XMOMZ
      RETURN
      END


