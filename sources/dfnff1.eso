C DFNFF1    SOURCE    GOUNAND   06/08/04    21:15:24     5520
      SUBROUTINE DFNFF1(NDDL,IESREF,IESREL,NBPOGO,NBELEV,NBELEF,NBELFV,
     $     NPJMI,
     $     DFFPG,JMIJAC,SSFACT,
     $     JDFFPG,
     $     IMPR,IRET)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C***********************************************************************
C NOM         : DFNFF1
C DESCRIPTION :
C
C
C
C LANGAGE     : Fortran 77 (sauf E/S)
C AUTEUR      : Stéphane GOUNAND (CEA/DRN/DMT/SEMT/LTMF)
C               mél : gounand@semt2.smts.cea.fr
C***********************************************************************
C APPELES          : -
C APPELE PAR       : DFNFR
C***********************************************************************
C ENTREES            : * NDDL   (type entier) : nombre de ddl (par
C                        élément) de la transformation géométrique.
C                      * IESREF (type entier) : dimension de l'espace de
C                        référence.
C                      * IESREL (type entier) : dimension de l'espace
C                        réel (i.e. géométrique).
C                      * NBPOGO (type entier) : nombre de points
C                        d'intégration.
C                      * NBELEM (type entier) : nombre d'éléments du
C                        maillage élémentaire courant.
C                      * DFFPG  (type réel)   : valeurs des dérivées
C                        premières des fonctions d'interpolation aux
C                        points de gauss sur l'élément de référence.
C                      * JMIJAC (type réel)   : valeurs de l'inverse de
C                        la matrice jacobienne aux points de Gauss sur
C                        le maillage élémentaire.
C ENTREES/SORTIES    : * JDFFPG (type réel)   : valeurs des dérivées
C                        premières des fonctions d'interpolation
C                        aux points de gauss sur l'élément réel.
C SORTIES            : -
C***********************************************************************
C VERSION    : v2, 03/10/03, refonte complète (modif SMPOUET)
C VERSION    : v1, 17/01/03, version initiale
C HISTORIQUE : v1, 17/01/03, création
C HISTORIQUE :
C HISTORIQUE :
C***********************************************************************
C Prière de PRENDRE LE TEMPS de compléter les commentaires
C en cas de modification de ce sous-programme afin de faciliter
C la maintenance !
C***********************************************************************
-INC CCOPTIO
      INTEGER NDDL,IESREF,IESREL,NBPOGO,NBELEV,NBELEF,NBELFV,NPJMI
      REAL*8 DFFPG (NDDL,IESREF,NBPOGO,NBELFV)
      REAL*8 JMIJAC(IESREF,IESREL,NPJMI,NBELEF)
      LOGICAL SSFACT(NBELFV,NBELEV)
      REAL*8 JDFFPG(NDDL,IESREL,NBPOGO,NBELEF)
*
      INTEGER IMPR,IRET
*
      INTEGER IDDL,ISREF,ISREL,IBPOGO,IBELEV,IBELEF,IBELFV,IPJMI
*
* Executable statements
*
      IF (IMPR.GT.1) WRITE(IOIMP,*) 'Entrée dans dfnff1'
      IBELEF=0
      DO 1 IBELEV=1,NBELEV
         DO IBELFV=1,NBELFV
            IF (SSFACT(IBELFV,IBELEV)) THEN
               IBELEF=IBELEF+1
               DO 12 IBPOGO=1,NBPOGO
                  IF (NPJMI.EQ.1) THEN
                     IPJMI=1
                  ELSE
                     IPJMI=IBPOGO
                  ENDIF
                  DO 122 ISREL=1,IESREL
                     DO 1222 ISREF=1,IESREF
                        DO 12222 IDDL=1,NDDL
                           JDFFPG(IDDL,ISREL,IBPOGO,IBELEF)=
     $                          JDFFPG(IDDL,ISREL,IBPOGO,IBELEF)+
     $                          (DFFPG(IDDL,ISREF,IBPOGO,IBELFV)
     $                          *JMIJAC(ISREF,ISREL,IPJMI,IBELEF))
12222                   CONTINUE
 1222                CONTINUE
 122              CONTINUE
 12            CONTINUE
            ENDIF
         ENDDO
 1    CONTINUE

*
* Normal termination
*
      IRET=0
      RETURN
*
* Format handling
*
*
* Error handling
*
 9999 CONTINUE
      IRET=1
      WRITE(IOIMP,*) 'An error was detected in subroutine dfnff1'
      RETURN
*
* End of subroutine DFNFF1
*
      END


