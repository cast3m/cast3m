C CHILTO    SOURCE    SERRE     16/12/13    21:15:02     9254
      SUBROUTINE CHILTO(IPT1,IPT2,IPT3,IPT4,
     &                  MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,
     &                  MPOVA6,MPOVA7,MPOVA8,MPOVA9,MPOVAA,
     &                  MPOVAB,MPOVAC,MPOVAD,MPOVAE,G1,HMAX,
     &                  MPOUT1,MPOUT2,MPOUT3,MPOUT4)
C--------------------------------------------------------------------
C Evaluation du coef. d'échange thermique en paroi et du débit de
C condensation lorsque la corrélation retenue est celle de CHILTON
C--------------------------------------------------------------------
C
C  E/   IPT1   : Maillage des points CELL
C  E/   IPT2   : Maillage des points LAYERW
C  E/   IPT3   : Maillage des points WALL
C  E/   IPT4   : Maillage de SEG2 CELL/LAYERW
C  E/   MPOVA1 : Pression totale
C  E/   MPOVA2 : Température du mélange gazeux
C  E/   MPOVA3 : Densité du mélange - (en kg/m3)
C  E/   MPOVA4 : Densité partielle de vapeur (en kg/m3)
C  E/   MPOVA5 : Pression partielle de vapeur
C  E/   MPOVA6 : Température de saturation
C  E/   MPOVA7 : Coefficient d'échange par conduction
C  E/   MPOVA8 : Température des murs
C  E/   MPOVA9 : Surface des parois
C  E/   MPOVAA : Flux de chaleur en paroi
C  E/   MPOVAB : Lambda du mélange
C  E/   MPOVAC : Nu du mélange
C  E/   MPOVAD : Constante des gaz parfait des incondensables
C  E/   MPOVAE : Coeff de diffusion de la vapeur
C  E/   G1     : Gravité (accélération de la pesanteur)
C  E/   HMAX   : Borne sup pour le coefficient de transfert thermique
C   /S  MPOUT1 : Coefficient d'échange global en paroi
C   /S  MPOUT2 : Débit de condensation / flux thermique
C   /S  MPOUT3 : Enthalpie liquide
C   /S  MPOUT4 : Coefficient d'échange par convection
C
C--------------------------------------------------------------------
C Les segments MPOUT... ne sont pas désactivés
C--------------------------------------------------------------------
C 03/07/02 : Le débit de condensation est mis à 0. si il est positif
C FD         On se sert de QW2 updaté afin d'en lisser les variations
C
C
C--------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN77
C Auteurs : F.DABBENE 12/01
C
C--------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      POINTEUR MPOVA7.MPOVAL,MPOVA8.MPOVAL,MPOVA9.MPOVAL,MPOVAA.MPOVAL
      POINTEUR MPOVAB.MPOVAL,MPOVAC.MPOVAL,MPOVAD.MPOVAL,MPOVAE.MPOVAL
      POINTEUR MPOUT1.MPOVAL,MPOUT2.MPOVAL,MPOUT3.MPOVAL,MPOUT4.MPOVAL
C
-INC CCOPTIO
-INC SMCHPOI
-INC SMCOORD
-INC SMELEME
-INC SMTABLE
      SEGMENT REDIR
        INTEGER IND1(NNGOT),IND2(NNGOT),IND3(NNGOT)
      ENDSEGMENT
C
C- Tableau d'adressage indirect
C
      SEGACT IPT1,IPT2,IPT3,IPT4
      SEGACT MPOVA1,MPOVA2,MPOVA3,MPOVA4,MPOVA5,MPOVA6,MPOVA7,MPOVA8,
     &       MPOVA9,MPOVAA,MPOVAB,MPOVAC,MPOVAD,MPOVAE
      NNGOT  = XCOOR(/1)/(IDIM+1)
      SEGINI REDIR
      NBLAY  = IPT2.NUM(/2)
      DO 10 I=1,NBLAY
         K = IPT2.NUM(1,I)
         IND2(K) = I
 10   CONTINUE
      NBWAL  = IPT3.NUM(/2)
      DO 20 I=1,NBWAL
         K = IPT3.NUM(1,I)
         IND3(K) = I
 20   CONTINUE
      NBCEL  = IPT1.NUM(/2)
      DO 30 I=1,NBCEL
         K = IPT1.NUM(1,I)
         IND1(K) = I
 30   CONTINUE
      UST    = 1.D0/3.D0
      DST    = 2.D0/3.D0
C
C- Pour chaque couple CELL/LAYERW on calcule les grandeurs liees a la
C  condensation
C
      SEGINI, MPOUT1=MPOVA7
      N  = MPOVA7.VPOCHA(/1)
      NC = 1
      SEGINI MPOUT2,MPOUT3,MPOUT4
      N1 = IPT4.NUM(/2)
      DO 70 I=1,N1
C
C Coeff d'échange : correction pour les murs en béton
C Evaluation du débit de condensation
C
       IPOS = IND2(IPT4.NUM(2,I))
       IF (IPOS.NE.0) THEN
          II1    = IND1(IPT4.NUM(1,I))
          JPOS   = IND3(IPT4.NUM(2,I))
          XLBD1  = MPOVAB.VPOCHA(II1,1)
          XNU1   = MPOVAC.VPOCHA(II1,1)
          RINC1  = MPOVAD.VPOCHA(II1,1)
          DV2    = MPOVAE.VPOCHA(II1,1)
          XTGAS1 = MPOVA2.VPOCHA(II1,1)
          RTOT1  = MPOVA3.VPOCHA(II1,1)
          RVAP3  = MPOVA4.VPOCHA(II1,1)
          PVP1   = MPOVA5.VPOCHA(II1,1)
          XTSAT1 = MPOVA6.VPOCHA(II1,1)
          PTOT1  = MPOVA1.VPOCHA(II1,1)
          TMUR1  = MPOVA8.VPOCHA(JPOS,1)
          AREA1  = MPOVA9.VPOCHA(IPOS,1)
          QW2    = MPOVAA.VPOCHA(IPOS,1)
          IF (TMUR1 .LT. XTSAT1) THEN
             PSATP1 = PSATT0(TMUR1)
             RVATP1 = ROVAP0(PSATP1,TMUR1)
             ROITP1 = (PTOT1 - PSATP1) / (RINC1 * TMUR1)
             XMASS1 = ABS((RVATP1+ROITP1-RTOT1)/RTOT1)
             HCONV  = 0.13D0*XLBD1*(((G1*XMASS1)/XNU1/XNU1)**UST)
             XKC1   = HCONV*(DV2**DST)/(XLBD1*(XNU1**(-1.D0*UST)))
             DEBIC1 = XKC1*AREA1*(RVAP3-RVATP1)/(PSATP1/PTOT1-1.D0)
          ELSE
             RVATP1 = ROVAP0(PVP1,TMUR1)
             ROITP1 = (PTOT1 - PVP1) / (RINC1 * TMUR1)
             XMASS1 = ABS((RVATP1+ROITP1-RTOT1)/RTOT1)
             HCONV  = 0.13D0 *XLBD1 * (((G1*XMASS1)/XNU1/XNU1) ** UST)
             DEBIC1 = 0.D0
          ENDIF
          DEBIC1 = MIN(DEBIC1,0.D0)
C
C---> C'est ici que l'on fixe les échanges d'énergie comp/mur :
C--->   Etat thermodynamique du comp    = PVAP TGAS
C--->   Etat thermodynamique du puisard = PTOT TMUR
C---> La chaleur Latente découle de la difference comp/puisard
C---> pour les états thermo ci-dessus CHOISIS A PRIORI.
C
          HL   = HLS0(PTOT1,TMUR1)
          HV   = HVS0(PVP1,XTGAS1)
          HLAT = HV - HL
C--------------------------------------------------------------------
          DT1 = XTSAT1 - TMUR1
          DT2 = TMUR1 - XTGAS1
          HCONV = MIN(HCONV,HMAX)
          IF (DT1.GT.0.1D0) THEN
            IF (DT2.LT.-0.1D0) THEN
              MPOUT1.VPOCHA(IPOS,1) = (DEBIC1*HLAT)/(AREA1*DT2) + HCONV
              QW2 = MPOUT1.VPOCHA(IPOS,1) * DT2
              MPOUT2.VPOCHA(IPOS,1) = -1.D0 * DEBIC1 / QW2
            ELSE
               MPOUT1.VPOCHA(IPOS,1) = HCONV
               MPOUT2.VPOCHA(IPOS,1) = 0.D0
            ENDIF
          ELSE
            MPOUT1.VPOCHA(IPOS,1) = HCONV
            MPOUT2.VPOCHA(IPOS,1) = 0.D0
          ENDIF
C
          HTOT1 = MPOUT1.VPOCHA(IPOS,1)
          IF (HTOT1 . LT . MPOVA7.VPOCHA(IPOS,1)) THEN
             MPOUT1 . VPOCHA(IPOS,1) = MPOVA7.VPOCHA(IPOS,1)
             HCONV  = HCONV + MPOVA7.VPOCHA(IPOS,1) - HTOT1
          ELSE
             MPOUT1 . VPOCHA(IPOS,1) = HTOT1
          ENDIF
          MPOUT3 . VPOCHA(IPOS,1) = HL
          MPOUT4 . VPOCHA(IPOS,1) = HCONV
       ENDIF
  70  CONTINUE
C
C- Ménage
C
      SEGSUP REDIR
      SEGDES IPT1,IPT2,IPT3,IPT4
C
      RETURN
      END






