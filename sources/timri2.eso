C TIMRI2    SOURCE    CHAT      05/01/13    03:40:46     5004
C TIMRIG    SOURCE    AM        93/01/12    21:36:38     781
      SUBROUTINE TIMRI2(REL,LRE,CARAC,COORD,WORK,KERRE)
C-----------------------------------------------------------------------
C  ROUTINE DE CALCUL DE LA MATRICE DE RAIDEUR ELEMENTAIRE DE L ELEMENT D
C  POUTRE DE TIMOSCHENKO DANS LES AXES GLOBAUX EN 2D
C
C  ENTREE
C          LRE     DIMENSION DU TABLEAU REL
C          CARAC   TABLEAU DE CARACTERISTIQUES GEOMETRIQUES ET
C                  MATERIELLES DE LA POUTRE  ( 5 VALEURS )
C          COORD   COORDONNEES DES 2 NOEUDS
C
C  SORTIE
C          REL     MATRICE DE RIGIDITE ELEMENTAIRE
C          KERRE   CODE D ERREUR  NON NUL SI PROBLEME , NUL SINON
C  TRAVAIL
C          WORK(300)
C
C     D'APRES TIMRIG DC 98
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(LRE,*),CARAC(*),COORD(3,*),P(2,2),
     .          XE(2),YE(2),WORK(*)
C
      KERRE=0
      SD=CARAC(1)
      XINZ=CARAC(2)
      SREDY=CARAC(3)
      YOUN=CARAC(4)
      CISAIL=CARAC(5)
      DO 2 I=1,2
      XE(I)=COORD(1,I)
  2   YE(I)=COORD(2,I)
      XL=(XE(2)-XE(1))**2+(YE(2)-YE(1))**2
      XL=SQRT(XL)
      IF(XL.EQ.0.) GO TO 999
C
C  CALCUL DE LA RIGIDITE EN AXES LOCAUX
C
      CALL TIRIL2(WORK,XL,YOUN,CISAIL,SD,SREDY,XINZ)
C
C  CALCUL DE LA MATRICE DE PASSAGE
C
      CALL POUPA2(XE,YE,P,KERRE)
      IF(KERRE.NE.0) RETURN
C
C  PASSAGE DE LA MATRICE DE RIGIDITE EN AXES GLOBAUX
C
      CALL POURO2(REL,LRE,P,WORK)
      RETURN
999   KERRE=1
      RETURN
      END


