C CHAL1     SOURCE    FANDEUR   17/12/20    21:15:11     9649

C=======================================================================
C=                            C H A L 1                                =
C=                            ---------                                =
C=                                                                     =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=   Calcul des flux equivalents a des sources volumiques              =
C=                                                                     =
C=  Parametres :  (E)=Entree  (S)=Sortie                               =
C=  ------------                                                       =
C=   IPMODE   (E)   Pointeur sur le segment MMODEL de la structure     =
C=   IPCHSO   (E)   Pointeur sur le segment MCHAML de sources donnees  =
C=                  en chaque element de la structure (champ variable) =
C=   IPCARA   (E)   Pointeur sur le segment MCHALM de CARACTERISTIQUES =
C=                  dans le cas des COQues et des BARRes               =
C=   MOCOMP   (E)   Nom de la composante du champ de flux equivalents  =
C=   IPFLUX   (S)   Pointeur sur le champ des flux nodaux equivalents  =
C=                                                                     =
C=  Remarque :  Le MODELE doit contenir exclusivement un seul type     =
C=  ----------  d'elements, soit MASSIFs, soit COQUEs, soit BARREs.    =
C=======================================================================

      SUBROUTINE CHAL1 (IPMODE,IFORMU,ITYPEF, IPCHSO,ISUPSO, IPCARA,
     &                  MOCOMP, IPFLUX)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO

-INC SMMODEL
-INC SMCHAML

      CHARACTER*(*) MOCOMP

      IPFLUX = 0

C  1 - CHANGEMENT DU SUPPORT DU MCHAML DE CARACTERISTIQUES S'IL EXISTE
C ====
C  Transport des points de Gauss aux noeuds si necessaire ?
C  On devrait plutot le faire sur ISUPSO ?
C  Mais comme pour l'instant on utilise IPCARA que pour des modeles ou
C  tout est calcule aux noeuds et ISUPSO = 1, cela va bien.
      IPCARB = IPCARA
      IF (IPCARA.NE.0) THEN
        CALL CHASUP(IPMODE,IPCARA,IPCARB,iok,ISUPSO)
        IF (IERR.NE.0 .OR. iok.NE.0) THEN
          CALL ERREUR(21)
          RETURN
        ENDIF
      ENDIF

C =====
C  2.1 - Activation du modele
C =====
      MMODEL = IPMODE
      SEGACT,MMODEL
      NSOU = mmodel.KMODEL(/1)
      DO isou = 1, NSOU
        IMODEL = mmodel.KMODEL(isou)
        SEGACT,IMODEL
      ENDDO
C =====
C  2.2 - Activation des sources
C =====
      MCHELM = IPCHSO
      SEGACT,MCHELM
c*      IF (mchelm.IMACHE(/1).NE.NSOU) THEN
c*        CALL ERREUR(21)
c*        RETURN
c*      ENDIF
C =====
C  2.3 - Activation de caracteristiques
C =====
      IF (IPCARB.NE.0) THEN
        MCHELM = IPCARB
        SEGACT,MCHELM
c*        IF (mchelm.IMACHE(/1).NE.NSOU) THEN
c*          CALL ERREUR(21)
c*          RETURN
c*        ENDIF
      ENDIF

C  3 - CREATION DU MCHAML RESULTAT (GLOBAL)
C ==========================================
      L1 = 18
      N1 = NSOU
      N3 = 6
      SEGINI,mchelm
C*      mchelm.IFOCHE = IFOMOD
      mchelm.IFOCHE = IFOUR
      mchelm.TITCHE = 'SOURCES.VOLUMIQUES'
      IPCHAL = mchelm

C  4 - CALCUL DES FLUX EQUIVALENTS AUX SOURCES VOLUMIQUES
C ========================================================
C  4.1 - Cas des elements MASSIFS (1D,2D,3D)
C =====
      IF      (ITYPEF.EQ.1) THEN
        CALL CHAMAS(IPMODE,IFORMU, IPCHSO,IPCARB,ISUPSO, MOCOMP,
     &              IPCHAL)
C =====
C  4.2 - Cas des elements de COQUE
C =====
      ELSE IF (ITYPEF.EQ.2) THEN
        CALL CHACOQ(IPMODE,IFORMU, IPCHSO,IPCARB,ISUPSO, MOCOMP,
     &              IPCHAL)
C =====
C  4.3 - Cas des elements BARREs
C =====
c*    ELSE IF (ITYPEF.EQ.3) THEN
      ELSE
        CALL CHABAT(IPMODE,IFORMU, IPCHSO,IPCARB,ISUPSO, MOCOMP,
     &              IPCHAL)
      ENDIF

C  5 - DESACTIVATION DES OBJETS UTILISES
C =======================================
C*      MMODEL = IPMODE
      DO isou = 1, NSOU
        IMODEL = mmodel.KMODEL(isou)
        SEGDES,IMODEL
      ENDDO
      SEGDES,MMODEL

      mchelm = IPCHSO
      SEGDES,mchelm

      IF (IPCARA.NE.0) THEN
        mchelm = IPCARB
        SEGDES,mchelm
      ENDIF

C Compactage eventuel du champ RESULTAT :
      mchelm = IPCHAL
      N1 = 0
      DO is = 1, NSOU
        IF (mchelm.IMACHE(is).NE.0) THEN
          N1 = N1 + 1
          mchelm.CONCHE(N1) = mchelm.CONCHE(is)
          mchelm.IMACHE(N1) = mchelm.IMACHE(is)
          mchelm.ICHAML(N1) = mchelm.ICHAML(is)
          DO js = 1, N3
            mchelm.INFCHE(N1,js) = mchelm.INFCHE(is,js)
          ENDDO
        ENDIF
      ENDDO
      IF (N1.NE.NSOU) THEN
        SEGADJ,mchelm
      ENDIF
      SEGDES,mchelm

C En cas d'erreur :
      IF (IERR.NE.0) THEN
        SEGSUP,mchelm
        IPCHAL = 0
      ENDIF

C Champ resultat (= 0 en cas d'erreur)
      IPFLUX = IPCHAL

      RETURN
      END


