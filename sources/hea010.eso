C HEA010    SOURCE    SERRE     12/07/10    21:15:18     7431
      SUBROUTINE HEA010(T,TC,HEAS,HEAF,DTCSDT,MP,TRAV)
C---------------------------------------------------------------------
C Calcul du coefficient 'ki' de RK4 (voir HEA011)
C---------------------------------------------------------------------
C
C---------------------------
C Parametres Entree/Sortie :
C---------------------------
C
C E/  T       : flottant : Temps courant au pas du RK4 considéré (s)
C E/  TC      : flottant : Température des plaques au pas du RK4
C                          considéré (K)
C  /S HEAS    : flottant : Puissance générée dans le solide (J/s)
C  /S HEAF    : flottant : Puissance transmise au fluide (J/s)
C  /S DTCSDT  : flottant : dTc/dt (K/s)
C  /S MP      : flottant : Débit de mélange traversant le HEATER (kg/s)
C E/  TRAV    : entier   : Pointeur du segment de travail
C
C------------------------------
C Variables de TRAV utilisées :
C------------------------------
C
C E/  PRESSION : Pression à l'entrée du HEATER (Pa)
C E/  TEMPENT  : Température à l'entrée du HEATER (K)
C E/  XiMOY    : Fraction molaire à l'entrée du HEATER
C E/  EPS_MH2  : Seuil en deça duquel on suppose que la consommation
C                d'H2 max est mise à 0. (kg/s)
C E/  EPS_DT   : Ecart entre la température des plaques et celle du
C                gaz (Tc-T) en deça duquel on suppose qu'il n'y a pas
C                d'échange convectif (K)
C E/  U        : Vitesse minimale dans le HEATER (= 0.01 m/s)
C E/  S        : Surface des plaques (m2)
C E/  SP       : Section de passage entrée/sortie fluide du HEATER (m2)
C E/  MC       : Masse des plaques (kg)
C E/  CPC      : Chaleur spécifique des plaques (J/kg/K)
C
C-------------------
C Autres variables :
C-------------------
C
C MH2     : Consommation fictive d'H2 par les plaques associée à la
C           puissance thermique délivrée (kg/s)
C
C---------------------------------------------------------------------
C
C Langage : ESOPE + FORTRAN 77
C
C Mise en oeuvre : H. Paillère (1997, TTMF)
C
C---------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 XE(7)
      REAL*8 MH2,MP
      segment trav
           integer iKALP,iMODEL
           real*8 e,L,Lch,Dh,S,sp,Ck
           real*8 mc,Cpc
           real*8 g,R,deltah
           real*8 M(nbesp),cstmod(ncst)
           real*8 Cpi(nbesp),al
           real*8 eps_mh2,eps_dt,eps_con,u
           real*8 XH2MOY,XO2MOY,XN2MOY,XH2OMOY,PRESSION,TEMPENT
           real*8 XHEMOY,XCO2MOY,XCOMOY
      endsegment
C
C Densité du mélange
      PE = PRESSION
      TE = TEMPENT
      XE(1) = XN2MOY
      XE(2) = XO2MOY
      XE(3) = XH2MOY
      XE(4) = XH2OMOY
      XE(5) = XHEMOY
      XE(6) = XCO2MOY
      XE(7) = XCOMOY
      ROE = FLA001(PE,TE,XE,TRAV)
C
C Débit minimum dans le PAR thermique (valeur par défaut)
      MP  = U*ROE*SP
C
C dmh2/dt équivalent à la puissance thermique du HEATER
      MH2 = CSTMOD(1) / deltah
C
C 1. Cas sans comsommation d'hydrogène (puissance thermique résiduelle)
      IF (MH2 .LT. EPS_MH2) THEN
         IF ((TC-TE) .LT. EPS_DT) THEN
C 1.1 et sans echange convectif
            HEAS   = 0.D0
            HEAF   = 0.D0
         ELSE
C 1.2 et avec echange convectif (bilan d'énergie des plaques)
            HEAS = 0.D0
            HEAF = -S*FLA009(PE,TE,TC,TE,XE,TRAV)*(TC-TE)
         ENDIF
      ELSE
C 2. Cas avec comsommation d'hydrogène
         IF ((TC-TE) .LT. EPS_DT) THEN
C 2.1 et sans echange convectif
            HEAS = DELTAH * MH2
            HEAF = 0.D0
         ELSE
C 2.2 et avec echange convectif (bilan d'énergie des plaques)
            MP = FLA015(T,TC,MH2,TRAV)
            HEAS = DELTAH * MH2
            HEAF = -S*FLA009(PE,TE,TC,TE,XE,TRAV)*(TC-TE)
         ENDIF
      ENDIF
      HEAS = HEAS * cstmod(2)
      DTCSDT = (HEAS + HEAF) / (MC*CPC)
C
      RETURN
      END










