C CQ4KSG    SOURCE    CHAT      05/01/12    22:27:01     5004
      SUBROUTINE CQ4KSG(DJAC,XSTRS,SHP,REL)
C=====================================================================
C           MATICE  KSIGMA   POUR L ELEMENT COQ4
C
C      ENTREES
C         DJAC        = JACOBIEN
C         XSTRS(6)    = CONTRAINTES AU POINT DE GAUSS
*         SHP         = Fonctions de forme et d{riv{es dans l'espace
*                       g{om{trique, au centre de l'{l{ment.
*                       SHP(2,... = d{riv{e par rapport @ "X" local,
*                       SHP(3,... = d{riv{e par rapport @ "Y" local.
*
C      SORTIES
C         REL (24,24) = MATRICE K SIGMA
C
C     DELHOMMEAU
*     P.M. 17/06/91: reprise pour nouveau COQ4, avec utilisation de
*                    "SHP" au lieu de "BGENE".
*
C=======================================================================
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION REL(24,*),XSTRS(*),SHP(6,*)
C
C
      SIGXX=XSTRS(1)
      SIGYY=XSTRS(2)
      SIGXY=XSTRS(3)
C
      DO 100 IA=1,4
      JJ=(IA-1)*6+1
      DO 200 IB=1,4
      II=(IB-1)*6+1
         CC= SHP(2,IA)*SHP(2,IB)*SIGXX + SHP(3,IA)*SHP(3,IB)*SIGYY
         DD= (SHP(2,IA)*SHP(3,IB) + SHP(2,IB)*SHP(3,IA)) * SIGXY
         S =(CC+DD)*DJAC*4.D0
         REL(II,JJ)    = S
         REL(II+1,JJ+1)= S
         REL(II+2,JJ+2)= S
 200  CONTINUE
 100  CONTINUE
C
      RETURN
      END


