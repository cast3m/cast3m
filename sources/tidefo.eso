C TIDEFO    SOURCE    CHAT      05/01/13    03:38:17     5004
      SUBROUTINE TIDEFO(V,DEFO,XL,IDEUX)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES DEFORMATIONS DE LA POUTRE DROITE
C
C  ENTREE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    XL      LONGUEUR
C    IDEUX   INDICE POUR DEFORMATIONS DU 2-EME ORDRE
C
C  SORTIE
C    DEFO    DEFORMATIONS AU MILIEU  6 PAR NOEUD
C
C    PP 4/9/92
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DEFO(*),V(*)
      DATA P5/0.5D0/
C
      RL=1.D00/XL
      RL2=RL*RL
C
C  DEFORMATIONS GENERALISEES
C
      EPS  = (V(7)-V(1))*RL
      EPTY = (V(8)-V(2))*RL - (V(6)+V(12))*P5
      EPTZ = (V(9)-V(3))*RL + (V(5)+V(11))*P5
      XSIX = (V(10)-V(4))*RL
      XSIY =-(V(5)-V(11))*RL
      XSIZ =-(V(6)-V(12))*RL
C
C  REMPLISSAGE DU TABLEAU DEFO
C
      DEFO(1)=EPS
      DEFO(2)=EPTY
      DEFO(3)=EPTZ
      DEFO(4)=XSIX
      DEFO(5)=XSIY
      DEFO(6)=XSIZ
C
      IF(IDEUX.EQ.0) RETURN
C
C  TERMES DU SECOND ORDRE pour la membrane
C
      AUX1=0.5D0*(EPS**2+((V(8)-V(2))*RL)**2 +((V(9)-V(3))*RL)**2)
      DEFO(1) = DEFO(1) + AUX1
      RETURN
      END


