C COQ8EP    SOURCE    CHAT      05/01/12    22:21:49     5004
      SUBROUTINE COQ8EP(XX,NBNN,NBPGAU,LRE,NSTRS,EPAIST,EXCEN,
     1    DZEGAU,SHPCOQ,SHPELE,XDDL,DEFO,IRR)
C=======================================================================
C
C ENTREES
C     XX(3,NBNN):  TABLEAU DES COORDONNEES DES NOEUDS
C     NBNN    : NOMBRE DE NOEUDS
C     NBPGAU  : NOMBRE DE POINTS DE GAUSS
C     LRE     :    NOMBRE DE DDL
C     NSTRS   :    NOMBRE DE DEFORMATIONS
C     EPAIST  :    EPAISSEUR DE LA COQUE
C     EXCEN   :    EXCENTREMENT DE LA COQUE
C  DZEGAU(NBPGAU): COORDONNEES REDUITES DES POINTS DE GAUSS DANS
C                  L'EPAISSEUR
C  SHPCOQ(6,NBNN,NBPGAU) :FONCTIONS DE FORME ET DERIVEES AUX PTS DE GSS
C  SHPELE(6,NBNN,NBNN) :FONCTIONS DE FORME ET DERIVEES AUX NOEUDS
C     XDDL(LRE):   DEPLACEMENTS AU NOEUDS
C SORTIES
C  DEFO(5*NBPGAU):5 DEFORMATIONS AUX NBPGAU POINTS DE GAUSS
C     IRR     :    INDICATEUR DE SUCCES ( 1 ) , D ECHEC (0 OU-1)
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(XZER=0.D0)
      DIMENSION DZEGAU(*),SHPCOQ(6,NBNN,*),SHPELE(6,NBNN,*)
      DIMENSION XX(3,*),DEFO(*),XDDL(*)
      DIMENSION TXR(3,3,8),B(5,48),TH(8),EXC(8),T(9),XSTRS(5)
C
      NDEFO=NSTRS*NBPGAU
      DO 2 J=1,NDEFO
      DEFO(J)=XZER
    2 CONTINUE
C
C   ---   DETERMINATION DES AXES LOCAUX AU NOEUD I   ---
C
      CALL CQ8LOC(XX,NBNN,SHPELE,TXR,IRR)
C
      DO 70 K=1,NBNN
      TH(K)=EPAIST
      EXC(K)=EXCEN
   70 CONTINUE
C
C----------------------------------------------------------------------
C     BOUCLE SUR LES POINTS DE GAUSS
C----------------------------------------------------------------------
C
C
      DO 100 IA=1,NBPGAU
      E3 = DZEGAU(IA)
C-------------------------------
C     CALCUL DE LA MATRICE B
C-------------------------------
      DO 3 I=1,5
      DO 3 J=1,LRE
      B(I,J)=XZER
    3 CONTINUE
      CALL BCOQ8E(IA,XX,NBNN,TH,EXC,B,DET,E3,SHPCOQ,TXR,IRR)
C
      IF(IRR.NE.1) RETURN
C
C     CALCUL DES DEFORMATIONS POUR LE POINT DE GAUSS
C
      CALL BST(B,XDDL,LRE,NSTRS,XSTRS)
C
C     RANGEMENT DANS DEFO(NSTRS*NBPGAU)
C
      DO 80 K=1,5
      JE=5*(IA-1)+K
      DEFO(JE)=XSTRS(K)
   80 CONTINUE
C
  100 CONTINUE
      RETURN
      END

