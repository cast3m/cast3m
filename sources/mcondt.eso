C MCONDT    SOURCE    FANDEUR   11/07/19    21:16:37     7042

* *******************************************************************
*     CALCUL DE LA MATRICE DE CONDUCTIVITE D'UNE COQUE(COQ2 ET COQ3)*
*     PAR UNE METHODE SEMI-ANALYTIQUE                               *
*********************************************************************
*
* PARAMETRES:  (E)=ENTREE  (S)=SORTIE  (+ = CONTENU DANS UN COMMUN )
*------------
*
*      CEL1   (E)  MATRICE BTKB INTERGREE SUR LA SURFACE MOYENNE
*      CEL2   (E)  MATRICE HTKH  INTEGREE SUR LA SURFACE MOYENNE
*      NBNN   (E)  NOMBRE DE NOEUDS PAR ELEMENT
*      EP     (E)  EPAISSEUR DE LA COQUE
*      CEL    (S)  MATRICE DE CONDUCTIBILITE TOTALE
*
* COMMENTAIRE :ON FORME LES SOUS-MATRICES DE LA MATRICE DE CONDUCTIVITE
* PAR DES COMBINAISONS LINEAIRES DE LA MATRICE BTKB(CEL1) ET HTKT(CEL2).
* LES COEFFICIENTS CORRESPONDENT AUX INTEGRALES  SUR L'EPAISSEUR.
*
*  AUTEUR,DATE DE CREATION:
*  ------------------------
*  PARVIZ DOWLATYARI MAI 1990
************************************************************************

      SUBROUTINE MCONDT(CEL1,CEL2,NBNN,EP,CEL)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

      DIMENSION CEL1(NBNN,*),CEL2(NBNN,*),CEL(3*NBNN,*)

      DATA UN,DEUX,TROIS,SEPT/1.D0,2.D0,3.D0,7.D0/
      DATA HUIT,QUINZE,SEIZE,TRENTE/8.D0,15.D0,16.D0,30.D0/

      C1  = (DEUX*EP)/QUINZE
      C2  = SEPT/(TROIS*EP)
      C3  = EP/QUINZE
      C4  = -HUIT/(TROIS*EP)
      C5  = -EP/TRENTE
      C6  = UN/(TROIS*EP)
      C7  = (HUIT*EP)/QUINZE
      C8  = SEIZE/(TROIS*EP)
      C9  = EP/QUINZE
      C10 = -HUIT/(TROIS*EP)
      C11 = (DEUX*EP)/QUINZE
      C12 = SEPT/(TROIS*EP)

      NBN2 = 2 * NBNN

      DO J = 1, NBNN
        J1 = J + NBNN
        J2 = J + NBN2

        DO I = 1, NBNN
          I1 = I + NBNN
          I2 = I + NBN2

          r_z1 = CEL1(I,J)
          r_z2 = CEL2(I,J)

*    SOUS-MATRICE K11 = C1*CEL1+C2*CEL2
          CEL(I ,J ) = C1*r_z1 + C2*r_z2
*    SOUS-MATRICE K21 = C3*CEL1+C4*CEL2
          CEL(I1,J ) = C3*r_z1 + C4*r_z2
*    SOUS-MATRICE K12 = K21
          CEL(J ,I1) = CEL(I1,J )
*    SOUS-MATRICE K31 = C5*CEL1+C6*CEL2
          CEL(I2,J ) = C5*r_z1 + C6*r_z2
*    SOUS-MATRICE K13 = K31
          CEL(J ,I2) = CEL(I2,J )
*    SOUS-MATRICE K22 = C7*CEL1+C8*CEL2
          CEL(I1,J1) = C7*r_z1 + C8*r_z2
*    SOUS-MATRICE K32 = C9*CEL1+C10*CEL2
          CEL(I2,J1) = C9*r_z1 + C10*r_z2
*    SOUS-MATRICE K23 = K32
          CEL(J1,I2) = CEL(I2,J1)
*    SOUS-MATRICE K33 = C11*CEL1+C12*CEL2
          CEL(I2,J2) = C11*r_z1 + C12*r_z2

        ENDDO

      ENDDO

      RETURN
      END


