C PODEFO    SOURCE    CHAT      11/03/22    21:15:13     6910
      SUBROUTINE PODEFO(V,DEFO,XL,IDEUX,carac,you,xnu)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES DEFORMATIONS DE LA POUTRE DROITE
C
C  ENTREE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    XL      LONGUEUR
C    IDEUX   INDICE POUR DEFORMATIONS DU 2-EME ORDRE
C
C  SORTIE
C    DEFO    DEFORMATIONS AUX 2 NOEUDS    6 PAR NOEUD
C            ( PAS D'EFFET D'EFFORT TRANCHANT || )
C
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION DEFO(*),V(*),carac(*)
C
      RL=1.D00/XL
      RL2=RL*RL
          X2=0.
          X3=0.
          xiny=carac(2)
          xinz=carac(3)
          sredy=carac(5)
          sredz=carac(6)
          cisail= you/(2.*(1+xnu))
* of
*          if( sredy.ne.0.) X2=You/cisail*rl2*xiny/sredy
*          if( sredy.ne.0.) X2=You/cisail*rl2*xinz/sredz
          if( sredy.ne.0.) X3=You/cisail*rl2*xinz/sredy
          if( sredz.ne.0.) X2=You/cisail*rl2*xiny/sredz
* of
             X13=1./(1.+12.*X3)
             X33=(1.+3.*X3)*X13
             X63=(1.-6.*X3)*X13
             X12=1./(1.+12.*X2)
             X32=(1.+3.*X2)*X12
             X62=(1.-6.*X2)*X12
C
C  DEFORMATIONS GENERALISEES
C
      EPS  =(V(7)-V(1))*RL
      XSIX =(V(10)-V(4))*RL
      XSIY1=
     .RL2*( 6.*V(3)*X12-4.*XL*X32*V(5)-6.*V(9)*X12-2.*XL*X62*V(11))
      XSIY2=
     .RL2*(-6.*V(3)*X12+2.*XL*X62*V(5)+6.*V(9)*X12+4.*XL*X32*V(11))
      XSIZ1=
     .RL2*(-6.*V(2)*X13-4.*XL*X33*V(6)+6.*V(8)*X13-2.*XL*X63*V(12))
      XSIZ2=
     .RL2*( 6.*V(2)*X13+2.*XL*X63*V(6)-6.*V(8)*X13+4.*XL*X33*V(12))
C
C  REMPLISSAGE DU TABLEAU DEFO
C
      DEFO(1)=EPS
      DEFO(2)=0.D0
      DEFO(3)=0.D0
      DEFO(4)=XSIX
      DEFO(5)=XSIY1
      DEFO(6)=XSIZ1
      DEFO(7)=EPS
      DEFO(8)=0.D0
      DEFO(9)=0.D0
      DEFO(10)=XSIX
      DEFO(11)=XSIY2
      DEFO(12)=XSIZ2
      IF(IDEUX.EQ.0) RETURN
C
C  TERMES DU SECOND ORDRE
C
      AUX1=0.5D0*(EPS**2+V(5)**2+V(6)**2)
      AUX2=0.5D0*(EPS**2+V(11)**2+V(12)**2)
      DEFO(1)=DEFO(1)+0.5D0*(AUX1+AUX2)
      DEFO(7)=DEFO(7)+0.5D0*(AUX1+AUX2)
C*OF?    DEFO(1)=DEFO(1)+AUX1
C*OF?    DEFO(7)=DEFO(7)+AUX2
      RETURN
      END



