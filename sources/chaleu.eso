C CHALEU    SOURCE    CB215821  18/09/21    21:15:21     9930

C=======================================================================
C=                            C H A L E U                              =
C=                            -----------                              =
C=                                                                     =
C=  OPERATEUR CAST3M "SOURCE" :                                        =
C=  ---------------------------                                        =
C=   FF1  =  'SOURCE'  MODL1  |  S1  MAIL1  |  ( CARA1 )  ( 'ELEM' ) ; =
C=                            |  CHP1       |                          =
C=                            |  CHEL1      |                          =
C=                                                                     =
C=   Cet operateur sert a calculer les flux nodaux equivalents a une   =
C=   source volumique de chaleur (CHPOINT au second membre)            =
C=                                                                     =
C=  ARGUMENTS :                                                        =
C=  -----------                                                        =
C=   MODL1  (MMODEL)    Modele (global) associe a la structure         =
C=   S1     (FLOTTANT)  Valeur algebrique de la source (constante)     =
C=   MAI1   (MAILLAGE)  Partie de la structure ou on impose la source  =
C=                      de chaleur de valeur S1.                       =
C=   CHP1   (CHPOINT)   Valeurs algebriques des sources aux NOEUDS     =
C=   CHEL1  (MCHAML)    Valeurs algebriques des sources par ELEMENT    =
C=   CARA1  (MCHAML)    Caracteristiques geometriques (utilisees dans  =
C=                      le cas des elements de type COQUE et BARRe)    =
C=                      Sous-type 'CARACTERISTIQUES'                   =
C=                                                                     =
C=  RESULTAT :                                                         =
C=  ----------                                                         =
C=   FF1    (CHPOINT)   Flux nodaux equivalents                        =
C=                                                                     =
C=  Remarques :                                                        =
C=  -----------                                                        =
C=   Le CHPOINT resultat FF1 est de nature DISCRETE.                   =
C=   Si le MOT 'ELEM' (facultatif) est present, le champ resultat FF1  =
C=   est alors un MCHAML aux NOEUDS.                                   =
C=                                                                     =
C=  Denis ROBERT, le 26 fevrier 1988.                                  =
C=======================================================================

      SUBROUTINE CHALEU

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO

-INC SMMODEL
-INC SMCHAML

      CHARACTER*4 MOTELE(1), MOCOMP
      DATA MOTELE / 'ELEM' /

C  1 - LECTURE DES ARGUMENTS DE L'OPERATEUR
C ==========================================
C  1.1 - Lecture FACULTATIVE du mot 'ELEM'
C =====
      ICLE = 0
      CALL LIRMOT(MOTELE,1,ICLE,0)
C =====
C  1.2 - Lecture OBLIGATOIRE du modele (IPMODL)
C =====
      MOTERR(1:8)='MODELE'
      CALL MESLIR(-137)
      CALL LIROBJ('MMODEL',IPMODL,1,IRet)
      IF (IERR.NE.0) RETURN
      CALL ACTOBJ('MMODEL  ',IPMODL,1)
C =====
C  1.3 - Lecture OBLIGATOIRE des sources volumiques donnees par :
C           1) un CHPOINT (IPCHPO)
C        ou 2) d'un maillage (IPGEOM) et d'un flottant (S1)
C        ou 3) d'un MCHAML (IPCHEL)
C =====
      IPCHPO = 0
      S1     = 0.D0
      IPGEOM = 0
      IPCHEL = 0
      CALL MESLIR(-138)
      CALL LIROBJ('CHPOINT',IPCHPO,0,iret)
      IF (iret.EQ.0) THEN
        CALL MESLIR(-139)
        CALL LIRREE(S1,0,iret)
        IF (iret.NE.0) THEN
          MOTERR(1:8)='MAILLAGE'
          CALL MESLIR(-137)
          CALL LIROBJ('MAILLAGE',IPGEOM,1,iret)
          IF (IERR.NE.0) RETURN
        ELSE
          CALL LIROBJ('MCHAML',IPCHEL,1,iret)
          IF (IERR.NE.0) RETURN
        ENDIF
      ENDIF
C =====
C  1.4 - Lecture FACULTATIVE du champ de caracteristiques (IPCARA)
C =====
      IPCARA = 0
      CALL MESLIR(-145)
      CALL LIROBJ('MCHAML',IPCARA,0,iret)
      IF (IERR.NE.0) RETURN
C =====
C  1.5 - Lecture de la composante de la source (MOCOMP)
C =====
      MOCOMP = '    '
      CALL LIRCHA(MOCOMP,0,iret)
      IF (IERR.NE.0) RETURN

C  2 - VERIFICATION DU MODELE A TRAITER
C ======================================
      IPMOD1 = 0
C IPMOD1 : Modele a traiter
C IFORMU : indice correspondant a la formulation
C ITYPEF : indicateur du type d'elements (massif, coque ou barre)
      CALL CHALVM(IPMODL,MOCOMP, IPMOD1,IFORMU,ITYPEF)
      IF (IERR.NE.0) GOTO 9900

C  3 - ANALYSE DU TERME SOURCE
C =============================
C Reduction du MCHAML de sources si fourni
      IF (IPCHEL.NE.0) THEN
        IPCHMZ = IPCHEL
        CALL ACTOBJ('MCHAML  ',IPCHEL,1)
        CALL REDUAF(IPCHMZ,IPMODL,IPCHEL,0,iok,kerre)
        IF (iok.NE.1) CALL ERREUR(kerre)
        IF (IERR.NE.0) GOTO 9900
      ENDIF
C Mise sous forme de MCHAML des donnees d'entrees et
C Determination du support du MCHAML de sources
      CALL CHALVS(IPMOD1,IFORMU,ITYPEF, IPCHPO,S1,IPGEOM,IPCHEL,
     &            IPCHSO,ISUPSO)
      IF (IERR.NE.0) GOTO 9900

C  4 - ANALYSE DU CHAMP DE CARACTERISTIQUES
C ==========================================
C  Verification de l'existence des caracteristiques
C  dans le cas d'elements COQUEs et BARREs.
      IF (IPCARA.EQ.0) THEN
        IF      (ITYPEF.EQ.2) THEN
          CALL ERREUR(514)
        ELSE IF (ITYPEF.EQ.3) THEN
          CALL ERREUR(518)
        ENDIF
        IF (IERR.NE.0) GOTO 9900
      ELSE
C En elements MASSIFs, aucune caracteristique n'est necessaire.
        IF (ITYPEF.EQ.1) IPCARA = 0
      ENDIF
      IF (IPCARA.NE.0) THEN
        IPCHMZ = IPCARA
        mchelm = IPCHMZ
        SEGACT,mchelm
        IF (mchelm.titche.NE.'CARACTERISTIQUES') THEN
          MOTERR(1:16) = 'CARACTERISTIQUES'
          CALL ERREUR(291)
          SEGDES,mchelm
          GOTO 9900
        ENDIF
        CALL ACTOBJ('MCHAML  ',IPCARA,1)
        CALL REDUAF(IPCHMZ,IPMODL,IPCARA,0,iok,kerre)
        IF (iok.NE.1) CALL ERREUR(kerre)
        IF (IERR.NE.0) GOTO 9900
      ENDIF

C  5 - CALCUL DES FLUX NODAUX EQUIVALENTS
C ========================================
      CALL CHAL1(IPMOD1,IFORMU,ITYPEF, IPCHSO,ISUPSO, IPCARA, MOCOMP,
     &           IPCHAL)
      IF (IERR.NE.0 .OR. IPCHAL.EQ.0) GOTO 9900

C  6 - ECRITURE DE L'OBJET RESULTAT (CHPOINT OU MCHAML)
C ======================================================
      IF (ICLE.EQ.0) THEN
        CALL CHAMPO(IPCHAL,0,IPCHAP,iret)
C= Le resultat est un CHPOINT de nature DISCRETE.
        CALL DTCHAM(IPCHAL)
        IF (iret.NE.1 .OR. IERR.NE.0) RETURN
        CALL ECROBJ('CHPOINT',IPCHAP)
      ELSE
        CALL ECROBJ('MCHAML',IPCHAL)
      ENDIF

C  7 - FIN
C =========
 9900 CONTINUE
      IF (IPMOD1.NE.0) THEN
        MMODE1 = IPMOD1
        SEGSUP,MMODE1
      ENDIF

      RETURN
      END



