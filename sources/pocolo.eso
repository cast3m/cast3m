C POCOLO    SOURCE    CHAT      05/01/13    02:15:54     5004
      SUBROUTINE POCOLO(V,CONT,XL,YOUN,CISAIL,SD,SREDY,SREDZ,
     .                  XINX,XINY,XINZ,IDEUX)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES CONTRAINTES DE LA POUTRE DROITE
C    D APRES TEDEL
C
C  ENTREE
C    V       VECTEUR DES DEPLACEMENTS LOCAUX AUX 2 NOEUDS DE LA POUTRE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    SREDZ   SECTION REDUITE A L EFFORT TRANCHANT TZ
C    XINX    INERTIE IX
C    XINY    INERTIE IY
C    XINZ    INERTIE IZ
C    YOUN    MODULE D YOUNG
C    CISAIL  MODULE DE CISAILLEMENT
C    IDEUX   INDICE POUR DEFORMATIONS DU SECOND ORDRE
C  SORTIE
C    CONT    CONTRAINTES AUX 2 NOEUDS    6 PAR NOEUD
C            SEULS LES MOMENTS DE FLEXION VARIENT LINEAIREMENT
C
C    AOUT 84
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION CONT(*),V(*)
C
      RL=1.D00/XL
      RL2=RL*RL
      RL3= RL2*RL
          X2=0.
          X3=0.
          IF( SREDZ. NE.0.) X2= YOUN/CISAIL*RL2*XINY/SREDZ
          IF( SREDY. NE.0.) X3= YOUN/CISAIL*RL2*XINZ/SREDY
             X13=1./(1.+12.*X3)
             X33=(1.+3.*X3)*X13
             X63=(1.-6.*X3)*X13
             X12=1./(1.+12.*X2)
             X32=(1.+3.*X2)*X12
             X62=(1.-6.*X2)*X12
C
C  DEFORMATIONS GENERALISEES
C
      EPS  =(V(7)-V(1))*RL
      XSIX =(V(10)-V(4))*RL
      XSIY1=
     .RL2*( 6.*V(3)*X12-4.*XL*X32*V(5)-6.*V(9)*X12-2.*XL*X62*V(11))
      XSIY2=
     .RL2*(-6.*V(3)*X12+2.*XL*X62*V(5)+6.*V(9)*X12+4.*XL*X32*V(11))
      XSIZ1=
     .RL2*(-6.*V(2)*X13-4.*XL*X33*V(6)+6.*V(8)*X13-2.*XL*X63*V(12))
      XSIZ2=
     .RL2*( 6.*V(2)*X13+2.*XL*X63*V(6)-6.*V(8)*X13+4.*XL*X33*V(12))
C
C  TERMES DU SECOND ORDRE
C
      IF(IDEUX.EQ.0) THEN
      EPS1=EPS
      EPS2=EPS
      ELSE IF(IDEUX.NE.0) THEN
      EPS1=EPS+0.5D0*(EPS**2+V(5)**2+V(6)**2)
      EPS2=EPS+0.5D0*(EPS**2+V(11)**2+V(12)**2)
      ENDIF
C
C  CONTRAINTES GENERALISEES
C
      SIG1  =YOUN*SD*(EPS1+EPS2)*0.5D0
      SIG2  =SIG1
      TOR   =CISAIL*XINX*XSIX
      XMOMZ1=YOUN*XINZ*XSIZ1
      XMOMZ2=YOUN*XINZ*XSIZ2
      XMOMY1=YOUN*XINY*XSIY1
      XMOMY2=YOUN*XINY*XSIY2
      TRANY =(XMOMZ1-XMOMZ2)*RL
      TRANZ =(XMOMY2-XMOMY1)*RL
C
C  REMPLISSAGE DU TABLEAU CONT
C
      CONT(1)=SIG1
      CONT(2)=TRANY
      CONT(3)=TRANZ
      CONT(4)=TOR
      CONT(5)=XMOMY1
      CONT(6)=XMOMZ1
      CONT(7)=SIG2
      CONT(8)=TRANY
      CONT(9)=TRANZ
      CONT(10)=TOR
      CONT(11)=XMOMY2
      CONT(12)=XMOMZ2
      RETURN
      END

