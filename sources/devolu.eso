C DEVOLU    SOURCE    CHAT      05/01/12    22:46:57     5004

C=======================================================================
C=                          D E V O L U                                =
C=                          -----------                                =
C=  Fonction :                                                         =
C=  ----------                                                         =
C=   Calcul du volume affecte a un point de Gauss connaissant initia-  =
C=   lement le jacobien en ce point.                                   =
C=   Les derivees des fonctions de forme par rapport aux coordonnees   =
C=   REELLES x,y,z sont aussi egalement calculees en ce point.         =
C=                                                                     =
C=  Parametres :  (E)=Entree  (S)=Sortie                               =
C=  ------------                                                       =
C=   XEL      (E)    Coordonnees GLOBALES des noeuds de l element      =
C=   SHP     (E/S)   Valeurs des fonctions de forme et de leurs        =
C=                   derivees au point considere (point de Gauss)      =
C=   MFR      (E)    Formulation associee a l'element fini             =
C=   NBNO     (E)    Nombre de NOEUDS de l element fini                =
C=   IFOU     (E)    Mode de calcul utilise (cf. IFOUR dans CCOPTIO)   =
C=   NIFOU    (E)    Numero du mode de Fourier (si IFOU=1)             =
C=   IDIM     (E)    Dimension du probleme traite (1 a 3)              =
C=   DIM3     (E)    Epaisseur dans l'hypothese des contraintes planes =
C=   RR       (E)    Rayon au point considere (axisymetrique,spherique)=
C=   DJac    (E/S)   Jacobien/Volume calcule en ce point de Gauss      =
C=                                                                     =
C=  Remarque :                                                         =
C=  ----------                                                         =
C=   Lors de l'entree dans le sous-programme, SHP(2 a 4,*) contient    =
C=   les DERIVEES des fonctions de forme par rapport aux coordonnees   =
C=   de REFERENCE Qsi,Eta,Dzeta.                                       =
C=   En sortie du sous-programme, SHP(2 a 4,*) contient les DERIVEES   =
C=   des fonctions de FORME par rapport aux coordonnees REELLES x,y,z. =
C=======================================================================

      SUBROUTINE DEVOLU (XEL,SHP,MFR,NBNO,IFOU,NIFOU,IDIM,DIM3,RR,DJAC)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCREEL

      DIMENSION XEL(3,*),SHP(6,*)

      PARAMETER (XUn=1.)
C= Quelques constantes (2.Pi et 4.Pi)
      PARAMETER (X2Pi=6.283185307179586476925286766559D0)
      PARAMETER (X4Pi=12.566370614359172953850573533118D0)

      CALL JACOBI(XEL,SHP,IDIM,NBNO,DJAC)

      RR=XUn
C ===
C  1 - Cas 2D CONTraintes PLANes
C ===
      IF (IFOU.EQ.-2) THEN
        IF (MFR.EQ.1.OR.MFR.EQ.31) DJAC=DJAC*DIM3
C ===
C  2 - Cas 2D AXISymetrie et FOURier
C ===
      ELSE IF (IFOU.EQ.0.OR.IFOU.EQ.1) THEN
        IF (MFR.NE.35) CALL DISTRR(XEL,SHP,NBNO,RR)
        IF (IFOU.EQ.0.OR.(IFOU.EQ.1.AND.NIFOU.EQ.0)) THEN
          DJAC=X2Pi*DJAC*RR
        ELSE
          DJAC=XPi*DJAC*RR
        ENDIF
C ===
C  3 - Cas 1D AXISymetriques
C ===
      ELSE IF (IFOU.EQ.12.OR.IFOU.EQ.13.OR.IFOU.EQ.14) THEN
        CALL DISTRR(XEL,SHP,NBNO,RR)
        DJAC=X2Pi*DJAC*RR
C ===
C  4 - Cas 1D SPHErique
C ===
      ELSE IF (IFOU.EQ.15) THEN
        CALL DISTRR(XEL,SHP,NBNO,RR)
        DJAC=X4Pi*DJAC*RR*RR
      ENDIF

      RETURN
      END



