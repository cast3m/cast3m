C DTPREC    SOURCE    STRU      06/12/14    21:15:10     5611
c     Sous-Programme du module MISTRAL1
C     --------------------------------------------------------------------------
      SUBROUTINE DTPREC (DTMI,DTMA,TMAX,
     &                   VEPSTH,VEPSEL,VEPSCR,VEPSE,EPSEQP,XXP,FI,PSIP,
     &                   VEPTHA,VEPELA,VEPCRA,VEPEA,EPSEPA,XXPA,FIA,
     &                   PSIPA, DTA,NP, NDPI,NDVP,NXX,NPSI, II, PINCR,
     &                   DT)
C     --------------------------------------------------------------------------
C     Determination du pas de temps de calcul DT en fonction :
C     - du minimum et du maximum imposes : DTMI et DTMA,
C     - de la duree maximale d'evolution consideree TMAX,
C     - des valeurs a l'instant t :
C       - des vitesses de dilatation thermique VEPSTH(IJ),
C       - des vitesses de deformations elastiques VEPSEL(IJ),
C       - des vitesses de croissance VEPSCR(IJ),
C       - des vitesses de deformations plastiques equivalentes VEPSE(IDP),
C       - des derivees des deformations plastiques equivalentes EPSEQP(IDP),
C       - des derivees des contraintes internes XXP(IJ,IXX),
C       - du flux de neutrons rapides FI,
C       - de la derivee PSIP de la variable de durcissement du a l'irradiation
C         PSI, si elle existe (NPSI>0),
C     - eventuellement, si NP est superieur ou egal a 1 :
C       - de leur valeurs a l'instant precedent : VEPTHA(IJ), VEPELA(IJ),
C       VEPCRA(IJ), VEPEA(IDP), EPSEPA(IDP), XXPA(IJ,IXX), FIA, PSIPA,
C       - de la duree DTA du pas de temps precedent,
C     - des increments maximaux autorises (dans PINCR) :
C       - des deformations de chaque nature,
C       - des contraintes internes,
C       - de la fluence de neutrons rapides,
C       - de PSI,
C     - des increments relatifs maximaux autorises (dans PINCR) :
C       - des vitesses de deformation de chaque nature,
C       - des derivees de contraintes internes,
C       - du flux de neutrons rapides,
C       - de la derivee de PSI.
C     --------------------------------------------------------------------------
      IMPLICIT INTEGER (I-N)
      IMPLICIT REAL*8 (A-H, O-Z)
c     std :
      PARAMETER ( NDVPMA = 4 , NXXMAX = 3 )
      PARAMETER ( IIMAX = 20+2*(1+NDVPMA)+6*NXXMAX )
c     std.
        DIMENSION VEPSTH(1:*),VEPSEL(1:*),VEPSCR(1:*),
     &          VEPSE(0:*),EPSEQP(0:*),XXP(1:6,1:*),
     &          VEPTHA(1:*),VEPELA(1:*),VEPCRA(1:*),
     &          VEPEA(0:*),EPSEPA(0:*),XXPA(1:6,1:*)
      DIMENSION PINCR(1:*)
c     std :
        DIMENSION DYMA(1:IIMAX),DYPRMA(1:IIMAX),YP(1:IIMAX),YPA(1:IIMAX)
c     std.
        DO IJ = 1,6
         YP(IJ) = VEPSTH(IJ)
         YPA(IJ) = VEPTHA(IJ)
         DYMA(IJ) = PINCR(1)
         DYPRMA(IJ) = PINCR(6)
        END DO
      I = 6
        DO IJ = 1,6
       I = I+1
         YP(I) = VEPSEL(IJ)
         YPA(I) = VEPELA(IJ)
         DYMA(I) = PINCR(2)
         DYPRMA(I) = PINCR(7)
        END DO
        DO IJ = 1,6
       I = I+1
         YP(I) = VEPSCR(IJ)
         YPA(I) = VEPCRA(IJ)
         DYMA(I) = PINCR(3)
         DYPRMA(I) = PINCR(8)
        END DO
        DO IDP = 1-NDPI,NDVP
         YP(I+1) = VEPSE(IDP)
         YPA(I+1) = VEPEA(IDP)
         YP(I+2) = EPSEQP(IDP)
         YPA(I+2) = EPSEPA(IDP)
         IF (IDP.EQ.0) THEN
          DYMA(I+1) = PINCR(4)
          DYPRMA(I+1) = PINCR(9)
          DYMA(I+2) = PINCR(4)
          DYPRMA(I+2) = PINCR(9)
         ELSE
          DYMA(I+1) = PINCR(5)
          DYPRMA(I+1) = PINCR(10)
          DYMA(I+2) = PINCR(5)
          DYPRMA(I+2) = PINCR(10)
         END IF
         I = I+2
        END DO
        DO IXX = 1,NXX
         DO IJ = 1,6
          I = I+1
          YP(I) = XXP(IJ,IXX)
          YPA(I) = XXPA(IJ,IXX)
          DYMA(I) = PINCR(11)
          DYPRMA(I) = PINCR(12)
         END DO
        END DO
      I = I+1
        YP(I) = FI
        YPA(I) = FIA
        DYMA(I) = PINCR(13)
        DYPRMA(I) = PINCR(14)
      IF (NPSI.GT.0) THEN
       I = I+1
       YP(I) = PSIP
       YPA(I) = PSIPA
       DYMA(I) = PINCR(15)
       DYPRMA(I) = PINCR(16)
      END IF
        CALL PACALC (DTMI,DTMA,TMAX, I, DYMA,DYPRMA,
     &             YP, DTA,YPA,NP, DT)
        RETURN
        END

