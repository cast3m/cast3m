C GTEMRD    SOURCE    CHAT      05/01/13    00:21:53     5004

C-----------------------------------------------------------------------
C   ENTREES
C     XEL(3,NBNO)=COORDONNEES LOCALES DE L ELEMENT
C     SHP(6,NBNO)=DERIVEES PAR RAPPORT A LA GEOMETRIE DE REFERENCE
C     JDIM=DIMENSION
C     NBNO=NOMBRE DE NOEUDS
C   SORTIES
C     DJAC =JACOBIEN
C-----------------------------------------------------------------------
      SUBROUTINE GTEMRD (XEL,SHP,JDIM,NBNO,DJAC)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO

      DIMENSION XEL(3,*),SHP(6,*)

C  CAS 1 DIMENSION
      IF (JDIM.EQ.1) THEN
        IF (IDIM.EQ.1) THEN
          dXdQsi=0.
          DO i=1,NBNO
            dXdQsi=dXdQsi+SHP(2,i)*XEL(1,i)
          ENDDO
          DJAC=ABS(dXdQsi)
        ELSE IF (IDIM.EQ.2) THEN
          dXdQsi=0.
          dYdQsi=0.
          DO i=1,NBNO
            dXdQsi=dXdQsi+SHP(2,i)*XEL(1,i)
            dYdQsi=dYdQsi+SHP(2,i)*XEL(2,i)
          ENDDO
          DJAC=SQRT(dXdQsi*dXdQsi+dYdQsi*dYdQsi)
        ELSE IF (IDIM.EQ.3) THEN
          dXdQsi=0.
          dYdQsi=0.
          dZdQsi=0.
          DO i=1,NBNO
            dXdQsi=dXdQsi+SHP(2,i)*XEL(1,i)
            dYdQsi=dYdQsi+SHP(2,i)*XEL(2,i)
            dZdQsi=dZdQsi+SHP(2,i)*XEL(3,i)
          ENDDO
          DJAC=SQRT(dXdQsi*dXdQsi+dYdQsi*dYdQsi+dZdQsi*dZdQsi)
        ENDIF

C  CAS 2 DIMENSIONS
      ELSE IF (JDIM.EQ.2) THEN
        dXdQsi=0.
        dYdQsi=0.
        dZdQsi=0.
        dXdEta=0.
        dYdEta=0.
        dZdEta=0.
        IF (IDIM.EQ.3) THEN
          DO i=1,NBNO
            dXdQsi=dXdQsi+SHP(2,i)*XEL(1,i)
            dXdEta=dXdEta+SHP(3,i)*XEL(1,i)
            dYdQsi=dYdQsi+SHP(2,i)*XEL(2,i)
            dYdEta=dYdEta+SHP(3,i)*XEL(2,i)
            dZdQsi=dZdQsi+SHP(2,i)*XEL(3,i)
            dZdEta=dZdEta+SHP(3,i)*XEL(3,i)
          ENDDO
          V1=dYdQsi*dZdEta-dZdQsi*dYdEta
          V2=dZdQsi*dXdEta-dXdQsi*dZdEta
          V3=dXdQsi*dYdEta-dYdQsi*dXdEta
          DJAC=SQRT(V1*V1+V2*V2+V3*V3)
        ELSE IF (IDIM.EQ.2) THEN
          DO i=1,NBNO
            dXdQsi=dXdQsi+SHP(2,i)*XEL(1,i)
            dXdEta=dXdEta+SHP(3,i)*XEL(1,i)
            dYdQsi=dYdQsi+SHP(2,i)*XEL(2,i)
            dYdEta=dYdEta+SHP(3,i)*XEL(2,i)
          ENDDO
          DJAC=ABS(dXdQsi*dYdEta-dXdEta*dYdQsi)
        ENDIF

C  CAS 3 DIMENSIONS
      ELSE IF (JDIM.EQ.3) THEN
        D11=0.
        D21=0.
        D31=0.
        D12=0.
        D22=0.
        D32=0.
        D13=0.
        D23=0.
        D33=0.
        DO i=1,NBNO
          D11=D11+SHP(2,i)*XEL(1,i)
          D21=D21+SHP(3,i)*XEL(1,i)
          D31=D31+SHP(4,i)*XEL(1,i)
          D12=D12+SHP(2,i)*XEL(2,i)
          D22=D22+SHP(3,i)*XEL(2,i)
          D32=D32+SHP(4,i)*XEL(2,i)
          D13=D13+SHP(2,i)*XEL(3,i)
          D23=D23+SHP(3,i)*XEL(3,i)
          D33=D33+SHP(4,i)*XEL(3,i)
        ENDDO
        DInv11=D22*D33-D23*D32
        DInv12=D32*D13-D12*D33
        DInv13=D12*D23-D22*D13
        DJAC=ABS(D11*DInv11+D21*DInv12+D31*DInv13)
      ENDIF

      RETURN
      END



