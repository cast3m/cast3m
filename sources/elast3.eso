C ELAST3    SOURCE    CHAT      05/01/12    23:29:35     5004
      SUBROUTINE ELAST3(ISENS,IFOUR,XMATT,MFR,NSTRS,DD,KERRE,INDIC)
C
C----------------------------------------------------------------------------
C Objet: Calcul de la matrice de Hooke endommagee(ISENS=1)
C        ou son inverse(ISENS=2)
C        dans le cadre du modele CERAMIQUE (materiau 65)
C----------------------------------------------------------------------------
C
C----------------------------------------------------------------------------
C Entree: ISENS= 1  DD en sortie est la matrice de Hooke endommagee
C           d'ou SIG=DD*EPSELAS
C              = 2  DD en sortie est la matrice de Hooke inverse
C            endommagee d'ou EPSELAS=DD*SIG
C           avec SIG contraintes et EPSELAS deformations elastiques
C         IFOUR= -2 EN CONTR. PLANES
C                -1 EN DEFORM. PLANES
C                 0 EN AXISYMETRIE
C                 1 EN SERIE DE FOURIER
C                 2 EN TRIDIM
C         NSTRS nombre de composantes des contraintes ou
C           des deformations
C         XMATT(NCOMAT) tableau des parametres scalaires du materiau
C           a une temperature T donnee
C           dont XMATT(1) module d'Young et XMATT(2) coeff de Poisson
C         MFR indice de la formulation mecanique (seulement massif
C           pour les materiaux endommageables)
C         INDIC = 0 ====> le point de Gauss n'est pas cassé
C         INDIC = 1 ====> le point de Gauss est cassé
C--------------------------------------------------------------------------------
C
C--------------------------------------------------------------------------------
C Sortie: DD(NSTRS,NSTRS) matrice de Hooke endommagee si ISENS=1
C                         son inverse si ISENS=2
C
C         KERRE indice qui regit les erreurs
C          Il vaut 99 si la formulation mecanique n'est pas disponible ou
C          s'il y a incompatibilite entre MFR et IFOUR
C _______________________________________________________________________________
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION XMATT(*)
      DIMENSION DD(NSTRS,NSTRS)
C
      CALL ZERO(DD,NSTRS,NSTRS)
      KERRE=0
C
      YUNG=XMATT(1)
      XNU=XMATT(2)
C Modifs Cmau
C      D1=1.D0-XD
      D1=1.D0
C
C ====================================================================
C SI LE PT. DE GAUSS EST ROMPU, ON DIV. SON MOD. D'YOUNG PAR 1 MILLION
C ====================================================================
      IF (INDIC.EQ.1) THEN
        D1=1.D-6
      ENDIF
C
      COEFF1=(D1*YUNG)/(1.D0-XNU*XNU)
      COEFF2=1.D0/(D1*YUNG)
      COEFF3=(D1*YUNG)/((1.D0+XNU)*(1.D0-2.D0*XNU))
      COEFF4=(1.D0-2.D0*XNU)/2.D0
C
      IF (MFR.EQ.1) THEN
C
C------------
C MASSIFS
C------------
        IF (IFOUR.NE.-2) THEN
C
C ----------------------------------
C DEFORMATIONS PLANES ET AXISYMETRIE
C ----------------------------------
          IF (ISENS.EQ.1) THEN
C
C----------------
C SIG=DD*EPSELAS
C----------------
            DD(1,1)=COEFF3*(1.D0-XNU)
            DD(1,2)=COEFF3*XNU
            DD(1,3)=DD(1,2)
            DD(2,2)=DD(1,1)
            DD(2,3)=DD(1,2)
            DD(3,3)=DD(1,1)
            DD(2,1)=DD(1,2)
            DD(3,1)=DD(1,2)
            DD(3,2)=DD(1,2)
C
            LA=4
            LB=4
C
C ---------------------------
C LB=6 POUR TRIDIM ET FOURIER
C ---------------------------
            IF (IFOUR.GT.0) LB=6
            DO 5 IA=LA,LB
              DD(IA,IA)=COEFF3*COEFF4
   5        CONTINUE
          ELSE
C
C-------------------
C EPSELAS=DD*SIG
C-------------------
            DD(1,1)=COEFF2
            DD(1,2)=-XNU*COEFF2
            DD(1,3)=DD(1,2)
            DD(2,2)=DD(1,1)
            DD(2,3)=DD(1,2)
            DD(3,3)=DD(1,1)
            DD(2,1)=DD(1,2)
            DD(3,1)=DD(1,3)
            DD(3,2)=DD(2,3)
            LA=4
            LB=4
C
C ---------------------------
C LB=6 POUR TRIDIM ET FOURIER
C ---------------------------
            IF (IFOUR.GT.0) LB=6
            DO 10 IA=LA,LB
              DD(IA,IA)=(1.D0+XNU)*DD(1,1)*2.D0
  10        CONTINUE
C
          ENDIF
C
C -------------
C CONTR. PLANES
C -------------
        ELSE
          IF (ISENS.EQ.1) THEN
C
C-----------------
C SIG=DD*EPSELAS
C-----------------
            DD(1,1)=COEFF1
            DD(1,2)=COEFF1*XNU
            DD(2,2)=COEFF1
            DD(4,4)=COEFF1*((1.D0-XNU)/2.D0)
            DD(2,1)=DD(1,2)
          ELSE
C
C------------------
C EPSELAS=DD*SIG
C------------------
            DD(1,1)=COEFF2
            DD(1,2)=-COEFF2*XNU
            DD(2,2)=COEFF2
            DD(4,4)=COEFF2*2.D0*(1.D0+XNU)
            DD(2,1)=DD(1,2)
            DD(3,1)=DD(1,2)
            DD(3,2)=DD(1,2)
C
          ENDIF
        ENDIF
C
      ELSE
        IF (MFR.EQ.5) THEN
C
C------------------------------------------
C COQUES EPAISSES (NON ENCORE DISPONIBLE)
C------------------------------------------
C
C FORMULATION NON DISPONIBLE
        ENDIF
        KERRE=99
      ENDIF
C
      RETURN
      END




