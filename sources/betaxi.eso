C BETAXI    SOURCE    CHAT      05/01/12    21:38:40     5004
      SUBROUTINE BETAXI(SIG0,NSTRS,DSIGT,VAR0,XMAT,IVAL,NCOMAT,XCAR,
     1  SIGF,VARF,DEFP,MFR,KERRE,ECOU,NECOU)
C----------------------------------------------------------------------
C             PLASTICITE MODELE BETON
C
C ENTREES
C     SIG0(NSTRS)   = CONTRAINTES INITIALES
C     NSTRS         = NOMBRE DE CONTRAINTES
C     DSIGT(NSTRS)  = INCREMENT DE CONTRAINTES TOTALES
C     VAR0(NVARI)   =  VARIABLES INTERNES DEBUT
C     VAR0(   1 )   =  EPSE
C     VAR0(   2 )   =  DAM1
C     VAR0(   3 )   =  DAM2
C     VAR0(   4 )   =  DAM3
C     VAR0(   5 )   =  ANGL
C     VAR0(   6 )   =  LAM1
C     VAR0(   7 )   =  LAM2
C     VAR0(   8 )   =  LAM3
C     VAR0(   9 )   =  LAM4
C     VAR0(  10 )   =  LAM5
C     XMAT(NCOMAT)  =  COMPOSANTES DE MATERIAU
C     IVAL(NCOMAT)  =  INDICE DES COMPOSANTES DE MATERIAU
C     NCOMAT        = NOMBRE DE COMPOSANTES DE MATERIAU
C     XCAR(ICARA)   =  CARACTERISTIQUES
C     MFR           =  NUMERO DE LA FORMULATION DE L'ELEMENT FINI
C                   = 1       MASSIF
C                   = 3       COQUE MINCE ( COQ2 , COQ3  ET DKT )
C                   = 5       COQUE EPAISSE ( COQ6 , COQ8 )
C                   = 7       POUTRE
C                   = 9       COQUE MINCE AVEC CISAILLEMENT TRANSVERSE
C                                    ( COQ4 ET DST )
C                   = 11      LIQUIDE
C                   = 13      TUYAU
C                   = 15      LINESPRING
C                   = 17      TUYAU FISSURE
C                   = 19      RACCORD MASSIF
C                   = 21      RACCORD COQUE
C                   = 23      SURFACE LIBRE
C                   = 25      MEMBRANE
C                   = 27      UNIAXIALE
C                   = 29      THERMIQUE
C                   = 31      INCOMPRESSIBLES
C                   = 33      POREUX
C                   = 35      JOINT
C                   = 37      HOMOGENEISE
C                   = 39      TUYO
C                   = 41      TUYAU ACOUSTIQUE PURE
C                   = 43      RACCORD TUYAU FLUIDE
C  SORTIES
C     SIGF(NSTRS)   = CONTRAINTES FINALES
C     VARF(NVARI)   = VARIABLES INTERNES FINALES
C     DEFP(NSTRS)   = DEFORMATIONS PLASTIQUES
C     KERRE         = 0    TOUT OK
C        1   SI DLAMBDA NEGATIF
C        2   NOMBRE MAX D ITERATIONS INTERNES DEPASSE
C       21   ON NE TROUVE PAS L INTERSECTION AVEC LA SURFACE DE CHARGE
C       22   SIG0 A L EXTERIEUR DE LA SURFACE DE CHARGE
C
C-----------------------------------------------------------------------
C      VARIABLES PASSEES PAR LES COMMONS COPTIO , ECOU  ET NECOU
C
C  IFOUR    INDICE DU TYPE DE PROBLEME
C            -3  DEFORMATIONS PLANES GENERALISEES
C            -2  CONTRAINTES PLANES
C            -1  DEFORMATIONS PLANES
C             0  AXISYMETRIQUE
C             1  SERIE DE FOURIER
C             2  TRIDIMENSIONNEL
C  ITYP      TYPE DE FORMULATION MECANIQUE
C  --------------- ATTENTION ---------------
C     IL EST ACTIF APRES L APPEL DE VISAVI
C  -----------------------------------------
C       ITYP=1   CAS DES ELEMENTS MASSIFS
C       ITYP=2   CAS DES COQUES
C       ITYP=3   CAS DES MEMBRANES
C       ITYP=4   CAS DES CABLES ET DES BARRES
C       ITYP=5   CAS QUELCONQUE
C       ITYP=6   CAS DES CONTRAINTES PLANES
C       ITYP=7   CAS DES COQUES A NU=0. OU CONTRAINTES PLANES
C       ITYP=8   CAS DES MEMBRANES A NU=0. OU CONTRAINTES PLANES
C       ITYP=9   CAS DES COQUES EPAISSES
C       ITYP=10  CAS DES JOINTS
C       ITYP=11  CAS DES POUTRES
C       ITYP=12  CAS DES TUYAUX
C       ITYP=13  CAS DES COQUES AVEC CISAILLEMENT TRANSVERSE
C
C-----------------------------------------------------------------------
C
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
-INC CCOPTIO

       SEGMENT ECOU
***     COMMON/ECOU/TEST,ALFAH,
       REAL*8 TEST, ALFAH,
     1 ecow1,ecow2,ecow3(6),ecow4(9),ecow5(6),
C     1 HPAS,TEMPS,COVNMS(6),VECPRO(9),VALPRO(6),
     2 ecow6(12),ecow7(6),ecow8(6),ecow9(6),ecow10(6),ecow11(6),
     2 ecow12(6),
C     2 CVNMSD(12),STOT(6),SIGEL(6),DSIGP(6),SIGT(6),W1(6),W2(6),
     1 ecow13(6),ecow14(6),ecow15(12),ecow16(3),
C     1 DALPHA(6),EPSPLA(6),E(12),XINV(3),
     2 ecow17(6),ecow18(6),ecow19,ecow20
C     2 SIPLAD(6),DSIGP0(6),TET,TETI
      ENDSEGMENT

      SEGMENT NECOU
*      COMMON/NECOU/NCOURB,IPLAST,IT,IMAPLA,ISOTRO,
*       INTEGER ncow1,  ncow2,ncow3,ncow4,  ncow5,
      INTEGER NCOURB,IPLAST,IT,   IMAPLA,ISOTRO,
     1 ITYP,ncow7, ncow8,
C    . ITYP, IFOURB,IFLUAG,
     2 ncow9,ncow10,ncow11,ncow12,ncow13,
C    . ICINE,ITHER, IFLUPL,ICYCL, IBI,
     3 ncow14,ncow15,ncow16,
C    . JFLUAG,KFLUAG,LFLUAG,
     4 ncow17,ncow18,ncow19,ncow20,ncow21
C    . IRELAX,JNTRIN,MFLUAG,JSOUFL,JGRDEF
      ENDSEGMENT


*      COMMON/ECOU/TEST,ALFAH,ZOZO(112)
*      COMMON/NECOU/NCOURB,IPLAST,IT,IMAPLA,ISOTRO,ITYP,IZOZO(15)
C
       DIMENSION SIG0(*),DSIGT(*),VAR0(*),XMAT(*),XCAR(*),IVAL(*)
       DIMENSION SIGF(*),VARF(*),DEFP(*)
       DIMENSION SIGMA(6),DSIGMA(6),STOT(6),SIGFIN(6)
       DIMENSION XLAMBD(5),DEFPLA(6),DSIGP(6)
       DIMENSION SPHER(6),AUXIL(6),DSIGZE(6),ORMAT(1)
      CHARACTER*8 CMATE
      CMATE='ISOTROPE'
C
C TEST DE CONSISTANCE DES DONNEES
C
      DO 100 I=1,NCOMAT
      IF(XMAT(I).LT.0.D0) KERRE=437
  100 CONTINUE
      IF(KERRE.EQ.437) RETURN
C
      YOUN= 0.D0
      XNU = 0.D0
      RHO = 0.D0
      ALPH= 0.D0
      CTR1= 0.D0
      CTR2= 0.D0
      CTR3= 0.D0
      ANGL= 0.D0
      CLCS= 0.D0
      EPCS= 0.D0
      CLCT= 0.D0
      PCT = 0.D0
      EPCT= 0.D0
      CLCD= 0.D0
      PCD = 0.D0
      EPCD= 0.D0
      CLPH= 0.D0
      PENT= 0.D0
C
      YOUN= XMAT(1)
      XNU = XMAT(2)
      RHO = XMAT(3)
      ALPH= XMAT(4)
      CTR1= XMAT(5)
      CTR2= XMAT(6)
      CTR3= XMAT(7)
      ANGL= XMAT(8)
      CLCS= XMAT(9)
      EPCS= XMAT(10)
      CLCT= XMAT(11)
      PCT = XMAT(12)
      CLCD= XMAT(13)
      PCD = XMAT(14)
      EPCD= XMAT(15)
      CLPH= XMAT(16)
      PENT= XMAT(17)
C
      IF(IIMPI.EQ.9) WRITE(IOIMP,2007) (XMAT(I),I=1,17)
      IF(IIMPI.EQ.9) WRITE(IOIMP,2008) (IVAL(I),I=1,17)
C
C
C  PETIT TEST SUR NU POUR CERTAINS CAS
C
      IF(MFR.EQ.1.AND.(IFOUR.GE.-1.OR.IFOUR.EQ.-3)) THEN
C
C VALEURS PAR DEFAUT
C
C                  DONNEES GENERALES
C
        IF(IVAL( 3).EQ.0) RHO =2.4D+3
        IF(IVAL( 4).EQ.0) ALPH=1.2D-5
C
C                  POUR LA TRACTION
C
        IF(IVAL( 5).EQ.0) CTR1=YOUN*1.2E-4
        IF(IVAL( 6).EQ.0) CTR2=CTR1
        IF(IVAL( 7).EQ.0) CTR3=CTR1
C
C                  POUR LA COMPRESSION
C
        IF(IVAL( 9).EQ.0) CLCS=YOUN*1.2D-3
        IF(IVAL(10).EQ.0) EPCS=10.D0*CLCS/YOUN
        IF(IVAL(12).EQ.0) PCT=1.6D0*CLCS
C
C                  BI-COMPRESSION
C
        REP=EPCS*YOUN/CLCS
        AAA=CLCT/CLCS
        BBB=PCT/CLCS
        CCC=CLCT/PCT
        UMN=1.D0-XNU
        ALF=((REP-UMN-(SQRT(0.5D0*REP*UMN)))/(2.D0*REP-UMN))*0.95D0
        DDD=(1.D0-ALF)/(1.D0-2.D0*ALF)
        CLBI=DDD*CLCS
C
C                CISAILLEMENT FRAGILE
C
        IF(IVAL(11).EQ.0) THEN
          CCC=((1.D0+BBB)/BBB)+((3.D0*ALF)/(1.D0-ALF))
          CLCT=CCC*PCT
        ELSE IF(IVAL(12).EQ.0) THEN
          BBB=((AAA-1.D0)*(1.D0-ALF))/(1.D0+2.D0*ALF)
          PCT=BBB*CLCS
        ENDIF
C
C                CISAILLEMENT DUCTILE
C
        IF(IVAL(14).EQ.0) PCD=1.125D0*PCT
        CCC=CLCT/PCT
        EEE=PCD/PCT
        FFF=(((1.D0+2.D0*ALF)*(EEE-1.D0))/(EEE*(1.D0-ALF)))+(CCC/EEE)
        FFF=FFF/1.05D0
        IF(IVAL(13).EQ.0) CLCD=FFF*PCD
        IF(IVAL(15).EQ.0) EPCD=5.D0*EPCS
C
C                PRESSION HYDROSTATIQUE
C
        BBB=PCT/CLCS
        IF(IVAL(16).EQ.0) CLPH=(((CCC+2.D0)*BBB)/3.D0)*1.05D0*CLCS
        IF(IVAL(17).EQ.0) PENT=YOUN/(9.D0*(1.D0-2.D0*XNU))
C
C INITIALISATION
C
        JEPRIN = 0
        PREC=1.D-2
        RFPR=1.D-3
        RFEP=1.D-7
        RFSG=YOUN*RFEP
C
C CALCUL ET TEST DES PARAMETRES DU BETON
C
C     CALCUL DES PENTES
C
        XK1=YOUN/(3.D0*(1.D0-2.D0*XNU))
        XK2=PENT
        EPSA=CLCS/YOUN
        EPSB=(CLCT-PCT)/YOUN
        EPSC=(CLCD-PCD)/YOUN
        PENTE1=(CLCD-PCD-CLCT+PCT)/(EPCD-EPSB)
        PENTE1=(YOUN*PENTE1)/(YOUN-PENTE1)
        PENTE2=-CLCS/(EPCS-EPSA)
        PENTE2=(YOUN*PENTE2)/(YOUN-PENTE2)
        PENTE3=XK1*XK2/(XK1-XK2)
C
        CLCS=-CLCS
        CLCSA=CLCS*RFEP
        CLCT=-CLCT
        CLCD=-CLCD
        PCT=-PCT
        PCD=-PCD
        EPSA=-EPSA
        EPSB=-EPSB
        EPSC=-EPSC
        EPCD=-EPCD
        EPCS=-EPCS
C
C     CALCUL DES CONSTANTES
C
        XI1A=CLCS
        XI1AA=CLCSA
        XI1B=CLCT+2.D0*PCT
        XI1C=CLCD+2.D0*PCD
        SEQA=ABS(CLCS)
        SEQAA=ABS(CLCSA)
        SEQB=ABS(CLCT-PCT)
        SEQC=ABS(CLCD-PCD)
C
        VMELAS=SEQB
C
        ALFADV=(SEQC-SEQB)/(XI1B-XI1C)
        DPELAS=ALFADV*XI1B+SEQB
C
        ALFAD1=(SEQB-SEQAA)/(XI1AA-XI1B)
        DPELA1=ALFAD1*XI1B+SEQB
C
        ALFAD2=(SEQB-SEQA)/(XI1A-XI1B)
        DPELA2=ALFAD2*XI1B+SEQB
        PENTE2=PENTE2*(ALFAD2-1.D0)*(ALFAD2-1.D0)
C
        PORELA=CLPH
C
        UMU=YOUN/(1.D0+XNU)/2.D0
        ALAM=2.D0*UMU*XNU/(1.-2.D0*XNU)
        CPLA=YOUN/(1.D0-(XNU*XNU))
C
        TRTRAC=CTR1+CTR2+CTR3
        XLAMER=(ALFAD2*TRTRAC-DPELA2)/PENTE2
        XLAMAX=(ALFAD2*DPELA1-ALFAD1*DPELA2)/(ALFAD1*PENTE2)
        DP2MIN=DPELA1*ALFAD2/ALFAD1
C
        SIGT=TRTRAC*0.333333333333333333333333333333333D0
        PMIN=-XI1B*0.333333333333333333333333333333333D0
        SIG1=(DPELA2-(2.D0*ALFAD2+1.D0)*PCD)/(ALFAD2-1.D0)
        SIG3=(DPELA2-(ALFAD2-1.D0)*CLCD)/(2.D0*ALFAD2+1.D0)
C
C     TEST SUR LES VALEURS DES PARAMETRES
C
        IF(CLCT.GT.CLCS.OR.CLCT.LT.CLCD) THEN
          KERRE=640
          WRITE(IOIMP,5010) ABS(CLCT),ABS(CLCS),ABS(CLCD)
        ENDIF
C
        IF(PCT.LT.PCD) THEN
          KERRE=640
          WRITE(IOIMP,5011) ABS(PCT),ABS(PCD)
        ENDIF
C
        IF(EPCS.GE.EPSA) THEN
          KERRE=640
          WRITE(IOIMP,5012) ABS(EPCS),ABS(EPSA)
        ENDIF
C
        IF(EPCD.GE.EPSC) THEN
          KERRE=640
        WRITE(IOIMP,5013)ABS(EPCD),ABS(EPSC)
        ENDIF
C
        IF(ALFADV.LT.0.D0) THEN
          KERRE=640
          WRITE(IOIMP,5017)
        ENDIF
C
        IF(ALFAD1.LT.0.D0.OR.ALFAD2.LT.0.D0) THEN
          KERRE=640
          WRITE(IOIMP,5018)
        ENDIF
C
        IF(ABS(CLCS).LT.SIGT) THEN
          KERRE=640
          WRITE(IOIMP,5014) ABS(CLCS),SIGT
        ENDIF
C
        IF(PMIN.GE.PORELA) THEN
          KERRE=640
          WRITE(IOIMP,5015) PMIN
        ENDIF
C
        IF(ALFAD1.GE.0.5D0) THEN
          KERRE=640
          WRITE(IOIMP,5019) ABS(CLCT),(4*ABS(PCT))
         ENDIF
C
        IF(DPELA2.GE.DPELAS) THEN
          KERRE=640
          WRITE(IOIMP,5020) ABS(CLCD),ABS(SIG1),ABS(PCD),ABS(SIG3)
        ENDIF
C
        IF(XK2.GE.XK1) THEN
          KERRE=640
          WRITE(IOIMP,5021) XK2,XK1
        ENDIF
C
        ECR2=(2.D0*YOUN*(0.5D0-ALFAD2)*(0.5D0-ALFAD2))/(1.D0-XNU)
        IF(ABS(PENTE2).GT.ECR2) THEN
          KERRE=640
          WRITE(IOIMP,5016) ABS(PENTE2),ECR2
        ENDIF
C
        ECR3=(1.5D0/(1.D0+XNU))+(3.D0*ALFAD2*ALFAD2/(1.D0-2.D0*XNU))
        ECR3=ECR3*YOUN
        IF(ABS(PENTE2).GT.ECR3) THEN
          KERRE=640
          WRITE(IOIMP,5022) ABS(PENTE2),ECR3
        ENDIF
C
        IF(KERRE.NE.0) RETURN
C
C ECOULEMENT PLASTIQUE
C
  333   MCOD=1
        CALL VISAVI(SIG0,DSIGT,VAR0,SIGMA,DSIGMA,SPHER,AUXIL,
     .          SIGF,DEFP,VARF,SIGFIN,DEFPLA,
     .        DSIGZE,ICENT2,MCOD,IBOU,MFR,NSTRS,XCAR,CMATE,ecou,necou)
        IF(ITYP.EQ.0) THEN
          KERRE=269
          RETURN
        ENDIF
C
C IMPRESSION DES PARAMETRES DU MODELE
C
        IF(IIMPI.EQ.9) THEN
          WRITE(IOIMP,1000)
          WRITE(IOIMP,1001) YOUN
          WRITE(IOIMP,1002) XNU
          WRITE(IOIMP,1003) RHO
          WRITE(IOIMP,1004) ALPH
          WRITE(IOIMP,1005)
          WRITE(IOIMP,1006) CTR1,CTR2,CTR3
          WRITE(IOIMP,1007) ANGL
          WRITE(IOIMP,1008)
          WRITE(IOIMP,1009) ABS(CLCS),ABS(EPCS)
          WRITE(IOIMP,1010) ABS(CLBI)
          WRITE(IOIMP,1011)
          WRITE(IOIMP,1012) ABS(CLCT),ABS(PCT)
          WRITE(IOIMP,1013) ABS(CLCD),ABS(PCD),ABS(EPCD)
          WRITE(IOIMP,1014)
          WRITE(IOIMP,1015) CLPH,PENT
        ENDIF
C
C INITIALISATIONS DES PARAMETRES
C
        EPSE=VAR0(1)
        IF(CTR1.GT.0.D0) RT1=(1.D0-VAR0(2))*CTR1
        IF(CTR1.LE.0.D0) RT1=0.D0
        IF(CTR2.GT.0.D0) RT2=(1.D0-VAR0(3))*CTR2
        IF(CTR2.LE.0.D0) RT2=0.D0
        IF(CTR3.GT.0.D0) RT3=(1.D0-VAR0(4))*CTR2
        IF(CTR3.LE.0.D0) RT3=0.D0
        ANGLE=VAR0(5)
        IF(IVAL( 8).NE.0) ANGLE=ANGL
        XLAMBD(1)=VAR0(6)
        XLAMBD(2)=VAR0(7)
        XLAMBD(3)=VAR0(8)
        XLAMBD(4)=VAR0(9)
        XLAMBD(5)=VAR0(10)
C
        IFIS=0
        IF((RT1.EQ.0.D0.OR.RT2.EQ.0.D0).AND.ANGLE.NE.0.D0) IFIS=1
        IF(IVAL( 8).NE.0) IFIS=1
C
        IDED=0
        IF(XLAMBD(3).GT.0.D0) IDED=IDED+1
        IF(XLAMBD(5).GT.0.D0) IDED=IDED+2
C
        DO 200 IB=1,IBOU
        STOT(IB)=SIGMA(IB)+DSIGMA(IB)
  200   CONTINUE
C
C ECOULEMENT PLASTIQUE
C
        IF(IIMPI.EQ.9) THEN
          WRITE(IOIMP,2000)
          WRITE(IOIMP,2001) (SIGMA(I),I=1,IBOU)
          WRITE(IOIMP,2002) (DSIGMA(I),I=1,IBOU)
          WRITE(IOIMP,2003) (STOT(I),I=1,IBOU)
          WRITE(IOIMP,2006) (XLAMBD(I),I=1,5)
          WRITE(IOIMP,2010) RT1,RT2,RT3
        ENDIF
C
        CALL DAMAGE(ALFADV,ALFAD1,ALFAD2,VMELAS,DPELAS,
     .              DPELA1,DPELA2,PORELA,RT1,RT2,RT3,
     .              PENTE1,PENTE2,PENTE3,XLAMBD,SIGMA,DSIGMA,STOT,
     .              SIGFIN,ANGLE,YOUN,XNU,XLAMER,XLAMAX,DP2MIN,
     .              PREC,RFSG,RFEP,RFPR,IFIS,IDED,IDAM,KERRE)

C
        IF(IIMPI.EQ.9) WRITE(IOIMP,2011) IDAM
C
        IF(KERRE.NE.0) THEN
           IF(JEPRIN.EQ.0) THEN
             KERRE=0
             JEPRIN=1
             IIMPI=9
             GO TO 333
           ELSE
             IIMPI=0
             RETURN
           ENDIF
        ENDIF
C
C PAS DE PLASTICITE COMPORTEMENT ELASTIQUE
C
        IF(IDAM.EQ.1) THEN
          DO 300 IB=1,IBOU
          SIGFIN(IB)=STOT(IB)
          DEFPLA(IB)=0.D0
  300     CONTINUE
        DEPSEQ=0.D0
C
        ELSE
C
C NOUS AVONS ENDOMMAGE LE MATERIAU
C
C       CALCUL DE DEFORMATION PLASTIQUE
C
        DO 400 IB=1,IBOU
        DSIGP(IB)=STOT(IB)-SIGFIN(IB)
  400   CONTINUE
        CALL EPSIG(DSIGP,DEFPLA,IFOUR,YOUN,XNU,ITYP,ORMAT,XCAR)
        DEPSEQ=SQRT(AVM(DEFPLA,DEFPLA))
C
        ENDIF
C
        IF(IIMPI.EQ.9) THEN
          WRITE(IOIMP,2000)
          WRITE(IOIMP,2001) (SIGMA(I),I=1,IBOU)
          WRITE(IOIMP,2002) (DSIGMA(I),I=1,IBOU)
          WRITE(IOIMP,2003) (STOT(I),I=1,IBOU)
          WRITE(IOIMP,2004) (SIGFIN(I),I=1,IBOU)
          WRITE(IOIMP,2005) (DEFPLA(I),I=1,IBOU)
          WRITE(IOIMP,2006) (XLAMBD(I),I=1,5)
        ENDIF
C
C MISE A JOUR DES VARIABLES INTERNES
C
        VARF(1)=EPSE+DEPSEQ
        IF(CTR1.GT.0.D0) VARF(2)=1.D0-RT1/CTR1
        IF(CTR1.LE.0.D0) VARF(2)=1.D0
        IF(CTR2.GT.0.D0) VARF(3)=1.D0-RT2/CTR2
        IF(CTR2.LE.0.D0) VARF(3)=1.D0
        IF(CTR3.GT.0.D0) VARF(4)=1.D0-RT3/CTR3
        IF(CTR3.LE.0.D0) VARF(4)=1.D0
        VARF(5)=ANGLE
        VARF(6)=XLAMBD(1)
        VARF(7)=XLAMBD(2)
        VARF(8)=XLAMBD(3)
        VARF(9)=XLAMBD(4)
        VARF(10)=XLAMBD(5)
C
        IF(IIMPI.EQ.9) WRITE(IOIMP,2009) EPSE,DEPSEQ,VARF(1)
C
         MCOD=2
        CALL VISAVI(SIG0,DSIGT,VAR0,SIGMA,DSIGMA,SPHER,AUXIL,
     .          SIGF,DEFP,VARF,SIGFIN,DEFPLA,
     .        DSIGZE,ICENT2,MCOD,IBOU,MFR,NSTRS,XCAR,CMATE,ecou,necou)
C
      ELSE
        KERRE=639
        RETURN
      ENDIF
C
C
C LES FORMATS D IMPRESSION
C
 1000 FORMAT(//,31X,'MATERIAU BETON',//)
 1001 FORMAT(6X,'MODULE D YOUNG  ',T51,'= ',1PD12.5)
 1002 FORMAT(6X,'COEF. DE POISSON',T51,'= ',1PD12.5)
 1003 FORMAT(6X,'MASSE VOLUMIQUE ',T51,'= ',1PD12.5)
 1004 FORMAT(6X,'COEF. DE DILATATION THERMIQUE',T51,'= ',1PD12.5)
 1005 FORMAT(/,1X,'DOMMAGE PAR TRACTION :',/)
 1006 FORMAT(6X,'CONTRAINTES LIMITES :',/
     *     11X,'DIRECTION(1) = ',1PD12.5,
     *      5X,'DIRECTION(2) = ',1PD12.5,5X,'DIRECTION(3) = ',1PD12.5/)
 1007 FORMAT(6X,'ANGLE DE FISSURATION',T51,'= ',1PD12.5/)
 1008 FORMAT(/,1X,'DOMMAGE PAR COMPRESSION :',/)
 1009 FORMAT(6X,'COMPRESSION SIMPLE :',/
     *     11X,'CONTRAINTE LIMITE = ',1PD12.5,
     *      5X,'DEFORMATION A RUPTURE   = ',1PD12.5/)
 1010 FORMAT(6X,'COMPRESSION BIAXIALE :',/
     *     11X,'CONTRAINTE LIMITE = ',1PD12.5/)
 1011 FORMAT(/,1X,'DOMMAGE PAR CISAILLEMENT :',/)
 1012 FORMAT(6X,'SOUS CONFINEMENT DE TRANSITION :',/
     *     11X,'CONTRAINTE LIMITE = ',1PD12.5,
     *      5X,'PRESSION DE CONFINEMENT = ',1PD12.5/)
 1013 FORMAT(6X,'SOUS CONFINEMENT DUCTILE :',/
     *     11X,'CONTRAINTE LIMITE = ',1PD12.5,
     *      5X,'PRESSION DE CONFINEMENT = ',1PD12.5,/
     *     11X,'DEFORMATION AU DEBUT DU PALIER PLASTIQUE = ',1PD12.5/)
 1014 FORMAT(/,1X,'DOMMAGE PAR PRESSION HYDROSTATIQUE :',/)
 1015 FORMAT(6X,'PRESSION LIMITE = ',1PD12.5,
     *     12X,'MODULE TANGENT = ',1PD12.5,//)
C
 2000 FORMAT(1X,'RESULTATS DE L ECOULEMENT PLASTIQUE')
 2001 FORMAT(1X,'SIGMA  =',6(1X,1PD12.5))
 2002 FORMAT(1X,'DSIGMA =',6(1X,1PD12.5))
 2003 FORMAT(1X,'STOT   =',6(1X,1PD12.5))
 2004 FORMAT(1X,'SIGFIN =',6(1X,1PD12.5))
 2005 FORMAT(1X,'DEFPLA =',6(1X,1PD12.5))
 2006 FORMAT(1X,'XLAMBD =',5(1X,1PD12.5))
 2007 FORMAT(7(6(1X,1PD12.5),/))
 2008 FORMAT(7(6(1X,I12),/))
 2009 FORMAT(1X,'EPSE0  =',1X,1PD12.5,1X,'DEPSEQ=',1X,1PD12.5,
     .       1X,'EPSE   =',1X,1PD12.5)
 2010 FORMAT(1X,'RT1    =',1X,1PD12.5,1X,'RT2   =',1X,1PD12.5,
     .       1X,'RT3    =',1X,1PD12.5)
 2011 FORMAT(1X,'INDICE D ENDOMMAGEMENT IDAM  =',I4)
C
 5010 FORMAT(1X,'ERREUR LA LIMITE LCT =',1PD12.5,' DOIT ETRE COMPRISE',
     .     /,1X,'ENTRE LCS =',1PD12.5,' ET LCD =',1PD12.5)
 5011 FORMAT(1X,'ERREUR LE CONFINEMENT PCT =',1PD12.5,' DOIT ETRE ',/,
     .       1X,'INFERIEUR A PCD =',1PD12.5)
 5012 FORMAT(1X,'ERREUR LA DEFORMATION A RUPTURE EN COMPRESSION SIMPLE'
     .         ,' EST MAUVAISE',/,
     .       1X,'EPCS =',1PD12.5,' DOIT ETRE SUPERIEURE A ',1PD12.5)
 5013 FORMAT(1X,'ERREUR LA DEFORMATION A RUPTURE EN COMPRESSION DUCTILE'
     .         ,' EST MAUVAISE',/,
     .       1X,'EPCD =',1PD12.5,' DOIT ETRE SUPERIEURE A ',1PD12.5)
 5014 FORMAT(1X,'ERREUR LA LIMITE EN COMPRESSION SIMPLE LCS =',1PD12.5,
     .     /,1X,'DOIT ETRE SUPERIREURE A LA LIMITE EN TRACTION LCT =',
     .           D12.5)
 5015 FORMAT(1X,'ERREUR LA LIMITE EN PRESSION HYDROSTATIQUE SE TROUVE',
     .          ' DANS LE DOMAINE FRAGILE',/,
     .       1X,'IL FAUT AUGMENTER LPH POUR QU''ELLE SOIT SUPERIEURE ',
     .          'A PMIN =',1PD12.5)
 5016 FORMAT(1X,'ERREUR MODULE D ECROUISSAGE DU DRUCKER EST TROP GRAND',
     .          ' =',1PD12.5,/,
     .       1X,'PENTE2 < 2 * E * (0.5 - ALFAD2 ) ** 2 / (1-NU) ',
     .          'DONC PENTE2 MAX VAUT =',1PD12.5)
 5017 FORMAT(1X,'ERREUR LA VALEUR DE ( LCD - PCD ) DOIT ETRE ',
     .          'SUPERIEURE A ( LCT - PCT )')
 5018 FORMAT(1X,'ERREUR LA VALEUR DE ( LCT - PCT ) DOIT ETRE ',
     .          'SUPERIEURE A LCS')
 5019 FORMAT(1X,'ERREUR LA LIMITE LCT =',1PD12.5,'DOIT ETRE INFERIEURE',
     .          ' A',1PD12.5)
 5020 FORMAT(1X,'ERREUR LA LIMITE LCD =',1PD12.5,'DOIT ETRE INFERIEURE',
     .          ' A',1PD12.5,'   OU BIEN  :',/,
     .       1X,'LE CONFINEMENT PCD =',1PD12.5,' DOIT ETRE SUPERIEURE',
     .          ' A',1PD12.5)
 5021 FORMAT(1X,'ERREUR LA PENTE DE LA PRESSION HYDROSTATIQUE ',
     .          'EST MAUVAISE',/,
     .       1X,'PENT =',1PD12.5,' DOIT ETRE INFERIEURE A ',1PD12.5)
 5022 FORMAT(1X,'ERREUR MODULE D ECROUISSAGE DU DRUCKER EST TROP GRAND',
     .           ' =',1PD12.5,/,
     .       1X,'PENTE2 < 1.5*E/(1+NU) + 3*ALFAD2**2*E/(1-2*NU) ',
     .          'DONC PENTE2 MAX VAUT ',1PD12.5)
C
      RETURN
      END









