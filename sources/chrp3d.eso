C CHRP3D    SOURCE    CHAT      07/10/22    21:15:32     5921
      SUBROUTINE CHRP3D(PPP,SIEPIN,SIEPOO,IOP)
      IMPLICIT REAL*8(A-H,o-Z)
      IMPLICIT INTEGER (I-N)
*
*     ------------------------------------------------------------------
*
*         CHGT DE REPERE POUR LES CONTRAINTES ET LES DEFORMATIONS
*           ECRITES SOUS FORME VECTORIELLE EN 3D
*                                               H. BUNG      02-93
*     ------------------------------------------------------------------
*
*     PPP      : MATRICE DE PASSAGE
*     SIEPIN   : VECTEUR DE CONTRAINTES OU DE DEFORMATIONS EN ENTREE
*     SIEPOO   : VECTEUR DE CONTRAINTES OU DE DEFORMATIONS EN SORTIE
*     IOP            ENTREE     SORTIE
*          = 0       SIG_G      SIG_L
*          = 1       SIG_L      SIG_G
*          = 2       EPS_G      EPS_L
*          = 3       EPS_L      EPS_G
*
*--    RAPPEL
*       [SIG] = [ S_11, S_22, S_33,   S_12,   S_23,   S_13 ]
*       [EPS] = [ E_11, E_22, E_33, 2*E_12, 2*E_23, 2*E_13 ]
*
*
*      IMPLICIT NONE
*
*
*---   VARIABLES GLOBALES
*      REAL *8 PPP(3,3),SIEPIN(*),SIEPOO(*)
*      INTEGER IOP
       DIMENSION PPP(3,*),SIEPIN(*),SIEPOO(*)
*
*---   VARIABLES LOCALES

      DIMENSION  AIN(9),AOO(9)
      DIMENSION LLM(9),LLV(6),LDEF(6)
*
      DATA LLM/1,4,6, 4,2,5, 6,5,3/
      DATA LLV/1,5,9, 2,6,3/
      DATA LDEF/2,3, 4,6, 7,8/
*
*
*----    PASSAGE VERS LA FORME MATRICIELLE
      DO IK=1,9
         AIN(IK) = SIEPIN(LLM(IK))
      END DO
*
      IF(IOP.GE.2) THEN
*---     TENSEUR DE DEFORMATIONS
         DO IK=1,6
            IKK      = LDEF(IK)
            AIN(IKK) = 0.5D0*AIN(IKK)
         ENDDO
      ENDIF
*
      KOP=MOD(IOP,2)
      IF(KOP.EQ.0)THEN
*          ENTREE : GLOBAL --> SORTIE : LOCAL
         CALL DR3GLM1(PPP,AIN,AOO)
      ENDIF
      IF(KOP.EQ.1) THEN
*          ENTREE : LOCAL --> SORTIE : GLOBAL
         CALL DR3GLM2(PPP,AOO,AIN)
      ENDIF
      IF(KOP.NE.0.AND.KOP.NE.1)THEN
*          STOP
         STOP 'CHR3D_1'
      ENDIF
C      SELECT CASE(KOP)
C      CASE (0)
C*          ENTREE : GLOBAL --> SORTIE : LOCAL
C         CALL DR3GLM1(PPP,AIN,AOO)
C      CASE (1)
C*          ENTREE : LOCAL --> SORTIE : GLOBAL
C         CALL DR3GLM2(PPP,AOO,AIN)
C      CASE DEFAULT
C*          STOP
C         STOP 'CHR3D_1'
C      END SELECT
*
*----    PASSAGE VERS LA FORME VECTORIELLE
      DO IK=1,6
         SIEPOO(IK) = AOO(LLV(IK))
      END DO
*
*----    POUR LES DEFORMATIONS
      IF(IOP.GE.2) THEN
         SIEPOO(4) = 2.D0*SIEPOO(4)
         SIEPOO(5) = 2.D0*SIEPOO(5)
         SIEPOO(6) = 2.D0*SIEPOO(6)
      ENDIF
*
      END


