C PODHL3    SOURCE    CHAT      05/01/13    02:16:14     5004
      SUBROUTINE PODHL3(RP,XL,VISC,CISAIL,SD,SREDY,SREDZ,
     .                  XINX,XINY,XINZ)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DE LA MATRICE DE RIGIDITE ELEMENTAIRE D UNE
C    POUTRE DROITE EN AXES LOCAUX
C    D APRES TEDEL
C
C  ENTREE
C    XL      LONGUEUR
C    SD      SECTION DROITE
C    SREDY   SECTION REDUITE A L EFFORT TRANCHANT TY
C    SREDZ   SECTION REDUITE A L EFFORT TRANCHANT TZ
C    XINX    INERTIE IX
C    XINY    INERTIE IY
C    XINZ    INERTIE IZ
C    VISC    MODULE DE VISCOSITE
C    CISAIL  MODULE DE VISCOSITE EN CISAILLEMENT
C  SORTIE
C    RP      MATRICE DE RIGIDITE ELEMENTAIRE 12X12
C
C     VIENT DE TEDEL AOUT 84
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION RP(12,12)
C
C  INITIALISATIONS
C
      DO 120 I=1,12
      DO 120 J=1,12
  120 RP(I,J)=0.D0
C
      RL=1.D00/XL
      RL2=RL*RL
      X2=0.D00
      SRED = 0.5D0*(SREDY + SREDZ)
      XINYZ = 0.5D0*(XINY + XINZ)
      IF((SREDY.NE.0).AND.(SREDZ.NE.0))
     .     X2= VISC/CISAIL*XINYZ*RL2/SRED
      X12= 1./(1.+12.*X2)
C
      RL3=RL2*RL
      EIYZ=VISC*XINYZ
C
C  REMPLISSAGE DE LA MATRICE
C
C
       RP(2,3)= 12.*EIYZ*RL3*X12
       RP(3,2)= -12.*EIYZ*RL3*X12
       RP(2,5)=  -6.*EIYZ*RL2*X12
       RP(5,2)=  6.*EIYZ*RL2*X12
       RP(2,9)=  -12. *EIYZ*RL3*X12
       RP(9,2)=  12. *EIYZ*RL3*X12
       RP(2,11)= -6.*EIYZ*RL2*X12
       RP(11,2)= 6.*EIYZ*RL2*X12
C
       RP(3,6)= -6.*EIYZ*RL2*X12
       RP(3,8)=  12.*EIYZ*RL3*X12
       RP(3,12)=  -6.*EIYZ*RL2*X12
       RP(6,3)= 6.*EIYZ*RL2*X12
       RP(8,3)=  -12.*EIYZ*RL3*X12
       RP(12,3)=  6.*EIYZ*RL2*X12
C
       RP(5,6)= 4.*EIYZ*RL
     . *(1.+3.*X2)*X12
       RP(5,8)= -6.*EIYZ*RL2*X12
       RP(5,12)=2.*EIYZ*RL
     .*(1.-6.*X2)*X12
       RP(6,5)= -4.*EIYZ*RL
     . *(1.+3.*X2)*X12
       RP(8,5)= 6.*EIYZ*RL2*X12
       RP(12,5)=-2.*EIYZ*RL
     .*(1.-6.*X2)*X12
C
      RP(6,9)=-6.*EIYZ*RL2*X12
      RP(6,11)=-2.*EIYZ*RL
     .      *(1.-6.*X2)*X12
      RP(9,6)=6.*EIYZ*RL2*X12
      RP(11,6)=2.*EIYZ*RL
     .      *(1.-6.*X2)*X12
C
       RP(8,9)= 12.*EIYZ*RL3*X12
       RP(8,11)= 6.*EIYZ*RL2*X12
       RP(9,8)= -12.*EIYZ*RL3*X12
       RP(11,8)= -6.*EIYZ*RL2*X12
C
       RP(9,12)= 6.*EIYZ*RL2*X12
       RP(12,9)= -6.*EIYZ*RL2*X12
C
       RP(11,12)= 4.*EIYZ*RL
     .  *(1.D0+3.*X2)*X12
C
       RP(12,11)= -4.*EIYZ*RL
     .   *(1.D0+3.*X2)*X12
C
      RETURN
      END



