C LAM2      SOURCE    CHAT      05/01/13    01:12:18     5004
C***********************************************************************
C*                                                                     *
C* PROJET      : Opérateur LIMI                                        *
C* NOM         : lAM2                                                  *
C* DESCRIPTION : Ce sous programme calcule les relations de fermeture  *
C*               des méthodes intégrales pour les couches limites      *
C*               laminaires dans le cas de la méthode à 2 équations.   *
C* LANGAGE     : Esope                                                 *
C* AUTEUR      : Guillaume VENCO - DRN/DMT/SEMT/LTMF                   *
C*                                                                     *
C***********************************************************************
C*                                                                     *
C* APPELES     : NEWT2.eso, sous-programme                             *
C*                                                                     *
C***********************************************************************
C*                                                                     *
C* ENTREES     : D2N : -Epaisseur de quantité de mouvement,            *
C*                      correspond à l'inconnue de l'équation intégrale*
C*                      de quantité de mouvement                       *
C*               D3N : -Epaisseur d'énergie cinétique.                 *
C*                      correspond à l'inconnue de l'équation intégrale*
C*                      d'énergie cinétique.                           *
C*               N   : -Nombre de noeuds du maillage                   *
C*                                                                     *
C* SORTIES     : H32 : -Facteur H32, correspond au rapport D3N/D2N     *
C*               H   : -Facteur de forme                               *
C*               BN  : -Coefficient de la relation permettant de       *
C*                      calculer le coefficient de frottement          *
C*               DN  : -Coefficient de la relation permetteant de      *
C*                      calculer le coefficient d'énergie cinétique    *
C*                                                                     *
C***********************************************************************
C*                                                                     *
C* VERSION     : 21/04/2000                                            *
C* CREATION    :                                                       *
C*                                                                     *
C***********************************************************************

      SUBROUTINE LAM2(D2N,D3N,H32,H,BN,DN,D1N,N)

C*       *********************************
C*       *** Déclaration des variables ***
C*       *********************************

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 D2N(N),D3N(N)
      REAL*8 H32(N),H(N),BN(N),DN(N),D1N(N)
      REAL*8 a0,a1,a2
      REAL*8 A00,B00

C*       *********************************
C*       ******** Sous-programme *********
C*       *********************************

      DO I=1,N

C---- Calcul du facteur H32
         H32(I) = D3N(I)*(D2N(I)**(-1))
c         write(6,*) 'H32=',H32(I)
C---- Test pour la poursuite du calcul
         IF(H32(I).LE.1.5215d0) THEN
            write(6,*) 'ERREUR: On ne peut pas déterminer H'
         ENDIF

C---- Calcul des coefficients de la relation donnant H
         a0 = -H32(I)-(50.84951/H32(I))+46.8818
         a1 = -23.78186
         a2 = (4.02923**2)

C---- Calcul de H en fonction de H32 par la méthode de dichotomie
C---- Recherche de la racine comprise entre les valeurs A00 et B00
         A00=1.512
         B00=4.
         CALL newt2(a0,a1,a2,A00,B00,H(I))

C---- Calcul du coefficient b(H)
         BN(I)= 2.99259*((((1./H(I))-(1./8.05846))**1.7)
     &          -((1./8.05846)**1.7))

C---- Calcul du coefficient d(H)
         DN(I)=H32(I)*(BN(I)-(H(I)-1)*(-0.06815+4.336355
     &          *(((1./H(I))-(1./4.02923))**2.095065)))*0.5

C---- Epaisseur de déplacement
         D1N(I) = H(I)*D2N(I)
      ENDDO

      RETURN
      END

C*       *********************************
C*       ****  Fin Sous-Programme  *******
C*       ********************************


