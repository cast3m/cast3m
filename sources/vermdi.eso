C VERMDI    SOURCE    BP208322  15/06/22    21:23:30     8543

C----------------------------------------------------------------------C
C----------------------- Formulation 'DIFFUSION' ----------------------C
C Verification que les noms de primale et duale du modele de diffusion C
C sont coherents s'ils ont deja ete utilises - Mise a jour de NOMDU et C
C NOMDD avec ces noms de primale et duale dans le cas contraire !      C
C----------------------------------------------------------------------C
C Sous-programme appele par :                                          C
C   - MODELI lors de la creation d'un modele                           C
C   - LIMODL lors de la restitution d'un modele (sauvegarde)           C
C----------------------------------------------------------------------C

      SUBROUTINE VERMDI(MDIINC,MDIDUA)

      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)

-INC CCOPTIO
-INC CCHAMP

      CHARACTER*(*) MDIINC,MDIDUA

      CALL PLACE(NOMDD,LNOMDD,ireti,MDIINC)
      CALL PLACE(NOMDU,LNOMDU,irete,MDIDUA)

      IF (ireti.EQ.0) THEN
        IF (irete.NE.0) THEN
          write(ioimp,*) 'Nom de duale deja utilise pour une ',
     &                   'autre inconnue !'
          CALL ERREUR(21)
          RETURN
        ENDIF
        IF (LNOMDD.GE.1000) THEN
          write(ioimp,*) 'Tableau NOMDD sous-dimensionne'
          CALL ERREUR(5)
          RETURN
        ENDIF
        IF (LNOMDU.GE.1000) THEN
          write(ioimp,*) 'Tableau NOMDU sous-dimensionne'
          CALL ERREUR(5)
          RETURN
        ENDIF
        LNOMDD = LNOMDD + 1
        NOMDD(LNOMDD) = MDIINC
        LNOMDU = LNOMDU + 1
        NOMDU(LNOMDU) = MDIDUA
      ELSE
        IF (irete.NE.ireti) THEN
          write(ioimp,*) 'Plusieurs noms de duale pour ',MDIINC
          CALL ERREUR(21)
          RETURN
        ENDIF
      ENDIF

      RETURN
      END



