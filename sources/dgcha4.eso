C DGCHA4    SOURCE    CHAT      05/01/12    22:48:25     5004
      SUBROUTINE DGCHA4(XDEP,XVITN,IDIM,IGP,XPALB,NLIAB,I,XFN,XFT,XPUS
     &                          ,iannul,XPAS)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*--------------------------------------------------------------------*
*                                                                    *
*     Op{rateur DYNE : algorithme de Fu - de Vogelaere               *
*     ________________________________________________               *
*                                                                    *
*     Calcul de la force de choc pour un choc {l{mentaire.           *
*                                                                    *
*     Param}tres:                                                    *
*                                                                    *
* e   XDEP    Valeur du d{placement sur la normale                   *
* e   XVITN   Valeur de la vitesse normale au plan tangent du contact*
* e   IGP     Indice de glissement au pas pr{c{dent                  *
* es  XPALB   Tableau contenant les param}tres de la liaison         *
* e   NLIAB   Nombre de liaisons sur la base B                       *
* e   I       Num{ro de la liaison                                   *
*  s  XFN     Valeur de la force de choc suivant la normale.         *
*  s  XFT     Module de la force de choc projetee sur le plan tangent*
*  s  XPUS    Valeur de la puissance d'usure instantanee             *
*                                                                    *
*                                                                    *
*     Auteur, date de cr{ation:                                      *
*                                                                    *
*     cas particulier de GIBERT
*                                                                    *
*--------------------------------------------------------------------*
      DIMENSION XPALB(NLIAB,*)
      XFN = 0.D0
      XFT = 0.D0
      XPUS = 0.D0
      XJEU  = XPALB(I,2)
*
      IF ((XDEP.GE.XJEU).AND.(iannul.EQ.0)) THEN
         ID2 = 7 + IDIM
         ID3 = 7 + 2*IDIM
         ID4 = 7 + 3*IDIM
         ID6 = 7 + 5*IDIM
         ID7 = 7 + 6*IDIM
         XRAIN = XPALB(I,1)
         XAMON = XPALB(I,7)
         XGLIS = XPALB(I,3)
         XADHE = XPALB(I,4)
         XRAIT = XPALB(I,5)
         XAMOT = XPALB(I,6)
         XVAL = XDEP - XJEU
         XFN = - XRAIN * XVAL - XAMON * XVITN
*
         CALL DGCHFR(IDIM,IGP,XPALB,NLIAB,I,ID2,ID3,ID4,ID6,ID7,XGLIS,
     &               XADHE,XRAIT,XAMOT,XFN,XFT,XPUS,XPAS)
         IF ( XFN.GT.0.D0) XFN = 0.D0
      ELSE
         IGP = -1
         ID4 = 7 + 3*IDIM
         DO 10 ID=1,IDIM
            XPALB(I,ID4+ID) = 0.D0
 10         CONTINUE
*        end do
      ENDIF
*
      RETURN
      END


