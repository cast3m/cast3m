C CLOUVE    SOURCE    CHAT      05/01/12    22:08:47     5004
      SUBROUTINE CLOUVE (AAA,YUNG,XNU,ET,CISA,IENDO,KERRE)
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION ET(*),CISA(*),AAA(6,6),UMBETA(3)
C
C***********************************************************************
C***************** CALCUL DU MODULE DE FISSURATION *********************
C***********************************************************************
C
C     INITIALISATION
C
      CALL ZDANUL(AAA,36)
      ET1=ET(1)
      ET2=ET(2)
      ET3=ET(3)
      UPUN=1.D0+XNU
      UMUN=1.D0-XNU
      UMDN=1.D0-2.D0*XNU
      EMET1=YUNG-ET1
      EMET2=YUNG-ET2
      EMET3=YUNG-ET3
      CISEL=YUNG/(2.D0*UPUN)
      UMBETA(1)=1.D0-(CISA(1)/CISEL)
      UMBETA(2)=1.D0-(CISA(2)/CISEL)
      UMBETA(3)=1.D0-(CISA(3)/CISEL)
C
C***********************************************************************
C************************ PAS DE FISSURATION ***************************
C***********************************************************************
C
      IF(IENDO.EQ.0) RETURN
C
      GO TO (100,200,300,400,500,600,700),IENDO
      KERRE=468
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN X SEULEMENT ***********************
C***********************************************************************
C
  100 DENO=(EMET1*UMUN)+(ET1*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(1,1)=EMET1*UMUN*USDENO
      AAA(1,2)=EMET1*XNU*USDENO
      AAA(1,3)=EMET1*XNU*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(6,6)=UMBETA(3)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN Y SEULEMENT ***********************
C***********************************************************************
C
  200 DENO=(EMET2*UMUN)+(ET2*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(2,1)=EMET2*XNU*USDENO
      AAA(2,2)=EMET2*UMUN*USDENO
      AAA(2,3)=EMET2*XNU*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(5,5)=UMBETA(2)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN Z SEULEMENT ***********************
C***********************************************************************
C
  400 DENO=(EMET3*UMUN)+(ET3*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(3,1)=EMET3*XNU*USDENO
      AAA(3,2)=EMET3*XNU*USDENO
      AAA(3,3)=EMET3*UMUN*USDENO
      AAA(5,5)=UMBETA(2)
      AAA(6,6)=UMBETA(3)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN XY ENSEMBLE ***********************
C***********************************************************************
C
  300 DENO=(EMET1*EMET2)+(ET1*EMET2*UPUN*UMUN)+(EMET1*ET2*UPUN*UMUN)+
     .     (ET1*ET2*UPUN*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(1,1)=EMET1*(EMET2+(ET2*UPUN*UMUN))*USDENO
      AAA(1,2)=EMET1*(XNU*ET2*UPUN)*USDENO
      AAA(1,3)=XNU*EMET1*(EMET2+(ET2*UPUN))*USDENO
      AAA(2,1)=EMET2*(XNU*ET1*UPUN)*USDENO
      AAA(2,2)=EMET2*(EMET1+(ET1*UPUN*UMUN))*USDENO
      AAA(2,3)=XNU*EMET2*(EMET1+(ET1*UPUN))*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(5,5)=UMBETA(2)
      AAA(6,6)=UMBETA(3)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN YZ ENSEMBLE ***********************
C***********************************************************************
C
  600 DENO=(EMET2*EMET3)+(ET2*EMET3*UPUN*UMUN)+(EMET2*ET3*UPUN*UMUN)+
     .     (ET2*ET3*UPUN*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(2,1)=XNU*EMET2*(EMET3+(ET3*UPUN))*USDENO
      AAA(2,2)=EMET2*(EMET3+(ET3*UPUN*UMUN))*USDENO
      AAA(2,3)=EMET2*(XNU*ET3*UPUN)*USDENO
      AAA(3,1)=XNU*EMET3*(EMET2+(ET2*UPUN))*USDENO
      AAA(3,2)=EMET3*(XNU*ET2*UPUN)*USDENO
      AAA(3,3)=EMET3*(EMET2+(ET2*UPUN*UMUN))*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(5,5)=UMBETA(2)
      AAA(6,6)=UMBETA(3)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN ZX ENSEMBLE ***********************
C***********************************************************************
C
  500 DENO=(EMET3*EMET1)+(ET3*EMET1*UPUN*UMUN)+(EMET3*ET1*UPUN*UMUN)+
     .     (ET3*ET1*UPUN*UPUN*UMDN)
      USDENO=1.D0/DENO
      AAA(1,1)=EMET1*(EMET3+(ET3*UPUN*UMUN))*USDENO
      AAA(1,2)=XNU*EMET1*(EMET3+(ET3*UPUN))*USDENO
      AAA(1,3)=EMET1*(XNU*ET3*UPUN)*USDENO
      AAA(3,1)=EMET3*(XNU*ET1*UPUN)*USDENO
      AAA(3,2)=XNU*EMET3*(EMET1+(ET1*UPUN))*USDENO
      AAA(3,3)=EMET3*(EMET1+(ET1*UPUN*UMUN))*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(5,5)=UMBETA(2)
      AAA(6,6)=UMBETA(3)
      RETURN
C
C***********************************************************************
C******************** FISSURATION EN XYZ ENSEMBLE **********************
C***********************************************************************
C
  700 DENO=(EMET1*EMET2*EMET3)+
     .   (ET1*EMET2*EMET3)+(EMET1*ET2*EMET3)+(EMET1*EMET2*ET3)+
     .   (UPUN*UMUN*((EMET1*ET2*ET3)+(ET1*EMET2*ET3)+(ET1*ET2*EMET3)))+
     .   (UPUN*UPUN*UMDN*(ET1*ET2*ET3))
      USDENO=1.D0/DENO
      AAA(1,1)=((EMET1*EMET2*EMET3)+(EMET1*ET2*EMET3)+(EMET1*EMET2*ET3)+
     .      (EMET1*ET2*ET3*UPUN*UMUN))*USDENO
      AAA(1,2)=XNU*EMET1*ET2*(EMET3+(ET3*UPUN))*USDENO
      AAA(1,3)=XNU*EMET1*ET3*(EMET2+(ET2*UPUN))*USDENO
      AAA(2,1)=XNU*EMET2*ET1*(EMET3+(ET3*UPUN))*USDENO
      AAA(2,2)=((EMET1*EMET2*EMET3)+(ET1*EMET2*EMET3)+(EMET1*EMET2*ET3)+
     .      (ET1*EMET2*ET3*UPUN*UMUN))*USDENO
      AAA(2,3)=XNU*EMET2*ET3*(EMET1+(ET1*UPUN))*USDENO
      AAA(3,1)=XNU*EMET3*ET1*(EMET2+(ET2*UPUN))*USDENO
      AAA(3,2)=XNU*EMET3*ET2*(EMET1+(ET1*UPUN))*USDENO
      AAA(3,3)=((EMET1*EMET2*EMET3)+(ET1*EMET2*EMET3)+(EMET1*ET2*EMET3)+
     .      (ET1*ET2*EMET3*UPUN*UMUN))*USDENO
      AAA(4,4)=UMBETA(1)
      AAA(5,5)=UMBETA(2)
      AAA(6,6)=UMBETA(3)
      RETURN
C
      END

