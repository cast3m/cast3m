C LIRFEM    SOURCE    CB215821  16/03/08    21:15:11     8842
      SUBROUTINE LIRFEM

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C BUT: Lecture des données provenant de HyperMesh sous forme de
C      fichier FEM (ASCII). Les données sont logées dans une table
C      qui est renvoyée comme résultat.
C
C Auteur : Clément BERTHINIER
C          Mars 2016
C
C Liste des Corrections :
C
C Appelé par : LIREFI
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC


      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)


C Déclaration des chaines de caractères
      CHARACTER*256 FicFem
      CHARACTER*80  LIGNE

C     Unite logique du fichier d'impression au format .fem et nom du fichier
      PARAMETER (IUFEM=67)

C     Version minimum du Templates HyperMesh qui sont Lus ou partiellement Lus
      PARAMETER (MINVER=12)

C Définition des COMMON utiles
-INC CCOPTIO
-INC SMCOORD


      NBLIGN = 0

C Lecture des arguments : Nom du fichier à lire (toto.fem)
      CALL LIRCHA(FicFem,1,IRETO1)
      IF (IERR.NE.0) RETURN

C Par defaut, Erreur Cast3M numero 424
C     Erreur 424 : Problème %i1 en ouvrant le fichier : %m1:40
      iOK=424
      L=LEN(FicFem)
      MOTERR(1:L)=FicFem(1:L)
      INTERR(1)=0

C Ouverture du fichier .fem
      CLOSE(UNIT=IUFEM,ERR=991)
      OPEN(UNIT=IUFEM,STATUS='OLD',FILE=FicFem(1:L),
     &     IOSTAT=IOS,FORM='FORMATTED')

C Traitement des erreurs d'ouverture des fichiers
      IF (IOS.NE.0) THEN
        INTERR(1)=IOS
C           IF (DEBCB) THEN
C             WRITE(IOIMP,*) 'Fichier introuvable : ',FicFem
C           ENDIF
        CALL ERREUR(424)
        RETURN
      ELSE
C        IF (DEBCB) THEN
C          WRITE(IOIMP,*) 'Ouverture OK du fichier FEM'
C        ENDIF

C       Changement de dimension (si necessaire)
        iOK=0
        IDIMI=IDIM
        IDIMF=3
        IF (IDIMF.NE.IDIMI) THEN
          CALL ECRENT(IDIMF)
          CALL ECRCHA('DIME')
          CALL OPTION(1)
          IF (IERR.NE.0) THEN
            CALL ERREUR(IERR)
            RETURN
          ENDIF
          WRITE(IOIMP,*) ' '
          WRITE(IOIMP,*) '     Passage en DIMEnsion 3'
          WRITE(IOIMP,*) ' '
        ENDIF
      ENDIF

 10   CONTINUE
C     Lecture de la ligne complete (80 caracteres)
 1000 FORMAT(A80)
      READ(IUFEM,1000,ERR=991,END=100) LIGNE
      NBLIGN = NBLIGN + 1
C       IF (DEBCB) THEN
C         WRITE(IOIMP,*) 'Nombre de LIGNES : ',NBLIGN
C       ENDIF

      IF (NBLIGN .EQ. 3) THEN
C       Lecture de la version du Template d'export
        IF (LIGNE(60:68) .EQ. 'hwdesktop') THEN
          READ(LIGNE(69:70),*) IVERLU
        ELSE
          READ(LIGNE(60:61),*) IVERLU
        ENDIF

        IF ( IVERLU .LT. MINVER) THEN
C         Teste si la version est supportée
          WRITE(IOIMP,*) '   Version non supportee   : ',IVERLU
          WRITE(IOIMP,*) '   Version Minimum requise : ',MINVER
          WRITE(IOIMP,*) '                             '
          iOK = 424
        ENDIF
      ENDIF

      IF (NBLIGN .EQ. 5) THEN
C       Lecture du nom du Template d'export
        IF (LIGNE(1:26) .NE. '$$   Template:  optistruct') THEN
          WRITE(IOIMP,*) '   Template non supporte  : ',LIGNE(17:50)
          WRITE(IOIMP,*) '   Template requis        : optistruct'
          WRITE(IOIMP,*) '                             '
          iOK = 424
        ENDIF
        GOTO 100
      ENDIF
      GOTO 10

 100  CONTINUE

C***********************************************************************
C      Orientation vers la source qui lit les fichiers .fem
C***********************************************************************
      MTABLE = 0
      IF    (IVERLU .LT. 14) THEN
        CALL femv12(IUFEM,NBLIGN,MTABLE)

      ELSEIF(IVERLU .GE. 14) THEN
        CALL femv14(IUFEM,NBLIGN,MTABLE)

      ELSE
        CALL ERREUR(21)
        RETURN

      ENDIF

C***********************************************************************
C      Fermeture du fichier en lecture
C***********************************************************************
      CLOSE(UNIT=IUFEM,ERR=991)

 991  CONTINUE

C Traitement des erreurs
      IF (iOK .NE.0) THEN
        CALL ERREUR(iOK)

      ELSEIF (IERR.NE.0) THEN
        CALL ERREUR(IERR)

      ELSE
        CALL ECROBJ('TABLE   ',MTABLE)

      ENDIF

      RETURN
      END


