C JAKBM     SOURCE    CHAT      05/01/13    00:48:48     5004
       SUBROUTINE JAKBM(JTL,JTR,WVEC_L,WVEC_R,NVECT,TVECT,
     &                   ga,v_inf)
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  JACBM
C
C DESCRIPTION       :  Voir KONJA6
C
C LANGAGE           :  FORTRAN 77 + ESOPE 2000 (avec estensions CISI)
C
C AUTEUR            :  S. KUDRIAKOV, DM2S/SFME/LTMF
C
C************************************************************************
C
c----------------------------------------------------------------------
c GENERAL DESCRIPTION:
c   This subroutine provides the jacobians which are the derivatives
c   of the numerical flux function defined at the cell interface
c   with respect to the conservative variables of the left and right
c   cells (relative to the cell interface).
c   The low-mach number corrections are made for the flux functions
c
c EQUATIONS: 2D Euler equations of gas dynamics
c
c
c REFERENCE: 1) JCP, 129, 364-382 (1996)
c               " A Sequel to AUSM: AUSM+ ".
c                     M.S.Liou
c            2) AIAA Journal, Sept. 1998
c          " Low-Diffusion Flux-Splitting Methods for Flows at All Speeds"
c                J.R.Edwards and M.S.Liou
c----------------------------------------------------------------------
c INPUT:
c
c      alpha   -- parameter of the AUSM+ scheme in the Pressure function;
c                 ( -3/4 <= alpha <= 3/16 ) (imposed as a parameter)
c
c      beta    -- parameter of the AUSM+ scheme in the Mach function;
c                 ( -1/16 <= beta <= 1/2 )  (imposed as a parameter)
c
c      wvec_l  -- vector of the primitive variables (rho,ux,uy,p) at the
c                 left cell;
c
c      wvec_r  -- vector of the primitive variables (rho,ux,uy,p) at the
c                 right cell;
c
c      nvect   -- normal vector to the interface (2 components in 2D);
c
c      tvect   -- tangential vector to the interface;
c
c      ga      -- ratio of the specific heats (assumed constant)
c
c      v_inf   -- parameter for reference velocity, see Ref. 2).
c----------------------------------------------------------------------
c
c OUTPUT:
c
c      jtl     -- jakobian matrix 4 by 4 - derivatives of the numerical
c                 flux function with respect to the conservative variables
c                 from the left cell;
c
c      jtr     -- jakobian matrix 4 by 4 - derivatives of the numerical
c                 flux function with respect to the conservative variables
c                 from the right cell.
c----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
       real*8 wvec_l(4),wvec_r(4)
       real*8 nvect(2),tvect(2)
       real*8 jl(4,4),jr(4,4)
       real*8 wl(4,4),wr(4,4)
       real*8 jtl(4,4),jtr(4,4)
       real*8 alpha,beta
       real*8 ga,gm1,temph
       real*8 n_x,n_y,t_x,t_y
       real*8 un_l, un_r, ut_l, ut_r
       real*8 ml,mr,Mplus,Mmin,mmid
       real*8 mpl_m, mmin_m,am
       real*8 rold_l,uold_l,vold_l,pold_l,eold_l
       real*8 rold_r,uold_r,vold_r,pold_r,eold_r
       real*8 Pplus,Pmin
       real*8 hr_l,hr_r,top,bot,bots
       real*8 br1,br3,br4,temp_l,temp_r,brac_l,brac_r
       real*8 aleft, arigh
       real*8 damr_l,damr_r,damu_l,damu_r
       real*8 damv_l,damv_r,damp_l,damp_r
       real*8 dmlr_l,dmlr_r,dmlu_l,dmlu_r
       real*8 dmlv_l,dmlv_r,dmlp_l,dmlp_r
       real*8 dmrr_l,dmrr_r,dmru_l,dmru_r
       real*8 dmrv_l,dmrv_r,dmrp_l,dmrp_r
       real*8 dMpr_l,dMpr_r,dMpu_l,dMpu_r
       real*8 dMpv_l,dMpv_r,dMpp_l,dMpp_r
       real*8 dMmr_l,dMmr_r,dMmu_l,dMmu_r
       real*8 dMmv_l,dMmv_r,dMmp_l,dMmp_r
       real*8 dmir_l,dmir_r,dmiu_l,dmiu_r
       real*8 dmiv_l,dmiv_r,dmip_l,dmip_r
       real*8 d3mr_l,d3mr_r,d3mu_l,d3mu_r
       real*8 d3mv_l,d3mv_r,d3mp_l,d3mp_r
       real*8 d2mr_l,d2mr_r,d2mu_l,d2mu_r
       real*8 d2mv_l,d2mv_r,d2mp_l,d2mp_r
       real*8 dPpr_l,dPpr_r,dPpu_l,dPpu_r
       real*8 dPpv_l,dPpv_r,dPpp_l,dPpp_r
       real*8 dPmr_l,dPmr_r,dPmu_l,dPmu_r
       real*8 dPmv_l,dPmv_r,dPmp_l,dPmp_r
       real*8 dpir_l,dpir_r,dpiu_l,dpiu_r
       real*8 dpiv_l,dpiv_r,dpip_l,dpip_r
       real*8 epsil,qq,amw,Mmin1,Mplus1
       real*8 fmid,mlw,mrw,termp
       real*8 ur_r,ur_l,urm,mhalf,mhalfr
       real*8 durr_l,durr_r,duru_l,duru_r
       real*8 durv_l,durv_r,durp_l,durp_r
       real*8 dmhr_l,dmhr_r,dmhu_l,dmhu_r
       real*8 dmhv_l,dmhv_r,dmhp_l,dmhp_r
       real*8 dmfr_l,dmfr_r,dmfu_l,dmfu_r
       real*8 dmfv_l,dmfv_r,dmfp_l,dmfp_r
       real*8 dfm_mf,dfm_mh
       real*8 dfmr_l,dfmr_r,dfmu_l,dfmu_r
       real*8 dfmv_l,dfmv_r,dfmp_l,dfmp_r
       real*8 m1mr_l,m1mr_r,m1mu_l,m1mu_r
       real*8 m1mv_l,m1mv_r,m1mp_l,m1mp_r
       real*8 m1pr_l,m1pr_r,m1pu_l,m1pu_r
       real*8 m1pv_l,m1pv_r,m1pp_l,m1pp_r
       real*8 tmpr_l,tmpr_r,tmpu_l,tmpu_r
       real*8 tmpv_l,tmpv_r,tmpp_l,tmpp_r
       real*8 coef,canc,v_inf
       real*8 sr_l,sr_r,su_l,su_r,sv_l,sv_r,sp_l,sp_r
       real*8 rum,rumr_l,rumu_l,rumv_l,rump_l
       real*8 rumr_r,rumu_r,rumv_r,rump_r
       integer i,j,k
       parameter(alpha = 0.1875D0, beta = 0.125D0)
       parameter(epsil = 1.0d0)
       canc=1.0d0
c-------------------------------------------------------------
       n_x=nvect(1)
       n_y=nvect(2)
       t_x=tvect(1)
       t_y=tvect(2)
c----------------------------
       gm1=ga-1.0d0
c----------------------------
       rold_l=wvec_l(1)
       uold_l=wvec_l(2)
       vold_l=wvec_l(3)
       pold_l=wvec_l(4)
c-----------------------
       rold_r=wvec_r(1)
       uold_r=wvec_r(2)
       vold_r=wvec_r(3)
       pold_r=wvec_r(4)
c------------------------------------------------------------------
c Computation of the specific total energy on the left and right.
c------------------------------------------------------------------
       eold_l=(uold_l*uold_l+vold_l*vold_l)/2.0d0
       eold_l=eold_l+pold_l/(gm1*rold_l)
       eold_r=(uold_r*uold_r+vold_r*vold_r)/2.0d0
       eold_r=eold_r+pold_r/(gm1*rold_r)
c------------------------------------------------------------------
c   Computing reference velocity and its derivatives
c------------------------------------------------------------------
       aleft=sqrt(ga*pold_l/rold_l)
       arigh=sqrt(ga*pold_r/rold_r)
       qq=sqrt(uold_l*uold_l+vold_l*vold_l)
       if(qq .lt. (epsil*v_inf)) then
          ur_l = epsil*v_inf
          durr_l=0.0d0
          duru_l=0.0d0
          durv_l=0.0d0
          durp_l=0.0d0
       else
          ur_l=qq
          durr_l=0.0d0
          duru_l=uold_l/qq
          durv_l=vold_l/qq
          durp_l=0.0d0
       endif
c------------------------------
       if(ur_l .ge. aleft) then
          ur_l=aleft
          durr_l=-aleft/(2.0d0*rold_l)
          duru_l=0.0d0
          durv_l=0.0d0
          durp_l=ga/(2.0d0*aleft*rold_l)
       endif
c------------------------------------------------------------------
       qq=sqrt(uold_r*uold_r+vold_r*vold_r)
       if(qq .lt. (epsil*v_inf)) then
          ur_r = epsil*v_inf
          durr_r=0.0d0
          duru_r=0.0d0
          durv_r=0.0d0
          durp_r=0.0d0
       else
          ur_r=qq
          durr_r=0.0d0
          duru_r=uold_r/qq
          durv_r=vold_r/qq
          durp_r=0.0d0
       endif
c---------------------------
       if(ur_r .ge. arigh) then
          ur_r=arigh
          durr_r=-arigh/(2.0d0*rold_r)
          duru_r=0.0d0
          durv_r=0.0d0
          durp_r=ga/(2.0d0*arigh*rold_r)
       endif
c------------------------------------------------------------------
c-------------------------------------------------------------------
c  Reference velocity at the interface is taken as an average
c  of the reference velocities of the neighbouring cells
c-------------------------------------------------------------------
       urm=0.5d0*(ur_l+ur_r)
       durr_l=0.5d0*durr_l
       duru_l=0.5d0*duru_l
       durv_l=0.5d0*durv_l
       durp_l=0.5d0*durp_l
c-------------------------
       durr_r=0.5d0*durr_r
       duru_r=0.5d0*duru_r
       durv_r=0.5d0*durv_r
       durp_r=0.5d0*durp_r
c-------------------------------------------------------------------
c Computation of the speed of sound and its derivatives;
c numerical speed of sound at the interface is taken as an average
c of the speeds of sounds of the neighbouring cells
c-------------------------------------------------------------------
       am=0.5d0*(aleft+arigh)
c-------------------------------------------------------------------
       if(abs(urm/am-1.0d0).le.0.000001d0) then
         coef=0.0d0
       else
         coef=1.0d0
       endif
c-------------------------------------------------------------------
       damr_r=-arigh/(4.0d0*rold_r)
       damu_r=0.0d0
       damv_r=0.0d0
       damp_r=ga/(4.0d0*arigh*rold_r)
c-----------------------
       damr_l=-aleft/(4.0d0*rold_l)
       damu_l=0.0d0
       damv_l=0.0d0
       damp_l=ga/(4.0d0*aleft*rold_l)
c-------------------------------------------------------------------
c Computing numerical Mach number and its derivatives,
c see p.370, under (A1).
c-------------------------------------------------------------------
       un_l=uold_l*n_x+vold_l*n_y
       un_r=uold_r*n_x+vold_r*n_y
       ut_l=uold_l*t_x+vold_l*t_y
       ut_r=uold_r*t_x+vold_r*t_y
c-------------------------------------------------------------------
       ml=un_l/am
       mr=un_r/am
       mhalf=0.5d0*(un_l+un_r)/am
c---------------------
       top=0.5d0*(un_l+un_r)/(am*am)
       dmhr_l=-top*damr_l
       dmhu_l=n_x/2.0d0/am-top*damu_l
       dmhv_l=n_y/2.0d0/am-top*damv_l
       dmhp_l=-top*damp_l
c---------------------
       dmhr_r=-top*damr_r
       dmhu_r=n_x/2.0d0/am-top*damu_r
       dmhv_r=n_y/2.0d0/am-top*damv_r
       dmhp_r=-top*damp_r
c--------------------------------
       mhalfr=urm/am
c---------------------
       top=urm/(am*am)
       dmfr_l=durr_l/am-top*damr_l
       dmfu_l=duru_l/am-top*damu_l
       dmfv_l=durv_l/am-top*damv_l
       dmfp_l=durp_l/am-top*damp_l
c---------------------
       dmfr_r=durr_r/am-top*damr_r
       dmfu_r=duru_r/am-top*damu_r
       dmfv_r=durv_r/am-top*damv_r
       dmfp_r=durp_r/am-top*damp_r
c-------------------------------------------------------------------
c Scaling function for the speed of sound and its derivatives
c-------------------------------------------------------------------
       top=(1.0d0-mhalfr*mhalfr)*(1.0d0-mhalfr*mhalfr)
       top=top*mhalf*mhalf+4.0d0*mhalfr*mhalfr
       bot=1.0d0+mhalfr*mhalfr
       if(abs(canc-0.0d0).lt.0.000001d0) then
         fmid=1.0d0
       else
         fmid=sqrt(top)/bot
       endif
c--------------------------
       temph=-4.0d0*(1.0d0-mhalfr*mhalfr)*mhalfr
       temph=temph*mhalf*mhalf+8.0d0*mhalfr
       dfm_mf=temph/(sqrt(top)*2.0d0*bot)
       dfm_mf=dfm_mf-sqrt(top)*2.0d0*mhalfr/(bot*bot)
c--------------------------
       temph=2.0d0*(1.0d0-mhalfr*mhalfr)*(1.0d0-mhalfr*mhalfr)*mhalf
       dfm_mh=temph/(2.0d0*bot*sqrt(top))
c--------------------------
       dfmr_l=dfm_mf*dmfr_l+dfm_mh*dmhr_l
       dfmu_l=dfm_mf*dmfu_l+dfm_mh*dmhu_l
       dfmv_l=dfm_mf*dmfv_l+dfm_mh*dmhv_l
       dfmp_l=dfm_mf*dmfp_l+dfm_mh*dmhp_l
c--------------------------
       dfmr_r=dfm_mf*dmfr_r+dfm_mh*dmhr_r
       dfmu_r=dfm_mf*dmfu_r+dfm_mh*dmhu_r
       dfmv_r=dfm_mf*dmfv_r+dfm_mh*dmhv_r
       dfmp_r=dfm_mf*dmfp_r+dfm_mh*dmhp_r
c--------------------------
       amw=fmid*am
       mlw=un_l/amw
       mrw=un_r/amw
c--------------------------
       damr_l=canc*coef*dfmr_l*am+fmid*damr_l
       damu_l=canc*coef*dfmu_l*am+fmid*damu_l
       damv_l=canc*coef*dfmv_l*am+fmid*damv_l
       damp_l=canc*coef*dfmp_l*am+fmid*damp_l
c--------------------------
       damr_r=canc*coef*dfmr_r*am+fmid*damr_r
       damu_r=canc*coef*dfmu_r*am+fmid*damu_r
       damv_r=canc*coef*dfmv_r*am+fmid*damv_r
       damp_r=canc*coef*dfmp_r*am+fmid*damp_r
c-/-/-/-/-/-/-/-/-/-/-/-/-/-/-/-/-/
       am=amw
c-------------------------------------------------------------------
c  Redefinition of the numerical mach numbers
c-------------------------------------------------------------------
       if(abs(canc-0.0d0).lt.0.000001d0) then
         top=2.0d0
         bot=0.0d0
       else
         top=1.0d0+mhalfr*mhalfr
         bot=1.0d0-mhalfr*mhalfr
       endif
       ml=0.5d0*(top*mlw+bot*mrw)
       mr=0.5d0*(top*mrw+bot*mlw)
c-------------------------------------------------------------------
c  Mplus and Mmin are calligraphic lettes M+ and M- from the paper,
c  see (19a) and (19b), p.367.
c-------------------------------------------------------------------
       if(abs(ml) .ge. 1.0d0) then
         Mplus=(ml+abs(ml))/2.0d0
       else
         Mplus=(ml+1.0d0)*(ml+1.0d0)/4.0d0
         Mplus=Mplus+beta*(ml*ml-1.0d0)*(ml*ml-1.0d0)
       endif
       Mplus1=(ml+abs(ml))/2.0d0
c-------------------------------------------------------------------
       if(abs(mr) .ge. 1.0d0) then
         Mmin=(mr-abs(mr))/2.0d0
       else
         Mmin=-(mr-1.0d0)*(mr-1.0d0)/4.0d0
         Mmin=Mmin-beta*(mr*mr-1.0d0)*(mr*mr-1.0d0)
       endif
       Mmin1=(mr-abs(mr))/2.0d0
c-------------------------------------------------------------------
c Derivatives of ml and mr with respect to both sets of primitive
c variables: left  and right.
c-------------------------------------------------------------------
       temp_l=-un_l/(am*am)
       temp_r=-un_r/(am*am)
c--------
       dmlr_l=temp_l*damr_l
       dmlr_r=temp_l*damr_r
       dmrr_l=temp_r*damr_l
       dmrr_r=temp_r*damr_r
c--------
       dmlu_l=n_x/am+temp_l*damu_l
       dmlu_r=temp_l*damu_r
       dmru_l=temp_r*damu_l
       dmru_r=n_x/am+temp_r*damu_r
c--------
       dmlv_l=n_y/am+temp_l*damv_l
       dmlv_r=temp_l*damv_r
       dmrv_l=temp_r*damv_l
       dmrv_r=n_y/am+temp_r*damv_r
c--------
       dmlp_l=temp_l*damp_l
       dmlp_r=temp_l*damp_r
       dmrp_l=temp_r*damp_l
       dmrp_r=temp_r*damp_r
c-----------------------------
       sr_l=dmlr_l
       sr_r=dmlr_r
       su_l=dmlu_l
       su_r=dmlu_r
       sv_l=dmlv_l
       sv_r=dmlv_r
       sp_l=dmlp_l
       sp_r=dmlp_r
c-----------------------------------------------------------------
c   Redefinition of the derivatives of the ml & mr
c-----------------------------------------------------------------
       temp_l=(mlw-mrw)*mhalfr
       temp_r=-temp_l
c--------
       dmlr_l=0.5d0*(top*dmlr_l+bot*dmrr_l)+canc*coef*temp_l*dmfr_l
       dmlu_l=0.5d0*(top*dmlu_l+bot*dmru_l)+canc*coef*temp_l*dmfu_l
       dmlv_l=0.5d0*(top*dmlv_l+bot*dmrv_l)+canc*coef*temp_l*dmfv_l
       dmlp_l=0.5d0*(top*dmlp_l+bot*dmrp_l)+canc*coef*temp_l*dmfp_l
c--------
       dmlr_r=0.5d0*(top*dmlr_r+bot*dmrr_r)+canc*coef*temp_l*dmfr_r
       dmlu_r=0.5d0*(top*dmlu_r+bot*dmru_r)+canc*coef*temp_l*dmfu_r
       dmlv_r=0.5d0*(top*dmlv_r+bot*dmrv_r)+canc*coef*temp_l*dmfv_r
       dmlp_r=0.5d0*(top*dmlp_r+bot*dmrp_r)+canc*coef*temp_l*dmfp_r
c--------
       dmrr_l=0.5d0*(top*dmrr_l+bot*sr_l)+canc*coef*temp_r*dmfr_l
       dmru_l=0.5d0*(top*dmru_l+bot*su_l)+canc*coef*temp_r*dmfu_l
       dmrv_l=0.5d0*(top*dmrv_l+bot*sv_l)+canc*coef*temp_r*dmfv_l
       dmrp_l=0.5d0*(top*dmrp_l+bot*sp_l)+canc*coef*temp_r*dmfp_l
c--------
       dmrr_r=0.5d0*(top*dmrr_r+bot*sr_r)+canc*coef*temp_r*dmfr_r
       dmru_r=0.5d0*(top*dmru_r+bot*su_r)+canc*coef*temp_r*dmfu_r
       dmrv_r=0.5d0*(top*dmrv_r+bot*sv_r)+canc*coef*temp_r*dmfv_r
       dmrp_r=0.5d0*(top*dmrp_r+bot*sp_r)+canc*coef*temp_r*dmfp_r
c-----------------------------------------------------------
c mmid is m_{1/2} (notation as in the paper, see (13),p.366)
c-----------------------------------------------------------
       mmid=Mplus+Mmin
c-----------------------------------------------------------
c Computing the derivatives of M+ and M-
c-----------------------------------------------------------
       if(ml .ge. 1.0d0) then
         dMpr_l=dmlr_l
         dMpu_l=dmlu_l
         dMpv_l=dmlv_l
         dMpp_l=dmlp_l
c--------------------
         dMpr_r=dmlr_r
         dMpu_r=dmlu_r
         dMpv_r=dmlv_r
         dMpp_r=dmlp_r
       else
        if((ml .gt. -1.0d0) .and. (ml .lt. 1.0d0)) then
         temph=(ml+1.0d0)/2.0d0
         dMpr_l=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlr_l
         dMpu_l=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlu_l
         dMpv_l=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlv_l
         dMpp_l=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlp_l
c--------------------
           dMpr_r=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlr_r
           dMpu_r=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlu_r
           dMpv_r=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlv_r
           dMpp_r=(temph+4.0d0*beta*ml*(ml*ml-1.0d0))*dmlp_r
         else
           dMpr_l=0.0d0
           dMpu_l=0.0d0
           dMpv_l=0.0d0
           dMpp_l=0.0d0
c---------------------
           dMpr_r=0.0d0
           dMpu_r=0.0d0
           dMpv_r=0.0d0
           dMpp_r=0.0d0
         endif
       endif
c-----------------------------------------------------------
c   addition of low Mach number
c-----------------------------------------------------------
       if(ml .ge. 0.0d0) then
         m1pr_l=dmlr_l
         m1pu_l=dmlu_l
         m1pv_l=dmlv_l
         m1pp_l=dmlp_l
c---------------------
         m1pr_r=dmlr_r
         m1pu_r=dmlu_r
         m1pv_r=dmlv_r
         m1pp_r=dmlp_r
       else
         m1pr_l=0.0d0
         m1pu_l=0.0d0
         m1pv_l=0.0d0
         m1pp_l=0.0d0
c--------------------
         m1pr_r=0.0d0
         m1pu_r=0.0d0
         m1pv_r=0.0d0
         m1pp_r=0.0d0
       endif
c-----------------------------------------------------------
       if(mr .ge. 1.0d0) then
         dMmr_l=0.0d0
         dMmu_l=0.0d0
         dMmv_l=0.0d0
         dMmp_l=0.0d0
c---------------------
         dMmr_r=0.0d0
         dMmu_r=0.0d0
         dMmv_r=0.0d0
         dMmp_r=0.0d0
       else
         if((mr .gt. -1.0d0) .and. (mr .lt. 1.0d0)) then
           temph=(-mr+1.0d0)/2.0d0
           dMmr_l=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrr_l
           dMmu_l=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmru_l
           dMmv_l=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrv_l
           dMmp_l=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrp_l
c--------------------
           dMmr_r=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrr_r
           dMmu_r=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmru_r
           dMmv_r=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrv_r
           dMmp_r=(temph-4.0d0*beta*mr*(mr*mr-1.0d0))*dmrp_r
         else
           dMmr_l=dmrr_l
           dMmu_l=dmru_l
           dMmv_l=dmrv_l
           dMmp_l=dmrp_l
c--------------------
           dMmr_r=dmrr_r
           dMmu_r=dmru_r
           dMmv_r=dmrv_r
           dMmp_r=dmrp_r
         endif
       endif
c-----------------------------------------------------------
c   addition of low Mach number
c-----------------------------------------------------------
       if(mr .le. 0.0d0) then
         m1mr_l=dmrr_l
         m1mu_l=dmru_l
         m1mv_l=dmrv_l
         m1mp_l=dmrp_l
c---------------------
         m1mr_r=dmrr_r
         m1mu_r=dmru_r
         m1mv_r=dmrv_r
         m1mp_r=dmrp_r
       else
         m1mr_l=0.0d0
         m1mu_l=0.0d0
         m1mv_l=0.0d0
         m1mp_l=0.0d0
c--------------------
         m1mr_r=0.0d0
         m1mu_r=0.0d0
         m1mv_r=0.0d0
         m1mp_r=0.0d0
       endif
c-----------------------------------------------------------------
c computing the derivatives of m_{1/2} (notation as in the paper)
c-----------------------------------------------------------------
       dmir_l=dMpr_l+dMmr_l
       dmir_r=dMpr_r+dMmr_r
c-------------
       dmiu_l=dMpu_l+dMmu_l
       dmiu_r=dMpu_r+dMmu_r
c-------------
       dmiv_l=dMpv_l+dMmv_l
       dmiv_r=dMpv_r+dMmv_r
c-------------
       dmip_l=dMpp_l+dMmp_l
       dmip_r=dMpp_r+dMmp_r
c----------------------------------------------------------------
c computing the main convective variables and their derivatives
c mpl_m is m^{+}_{1/2} (paper's notation) and
c mmin_m  is m^{-}_{1/2} (paper's notation), see (A2) on p.370.
c----------------------------------------------------------------
       termp=(Mmin1-Mmin+Mplus-Mplus1)*(1.0d0/(mhalfr*mhalfr)-1.0d0)
       termp=termp*(pold_l-pold_r)/(pold_l/rold_l+pold_r/rold_r)
c-------------------------------------------------------------
c    derivatives of the termp
c-------------------------------------------------------------
       top=(Mmin1-Mmin+Mplus-Mplus1)
       bots=1.0d0/(pold_l/rold_l+pold_r/rold_r)
       bot=(pold_l-pold_r)*bots
       temph=1.0d0/(mhalfr*mhalfr)-1.0d0
c---------------------------
       tmpr_l=(m1mr_l-dMmr_l+dMpr_l-m1pr_l)*bot*temph
       tmpr_l=tmpr_l-2.0d0*bot*top*dmfr_l/(mhalfr*mhalfr*mhalfr)
       tmpr_l=tmpr_l+temph*top*bot*bots*(pold_l/rold_l/rold_l)
c---------------------------
       tmpu_l=(m1mu_l-dMmu_l+dMpu_l-m1pu_l)*bot*temph
       tmpu_l=tmpu_l-2.0d0*bot*top*dmfu_l/(mhalfr*mhalfr*mhalfr)
c---------------------------
       tmpv_l=(m1mv_l-dMmv_l+dMpv_l-m1pv_l)*bot*temph
       tmpv_l=tmpv_l-2.0d0*bot*top*dmfv_l/(mhalfr*mhalfr*mhalfr)
c---------------------------
       tmpp_l=(m1mp_l-dMmp_l+dMpp_l-m1pp_l)*bot*temph
       tmpp_l=tmpp_l-2.0d0*bot*top*dmfp_l/(mhalfr*mhalfr*mhalfr)
       tmpp_l=tmpp_l+temph*top*bots*(1.0d0-bot/rold_l)
c------------rrrrrrrr-------
c------------rrrrrrrr-------
       tmpr_r=(m1mr_r-dMmr_r+dMpr_r-m1pr_r)*bot*temph
       tmpr_r=tmpr_r-2.0d0*bot*top*dmfr_r/(mhalfr*mhalfr*mhalfr)
       tmpr_r=tmpr_r+temph*top*bot*bots*(pold_r/rold_r/rold_r)
c---------------------------
       tmpu_r=(m1mu_r-dMmu_r+dMpu_r-m1pu_r)*bot*temph
       tmpu_r=tmpu_r-2.0d0*bot*top*dmfu_r/(mhalfr*mhalfr*mhalfr)
c---------------------------
       tmpv_r=(m1mv_r-dMmv_r+dMpv_r-m1pv_r)*bot*temph
       tmpv_r=tmpv_r-2.0d0*bot*top*dmfv_r/(mhalfr*mhalfr*mhalfr)
c---------------------------
       tmpp_r=(m1mp_r-dMmp_r+dMpp_r-m1pp_r)*bot*temph
       tmpp_r=tmpp_r-2.0d0*bot*top*dmfp_r/(mhalfr*mhalfr*mhalfr)
       tmpp_r=tmpp_r-temph*top*bots*(1.0d0+bot/rold_r)
c-------------------------------------------------------------
       if(mmid .ge. 0.0d0) then
         mpl_m = mmid
         d2mr_l=dmir_l
         d2mu_l=dmiu_l
         d2mv_l=dmiv_l
         d2mp_l=dmip_l
c------------
         d2mr_r=dmir_r
         d2mu_r=dmiu_r
         d2mv_r=dmiv_r
         d2mp_r=dmip_r
c------------
       else
         mpl_m = 0.0d0
         d2mr_l=0.0d0
         d2mu_l=0.0d0
         d2mv_l=0.0d0
         d2mp_l=0.0d0
c------------
         d2mr_r=0.0d0
         d2mu_r=0.0d0
         d2mv_r=0.0d0
         d2mp_r=0.0d0
       endif
c---------------------------------------------
cc       derivatives for the term termm
cc------------------------------------------------------------------
       if(mmid .le. 0.0d0) then
         mmin_m = mmid
         d3mr_l=dmir_l
         d3mu_l=dmiu_l
         d3mv_l=dmiv_l
         d3mp_l=dmip_l
c------------
         d3mr_r=dmir_r
         d3mu_r=dmiu_r
         d3mv_r=dmiv_r
         d3mp_r=dmip_r
c------------
       else
         mmin_m = 0.0d0
         d3mr_l=0.0d0
         d3mu_l=0.0d0
         d3mv_l=0.0d0
         d3mp_l=0.0d0
c------------
         d3mr_r=0.0d0
         d3mu_r=0.0d0
         d3mv_r=0.0d0
         d3mp_r=0.0d0
       endif
c---------------------------------------------------------------
c Computing the calligraphic P+ and P- with their derivatives,
c see (21a) & (21b) on p.368.
c---------------------------------------------------------------
       if(ml .ge. 1.0d0) then
         Pplus = 1.0d0
       else
         if((ml .gt. -1.0d0) .and. (ml .lt. 1.0d0)) then
           Pplus=(ml+1.0d0)*(ml+1.0d0)*(2.0d0-ml)/4.0d0
           Pplus=Pplus+alpha*ml*(ml*ml-1.0d0)*(ml*ml-1.0d0)
         else
           Pplus = 0.0d0
         endif
       endif
c---------------------------------------------------------------
       if(mr .ge. 1.0d0) then
         Pmin = 0.0d0
       else
         if((mr .gt. -1.0d0) .and. (mr .lt. 1.0d0)) then
           Pmin=(mr-1.0d0)*(mr-1.0d0)*(2.0d0+mr)/4.0d0
           Pmin=Pmin-alpha*mr*(mr*mr-1.0d0)*(mr*mr-1.0d0)
         else
           Pmin = 1.0d0
         endif
       endif
c---------------------------------------------------------------
       brac_l=(ml+1.0d0)*(2.0d0-ml)/2.0d0-(ml+1.0d0)*(ml+1.0d0)/4.0d0
       brac_l=brac_l+alpha*(ml*ml-1.0d0)*(ml*ml-1.0d0)
       brac_l=brac_l+4.0d0*alpha*ml*ml*(ml*ml-1.0d0)
c--------------
       brac_r=(mr-1.0d0)*(2.0d0+mr)/2.0d0+(mr-1.0d0)*(mr-1.0d0)/4.0d0
       brac_r=brac_r-alpha*(mr*mr-1.0d0)*(mr*mr-1.0d0)
       brac_r=brac_r-4.0d0*alpha*mr*mr*(mr*mr-1.0d0)
c---------------------------------------------------------------
       if((ml .gt. -1.0d0) .and. (ml .lt. 1.0d0)) then
         dPpr_l=brac_l*dmlr_l
         dPpr_r=brac_l*dmlr_r
c------------
         dPpu_l=brac_l*dmlu_l
         dPpu_r=brac_l*dmlu_r
c------------
         dPpv_l=brac_l*dmlv_l
         dPpv_r=brac_l*dmlv_r
c------------
         dPpp_l=brac_l*dmlp_l
         dPpp_r=brac_l*dmlp_r
c------------
       else
         dPpr_l=0.0d0
         dPpr_r=0.0d0
c-----------
         dPpu_l=0.0d0
         dPpu_r=0.0d0
c-----------
         dPpv_l=0.0d0
         dPpv_r=0.0d0
c-----------
         dPpp_l=0.0d0
         dPpp_r=0.0d0
c-----------
       endif
c---------------------------------------------------------------
       if((mr .gt. -1.0d0) .and. (mr .lt. 1.0d0)) then
         dPmr_l=brac_r*dmrr_l
         dPmr_r=brac_r*dmrr_r
c------------
         dPmu_l=brac_r*dmru_l
         dPmu_r=brac_r*dmru_r
c------------
         dPmv_l=brac_r*dmrv_l
         dPmv_r=brac_r*dmrv_r
c------------
         dPmp_l=brac_r*dmrp_l
         dPmp_r=brac_r*dmrp_r
c------------
       else
         dPmr_l=0.0d0
         dPmr_r=0.0d0
c-----------
         dPmu_l=0.0d0
         dPmu_r=0.0d0
c-----------
         dPmv_l=0.0d0
         dPmv_r=0.0d0
c-----------
         dPmp_l=0.0d0
         dPmp_r=0.0d0
c-----------
       endif
c-------------------------------------------------------------------
c computing pmid -- p_{1/2} and its derivatives, see (20b), p.367.
c-------------------------------------------------------------------
       dpir_l=dPpr_l*pold_l+dPmr_l*pold_r
       dpiu_l=dPpu_l*pold_l+dPmu_l*pold_r
       dpiv_l=dPpv_l*pold_l+dPmv_l*pold_r
       dpip_l=dPpp_l*pold_l+Pplus+dPmp_l*pold_r
c----------------------------
       dpir_r=dPpr_r*pold_l+dPmr_r*pold_r
       dpiu_r=dPpu_r*pold_l+dPmu_r*pold_r
       dpiv_r=dPpv_r*pold_l+dPmv_r*pold_r
       dpip_r=dPpp_r*pold_l+Pmin+dPmp_r*pold_r
c---------------------------------------------------------------------
c  Computation of the mass flux (rho * u)_1/2
c---------------------------------------------------------------
       rum=am*(mpl_m*rold_l+mmin_m*rold_r)+canc*am*termp
c-------------------------------------------------------
       rumr_l=damr_l*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mr_l*rold_l+mpl_m+d3mr_l*rold_r)
       rumr_l=rumr_l+canc*coef*(damr_l*termp+am*tmpr_l)
       rumu_l=damu_l*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mu_l*rold_l+d3mu_l*rold_r)
       rumu_l=rumu_l+canc*coef*(damu_l*termp+am*tmpu_l)
       rumv_l=damv_l*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mv_l*rold_l+d3mv_l*rold_r)
       rumv_l=rumv_l+canc*coef*(damv_l*termp+am*tmpv_l)
       rump_l=damp_l*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mp_l*rold_l+d3mp_l*rold_r)
       rump_l=rump_l+canc*coef*(damp_l*termp+am*tmpp_l)
c-------------------------------------------------
       rumr_r=damr_r*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mr_r*rold_l+mmin_m+d3mr_r*rold_r)
       rumr_r=rumr_r+canc*coef*(damr_r*termp+am*tmpr_r)
       rumu_r=damu_r*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mu_r*rold_l+d3mu_r*rold_r)
       rumu_r=rumu_r+canc*coef*(damu_r*termp+am*tmpu_r)
       rumv_r=damv_r*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mv_r*rold_l+d3mv_r*rold_r)
       rumv_r=rumv_r+canc*coef*(damv_r*termp+am*tmpv_r)
       rump_r=damp_r*(mpl_m*rold_l+mmin_m*rold_r)+
     &        am*(d2mp_r*rold_l+d3mp_r*rold_r)
       rump_r=rump_r+canc*coef*(damp_r*termp+am*tmpp_r)
c---------------------------------------------------------------------
c computing JACOBIAN as a derivative of the numerical flux function
c with respect to the primitive variables.
c Notation: jl(i,j) --- is the derivative of the i-component of the
c           flux function with respect to the j-component of the
c           vector of primitive variables of the left state.
c           jr(i,j) --- is the derivative of the i-component of the
c           flux function with respect to the j-component of the
c           vector of primitive variables of the right state.
c---------------------------------------------------------------------
       jl(1,1)=rumr_l
       jl(1,2)=rumu_l
       jl(1,3)=rumv_l
       jl(1,4)=rump_l
c------------------------------------
       jr(1,1)=rumr_r
       jr(1,2)=rumu_r
       jr(1,3)=rumv_r
       jr(1,4)=rump_r
c------------------------------------
c---------------------------------------------------------
       if(rum .ge. 0.0d0) then
         br3=rumr_l*un_l
         br4=rumr_l*ut_l
       else
         br3=rumr_l*un_r
         br4=rumr_l*ut_r
       endif
       jl(2,1)=n_x*(br3+dpir_l)+br4*t_x
       jl(3,1)=n_y*(br3+dpir_l)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rumu_l*un_l+rum*n_x
         br4=rumu_l*ut_l+rum*t_x
       else
         br3=rumu_l*un_r
         br4=rumu_l*ut_r
       endif
       jl(2,2)=n_x*(br3+dpiu_l)+br4*t_x
       jl(3,2)=n_y*(br3+dpiu_l)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rumv_l*un_l+rum*n_y
         br4=rumv_l*ut_l+rum*t_y
       else
         br3=rumv_l*un_r
         br4=rumv_l*ut_r
       endif
       jl(2,3)=n_x*(br3+dpiv_l)+br4*t_x
       jl(3,3)=n_y*(br3+dpiv_l)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rump_l*un_l
         br4=rump_l*ut_l
       else
         br3=rump_l*un_r
         br4=rump_l*ut_r
       endif
       jl(2,4)=n_x*(br3+dpip_l)+br4*t_x
       jl(3,4)=n_y*(br3+dpip_l)+br4*t_y
c-------------------------------------------------------------
c-------------------------------------------------------------
       if(rum .ge. 0.0d0) then
         br3=rumr_r*un_l
         br4=rumr_r*ut_l
       else
         br3=rumr_r*un_r
         br4=rumr_r*ut_r
       endif
       jr(2,1)=n_x*(br3+dpir_r)+br4*t_x
       jr(3,1)=n_y*(br3+dpir_r)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rumu_r*un_l
         br4=rumu_r*ut_l
       else
         br3=rumu_r*un_r+rum*n_x
         br4=rumu_r*ut_r+rum*t_x
       endif
       jr(2,2)=n_x*(br3+dpiu_r)+br4*t_x
       jr(3,2)=n_y*(br3+dpiu_r)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rumv_r*un_l
         br4=rumv_r*ut_l
       else
         br3=rumv_r*un_r+rum*n_y
         br4=rumv_r*ut_r+rum*t_y
       endif
       jr(2,3)=n_x*(br3+dpiv_r)+br4*t_x
       jr(3,3)=n_y*(br3+dpiv_r)+br4*t_y
c-------------------
       if(rum .ge. 0.0d0) then
         br3=rump_r*un_l
         br4=rump_r*ut_l
       else
         br3=rump_r*un_r
         br4=rump_r*ut_r
       endif
       jr(2,4)=n_x*(br3+dpip_r)+br4*t_x
       jr(3,4)=n_y*(br3+dpip_r)+br4*t_y
c-------------------------------------------------------------
c  ------  f44444444444444444444444444444 ---------
c-------------------------------------------------------------
       hr_l=(uold_l*uold_l+vold_l*vold_l)/2.0d0+ga*pold_l/gm1/rold_l
       hr_r=(uold_r*uold_r+vold_r*vold_r)/2.0d0+ga*pold_r/gm1/rold_r
c-------------------------------------------------------------
       if(rum .ge. 0.0d0) then
         br1=rumr_l*hr_l-rum*ga*pold_l/gm1/rold_l/rold_l
       else
         br1=rumr_l*hr_r
       endif
       jl(4,1)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rumu_l*hr_l+rum*uold_l
       else
         br1=rumu_l*hr_r
       endif
       jl(4,2)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rumv_l*hr_l+rum*vold_l
       else
         br1=rumv_l*hr_r
       endif
       jl(4,3)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rump_l*hr_l+rum*ga/gm1/rold_l
       else
         br1=rump_l*hr_r
       endif
       jl(4,4)=br1
c----------------------------------------------------------
       if(rum .ge. 0.0d0) then
         br1=rumr_r*hr_l
       else
         br1=rumr_r*hr_r-rum*ga*pold_r/gm1/rold_r/rold_r
       endif
       jr(4,1)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rumu_r*hr_l
       else
         br1=rumu_r*hr_r+rum*uold_r
       endif
       jr(4,2)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rumv_r*hr_l
       else
         br1=rumv_r*hr_r+rum*vold_r
       endif
       jr(4,3)=br1
c---------------------
       if(rum .ge. 0.0d0) then
         br1=rump_r*hr_l
       else
         br1=rump_r*hr_r+rum*ga/gm1/rold_r
       endif
       jr(4,4)=br1
c-------------------------------------------------------------
c  matrix wl(i,j) represents the derivative of the i-component
c  of the vector of primitive variables of the left state with
c  respect to the j-component of the vector of the conservative
c  variables of the left state.
c
c  Here: (rho, u, v, p) - vector of primitive variables;
c        (rho, rho u, rho v, rho e) -- conservative variables.
c-------------------------------------------------------------
       wl(1,1)=1.0d0
       wl(1,2)=0.0d0
       wl(1,3)=0.0d0
       wl(1,4)=0.0d0
c------------------------------
       wl(2,1)=-uold_l/rold_l
       wl(2,2)=1.0d0/rold_l
       wl(2,3)=0.0d0
       wl(2,4)=0.0d0
c------------------------------
       wl(3,1)=-vold_l/rold_l
       wl(3,2)=0.0d0
       wl(3,3)=1.0d0/rold_l
       wl(3,4)=0.0d0
c------------------------------
       wl(4,1)=gm1*(uold_l*uold_l+vold_l*vold_l)/2.0d0
       wl(4,2)=-uold_l*gm1
       wl(4,3)=-vold_l*gm1
       wl(4,4)=gm1
c------------------------------
c------------------------------
       wr(1,1)=1.0d0
       wr(1,2)=0.0d0
       wr(1,3)=0.0d0
       wr(1,4)=0.0d0
c------------------------------
       wr(2,1)=-uold_r/rold_r
       wr(2,2)=1.0d0/rold_r
       wr(2,3)=0.0d0
       wr(2,4)=0.0d0
c------------------------------
       wr(3,1)=-vold_r/rold_r
       wr(3,2)=0.0d0
       wr(3,3)=1.0d0/rold_r
       wr(3,4)=0.0d0
c------------------------------
       wr(4,1)=gm1*(uold_r*uold_r+vold_r*vold_r)/2.0d0
       wr(4,2)=-uold_r*gm1
       wr(4,3)=-vold_r*gm1
       wr(4,4)=gm1
c----------------------------------------------
       do 1 i=1,4
        do 2 j=1,4
         jtl(i,j)=0.0d0
         jtr(i,j)=0.0d0
         do 3 k=1,4
          jtl(i,j)=jtl(i,j)+jl(i,k)*wl(k,j)
          jtr(i,j)=jtr(i,j)+jr(i,k)*wr(k,j)
 3       continue
 2      continue
 1     continue
c----------------------------------------------------------------------
       return
       end

































