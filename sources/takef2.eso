C TAKEF2    SOURCE    CHAT      05/01/13    03:31:45     5004
C TAKEFO    SOURCE    AM        95/12/27    22:09:24     1962
      SUBROUTINE TAKEF2(SIG0,NSTRS,DEPST,VAR0,XMAT,NCOMAT,XCAR,TRAC,
     .                 NCOURB,SIGF,VARF,DEFP,KERRE)
C-----------------------------------------------------------------------
C
C    ROUTINE DE CALCUL DES CONTRAINTES EN CISAILLEMENT
C    CAS DU TAKEDA TRILINEAIRE SUR L'EFFORT TRANCHANT
C
C  ENTREE
C    SIG0    CONTRAINTES INITIALES
C    DEPST   INCREMENT OF GENERALIZED STRAIN
C    XMAT    PROPRIETES DU MATERIAU
C    XCAR    CARACTERISTIQUE DU MATERIAU
C    VAR0    VARFABLES INTERNES AU DEBUT
C    TRAC    COURBE DE CHARGEMENT
C  SORTIE
C    SIGF    CONTRAINTES A LA FIN
C    VARF    VARFABLES INTERNES A LA FIN
C    DEFP    DEFORMATION PLASTIQUE
C
C    materiau takeda PP 17/9/92, AVRIL 92 A.P. et P.P.
C                    PP renewed new ECOU 24/11/95
C-----------------------------------------------------------------------
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8(A-H,O-Z)
*
      DIMENSION SIG0(*),DEPST(*),VAR0(*),XMAT(*),XCAR(*),TRAC(*),
     .          SIGF(*),VARF(*) ,DEFP(*)
C
C-----------------------------------------------------------------------
C  CARACTERISTIQUES
C  ================
C
C      SECT = XCAR(1)   SECTION
C      INRZ = XCAR(2)   Z-INERTIAL BENDING
C      SRDY = XCAR(3)   TRANSVERSE SHEAR'S REDUCED SECTION
C-----------------------------------------------------------------------
C  VARIABLES INTERNES
C  ==================
C
C      IFR1   = VARF(1)
C      GATA   = VARF(2)
C      UEP1   = VARF(3)
C      PEP1   = VARF(4)
C      UEN1   = VARF(5)
C      PEN1   = VARF(6)
C      UCP1   = VARF(7)
C      UCN1   = VARF(8)
C      E1P1   = VARF(9)
C      E1N1   = VARF(10)
C      E2P1   = VARF(11)
C      E2N1   = VARF(12)
C      USP1   = VARF(13)
C      USN1   = VARF(14)
C      UTP1   = VARF(15)
C      PTP1   = VARF(16)
C      UTN1   = VARF(17)
C      PTN1   = VARF(18)
C      EP11   = VARF(19)
C      EN11   = VARF(20)
C      DES0   = VARF(21)
C-----------------------------------------------------------------------
C  ON RECUPERE LES PARAMETRES DU MODELE
C  ====================================
C
C      E0    = XMAT(1)
C      NU    = XMAT(2)
C      SFDP  = XMAT(6)
C      SFDN  = XMAT(7)
C      PINP  = XMAT(8)
C      PINN  = XMAT(9)
C      SRDP  = XMAT(10)
C      SRDN  = XMAT(11)
C
           IF(NCOURB.EQ.4)THEN
C
C LA COURBE EST SYMETRIQUE
C
       GAZ=TRAC(3)/TRAC(4)
       CMOP=TRAC(3)
       CMON=-CMOP
       YCUP=TRAC(6)
       YCUN=-YCUP
       YMOP=TRAC(5)
       YMON=-YMOP
       GAYP=(TRAC(7)-TRAC(5))/(TRAC(8)-TRAC(6))
       GAYN=GAYP
C
       XMAT(7)=XMAT(6)
       XMAT(9)=-XMAT(8)
       XMAT(11)=XMAT(10)
C
           ELSEIF(NCOURB.EQ.7)THEN
C
C LA COURBE EST NON SYMETRIQUE
C
       GAZ=(TRAC(9)-TRAC(5)) / (TRAC(10)-TRAC(6))
       CMOP=TRAC(9)
       CMON=TRAC(5)
       YCUP=TRAC(12)
       YCUN=TRAC(4)
       YMOP=TRAC(11)
       YMON=TRAC(3)
       GAYP=(TRAC(13)-TRAC(11))/(TRAC(14)-TRAC(12))
       GAYN=(TRAC(3) -TRAC(1) )/(TRAC(4) -TRAC(2))
C
           ELSE
C
C LA COURBE EST ERRONEE
C
       KERRE=31
       RETURN
C
           ENDIF
C
C-----------------------------------------------------------------------
C
C  TEST ON ELASTIC STIFFNESS
C
      XMODG=0.5D0 / (1.D0 + XMAT(2))
      GAZZ0   = XMAT(1)*XCAR(3)*XMODG
      IF(ABS(GAZ-GAZZ0)/GAZ.GT.1.D-6)THEN
        KERRE=36
        RETURN
      ENDIF
C
      XEFFZ   = SIG0(2)
C
C  APPELE AU PROGRAME QUI CALCULE L'EFFORT TRANCHANT ( XEFFZ )
C  CORRESPONDENT A L' INCREMENT LINEAIRE DE MOMENT - DP1 -
C  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
      DO 1 I=1,21
        VARF(I)=VAR0(I)
 1    CONTINUE
C
C  CONVERSION ENTIER
C
      IFR1=INT(VARF(1))
C
C     INITIALISATION DE IFR1 ET DE VARF(.,2)
C
      IFC1=0
      IF(IFR1.EQ.0) THEN
        IFR1 = 1
        VARF(2)=GAZZ0
      ENDIF
C
C  DEFORMATIONS GENERALISEES
C  =========================
C
C  INCREMENT DE MOMENT AUTOUR DE YY
C
C     DP1 = D_SIGMA / LINEAR_STIFFNESS * CURRENT STIFFNESS
C
C     DP1 = DSIGT(2) / GAZZ0 * VARF(2)
      DP1 = DEPST(2) * VARF(2)
C
C
C===================================================================
      CALL DDNSTH(IFR1,IFC1,DP1,XEFFZ,VARF(21),
     * VARF(2),GAZZ0,GAYP,GAYN,CMOP,CMON,YMOP,
     * YMON,YCUP,YCUN,XMAT(6),XMAT(7),XMAT(8),XMAT(9),
     * XMAT(10),XMAT(11),VARF(3),VARF(4),VARF(5),
     * VARF(6),VARF(7),VARF(8),VARF(9),
     * VARF(10),VARF(11),VARF(12),VARF(13),
     * VARF(14),VARF(15),VARF(16),VARF(17),
     * VARF(18),VARF(19),VARF(20))
C===================================================================
C
C  CONVERSION ENTIER
C
      VARF(1)=DBLE(IFR1)
C
C  REMPLISSAGE DU TABLEAU CONT (PARTIE ELASTIQUE)
C
      CISA=0.5D0*XMAT(1)/(1.D0+XMAT(2))
C     SIGF(1)=SIG0(1) + DSIGT(1)
      SIGF(1)=SIG0(1) + XMAT(1)*XCAR(1)*DEPST(1)
C     SIGF(3)=SIG0(3) + DSIGT(3)
      SIGF(3)=SIG0(3) + CISA*XCAR(2)*DEPST(3)
C
      SIGF(2)=XEFFZ
C
C  LES NOUVAUX VALEURS DES VARIABLES INTERNES
C
C ----- NOTE: VARF(2) CONTAINS THE CURRENT (GA) THAT WILL BE USED
C             TO COMPUTE THE TANGENT STIFFNESS MATRIX
C
      RETURN
      END



