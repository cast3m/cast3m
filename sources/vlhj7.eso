C VLHJ7     SOURCE    CHAT      05/01/13    04:08:07     5004
      SUBROUTINE VLHJ7(RHO,UX,UY,UZ,P,GAM,CNX,CNY,CNZ,DFRUN)
C
C************************************************************************
C
C PROJET            :  CASTEM 2000
C
C NOM               :  VLHJ7
C
C DESCRIPTION       :  Calcul du Jacobien du flux numerique VLH
C                      Contribution des faces sur les murs
C                      Voir VLHJ6
C
C LANGAGE           :  FORTRAN 77
C
C AUTEUR            :  A. BECCANTINI SFME/LTMF
C
C************************************************************************
C
C APPELES           :  MPFUNT
C
C************************************************************************
C
C**** Entrées:
C
C     RHO           =  densité
C
C     UX, UY, UZ    =  vitesse
C
C     P             =  pression
C
C     GAM           =  le "gamma"
C
C     CNX, CNY, CNZ =  normale à la face
C
C
C**** Sorties:
C
C     DFRUN(5)= derivés partielles de F_RHOUN par raport a RHO, UX,
C                UY, UZ, P
C
C************************************************************************
C
C HISTORIQUE (Anomalies et modifications éventuelles)
C
C HISTORIQUE :  Créé le 23.07.01
C
C************************************************************************
C
C N.B.: Toutes les variables sont declarées explicitement
C
      IMPLICIT INTEGER(I-N)
      REAL*8 RHO,UX,UY,UZ,P,GAM,CNX,CNY,CNZ
     &     ,DFRUN(5)
     &     ,UN,PSRHO,CSON2,CSON
     &     ,MACH,MNUM,DERMNU,PNUM,DERPNU
     &     ,CELL
C
      UN = UX * CNX + UY * CNY + UZ * CNZ
      PSRHO = P / RHO
      CSON2 = GAM * PSRHO
      CSON = SQRT(CSON2)
      MACH = UN / CSON
      CALL MPFUNT(MACH,MNUM,DERMNU,PNUM,DERPNU)
C
      CELL = DERMNU * UN + MNUM * CSON
      DFRUN(1) = CELL * UN + DERPNU * MACH * PSRHO
      CELL = (CELL *  RHO + P / CSON * DERPNU) * 2.0D0
      DFRUN(2) = CELL * CNX
      DFRUN(3) = CELL * CNY
      DFRUN(4) = CELL * CNZ
      DFRUN(5) = (CSON * MNUM - UN * DERMNU) * UN / PSRHO +
     &     2.0D0 * PNUM - MACH * DERPNU
C
      RETURN
      END




