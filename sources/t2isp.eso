C T2ISP     SOURCE    CHAT      05/01/13    03:30:11     5004
      SUBROUTINE T2ISP(BOITE,NUM,ITRI,NBE,COORD,NBN)
C     **********************************************************************
C     OBJET : TRIANGULATION D'UN CERCLE AVEC UN POINT INTERIEUR
C     EN ENTREE  :
C        BOITE   :  LA BOITE QUE DOIT CONTENIR LE MAILLAGE
C        NUM     :  NUMERO D'UN POINT A L'INTERIEUR DE LA BOITE
C     EN SORTIE  :
C        ITRI    :  LA DEFINITION DES TRIANGLES (LEURS POINTS)
C        NBE     :  LE NOMBRE D'ELEMENTS
C        COORD   :  TABLEAU DES COORDONNEES
C        NBN     :  NOMBRE DE POINTS DE LA TRIANGULATION INITIALE
C     **********************************************************************
      IMPLICIT INTEGER(I-N)
      IMPLICIT REAL*8 (A-H,O-Z)
C    Include contenant quelques constantes dont XPI :
-INC CCREEL
      REAL*8       BOITE(4),COORD(*)
      INTEGER    NUM, NBN,ITRI(*),NBE
C
      REAL*8     R, COEF
      INTEGER  I
      INTEGER  NBPT
      REAL*8     DTETA, TETA
C
C
      NBN = -1
      NBE = -1
      COEF = 2.D0
      NBPT = 50
C
      R = 0.0D0
      DO 10 I=1,2
         R = MAX(R,(BOITE(2+I) - BOITE(I)))
 10   CONTINUE
      IF( NBPT .LT. 3 )GOTO 999
      DTETA = (2D0 * XPI) / NBPT
      DO 20 I=1,NBPT
         TETA = (I-1) * DTETA
         COORD((I-1)*2+1) = COEF * R * COS(TETA)
         COORD((I-1)*2+2) = COEF * R * SIN(TETA)
 20   CONTINUE
      DO 30 I=1,NBPT
         ITRI((I-1)*3+1) = NUM
         ITRI((I-1)*3+2) = I
         ITRI((I-1)*3+3) = MOD(I,NBPT)+1
 30   CONTINUE
      NBE = NBPT
      NBN = NBPT
 999  END








