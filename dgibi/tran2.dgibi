* fichier :  tran2.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************
* CAS TEST DU 91/06/24   PROVENANCE : DELA

*******************************************
*    Test tran2.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;

*-----------------------------------------*
*                                         *
*              TEST TRAN2                 *
*                                         *
*--- Problème :                           *
*                                         *
* Problème de Valentin :                  *
*          thermique transitoire linéaire *
*                                         *
*                                         *
*--- Description de la géométrie :        *
*                                         *
*          A  -----  B                    *
*            |     |                      *
*            |     |          AB = 1.     *
*            |     |          BC = 10.    *
*            |     |          EC = 4.     *
*            |     |                      *
*            |     |                      *
*          F |-----| E                    *
*            |     |                      *
*          D |     | C                    *
*            X------    -----> R          *
*          R=0.                           *
*                                         *
*     Conductivité isotrope       : K = 1.*
*     Capacité du matériau        : C = 1.*
*                                  RHO= 1.*
*     Sur AB , température imposée: T = 1.*
*     Sur BC , convection forcée  : TE= 1.*
*                                   H= 10.*
*     Dans CDFE , source volumique: Q = 1.*
*     Ailleurs, flux imposé       : F = 0.*
*                                         *
*--- Calcul :                             *
*     Calcul axisymétrique                *
*     Recherche de la température maximale*
*     dans le cylindre épais par une      *
*     méthode pas à pas. L'évolution de la*
*     température est comparée avec la    *
*     solution analytique                 *
*                                         *
*--- Résultats :                          *
*                                         *
*     Comparer avec le cas-test THERMX    *
*     'PLI45'                             *
*     La température maximale doit tendre *
*     vers 1.3                            *
*                                         *
*-----------------------------------------*

TITR 'PROBLEME DE VALENTIN - THERMIQUE TRANSITOIRE LINEAIRE' ;
OPTI DIME 2 ELEM TRI6 MODE AXIS ;
TEMPS ;

*------- Création de la géométrie --------*
PD = 0.  0. ;
PC = 1.  0. ;
PB = 1. 10. ;
PA = 0. 10. ;
PE = 1.  4. ;
PF = 0.  4. ;
D1   = DROITE 4 PD PC ;
D2   = DROITE 5 PC PE ;
D3   = DROITE 4 PE PF ;
D4   = DROITE 5 PF PD ;
LIG1 = D1 ET D2 ET D3 ET D4 ;
D5   = DROITE 8 PF PA ;
D6   = DROITE 4 PA PB ;
D7   = DROITE 8 PB PE ;
LIG2 = D3 ET D5 ET D6 ET D7 ;
SURF1 = SURF LIG1 PLANE ;
SURF2 = SURF LIG2 PLANE ;
SURF3 = SURF1 ET SURF2 ;

SI(NEG GRAPH 'N');
   TRAC 'QUAL' SURF3 ;
FINSI;

*--- DONNEES DU PROBLEME DE THERMIQUE ----*

*------ 1 - Modélisation du maillage SURF3:
*           formulation + matériau
MOD1 = MODE SURF3 THERMIQUE ISOTROPE ;

*------ 2 - Création d'un CHAMELEM de
*           sous-type CARACTERISTIQUES
MAT1 = MATE MOD1 K 1. 'C' 1. RHO 1.  ;

*------ 3 - Condition de source volumique :
*           calcul du CHPOINT des flux
*           nodaux équivalents
S1   = SOURCE MOD1 1. SURF1          ;

*------ 4 - Modèle pour la formulation
*           convection et type d'élément
MOD2 = MODE (D2 ET D7) THERMIQUE  CONVECTION;
MOD4 = MOD1 ET MOD2;

*------ 5 - Création de CHAMELEM de
*           caractéristiques pour la
*           convection
MAT2 = MATE MOD2 H 10. ;
MAT4 = MAT1 ET MAT2    ;

*------ 6 - Blocage des températures
B1   = BLOQ T D6 ;

*------ 7 - Valeur de T imposée
E1   = DEPI B1 1.  ;

*------ 8 - Condition de flux nul : calcul
*           du CHPOINT de flux équivalent
FF1  = FLUX MOD1 0. D1               ;

*------ 9 - Assemblage des CHPOINTs de flux
*           équivalents
QQQ1 = S1 ET FF1  ;

*------ 10 - chargement
D69   = D7 'ET' D2;
CH_TE = 'MANU' 'CHPO' 1 D69 'T' 1.;

PP1    = PROG 0. PAS 0.05 NPAS 50 ;
PP2    = PROG 51. * 1.            ;
MEVOL1 = EVOL MANU 'TEMPS' PP1 'COEFF' PP2;
MEVOL2 = EVOL MANU 'TEMPS' (PROG 0. 2.) 'T'  (PROG 1. 1.);
CHA1   = CHAR 'Q   ' QQQ1 MEVOL1      ;
CHA2   = CHAR 'TIMP' E1 MEVOL2;
CHA3   = CHAR 'TECO' CH_TE MEVOL2;
CHA4   = CHA1 ET CHA2 ET CHA3;

* ----- 11 - remplissage pour PASAPAS
CHTER0 = MANU CHPO SURF3 1 T 1. ;

ETAB = TABL                           ;
ETAB.'TEMPERATURES'        = TABL     ;
ETAB.'TEMPERATURES' . 0    = CHTER0   ;
ETAB.'MODELE'              = MOD4     ;
ETAB.'CARACTERISTIQUES'    = MAT4     ;
ETAB.'BLOCAGES_THERMIQUES' = B1       ;
ETAB.'CHARGEMENT'          = CHA4     ;
ETAB.'PROCEDURE_THERMIQUE' = LINEAIRE ;
ETAB.'TEMPS_CALCULES'      = PROG 0.1 0.2 0.3 0.4 0.5 0.6 0.7 0.8 0.9 1. 1.1 1.2 1.3;


PASAPAS ETAB ;

LISTT = PROG ;
TEMP1 = PROG ;
REPETER BOUCSOR (DIME ETAB.'TEMPERATURES');
  ITERA = &BOUCSOR;
  LISTT = INSE LISTT ITERA (ETAB.'TEMPS'. (ITERA-1)) ;
  TEMP1 = INSE TEMP1 ITERA ( MAXI ( ETAB.'TEMPERATURES'. (ITERA-1) ));
FIN BOUCSOR ;

*----- 12 - SORTIE DES RESULTATS
EV1  = EVOL BLEU MANU 'TEMPS' LISTT  'TEMPERATURE' TEMP1 ;
TIME = PROG   0.1 0.200 0.300 0.400 0.500 0.600 0.700 0.800 0.900 1.000 1.100 1.200 1.300 ;
LTEM = PROG   1.076 1.135 1.179 1.212 1.236 1.253 1.266 1.276 1.282 1.287 1.290 1.293 1.295 ;
EV2  = EVOL ROUGE MANU TIME LTEM ;

SI(NEG GRAPH 'N');
  TITR 'EVOLUTION DE LA TEMPERATURE MAXIMALE';
  DESS (EV1 ET EV2) ;
FINSI;

TEMPS ;

* CODE DE FONCTIONNEMENT BASé SUR UN ECART
* MILIEU DE COURBE
* TEST RUDIMENTAIRE POUVANT ETRE OPTIMISé
TREF = 1.253;
ITER1A= 6;
TMAX = MAXI (ETAB.'TEMPERATURES'.ITER1A);
RESI = 100. * (ABS((TMAX-TREF)/TREF));
SI(RESI <EG 5.);
  ERRE 0;
SINO;
  ERRE 5;
FINSI;


FIN;








