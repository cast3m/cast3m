* fichier :  comp1.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

************************************************************************
*                CYLINDRE COMPOSITE BICOUCHE
*           FIBRES ENROULEES -45/+45 AUTOUR DE L'AXE
*                     PRESSION INTERNE
*
*
* Un cylindre bloqué à sa base en déplacement suivant
* l'axe Z est soumis à une pression interne.
* 3D element DKT
* Ref : Rapport CEA , DEMT 85-482 , M. Hittinger, 1985
* Remise a plat : BP, 2017-01-23
*
************************************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;
* GRAPH = 'O';

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
*   OPTI TRAC X ;
  OPTI TRAC 'PSC' EPTR 5;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

TITRE 'CYLINDRE COMPOSITE BICOUCHE SOUS PRESSION INTERNE';
OPTION DIME 3 ELEM TRI3 MODE TRIDIM ;

TEMPS ;
DENS 0.1 ;

 NAX = 8; NCIRC = 16;
 NAX = 4; NCIRC = 8;
*  NAX = 1; NCIRC = 16;


*______________________________________________________
*   GEOMETRIE
*______________________________________________________
R = 1.05 ; H = 1. ;
PA = R 0. 0.  ; PB = R 0. H ;
O1 = 0. 0. 0. ; O2 = 0. 0. H ;
NR1 = 1. 1. 0. ;
LI1 = PA DROIT NAX PB ;
CYLP = ROTA LI1 NCIRC 90 O1 O2 ;
CYL1 = ORIE CYLP NR1 ;
CYL2 LIB = TOUR CYL1 LI1 90 O1 O2 ;
CYL3 LIC = TOUR CYL2 LIB 90 O1 O2 ;
CYL4 LID = TOUR CYL3 LIC 90 O1 O2 ;
CYL = CYL1 ET CYL2 ET CYL3 ET CYL4 ;
ELIM CYL ;
OEIL = 10. 10. 5. ;
*
SI (NEG GRAPH 'N') ;
  TRAC OEIL CACH CYL 'QUAL' ;
FINSI ;

*______________________________________________________
*   DESCRIPTION DU MATERIAU ORTHOTROPE : COUCHE 1
*______________________________________________________

MOD1 = MODE CYL MECANIQUE ELASTIQUE ORTHOTROPE DKT ;
* MAT1 = MATE MOD1 DIRE O2 INCL 45 NR1 YG1 7E6 YG2
MAT1 = MATE MOD1 DIRE O2 INCL 45 YG1 7E6 YG2
       1.3E6 NU12 0.28 G12 5E5 ;
CAR1 = CARA MOD1 EPAI 0.05 EXCENTREMENT 0.025 ;
MAT1=MAT1 ET CAR1;

* verification graphique de l'orientation :
* 3D coque => 3 vecteurs : V1, V2 et V3
v123 = VLOC MOD1 MAT1;
v123 = CHAN 'GRAVITE' v123 MOD1;
mo123 = mots 'V1X' 'V1Y' 'V1Z' 'V2X' 'V2Y' 'V2Z' 'V3X' 'V3Y' 'V3Z';
ve123 = VECT v123 MOD1 0.07 mo123 (mots 'AZUR' 'OR' 'POUR');
SI (NEG GRAPH 'N');
  TITRE 'COMP1 couche 1 : V1(AZUR)  V2 (JAUNE)  V3(ROUG)';
  TRACE OEIL ve123 CYL 'CACH';
FINSI ;

*______________________________________________________
*   DESCRIPTION DU MATERIAU ORTHOTROPE : COUCHE 2
*______________________________________________________
MOD2 = MODE CYL MECANIQUE ELASTIQUE ORTHOTROPE DKT ;
* MAT2 = MATE MOD2 DIRE O2 INCL -45 NR1 YG1 7E6 YG2
MAT2 = MATE MOD2 DIRE O2 INCL -45 YG1 7E6 YG2
       1.3E6 NU12 0.28 G12 5E5 ;
CAR2 = CARA MOD2 EPAI 0.05 EXCENTREMENT -0.025 ;
MAT2=MAT2 ET CAR2;

* verification graphique de l'orientation :
* 3D coque => 3 vecteurs : V1, V2 et V3
v123 = VLOC MOD2 MAT2;
v123 = CHAN 'GRAVITE' v123 MOD2;
ve123 = VECT v123 MOD2 0.07 mo123 (mots 'AZUR' 'BRON' 'POUR');
SI (NEG GRAPH 'N');
  TITRE 'COMP1 couche 2 : V1(AZUR)  V2 (JAUNE)  V3(ROUG)';
  TRACE OEIL ve123 CYL 'CACH';
FINSI ;

* chargement
MOP = 'MODE' CYL 'CHARGEMENT' 'PRESSION' DKT ;
MAP = 'PRES' MOP 'PRES' 1. ;

*______________________________________________________
*   CREATION DE LA RIGIDITE
*______________________________________________________


MODORT = MOD1 ET MOD2 'ET' MOP ;
MATORT = MAT1 ET MAT2 ;

RIG12 = RIGI MODORT MATORT ;


*______________________________________________________
*   CONDITIONS AUX LIMITES
*______________________________________________________

CO1 = COTE 2 CYL1 ; CO2 = COTE 2 CYL2 ;
CO3 = COTE 2 CYL3 ; CO4 = COTE 2 CYL4 ;
COB = (CO1 ET CO2 ET CO3 ET CO4 ) coul BLEU;
CO1 = COTE 4 CYL1 ; CO2 = COTE 4 CYL2 ;
CO3 = COTE 4 CYL3 ; CO4 = COTE 4 CYL4 ;
COA = (CO1 ET CO2 ET CO3 ET CO4 ) coul BOUT;

PB1 = CYL point proch (0. R H);
PB2 = CYL point proch ((-1.*R) 0. H);
PB3 = CYL point proch (0. (-1.*R) H);

CDL1 = BLOQ 'UZ' COB ;
CDL2 = ( BLOQ 'UY' (PB  et PB2) )
    et ( BLOQ 'UX' (PB1 et PB3) );
CDL = CDL1 ET CDL2 ;

*______________________________________________________
*   CALCUL
*______________________________________________________
RIGITOT = RIG12 ET CDL ;

FP = 'BSIG' MOP MAP ;
*
DEP1 = RESO RIGITOT FP ;

DEFO0= DEFO CYL 0.0 DEP1 GRIS;
DEFO1= DEFO CYL DEP1 (EXCO DEP1 'UX');

SI (NEG GRAPH 'N') ;
 TRAC OEIL  (DEFO1 et DEFO0) ;
 TRAC (0. 0. 100.)  (DEFO1 et DEFO0) ;
* deplacement UX et UY
  evux = EVOL 'BLEU' 'CHPO' DEP1 COB  'UX';
  evuy = EVOL 'VERT' 'CHPO' DEP1 COB  'UY';
  Tlege = tabl; Tlege . 'TITRE' = tabl;
  Tlege . 'TITRE' . 1 = mot 'UX'; Tlege . 'TITRE' . 2 = mot 'UY';
  DESS (evux ET evuy) LEGE SO Tlege MIMA 'TITRE' 'en z=H';
  evux = EVOL 'BLEU' 'CHPO' DEP1 COA  'UX';
  evuy = EVOL 'VERT' 'CHPO' DEP1 COA  'UY';
  Tlege = tabl; Tlege . 'TITRE' = tabl;
  Tlege . 'TITRE' . 1 = mot 'UX'; Tlege . 'TITRE' . 2 = mot 'UY';
  DESS (evux ET evuy) LEGE SO Tlege MIMA 'TITRE' 'en z=0';
FINSI;

* ref = calcul element LC8 tire de [DEMT 85-482]
uref = (0.63615E-5 + 0.64702E-5) /2.;

* VALEUR pour le TEST
ur1A  = EXTR DEP1 'UX' PA ;
ur1B  = EXTR DEP1 'UX' PB ;
MESS '************************************************';
MESS ' DEPLACEMENT RADIAL REFERENCE : ' uref;
MESS ' DEPLACEMENT RADIAL EN PA CALCULE :' ur1A ur1B;
RES1= ABS((ur1A - uref) / uref);
MESS 'ECART RELATIF : ' RES1 ;
MESS '************************************************';
SAUT 2 LIGN ;
TEMPS ;
*______________________________________________________
*                    CODE BON FONCTIONNEMENT
*______________________________________________________

SI (RES1 <EG 5.E-2);
   ERRE 0 ;
SINO;
   ERRE 5 ;
FINSI ;
FIN;










