* fichier :  dy_dev11.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
******************************************************************
*
*    Cas-Test de la liaison palier de l'operateur DYNE
*
* Masse ponctuelle sur 1 palier a 3 lobes
* Chargement statique des paliers Fz = -784 N
*
* Auteur : Inconnu
* Modifs : - BP, 2015-01-23 : on reecrit le cas-test
*
******************************************************************
*
OPTI 'DIME' 3 'ELEM' SEG2 ;
OPTI EPSI LINEAIRE;
COMPLET = FAUX;
* COMPLET = VRAI;
GRAPH   = FAUX;
*GRAPH   = VRAI; OPTI 'TRAC' 'PSC' 'POTR' 'HELVETICA_16' 'EPTR' 6;
*
* Nombre de pas de temps, pas de temps
si (COMPLET);  NPas1 = 5000 ; PTps1 =  1.E-5;
sinon;         NPas1 = 2000 ; PTps1 =  1.E-5;
finsi;
* Sortie tous les NSor1 pas de temps
NSor1 = 10 ;
NbMod1 = 2;
*
* donnees du palier :
Lpalier = 0.05;
Rarbre = 0.06;
mu = 0.017;
rhofl = 840.;
OMEGHZ = 2500./60.;
Wcharg = 784.;
* donnees des 3 lobes :
xjeu   = 0.25E-3; xjeum1 = 1./ xjeu;
apchrg = 0.6*xjeu;
psi1 = 85.; psi2 = 205.; psi3 = 325.;
beta = 110.;
teta1 = 30.; teta2 = 150.; teta3 =270.;
Rcou = Rarbre + xjeu;
Rlob = Rcou + apchrg;

**** tracé des lobes ****
si (GRAPH);
  dtet = 1.;
  Xampl = 1.;
  Xa = Xampl * apchrg;
* coussinet
  t1 = prog -180. PAS dtet 180.;
  y1 = sin t1; z1 = -1.* (cos t1);
  evarb = evol 'ORAN' 'MANU' (Rarbre*y1) (Rarbre*z1);
  evcou = evol 'JAUN' 'MANU' (Rcou*y1)   (Rcou*z1);
* lobe 1 :
  az1 = -1.* Xa * (cos psi1);
  ay1 =      Xa * (sin psi1);
  tlob1 = prog teta1 PAS dtet (teta1 + beta);
  zlob1 = az1 + (Rlob * (cos tlob1));
  ylob1 = ay1 - (Rlob * (sin tlob1));
  evlob1 = evol 'BLEU' 'MANU' ylob1 zlob1;
* lobe 2 :
  az2 = -1.* Xa * (cos psi2);
  ay2 =      Xa * (sin psi2);
  tlob2 = prog teta2 PAS dtet (teta2 + beta);
  zlob2 = az2 + (Rlob * (cos tlob2));
  ylob2 = ay2 - (Rlob * (sin tlob2));
  evlob2 = evol 'TURQ' 'MANU' ylob2 zlob2;
* lobe 3 :
  az3 = -1.* Xa * (cos psi3);
  ay3 =      Xa * (sin psi3);
  tlob3 = prog teta3 PAS dtet (teta3 + beta);
  zlob3 = az3 + (Rlob * (cos tlob3));
  ylob3 = ay3 - (Rlob * (sin tlob3));
  evlob3 = evol 'OCEA' 'MANU' ylob3 zlob3;

* dess (evarb et evcou et evlob1 et evlob2 et evlob3) 'CARR' ;
  dess (evlob1 et evlob2 et evlob3) 'CARR' ;
* dess (evarb et evlob1 et evlob2 et evlob3) 'CARR' ;

finsi;

*
P1 = 0.  0. 0. ;
Mail1 = MANU 'POI1' P1;
*
moprim = mots 'UY' 'UZ';
M1 = 1.;
* M1 = 0.1;
MMass1 = MANU 'RIGIDITE' 'TYPE' 'MASSE' Mail1 moprim (prog M1 0 0 M1);
MRigi1 = MANU 'RIGIDITE' 'TYPE' 'RIGIDITE' Mail1 moprim (prog 0 0 0 0);
*
chpuy = manu 'CHPO' Mail1 2 'UY' 1. 'UZ' 0. 'NATURE' 'DIFFUS';
chpuz = manu 'CHPO' Mail1 2 'UY' 0. 'UZ' 1. 'NATURE' 'DIFFUS';
lchp = SUIT chpuy chpuz;

TBasR1 = VIBR 'PROC' (prog -0.1)  (lect NbMod1)
         MRigi1 MMass1  lchp;

mess 'mode 1 : f=' TBasR1 . 'MODES' . 1 . 'FREQUENCE'
              'M=' TBasR1 . 'MODES' . 1 . 'MASSE_GENERALISEE';
mess 'mode 2 : f=' TBasR1 . 'MODES' . 2 . 'FREQUENCE'
              'M*=' TBasR1 . 'MODES' . 2 . 'MASSE_GENERALISEE';

*
* Creation de la table des CONDITIONS INITIALES
************************************************
TInit1 = TABLE 'INITIAL' ;
*
*UY0 = 0. ; UZ0 = -1.e-6 ;
UY0 = 0. ; UZ0 = 0. ;
Dep0 = MANU 'CHPO' Mail1 2 'UY' UY0 'UZ' UZ0 NATURE DIFFUS ;
*
REPE bb1 NbMod1 ;
 i1 = &bb1 ;
 Def1 = TBasR1 . 'MODES' . i1 . 'DEFORMEE_MODALE' ;
 M_G1 = TBasR1 . 'MODES' . i1 . 'MASSE_GENERALISEE' ;
 P_R1 = TBasR1 . 'MODES' . i1 . 'POINT_REPERE' ;
 qi1 = (YTMX Def1 MMass1 Dep0) / M_G1 ;
 f_r1 = TBasR1 . 'MODES' . i1 . 'FREQUENCE';
 mess 'Mode' i1 'f=' f_r1 ' M*=' qi1;
 SI (EGA i1 1) ;
  TInit1.'DEPLACEMENT' = MANU 'CHPO' P_R1 1 'ALFA' qi1 NATURE DIFFUS ;
  TInit1.'VITESSE' = MANU 'CHPO' P_R1 1 'ALFA' 0. NATURE DIFFUS ;
 SINON ;
  TInit1.'DEPLACEMENT' = TInit1.'DEPLACEMENT' ET
                      (MANU 'CHPO' P_R1 1 'ALFA' qi1 NATURE DIFFUS) ;
  TInit1.'VITESSE' = TInit1.'VITESSE' ET
                  (MANU 'CHPO' P_R1 1 'ALFA' 0. NATURE DIFFUS) ;
 FINSI ;
FIN bb1 ;

*
* Creation de la table des LIAISONS (paliers)
**********************************************
*
TLiai1 = TABLE 'LIAISON' ;
TLiai1 . LIAISON_B = TABLE 'LIAISON_B' ;
PAL1 = TABLE 'LIAISON_ELEMENTAIRE' ;
TLiai1 . LIAISON_B . 1 = PAL1 ;
PAL1 . TYPE_LIAISON     = 'PALIER_FLUIDE' ;
PAL1 . MODELE_PALIER    = RODELI ;
PAL1 . POINT_SUPPORT    = P1 ;
PAL1 . LONGUEUR_PALIER  = Lpalier ;
PAL1 . RAYON_ARBRE      = Rarbre ;
PAL1 . VISCOSITE_FLUIDE = mu ;
PAL1 . RHO_FLUIDE       = rhofl ;
PAL1 . PRESSION_ADMISSION = 0. ;
PAL1 . VITESSE_ROTATION = 2 * pi * OMEGHZ;
* PAL1 . EPSI = 0.;
* PAL1 . PHII = 0. ;
* PAL1 . AFFICHAGE = 0 ;
PAL1 . GEOMETRIE_PALIER = TABLE ;
PAL1 . GEOMETRIE_PALIER . NOMBRE_LOBES = 3 ;
* xprec = 1.E-4;
xprec = 1.E-6;
PAL1 . GEOMETRIE_PALIER . CRITERE_ARRET = xprec;

PAL1 . GEOMETRIE_PALIER . 1 = TABLE ;
PAL1 . GEOMETRIE_PALIER . 1 . ASYMETRIE = psi1 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 1 . JEU_USINAGE = xjeu ;
PAL1 . GEOMETRIE_PALIER . 1 . PRECHARGE = apchrg ;
PAL1 . GEOMETRIE_PALIER . 1 . ANGLE_DEBUT = teta1 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 1 . AMPL_ANGLE = beta * pi / 180. ;
* PAL1 . GEOMETRIE_PALIER . 1 . COEF_SUR = 1.715 ;
* PAL1 . GEOMETRIE_PALIER . 1 . NB_MAILLES = 100 ;

PAL1 . GEOMETRIE_PALIER . 2 = TABLE ;
PAL1 . GEOMETRIE_PALIER . 2 . ASYMETRIE = psi2 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 2 . JEU_USINAGE = xjeu ;
PAL1 . GEOMETRIE_PALIER . 2 . PRECHARGE = apchrg ;
PAL1 . GEOMETRIE_PALIER . 2 . ANGLE_DEBUT = teta2 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 2 . AMPL_ANGLE = beta * pi / 180. ;
* PAL1 . GEOMETRIE_PALIER . 2 . COEF_SUR = 1.715 ;
* PAL1 . GEOMETRIE_PALIER . 2 . NB_MAILLES = 100 ;

PAL1 . GEOMETRIE_PALIER . 3 = TABLE ;
PAL1 . GEOMETRIE_PALIER . 3 . ASYMETRIE = psi3 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 3 . JEU_USINAGE = xjeu ;
PAL1 . GEOMETRIE_PALIER . 3 . PRECHARGE = apchrg ;
PAL1 . GEOMETRIE_PALIER . 3 . ANGLE_DEBUT = teta3 * pi / 180. ;
PAL1 . GEOMETRIE_PALIER . 3 . AMPL_ANGLE = beta * pi / 180. ;
* PAL1 . GEOMETRIE_PALIER . 3 . COEF_SUR = 1.715 ;
* PAL1 . GEOMETRIE_PALIER . 3 . NB_MAILLES = 100 ;
*
*
* Creation de la table des VARIABLES DE SORTIE
***********************************************
TSort1 = TABLE 'SORTIE' ;
TSort1 . 'VARIABLE' = TABLE 'VARIABLE' ;
TSort1 . 'LIAISON_B' = TABLE 'LIAISON_B' ;
TSort1 . 'LIAISON_B' . PAL1 = VRAI ;
*
* Creation de la table du CHARGEMENT (chargement dynamique)
*************************************
TChar1 = TABLE 'CHARGEMENT' ;
LTps1 = PROG 0.0 'PAS' PTps1 'NPAS' (NPas1+1) ;
FsY1 = 0. ;
FsZ1 = -1. * Wcharg ;

Chp_Fs1 = MANU CHPO 2 (P1)
           'FY' FsY1 'FZ' FsZ1 ;
Ev_Fs1 = EVOL MANU LTps1 (PROG (NPas1+2) * 1.) ;
Char_Fs1 = CHAR Chp_Fs1 Ev_Fs1 ;
TChar1 . 'BASE_A' = PJBA TBasR1 Char_Fs1 ;


****************************************************************
* Cacul temporel 1 : Appel a DYNE
****************************************************************

TResu1 = DYNE 'DE_VOGELAERE' TBasR1 TChar1 TInit1 TSort1
              TLiai1 PTps1 NPas1 NSor1 ;

uy1 = TResu1 . PAL1 . 'UY_ARBRE';
uz1 = TResu1 . PAL1 . 'UZ_ARBRE';
vy1 = TResu1 . PAL1 . 'VY_ARBRE';
vz1 = TResu1 . PAL1 . 'VZ_ARBRE';
fy1 = TResu1 . PAL1 . 'FY_PALIER';
fz1 = TResu1 . PAL1 . 'FZ_PALIER';
tprog = TResu1 . 'TEMPS_DE_SORTIE' ;
nt = dime tprog;


* graphiques
********************************
si (GRAPH);
  evuy1 = EVOL BLEU 'MANU' 't' tprog 'UY/e' (uy1*xjeum1);
  evuz1 = EVOL TURQ 'MANU' 't' tprog 'UZ/e' (uz1*xjeum1);
  dess (evuy1 et evuz1) POSX CENT POSY CENT;

  evvy1 = EVOL BLEU 'MANU' 't' tprog 'VY' (vy1);
  evvz1 = EVOL TURQ 'MANU' 't' tprog 'VZ' (vz1);
  dess (evvy1 et evvz1) POSX CENT POSY CENT;

  evfy1 = EVOL BLEU 'MANU' 't' tprog 'FY' fy1;
  evfz1 = EVOL TURQ 'MANU' 't' tprog 'FZ' fz1;
  dess (evfy1 et evfz1) POSX CENT POSY CENT;
  dess (evfz1) POSX CENT POSY CENT YBOR 780. 790.;
  dess (evfy1) POSX CENT POSY CENT YBOR -1. 2. YGRA 0.5;

  evyz1 = EVOL BLEU 'MANU' 'UY/e' (uy1*xjeum1) 'UZ/e' (uz1*xjeum1);
  t1 = prog -180. PAS 1. 180.;
  y1 = sin t1; z1 = -1.* (cos t1);
  evjeu = evol 'MANU' y1 z1;
  Tjeu = tabl; Tjeu . 2 = mot 'TIRR';
  dess (evyz1 et evjeu )
    POSX CENT POSY CENT
    XBOR 0. 1. YBOR -1. 0. 'CARR' Tjeu;
  dess (evyz1 et evjeu)
    POSX CENT POSY CENT 'CARR' Tjeu;
finsi;
*excentricite et angle de calage
prex1 = ((uy1**2) + (uz1**2))**0.5;
mas00 =  (masq (abs uy1) 'EGINFE' 1.E-30 )
      *  (masq (abs uz1) 'EGINFE' 1.E-30 );
uy100 = (((prog nt*1.) - mas00) * uy1)  + mas00;
prang1 = ATG uy100 (-1.*uz1);
mess 'excentricite ='; list prex1;
mess 'angle de calage ='; list prang1;
tfin = extr tprog nt;
an1fin = extr prang1 nt;
ex1fin = extr prex1 nt;
mess 'excentricite relative (t=' tfin ')=' (ex1fin*xjeum1);



si (COMPLET);
****************************************************************
* Caclul temporel 2 : Appel a DYNE
****************************************************************

* Creation de la table des CONDITIONS INITIALES
************************************************
TInit2 = TABLE 'INITIAL' ;
*
UY02 = 0.2*xjeu ;
UZ02 = -0.2*xjeu ;
Dep02 = MANU 'CHPO' Mail1 2 'UY' UY02 'UZ' UZ02 NATURE DIFFUS ;
*
REPE bb2 NbMod1 ;
 i2 = &bb2 ;
 Def2 = TBasR1 . 'MODES' . i2 . 'DEFORMEE_MODALE' ;
 M_G2 = TBasR1 . 'MODES' . i2 . 'MASSE_GENERALISEE' ;
 P_R2 = TBasR1 . 'MODES' . i2 . 'POINT_REPERE' ;
 qi2 = (YTMX Def2 MMass1 Dep02) / M_G2 ;
 f_r2 = TBasR1 . 'MODES' . i2 . 'FREQUENCE';
 mess i2 qi2;
 SI (EGA i2 1) ;
  TInit2.'DEPLACEMENT' = MANU 'CHPO' P_R2 1 'ALFA' qi2 NATURE DIFFUS ;
  TInit2.'VITESSE' = MANU 'CHPO' P_R2 1 'ALFA' 0. NATURE DIFFUS ;
 SINON ;
  TInit2.'DEPLACEMENT' = TInit2.'DEPLACEMENT' ET
                      (MANU 'CHPO' P_R2 1 'ALFA' qi2 NATURE DIFFUS) ;
  TInit2.'VITESSE' = TInit2.'VITESSE' ET
                  (MANU 'CHPO' P_R2 1 'ALFA' 0. NATURE DIFFUS) ;
 FINSI ;
FIN bb2 ;

* Caclul temporel 2 : Appel a DYNE
************************************************
TResu2 = DYNE 'DE_VOGELAERE' TBasR1 TChar1 TInit2 TSort1
              TLiai1 PTps1 NPas1 NSor1 ;
*
uy2 = TResu2 . PAL1 . 'UY_ARBRE' ;
uz2 = TResu2 . PAL1 . 'UZ_ARBRE' ;
vy2 = TResu2 . PAL1 . 'VY_ARBRE';
vz2 = TResu2 . PAL1 . 'VZ_ARBRE';
fy2 = TResu2 . PAL1 . 'FY_PALIER';
fz2 = TResu2 . PAL1 . 'FZ_PALIER';
tprog = TResu2 . 'TEMPS_DE_SORTIE' ;

* graphiques
********************************
si (GRAPH);
  evuy2 = EVOL ROUG 'MANU' 't' tprog 'UY/e' (uy2*xjeum1);
  evuz2 = EVOL ROSE 'MANU' 't' tprog 'UZ/e' (uz2*xjeum1);
  dess (evuy2 et evuz2) POSX CENT POSY CENT;

  evfy2 = EVOL ROUG 'MANU' 't' tprog 'FY' fy2;
  evfz2 = EVOL ROSE 'MANU' 't' tprog 'FZ' fz2;
  dess (evfy2 et evfz2) POSX CENT POSY CENT;
  dess (evfz2) POSX CENT POSY CENT YBOR 780. 790.;
  dess (evfy2) POSX CENT POSY CENT YBOR -1. 2. YGRA 0.5;

  evyz2 = EVOL ROUG 'MANU' 'UY/e' (uy2*xjeum1) 'UZ/e' (uz2*xjeum1);

  Tjeu = tabl; Tjeu . 2 = mot 'TIRC'; Tjeu . 3 = mot 'TIRR';
  dess (evyz1 et evyz2 et evjeu)
    POSX CENT POSY CENT
    XBOR 0. 1. YBOR -1. 0. 'CARR' Tjeu;
  dess (evyz1 et evyz2 et evjeu)
    POSX CENT POSY CENT 'CARR' Tjeu;
  dess (evyz1 et evyz2) POSX CENT POSY CENT;
finsi;

*excentricite et angle de calage
prex2  = ((uy2**2) + (uz2**2))**0.5;
prang2 = ATG uy2 (-1.*uz2);
mess 'excentricite ='; list prex2;
mess 'angle de calage ='; list prang2;
nt = dime tprog;
tfin = extr tprog nt;
an2fin = extr prang2 nt;
ex2fin = extr prex2 nt;
mess 'excentricite relative (t=' tfin ')=' (ex2fin*xjeum1);

finsi;


****************************************************************
* Tests de Non Regression
****************************************************************

* tolerance
si (COMPLET) ; ertol = 0.02;
sinon;         ertol = 0.05;
finsi;

* excentricite et angle de calage de reference
* ref : code exterieur du LMS Poitier (devenu institut P'Prime)
*       cf. travaux de Frene, Nicolas, ...
exref = 3.6656E-5;
anref = 39.326 ;

erex1 = ABS ((ex1fin - exref) / exref);
eran1 = ABS ((an1fin - anref) / anref);
si (complet);
  erex2 = ABS ((ex2fin - exref) / exref);
  eran2 = ABS ((an2fin - anref) / anref);
finsi;

opti echo 0;
saut lign ;
mess '--------------------------------------------------------';
mess 'excentricite de reference = 3.6656E-5='exref ;
mess 'excentricite  (t=' tfin ')=' (ex1fin) ' erreur=' erex1;
si (COMPLET);
  mess 'excentricite  (t=' tfin ')=' (ex2fin) ' erreur=' erex2;
  si (erex2 > erex1); erex1 = erex2; finsi;
finsi;
mess '--------------------------------------------------------';
mess 'angle de calage de reference = 39.326 degrés='anref ;
mess 'angle de calage (t=' tfin ')=' an1fin ' erreur=' eran1;
si (COMPLET);
  mess 'angle de calage (t=' tfin ')=' an2fin ' erreur=' eran2;
  si (eran2 > eran1); eran1 = eran2; finsi;
finsi;
mess '--------------------------------------------------------';

opti echo 1;

TEMP ;

SI ((erex1 > ertol) ou (eran1 > ertol));
 ERRE 5;
SINON;
 ERRE 0;
FINSI;


fin ;






