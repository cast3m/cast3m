* fichier :  pore2.dgibi
************************************************************************
* Section : Thermique Mecanique
* Section : Mecanique Plastique
************************************************************************
*           Test Pore2.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
complet = faux;
*************************************************
*  pour calcul complet mettre complet à : vrai;
***************************************************
*******************************************************
*                                                     *
*  TEST PORE2                                         *
*                                                     *
*  CYLINDRE EPAIS EN MILIEU POREUX                    *
*  AVEC EFFETS THERMIQUES                             *
*                                                     *
*  REFERENCE : Benchmark INTERCLAY 1.4  Basic Model   *
*                                                     *
*      Le milieu est elastoplastique,                 *
*      modele Von-Mises associe                       *
*                                                     *
*      On decharge la contrainte a l'interieur du     *
*      trou puis on impose un champ de temperature    *
*                                                     *
*      On compare avec les valeurs de reference       *
*      obtenues aussi par CASTEM2000 et en accord     *
*      avec les resultats INTERCLAY                   *
*                                                     *
*******************************************************
*
OPTI ECHO 0;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;

TITRE   ' TEST PORE2  ' ;
GRAPH = 'N';
OPTIO DIME 2 MODEL AXIS   ELEM QUA8  ;
*
*=====================================================*
*             geometrie : maillage                    *
*=====================================================*
*
* Dimension en metres
*
DENS 0.5;
P1 = 2.5 0. ; P2 = 25. 0. ; Q1 = 2.5 0.5 ;Q2 = 25. 0.5;
PP1 = 5 0. ; QQ1 = 5 0.5 ;
LI1 = P1 D  P2 'DINI' 0.25 'DFIN' 2.5 ;
LI2 =  P2 D Q2 ;
LI3  = INVE ( Q1 D Q2 'DINI' 0.25 'DFIN' 2.5 );
LI4  = Q1 D P1 ;
LI  = LI1  ET LI2  ET LI3  ET LI4  ;
CADRE = DALLER LI1  LI2  LI3  LI4  PLAN ;
SI (NEG GRAPH 'N');
   TRAC CADRE ;
FINSI;
*
*=====================================================*
*      modele - materiau - caracteristiques en Pa     *
*                 conditions aux limites              *
*=====================================================*
*
 MO=MODE CADRE POREUX ELASTIQUE PLASTIQUE
                     DRUCKER_PRAGER ;
*
 MAT=MATE MO YOUNG 200.13E6 NU 0.25 MOB  7500E6 COB 1.
 PERM 4.E-12   VISCO 4.
 ALPH (5.E-5 / 3. )  KF 3000E6   RHOF 0.
 ALPF 40E-5 ALPM (19.E-5 / 3.)  RHO 0.
 ALFA 0.         BETA 0.57735027 K 1.E6
 GAMM 0.         DELT 0.57735027
 ETA  0.         MU   0.57735027 KL 1.E6 H 0. ;
*
*  on bloque les faces superieure et inferieure
*
BL1=BLOQ UZ  LI1 ;
BL2=BLOQ UZ  LI3 ;
*
BL = BL1 ET BL2 ;
*
*=====================================================*
*         1-ere etape  : chargement mecanique         *
*   Initialisation de la table pour appel a PASAPAS   *
*=====================================================*
*
*   contraintes initiales
*
ZOZO = MANU CHML MO    SMRZ 0.
   SMRR -5.E6 SMTT -5.E6 SMZZ -5.E6 MSR0 0.
   TYPE CONTRAINTES  STRESSES ;
ZUZU = MANU CHML MO    GARZ 0.
   EPRR 0.    EPTT  0.   EPZZ 0.    PRES 2.5E6
   TYPE DEFORMATIONS  STRESSES ;
FF = BSIGMA MO ZOZO;
EVT1= EVOL MANU 'TEMPS' (PROG 0       14 )
                'CHARGE' (PROG 1     1 );
 CHA1 = CHAR 'MECA' FF  EVT1 ;
*
*    pression
*
FPI2 = PRESS MASS MO -1.E6   LI4 ;
EVT2= EVOL MANU 'TEMPS' (PROG 0 PAS 1 14 )
                'CHARGE' (PROG 0 PAS 0.25 3.5) ;
CHA2 = CHAR 'MECA' FPI2 EVT2 ;
CHATT = CHA1 ET CHA2 ;
*
TAB1 = TABLE;
TAB1 . 'CONTRAINTES' = TABLE;
TAB1 . 'DEFORMATIONS' = TABLE;
TAB1 . 'DEPLACEMENTS' = TABLE;
TAB1.'CONTRAINTES' . 0 = ZOZO;
TAB1.'DEFORMATIONS'. 0 = ZUZU;
TAB1.'DEPLACEMENTS' . 0 = MANU CHPO CADRE  1 P 2.5E6 ;
TAB1.'TEMPS_CALCULES' = PROG 0 6  10 14;
TAB1.'MODELE' = MO;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'BLOCAGES_MECANIQUES' = BL;
TAB1.'CHARGEMENT' = CHATT;
TAB1.'PRECISION'=5.E-7;
*
TMASAU=table;
tab1 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
TAB1.'MONO'=monop;tab1.'NB_EL_PARA'=0.9;
PASAPAS TAB1 ;
*
*=====================================================*
*            2-eme etape  : temperature               *
*   Initialisation de la table pour appel a PASAPAS   *
*=====================================================*
*
TAB2 = TABLE;
TAB2 . 'VARIABLES_INTERNES' = TABLE;
TAB2 . 'DEPLACEMENTS' = TABLE;
TAB2 . 'CONTRAINTES' = TABLE;
TAB2 . 'DEFORMATIONS' = TABLE;
SS0 = PECHE TAB1 CONTRAINTES ;
EE0 = PECHE TAB1 DEFORMATIONS ;
TAB2.'CONTRAINTES' . 0 = SS0;
TAB2.'DEFORMATIONS' . 0 = EE0;
TAB2.'PRECISION'=5.E-7;
TAB2.'VARIABLES_INTERNES' . 0 =
                     PECHE TAB1 VARIABLES_INTERNES ;
TAB2.'DEPLACEMENTS' . 0 = PECHE TAB1 DEPLACEMENTS  ;
*
FF = BSIGMA MO SS0 ;
EVT1= EVOL MANU 'TEMPS'  (PROG 0     100)
                'CHARGE' (PROG 1     1 );
CHA1 = CHAR 'MECA' FF EVT1 ;
*
LEFAC = 1. / ( LOG ( 2.5/25.) ) ;
RAD   = COOR 1 CADRE ;
DT  = NOMC (  LEFAC * ( LOG (RAD / 25. ) ) ) T ;
*
 TABTER = TABLE ;TEMP = TABLE;
 TEMP . 0 = 0.;
 TEMP . 1 = 100.;
 TABTER . 0 = MANU CHPO CADRE 1 T 0. ;
 TABTER . 1 = (DT * 100.)  ;
 CHA2 = CHAR 'T' TEMP TABTER;
*
CHATT = CHA1 ET CHA2;
*
si complet;
tfin = 70.;
sinon;
tfin = 15.;
finsi;
TAB2.'TEMPS_CALCULES' = PROG 0  PAS 5.  tfin; ;
TAB2.'MODELE' = MO;
TAB2.'CARACTERISTIQUES' = MAT;
TAB2.'BLOCAGES_MECANIQUES' = BL;
TAB2.'CHARGEMENT' = CHATT;
*
TMASAU=table;
tab2 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
TAB2.'PROCESSEURS'= MOT 'AUTOMATIQUE';
PASAPAS TAB2 ;
*
*=====================================================*
*              affichage des resultats                *
*=====================================================*
*
MESS ' RESULTATS TEST PORE2 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS
'   On cherche les contraintes et la pression ';
MESS
'   Elles sont comparees a des valeurs de reference ';
SAUT 1 LIGN;
SAUT 1 LIGN;
*
*----------- r{sultats --------------------------------
*
*    DISTRIBUTION DES CONTRAINTES ET PRESSION
*    POUR T = 70.
*
si complet;
TT = 70. ;
sinon;
TT = 15.;
finsi;
SIGAA = PECHE TAB2 CONTRAINTES  TT;
EPSAA = PECHE TAB2 DEFORMATIONS  TT;
SIGAB = CHAN 'CHPO' MO SIGAA ;
 EPSAB = CHAN 'CHPO' MO EPSAA ;
SIGARR= EVOL CHPO  SIGAB SMRR LI1 ;
SIGATT= EVOL CHPO  SIGAB SMTT LI1 ;
LAPP =  EVOL CHPO  EPSAB PRES LI1 ;
SI (NEG GRAPH 'N');
   DESS SIGARR ; DESS SIGATT ; DESS LAPP ;
FINSI;
EVSIGAR = EXTR SIGARR ORDO 1;
EVSIGAT = EXTR SIGATT ORDO 1;
EVPP    = EXTR LAPP   ORDO 1;
*
*   VALEURS DE REFERENCE  calculees a la date du 1/2/93
*
si complet;
*  CONTRAINTE RADIALE
SOLRR = PROG
 -1.59459E+06 -1.56051E+06 -1.78383E+06 -1.76850E+06
 -1.96931E+06 -1.97473E+06 -2.15711E+06 -2.17942E+06
 -2.34668E+06 -2.38270E+06 -2.53753E+06 -2.58467E+06
 -2.72927E+06 -2.78543E+06 -2.92159E+06 -2.98507E+06
 -3.11427E+06 -3.18371E+06 -3.30718E+06 -3.38150E+06
 -3.50030E+06 -3.57863E+06 -3.69361E+06 -3.77540E+06
 -3.88744E+06;
SOLRR = SOLRR ET (PROG
 -3.97178E+06 -4.08114E+06 -4.16891E+06 -4.29345E+06
 -4.34768E+06 -4.48547E+06 -4.54414E+06 -4.62704E+06
 -4.68421E+06 -4.74775E+06 -4.79065E+06 -4.83863E+06
 -4.86963E+06 -4.90481E+06 -4.92612E+06 -4.95084E+06
 -4.96421E+06 -4.98036E+06 -4.98723E+06 -4.99637E+06
 -4.99792E+06 -5.00142E+06 );
*
*  CONTRAINTE TANGENTIELLE
SOLTT = PROG
 -3.59051E+06 -3.55538E+06 -3.77813E+06 -3.76143E+06
 -3.96142E+06 -3.96508E+06 -4.14632E+06 -4.16639E+06
 -4.33216E+06 -4.36546E+06 -4.51847E+06 -4.56247E+06
 -4.70501E+06 -4.75775E+06 -4.89181E+06 -4.95180E+06
 -5.07914E+06 -5.14532E+06 -5.26743E+06 -5.33906E+06
 -5.45722E+06 -5.53370E+06 -5.64905E+06 -5.72971E+06
 -5.84300E+06;
SOLTT = SOLTT ET ( PROG
 -5.92761E+06 -6.03950E+06 -6.12667E+06 -6.24105E+06
 -6.30245E+06 -6.28413E+06 -6.13481E+06 -6.01293E+06
 -5.89978E+06 -5.80418E+06 -5.70545E+06 -5.62098E+06
 -5.53670E+06 -5.46384E+06 -5.39075E+06 -5.32680E+06
 -5.26302E+06 -5.20651E+06 -5.15031E+06 -5.09985E+06
 -5.04987E+06 -5.00444E+06 );
*
*  PRESSION
SOLPP = PROG
  1.36747E+06  1.44034E+06  1.51321E+06  1.58674E+06
  1.66027E+06  1.73387E+06  1.80747E+06  1.88113E+06
  1.95479E+06  2.02829E+06  2.10179E+06  2.17490E+06
  2.24800E+06  2.32049E+06  2.39297E+06  2.46467E+06
  2.53636E+06  2.60709E+06  2.67783E+06  2.74773E+06
  2.81762E+06  2.88621E+06  2.95480E+06  3.02386E+06
  3.09291E+06;
SOLPP = SOLPP ET (PROG
  3.15682E+06  3.22073E+06  3.29927E+06  3.37782E+06
  3.40824E+06  3.43867E+06  3.36939E+06  3.30011E+06
  3.24558E+06  3.19105E+06  3.13296E+06  3.07487E+06
  3.01765E+06  2.96042E+06  2.90299E+06  2.84556E+06
  2.78819E+06  2.73081E+06  2.67343E+06  2.61604E+06
  2.55866E+06  2.50128E+06 );
sinon;
SOLRR = prog
 -1.56503E+06 -1.57213E+06 -1.75778E+06 -1.77784E+06
 -1.94803E+06 -1.98244E+06 -2.14000E+06 -2.18611E+06
 -2.33334E+06 -2.38903E+06 -2.52782E+06 -2.59133E+06
 -2.72321E+06 -2.79313E+06 -2.91938E+06 -2.99454E+06
 -3.11618E+06 -3.19563E+06 -3.31352E+06 -3.39647E+06
 -3.51129E+06 -3.59714E+06 -3.70949E+06 -3.79760E+06
 -3.90787E+06;
solrr = solrr et (prog
 -3.99816E+06 -4.12859E+06 -4.17682E+06 -4.32275E+06
 -4.37916E+06 -4.46316E+06 -4.52542E+06 -4.59310E+06
 -4.64335E+06 -4.69793E+06 -4.73815E+06 -4.78194E+06
 -4.81401E+06 -4.84898E+06 -4.87436E+06 -4.90211E+06
 -4.92201E+06 -4.94386E+06 -4.95928E+06 -4.97629E+06
 -4.98804E+06 -5.00116E+06);
soltt = prog
 -3.56189E+06 -3.56846E+06 -3.75410E+06 -3.77364E+06
 -3.94386E+06 -3.97775E+06 -4.13540E+06 -4.18103E+06
 -4.32842E+06 -4.38367E+06 -4.52270E+06 -4.58583E+06
 -4.71802E+06 -4.78765E+06 -4.91426E+06 -4.98918E+06
 -5.11126E+06 -5.19054E+06 -5.30890E+06 -5.39174E+06
 -5.50708E+06 -5.59285E+06 -5.70571E+06 -5.79390E+06
 -5.90481E+06;
soltt = soltt et ( prog
 -5.99480E+06 -6.11303E+06 -6.16665E+06 -6.10728E+06
 -6.00175E+06 -5.91312E+06 -5.83171E+06 -5.76581E+06
 -5.69639E+06 -5.63972E+06 -5.58251E+06 -5.53566E+06
 -5.48786E+06 -5.44850E+06 -5.40854E+06 -5.37545E+06
 -5.34188E+06 -5.31389E+06 -5.28556E+06 -5.26174E+06
 -5.23769E+06 -5.21735E+06);

solpp = prog
  3.52566E+05  4.45884E+05  5.39202E+05  6.32610E+05
  7.26018E+05  8.19101E+05  9.12184E+05  1.00502E+06
  1.09786E+06  1.19042E+06  1.28298E+06  1.37529E+06
  1.46759E+06  1.55963E+06  1.65167E+06  1.74345E+06
  1.83523E+06  1.92692E+06  2.01860E+06  2.10969E+06
  2.20078E+06  2.29305E+06  2.38533E+06  2.47274E+06
  2.56016E+06;
solpp = solpp et ( prog
  2.66334E+06  2.76651E+06  2.80560E+06  2.84469E+06
  2.82168E+06  2.79866E+06  2.78895E+06  2.77924E+06
  2.76631E+06  2.75339E+06  2.74124E+06  2.72909E+06
  2.71676E+06  2.70442E+06  2.69213E+06  2.67984E+06
  2.66755E+06  2.65525E+06  2.64295E+06  2.63066E+06
  2.61836E+06  2.60606E+06);

finsi;

*
*   ecarts
*

ERSIGAR =(MAXI ABS ( EVSIGAR - SOLRR ))/
         (MAXI ABS SOLRR);
ERSIGAT =(MAXI ABS ( EVSIGAT - SOLTT ))/
         (MAXI ABS SOLTT);
ERPP    =( MAXI ABS (EVPP - SOLPP))/
         (MAXI ABS SOLPP);
*
SAUT 1 LIGN;
MESS
'Ecart relatif en Contraintes radiales           :' ERSIGAR ;
SAUT 1 LIGN;
MESS
'Ecart relatif en Contraintes circonferentielles :' ERSIGAT ;
SAUT 1 LIGN;
MESS
'Ecart relatif en Pressions                      :' ERPP    ;
*
*
*=====================================================*
*                  code fonctionnement                *
*=====================================================*
*
* L'ecart maximum entre valeurs de reference et
* calculee doit etre inferieur a 4.d-4
*
SAUT 2 LIGNE;
SI (ERSIGAR <EG 0.001) ;
  SI (ERSIGAT <EG 0.001) ;
    SI (ERPP <EG 0.001) ;
        ERRE 0;
    SINON;
        ERRE 5;
    FINSI;
  SINON;
    ERRE 5;
  FINSI;
SINON;
  ERRE 5;
FINSI;
*
SAUT 1 LIGN;
*
FIN;


























