* fichier :  rupt8.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*           Test Rupt8.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
****************************************************
*                                                  *
*     VALIDATION DE LA METHODE G_THETA EN CAS      *
*     D'UNE PLAQUE EN TRACTION PURE. SOLUTION      *
*  DE REFERENCE : Compendium of STRESS INTENSITY   *
*  FACTORS by Rooke & Cartwright. Section Plates   *
*   and Shells "Central crack in a finite width    *
*         plate : uniform bending moment"          *
*                                                  *
*                                                  *
*  GEOMETRIE :                                     *
*                                                  *
*  longueur de la fissure : 2a = 2 x 0.30 M        *
*  longueur de la plaque  : 2b = 2 x 1.00 M        *
*  Hauteur de la plaque   : 2h = 2 x 5.25 M        *
*  Epaisseur de la plaque : e  = 0.001    M        *
*                                                  *
*  MATERIAU :                                      *
*                                                  *
*  Module d'Young         : E = 2.E11              *
*  Coefficient de poisson : NU = 0.3               *
*                                                  *
*  CHARGEMENT :                                    *
*                                                  *
*  Traction pure          : SIG = 5.E6 N/M2        *
*                                                  *
*  SOLUTION ANALYTIQUE :                           *
*                                                  *
*  Le taux de restitution d'energie : G = 129.5    *
*                                                  *
****************************************************
opti echo 1;
OPTI DIME 3 ELEM TRI3 DENS 1.;
N4 = 6; N8 = 13;
N12 = (N4 + N8)*(-1);
O1 = 0. 0. 1000.;
ADIM = 0.3; BDIM = 1.; TAFIS = ADIM / 33.;
HH1 = BDIM / 2.0;
P1 = 0. 0. 0.; P2 = BDIM 0. 0.; P3 = ADIM 0. 0.;
P3G = (ADIM - TAFIS) 0. 0.; P3D = (ADIM + TAFIS) 0. 0.;
ALF1 = ATG HH1 ADIM; ALF2 = ATG HH1 (BDIM - ADIM);
X1 = TAFIS * (COS ALF1); Y1 = TAFIS * (SIN ALF1);
P31 = (ADIM - X1) Y1 0.;
X1 = TAFIS * (COS ALF2); Y1 = TAFIS * (SIN ALF2);
P32 = (ADIM + X1) Y1 0.;
C1 = C N4 P3G P3 P31; C2 = C N8 P31 P3 P32;
C3 = C N4 P32 P3 P3D;
SU1 = COUT (C1 ET C2 ET C3) P3;
P4 = 0. HH1 0.; P5 = BDIM HH1 0.;
L1 = D N4 P1 P4 DINI (2.0*TAFIS) DFIN (8.0*TAFIS);
L2 = D N12 P4 P31 DINI (9.0*TAFIS) DFIN (0.3*TAFIS);
L3 = D N12 P1 P3G DINI (3.0*TAFIS) DFIN (1.5*TAFIS);
L4 = D N8 P4 P5 ;
L5 = D N12 P5 P32 DINI (14.0*TAFIS) DFIN (0.3*TAFIS);
L6 = D N4 P2 P5 DINI (2.0*TAFIS) DFIN (10.0*TAFIS);
L7 = D N12 P2 P3D DINI (10.0*TAFIS) DFIN (1.5*TAFIS);
SU2 = DALL L1 L3 C1 L2;
SU3 = DALL L2 L4 L5 C2;
SU4 = DALL L6 L5 C3 L7;
SU5 = L4 TRAN 7 (0. (1.0*BDIM) 0.);
L8 = COTE 3 SU5; Y1 = COOR 2 (POINT L8 FINA);
P6 = 0. (Y1 + (BDIM/4.)) 0.;
P7 =BDIM (Y1 + (BDIM/4.)) 0.;
L9 = D N4 P7 P6;
SU6 = COUT L8 L9;
SU7 = L9 TRAN 8 (0. (3.5*BDIM) 0.);
L10 = COTE 3 SU7; Y1 = COOR 2 (POINT L10 FINA);
L11 = D 1 P3 P3D;
SUT = COUL TURQ (SU1 ET SU2 ET SU3 ET SU4 ET SU5 ET
                 SU6 ET SU7);
ELIM 1.E-8 SUT;
L1 = (CONT SUT) ELEM APPU (SUT POIN
     DROI (0. 0. 0.) (BDIM 0. 0.) 1.E-8);
L2 = (CONT SUT) ELEM APPU (SUT POIN
     DROI (BDIM 0. 0.) (BDIM Y1 0.) 1.E-8);
L3 = (CONT SUT) ELEM APPU (SUT POIN
     DROI (BDIM Y1 0.) (0. Y1 0.) 1.E-8);
L4 = (CONT SUT) ELEM APPU (SUT POIN
     DROI (0. Y1 0.) (0. 0. 0.) 1.E-8);
PINI3 = POIN L3 INIT; PFIN3 = POIN L3 FINA;
PMIL3 = (CHAN POI1 L3) DIFF (PINI3 ET PFIN3);
MOEXT = 1. / ((NBNO L3) - 1);
MOMIL = MOEXT * (NBNO PMIL3);
******
****** SOLUTION CONTRAINTE ET DEPLACEMENT
******
EPAI_T = 0.001;
NB_MUL = 21;
EPAI_C = EPAI_T * 1.E-6;
EPAI_I = (EPAI_T - (EPAI_C*NB_MUL))/(NB_MUL - 1);
ELEFIS = SUT ELEM APPU LARG P3;
ELEFIS = SUT ELEM APPU LARG ELEFIS;
ELEFIS = SUT ELEM APPU LARG ELEFIS;
TBEPAI = TABLE; TBEXCE = TABLE;
REPETER BC_MUL1 ((2*NB_MUL) - 1);
  III = (&BC_MUL1 - NB_MUL) / 2.;
  TBEXCE.&BC_MUL1 = (EPAI_I + EPAI_C)*III;
  SI (EGA 0. ((&BC_MUL1/2.) - (&BC_MUL1/2)) 1.E-10);
    TBEPAI.&BC_MUL1 = EPAI_I;
  SINON;
    TBEPAI.&BC_MUL1 = EPAI_C;
  FINSI;
FIN BC_MUL1;
REPETER BC_MUL2 ((2*NB_MUL) - 1);
  SI (EGA &BC_MUL2 1);
   MOD_MUL = MODE ELEFIS MECANIQUE ELASTIQUE
             DKT CONS (CHAI &BC_MUL2);
   MAT_MUL = MATE MOD_MUL YOUN 2.E11 NU 0.3
             EPAI TBEPAI.&BC_MUL2 EXCE TBEXCE.&BC_MUL2;
  SINON;
   MOD_MU1 = MODE ELEFIS MECANIQUE ELASTIQUE
             DKT CONS (CHAI &BC_MUL2);
   MAT_MU1 = MATE MOD_MU1 YOUN 2.E11 NU 0.3
             EPAI TBEPAI.&BC_MUL2 EXCE TBEXCE.&BC_MUL2;
   MOD_MUL = MOD_MUL ET MOD_MU1;
   MAT_MUL = MAT_MUL ET MAT_MU1;
  FINSI;
FIN BC_MUL2;
MOD_R = MODE (SUT DIFF ELEFIS) MECANIQUE ELASTIQUE DKT;
MAT_R = MATE MOD_R YOUN 2.E11 NU 0.3 EPAI EPAI_T;
MO1 = MOD_MUL ET MOD_R;
MA1 = MAT_MUL ET MAT_R;
BL1 = BLOQ UY RX  (L7 ET L11);
BL2 = BLOQ UX RY L4;
BL3 = BLOQ UZ RZ L3;
RI = BL1 ET BL2 ET BL3 ET (RIGI MA1 MO1);
FO2 = ((FORC FY MOEXT (PINI3 ET PFIN3)) ET
      (FORC FY MOMIL PMIL3))*5.e3;
FO3 = FO2;
DEPTO = RESO RI FO3 ;
REATO = REAC DEPTO RI;
SIGTO = SIGMA MA1 MO1 DEPTO ;
******************************
SUPTAB = TABLE ;
SUPTAB.'OBJECTIF' = MOT 'J';
SUPTAB.'LEVRE_SUPERIEURE' = (cont sut) ELEM comp  p3 p1;
SUPTAB.'FRONT_FISSURE' = P3 ;
SUPTAB.'MODELE' = MO1;
SUPTAB.'CARACTERISTIQUES' = MA1;
SUPTAB.'SOLUTION_RESO' = DEPTO;
SUPTAB.'CHARGEMENTS_MECANIQUES' = FO3;
SUPTAB.'ELEMENT_MULTICOUCHE' = ELEFIS ELEM 1;
SUPTAB.'COUCHE' = 7;
G_THETA SUPTAB;
* RECUPERATION DU RESULTAT ET COMPARAISONS ELEMENT 25 ;
*                                                     ;
G   = SUPTAB.'RESULTATS'.'GLOBAL'                     ;
Kth = 50.1E5                                          ;
K = (2.E11 * G)**0.5                                  ;
mess '  '                                             ;
mess '  '                                             ;
mess 'taux de restitution energetique  G :' G         ;
mess 'solution calculee             Kcal :' K         ;
mess 'solution analytique           Kthe :' Kth       ;
*                   CODE FONCTIONNEMENT               ;
RESI = abs (( k - Kth ) / Kth )                       ;
SI (RESI <  5E-2)                                     ;
    ERRE  0                                           ;
SINO                                                  ;
    ERRE  5                                           ;
FINSI                                                 ;
*RECUPERATION DU RESULTAT ET COMPARAISONS ELEMENT 117 ;
*                                                     ;
SUPTAB.'ELEMENT_MULTICOUCHE' = ELEFIS ELEM 117;
G_THETA SUPTAB;
G   = SUPTAB.'RESULTATS'.'GLOBAL'                     ;
Kth = 50.1E5                                          ;
K = (2.E11 * G)**0.5                                  ;
mess '  '                                             ;
mess '  '                                             ;
mess 'taux de restitution energetique  G :' G         ;
mess 'solution calculee             Kcal :' K         ;
mess 'solution analytique           Kthe :' Kth       ;
*                   CODE FONCTIONNEMENT               ;
RESI = abs (( k - Kth ) / Kth )                       ;
SI (RESI <  5E-2)                                     ;
    ERRE  0                                           ;
SINO                                                  ;
    ERRE  5                                           ;
FINSI                                                 ;
*RECUPERATION DU RESULTAT ET COMPARAISONS ELEMENT 118 ;
*                                                     ;
SUPTAB.'ELEMENT_MULTICOUCHE' = ELEFIS ELEM 118;
G_THETA SUPTAB;
G   = SUPTAB.'RESULTATS'.'GLOBAL'                     ;
Kth = 50.1E5                                          ;
K = (2.E11 * G)**0.5                                  ;
mess '  '                                             ;
mess '  '                                             ;
mess 'taux de restitution energetique  G :' G         ;
mess 'solution calculee             Kcal :' K         ;
mess 'solution analytique           Kthe :' Kth       ;
*                   CODE FONCTIONNEMENT               ;
RESI = abs (( k - Kth ) / Kth )                       ;
SI (RESI <  5E-2)                                     ;
    ERRE  0                                           ;
SINO                                                  ;
    ERRE  5                                           ;
FINSI                                                 ;
FIN;



















