* fichier :  ther3.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

*******************************************
*    Test ther3.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*-----------------------------------------*
*                 THER3                   *
*                                         *
*   TEST DES CL DE TEMPERATURE POUR       *
*   PYR5 CUB8 TET4 PRI6 TRIDIM:           *
*-----------------------------------------*
*                                         *
*    TEMPERATURE IMPOSEE + CONVECTION     *
*                        + FLUX           *
*                        + SOURCE         *
*                                         *
* Comme dans le test THER1, cet exemple   *
* permet de tester les conditions aux li- *
* -mites de température pour des éléments *
* massifs linéaires PYR5, CUB8, TET4, PRI6*
*                                         *
* Un parallélépipède rectangle constitué  *
* des éléments cités plus haut est soumis *
* à une température imposée à sa face su- *
* -périeure et à une condition de flux im-*
* -posée, une condition de source volu-   *
* -mique ou une condition de convection   *
* forcée.                                 *
*                                         *
* Les résultats sont présentés sous forme *
* d'isotempérature dans une tranche verti-*
* -cale du parallélépipède. Les courbes   *
* doivent etre parallèles aux faces supé- *
* -rieure et inférieure.                  *
*                                         *
*            21 DECEMBRE 1988.            *
*-----------------------------------------*
*
*  Disposition des noeuds des éléments :
*
*          A4________F4_________D4
*          /         /          /|
*         /         /          / |
*       B4_________E4_________C4 D3
*        |         |          | /
*        |  6PYR5  |  12TE4   |/
*       B3_________E3_________C3
*
*   IN: noeud milieu de
*       (AN BN EN FN AN+1 BN+1 EN+1 FN+1)
*
*          A3________F3_________D3
*          /         /          /|
*         /         /          / |
*       B3_________E3_________C3 D2
*        |         |          | /
*        |  1CUB8  |  6PYR5   |/
*       B2_________E2_________C2
*
*   JN: noeud milieu de
*       (DN CN EN FN DN+1 CN+1 EN+1 FN+1)
*
*          A2________F2_________D2
*          /         /          /|
*         /         /          / |
*       B2_________E2_________C2 D1
*        |         |          | /
*        |  6PYR5  |   2PRI6  |/
*       B1_________E1_________C1
*
*
*-----------------------------------------*

*---  OPTIONS GENERALES DE CALCUL:
TITR 'THER3 - C.L. TEMPERATURE - ELEMENTS LINEAIRES 3D' ;
OPTION DIME 3 ELEM SEG2 ;
TEMPS ;

*---  CREATION DE LA GEOMETRIE:
*     POINTS SUPPORTS DES ELEMENTS
 A1=0.  0.   0.;  B1=1.  0.   0.;
 C1=1.  3.   0.;  D1=0. 3.   0.;
 A2=0.  0.   2.;  B2=1.  0.   2.;
 C2=1.  3.   2.;  D2=0. 3.   2.;
 A3=0.  0.   4.;  B3=1.  0.   4.;
 C3=1.  3.   4.;  D3=0. 3.   4.;
 A4=0.  0.   6.;  B4=1.  0.   6.;
 C4=1.  3.   6.;  D4=0. 3.   6.;
 E1=1.  1.5  0.;  F1=0.  1.5  0.;
 E2=1.  1.5  2.;  F2=0. 1.5  2.;
 E3=1.  1.5  4.;  F3=0.  1.5  4.;
 E4=1.  1.5  6.;  F4=0. 1.5  6.;
 I1= .5  .75 1.;  I3= .5  .75 5.;
 J1= .5 2.25 1.;  J2=.5 2.25 3.;
 J3= .5 2.25 5.;

*---  CREATION DES PYRAMIDES A 5 NOEUDS:
PP01 = MANU PYR5 A1  B1  E1  F1  I1 ;
PP02 = MANU PYR5 E1  F1  F2  E2  I1 ;
PP03 = MANU PYR5 E2  F2  A2  B2  I1 ;
PP04 = MANU PYR5 A2  B2  B1  A1  I1 ;
PP05 = MANU PYR5 B1  E1  E2  B2  I1 ;
PP06 = MANU PYR5 A1  F1  F2  A2  I1 ;

PP07 = MANU PYR5 D2  C2  E2  F2  J2 ;
PP08 = MANU PYR5 E2  F2  F3  E3  J2 ;
PP09 = MANU PYR5 E3  F3  D3  C3  J2 ;
PP10 = MANU PYR5 D3  C3  C2  D2  J2 ;
PP11 = MANU PYR5 C2  E2  E3  C3  J2 ;
PP12 = MANU PYR5 D2  F2  F3  D3  J2 ;

PP13 = MANU PYR5 A3  B3  E3  F3  I3 ;
PP14 = MANU PYR5 E3  F3  F4  E4  I3 ;
PP15 = MANU PYR5 E4  F4  A4  B4  I3 ;
PP16 = MANU PYR5 A4  B4  B3  A3  I3 ;
PP17 = MANU PYR5 B3  E3  E4  B4  I3 ;
PP18 = MANU PYR5 A3  F3  F4  A4  I3 ;

*VOLUME COMPOSE DES 18 PYRAMIDES A 5 NOEUDS
VOL1 = PP01 ET PP02 ET PP03 ET PP04 ET PP05 ET PP06 ET PP07 ET PP08 ET PP09 ET PP10 ET PP11 ET PP12 ET PP13 ET PP14 ET PP15 ET PP16 ET PP17 ET PP18 ;

*---  CREATION DES PRISMES A 6 NOEUDS:
PR01 = MANU PRI6 E1 D1 F1 E2 D2 F2 ;
PR02 = MANU PRI6 E1 C1 D1 E2 C2 D2 ;

*  VOLUME COMPOSE DES 2 PRISMES A 6 NOEUDS
VOL2 = PR01 ET PR02 ;

*---  CREATION DES TETRAEDRES A 4 NOEUDS:
TT01 = MANU TET4 E3  D3  F3  J3 ;
TT02 = MANU TET4 E3  D3  C3  J3 ;
TT03 = MANU TET4 C3  D4  D3  J3 ;
TT04 = MANU TET4 C3  D4  C4  J3 ;
TT05 = MANU TET4 C4  F4  E4  J3 ;
TT06 = MANU TET4 C4  F4  D4  J3 ;
TT07 = MANU TET4 E3  F4  E4  J3 ;
TT08 = MANU TET4 E3  F4  F3  J3 ;
TT09 = MANU TET4 E3  C4  C3  J3 ;
TT10 = MANU TET4 E3  C4  E4  J3 ;
TT11 = MANU TET4 F3  D4  D3  J3 ;
TT12 = MANU TET4 F3  D4  F4  J3 ;

*VOLUME COMPOSE DES 12 TETRA A 4 NOEUDS
VOL3 = TT01 ET TT02 ET TT03 ET TT04 ET TT05 ET TT06 ET TT07 ET TT08 ET TT09 ET TT10 ET TT11 ET TT12 ;

*---  CREATION DU CUBE A 8 NOEUDS:
VOL4 = MANU CUB8 B2 E2 F2 A2 B3 E3 F3 A3 ;

*--- REUNION DES VOLUMES ELEMENTAIRES:
VOLTOT = VOL1 ET VOL2 ET VOL3 ET VOL4 ;

*---  POST-TRAITEMENT: TRACE DU MAILLAGE
OEIL = 100. 100. 100. ;
TITR 'ELEMENTS PYR5 CUB8 TET4 PRI6 : MAILLAGE' ;
SI(NEG GRAPH 'N');
    TRAC 'QUAL' OEIL VOLTOT ;
FINSI;

VOLSOUR = PP13 ET PP14 ET PP15 ET PP16 ET PP17 ET PP18 ET TT01 ET TT02 ET TT03 ET TT04 ET TT05 ET TT06 ET TT07 ET TT08 ET TT09 ET TT10 ET TT11 ET TT12 ;

*---  L_INF ETANT BASEE SUR LES POINTS:
*     A1 B1 E1 C1 D1 F1
DD1 = D 1 A1 B1 ;
DD2 = D 1 B1 E1 ;
DD3 = D 1 E1 C1 ;
DD4 = D 1 C1 D1 ;
DD5 = D 1 D1 F1 ;
DD6 = D 1 F1 A1 ;

L_INF = DD1 ET DD2 ET DD3 ET DD4 ET DD5 ET DD6 ;

*---  SURFACE INFERIEURE:
S_INF = SURF L_INF PLANE ;

*---  L_SUP ETANT BASEE SUR LES POINTS:
*     A4 B4 E4 C4 D4 F4

LL1 = D 1 A4 B4 ;
LL2 = D 1 B4 E4 ;
LL3 = D 1 E4 C4 ;
LL4 = D 1 C4 D4 ;
LL5 = D 1 D4 F4 ;
LL6 = D 1 F4 A4 ;

L_SUP = LL1 ET LL2 ET LL3 ET LL4 ET LL5 ET LL6 ;

*---  SURFACE SUPERIEURE:
S_SUP = SURF L_SUP PLANE ;

*---  DONNEES DU PROBLEME DE THERMIQUE:
*---  MODELISATION:
MOD1 = MODE VOLTOT THERMIQUE ISOTROPE   ;
MOD2 = MODE S_SUP  THERMIQUE CONVECTION ;

*--DONNEE DES CARACTERISTIQUES DU MATERIAU:
KC = 100. ;
MAT1 = MATE MOD1 'K' KC ;
HC = 15. ;
MAT2 = MATE MOD2 'H' HC ;

*--- CREATION DES MATRICES DE CONDUCTIVITE:
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;

*---  TEMPERATURES IMPOSEES: BLOQUE + DEPI
BB1 = BLOQ S_INF 'T' ;
TI = 10. ;
EE1 = DEPI BB1 TI ;

*---  FLUX CONVECTIF A LA SURFACE:
TEX = 200. ;
CNV1 = CONVECTION MOD2 MAT2 'T' TEX ;

*---  FLUX IMPOSE:
PHI = 20. ;
FLU1 = FLUX MOD1 PHI S_SUP ;

*---  SOURCE VOLUMIQUE:
QVOL = 2. ;
S1 = SOUR MOD1 QVOL VOLSOUR ;

*--  ASSEMBLAGE DES TERMES DE CONDUCTIVITE:
CCC1 = CND1 ET CND2 ET BB1 ;
CCC2 = CND1 ET BB1 ;

*ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS:
FFF1 = EE1 ET CNV1 ;
FFF2 = EE1 ET FLU1 ;
FFF3 = EE1 ET S1 ;

*---  1ERE RESOLUTION:
*          TEMPERATURE IMPOSEE + CONVECTION
CHTER1 = RESO CCC1 FFF1 ;

*--- POST-TRAITEMENT
Z1 = COOR 3 F1;
Z2 = COOR 3 F4;

*Valeur Théorique de la température en Z2
TREF1=((KC * TI) + (HC * Z2 * TEX)) /  ((KC     ) + (HC * Z2      ))  ;
                         TETA1  = EXTR CHTER1 T F4 ;
RESI1=100. * (ABS((TETA1-TREF1)/TREF1));

MESS 'Temperature theorique :' TREF1' C';
MESS 'Temperature calculee  :' TETA1' C';
MESS '    Soit un ecart de : ' RESI1 '%';
RESITOT = PROG RESI1 ;
SAUT 1 LIGN ;

OEIL = 100. 0. 0. ;
SI(NEG GRAPH 'N');
    TITR 'ELEMENTS PYR5 CUB8 TET4 PRI6 : CONVECTION' ;
    TRAC OEIL VOLTOT CHTER1 ;
FINSI;

*---  2EME RESOLUTION:
*          TEMPERATURE IMPOSEE + FLUX
CHTER2 = RESO CCC2 FFF2 ;

TREF2= TI + (PHI * Z2 / KC );
             TETA2  = EXTR CHTER2 T F4 ;
RESI2=100. * (ABS((TETA2-TREF2)/TREF2));
MESS 'Temperature theorique :' TREF2' C';
MESS 'Temperature calculee  :' TETA2' C';
MESS '    Soit un ecart de : ' RESI2 '%';
RESITOT = RESITOT ET (PROG RESI2) ;
SAUT 1 LIGN ;

SI(NEG GRAPH 'N');
  TITR 'ELEMENTS PYR5 CUB8 TET4 PRI6 : FLUX';
  TRAC OEIL VOLTOT CHTER2 ;
FINSI;

*---  3EME RESOLUTION:
*          TEMPERATURE IMPOSEE + SOURCE
CHTER3 = RESO CCC2 FFF3 ;

TREF3= TI - (QVOL * (Z2**2) / (2 * KC)) +  (QVOL * (Z2**2) / (    KC));
                         TETA3  = EXTR CHTER3 T F4 ;
RESI3=100. * (ABS((TETA3-TREF3)/TREF3));
MESS 'Temperature theorique :' TREF3' C';
MESS 'Temperature calculee  :' TETA3' C';
MESS '    Soit un ecart de : ' RESI3 '%';
RESITOT = RESITOT ET (PROG RESI3) ;
SAUT 1 LIGN ;

SI(NEG GRAPH 'N');
  TITR 'ELEMENTS PYR5 CUB8 TET4 PRI6 : SOURCE';
  TRAC OEIL VOLTOT CHTER3 ;
FINSI;

*       CODE DE FONCTIONNEMENT
LIST  RESITOT;
SI((MAXI RESITOT) <EG 1.2);
   ERRE  0;
SINO;
   ERRE  5;
FINSI;
FIN;



