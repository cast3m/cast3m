* fichier :  fiabi2.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
*           Test fiabi1.dgibi: Jeux de données        *
*           ---------------------------------         *
*
* CAS TEST DU 01/04/10     PROVENANCE : TEST
SAUT PAGE;
*
*******************************************************
*                                                     *
*  TEST FIABI1                                        *
*                                                     *
*  TIGE EN TRACTION - Méthodes F.O.R.M./QUADRATURE    *
*                                                     *
*                                                     *
*      La tige est soumise à une traction P. L'aire   *
*  de la section droite est A. La limite élastique    *
*  de la résistance est FY.                           *
*                                                     *
*      On se propose de calculer la probabilité de    *
*  défaillance de la tige à la rège de dimensionnement*
*  FY * A - P > 0.                                    *
*                                                     *
*      Dans ce premier test, on ne s'appuie sur aucun *
*  maillage. l'objectif est de comparer deux méthodes *
*  de fiabilité (F.O.R.M.) et de sensibilité          *
*  (QUADRATURE), en terme de précision                *
*                                                     *
*******************************************************
*
*Variables testées et leur loi de distribution:

* Fy :limite élastique de l'acier ( MPa )
*                         Log-Normale (272.72E6 , 16.36E6)
* P : traction appliquée ( MN )
*                         Log-Normale (70E6 , 15E6)
* A est tout d'abord fixée (m²):
                                A = 0.42 ;

*======================================================*
*======================================================*
*      CALCUL NUMERIQUE                                *
*======================================================*
*======================================================*


*Données préalables:

MESS 'Le domaine de défaillance est défini par :';
SAUT 1 LIGN;
MESS 'FY * A - P > 0';
SAUT 1 LIGN;

OPTI ECHO 0 ;

*Ecrivons la densité conjointe de probabilité de la loi
*normale centrée réduite comme une évolution:

Mfy = 272.72E6;   Sfy = 16.36E6;
Mp = 70.E6;       Sp = 15.E6;
A = 0.42 ;

* D'ou l'indice de fiabilité
*
   MR = log (A*Mfy/((1+((Sfy/Mfy)**2))**0.5));
   MS = log (MP/((1+((SP/MP)**2))**0.5));
   SR = (log(1+(((Sfy/Mfy)**2)))**0.5);
   SS = (log(1+(((SP/MP)**2)))**0.5);
   bta = ((MR-MS)/(((SR**2)+(SS**2))**0.5));

* On intègre sur le domaine :

   Lbta = PROG ((-1)*100) PAS ((100-bta)/100000) ((-1)*bta);
   LDens = ((EXP(0.5*(-1)*(Lbta**2))) / ((2*PI)**0.5));
   LFa = EVOL MANU  'X' Lbta 'DENSITE' LDens;

*L'opérateur SOMM calcule l'intégrale des ordonnées
*de cette densité sur ses abscisses par la méthode
*des trapèzes.

   PfL =(INTG  LFa ('ABSO')) ;
   PfL =EXTR PfL 1;

*======================================================*
*======================================================*
*      Méthode de fiabilité :                          *
*      APPROXIMATION AU PREMIER ORDRE DE L'ÉTAT-LIMITE *
*      (First Order Reliability Method)                *
*======================================================*
*======================================================*

DEBPROC PERF lr1*listreel;
Fy=extr 1 lr1;
P=extr 2 lr1;
*A = O.42;
limit = ((Fy*0.42) - P)/1e6;
FINPROC limit;
*
DEBPROC intmci lr1*listreel;
txv0=table;
h1=0.1;
lrx1=prog 0 0 ;
lr2=prog;
nbva=dime lr1;

 REPETER bou1 nbva;
 lrx2=lrx1 * 1.;
 remp lrx2 &bou1 h1;
* list lrx2;
 lr2 =lr2 et (prog ((PERF (lr1 - lrx2)) - (PERF (lr1 + lrx2))/(-2*h1)));
 FIN bou1;

FINPROC lr2;
*
tab=table;
*tab . form = 'mexcel';
tab . param_optimisation =table;
tab . param_optimisation . methode = sta;
tab . param_optimisation . t0 =0.8;
tab . param_optimisation . s0 =0.3;
tab . param_optimisation . vxmin =prog  2E8 5E7;
tab . param_optimisation . vxmax =prog 3E8 9E9 ;
tab . param_optimisation . vcmax = prog 0;
tab . param_optimisation . maxiteration= 100;
tab . noms_des_variables = mots xFy xP;
tab . max_iteration = 100;

tab . fct_limite=table;
tab . fct_limite . 1 = 'PERF';
tab . grad_fct_limite=table;
tab . grad_fct_limite . 1 = 'INTMCI';
tab . param_va =table;
tab . param_va . 1 =table;
tab . param_va . 2 =table;

tab . param_va . 1 . typva  =loi_lognormale;
tab . param_va . 2 . typva  =loi_lognormale;

tab . param_va . 1 . MOYENNE    = 272.72E6;
tab . param_va . 1 . ECART_TYPE = 16.36E6;
tab . param_va . 2 . MOYENNE    = 70E6;
tab . param_va . 2 . ECART_TYPE = 15E6;

tab . points_initiaux = table;
tab . points_initiaux . 1 = prog 250E6 70E6;
tab . resu=table;

*appel a fiabilite
*list mexcel;

fiabili tab;
list tab . resu . 1 . indfiab;
list tab . resu . 1 . 'XFY ';
list tab . resu . 1 . 'XP  ';

*Calcul de la probabilité de défaillance en
*fonction de l'indice de fiabilité BETA

BET = (extr  (tab . resu . 1 . indfiab)
        (dime(tab . resu . 1 . indfiab)));

LBETA = PROG ((-1)*10) PAS ((100-BET)/1000) ((-1)*BET);
LDensF = ((EXP(0.5*(-1)*(LBETA**2))) / ((2*PI)**0.5));
LFF = EVOL MANU  'X' LBETA 'DENSITE' LDensF;
*dess LFF;
PfF =INTG  LFF ('ABSO') ;
PfF =EXTR PfF 1;

ERFL = (ABS (PfL - PfF))/(MAXI (PROG PfL PfF))*100;

*======================================================*
*======================================================*
*      Méthode de sensibilité :                        *
*      APPROXIMATION PAR QUADRATURE                    *
*======================================================*
*======================================================*

*================================
*CALCULS DES POINTS D'INTEGRATION
*================================

NP_FY = 4;
NP_P = 4;

TAB_FY = QUADRATU 'LOGN' 272.72E6 16.36E6 NP_FY ;
TAB_P = QUADRATU 'LOGN' 70.E6 15.E6 NP_P;

*=====================
*DEFINITION DES TABLES
*=====================

TABLIM = TABLE;
TabGLO = TABLE;
ITEM = 0 ;
REPE Bc_FY NP_FY;
 REPE Bc_P NP_P;

     ITEM = ITEM + 1 ;
     TabGLO .ITEM = TABLE;
     TabGLO .ITEM. 'POIDS' =
        TAB_FY .&Bc_FY. 'POIDS' *
        TAB_P .&Bc_P. 'POIDS' ;

     TabGLO .ITEM. 'FY' = TAB_FY .&Bc_FY. 'POINT';
     TabGLO .ITEM. 'P' = TAB_P .&Bc_P. 'POINT';

 FIN Bc_P;
FIN Bc_FY;

*=================================================
* PROCEDURE DEFINISSANT LA FONCTION DE PERFORMANCE
*=================================================

DEBPROC PERF FY P;
A = 0.42;
G = ((FY*A) - P);
FINPROC G;

list cas;
*=========================================
* CALCUL DE LA PROBABILITE DE DEFAILLANCE
*=========================================


N = DIME TABglo;
list N;
TabRES = TABLE;
ITEM = 0;
REPE BOUC N;
*Mess &bouc ;
 TabRES .&BOUC = TABLE;
 TabRES .&BOUC. POIDS = TABGLO .&BOUC. POIDS;
 TabRES .&BOUC. G  = PERF TABglo .&BOUC. FY
                          TABglo .&BOUC. P;
FIN BOUC;

TABG = table;

REPE BOUC N;

 TABG .&BOUC = table;
 TABG .&BOUC. POIDS = TabRES .&BOUC. POIDS;
 TABG .&BOUC. POINT = TabRES .&BOUC. G;

FIN BOUC;

mog sig syg apg = PARASTAT TABG;

*Calcul de Pf

PfQ = PROB mog sig syg apg 0.;
betaQ = INDIBETA 0. 7. pfQ;

ERQL = (ABS(PfL - PfQ))/(MAXI (PROG PfL PfQ))*100;

*=======================================================*
*                   Affichage des resultats             *
*=======================================================*

OPTI ECHO 1 ;

MESS 'RESULTATS POUR UNE LOI LOG-NORMALE';
MESS '******************************';
SAUT 1 LIGN;
MESS ' VALEURS PAR INTEGRATION NUMERIQUE ';
MESS ' --------------------------------- ';
SAUT 1 LIGN;
MESS ' L indice de fiabilite est :  ' bta;
MESS ' La probabilite associee est :'PFN;
SAUT 1 LIGN;
MESS ' RESULTATS TEST F.O.R.M. ';
MESS ' ----------------------- ';
SAUT 1 LIGN;
MESS ' L indice de fiabilite est :  '  BET;
MESS ' La probabilite associee est :' PFF;
SAUT 1 LIGN;
MESS '    Soit un ecart de :' ERFL '%';
SAUT 1 LIGN;
SAUT 1 LIGN;
MESS ' RESULTATS TEST QUADRATURE ';
MESS ' ------------------------- ';
SAUT 1 LIGN;
MESS ' L indice de fiabilite est :  ' betaQ;
MESS ' La probabilité associee est :' PFQ;
SAUT 1 LIGN;
MESS '    Soit un ecart de :' ERQL '%';
SAUT 1 LIGN;

*
*========================================================*
*                  code fonctionnement                   *
*========================================================*
*
* L'écart maximum entre valeurs approchées et numérique
* doit etre inferieure a 5%.
*
SAUT 2 LIGNE;
SI ((ERFL <EG 5) OU (ERQL <EG 5)) ;
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;










