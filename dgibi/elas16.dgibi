* fichier :  elas16.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
graph='N';
saut page;
********************************************************************
* Etude d'une poutre sur appuis simple chargee de facon repartie
* (on en etudie que la demi-longueur)
*
*
*  symetrie                           appuis simple
*
*     |-------------------------------------*
*                                           ^
*     p1                                    p2
********************************************************************
* Notations:
*
* t =epaisseur
* L =longueur totale
* I =moment d'inertie
*
* q =charge par unite de longueur
* e =module d'Young
* nu=Poisson
********************************************************************
* Solution theorique:
*
* deflection/(qL**4/24eI) =  5/16 + (t/L)**2(1+nu)(1/2)
********************************************************************
* Objectif du test:
*
* on cherche a verifier la solution theorique pour la suite
* d'epaisseur reduite suivante:
*
* t/L = .0001 .001 .01 .1  1. 10.
********************************************************************
*
* 1) option de calcul
opti dime 3 elem seg2;
*
* 2) quelques donees et calcul de quelques constantes
*     pour la solution theorique
*
q=1.; e=1.e10; nu=.3; L=1.;
*
qL4s24e=(q * (L ** 4))/(24. * e);
cinqs16=5. / 16.;
nup1s2 = (nu + 1.)/2.;
*
* 3) maillage
*
nel=50;
p1 = 0 0 0; p2 = (L/2.) 0 0;
d1= p1 d nel p2;
*
* 4) modele et materiaux
*
modti=MODE d1 mecanique elastique timo;
mater=MATE modti 'YOUN' e 'NU' nu;
*
* 5) chargement reparti sur la poutre
*
j=0; repe loopel nel; j=j+1;
  elj=d1 elem j;
  pee=elj point init;
  pde=elj point final;
  vec=pde moin pee;
  xl=norm vec;
*
  valf1 = (q * xl) * 0.5 ;
  valf2 = valf1;
*
  chrtij=(manu 'CHPO' pee 1 fz valf1 nature discret)
     et  (manu 'CHPO' pde 1 fz valf2 natu discret);
*
  si (j ega 1); chrti=chrtij;
  sinon;        chrti=chrtij et chrti;
  finsi;
*
fin loopel;
*
* 6) bloquage
*
bloqtot=bloq d1 'UY' 'RX' 'RZ';
bloqp1 =bloq p1 'UX' 'RY';
bloqp2 =bloq p2 'UZ';
*
* 7) definition des differentes epaisseurs
*
tt=prog .0001 .001 .01 .1  1. 10.;
nt=dime tt;
*
* 8) loop sur les epaisseurs
*
cerre=0;
j=0; repe loopep nt; j=j+1;
  t=extr tt j;
*
** 8.1) etablissement des caracteristiques geometriques
*
  b=1.;
  section=b*t; inery=(b/12) * (t ** 3);
  inerz=1.d+10; torsion=1.d+10;
  carac= 'CARA' modti 'SECT' section 'INRY' inery 'INRZ' inerz
                       'TORS' torsion 'SECZ' section;
  mc=mater et carac;
*
** 8.2) calcul de la rigidite
*
  rigti=rigi modti mc;
  rigtti=rigti et bloqtot et bloqp1 et bloqp2;
*
** 8.3) solution
*
  depti=reso rigtti chrti;
*
** 8.4) deflections theoriques en p1
*
  qL4s24ei=qL4s24e / inery;
  theorie=cinqs16 + (((t / L) ** 2) * nup1s2);
  calcul=(extr depti 'UZ' p1) / qL4s24ei;
  erreur=abs ((calcul - theorie) / theorie);
*
** 8.5) comparaison et message
*
  mess 'deflection pour t/L=' (t/L);
  mess 'calcul,theorie,erreur:  ' calcul theorie erreur;
  mess '***********************';
*
** 8.6) erreur
*
  si (erreur > 5.d-4); cerre=cerre+1; finsi;
*
fin loopep;
si (cerre > 0); erre 5;
sinon;          erre 0; finsi;
fin;








