* fichier :  elas2.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas2.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

*______________________________________________________
*   TEST ELAS2
*
* Un cylindre est pincé à une de ses extrémités,
* l'autre extrémité étant encastrée.
*
* Pour des raisons de symétrie seul 1/4 de la structure
* a est modélisé.
*
* Les conditions aux limites sont imposées pour
* respecter les symétries.
* Une force ponctuelle est appliquée diametralement.
*
* Le déplacement en X d'un point de la base est comparé
* au déplacement théorique (déplacement nul puisque
* encastré).
* Le déplacement en Z du point d'application de force
* est comparé à la valeur théorique (-1.140)
*______________________________________________________

OPTION DIME 3 ELEM TRI3 ;
TITRE 'CYLINDRE PINCE A BORDS ENCASTRES' ;
TEMPS ;
*-----------------------------------------------------
* DEFINITION DE LA GEOMETRIE
*-----------------------------------------------------
P1=7 0 0 ;P2=0 0 7 ;C1=0 0 0 ;VECT=0 70 0 ;
LIG1=C 6 P1 C1 P2 ;SURF=LIG1 TRAN 30 VECT ;
LIG1 LIG2 LIG3 LIG4=COTE SURF ;P3=LIG2 POIN FINA ;
*
OEIL1 = 100 100 100;
SI(NEG GRAPH 'N');
    TRAC OEIL1 SURF 'QUAL';
FINSI;
*-----------------------------------------------------
* DEFINITION DU MODELE
*-----------------------------------------------------
MOD1=MODE SURF MECANIQUE ELASTIQUE DKT ;
MAT1=MATE MOD1        YOUN 3.E7   NU 0.3 ;
EPEE=CARA MOD1        EPAI 0.1 ;
MAT1=MAT1 ET EPEE;
*-----------------------------------------------------
*** CALCUL RIGIDITE ET MASSE ;
*-----------------------------------------------------
*
RIG1=RIGI MOD1 MAT1;
*
*-----------------------------------------------------
* CONDITIONS AUX LIMITES
*-----------------------------------------------------
ENC1=BLOQ LIG4 UZ ;ENC2=BLOQ LIG4 RX ;ENC3=BLOQ LIG4 RY ;
ENC4=BLOQ LIG2 UX ;ENC5=BLOQ LIG2 RY ;ENC6=BLOQ LIG2 RZ ;
ENC7=BLOQ LIG3 UY ;ENC8=BLOQ LIG3 RX ;ENC9=BLOQ LIG3 RZ ;
ENC10=BLOQ LIG1 DEPL ROTA ;
ENC11=ENC1 ET ENC2 ET ENC3 ET ENC4 ET ENC5 ET ENC6 ET
ENC7 ET ENC8 ET ENC9 ET ENC10 ;
*------------------------------------------------------
* FORCES
*------------------------------------------------------
F1=0 0 -2500 ;
EFOR=FORC F1 P3 ;
RIG2=RIG1 ET ENC11;
*------------------------------------------------------
* RESOLUTION ET RESULTATS
*------------------------------------------------------
DE1=RESOU RIG2 EFOR;
D1X = EXTR DE1 UX P1 ;
D3Z = EXTR DE1 UZ P3 ;

DEFO1= DEFO SURF DE1;
DZ   = EXCO DE1 UZ;
SI (NEG GRAPH 'N') ;
  TRAC OEIL1 'CACH' DEFO1 DZ;
FINSI;

*------------------------------------------------------
* SORTIE DES RESULTATS
*------------------------------------------------------
SAUT PAGE ;
MESS ' DEPLACEMENT EN X DE LA BASE     VALEUR THEORIQUE :  0.00' ;
MESS ' DEPLACEMENT EN X DE LA BASE     VALEUR CALCULEE  : ' D1X  ;
SAUT 2 LIGNE ;
MESS ' DEPLACEMENT EN Z THEORIQUE :  -1.140' ;
MESS ' DEPLACEMENT EN Z CALCULE   : ' D3Z   ;
TEMPS ;
*              CODE FONCTIONNEMENT
DZREF=-1.14;
RESI1=ABS((D3Z-DZREF)/DZREF);
SI( RESI1 <EG 6E-4 );
    ERRE 0;
SINO;
    ERRE 5;
FINSI;

DXREF= 0.0;
RESI2= ABS D1X;
SI( RESI2 <EG DXREF);
    ERRE 0;
SINO;
    ERRE 5;
FINSI;

FIN;




