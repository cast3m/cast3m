* fichier :  ccar1.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************
**
**  --- 10 OCTOBRE 1997 ---
**
**   TEST CAVITE CARRE
**
**   teste NS(SUPG) DUDW en IMPL
**   Formulation QUADR P0 et P1

GRAPH = 'N'                   ;
COMPLET = FAUX ;

SI ( COMPLET ) ;
ds1=0.02 ; ds2=0.2 ;
ds1=0.02 ; ds2=0.1 ;
SINON        ;
err1=5.e-5;
ds1=0.04 ; ds2=0.3 ;
FINSI ;



 DEBPROC TEST KPRESS*MOT TYPK*MOT MACRO*MOT GRAPH*MOT ;
 option dime 2 elem TYPK ;

 p1= 0 0 ; p12=0.5 0. ; p2= 1 0 ;

 ab=p1 d dini ds1 dfin ds2 p12 d dini ds2 dfin ds1 p2 ;
 mt= ab trans dini ds1 dfin ds2 (0 0.5) trans dini ds2
 dfin ds1 (0 0.5) ;
 bc=cote 2 mt ; cd=cote 3 mt ; da=cote 4 mt ;
 AA=bc moin (0.5 0.) ;
 BB=ab plus (0. 0.5) ;
 elim (AA et BB et mt) 1.e-3 ;

 mt = mt chan quaf ;
 $mt=mode mt 'NAVIER_STOKES' MACRO ;

EPSS=1.e-8;
MU  =1. ;
RO= 400. ;
DT=1. ;


   RV= eqex   'NITER' 5 'OMEGA' 0.9
  'OPTI' 'EF' 'IMPL' 'SUPG' KPRESS
  ZONE  $mt       OPER    NS 1. 'UN' (MU/RO)    INCO 'UN'
  ZONE  $mt       OPER    DUDW EPSS       INCO 'UN'
  CLIM
  UN UIMP CD   1. UN VIMP CD 0.  UN UIMP DA   0. UN VIMP DA 0.
  UN UIMP AB   0. UN VIMP AB 0.  UN UIMP BC   0. UN VIMP BC 0. ;

rv.inco= table inco ;
rv.inco.un =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.pres =  kcht $mt  scal KPRESS 1.e-5  ;

 exec rv ;

 AA= chan AA QUAF ;
 BB= chan BB QUAF ;
 $AA=mode AA 'NAVIER_STOKES' MACRO ;
 $BB=mode BB 'NAVIER_STOKES' MACRO ;

srti=doma $AA 'MAILLAGE'        ;
srth=doma $BB 'MAILLAGE'        ;
evolV = EVOL 'CHPO' (rv.'INCO'.'UN') UX (srti  ) ;
evolH = EVOL 'CHPO' (rv.'INCO'.'UN') UY (srth  ) ;
evx=extr evolV 'ORDO' ;
evy=extr evolV 'ABSC' ;
evolV= evol 'MANU' 'Vitesse' evx 'Hauteur' evy ;
 list evx ;
rv.'EVOLV'=evolV ;
rv.'EVOLH'=evolH ;

si ('EGA' graph 'O' );
TAB1=TABLE;
TAB1.'TITRE'=TABLE ;
TAB1 . 1 ='MARQ      REGU '     ;
TAB1.'TITRE' . 1 = mot 'Composante_UX '     ;
DESS evolV 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;

TAB1.'TITRE' . 1 = mot 'Composante_UY '     ;
DESS evolH 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;
 c1=vect (rv.inco.'UN') 0.3 ux uy jaune ;
 trace c1 mt ;

finsi ;

FINPROC RV ;

 RV= test 'CENTREP0' 'QUA8' 'QUAF' GRAPH ;
 evv= (rv.'EVOLV');
 evh= (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog 1.00000E-10 -2.22749E-02 -3.80702E-02 -5.75663E-02 -7.35660E-02
 -9.15793E-02 -9.26008E-02 -7.81172E-02 -5.32730E-02 -1.67005E-02
 -6.87561E-03 -1.41800E-03  5.91220E-02   .15384       .39620
 .65237       .99999 ;

 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP0 QUA8 QUADR ' er ;
*si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;


 RV= test 'CENTREP1' 'QUA8' 'QUAF' GRAPH ;
 evv= evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  1.00000E-10 -1.87211E-02 -3.55770E-02 -6.17477E-02 -8.77244E-02
  -.13311      -.17850      -.17244     -8.83844E-02 -2.12847E-02
  5.57624E-02   .10414       .17627       .26503       .47630
   .70881       .99999 ;

 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 QUA8 QUADR ' er ;
*si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP0' 'TRI6' 'QUAF' GRAPH ;
 evv=evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
1.00000E-10 -1.56447E-02 -2.79912E-02 -4.64291E-02 -6.41763E-02
 -8.99376E-02  -.11453      -.10648     -6.93449E-02 -2.83652E-02
 4.67842E-03  2.71908E-02  8.75278E-02   .18690       .41224
 .67173       .99999 ;

 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP0 TRI6 QUADR ' er ;
*si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP1' 'TRI6' 'QUAF' GRAPH ;
 evv= evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
 1.00000E-10 -2.34228E-02 -4.66537E-02 -7.48354E-02  -.10855
 -.14910      -.19298      -.17228     -9.81535E-02 -1.20163E-02
7.45734E-02   .11687       .19620       .27172       .48463
.70677       .99999 ;

 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 TRI6 QUADR ' er ;
*si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;


si ('EGA' graph 'O' );
titre 'Coupe oy à x=1/2 ' ;
TAB1=TABLE;
TAB1.'TITRE'= table ;
TAB1 . 1 ='TIRR MARQ CROI REGU ';
TAB1.'TITRE' . 1 ='P0-Q2';
TAB1 . 2 ='TIRM MARQ PLUS REGU ';
TAB1.'TITRE' . 2 ='P1-Q2 ';
TAB1 . 3 ='TIRR MARQ LOSA REGU ';
TAB1.'TITRE' . 3 ='P0-P2+bulle';
TAB1 . 4 ='TIRM MARQ CARR REGU ';
TAB1.'TITRE' . 4 ='P1-P2+bulle';
DESS EVV 'TITX' 'Oy'    LEGE  TAB1 ;
titre 'Coupe ox à y=1/2 ' ;
DESS EVH 'TITX' 'Ox'    LEGE  TAB1 ;
FINSI ;


FIN ;



















