* fichier :  test_infill.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
********************************
*
* Cas test modele global infill_uni
*
*  D. COMBESCURE - EMSI 1999
*
********************************
opti dime 2 elem seg2;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
*
p0 = 0.  0.;
p1 = 0.  1.;
bar1 = p0 d 1 p1;
*
Long = 0.25;
Larg = 0.25;
*
younbar = 30000.;
*
nubar = 0.;
*
Sbar = Larg*Long;
*
* Courbe contrainte axiale - déformation
*
Fmax = Sbar*2.1;
UXX1 = prog 0. (0.5*Fmax/(younbar*Sbar)) (2.*Fmax/(younbar*Sbar))
       (10.*Fmax/(younbar*Sbar)) (20.*Fmax/(younbar*Sbar));
FXX1 = prog 0. (0.5*Fmax/Sbar) (Fmax/Sbar) (1.2*Fmax/Sbar)
      (0.6*Fmax/Sbar);
DXFXTh = evol manu UXX1 (Sbar*FXX1);
*
DMAXP = 1. - ((extr FXX1 3 )/(younbar*(extr UXX1 3 )));
DELAP = (extr UXX1 2);
Efiss = (1. - DMAXP)*younbar;
*
XMONOP = PROG;
YMONOP = PROG;
j0 = 2;
repeter LAB1 ((DIME FXX1) - 2 );
   j0=j0+1;
   YY = (extr FXX1 j0);
   XX = (extr UXX1 j0) - ((extr FXX1 j0)/Efiss);
   XMONOP = INSE XMONOP (j0 - 2) (maxi (prog XX 0.));
   YMONOP = INSE YMONOP (j0 - 2) YY;
fin lab1;
MONOP = EVOL MANU XMONOP (SBAR*YMONOP);
*
modbar1 = mode bar1 mecanique elastique plastique infill_uni
               barr;
matbar1 = mate modbar1 YOUN younbar NU nubar SECT Sbar
     DELA delap DMAX dmaxp GAMM 0. GAMP 0.
     BETA 0.2 ALFA 0.5 TETA 1. MONO monop  RHO 0.;
*
BL0 = BLOQ DEPL p0;
BL1 = BLOQ UX p1;
BL2 = BLOQ UY p1;
DEPP1 = DEPI BL2 (-1.);
Protemp = prog 0. 10. 20. 101.;
ProHori = PROG 0. 0.002 -0.002 0.;
Procalc = PROG 0. pas 0.25 10.;
CharHori = CHAR DIMP DEPP1 (evol manu Protemp ProHori);
*
TAB1 = TABLE;
TAB1 . 'MOVA'          = 'RIEN';
TAB1 . 'TEMPS_CALCULES' = ProCalc ;
*TAB1 . 'CONVERGENCE_FORCEE ' = 'FAUX';
TAB1 .'MAXITERATION'  = 50;
*TAB1.'PRECISION' = 1.D-5;
TAB1 .'CHARGEMENT' = Charhori;
TAB1 .'DYNAMIQUE' = faux;
TAB1 .'MODELE' = modbar1;
TAB1 .'CARACTERISTIQUES'= matbar1;
TAB1 .'BLOCAGES_MECANIQUES' = BL0 ET BL1 ET BL2;
*
PASAPAS TAB1;
*
*
DEPT = TAB1.DEPLACEMENTS;
CONT = TAB1.CONTRAINTES;
TIME = INDEX DEPT;
DIMT = DIME TIME;
*
TIMP = PROG;
DEPX = PROG;
GXYT = prog;
EFFX = prog;
*
EFFXXX = PROG;
REPETER BOU1 (DIMT - 1);
  DEP1 = DEPT.(TIME.(&BOU1 + 1));
  CON1 = CONT.(TIME.(&BOU1 + 1));
  FOR1 = REAC (BL0) DEP1;
  DEPX = DEPX ET (PROG  (extr DEP1 UY p1));
  EFFX = EFFX ET (PROG (extr FOR1 FY p0));
  TIMP = TIMP ET (PROG (TIME.(&BOU1 + 1)));
FIN BOU1;
*
DXFX = EVOL MANU ((-1.)*DEPX) (EFFX);
TIFX =  EVOL MANU  TIMP (EFFX);
*
dreal = extr (ipol (prog -0.001) DEPX EFFX) 1;
dtheo =  extr (ipol (prog 0.001) UXX1 (Sbar*FXX1)) 1;
prec = abs (dreal - dtheo);
*
xlim0 = 1.D-7;
SI (prec > xlim0);
  'ERRE' 5;
SINON;
   'ERRE' 0;
FINSI;
*
FIN;
*











