* fichier toposurf_03.dgibi
************************************************************************
************************************************************************

************************************************************************
** Extraction of the 3D smoothed surface from a simple 3D topology
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** 3.0 2018/01/29 Updated to make it compatible with TOPOSURF V3.0
** 2.0 2018/01/26 Updated to make it compatible with TOPOSURF V2.0
** 1.0 2016/12/13 Original version compatible with TOPOSURF V1.0
************************************************************************

* General options
OPTI 'DIME' 3 'ELEM' CU20 ;
graph0 = FAUX ;

* Mesh
p0 = 0.0 0.0 0.0 ;
p1 = 0.0 100.0 0.0 ;
p2 = 100.0 0.0 0.0 ;
lgn0 = DROI 50 p1 p0 ;
srf0 = TRAN lgn0 50 p2 ;
msh0 = VOLU 'TRAN' 25 srf0 (0.0 0.0 -50.0) ;

* Model
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;

* Topology (usualy obtained from the procedure TOPOPTIM)
pnt0 = msh0 POIN 'SPHE' (50.0 50.0 0.0) (50.0 85.0 0.0) 10.0 ;
elm0 = msh0 ELEM 'APPU' pnt0 ;
topo0 = MANU 'CHML' mod0 'SCAL' 1.0 'TYPE' 'SCALAIRE' 'GRAVITE' ;
topo0 = ((topo0 * 0.0) REDU (DIFF msh0 elm0)) + (topo0 REDU elm0) ;

* Plot the topology to screen
SI graph0 ;
    TRAC topo0 mod0 ;
FINS ;

* Topology table
tab0 = TABL ;
tab0.'TOPOLOGIE'     = topo0 ;
tab0.'MODELE'        = mod0 ;
tab0.'TAUX_FILTRAGE' = 3 ;

* Generate a smoothed surface from the topology
srf0 = TOPOSURF tab0 ;

* Plot the smoothed surface to screen
SI graph0 ;
    TRAC srf0 'FACE' ;
FINS ;

* Save the smoothed surface in STL format
OPTI 'SORT' 'toposurf_03.stl' ;
SORT 'STL' srf0 ;

FIN ;



