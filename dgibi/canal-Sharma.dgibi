* Ce cas teste le modèle K-epsilon Bas Reynolds de Launder-Sharma
* sur l'écoulement turbulent dans un canal plan.
*  opti trace 'PSC';
 COMPLET = FAUX ;
 GRAPH   = FAUX ;
*COMPLET = VRAI ;
*GRAPH   = VRAI ;

*-----------------------------------------------------------------------
 opti dime 2 elem TRI6;
 opti dime 2 elem QUA8;
*-----------------------------------------------------------------------
*      |Triangles | Quadrangles
* ---------------------------
* LINE |          | OK
* LINB |          |
* MACRO|   ?      | OK
* QUAF |          | OK
*-----------------------------------------------------------------------

 DISCR=MACRO;
 KPRES=CENTREP1;
 KSUPG=SUPG   ;

******=================================================**********
******=================================================**********
 H=1. ;

Si COMPLET ;
 NBIT =450;
 DT  = 0.02;
 KGEO=2;
 NBIT =1800;
 DT  = 0.01;
 L=30.*H;
Sinon;
 NBIT = 40;
 DT  = 0.15;
 KGEO=1;
 L=3.*H;
Finsi;

*----------------------- Maillage --------------------------------------
epsi = 1.e-5;

 Si(EGA KGEO 1);
*dcl=H/200;
*dm=H/20;
*nm=15;
*dcx=H/12.;
*dmx=H/10.;

 dcl=H/130;
 dm=H/13;
 nm=13;
 dcx=H/10.;
 dmx=H/8.;
 Finsi;

 Si(EGA KGEO 2);
 dcl=H/300;
 dm=H/30;
 nm=20;
 dcx=H/24.;
 dmx=H/20.;
 Finsi;

 Si(EGA KGEO 3);
 dcl=H/400;
 dm=H/40;
 nm=25;
 dcx=H/36.;
 dmx=H/30.;
 Finsi;

P0 = 0. 0. ;
P1 = L  0. ;
P2 = L H   ;
P3 = 0. H  ;
PS3 = (P0 PLUS P3)*0.5;
PS31= (P0 PLUS P3)*0.2;
PS32= (P3 MOINS PS31);
entree= P0 d dini dcl dfin dm ps31 d nm ps32
 d dini dm dfin dcl P3;
sortie=(entree plus (L 0.)) inve;
par1  = p0 d dini dcx dfin dmx p1 ;
par2  = (par1 plus (0. H)) inve;

pc= 1. 1.;
ang= 43.;
ang= 60.;
 ang=  0. ;
entree=entree TOUR ang  pC;
sortie=sortie TOUR  ang pC;
par1  =par1   TOUR  ang pC;
par2  =par2   TOUR  ang pC;

elim (entree et par1 et par2 et sortie) epsi;
mt = daller entree par2 sortie par1;
v1=1. 0. ;
v2=0. -1.;
v3=-1. 0.;
v4=0. 1. ;
vt1= tour v1  ang p0;
vt2= tour v2  ang p0;
vt3= tour v3  ang p0;
vt4= tour v4  ang p0;
list vt1;
list vt2;
list vt3;
list vt4;

Mmt    = chan mt     QUAF ;
Mentree= chan entree QUAF ;
Msortie= chan sortie QUAF ;
Mpar1  = chan par1   QUAF ;
Mpar2  = chan par2   QUAF ;

elim (Mmt et Mentree et Msortie et Mpar1 et Mpar2) epsi ;

$mt     = model Mmt     'NAVIER_STOKES' DISCR;
$entree = model Mentree 'NAVIER_STOKES' DISCR;
$sortie = model Msortie 'NAVIER_STOKES' DISCR;
$par1   = model Mpar1   'NAVIER_STOKES' DISCR;
$par2   = model Mpar2   'NAVIER_STOKES' DISCR;

doma $mt 'IMPR';
doma $entree 'IMPR';

mt     = doma $mt     maillage;
entree = doma $entree maillage;
sortie = doma $sortie maillage;
par1   = doma $par1   maillage;
par2   = doma $par2   maillage;
par2i  = inve par2;
axe    = par1 plus (0. (H/2.));
elim (axe et mt) epsi;
  entre1= chan entree poi1;
  entre1= elem entre1 (lect 2 pas 1 ((nbel entre1) - 1 ));

Si Graph;
trace mt TITR ' Maillage ';

* On vérifie l'allongement
diamin=doma $mt 'DIAMIN';
diamax=doma $mt 'DIAMAX';
ksi=diamax*(inve diamin);
ksi=elno $mt ksi centre;
trace ksi mt (cont mt) TITR ' Facteur d allongemet du maillage';
Finsi;

*------------------- Fin Maillage --------------------------------------

* on impose un profil de vitesse en entrée soit plat (nn=1.e3) soit
* turbulent (nn=7).
nn=1.e3;
nn=7.;
* La vitesse débitante est de 1.
U0  = 1. ;

tasser entree;
ye=coor 2 entree;
Dgj=doma $entree 'XXDIAGSI';
a=(SOMT (Dgj*((ye*(H - ye))**(1./nn ))));
Ufr=U0/a*((ye*(H - ye))**(1./nn ));
Si Graph;
dess (evol chpo ufr entree) TITR 'Profil de vitesse en entrée';
Finsi;

uux=(cos ang) * U0;
uuy=(sin ang) * U0;
KE=((0.02*Ufr)**2.);
EE=KE**1.5/H;

MU  = 3.14E-5;
Ro  = 1.;
YP  = 1.e-2 ;
Nu  = Mu /Ro ;

Rey= U0 *H * 2. / NU;
mess ' Reynolds = ' Rey;

RV= EQEX  'OMEGA' 1.    'NITER' 1  ITMA NBIT    'FIDT' 1
'OPTI' 'EF' 'IMPL'   KSUPG  KPRES
  ZONE  $mt  'OPER' 'KEPSILON' 1. 'UN' 'NU' DT   'INCO' 'KN' 'EN'
  ZONE  $mt  'OPER' 'NS'       1. 'UN' 'MUF'     'INCO' 'UN'
'OPTI' 'EFM1' 'IMPL' 'CENTREE'
  ZONE  $mt  'OPER'  DFDT 1. 'UNM' DT  INCO 'UN';

 rv.'ALGO_KEPSILON'=mots 'Sharma';
 Mdl = chai 'Launder-Sharma' ;

  RV= EQEX RV  CLIM
  UN UIMP entre1 Ufr  UN VIMP entree (uuy + 1.e-4)
  UN UIMP (par1 et par2) 0. UN VIMP (par1 et par2) 0.
  KN TIMP entre1 KE  EN TIMP entre1 EE
  KN TIMP (par1 et par2) 0. EN TIMP (par1 et par2) 0.
  ;

 rv.'METHINV'.TYPINV=3 ;
 rv.'METHINV'.IMPINV=0 ;
 rv.'METHINV'.NITMAX=700;
 rv.'METHINV'.PRECOND=3 ;
 rv.'METHINV'.ILUTLFIL=4 ;
 rv.'METHINV'.ILUTDTOL=0.;
 rv.'METHINV'.RESID  =1.e-8;
 rv.'METHINV' . 'FCPRECT'=1   ;
 rv.'METHINV' . 'FCPRECI'=1   ;

    RVP= EQEX
    'OPTI' 'EF' KPRES
    ZONE  $mt    'OPER'  KBBT  (-1.) 'INCO' 'UN' 'PRES'
    ;

    rvp.'METHINV'.TYPINV=1        ;
    rvp.'METHINV'.IMPINV=0 ;
    rvp.'METHINV'.NITMAX=5000;
    rvp.'METHINV'.PRECOND=3 ;
    rvp.'METHINV'.ILUTLFIL=10;
    rvp.'METHINV'.ILUTDTOL=0.;
    rvp.'METHINV'.RESID  =1.e-10 ;
    rvp.'METHINV' . 'FCPRECT'=50000 ;
    rvp.'METHINV' . 'FCPRECI'=50000 ;


    RV.'PROJ'= RVP ;

rv.'INCO'= table inco ;
rv.'INCO'.'UN'  =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.'INCO'.'KN'  =  kcht $mt  scal sommet 1.e-5 ;
rv.'INCO'.'EN'  =  kcht $mt  scal sommet 1.e-5 ;
rv.'INCO'.'UNM' =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.'INCO'.'PRES'=  kcht $mt  scal kpres  0.;
rv.'INCO'.'MUF'=kcht $mt scal sommet nu;
rv.'INCO'.'NU'=NU;
rv.'INCO'.'MU'=MU;

 rv.'LTPS' = PROG ;
 rv.'Tps' = 1. ;
 rv.'DT'  = DT;
 rv.'NUPAT' =1;


 exec rv ;

 Si Complet
 opti sauv 'canalKES_1';
 sauv ;
 opti sauv 'bidon' ;

 exec rv ;
 opti sauv 'canalKES_2';
 sauv ;
 opti sauv 'bidon' ;

 exec rv ;
 opti sauv 'canalKES_3';
 sauv ;
 opti sauv 'bidon' ;

 exec rv ;
 opti sauv 'canalKES_4';
 sauv ;
 opti sauv 'bidon' ;

 exec rv ;
 opti sauv 'canalKES_5';
 sauv ;
 opti sauv 'bidon' ;

* On purge la mémoire de tous les objets MATRIK pour avoir un petit
* fichier de résultats
rv.'TABRES'=0.;
rv.'PROJ'=0.;
rv.'METHINV'=0;
rv.'METHINV2'=0;
rv.'METHINV3'=0;
rv.'METH_KEP'=0;
rvp=0.;

 opti sauv 'canalKESharma';
 sauv ;
 Finsi;

 un = rv.inco.'UN' ;
 qs=dbit un $sortie IMPR;
 um=(1./H)*qs;
 unn=(1./um)*un;
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*                   POST-TRAITEMENT
*-----------------------------------------------------------------------
*--- Edition d'un profil de vitesse asymptotique de Référence ----------
*--- Modèle de Buleev
Debproc ANABU H*'FLOTTANT' Rey*'FLOTTANT' UM*'FLOTTANT' ;
H2=H*H;
p0=0. 0. ; p1=0. H. ;  Pm=0. (H/2.); dpar=H/Rey*0.05;  dctr=H*0.05;
de= p0 d dini dpar dfin dctr pm d dini dctr dfin dpar p1 ;
z= coor 2 de;
Dh=2.*H;
Rho=1.;
mu=um/Rey*Rho*Dh;
m=7.1;
nu=mu/rho;

uet=um;
REPETER CCP 15 ;
fp=(uet/Um)**2.*8. ;
F=fp/2/Dh*rho*(um*um);
xi   = z*(1./H);
xiet = (1. - ((1. - (nu*m/h/Uet*4))**0.5))/2.;
u1=H2*F/2./mu*xi*(1. - xi);
ax=xi*(1. - xi)/xiet/(1. - xiet) + 1.e-10;
ax=m*m*F*H/Rho* (log ax);
ax =(abs ax) + 1.e-10;
ax0=(h*h*F/mu/2*xiet*(1. - xiet))**2.;
u2=(ax0+ax)**0.5;
iua=masq xi 'INFERIEUR' xiet;
iub=masq xi 'SUPERIEUR' (1. - xiet);
iu1=iua+iub;

uref = (iu1 * u1) + ((1. - iu1)*u2) ;
evuref = evol chpo uref de ;
umn=INTG evuref ;
umn=(extr 1 umn)/H; Reyn=rho*umn*Dh/mu;
w=0.9;
uet=(w*(uet*um/umn)) + ((1. - w)*uet);
dumn=abs ((umn - um)/um);
mess 'Iteration ' &ccp '  Umn=' umn ' Reyn=' Reyn ' Uet=' Uet
 ' dumn=' dumn;
*dess evuref ;
Si (dumn < 1.e-4) ; quitter CCP; Finsi ;
Fin CCP;
* Calcul des u+g et u+d
yplus= (extr evuref 'ABSC')*uet/nu;
evuplus=evuref*(1./uet);
yy=extr evuplus ORDO ;
lyz=lect (dime yy) pas -1 1;
yyz=extr yy lyz;
evuplud = (evol manu yplus yyz) coul verte;
evuplug = (evol manu yplus yy) coul rouge ;

Resproc evuplug evuplud evuref uet;
Finproc ;

DEBPROC KADER Pr*'FLOTTANT' Ret*'FLOTTANT' KOUL*'MOT';
*--- Profil de Kader ---------------------------------------------------

yplusr= prog 0.1 pas 0.1 30 pas 1. 100. pas 50. 1.e3
 pas 500. 1.e4 ;
unp = prog (dime yplusr)*1.;
ysr=yplusr/Ret;
Gamma=((Pr*yplusr)**4.)/(unp+(5.*(Pr**3.)*yplusr));
Gamma=Gamma*(-0.01);
tplusr=Pr*yplusr*(exp Gamma);
beta=((3.85*(Pr**(1./3.)) - 1.3)**2.);
beta=beta+(2.12*(log Pr));
aa1=1.5*(2.*unp - ysr);
aa2=2.5*(2.*unp - ysr);
bb1=(unp - ysr)*(unp - ysr)*2. + unp;
bb2=(unp - ysr)*(unp - ysr)*4. + unp;
aa1=aa1/bb1 ;
aa2=aa2/bb2 ;
cc1= ((2.12*(Log ((unp+yplusr)*aa1))) + (beta*unp))
 *(exp(unp/Gamma));
cc2= ((2.12*(Log ((unp+yplusr)*aa2))) + (beta*unp))
 *(exp(unp/Gamma));
tplusr1=tplusr + cc1;
tplusr2=tplusr + cc2;

evt1=evol manu   yplusr tplusr1;
evt2=evol manu   yplusr tplusr2;
evt1=evt1 coul KOUL;
evt2=evt2 coul KOUL;
*--- Profil de Kader ---------------------------------------------------
FINPROC evt1 evt2;

DEBPROC POST ;
 tabk = table ;
evupd1 evupg1 evuref1 uet1 = anabu 1. 2000. 1. ;
evupd2 evupg2 evuref2 uet2 = anabu 1. 1.e4  1. ;
evupd3 evupg3 evuref3 uet3 = anabu 1. 1.e5  1. ;
evupd4 evupg4 evuref4 uet4 = anabu 1. 1.e6  1. ;
evupd5 evupg5 evuref5 uet5 = anabu 1. 1.e7  1. ;

*--- Profil de Reichardt -----------------------------------------------
Ka = 0.41;
yplusr= prog 0.1 pas 0.1 30 pas 1. 100. pas 50. 1.e3
 pas 500. 1.e4 pas 1.e4 2.e5 ;
unp = prog (dime yplusr)*1.;

E=9.5;
uplusr=(1./Ka)*(Log (unp + (Ka*yplusr)));
uplusr=uplusr + (7.8* (unp - (exp (yplusr*(-1./11))))
 - ( (yplusr*(-1./11)) * (exp (yplusr*(-1./3.))) ) );

evr1=evol manu   yplusr uplusr;
evr1=evr1 coul verte;
*--- Profil de Reichardt -----------------------------------------------

*--- Profil de Référence pour le KLbr ----------------------------------

 xrr=prog
  2.99401E-06  1.24325E-03  4.78060E-03  1.05194E-02  1.83637E-02
  2.82181E-02  3.99867E-02  5.35741E-02  6.88838E-02  8.58208E-02
  0.10429      0.12419      0.14544      0.16793      0.19156
  0.21625      0.24190      0.26841      0.29568      0.32363
  0.35214      0.38114      0.41052      0.44019      0.47005
  0.50000      0.52995      0.55981      0.58948      0.61886
  0.64786      0.67637      0.70432      0.73159      0.75810 ;
 xrr = xrr et (prog
  0.78375      0.80844      0.83207      0.85456      0.87581
  0.89571      0.91418      0.93112      0.94643      0.96001
  0.97178      0.98163      0.98948      0.99522      0.99875
  1.00000);
 urr = prog
   0.0000       9.36980E-02  0.35215      0.56346      0.68057
   0.75493      0.80827      0.84978      0.88391      0.91302
   0.93845      0.96103      0.98127      0.99955       1.0161
   1.0311       1.0447       1.0569       1.0678       1.0774
   1.0858       1.0927       1.0983       1.1024       1.1049
   1.1058       1.1051       1.1027       1.0988       1.0934
   1.0865       1.0783       1.0688       1.0580       1.0459 ;
 urr = urr et (prog
   1.0325       1.0177       1.0013       0.98319      0.96315
   0.94083      0.91571      0.88702      0.85347      0.81285
   0.76100      0.68940      0.57826      0.37914      0.11368
   5.00378E-04 );
 krr = prog
  1.14242E-08  5.70695E-04  5.85036E-03  9.18812E-03  9.24352E-03
  8.78015E-03  8.29986E-03  7.86419E-03  7.46515E-03  7.08857E-03
  6.72369E-03  6.36405E-03  6.00640E-03  5.64984E-03  5.29542E-03
  4.94455E-03  4.60029E-03  4.26604E-03  3.94641E-03  3.64662E-03
  3.37300E-03  3.13278E-03  2.93380E-03  2.78421E-03  2.69096E-03
  2.65941E-03  2.69096E-03  2.78421E-03  2.93380E-03  3.13278E-03
  3.37300E-03  3.64662E-03  3.94641E-03  4.26604E-03  4.60029E-03 ;
 krr = krr et (prog
  4.94455E-03  5.29542E-03  5.64984E-03  6.00640E-03  6.36405E-03
  6.72369E-03  7.08857E-03  7.46515E-03  7.86419E-03  8.29986E-03
  8.78015E-03  9.24352E-03  9.18812E-03  5.85036E-03  5.70695E-04
  1.14242E-08);
 mrr = prog
   2.0000       2.0031       2.4276       4.7639       9.0737
   15.067       22.284       30.207       38.403       46.558
   54.454       61.929       68.847       75.089       80.550
   85.148       88.826       91.561       93.372       94.328
   94.549       94.214       93.548       92.808       92.241
   92.030       92.241       92.808       93.548       94.214
   94.549       94.328       93.372       91.561       88.826;
 mrr = mrr et (prog
   85.148       80.550       75.089       68.847       61.929
   54.454       46.558       38.403       30.207       22.284
   15.067       9.0737       4.7639       2.4276       2.0031
   2.0000);

evkrr=evol manu xrr krr;
evurr=evol manu xrr urr;
evmrr=evol manu xrr mrr;

dess evurr TITR ' Profil de vitesse de référence (KL) en sortie';
dess evkrr TITR ' Profil de K de référence (KL) en sortie';
dess evmrr TITR ' Profil de Mueff/mu de référence (KL) en sortie';

*--- Profil de Référence pour le KLbr ----------------------------------

 tabk.'Rey'=Rey;
 un = rv.inco.'UN' ;

 qs=dbit un $sortie IMPR;
 um=(1./H)*qs;
 unn=(1./um)*un;

TAB1 = TABLE ;
tab1.1 = 'MOT' '    ';
tab1.2 = 'MOT' 'MARQ CROI REGU';
tab1.3 = 'MOT' 'MARQ CARR REGU';
tab1.4 = 'MOT' 'MARQ LOSA REGU';
tab1.5 = 'MOT' 'MARQ PLUS REGU';
tab1.6 = 'MOT' 'MARQ TRIA REGU';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT'  'Re 2.e3' ;
 tab1.'TITRE'. 2 = 'MOT'  'Re 1.e4' ;
 tab1.'TITRE'. 3 = 'MOT'  'Re 1.e5' ;
 tab1.'TITRE'. 4 = 'MOT'  'Re 1.e6' ;
 tab1.'TITRE'. 5 = 'MOT'  'Re 1.e7' ;
 tab1.'TITRE'. 6 = 'MOT' 'Reichardt' ;

tit1=chai 'Comparaison Profils de vitesse Buleev à différents Reynolds';
tit2=chai tit1 ' et Reichardt';
dess (evupd1 et evupd2 et evupd3 et evupd4 et evupd5 et evr1)
 LOGX XBOR 0.1 5.e5 'GRIL' 'CARR' lege TAB1 titr tit2;

dess (evuref1 et evuref2 et evuref3 et evuref4 et evuref5)
 'GRIL' 'CARR' lege TAB1 titr tit1;
*-----------------------------------------------------------------------

unx =exco un 'UX';
dudy=exco (kops 'GRADS' unx $mt) 'UY';
uet=(mu*(abs (dudy + 1.e-5)))**0.5;
uet1=redu uet par1;
uet2=redu uet par2;
evuet1= evol chpo uet1 par1;
evuet2= evol chpo uet2 (inve par2);

dess (evuet1 et evuet2) TITR ' U* le long des deux parois'
tity 'm/s' titx 'm';

mess ' Liste de U*';
yevuet1=extr evuet1 'ORDO';
yevuet2=extr evuet2 'ORDO';
dl=(dime yevuet1) ;
ll=lect (dl - 10) pas 1 dl;
list (extr yevuet1 ll) ;
list (extr yevuet2 ll) ;


 f1=8.*(((1./um)*uet1)**2.);
 f2=8.*(((1./um)*uet2)**2.);
 fiso = 0.202*(Rey**(-0.196));
 evf1=evol chpo f1 par1;
 evf1=evf1 coul rouge;
 evf2=evol chpo f2 (inve par2);
 evf2=evf2 coul verte;
 xx=extr evf1 'ABSC';
 yfiso= prog (dime xx)*fiso;
 evfiso=evol manu xx yfiso ;
 evfiso=evfiso coul bleue ;

TAB1 = TABLE ;
tab1.1 = 'MOT' 'TIRR';
tab1.2 = 'MOT' 'TIRC';
tab1.3 = 'MOT' 'TIRM';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'Corr' ;
 tab1.'TITRE'. 2 = 'MOT' 'f paroi1' ;
 tab1.'TITRE'. 3 = 'MOT' 'f paroi2' ;

 fmax = maxi yfiso;

 dess (evfiso et evf2 et evf1) YBOR 0. (5.*fmax)
 TITR ' Coefficient de frottement' 'GRIL' lege TAB1;

tabk.'evfiso'=evfiso;
tabk.'evf2'  =evf2  ;
tabk.'evf1'  =evf1  ;
tabk.'fmax'  =fmax  ;

yp1=norm ((poin 2 entre1) moins (poin 1 entre1));
yp2=norm ((poin entre1 final) moins (poin ((nbno entre1) - 1) entre1));

mess ' YP1=' YP1 '  YP2=' YP2;

 yplus1= yp1*uet1*(rv.inco.'MU'**(-1.));
 yplus2= yp2*uet2*(rv.inco.'MU'**(-1.));

 evyplus1=evol chpo yplus1 par1;
 evyplus2=evol chpo yplus2 (inve par2);

 dess (evyplus1 et evyplus2) TITR ' Y+ le long des deux parois'
tity ' ' titx 'm';

 sort2=elem sortie (lect 1 pas 1 ((nbel sortie)/2));
 sort1= syme sort2 'DROIT' (point 1 axe) (axe poin final);
 elim (sort1 et sort2 et sortie) epsi;

 uet1=extr (nbno par1) (extr evuet1 'ORDO');
 uet2=extr (nbno par1) (extr evuet2 'ORDO');
 mess ' uet1 et uet2=' uet1 uet2;
 ypls1= evol chpo ((1./mu) * uet1 * (coor 2 sort1)) (inve sort1);
 ypls2= evol chpo ((1./mu) * uet2 * (H - (coor 2 sort2))) (     sort2);

 upl1=(evol chpo unx (inve sort1))*(1./uet1);
 upl2=(evol chpo unx sort2)*(1./uet2);
 xypls1= ORDO (extr ypls1 'ORDO');
 yypls1= ORDO (extr upl1  'ORDO');

 xypls1=xypls1 enlev 1;
 yypls1=yypls1 enlev 1;
 evupl1=evol manu xypls1 yypls1;


 xypls2=extr ypls2 'ORDO';
 yypls2=extr upl2 'ORDO';
 xypls2=xypls2 enlev 1;
 yypls2=yypls2 enlev 1;
 evupl2=evol manu xypls2 yypls2 ;
 evupl1=evupl1 coul rouge;
 evupl2=evupl2 coul verte;

TAB1 = TABLE ;
tab1.1 = 'MOT' '    ';
tab1.2 = 'MOT' 'TIRC';
tab1.3 = 'MOT' 'TIRM';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'Reichardt' ;
 tab1.'TITRE'. 2 = 'MOT' 'U+ paroi1' ;
 tab1.'TITRE'. 3 = 'MOT' 'U+ paroi2' ;

 dess (evr1 et evupl2 et evupl1) 'LOGX' 'XBOR' 1. 500.
 TITR ' U+ en sortie' 'GRIL' lege TAB1
tity ' ' titx 'm';

tabk.'evr1'  =evr1  ;
tabk.'evupl1'=evupl1;
tabk.'evupl2'=evupl2;

 nutsnu=(rv.inco.'MUF')*(1./MU);
 trace nutsnu mt (cont mt) 'TITR' ' Nu effectif / nu ';

*-------------------------------------------------------------------------
 kn=rv.inco.'KN';
 kpl1=(evol chpo kn (inve sort1));
 kpl2=(evol chpo kn sort2);
 kpl1=extr kpl1 'ORDO'; ordo kpl1;
 kpl2=extr kpl2 'ORDO'; ordo kpl2;
 kpl1=kpl1 enlev 1 ;
 kpl2=kpl2 enlev 1 ;

 evkpl1=evol manu xypls1 kpl1;
 evkpl2=evol manu xypls2 kpl2;
 evkpl1=evkpl1 coul rouge;
 evkpl2=evkpl2 coul verte;

TAB1 = TABLE ;
tab1.1 = 'MOT' 'TIRC';
tab1.2 = 'MOT' 'TIRM';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'k paroi1' ;
 tab1.'TITRE'. 2 = 'MOT' 'k paroi2' ;

 dess (evkpl2 et evkpl1)
* 'LOGX' 'XBOR' 1. 50. LOGY
 TITR ' k en sortie' 'GRIL' lege TAB1
tity ' ' titx 'm';
*-------------------------------------------------------------------------
 en=rv.inco.'EN';
 epl1=(evol chpo en (inve sort1));
 epl2=(evol chpo en sort2);
 epl1=extr epl1 'ORDO'; ordo epl1;
 epl2=extr epl2 'ORDO'; ordo epl2;
 epl1=epl1 enlev 1 ;
 epl2=epl2 enlev 1 ;

 evepl1=evol manu xypls1 epl1;
 evepl2=evol manu xypls2 epl2;
 evepl1=evepl1 coul rouge;
 evepl2=evepl2 coul verte;

TAB1 = TABLE ;
tab1.1 = 'MOT' 'TIRC';
tab1.2 = 'MOT' 'TIRM';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'e paroi1' ;
 tab1.'TITRE'. 2 = 'MOT' 'e paroi2' ;

 dess (evepl2 et evepl1) 'LOGX' 'XBOR' 1. 50. LOGY
 TITR ' epsilon en sortie' 'GRIL' lege TAB1
tity ' ' titx 'm';
*-------------------------------------------------------------------------


 npl1=(evol chpo nutsnu (inve sort1));
 npl2=(evol chpo nutsnu sort2);
 npl1=extr npl1 'ORDO'; ordo npl1;
 npl2=extr npl2 'ORDO'; ordo npl2;
 npl1=npl1 enlev 1 ;
 npl2=npl2 enlev 1 ;

 evnpl1=evol manu xypls1 npl1;
 evnpl2=evol manu xypls2 npl2;
 evnpl1=evnpl1 coul rouge;
 evnpl2=evnpl2 coul verte;

TAB1 = TABLE ;
tab1.1 = 'MOT' 'TIRC';
tab1.2 = 'MOT' 'TIRM';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'nt/n paroi1' ;
 tab1.'TITRE'. 2 = 'MOT' 'nt/n paroi2' ;

 dess (evnpl2 et evnpl1) 'LOGX' 'XBOR' 1. 500.
 TITR ' Nut/nu en sortie' 'GRIL' lege TAB1
tity ' ' titx 'm';

tabk.'evnpl1'=evnpl1;
tabk.'evnpl2'=evnpl2;

 ung = vect un 0.5 'UX' 'UY' jaune;
 trace ung mt 'TITR' ' Vitesses ';

 pn = elno $mt rv.inco.PRESSION kpres;
 trace pn mt (cont mt) TITR 'Pression';
 evpaxe= evol chpo pn axe;
 dess evpaxe TITR ' Pression sur l axe';

*-----------------------------------------------------------------------
 evax= evol chpo (exco un 'UX') axe   ;
 dess evax TITR 'U sur l axe'
tity 'm/s' titx 'm';
*-----------------------------------------------------------------------

*-----------------------------------------------------------------------
 evmuax= evol chpo nutsnu axe   ;
 dess evmuax TITR 'Nut/Mu sur l axe'
tity ' ' titx 'm';
tabk.'evmuax'=evmuax;
*-----------------------------------------------------------------------

*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

*profils de référence
 evupdr evupgr evusr uetr = anabu 1. Rey 1. ;
mess '***************************************************************';
mess '***************************************************************';
mess ' UET1 = ' UET1  '  UET2 = ' UET2 '  UETR = ' UETR ;
mess '***************************************************************';
mess '***************************************************************';

 evus= evol chpo (exco unn 'UX') sortie;
 usix  = extr evusr 'ABSC';
 usi   = ipol usix (extr evus 'ABSC') (extr evus 'ORDO');
 evusi = evol manu usix usi ;
 evdu  = (evusi - evusr)*(evusi - evusr);

tab1 = table;
tab1.1 = 'MOT' 'TIRC';
tab1.1 = 'MOT' '    ';
tab1.2 = 'MOT' '    ';
tab1.3 = 'MOT' '    ';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'KL Référence';
 tab1.'TITRE'. 2 = 'MOT' Mdl ;
 tab1.'TITRE'. 3 = 'MOT' 'Buleev analytique' ;
 evusr=evusr coul rouge;
 evurr=evurr coul vert ;

 dess (evurr et evus et evusr) TITR 'U en sortie' 'GRIL' LEGE TAB1
tity 'm/s' titx 'm';

 evkrr=evkrr coul verte;
 evks = evol chpo rv.inco.'KN' sortie;

 dess (evkrr et evks) TITR 'K en sortie' 'GRIL' LEGE TAB1
tity 'm2/s2' titx 'm';

 evmrr=evmrr coul verte;
 evms = evol chpo ((1./MU)*(rv.inco.'MUF')) sortie;

 dess (evmrr et evms) TITR 'Mueff en sortie' 'GRIL' LEGE TAB1
tity 'm2/s' titx 'm';

 tabk.'evus'=evus;
 tabk.'evusr'=evusr;

 dess evdu TITR '|U - uref|**2';

 du    = extr 1 (INTG evdu);
 errn = (abs (du - 1.23566E-03));
 mess ' delta u = ' du ' errn = ' errn;
 Si (NON COMPLET);
 Si(errn > 0.0001); erreur 5 ; Finsi ;
 Finsi;

FINPROC tabk;

******* Profils de T+ de Kader ***********************************
Si FAUX;

 evt2p1 evt1p1=kader 0.7 1.e4 'VERTE';
 evt2p2 evt1p2=kader 0.7 1.e5 'ROUGE';
 evt2p3 evt1p3=kader 0.7 1.e6 'BLEUE';

tab1 = table;
tab1.1 = 'MOT' '    ';
tab1.2 = 'MOT' 'TIRC';
tab1.3 = 'MOT' '    ';
tab1.4 = 'MOT' 'TIRC';
tab1.5 = 'MOT' '    ';
tab1.6 = 'MOT' 'TIRC';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' 'T+1 Ret=1.e4' ;
 tab1.'TITRE'. 2 = 'MOT' 'T+2 Ret=1.e4' ;
 tab1.'TITRE'. 3 = 'MOT' 'T+1 Ret=1.e5' ;
 tab1.'TITRE'. 4 = 'MOT' 'T+2 Ret=1.e5' ;
 tab1.'TITRE'. 5 = 'MOT' 'T+1 Ret=1.e6' ;
 tab1.'TITRE'. 6 = 'MOT' 'T+2 Ret=1.e6' ;

 dess (evt1p1 et evt2p1 et evt1p2 et evt2p2 et evt1p3 et evt2p3)
  LOGX XBOR 0.1 1.E5 tity 'T+' titx 'Y+' 'GRIL' LEGE TAB1
 TITR ' Teta+ Cas 1 et 2 Fonction du Reynolds Turbulent';

 evt2p1 evt1p1=kader 0.1 1.e4 'VERTE';
 evt2p2 evt1p2=kader 1.  1.e5 'ROUGE';
 evt2p3 evt1p3=kader 10. 1.e6 'BLEUE';

 tab1.'TITRE'. 1 = 'MOT' 'T+1 Pr=0.1' ;
 tab1.'TITRE'. 2 = 'MOT' 'T+2 Pr=0.1' ;
 tab1.'TITRE'. 3 = 'MOT' 'T+1 Pr=1.' ;
 tab1.'TITRE'. 4 = 'MOT' 'T+2 Pr=1.' ;
 tab1.'TITRE'. 5 = 'MOT' 'T+1 Pr=10.' ;
 tab1.'TITRE'. 6 = 'MOT' 'T+2 Pr=10.' ;

 dess (evt1p1 et evt2p1 et evt1p2 et evt2p2 et evt1p3 et evt2p3)
  LOGX XBOR 0.1 1.E5 tity 'T+' titx 'Y+' 'GRIL' LEGE TAB1
 TITR ' Teta+ Cas 1 et 2 Fonction du Prandtl';
Finsi;
******* Profils de T+ de Kader  FIN ******************************


*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*profils de référence
 evupdr evupgr evusr uetr = anabu 1. Rey 1. ;
mess '***************************************************************';
mess '***************************************************************';
mess ' UET1 = ' UET1  '  UET2 = ' UET2 '  UETR = ' UETR ;
mess '***************************************************************';
mess '***************************************************************';

 evus= evol chpo (exco unn 'UX') sortie;
 usix  = extr evusr 'ABSC';
 usi   = ipol usix (extr evus 'ABSC') (extr evus 'ORDO');
 evusi = evol manu usix usi ;
 evdu  = (evusi - evusr)*(evusi - evusr);

 Si GRAPH ;
  t1=POST;

tab1 = table;
tab1.1 = 'MOT' 'TIRC';
tab1.2 = 'MOT' '    ';
tab1.'TITRE'    = TABLE         ;
 tab1.'TITRE'. 1 = 'MOT' Mdl ;
 tab1.'TITRE'. 2 = 'MOT' 'Buleev analytique' ;
 evusr=evusr coul rouge;

 dess (evus et evusr) TITR 'U en sortie' 'GRIL' LEGE TAB1
tity 'm/s' titx 'm';

 dess evdu TITR '|U - uref|**2';
 Finsi;

 du    = extr 1 (INTG evdu);
 errn = (abs (du - 1.23566E-03));
 mess ' delta u = ' du ' errn = ' errn;
 Si (NON COMPLET);
 Si(errn > 0.001); erreur 5 ; Finsi ;
 Finsi;

 FIN;












