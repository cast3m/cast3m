* fichier :  pore1.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Pore1.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
SAUT PAGE;
*
*******************************************************
*                                                     *
*  TEST PORE1                                         *
*                                                     *
*  CYLINDRE EPAIS EN MILIEU POREUX                    *
*                                                     *
*  REFERENCE : Benchmark INTERCLAY 1.1  Variante 2    *
*                                                     *
*     Le milieu est elastoplastique, modele           *
*     Drucker-Prager non associe                      *
*     On decharge la contrainte a l'interieur du trou *
*                                                     *
*     On se propose de caculer la convergence du trou *
*     valeur de reference  : -0.266 m                 *
*     ainsi que la contrainte circonferentielle au    *
*     bord du trou                                    *
*     valeur de reference  : -3.47E6 Pa               *
*     Il s'agit de valeurs calculees par castem2000   *
*     et en accord avec les resultats de INTERCLAY    *
*                                                     *
*******************************************************
*
OPTI ECHO 0;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
TITRE   ' TEST PORE1  ' ;
GRAPH = 'N';
OPTIO DIME 2 MODEL AXIS   ELEM QUA8  ;
*
*=====================================================*
*             geometrie : maillage                    *
*=====================================================*
*
* Dimension en metres
*
DENS 0.5;
P1 = 2.5 0. ; P2 = 25. 0. ; Q1 = 2.5 0.5 ;
Q2 = 25. 0.5; PP1 = 5 0.  ; QQ1 = 5 0.5 ;
LI1 = P1 D  P2 'DINI' 0.25 'DFIN' 2.5 ;
LI2 =  P2 D Q2 ;
LI3  = INVE ( Q1 D Q2 'DINI' 0.25 'DFIN' 2.5 );
LI4  = Q1 D P1 ;
LI  = LI1  ET LI2  ET LI3  ET LI4  ;
CADRE = DALLER LI1  LI2  LI3  LI4  PLAN ;
SI (NEG GRAPH 'N');
   TRAC CADRE ;
FINSI;
*
*=====================================================*
*      modele - materiau - caracteristiques en Pa     *
*                 conditions aux limites              *
*=====================================================*
*
 MO=MODE CADRE POREUX ELASTIQUE PLASTIQUE
                     DRUCKER_PRAGER ;
*
MAT=MATE MO YOUNG 200.13E6 NU 0.25 MOB  7500E6 COB 1.
 PERM 4.E-12   VISCO 4.
ALPH 0. KF 0. RHOF 0. ALPF 0. ALPM 0. RHO 0.
 ALFA 0.1270509  BETA 0.57735027 K 0.
 GAMM 0.         DELT 0.57735027
 ETA  0.1270509  MU   0.57735027 KL 0. H 0. ;
*
*  on bloque les faces superieure et inferieure
*
BL1=BLOQ UZ  LI1 ;
BL2=BLOQ UZ  LI3 ;
CL = BL1 ET BL2  ;
*
*=====================================================*
*                  Chargement                         *
*=====================================================*
*
*   contraintes initiales
*
ZOZO = MANU CHML MO    SMRZ 0.
   SMRR -5.E6 SMTT -5.E6 SMZZ -5.E6 MSR0 0.
   TYPE CONTRAINTES  STRESSES ;
ZUZU = MANU CHML MO    GARZ 0.
   EPRR 0.    EPTT 0.    EPZZ 0.    PRES 2.5E6
   TYPE DEFORMATIONS  STRESSES ;
FF = BSIGMA MO ZOZO;
EVT1= EVOL MANU 'TEMPS'  (PROG 0   14 )
                'CHARGE' (PROG 1   1  );
CHA1 = CHAR MECA FF EVT1 ;
*
*    pression
*
FPI2 = PRESS MASS MO -1.E6   LI4 ;
EVT2= EVOL MANU 'TEMPS' (PROG 0 PAS 1 14 )
                'CHARGE' (PROG 0 PAS 0.25 3.5) ;
CHA2 = CHAR MECA FPI2 EVT2 ;
CHATT = CHA1 ET CHA2 ;
*
*=====================================================*
*   Initialisation de la table pour appel a PASAPAS   *
*=====================================================*
*
TAB1 = TABLE;
TAB1 . 'CONTRAINTES' = TABLE;
TAB1 . 'DEFORMATIONS' = TABLE;
TAB1 . 'DEPLACEMENTS' = TABLE;
TAB1.'CONTRAINTES' . 0 = ZOZO;
TAB1.'DEFORMATIONS' . 0 = ZUZU;
TAB1.'DEPLACEMENTS' . 0 = MANU CHPO CADRE  1 P 2.5E6 ;
LREE = PROG 0 PAS 1 14;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'CHARGEMENT' = CHATT;
TAB1.'MODELE' = MO;
TAB1.'TEMPS_CALCULES' = LREE;
TAB1.'BLOCAGES_MECANIQUES' = CL;
*
PASAPAS TAB1;
*=====================================================*
*                affichage des resultats              *
*=====================================================*
*
MESS ' RESULTATS TEST PORE1 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS
'  On cherche la convergence du trou et la contrainte';
MESS
'  circonferentielle ';
MESS
'  Elles sont comparees a des valeurs de reference ';
SAUT 1 LIGN;
SAUT 1 LIGN;
CONVTH = -0.266 ;
SIGTTH = -3.47E6 ;
LEDEP = PECHE TAB1 DEPLACEMENTS 14 ;
SIGAA = PECHE TAB1 CONTRAINTES  14 ;
SIGAB = CHAN 'CHPO' MO SIGAA ;
CONVXX = EXTR LEDEP UR P1 ;
SIGTXX = EXTR SIGAB SMTT P1 ;
ERGXXA = ( CONVXX - CONVTH ) / CONVTH ;
ERGXXA = ABS ERGXXA ;
ERGXXB = ( SIGTXX - SIGTTH ) / SIGTTH ;
ERGXXB = ABS ERGXXB ;
*
MESS ' Convergence de reference  :' CONVTH 'm';
MESS ' Convergence calculee      :' CONVXX 'm';
MESS '    Soit un ecart relatif de :' ERGXXA ;
SAUT 1 LIGN;
MESS ' Contrainte circonferentielle de reference  :'
     SIGTTH 'Pa';
MESS ' Contrainte circonferentielle calculee      :'
     SIGTXX 'Pa';
MESS '    Soit un ecart relatif de :' ERGXXB ;
*
*
*=====================================================*
*                  code fonctionnement                *
*=====================================================*
*
*    L'ecart maximum entre valeur theorique et calculee
* doit etre inferieure a 2%.
*
SAUT 2 LIGNE;
SI (ERGXXA <EG 5) ;
  SI (ERGXXB <EG 5) ;
    ERRE 0;
  SINON;
    ERRE 5;
  FINSI;
SINON;
  ERRE 5;
FINSI;
*
SAUT 1 LIGN;
*
FIN;
















