* fichier :
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*           Test TestIC20.dgibi: Jeux de données      *
*           ------------------------------------      *
*                                                     *
*
* FICHIER GIBIANE POUR TESTER LES ELEMENTS
* INCOMPRESSIBLES volumiques quadratiques
*
* Maillage d'un cube avec les
* éléments IC
*
* On soumet le cube à un champ de contrainte
* hydrostatique (contribution: alpha=1) et à un champ
* de contrainte déviatorique (contribution: beta=0.001)
*
* Les résultats théoriques sont déterminés dans le cas
* incompressible (coefficient de Poisson proche de 0.5)
*
* cf rapport DMT sur ces elements
*
*
*-----MAILLAGE-----------
*
opti dime 2 mode plan defo elem qua8 ;
opti dime 2 mode plan defo elem tri6 ;
*
p1 = 0. 0. ;
p2 = 100. 0. ;
p3 = 100. 100. ;
p4 = 0. 100. ;
*
nm2 = 10;
li1 = d nm2 p1 p2 ;
li2 = d nm2 p2 p3 ;
li3 = d nm2 p3 p4 ;
li4 = d nm2 p4 p1 ;
*
sur1 = dall li1 li2 li3 li4 'PLAN' ;
*
opti dime 3 elem cu20 ;
opti dime 3 elem pr15 ;
e_z = 0. 0. 100. ;
vol1 = volu sur1 tran nm2 e_z ;
*sur2 = changer (face 2 vol1) tri6 ;
sur2 = face 2 vol1;
*sur3 = changer (face 3 vol1) tri6 ;
sur3 = face 3 vol1 ;
svol1 = enve vol1 ;
*svol1 = changer svol1 tri6 ;
*opti dime 3 elem te10 ;
*vol1 = volu svol1 ;
*elim 1.e-4 svol1 sur3;
*elim 1.e-4 svol1 sur2;
*elim 1.e-4 svol1 sur1;

ph3 = point vol1 proc (p3 plus e_z) ;
*
*-----MODELE MECANIQUE------------
*
* Valeur du coefficient de Poisson
*
nu1 = 0.499 ;
*
* Courbe de traction :
lep1 = prog 0. 2.e-3 1. ;
lsm1 = prog 0. 420.e6 ((1.-2.e-3)*1.e9 + 420.e6) ;
trac1 = evol manu lep1 lsm1 ;
*dess trac1 titr
*'Courbe de traction (Young = 210 GPa, pente plastique = 1 GPa)' nclk;
*
*mod1 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' IC20 ;
*mod2 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
* 'PLASTIQUE' 'ISOTROPE' ic20 ;
mod1 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' bbar ;
mod2 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
 'PLASTIQUE' 'ISOTROPE' bbar ;
*mod1 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' IC10 ;
*mod2 = modele vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
* 'PLASTIQUE' 'ISOTROPE' IC10 ;
mat1 = mate mod1 'YOUNG' 2.e11 'NU' nu1 ;
mat2 = mate mod2 'YOUNG' 210.e9 'NU' nu1 trac trac1 ;
*
*
*-----CONDITIONS AUX LIMITES------------
*
bl1 = bloq sur1 'UZ' ;
bl2 = bloq li1 'UY' ;
bl3 = bloq p1 'UX' 'UY' ;
bl4 = bloq li4 'UX' ;
*
*
*----------------CHARGEMENT-----------------
*
* Coefficients de contribution de chargement
* (alpha-->hydrostatique)
* (beta-->déviatorique)
*
alpha = 1. ;
beta = 1.e-3 ;
*
*-----Chargement déviatorique------------
*
*-----sur li1-----
*
chpx1 = coor 3 sur3 ;
chpresy1 = pres 'MASS' mod1 (chpx1 * (-2.)*1.e5) ;
chpres1 = beta * chpresy1 ;
vecfor1 = vect chpres1 1.E-7 'FX' 'FY' 'FZ' rouge ;
*
*
*-----Chargement hydrostatique------------
*
*-----svol1 ----
*
presy1 = pres 'MASS' mod1 (1.e5) svol1 ;
pres1 = alpha * presy1 ;
vfor1 = vect pres1 5.E-7 'FX' 'FY' 'FZ' vert ;
*
*trac vol1 cach ;
*trac vfor1 svol1 cach ;
*
*---------CALCUL DES DEPLACEMENTS --------------------
*
*
* Solution analytique au point Ph3: uzan.
*
x3 = coor 1 ph3 ;
z3 = coor 3 ph3 ;
mu = 1.e11 / ( 1 + nu1 ) ;
uzan = ( 2 * x3 * z3 ) / ( 2 * mu ) * 1.e5 * beta ;
*
* Solution Castem au point P3: uycas.
rigi1 = rigi mod1 mat1 ;
*
deptot = reso (rigi1 et bl1 et bl2 et bl3 et bl4) pres1 ;
*deptot = reso (rigi1 et bl1 et bl3) pres1  ;
*( pres2 et pres3 et pres4 et pres1 et (beta *chforx3)) ;
*(chpres2 et chpres3 et chpres4 et chpres1 et
uzcas = extr deptot 'UZ' ph3 ;
*list (redu deptot sur2) ;
*
* Calcul de l erreur sur le déplacement.
*
erry = abs (( uzan - uzcas ) / ( uzan )) * 100 ;
*
* Test de précision sur le calcul du déplacement uy
* (<1%).
*
si ( erry >eg 1. ) ;
*     erreur 5 ;
mess 'erry' erry ;
finsi ;
*
*
*-----------CALCUL CONTRAINTES-------------------------
*
*
* Solution analytique: pression = alpha*1.E5.
*
* Solution Castem : pression maximale = maxpres1.
*
sig1 = sigma mod1 deptot mat1 ;
sxx = exco 'SMXX' sig1 'P' ;
syy = exco 'SMYY' sig1 'P' ;
szz = exco 'SMZZ' sig1 'P' ;
sp = (sxx + syy + szz) / 3. ;
maxpres1 = maxi sp ;
*
*trac sig1 mod1  ;
* Calcul de l erreur sur la pression.
*
errpress = abs ((maxpres1 + (alpha * 1.E5)) /
           (alpha * 1.E5)) * 100. ;
*
* Test de précision sur le calcul de la pression (<1%).
*
mess 'errpress' errpress maxpres1;
si ( errpress >eg 1. ) ;
     erreur 5 ;
finsi ;
*
ev_ic = evol manu (prog 0. 1. ) (prog 0. 10.) ;
ch_hy = char meca ev_ic pres1 ;

t_ic = table ;
t_ic . modele = mod2 ;
t_ic . caracteristiques = mat2 ;
t_ic . blocages_mecaniques = bl1 et bl2 et bl3  ;
t_ic . chargement = ch_hy ;
t_ic . temps_sauves = prog 1. ;
pasapas t_ic ;
s_ic = t_ic . contraintes . 1 ;
*trac s_ic mod2 ;
*list (redu t_ic . deplacements . 1 sur2) ;
*

fin;








