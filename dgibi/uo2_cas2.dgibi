* fichier :  uo2_cas2.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test uo_cas2.dgibi: Jeux de donnees
*     ------------------------------------
*
'OPTI' 'DIME' 3 'MODE' 'TRID'        ;
'OPTI' 'ELEM' 'CUB8'                 ;
'OPTI' 'TRAC' 'PSC'                  ;
'TEMPS' 'ZERO'                       ;
*
**********************************************************
*                TEST DE VALIDATION                      *
*                ------------------                      *
*                   MODELE UO2                           *
*   (COUPLAGE DES MODELES GATT_MONERIE ET OTTOSEN)       *
*  UO2 STANDARD COMPRESSIBLE AVEC COUPLAGE STATIQUE      *
*                                                        *
*  MAILLAGE:                                             *
*     CUBE                                               *
*                                                        *
*  CHARGEMENT:                                           *
*     DEPLACEMENT VERTICAL IMPOSE EN  TRACTION AVEC      *
*     VITESSE CTE PAR MORCEAU (POSITIVE PUIS NEGATIVE    *
*     PUIS POSITIVE)                                     *
*     TEMPERATURE         LINEAIRE CROISSANTE            *
*     DENSITE DE FISSIONS LINEAIRE CROISSANTE            *
*     OTTOSEN ACTIVE (TRACTION ELASTIQUE - FISSURATION - *
*     OUVERTURE - FERMETURE - COMPRESSION - REOUVERTURE -*
*     POURSUITE DE FISSURATION VERS RUPTURE)             *
*     GONFLEMENT ACTIVE                                  *
*     PAS DE VISCOPLASTICITE (GATT_MONERIE)              *
*     PAS DE DENSIFICATION                               *
**********************************************************
*
DESSI = FAUX ;
*
* -----------------------------*
* MAILLAGE D UN CUBE DE COTE 1 *
* -----------------------------*
   HS2  = 2E-5                                                 ;
*
   OO   = 0.     0.         0.                                 ;
   A1   = HS2    0.         0.                                 ;
   A2   = HS2    0.         HS2                                ;
   A3   = 0.     0.         HS2                                ;
*
   LB   = 'DROIT' 1  OO A1                                     ;
   LD   = 'DROIT' 1  A1 A2                                     ;
   LH   = 'DROIT' 1  A2 A3                                     ;
   LG   = 'DROIT' 1  A3 OO                                     ;
*
   SUA  = 'DALL' LB  LD  LH  LG  'PLAN'                        ;
   SU2  = SUA 'PLUS' (0. HS2 0.)                               ;
   VOL1 = 'VOLU' 1 SUA SU2                                     ;
   'ELIM' (HS2/1000.) VOL1                                     ;
*
   SUB = 'POIN' ('COOR' 3 VOL1) 'INFERIEUR' (HS2/1000.)        ;
   SUH = 'POIN' ('COOR' 3 VOL1) 'SUPERIEUR' (HS2 - (HS2/1000.));
   LG  = 'POIN' ('COOR' 1 VOL1) 'INFERIEUR' (HS2/1000.)        ;
* -----------------------------------------------------*
* Conditions aux limites + deplacement impose sur CLZH *
* -----------------------------------------------------*
CLZB = 'BLOQ' UZ SUB                      ;
CLZH = 'BLOQ' UZ SUH                      ;
CLXG = 'BLOQ' UX LG                       ;
CLYG = 'BLOQ' UY SUA                      ;
CLT  = CLZB 'ET' CLZH 'ET' CLXG 'ET' CLYG ;
* -------*
* Modele *
* -------*
MODL1= MODE VOL1 MECANIQUE ELASTIQUE VISCOPLASTIQUE UO2 ;
*
* Temperature moyenne
* -------------------
  TE1 = 1420. + 273.  ;
*
* Porosite initiale (cte au cours du calcul)
* ------------------------------------------
  PO   = 4.52E-2      ;

TA = @GATTPAR '../divers/fichier_gatt' ;
*
PP = 'MANU' 'CHML' MODL1 'T' TE1 'PORO' PO RIGIDITE ;
*
*
* --------------------------------------*
* 'YOUN' constant ou fonction de T et f *
* --------------------------------------*
 MATREE = MATE MODL1 'YOUN' (TA.'YOUN')          ;
 EE     = 'VARI' 'NUAG' MODL1 MATREE PP          ;
* EE     = (TA.'YOUN')                            ;
 MATREE = MATE MODL1 'YOUN' EE                   ;
* ------------------------------------*
* 'NU' constant ou fonction de T et f *
* ------------------------------------*
 MATRUU = MATE MODL1 'NU' (TA.'NU')              ;
 UU     = 'VARI' 'NUAG' MODL1 MATRUU PP          ;
* UU     = (TA.'NU')                              ;
* --------------------------------------*
* 'ALPH' constant                       *
* --------------------------------------*
 XALFA  = 1.319E-5 ;
* -----------------------------------------------------------------*
* Temperature de reference et temperature de reference pour 'ALPH' *
* -----------------------------------------------------------------*
 TREF   = TE1 ;
 TALPHA = 0.  ;
*TALPHA = TE1 ;
*
* -----------------------------*
* Materiau partie GATT_MONERIE *
* -----------------------------*
* fluage primaire
  TA.'KPUO'   = 0.                ;
* fluage secondaire (2 mecanismes)
  TA.'K1'   = 0.                  ;
  TA.'K2'   = 0.                  ;
* fluage d"irradiation
  TA.'A'    = 0.                  ;
* densification/gonflement
  TA.'BUMI' = 5E3 * (TA.'BUMI')   ;
*
MATR11 = 'MATE' MODL1 'YOUN' EE 'NU' UU 'RHO' (TA.'RHO')  'ALPH' XALFA ;
*
MATR12 = 'MATE' MODL1 'R'    (TA.'R')    'DG0' (TA.'DG0') 'DG'  (TA.'DG') 'K1'   (TA.'K1')   'M1'  (TA.'M1')  'Q1'  (TA.'Q1')  'N1'   (TA.'N1') 'K2'   (TA.'K2')   'M2'  (TA.'M2')  'Q2'  (TA.'Q2')  'N2'   (TA.'N2') 'OMEG' (TA.'OMEG') 'H'   (TA.'H')   'Q'   (TA.'Q')   'BETA' (TA.'BETA') 'K'    (TA.'K')    'A'   (TA.'A')   'Q3'  (TA.'Q3')  'N3'   (TA.'N3') 'CR'   (TA.'CR')   'CR1' (TA.'CR1') 'CR2' (TA.'CR2') 'CR3'  (TA.'CR3');
*
MATR13 = 'MATE' MODL1  'KP' (TA.'KPUO')   'AP' (TA.'AP') 'BP' (TA.'BP')     'QP' (TA.'QP') ;
*
MATR14 = 'MATE' MODL1 'ADEN' (TA.'ADEN')   'KGON' (TA.'KGON') 'POR0' (TA.'POR0')   'BUMI' (TA.'BUMI') 'EFIS' (TA.'EFIS')                                ;
*
* TYPE = 0. combustible UO2            sinon combustible AFA3GLAA
* COMP = 0. combustible compressible   sinon incompressible
* DYN  = 0. couplage statique          sinon dynamique
*
* -----------------------------*
* Materiau partie OTTOSEN      *
* -----------------------------*
 MATR16 = 'TAIL' MODL1 ;
*
* Resistance au cisaillement 'GS  ' par defaut: 'YOUN'*1.8E-4
* Ouverture a rupture 'WRUP' = 0 --> 'WRUP' = 2*'GFTR'/'LTR '
* Relation bilineaire 'BILI' = 0 --> 'BILI' = 0 (ouverture)
*
 XGFTR = 1.8   ; XGFTR1=XGFTR ; XGFTR2=XGFTR ; XGFTR3=XGFTR ;
 XLTR  = 150E6 ; XLTR1 =XLTR  ; XLTR2 =XLTR  ; XLTR3 =XLTR  ;
 XBTR  = 0.2   ;
*
 MATR17 = 'MATE' MODL1  'GFTR' XGFTR    'LTR ' XLTR     'BTR ' XBTR 'WRUP' 0.       'BILI' 0. 'GFT1' XGFTR1   'GFT2' XGFTR2   'GFT3' XGFTR3 'GS1 ' 0.       'GS2 ' 0.       'GS3 ' 0. 'LTR1' XLTR1    'LTR2' XLTR2    'LTR3' XLTR3 'WRU1' 0.       'WRU2' 0.       'WRU3' 0. 'BIL1' 0.       'BIL2' 0.       'BIL3' 0.     ;
*
MATR1 = MATR11 'ET' MATR12 'ET' MATR13 'ET' MATR14 'ET' MATR16 'ET' MATR17  ;
*
*
RHO0 = 1. - TA.'POR0'                      ;
TAU0 = TA.'EFIS'*270./238./10950./RHO0     ;
*
*
 'DEBPROC' TRINOME XA*'FLOTTANT' XB*'FLOTTANT' XC*'FLOTTANT' mau*'MOT';
    DISCRI  = ( (XB**2.) - (4.*XA*XC) )**0.5                 ;
    RACINE1 = ( (-1.*XB) + DISCRI ) / (2.*XA)                ;
    RACINE2 = ( (-1.*XB) - DISCRI ) / (2.*XA)                ;
    LRACINE = 'PROG' RACINE1 RACINE2                         ;
    PRACINE = RACINE1 * RACINE2                              ;
    SRACINE = RACINE1 + RACINE2                              ;
    'SI' (PRACINE '<' 0.)                                    ;
        RACINE = 'MAXI' LRACINE                              ;
    'SINON'                                                  ;
        RACINE = 'MINI' LRACINE                              ;
    'FINSI'                                                  ;
 'FINPROC' RACINE                                            ;
*
*
CONVSIG = 1E6                              ;
CONVTO  = 24.*3600E3                       ;
*
TVIT     = 1.0E6                           ;
UZ_TVIT  = HS2/200.                        ;
VUZ1     = UZ_TVIT/TVIT                    ;
VUZ2     = -0.5 * VUZ1                     ;
VUZ3     =  1.7 * VUZ1                     ;
*
TBIF  = 0.6E6                              ;
TFIN  = 1.0E6                              ;
TFIN2 = 1.9E6                              ;
* ---------------------------------*
* Chargement en deplacement impose *
* ---------------------------------*
DEPZ     = 'DEPI' CLZH 1                                   ;
UZ_TBIF  = VUZ1 * TBIF                                     ;
UZ_TFIN  = UZ_TBIF + (VUZ2*(TFIN-TBIF))                    ;
UZ_TFIN2 = UZ_TFIN + (VUZ3*(TFIN2-TFIN))                   ;
'TITR' 'DEPLACEMENT IMPOSE'                                ;
EVDEP    = 'EVOL' 'MANU' 'Temps (s)' ('PROG' 0. TBIF     TFIN      TFIN2   ) 'Dep (m)'   ('PROG' 0. UZ_TBIF  UZ_TFIN   UZ_TFIN2)    ;
LDEPX = 'EXTR' EVDEP 'ABSC'                                ;
LDEPY = 'EXTR' EVDEP 'ORDO'                                ;
si DESSI ; 'DESS' EVDEP ; 'FINSI'                          ;
CHARMECA = 'CHAR' 'DIMP' DEPZ EVDEP                        ;
* --------------------------*
* Chargement en temperature *
* --------------------------*
TP1      = 1E-4                                                  ;
TE0      = 1320. + 273.                                          ;
TE2      = (TP1*TFIN2) + TE0                                     ;
CHTEMP   = 'MANU' 'CHPO' VOL1 1 'T' 1.                           ;
EVT      = 'EVOL' 'MANU' ('PROG' 0.  TFIN2)  ('PROG' TE0 TE2)    ;
CHARTEMP = 'CHAR' 'T' CHTEMP EVT                                 ;
* ---------------------------------*
* Chargement en densite de fission *
* ---------------------------------*
PHIP1    = 3E14                                                   ;
PHI2     = PHIP1 * TFIN2                                          ;
CHFISS   = 'MANU' 'CHPO' VOL1 1 'DFIS' 1.                         ;
EVF      = 'EVOL' 'MANU' ('PROG' 0.  TFIN2) ('PROG' 0. PHI2)      ;
CHARFISS = 'CHAR' 'DFIS' CHFISS EVF                               ;
*
* ---------------------*
* -------------------- *
* INSTANTS PRIVILEGIES *
* -------------------- *
* ---------------------*
XA     = (TA.'KGON' / 6.) * TAU0 * PHIP1                  ;
*
LZ     = 'MAXI' ('EXCO' MATR16 'LZZ')                     ;
PENTZ  = -1. * ( (XLTR1**2) / (2.*XGFTR1) )               ;
JZ     = 1./(LZ*PENTZ)                                    ;
LZLIM  = ('MAXI' EE) / (('ABS' PENTZ) * (1.+('MAXI' UU))) ;
WRUPT  = (2.*XGFTR1) / XLTR1                              ;
ERUPT  = WRUPT / LZ                                       ;
WFERM  = XBTR * WRUPT                                     ;
EFERM  = WFERM / LZ                                       ;
* ------------*
* Fissuration *
* ------------*
ET1   = VUZ1/HS2                        ;
ET0   = 0.                              ;
B0    = ET1 - (XALFA*TP1)      ; XB = -1. * B0 ;
XC    = (XLTR1/('MAXI' EE)) - ET0       ;
TFISS = TRINOME XA XB XC 'TFISS'        ;
* ----------*
* Fermeture *
* ----------*
* Ouverture residuelle de fermeture
ET1     = VUZ2    / HS2                    ;
ET0     = UZ_TBIF / HS2                    ;
ETH0    = XALFA*TP1*TBIF          ;
GF0     = XA*(TBIF**2)                     ;
FZ0     = ET0-(ETH0+GF0)                   ;
NUM     = FZ0 + (XLTR1*JZ)                 ;
DEN     = (1./('MAXI' EE)) + JZ            ;
SMAX    = NUM/DEN                          ;
EMAX    = JZ*(SMAX-XLTR1)                  ;
WMAX    = EMAX * LZ                        ;
WFERM   = XBTR * WMAX                      ;
EFERM   = WFERM / LZ                       ;
JZ_PRIM = (EMAX - EFERM) / SMAX            ;
*
B0      = ET1 - (XALFA*TP1)       ; XB = -1. * B0 ;
XC      = (EFERM + (ET1*TBIF)) - ET0       ;
TFERM   = TRINOME XA XB XC 'TFERM'         ;
* ------------*
* Reouverture *
* ------------*
ET1     = VUZ3    / HS2                    ;
ET0     = 'IPOL' TFERM LDEPX (LDEPY/HS2)   ;
ETH0    = XALFA*TP1*TFERM         ;
GF0     = XA*(TFERM**2)                    ;
FZ0     = ET0-(ETH0+GF0)                   ;
ET0     = UZ_TFIN / HS2                    ;
*
B0      = ET1 - (XALFA*TP1)       ; XB = -1. * B0 ;
XC      = (FZ0 + (ET1*TFIN)) - ET0         ;
TREOUV   = TRINOME XA XB XC 'TREOUV'       ;
* --------------------------- *
* Poursuite de la fissuration *
* --------------------------- *
ET0     = 'IPOL' TREOUV LDEPX (LDEPY/HS2)  ;
ETH0    = XALFA*TP1*TREOUV        ;
GF0     = XA*(TREOUV**2)                   ;
FZ0     = ET0-(ETH0+GF0)                   ;
ET0     = UZ_TFIN / HS2                    ;
*
B0      = ET1 - (XALFA*TP1)       ; XB = -1. * B0 ;
XC0     = (1./('MAXI' EE)) + JZ_PRIM       ;
XC0     = SMAX * XC0                       ;
XC      = (FZ0 + XC0 + (ET1*TFIN)) - ET0   ;
TBIF2   = TRINOME XA XB XC 'TBIF2'         ;
* -------------------*
* Liste des instants *
* -------------------*
VPAS  = TVIT/40.                                                      ;
LTIME = ('PROG' 0. 'PAS' VPAS TBIF  'PAS' VPAS TFIN  'PAS' VPAS TFIN2) 'ET' ('PROG' TFERM TREOUV TBIF2)                                   ;
LTIME = 'ORDO' LTIME                                                  ;
UN    = 'PROG' ('DIME' LTIME)*1                                       ;
* -------------------*
* Deformation totale *
* -------------------*
EVEPS = EVDEP / HS2                                ;
LEPSX = 'EXTR' EVEPS 'ABSC'                        ;
LEPSY = 'EXTR' EVEPS 'ORDO'                        ;
LEPS  = 'IPOL' LTIME LEPSX LEPSY                   ;
'TITR' 'DEFORMATION IMPOSEE'                       ;
EVEPS = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Def' LEPS ;
'SI' DESSI ; 'DESS' EVEPS ; 'FINSI' ;
* -------------------------------------*
* Temperature et deformation thermique *
* -------------------------------------*
LT  = (TP1*LTIME) + ((TE0-273.)*UN)               ;
'TITR' 'TEMPERATURE'                              ;
EVT = 'EVOL' 'MANU' 'Temps (s)' LTIME 'T (°C)' LT ;
'SI' DESSI ; 'DESS' EVT ; 'FINSI' ;
*
LTH    = XALFA*TP1*LTIME                      ;
'TITR' 'DEFORMATION THERMIQUE'                         ;
EVDTH  = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Def Th' LTH  ;
'SI' DESSI ; 'DESS' EVDTH ; 'FINSI' ;
* -------------------*
* Densite de fission *
* -------------------*
LPHI     = PHIP1*LTIME                                            ;
'TITR' 'DENSITE DE FISSION'                                       ;
EVF      = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Phi (SI)' LPHI        ;
'SI' DESSI ; 'DESS' EVF ; 'FINSI' ;
* ------------------------------------------------*
* Taux de combustion et deformation de gonflement *
* ------------------------------------------------*
 LTO     = (TAU0*PHIP1/2.)*(LTIME**2)                                  ;
'TITR' 'TAUX DE COMBUSTION'                                            ;
 EVTO_SI = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Taux(SI)' LTO              ;
'SI' DESSI ; 'DESS' EVTO_SI ; 'FINSI' ;
 EVTO    = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Taux(MWj/tU)' (LTO/CONVTO) ;
'SI' DESSI ; 'DESS' EVTO ; 'FINSI' ;
*
 LGF     = (TA.'KGON' / 3.) * LTO                                      ;
'TITR' 'DEFORMATION DE GONFLEMENT'                                     ;
 EVGF    = 'EVOL' 'MANU' 'Temps (s)' LTIME 'Def Gonf' LGF              ;
'SI' DESSI ; 'DESS' EVGF ; 'FINSI' ;

* ***************** *
* AVANT FISSURATION *
* ***************** *
LTIME_AV = LTIME 'MASQ' 'EGINFE' TFISS                             ;
LFZ      = LEPS-(LTH+LGF)                                          ;
*
LSIGZ  = ('MAXI' EE) * LFZ                                         ;
SIGF   = 'IPOL' TFISS LTIME (LSIGZ/CONVSIG)                        ;
* ************************************** *
* APRES FISSURATION ET AVANT BIFURCATION *
* ************************************** *
LTIME_AP = LTIME 'MASQ' 'SUPERIEUR' TFISS                          ;
LJZ      = JZ * LTIME_AP                                           ;
NUM      = LFZ + (XLTR1*LJZ)                                       ;
LE1      = (1./('MAXI' EE))*UN                                     ;
DEN      = LE1 + LJZ                                               ;
LSIGZ    = NUM/DEN                                                 ;
*
LXLTR1  = XLTR1*UN                                                 ;
LWZ     = LTIME_AP * ( (LSIGZ-LXLTR1)/PENTZ )                      ;
*
LEPSZF  = LWZ / LZ                                                 ;
*
SIGZB = 'IPOL' TBIF LTIME (LSIGZ/CONVSIG)                          ;
WB    = 'IPOL' TBIF LTIME LWZ                                      ;
* ********************************* *
* APRES BIFURCATION ET EN FERMETURE *
* ********************************* *
LTIME_AV = LTIME 'MASQ' 'EGINFE'    TBIF                 ;
LTIME_AP = LTIME 'MASQ' 'SUPERIEUR' TBIF                 ;
LJZ_PRIM = JZ_PRIM * LTIME_AP                            ;
LEFERM   = EFERM   * LTIME_AP                            ;
NUM      = LFZ - LEFERM                                  ;
DEN      = LE1 + LJZ_PRIM                                ;
LSIGZ    = (LSIGZ  * LTIME_AV) + ((NUM/DEN) * LTIME_AP)  ;
*
LEMAX    = EMAX * LTIME_AP                               ;
LSMAX    = SMAX * LTIME_AP                               ;
LEPSZF   = (LEPSZF * LTIME_AV) + LEMAX + (LJZ_PRIM * (LSIGZ - LSMAX))          ;
*
LWZ      = LZ * LEPSZF                                   ;
*
SIGZC  = 'IPOL' TFERM  LTIME (LSIGZ/CONVSIG)             ;
WFERMC = 'IPOL' TFERM  LTIME LWZ                         ;
* ****************************** *
* APRES FERMETURE EN COMPRESSION *
* ****************************** *
LTIME_AV = LTIME 'MASQ' 'EGINFE'    TFERM              ;
LTIME_AP = LTIME 'MASQ' 'SUPERIEUR' TFERM              ;
*
ET0      = 'IPOL' TFERM LTIME LEPS                     ;
ETH0     = XALFA*TP1*TFERM                    ;
GF0      = XA*(TFERM**2)                               ;
FZ0      = ET0-(ETH0+GF0)                              ;
*
LSIGZ_AP = ('MAXI' EE) * (LFZ - (FZ0 * LTIME_AP))      ;
LSIGZ    = (LSIGZ  * LTIME_AV) + (LSIGZ_AP * LTIME_AP) ;
*
LEPSZF   = (LEPSZF * LTIME_AV) + (EFERM * LTIME_AP)    ;
*
LWZ      = LZ * LEPSZF                                 ;
*
SIGZC    = 'IPOL' TREOUV  LTIME (LSIGZ/CONVSIG)        ;
* ****************************************** *
* REOUVERTURE AVANT POURSUITE DE FISSURATION *
* ****************************************** *
LTIME_AV = LTIME 'MASQ' 'EGINFE'    TREOUV               ;
LTIME_AP = LTIME 'MASQ' 'SUPERIEUR' TREOUV               ;
*
ET0      = 'IPOL' TREOUV LTIME LEPS                      ;
ETH0     = XALFA*TP1*TREOUV                     ;
GF0      = XA*(TREOUV**2)                                ;
FZ0      = ET0-(ETH0+GF0)                                ;
NUM      = LFZ - (FZ0 * LTIME_AP)                        ;
LJZ_PRIM = JZ_PRIM * LTIME_AP                            ;
DEN      = LE1 + LJZ_PRIM                                ;
LSIGZ    = (LSIGZ  * LTIME_AV) + ((NUM/DEN) * LTIME_AP)  ;
*
LEMAX    = EMAX * LTIME_AP                               ;
LSMAX    = SMAX * LTIME_AP                               ;
LEPSZF   = (LEPSZF * LTIME_AV) + LEMAX + (LJZ_PRIM * (LSIGZ - LSMAX))          ;
*
LWZ      = LZ * LEPSZF                                   ;
* ************************ *
* POURSUITE DE FISSURATION *
* ************************ *
LTIME_AV = LTIME 'MASQ' 'EGINFE'    TBIF2               ;
LTIME_AP = LTIME 'MASQ' 'SUPERIEUR' TBIF2               ;
*
LJZ      = JZ * LTIME_AP                                           ;
NUM      = LFZ + (XLTR1*LJZ)                                       ;
DEN      = LE1 + LJZ                                               ;
LSIGZ    = (LSIGZ  * LTIME_AV) + ((NUM/DEN) * LTIME_AP)            ;
*
LWZ      = (LWZ * LTIME_AV) + (LTIME_AP * ( (LSIGZ-LXLTR1)/PENTZ ));
*
LEPSZF  = LWZ / LZ                                                 ;
*
SIGZC  = 'IPOL' TBIF2  LTIME (LSIGZ/CONVSIG)             ;
WC     = 'IPOL' TBIF2  LTIME LWZ                         ;
*
'TITR' 'CONTRAINTE DANS LA DIRECTION Z'                              ;
EVSIGZ = 'EVOL' 'ROUG' 'MANU' 'Temps (s)' LTIME 'SIGZ(MPa)' (LSIGZ/CONVSIG)             ;
'SI' DESSI ; 'DESS' EVSIGZ; 'FINSI'                                   ;
'TITR' 'OUVERTURE DANS LA DIRECTION Z'                                ;
EVWZ   = 'EVOL' 'ROUG' 'MANU' 'Temps (s)' LTIME 'WZ(m)' LWZ           ;
'SI' DESSI ; 'DESS' EVWZ; 'FINSI'                                     ;
'TITR' 'DEFORMATION DE FISSURATION DANS LA DIRECTION Z'               ;
EVEPSFZ = 'EVOL' 'ROUG' 'MANU' 'Temps (s)' LTIME 'EPSFZ' LEPSZF       ;
'SI' DESSI ; 'DESS' EVEPSFZ; 'FINSI'                                  ;
'TITR' 'RELATION CONTRAINTE/OUVERTURE DANS LA DIRECTION Z'            ;
EV_FISZ = 'EVOL' 'ROUG' 'MANU' 'WZ(m)' LWZ 'SIGZ(MPa)' (LSIGZ/CONVSIG);
'SI' DESSI ; 'DESS' EV_FISZ; 'FINSI'                                  ;
*-------------------*
* Instants calcules *
*-------------------*
LIST1 = 'PROG' 0.  'PAS' 200.   3000. 'PAS' 1000.  10000. 'PAS' 10000. TFIN2 ;
* ---------------------------------------*
* Variables internes initiales 'PORO'=PO *
* ---------------------------------------*
VAR00 = 'ZERO' MODL1 'VARINTER'                                    ;
VAR01 = 'MANU' 'CHML' MODL1 'PORO' PO 'TYPE' 'VARIABLES INTERNES' 'STRESSES' ;
VAR0 = VAR00 + VAR01                                               ;
*
CHARTOT = CHARMECA 'ET' CHARTEMP 'ET' CHARFISS             ;
*
 TAB1                          = TABLE   ;
* TAB1.'TEMPERATURES'           = TABLE   ;
 TAB1.'VARIABLES_INTERNES'     = TABLE   ;
 TAB1.'BLOCAGES_MECANIQUES'    = CLT     ;
 TAB1.'MODELE'                 = MODL1   ;
 TAB1.'CHARGEMENT'             = CHARTOT ;
 TAB1.'VARIABLES_INTERNES' . 0 = VAR0    ;
 TAB1.'CARACTERISTIQUES'       = MATR1   ;
 TAB1.'TEMPS_CALCULES'         = LIST1   ;
 TAB1.'TEMPS_SAUVES'           = LIST1   ;
*TAB1.'TALPHA_REFERENCE'       = TALPHA  ;
*
TMASAU=table;
tab1 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;

 PASAPAS TAB1                            ;
*
*
TT    = TAB1 . 'TEMPS'                      ;
SS    = TAB1 . 'CONTRAINTES'                ;
VV    = TAB1 . 'VARIABLES_INTERNES'         ;
IN    = TAB1 . 'DEFORMATIONS_INELASTIQUES'  ;
NCONT = 'DIME' TAB1 . 'CONTRAINTES'         ;
*
LZEIT  = 'PROG' ;
LSIGZ  = 'PROG' ;
LW1    = 'PROG' ;
LW2    = 'PROG' ;
LW3    = 'PROG' ;
LEPSZF = 'PROG' ;
*
*
'REPE' BLOC NCONT ;
*
  IND  = &BLOC - 1         ;
  ZEIT = TT.IND            ;
*
  SIG  = SS.IND            ;
  SIGZ = 'EXCO' SIG 'SMZZ' ;
  SIGZ_MAX = 'MAXI' SIGZ   ; SIGZ_MIN = 'MINI' SIGZ   ;
*
  VI    = VV.IND                    ;
  EI    = IN.IND                    ;
*
  W1    = 'MAXI' ('EXCO' VI 'W1')   ;
  W2    = 'MAXI' ('EXCO' VI 'W2')   ;
  W3    = 'MAXI' ('EXCO' VI 'W3')   ;
  EPSZF = 'MAXI' ('EXCO' EI 'EIZZ') ;
*
  LZEIT  = LZEIT  'ET' ('PROG' ZEIT)       ;
  LSIGZ  = LSIGZ  'ET' ('PROG' SIGZ_MAX)   ;
  LW1    = LW1    'ET' ('PROG' W1)         ;
  LW2    = LW2    'ET' ('PROG' W2)         ;
  LW3    = LW3    'ET' ('PROG' W3)         ;
  LEPSZF = LEPSZF 'ET' ('PROG' EPSZF)      ;
*
'FIN' BLOC ;
*
*
* -----------------------*
* Controle des resultats *
* -----------------------*
LSOUS   = 'PROG' 1E3 'PAS' 2E3 9E3 1E5 8E5 1.5E6 1.6E6 ;

UN      = 'PROG' ('DIME' LSOUS)*1                          ;
*
SZ_CAL  = 'IPOL' LSOUS LZEIT LSIGZ                              ;
SZ_ANA  = 'IPOL' LSOUS LTIME (('EXTR' EVSIGZ 'ORDO') * CONVSIG) ;
EFZ_CAL = 'IPOL' LSOUS LZEIT LEPSZF                             ;
EFZ_ANA = 'IPOL' LSOUS LTIME ('EXTR' EVEPSFZ 'ORDO')            ;
WZ_CAL  = 'IPOL' LSOUS LZEIT LW1                                ;
WZ_ANA  = 'IPOL' LSOUS LTIME ('EXTR' EVWZ 'ORDO')               ;
*
PREX = 5E-2                                            ;
T_SZ = (SZ_ANA - SZ_CAL) / (SZ_ANA + UN)               ;
T_SZ = 'MAXI' ('ABS' T_SZ)                             ;
'SI' ( T_SZ '<EG' PREX )                               ;
   'ERRE' 0                                            ;
'SINO'                                                 ;
   'MESS' 'ERREUR MAXIMALE CONTRAINTE SELON Z > ' PREX ;
   'ERRE' 5                                            ;
'FINS'                                                 ;
*
WRUPT   = (2.*XGFTR1) / XLTR1                          ;
ERUPT   = WRUPT / LZ                                   ;
NONZERO = (ERUPT / 1E6) * UN                           ;
T_FZ = (EFZ_ANA - EFZ_CAL) / (EFZ_ANA +  NONZERO)      ;
T_FZ = 'MAXI' ('ABS' T_FZ)                             ;
'SI' ( T_FZ '<EG' PREX )                               ;
   'ERRE' 0                                            ;
'SINO'                                                 ;
   'MESS' 'ERREUR MAXIMALE DEFORMATION DE FISSURATION SELON Z > ' PREX                            ;
   'ERRE' 5                                            ;
'FINS'                                                 ;
*
NONZERO = (WRUPT / 1E6) * UN                           ;
T_WZ = (WZ_ANA - WZ_CAL) / (WZ_ANA +  NONZERO)         ;
T_WZ = 'MAXI' ('ABS' T_WZ)                             ;
'SI' ( T_WZ '<EG' PREX )                               ;
   'ERRE' 0                                            ;
'SINO'                                                 ;
   'MESS' 'ERREUR MAXIMALE OUVERTURE SELON Z > ' PREX  ;
   'ERRE' 5                                            ;
'FINS'                                                 ;
*
*
TAB               = 'TABLE'               ;
TAB . 'TITRE'     = 'TABLE'               ;
TAB . 'TITRE' . 1 = 'Solution Analytique' ;
TAB . 'TITRE' . 2 = 'Solution Calculee'   ;
*
*
EVSIGZ0 = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'SIGZ(MPa)' (LSIGZ/CONVSIG);
'SI' DESSI                                 ;
   'TITR' 'CONTRAINTE DANS LA DIRECTION Z' ;
   'DESS' (EVSIGZ 'ET' EVSIGZ0) TAB 'LEGE' ;
'FINSI'                                    ;
*
EVWZ0  = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'WZ(m)' LW1 ;
'SI' DESSI                                            ;
   'TITR' 'OUVERTURE DANS LA DIRECTION Z'             ;
   'DESS' (EVWZ 'ET' EVWZ0) TAB 'LEGE'                ;
'FINSI'                                               ;
*
EVEPSFZ0 = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'EPSFZ' LEPSZF ;
'SI' DESSI                                                 ;
   'TITR' 'DEFORMATION DE FISSURATION DANS LA DIRECTION Z' ;
   'DESS' (EVEPSFZ 'ET' EVEPSFZ0) TAB 'LEGE'               ;
'FINSI'                                                    ;
*
EVFISZ = 'EVOL' 'MANU' 'WZ(m)' LW1 'SIGZ(MPa)' (LSIGZ/CONVSIG)       ;
'SI' DESSI                                                           ;
   'TITR' 'RELATION CONTRAINTE/OUVERTURE DANS LA DIRECTION Z'        ;
   'DESS' (EV_FISZ 'ET' EVFISZ) TAB 'LEGE'                           ;
'FINSI'                                                              ;
*
'FIN';














