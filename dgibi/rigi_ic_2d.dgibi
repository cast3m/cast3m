************************************************************************
*            Petit test sur les elements 2D incompressibles            *
*                      (ICT3, ICQ4, ICT6, ICQ8)                        *
* Verification du calcul de la rigidite                                *
* Calculs en 2D : deformations et contraintes planes, axisymmetrie     *
* Sur un essai de traction simple, en effort impose, on verifie qu'il  *
* est bien retrouve a l'encastrement                                   *
************************************************************************


** Options generales
OPTI 'DIME' 2 'ECHO' -1 ;
MESS ;
leg = MOTS 'TRI3' 'QUA4' 'TRI6' 'QUA8' ;
lef = MOTS 'ICT3' 'ICQ4' 'ICT6' 'ICQ8' ;
yo = 200.E9 ;
nu = 0.3 ;
pri = 1.E3 ;
errmax = 0. ;
tol1 = 1.E-10 ;


** Points du maillage
p1 = 0. 0. ;
p2 = 1. 0. ;
p3 = 1. 2. ;
p4 = 0. 2. ;


** Valeurs theoriques de l'effort de reaction (F = pression * section)
fdp = pri * 1. ;
fcp = pri * 1. ;
fax = pri * pi * 1. ;


** Boucle sur les types d'elements finis
n1 = DIME lef ;
REPE b1 n1 ;
  typeg = EXTR leg &b1 ;
  typef = EXTR lef &b1 ;
  MESS ;
  MESS 'Element fini ' typef ;
  MESS '-----------------' ;
  MESS 'Hypothese    | F calculee     | F theorique    | Ecart relatif';
* maillage
  OPTI 'ELEM' typeg ;
  lb = DROI 1 p1 p2 ;
  ld = DROI 1 p2 p3 ;
  lh = DROI 1 p3 p4 ;
  lg = DROI 1 p4 p1 ;
  s1 = DALL lb ld lh lg ;
* boucle sur les hypotheses 2D
  lf = PROG ;
  REPE b2 3 ;
*   matrices de blocage
    SI (EGA &b2 1) ;
      moh = 'def.  planes ' ;
      fth = fdp ;
      OPTI 'MODE' 'PLAN' 'DEFO' ;
      bl1 = BLOQ 'UY' lb ;
      bl2 = BLOQ 'UX' lg ;
    FINSI ;
    SI (EGA &b2 2) ;
      moh = 'cont. planes ' ;
      fth = fcp ;
      OPTI 'MODE' 'PLAN' 'CONT' ;
      bl1 = BLOQ 'UY' lb ;
      bl2 = BLOQ 'UX' lg ;
    FINSI ;
    SI (EGA &b2 3) ;
      moh = 'axisymetrie  ' ;
      fth = fax ;
      OPTI 'MODE' 'AXIS' ;
      bl1 = BLOQ 'UZ' lb ;
      bl2 = BLOQ 'UR' lg ;
    FINSI ;
    bl = bl1 ET bl2 ;
*   matrice de rigidite
    mo = MODE s1 'MECANIQUE' typef ;
    ma = MATE mo 'YOUN' yo 'NU' nu ;
    r = RIGI mo ma ;
*   second membre
    f = PRES 'MASS' mo lh (-1. * pri) ;
*   resolution et calcul de l'effort de reaction a la base
    u = RESO (r ET bl) f ;
    f1 = MAXI 'ABS' (RESU (REAC u bl1)) ;
*   calcul de l'ecart relatif
    SI (EGA &b2 1) ;
      err1 = ABS ((f1 - fdp) / fdp) ;
    FINSI ;
    SI (EGA &b2 2) ;
      err1 = ABS ((f1 - fcp) / fcp) ;
    FINSI ;
    SI (EGA &b2 3) ;
      err1 = ABS ((f1 - fax) / fax) ;
    FINSI ;
    MESS moh '|' f1 '|' fth '|' err1 ;
    errmax = MAXI (PROG errmax err1) ;
  FIN b2 ;
FIN b1 ;


** Fin du cas test
SAUT 1 'LIGN' ;
SI (errmax > tol1) ;
  MESS 'ECHEC  DU CAS TEST' ;
  MESS 'Ecart relatif max. = ' errmax ;
  ERRE 5 ;
SINON ;
  MESS 'SUCCES DU CAS TEST' ;
FINSI ;


FIN ;



