OPTI 'DIME' 3 'ELEM' 'CUB8' 'ECHO' 0 ;



************************************************************************
*                Test de l'operateur IPOL option 'PID'                 *
*                 interpolation multi-dimensionnelle                   *
*                par Ponderation Inverse a la Distance                 *
*                                                                      *
* Application a l'interpolation d'une fonction de 1 variable, puis     *
* une fonction de 2 variables d'espace (sinus cardinal)                *
* - test avec des CHPOINTs et des MCHAMLs                              *
* - test sur des points aleatoires                                     *
* - test sur des points identiques au nuage                            *
************************************************************************



** Indicateur pour le trace des champs interpoles
itrac = FAUX ;

** Tolerance pour les tests des valeurs interpolees
tol1 = 1.E-12 ;





************************************************************************
*                           PETIT TEST EN 1D                           *
************************************************************************

** Maillage d'une ligne
p1 = 0. 0. 0. ;
p2 = 10. 0. 0. ;
l1 = DROI 3 p1 p2 ;

** Fonction sinus cardinal
x = COOR 1 l1 ;
p0 = x POIN 'EGALE' 0. ;
mp1 = CHAN 'POI1' l1 ;
mp1b = DIFF mp1 p0 ;
xb = REDU x mp1b ;
f1 = (SIN (xb*180./ pi)) / xb ;
f1b = MANU 'CHPO' p0 1 'SCAL' 1. 'NATURE' 'DIFFUS' ;
f1 = f1 ET f1b ;
ev0 = EVOL 'CHPO' f1 l1 ;

** Nuage contenant l'information de depart
ch1 = (NOMC 'X' x) ET (NOMC 'F1' f1) ;
nu1 = NUAG ch1 ;

** Points ou effectuer l'interpolation
p3 = -5. 0. 0. ;
p4 = 15. 0. 0. ;
l2 = DROI 10000 p3 p4 ;

** Interpolation
x2 = NOMC (COOR 1 l2) 'X' ;
np = 2. ;
f2p = IPOL nu1 x2 'PID' np ;
f2g = IPOL nu1 x2 'GAUSS' ;
f2r = IPOL nu1 x2 'RATIO' ;

** Trace des graphes
SI itrac ;
  lx2 = EXTR (EVOL 'CHPO' x2 l2) 'ORDO' ;
  lp = EXTR (EVOL 'CHPO' f2p l2) 'ORDO' ;
  lg = EXTR (EVOL 'CHPO' f2g l2) 'ORDO' ;
  lr = EXTR (EVOL 'CHPO' f2r l2) 'ORDO' ;
  evp = EVOL 'ROUG' 'MANU' lx2 lp ;
  evg = EVOL 'VERT' 'MANU' lx2 lg ;
  evr = EVOL 'JAUN' 'MANU' lx2 lr ;
  tit1 = CHAI 'IPOL par PID (rouge), par GAUSS (vert) '
              'et par RATIO (jaune)' ;
  tleg = TABL ;
  tleg . 1 = MOT 'MARQ CARR NOLI' ;
  tleg . TITRE = TABL ;
  tleg . TITRE . 1 = 'Sinus cardinal' ;
  tleg . TITRE . 2 = CHAI 'PID avec p = ' np ;
  tleg . TITRE . 3 = 'GAUSS' ;
  tleg . TITRE . 4 = 'RATIO' ;
  DESS (ev0 ET evp ET evg ET evr) 'TITR' tit1 'LEGE' tleg ;
FINSI ;





************************************************************************
*                           PETIT TEST EN 2D                           *
************************************************************************

** CREATION DU NUAGE
** -----------------
** Un petit maillage
ne = 30 ;
xmax = 10. ;
mxmax = -1. * xmax ;
l1 = D ne (mxmax mxmax 0.) (xmax mxmax 0.) ;
s1 = l1 TRAN ne (0. (2. * xmax) 0.) ;
mp1 = CHAN 'POI1' s1 ;

** Coordonnees x et y puis fonction (ici on prend un sinus cardinal
*  un peu tordu)
x y z = COOR s1 ;
r = ((x*x) + (y*y))**0.5 ;
p0 = r POIN 'EGALE' 0. ;
mp1b = DIFF mp1 p0 ;
rb = REDU r mp1b ;
xb = REDU x mp1b ;
f1 = (10.*(SIN (rb*180./ pi))/rb) + (0.001*(xb**4)) ;
f1b = MANU 'CHPO' p0 1 'SCAL' 10. 'NATURE' 'DIFFUS' ;
f1 = f1 ET f1b ;
** Nuage de valeurs : ensemble de n-uplets (x,y,f1)
ch1 = (NOMC 'X' x) ET (NOMC 'Y' y) ET (NOMC 'F1' f1) ;
nu1 = NUAG ch1 ;



** CREATION DES POINTS A CALCULER
** ------------------------------
** On tire nbp points dans les environs du nuage
nbp = 100 ;
lx = BRUI 'BLAN' 'GAUS' 0. (0.6 * xmax) nbp ;
ly = BRUI 'BLAN' 'GAUS' 0. (0.6 * xmax) nbp ;
lz = PROG nbp*0. ;
mp2 = POIN lx ly lz ;

** On y ajoute quelques points "singuliers" : presents dans le nuage
mp3 = (s1 POIN 'PROC' (0. 0. 0.)) ET
      (s1 POIN 'PROC' (0. xmax 0.)) ET
      (s1 POIN 'PROC' (mxmax mxmax 0.)) ET
      (s1 POIN 'PROC' ((-0.5 * xmax) (0.2 * xmax) 0.)) ET
      (s1 POIN 'PROC' ((0.3 * xmax) (0.3 * xmax) 0.)) ;
mp2 = mp2 ET mp3 ;



** INTERPOLATION
** -------------
** Interpolation (dans un CHPOINT et un MCHAML)
p = 3 ;
xmp2 ymp2 zmp2 = COOR mp2 ;
chp2 = (NOMC 'X' xmp2) ET (NOMC 'Y' ymp2) ;
f2p = IPOL nu1 chp2 'PID' p ;
chm2 = CHAN 'CHAM' chp2 mp2 ;
f2m = IPOL nu1 chm2 'PID' p ;



** COMPARAISON VALEURS CALCULEES / THEORIE
** ---------------------------------------
maxerr = 0. ;
MESS ; MESS ;
MESS 'Numero   | Valeur         | Valeurs        | Erreur  ' ;
MESS 'du point | theorique      | interpolees    | relative' ;
MESS '--------------------------------------------------------' ;
REPE b0 (NBEL mp2) ;
  p0 = mp2 POIN &b0 ;
* valeurs calculees (CHPOINT et MCHAML)
  f0ca1 = EXTR f2p 'F1' p0 ;
  f0ca2 = EXTR f2m 'F1' 1 &b0 1 ;
* valeur theorique
  x0 y0 z0 = COOR p0 ;
  lw = PROG ;
  f0th = 0. ;
  REPE b1 (NBEL mp1) ;
    p1 = mp1 POIN &b1 ;
    x1 = EXTR ch1 'X' p1 ;
    y1 = EXTR ch1 'Y' p1 ;
    d1 = (((x1 - x0)**2) + ((y1 - y0)**2))**0.5 ;
    SI (d1 <EG 1.E-15) ;
      lw = PROG 1. ;
      f0th = (EXTR ch1 'F1' p1) ;
      QUIT b1 ;
    FINSI ;
    w1 = 1. / (d1**p) ;
    lw = lw ET w1 ;
    f0th = f0th + (w1 * (EXTR ch1 'F1' p1)) ;
  FIN b1 ;
  wtot = SOMM lw ;
  f0th = f0th / wtot ;
* comparaison
  err1 = ABS ((f0ca1 - f0th) / f0th) ;
  err2 = ABS ((f0ca2 - f0th) / f0th) ;
  MESS &b0 ' |' f0th '|' f0ca1 '|' err1 ;
  MESS '         |                |' f0ca2 '|' err2 ;
  MESS '--------------------------------------------------------' ;
  maxerr = MAXI (PROG maxerr err1 err2) ;
FIN b0 ;



** TEST FINAL
** ----------
MESS ; MESS ;
SI (maxerr <EG tol1) ;
  MESS '***** Succes du cas test ! *****' ;
SINON ;
  MESS '***** Echec  du cas test ! *****' ; MESS ; MESS ;
  ERRE 5 ;
FINSI ;



** VISUALISATION DES POINTS CALCULES SUR LE NUAGE INITIAL
** ------------------------------------------------------
SI itrac ;
  u1 = (MANU 'CHPO' s1 2 'UX' 0. 'UY' 0. 'NATURE' 'DIFFUS') ET
       (NOMC 'UZ' f1 'NATURE' 'DIFFUS') ;
  s1b = s1 PLUS u1 ;
  u2p = (MANU 'CHPO' mp2 2 'UX' 0. 'UY' 0. 'NATURE' 'DIFFUS') ET
        (NOMC 'UZ' f2p 'NATURE' 'DIFFUS') ;
  mp2p = (mp2 PLUS u2p) COUL 'ROUG' ;
  f2g = IPOL nu1 chp2 'GAUSS' ;
  u2g = (MANU 'CHPO' mp2 2 'UX' 0. 'UY' 0. 'NATURE' 'DIFFUS') ET
        (NOMC 'UZ' f2g 'NATURE' 'DIFFUS') ;
  mp2g = (mp2 PLUS u2g) COUL 'VERT' ;
  f2r = IPOL nu1 chp2 'RATIO' ;
  u2r = (MANU 'CHPO' mp2 2 'UX' 0. 'UY' 0. 'NATURE' 'DIFFUS') ET
        (NOMC 'UZ' f2r 'NATURE' 'DIFFUS') ;
  mp2r = (mp2 PLUS u2r) COUL 'JAUN' ;
  tit1 = CHAI 'IPOL par PID (rouge), par GAUSS (vert) '
              'et par RATIO (jaune)' ;
  TRAC (s1b ET mp2p et mp2g et mp2r) 'TITR' tit1 ;
FINSI ;



FIN ;


