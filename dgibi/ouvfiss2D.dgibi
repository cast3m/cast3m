*
*
* TEST DE l'OPERATEUR OUVFISS EN 2D
*
* ON TIRE SUR UN BARREAU ENDOMMAGEABLE
*
* UN DEFAUT EST CREE POUR LOCALISER L'ENDOMMAGEMENT
*
* UNE SEULE FISSURE EST GENEREE, DONC L'OUVERTURE EST Delta(L) - Sigma/E0
*
* On UTILISE DU MAZARS A ECROUISSAGE LINEAIRE
*
OPTI DIME 2 ELEM QUA4 MODE PLAN CONT;
h=0.02;
L=0.2;
*
* caracteristiques
*
YG=3.E10;
Ft=3.E6;
Kt=Ft/YG;
GF=300.;
*
* AT<-10 pour écrouissage linéaire
*
AT=-20.;
*
* BT est la déformation à laquelle la contrainte s'annule
*
BT=2*GF/Ft/h;
DENSITE h;
P1=0. 0.;
P2=L 0.;
P3=0. H;
D1=D P1 P3;
S1=D1 TRANS P2;
D2=S1 COTE 3;
MOD1=MODE S1 MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT MAZARS;
MAT1=MATE MOD1 YOUN YG NU 0.2 KTR0 (KT/0.9) ATRA
               AT BTRA BT ACOM 1.4 BCOM 1900.;
CH1=MANU CHAM MOD1 'TYPE'
   'CARACTERISTIQUES' 'POSI' 'RIGIDITE' KTR0 1 1 1 (0.1*KT);
MAT1=MAT1 - CH1;
CL1=BLOQ D1 UX;
CL2=BLOQ P1 UY;
CL3=BLOQ D2 UX;
CLT=CL1 ET CL2 ET CL3;
F1=DEPI CL3 1.;
PROG1=PROG 0. 1.;
EVOL1=EVOL MANU PROG1 PROG1;
CHAR1=CHARGEMENT F1 EVOL1 'DIMP';
T0=Kt*L;
DELTAT=T0/10.;
TF=BT*h;
LT1=PROG (0.9*T0) PAS (T0/10.) TF;
TAB1=TABLE;
TAB1.MODELE=MOD1;
TAB1.CARACTERISTIQUES=MAT1;
TAB1.BLOCAGES_MECANIQUES=CLT;
TAB1.CHARGEMENT=CHAR1;
TAB1.TEMPS_CALCULES=LT1;
PASAPAS TAB1;
*
* courbe globale
*
ev1=@global tab1 evol1 cl3 fx;
*
* ouverture de fissure théorique
*
progd=extr ev1 absc;
progf=extr ev1 ordo;
*
* contrainte = F/h, deformation=F/(E*h),
*  deplacement élastique = F*L/(E*h)
*
progouv=progd - (progf * L / YG / h);
evouv=evol manu progd 'deplacement' progouv 'ouverture';
*Dess evouv;
ouvfiss tab1;
*
* ouverture calculee
*
progouv2=prog;
n1=dime tab1.temps;
repeter bou1 n1;
  ouv1=extr tab1 . OUV . (&BOU1 - 1) EPXX 1 1 1;
  PROGOUV2=INSE PROGOUV2 &BOU1 OUV1;
FIN BOU1;
evouv2=evol manu progd 'deplacement' progouv2 'ouverture';
*Dess evouv2;
EVERR=ABS (EVOUV - EVOUV2);
SOM1=EXTR 1 (INTG EVERR);
SOM2=EXTR 1 (INTG EVOUV);
ERR_REL=SOM1/SOM2;
SI (ERR_REL > 1.E-2) ;
  ERREUR 5;
FINSI;

FIN;












