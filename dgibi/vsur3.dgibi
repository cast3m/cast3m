* fichier :  vsur3.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*********************************************************
*  vsur3.dgibi : Test de VSUR en coq6 et coq8           *
*********************************************************
opti echo 0 dime 3 elem qua8;
*
*  Définition de la géométrie
*
p0 = 0. 0. 0.;
p1 = 9. 0. 6.;
p2 = 7. 6. 10.;
p3 = -2. 6. 4.;
p4 = 8. 3. 8.;
p5 = -1. 3. 2.;
l1 = D 3 p0 p1;
l2 = D 2 p1 p2;
l3 = D 5 p2 p3;
l4 = D 4 p3 p0;
*
cn1 = l1 et l2 et l3 et l4;
obj = SURF cn1 'PLANE';
*
*  Définition des modèles
*
obj6 = obj elem 'TRI6';
obj8 = obj elem 'QUA8';
mo6 =  modeli obj6  mecanique elastique isotrope coq6;
mo8 =  modeli obj8  mecanique elastique isotrope coq8;
mod1 = mo6 et mo8;
*
*  Comparaison des champs obtenus par VSUR
*
* Premier modèle : coq6 et coq8
che1 = VSUR mod1;
chn1 = VSUR mod1 'NORM';
chj1 = JACO mod1;
cnx1 = exco 'VX' che1 'SCAL';
cny1 = exco 'VY' che1 'SCAL';
cnz1 = exco 'VZ' che1 'SCAL';
cnn1 = (cnx1*cnx1) + (cny1*cny1) + (cnz1*cnz1);
cjn1 = chj1*chj1;
cze1 = cnn1 - cjn1;
*list cze1;
cnx1 = exco 'VX' chn1 'SCAL';
cny1 = exco 'VY' chn1 'SCAL';
cnz1 = exco 'VZ' chn1 'SCAL';
cnn1 = (cnx1*cnx1) + (cny1*cny1) + (cnz1*cnz1);
*list cnn1;
CPZ1 = CHAN CHPO MOD1 CZE1;
CPN1 = CHAN CHPO MOD1 CNN1;
RE11 = MAXI CPZ1 'ABS';
RE12 = (MAXI CPN1 'ABS') - 1.;
RE13 = (MAXI CPN1 'ABS') - (MINI CPN1 'ABS');
RES1 = RE11 + RE12 + RE13;
SI (res1 < 1.e-6) ;
  MESS 'OPERATEUR <VSUR> ERR  0';
  ERRE  0;
SINON;
  MESS 'OPERATEUR <VSUR> ERR  5';
  ERRE  5;
FINSI;
fin;








