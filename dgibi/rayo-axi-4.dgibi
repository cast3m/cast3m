* fichier :  rayo-axi-4.dgibi

*******************************************
* Section : Thermique Rayonnement
*******************************************

***************************************************
*                                                 *
* Ce jeu de données permet la vérification du     *
* calcul des facteurs de forme dans le cas        *
* axisymétrique. On calcule le flux dû au         *
* rayonnement entre 2 sphères concentriques, la   *
* température de chacune des sphères étant        *
* homogène. Le résultat est comparé à la solution *
* analytique, voir                                *
*                                                 *
* Jean Crabol, Transfert de Chaleur, tome 2,      *
* Masson, 1990, pp.169-175                        *
*                                                 *
***************************************************

**** Options                                   ****

option dime 2 mode axis elem qua4 ;

graph = faux ;

****************************************************
****    DEFINITION DE LA GEOMETRIE DU PROBLEME  ****
****************************************************

****    Dimensions de la sphere interieure      ****

RIint  =  97.2e-3;
MRIint = -97.2e-3;
RIext  =  99.0e-3;
MRIext = -99.0e-3;

****    Dimensions de la sphere exterieure      ****

REint  =  130.0e-3;
MREint = -130.0e-3;

REext  =  134.5e-3;
MREext = -134.5e-3;

***    Nombres de divisions                     ****

n1 = 10 ; n2 = 2 ; n3 = 4 ;

***    Le centre                                ***

O = 0. 0. ;

****    Points sur l'enceinte interieure        ****

P1 = 0. RIint;
P2 = RIint 0.;
P3 = 0. MRIint;
P4 = 0. MRIext;
P5 = RIext 0.;
P6 = 0. RIext;

****    Points sur l'enceinte exterieure        ****

Q1 = 0. REint;
Q2 = REint 0.;
Q3 = 0. MREint;
Q4 = 0. MREext;
Q5 = REext 0.;
Q6 = 0. REext;

***    Les lignes (droites et arcs)             ****

ARCP1 = cerc n1 P1 O P2 ;
ARCP2 = cerc n1 P2 O P3 ;
ARCII = ARCP1 et ARCP2 ;

ARCP3 = cerc n1 P4 O P5 ;
ARCP4 = cerc n1 P5 O P6 ;
ARCIE = ARCP3 et ARCP4 ;


ARCQ1 = cerc n1 Q1 O Q2 ;
ARCQ2 = cerc n1 Q2 O Q3 ;
ARCEI = ARCQ1 et ARCQ2 ;

ARCQ3 = cerc n1 Q4 O Q5 ;
ARCQ4 = cerc n1 Q5 O Q6 ;
ARCEE = ARCQ3 et ARCQ4 ;

P6P1 = droi n2 P6 P1 ;
P3P4 = droi n2 P3 P4 ;


Q6Q1 = droi n2 Q6 Q1 ;
Q3Q4 = droi n2 Q3 Q4 ;


Q1P6 = droi n3 Q1 P6 ;
P4Q3 = droi n3 P4 Q3 ;

***    Les surfaces                              ****

SPHERINT = P6P1 ARCII P3P4 ARCIE DALLE ;

SPHEREXT = Q6Q1 ARCEI Q3Q4 ARCEE DALLE ;

SPHERAIR = Q1P6 ARCIE P4Q3 ARCEI DALLE ;

iARCIE=inve ARCIE ;
iARCEI=inve ARCEI ;
cavite = iARCIE et iARCEI ;

TOUTacie  = SPHERINT et SPHEREXT ;
TOUT = TOUTacie ;

si(graph) ;
   titr 'Le maillage du modele' ;
   trac TOUT ;
finsi ;

**** Rayonnement ****

MRI = MODELI iARCIE thermique RAYONNEMENT 'CAVITE'  CONS 'CAV1';
MRE = MODELI iARCEI thermique RAYONNEMENT 'CAVITE'  CONS 'CAV1';
mrt = mri et mre ;

vei = 0.9 ;
vee = 0.3 ;
ei = mate mri   'EMIS' vei  ;
ee = mate mre   'EMIS' vee ;
chemis = ei et ee ;

tref = 0. ;

*** On testera sur le champ de température suivant ***

T_inter =  700.0 ;
T_exter = 1000.0 ;

T_inter = T_inter + 273.0 ;
T_exter = T_exter + 273.0 ;

chptint = manu chpo spherint 1 T T_inter nature discret ;
chptext = manu chpo spherext 1 T T_exter nature discret ;

chptmpt = chptint et chptext ;

*** Les facteurs de forme ****************************

ff = ffor mrt  chemis;

*** Matrice de rayonnement ***************************

mr = raye mrt ff chemis ;

*** Conductivité due au rayonnement ******************

cte_sb = 5.67e-8 ;

cr =  rayn mrt mr (chan CHAM chptmpt mrt GRAVITE) cte_sb ;

*** Flux résultant ***********************************

flux12 = cr * chptmpt ;

flux1 = redu flux12 iARCIE ;
flux2 = redu flux12 iARCEI ;

rsfl1 = maxi (resu flux1) ;
rsfl2 = maxi (resu flux2) ;


*** Solution analytique *******************************

solflux = cte_sb * 4 * pi * RIext * RIext * ((T_exter**4)-(T_inter**4));
denom1 = 1.0 / vei ;
denom2 = (RIext * RIext * ((1.0/vee) - 1.0)) / (REint*REint) ;
denom = denom1 + denom2 ;
solflux = solflux / denom ;

diffrel = 100 * ((abs rsfl2) - solflux) / solflux ;

opti echo 0 ;
mess 'Flux arrivant sur la sphère intérieure' rsfl1 ;
mess 'Flux partant de la sphère extérieure  ' rsfl2 ;
mess 'Solution analytique pour le flux = ' solflux ;
mess 'Erreur obtenue est de ' diffrel '%';
opti echo 1 ;

*** Test si c'est OK *********************************

si((abs diffrel) > 1.) ;
   erre 5 ;
finsi;

*** Bye **********************************************

fin ;








