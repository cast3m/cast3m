************************************************************
** raff04.dgibi
************************************************************
*                                                          *
*  Calcul elasitique 3D des facteurs d'intencité de        *
*  contraintes pour une fissure circulaire sous un         *
*  chargempent de traction à l'infini incliné de 45 deg    *
*  par rapport au pla de fissure (mode mixte)              *
*                                                          *
*  Test De RAFF avec X-FEM et GTHETA                       *
*                                                          *
*   Comparaison de la simulations avec une solution de     *
*   reference pour k1, k2 et k3 donne par :                *
*   [TADA, STRESS ANALYSIS HANDBOOK,1973]                  *
*                                                          *
*création : gg, le 15.03.2017                              *
************************************************************
*** Options

*** options de calcul
opti dime 3 elem CUB8 mode trid;

*** options de trace

graph = faux ;
*graph = vrai ;

*complet = vrai;
complet = faux;

opti 'TRAC' X ;
*opti 'TRAC' psc ;


*******************************************************
*** Maillage : cube sain avec raff


* Dimensions du maillage
L1 = 0.2 ;
L2 = L1 / 2.;
L4 = L1 / 4.;
a0 = L1 / 10 ;
*a0 = L1 / 7.5 ;


na1 = 4;


* paramettre de raffinement

dmin= 1.5 * L4;
densmax= L4 ;
densmin=densmax/8.;
dmax=dmin + densmax;

si (complet) ;
  dmin2= 0.7 * dmin;
  densmax2= dmin ;
  densmin2=densmax/16;
  dmax2=dmin2 + densmax2;
finsi;

* maillage de la fissure
dx1 = a0 / 8;

dx1elim = 1.E-4 * dx1;
  DENS dx1;




* oeuil pour la visualisation
eye1 = -100. -70. 30. ;
eye2 = 0. -0. 100. ;
eye3 = -100. -30. 10.;

*  boite dallée (a)
vzL1 = (0. 0. L1);
pa00 = 0.  0.  0.;
pa01 = L1  0.  0.;
pa02 = L1  L1  0.;
pa03 = 0.  L1  0.;
la01 = pa00 droi na1 pa01;
la02 = pa01 droi na1 pa02;
la03 = pa02 droi na1 pa03;
la04 = pa03 droi na1 pa00;

* on cree des surface qu on eliminera apres coup
la06 = la02 plus vzL1;
la07 = la03 plus vzL1;
sa02 = la02 regl na1 la06;
sa03 = la03 regl na1 la07;

* creation du volume
sa01 = dall la01 la02 la03 la04 'PLAN';
va01 = sa01 volu na1 'TRAN' vzL1;
* recup
pa04 = va01 POIN 'PROCH' (pa00 plus vzL1);
pa05 = va01 POIN 'PROCH' (pa01 plus vzL1);
pa06 = va01 POIN 'PROCH' (pa02 plus vzL1);
pa07 = va01 POIN 'PROCH' (pa03 plus vzL1);
elim va01 sa02 dx1elim;
elim va01 sa03 dx1elim;
sa04 = va01 face 2;



***********************************************************
*** Maillage de la fissure
&ELEM = VALE 'ELEM';
opti elem TRI3;

*** petite fissure circulaire inclinée
*   beta0 = 0.;
*  beta0 = 20.;
beta0 = 45.;
*  a0 = L1 / 10.;   deja defini + haut pour caler la finesse du maillage
rapab0 = 1.;
b0 = a0 * rapab0;
* finesse du maillage de la fissure
da0 = dx1 / 4.;
si (complet);
    da0 = da0/2.;
finsi;
*  da0 = dx1 / 12.;
na0 = enti (a0 / da0);
nb0 = enti (b0 / da0);

* on maille un cercle dans le plan
* cad avec b0=a0 : on ajustera apres si ellipse
* on tournera apres si inclinee
DENS da0;
* on fait un carré
pf0 = 0. 0. 0.;
xf1 = a0 * ( (2**(-1.5)) + 0.5) / 2.;
pf1 = xf1  0.  0.;
pf2 = xf1 xf1  0.;
pf3 =  0. xf1  0.;
lf1 = pf0 droi pf1;
lf2 = pf1 droi pf2;
lf6 = pf2 droi pf1;
lf3 = pf2 droi pf3;
lf5 = pf3 droi pf2;
lf4 = pf3 droi pf0;
sf1 = dall lf1 lf2 lf3 lf4 'PLAN';
sf1 = inve sf1;
nf1 = nbel lf1;
* on fait le disque
pf4 = a0. 0. 0.;
xf5 = a0 / (2**0.5);
pf5 = xf5 xf5 0.;
pf6 = 0. a0. 0.;
cf5 = CERC nf1 pf6 pf0 pf5;
cf6 = CERC nf1 pf5 pf0 pf4;
sf2 =  regl (cf5 et cf6) (lf5 et lf6);
* assemblage
sfron0 = (cf5 et cf6) coul 'ROUG';
scrack0 = (sf1 et sf2) coul 'ORAN';
* rotation de beta0
DEPL scrack0 'TOUR' beta0 pf0 pf1;
* tres important
opti elem &ELEM;

scrack0 = ELIM scrack0 dx1elim;
sfron0 = ELIM sfron0  dx1elim;


***********************************************************
*symétries
* de la structure
* par % a z=0

va01bis = va01 syme 'PLAN' pa00 pa01 pa03;

va01 = va01 et va01bis;

* par % a y=0
va01bis = va01 syme 'PLAN' pa00 pa01 pa04;

va01 = va01 et va01bis;
ELIM va01 dx1elim;

* par % a X=0

va01bis = va01 syme 'PLAN' pa00 pa03 pa04;

va01 = va01 et va01bis;
ELIM va01 dx1elim;


* de la fissure
sfron0b scrack0b = sfron0 scrack0 syme 'DROIT' pa00 pa01;
scrack0b = INVE scrack0b ;
sfron0b = INVE sfron0b ;

scrack0 = scrack0 et scrack0b;
sfron0 = sfron0 et sfron0b;

sfron0b scrack0b = sfron0 scrack0 syme 'DROIT' pa00 pf6;
scrack0b = INVE scrack0b ;
sfron0b = INVE sfron0b ;

scrack0 = scrack0 et scrack0b;
sfron0 = sfron0 et sfron0b;

ELIM scrack0 dx1elim;
ELIM sfron0 dx1elim;

**********************************************************
*** Raffinement autour de la fissure

xa ya za= coor va01 ;

DF1= (((ya**2) + (xa**2.) + (za**2))**0.5);

aff = (((densmax - densmin)*(DF1 - dmax))/(dmax - dmin)) + densmax ;
dens1 = born aff 'SCAL' 'COMPRIS' (densmin*1.000001)
(densmax*1.000001);


vb01 = raff dens1 va01;

si (complet);
  vb01 = raff dens1 va01;

  xb yb zb= coor vb01 ;

  DF2= (((yb**2) + (xb**2.) + (zb**2))**0.5);
  aff2 = (((densmax2 - densmin2)*(DF2 - dmax2))/(dmax2 - dmin2))
       + densmax2 ;
  dens2 = born aff2 'SCAL' 'COMPRIS' (densmin2*1.000001)
  (densmax*21.000001);


  vc01 = raff dens2 vb01;

  vol0 = ELIM vc01 dx1elim;
sinon ;
  vol0 = ELIM vb01 dx1elim;
finsi;


si (graph) ;
    p001 = 0. 0. 1.;
    p010 = 0. 1. 0.;
    trac va01  eye1 'COUP' pf0 pf1 pf3 'CACH'
    'TITR' 'Maillage avant raff';
    trac dens1 va01 eye1 'COUP' pf0 pf1 pf3 'TITR' 'Champ de densite';
    trac dens1 va01 eye1 'COUP' pf0 p010 p001 'TITR' 'Champ de densite';
    trac vb01  eye1 'COUP' pf0 pf1 pf3 'CACH' 'TITR'
    'Maillage apres raff';
    trac vb01  eye1 'COUP' pf0 p010 p001 'CACH' 'TITR'
    'Maillage apres raff';
    si (complet);
       trac dens2 vb01 eye1 'COUP' pf0 pf1 pf3
       'TITR' ' Deuxieme champ de densite';
       trac vc01  eye1 'COUP' pf0 pf1 pf3 'CACH' 'TITR'
       'Maillage apres raff';
    finsi;
finsi ;

**********************************************************************
* séparation en deux maillages (fem et X-fem)
x0 y0 z0 = coor vol0;

si (complet);
  zz0 = COLI y0 (cos beta0) z0 (-1.* (sin beta0));
  yy0 = COLI y0 (sin beta0) z0 (cos beta0);
  pxfem_r = ((( x0**2) + (yy0**2))**0.5) POIN 'COMPRIS'
            (0.2*a0) (0.6 * dmin) ;

  pxfem_h = (abs zz0) POIN 'EGINFE' (0.12 * dmin) ;
  pxfem = INTE pxfem_r pxfem_h;
sinon;
*     pxfem =(((y0**2) + (x0**2.) + (z0**2))**0.5) POIN
*        'EGINFE' (0.6 * dmin) ;
  zz0 = COLI y0 (cos beta0) z0 (-1.* (sin beta0));
  yy0 = COLI y0 (sin beta0) z0 (cos beta0);
  pxfem_r = (((x0**2) + (yy0**2))**0.5) POIN 'COMPRIS'
            (0.6*a0) (0.3 * dmin) ;
  pxfem_h = (abs zz0) POIN 'EGINFE' (0.12 * dmin) ;
  pxfem = INTE pxfem_r pxfem_h;
finsi;
*

vol1 = vol0 ELEM 'APPU' 'LARG' pxfem;
*vol1 = vol1 coul 'ROUG';
vol2 = vol0 DIFF vol1;

*** construction des level set ***
* psi1 phi1 = PSIP vol1 scrack0 'DEUX'  sfron0;
psi1 phi1 = PSIP vol0 scrack0 'DEUX'  sfron0;

* qq objets sympas pour le post traitement
con0 = aret vol0;
env0 = ENVE vol0;

* qq objets utiles pour les CL
x2 y2 z2 = coor vol2;
lignx0 = ((x2**2)+(z2**2)) poin 'EGINFE' dx1elim;
poiy0 = pa00 ;
facz0 = z0 poin 'EGINFE' ((-1. * L1) + dx1elim);
facz2 = z0 poin 'EGSUPE' (L1 - dx1elim);
facz0 = env0 elem 'APPU' 'STRICT' facz0;
facz2 = env0 elem 'APPU' 'STRICT' facz2;

* tracés
mess '      RAFF va01    =  vol0      ';
mess ' NBNO=' (nbno va01)  (nbno vol0);
mess ' NBEL=' (nbel va01)  (nbel vol0);


* opti donn 5 ;
**********************************************************
***   MODELE & MATERIAU   ***
**********************************************************

* coef elastique
Ey0 = 2.E11;
*  nu0 = 0.03;
nu0 = 0.3 ;
rho0= 7800. ;
mu0 = (0.5 * Ey0) / (1. + nu0) ;
Estar0 = Ey0 / (1. - (nu0**2));

* Loi de Paris  (on convertit C : mm -> m)
*  mParis = 3.;
*  CParis = 1.E-9 * 1.E-3;


**********************************************************
*** Modele & materiau  ***
*
* zone de propagation (X-FEM)
mod1 = MODE vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' 'XC8R';
mat1 = MATE mod1 'YOUN' Ey0 'NU' nu0 'RHO' rho0  ;
che1X = TRIE mod1 psi1 phi1 ;

* constructionsion des blocages des ddl X-fem non actifs dans
* les éléments de transition et des relations de conformité dues au
*raffinement dans cette zone.
rel1X = RELA mod1;

* elements standards
mod2 = MODE vol2 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' 'CUB8';
mat2 = MATE mod2 'YOUN' Ey0 'NU' nu0 'RHO' rho0 ;
*
* constructionsion et des relations de conformité dues au
*raffinement dans cette zone.
rel2 = RELA mod2;


mod1tot = mod1 et mod2 ;
mat1tot = mat1 et mat2 ;
rel1tot = rel1x et rel2;


**********************************************************
***   CL et DEPLACEMENTS IMPOSES   ***
**********************************************************
szz0 = -100.E6;


*** CL ***
px = L1 0. 0. ;
px = POIN vol2 'PROC' px;
py = 0. L1 0. ;
py = POIN vol2 'PROC' py;
pz = 0. 0. L1 ;
pz = POIN vol2 'PROC' pz;

*blocage des mouvements de coprs rigide

cl0x = bloq px 'UY' 'UZ';
cl0y = bloq py 'UX' 'UZ';
cl0z = bloq pz 'UX' 'UY';

cl1tot =  (cl0x et cl0y et cl0z ) ;

*** chargement en mode mixte ***

for1 = PRES 'MASS' mod1tot facz2 szz0;
for2 = PRES 'MASS' mod1tot facz0 szz0;
for1tot = for1 et for2 ;

si(graph);
    trac (vect for1tot  'FORC' 'BLEU') ((coul (px et py et pz )
    'VERT') et con0  et scrack0 et sfron0)
    'TITR' 'CHARGEMENT ET CONDITIONS AUX LIMITES';
    trac (con0 et scrack0 et ((enve vol1) coul ROUG))
     TITR 'Erichissement';
fins;


*** matrice elastique et assemblage
Rig1tot = RIGI mod1tot mat1tot;
K1tot = Rig1tot et cl1tot et rel1tot;


*******************************************************
* CALCUL ELASTIQUE
*******************************************************

u1 = RESO K1tot for1tot ;


* ****************************
* *** deformees ***
*
si(graph);
   u1phy = XFEM 'RECO' u1 mod1tot ;
   sig1  = SIGM mod1tot u1 mat1tot;
   ucrk1u ucrk1d = XFEM 'FISS' scrack0  u1 mod1tot ;
    amp1 = 200.;
    def2 =  (DEFO u1phy  con0  amp1)
          et (DEFO ucrk1u scrack0 amp1 'ORAN')
          et (DEFO ucrk1d scrack0  amp1 'ROUG');
    TRAC def2 eye3 'CACH' 'TITR' 'Deformee';
    TRAC u1 vol0 eye1 'COUP' pf0 pf1 pf3 'TITR' 'Deplacements';
    TRAC u1 vol0 eye1 'COUP' pf0 p010 p001 'TITR' 'Deplacements';
    TRAC sig1 mod1tot eye1 'COUP' pf0 pf1 pf3 'TITR' 'Contraintes';
    TRAC sig1 mod1tot eye1 'COUP' pf0 p010 p001 'TITR' 'Contraintes';
finsi;
*  opti donn 5;


************************************************************************
*        On calcule les facteurs d'intensité de contraintes            *
*        sur une moitier du front car G_theta ne prévoi pas           *
*        de faire ce calcul en 3D pour des front déssinant de          *
*        contours fermé.                                               *
************************************************************************



xc yc zc = coor scrack0;
xneg =  xc poin 'EGINF' 0.0 ;
xpos =  xc poin 'EGSUP' 0.0 ;
sfron1 = sfron0 elem 'APPU' 'STRI' xpos COUL VERT;
sfron2 = sfron0 elem 'APPU' 'STRI' xneg;

si (graph);
    TRAC (SFRON1 et SFRON2) 'TITR' 'Deux demi fronts';
finsi;
***   Calcul des FIC par methode integrale G_THETA   ***
KTAB = tabl ;
KTAB . 'OBJECTIF' = MOT 'DECOUPLAGE';
*KTAB . 'OBJECTIF' = MOT 'J';
KTAB .'PSI' = psi1;
KTAB .'PHI' = phi1;
KTAB .'FRONT_FISSURE' = sfron1 ;
KTAB .'MODELE' = mod1tot;
KTAB .'CARACTERISTIQUES' = mat1tot;
KTAB .'SOLUTION_RESO' = u1;
KTAB .'CHARGEMENTS_MECANIQUES' = for1tot;
KTAB . 'POINT_CENTRE' = PA00;
KTAB . 'COUCHE' = 2;

G_THETA KTAB;

Kchpo1 = KTAB . 'CHPO_RESULTATS' ;


K1ev = EVOL BLEU 'CHPO' Kchpo1 'K1' sfron1;
K2ev = EVOL DEFA 'CHPO' Kchpo1 'K2' sfron1;
K3ev = EVOL ROUG 'CHPO' Kchpo1 'K3' sfron1;

S1pr = EXTR K1ev 'ABSC';
K1pr = EXTR K1ev 'ORDO';
K2pr = EXTR K2ev 'ORDO';
K3pr = EXTR K3ev 'ORDO';

************************************************************************
*        Formule analytique des FIC en milieu infinie                  *
*        pour une fissure circulaire inclinée                          *
*  "The stress analysis of crack Handook" H.TADA P.C0.Paris G.R.IRWIN  *
************************************************************************

K1A = (2. * (-1.0)* szz0 * ((sin (-1.0 * beta0))**2) *
      (( a0 / pi )**0.5))+ ( S1pr * 0.);

CA = (4. * (-1.0)* szz0 * (sin (-1.0 * beta0)) * (cos (-1.0 * beta0)) *
     (( a0 / pi )**0.5))   / (2. - nu0);

K2A= CA * ( cos ((S1pr * 180. ) / (a0 * pi )));

K3A= CA * (1. - nu0)* ( sin ((S1pr * 180. ) / (a0 * pi )));

K1Aev = EVOL BLEU 'MANU' S1pr K1A;
K2Aev = EVOL DEFA 'MANU' S1pr K2A;
K3Aev = EVOL ROUG 'MANU' S1pr K3A;
si (graph);
** Calcul des FIC par methode integrale G_THETAsur l autre demi front **
  KSTAB = tabl ;
  KSTAB . 'OBJECTIF' = MOT 'DECOUPLAGE';
*  KSTAB . 'OBJECTIF' = MOT 'J';
  KSTAB .'PSI' = psi1;
  KSTAB .'PHI' = phi1;
  KSTAB .'FRONT_FISSURE' = sfron2 ;
  KSTAB .'MODELE' = mod1tot;
  KSTAB .'CARACTERISTIQUES' = mat1tot;
  KSTAB .'SOLUTION_RESO' = u1;
  KSTAB .'CHARGEMENTS_MECANIQUES' = for1tot;
  KSTAB . 'POINT_CENTRE' = PA00;
  KSTAB . 'COUCHE' = 2;

  G_THETA KSTAB;

  KSchpo1 = KSTAB . 'CHPO_RESULTATS' ;

  KS1ev = EVOL VERT 'CHPO' KSchpo1 'K1' sfron2;
  KS2ev = EVOL ORAN 'CHPO' KSchpo1 'K2' sfron2;
  KS3ev = EVOL ROSE 'CHPO' KSchpo1 'K3' sfron2;

  SS1pr = EXTR KS1ev 'ABSC';
  KS1pr = EXTR KS1ev 'ORDO';
  KS2pr = EXTR KS2ev 'ORDO';
  KS3pr = EXTR KS3ev 'ORDO';

  KS1A = (2. * (-1.0)* szz0 * ((sin (-1.0 * beta0))**2) *
      (( a0 / pi )**0.5))+ ( S1pr * 0.);

  CSA = (4. * (-1.0)* szz0 * (sin (-1.0 * beta0)) *
 (cos (-1.0 * beta0)) * (( a0 / pi )**0.5))   / (2. - nu0);

  KS2A= CSA * ( cos ((S1pr * 180. ) / (a0 * pi ) + 180.));

  KS3A= CSA * (1. - nu0)* ( sin ((SS1pr * 180. ) / (a0 * pi ) + 180. ));

  KS1Aev = EVOL VERT 'MANU' SS1pr KS1A;
  KS2Aev = EVOL ORAN 'MANU' SS1pr KS2A;
  KS3Aev = EVOL ROSE 'MANU' SS1pr KS3A;


  tdess1 = tabl;
  tdess1.'TITRE'= TABL;
  tdess1.'TITRE' . 1= MOT 'KI sim';
  tdess1.'TITRE' . 2= MOT 'KII sim ';
  tdess1.'TITRE' . 3= MOT 'KIII sim ';
  tdess1.'TITRE' . 4= MOT 'KI ana';
  tdess1.'TITRE' . 5= MOT 'KII ana ';
  tdess1.'TITRE' . 6= MOT 'KIII ana ';
  tdess1 . 4 = 'MOT' ' TIRR ';
  tdess1 . 5 = 'MOT' ' TIRR ';
  tdess1 . 6 = 'MOT' 'TIRR ';
  dess (K1ev et K2ev et K3ev et K1Aev et K2Aev et K3Aev )
  'LEGE' tdess1 'TITR' 'SIF sur un demi front';
  dess (KS1ev et KS2ev et KS3ev et KS1Aev et KS2Aev et KS3Aev)
  'LEGE' tdess1 'TITR' 'SIF sur l autre demi front';
fins;
************************************************************************
*   Erreur en norme L2 des FIC le long du front                        *
*  (On s'eloigne des bords du demi front sur lequel on a fait          *
*  le calcul g-theta pour ignorer les effet de bords dues a G-theta)   *
************************************************************************
si (complet);
   dint = 17;
   fint = 92;
   ip6 = 55;
   ip5 = 28;
sinon ;
   dint = 6;
   fint = 51;
   ip6 = 29;
   ip5 = 15;
finsi;

DiffL2K1 =((K1Aev - K1ev)**2) /  ((EXTR K1A ip6)**2);
DiffL2K2 =((K2Aev - K2ev)**2) /   ((EXTR K2A 1)**2);
DiffL2K3 =((K3Aev - K3ev)**2) /  ((EXTR K3A ip6)**2);

ErrL2K1 =((intg DiffL2K1 dint fint)/
        ((EXTR S1pr fint ) -(EXTR S1pr dint)))**0.5;
ErrL2K2 =((intg DiffL2K2 dint fint)/
        ((EXTR S1pr fint ) - (EXTR S1pr dint)))**0.5;
ErrL2K3 =((intg DiffL2K3 dint fint)/
        ((EXTR S1pr fint ) -(EXTR S1pr dint)))**0.5;
ErrL2K1 = extr ErrL2K1 1;
ErrL2K2 = extr ErrL2K2 1;
ErrL2K3 = extr ErrL2K3 1;
list ErrL2K1;
list ErrL2K2;
list ErrL2K3;


si (graph);
 opti donn 5;
finsi;

SI ((ErrL2K1 < (0.05)) et  (ErrL2K2 < (0.10)) et (ErrL2K3 < 0.15));
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
  Erre 0;
Sinon;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  Erre 5;
finsi;
si (graph);
   ind1K = inde (KTAB . 'CHAMP_THET1');
   teta1 = KTAB . 'CHAMP_THET1' . (ind1K . 1);
   vtheta1 = vect teta1 'BLEU' ;
   trac vtheta1  (vol1 et scrack0 ) 'COUP' pa00 vzL1 pa03 'CACH';
finsi;

TEMP 'IMPR' 'MAXI' 'CPU';
FIN ;









