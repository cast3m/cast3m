* fichier :  fluage_maxwell_1.dgibi
************************************************************************
* Section : Mecanique Fluage
************************************************************************
*       Test fluage_maxwell_1.dgibi: Jeux de données  *
*       --------------------------------------------  *
*                                                     *
*
**************************************************
*                                                *
*  TEST DE VERIFICATION D'UNE LOI DE COMPORTEMENT*
*  --------------------------------------------  *
*              DE MATERIAU                       *
*              -----------                       *
*                                                *
*  LOI DE COMPORTEMENT DE FLUAGE DE :            *
*              MAXWELL                           *
*                                                *
*  TEST POUR DES ELEMENTS MASSIFS                *
*  ------------------------------                *
*                                                *
*  MAILLAGE:                                     *
*   UNE PLAQUE CARREE DE COTE L=1 M              *
*                                                * 
*  CHARGEMENT:                                   *
*   CISAILLEMENT PUR                             *
*   FORCES IMPOSEES                              *
*   DEFORMATIONS PLANES                          *  
*                                                *
************************************************** 

opti dime 2 elem qua4 ; 

* 
* Maillage 
* 
p1 = 0. 0. ; 
p2 = 1. 0. ; 
p3 = 1. 1. ; 
p4 = 0. 1. ; 
li1 = d p1 1 p2 ; 
li2 = d p2 1 p3 ; 
li3 = d p3 1 p4 ; 
li4 = d p4 1 p1 ; 
mail1 = 'SURF' ( li1 'ET' li2 'ET' li3 'ET' li4 ) 'PLAN' ; 
* 
* Modele 
* 
AF1 = 1.E-45 ; 
AF2 = 5. ; 
AF3 = 1.2 ; 
SMAX = 2.1E8; 
youn0 = 1.5E+4; 
nu0 = 0.3 ; 

prog1=prog 0.    1.D12; 
prog20=prog 1.5E+4 1.5E+4; 
E0=evol manu prog1 prog20; 

*prog1=prog 0. 1.D12; 
prog21=prog 1.11E+6 1.11E+6; 
E1=evol manu prog1 prog21; 

*prog1=prog 0.    1.D12; 
prog22=prog 1.12E+6 1.12E+6; 
E2=evol manu prog1 prog22; 

*prog1=prog 0. 1.D12; 
prog23=prog 3.43E+5 3.43E+5; 
E3=evol manu prog1 prog23; 

*prog1=prog 0. 1.D12; 
prog24=prog 4.14E+5 4.14E+5; 
E4=evol manu prog1 prog24; 

proyou= prog20 + prog21 + prog22 + prog23 + prog24 ;
youn0 = evol manu 'TEMP' prog1 'YOUN' proyou; 

TRR1 = 0.46; 
TRR2 = 0.45; 
TRR3 = 3.62; 
TRR4 = 45.1; 

mod0 =MODE mail1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' 'FLUAGE' 'MAXWELL'; 
mat0 =MATE mod0 'YOUN' youn0 'NU' nu0
'EM0' E0 'EM1' E1 'EM2' E2 'EM3' E3 'EM4' E4
'TR1' TRR1 'TR2' TRR2 'TR3' TRR3 'TR4' TRR4;

* 
* Conditions aux limites 
* 
cl1 = 'BLOQ' mail1 'UY' ; 
cl2 = 'BLOQ' li1 'UX' ; 
cl0 = cl1 'ET' cl2 ; 
* 
* Chargement 
* 
depmax = 7.E-1 ; 
*chp1 = 'COOR' 2 mail1 ; 
*chp1 = chp1 * depmax / 1. ; 
*chp1 = 'NOMC' chp1 'UX' ; 
*titr ' Champ de forces impose' ; 
**trace chp1 mail1 ; 
*chp2 = 'MANU' 'CHPO' mail1 1 'UY' 0. ; 
*chp0 = chp1 + chp2 ; 
dep1 = 'FORCE' li1 'FX' ( -1. * depmax ) 'FY' 0. ; 
dep2 = 'FORCE' li3 'FX' depmax 'FY' 0. ; 
dep0 = dep1 'ET' dep2 ; 
ev0 = 'EVOL' 'MANU' temps ( 'PROG' 0. 1.E-4 1000. ) 
y ( 'PROG' 0. 1. 1. ) ; 
cha0 = 'CHAR' 'MECA' dep0 ev0 ; 
* 
dt0 = 0.020 ; 
dt1 = 2. * dt0 ; 
dt2 = 5. * dt1 ; 
dt3 = 5. * dt2 ; 
tfin0 = 20. ; 
* 
* Appel à PASAPAS 
* 
ta1 = 'TABLE' ; 
ta1 .'MODELE' = mod0 ; 
ta1 .'CARACTERISTIQUES' = mat0 ; 
ta1 .'CHARGEMENT' = cha0 ; 
ta1 .'BLOCAGES_MECANIQUES' = cl0 ; 
ta1 .'TEMPS_CALCULES' = ( 'PROG' 0. 'PAS' 1.E-4 2.E-4 ) 'ET' 
( 'PROG' 3.E-4 dt0 ) 'ET' 
( 'PROG' dt1 'PAS' dt0 dt2 ) 'ET' 
( 'PROG' ( dt2 + dt2 ) 'PAS' dt2 dt3 ) 'ET' 
( 'PROG' ( dt3 + dt3 ) 'PAS' dt3 tfin0 ) ; 
* 
list ta1.'TEMPS_CALCULES'; 
TMASAU=table; 
ta1. 'MES_SAUVEGARDES'=TMASAU; 
TMASAU.'DEFTO'=VRAI; 
TMASAU.'DEFIN'=VRAI; 
* 
PASAPAS ta1; 

FIN;


