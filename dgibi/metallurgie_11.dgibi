* fichier :  metallurgie_11.dgibi
************************************************************************
* Section : Metallurgie
************************************************************************

*__________________________________________________________________*
*   TEST METALLURGIE_11                                            *
*                                                                  *
*   CALCUL DES PROPORTIONS DE PHASE METALLURGIQUE                  *
*                                                                  *
*   Un MODELE thermo-metallurgique est cree et un diagramme TRC    *
*   correspondant est cree                                         *
*                                                                  *
*   Le modele metallurgique est code en ESOPE (Cast3m)             *
*                                                                  *
*__________________________________________________________________*
*                                                                  *
*  On trace le diagramme TRC                                       *
*__________________________________________________________________*
*
'OPTI' 'TRAC' PSC 'EPTR' 10 ;

************************************************************************
*  Pour faire des beaux DESSINS
************************************************************************
* LISTMOTS contenant les couleurs
  LESCOUL = 'MOTS' 'VIOL' 'VERT' 'CYAN' 'BRIQ' 'BOUT' 'JAUN' 'OLIV'
                   'ORAN' 'ROUG' 'BLEU' 'BRUN' 'CARA' 'GRIS' 'NOIR' ;
  NBCOUL  = 'DIME' LESCOUL;
************************************************************************

OPTI DIME 2 ELEM 'TRI3';

P1 = 0. 0. ;
P2 = 1. 0. ;
P3 = 0. 1. ;

MAILT   = MANU 'TRI3' P1 P2 P3;

LISTPHA  = MOTS 'AUST' 'MART' 'BAIN' 'FERR' 'MB  '               ;
LISTREAC = MOTS 'MB  ' 'MART' 'BAIN' 'FERR' 'AUST' 'AUST' 'AUST' ;
LISTPROD = MOTS 'AUST' 'AUST' 'AUST' 'AUST' 'MART' 'BAIN' 'FERR' ;
LISTTYPE = MOTS 'LEBL' 'LEBL' 'LEBL' 'LEBL' 'KOIS' 'LEBL' 'LEBL' ;
NOMCONS = '16MND5';

MODCP1 = MODE MAILT 'METALLURGIE' 'PHASES' LISTPHA
                                  'REACTIFS' LISTREAC
                                  'PRODUITS' LISTPROD
                                  'TYPE' LISTTYPE
                                  'CONS' NOMCONS ;

TINI = 900.  ;
TFIN = 20.   ;


*1ere transformation :
PEQ1 = EVOL 'MANU' 'T'   (PROG 716. 802. )
                   'PEQ' (PROG 0.   1.   );
F1   = EVOL 'MANU' 'TPOI'(PROG -1.D-6 0. )
                   'F'   (PROG  0.    1. );
TAU1 = EVOL 'MANU' 'T'   (PROG 716. 802. )
                   'TAU' (PROG 12.  0.5  );

*2eme transformation : parametres identiques a la premiere

*3eme transformation : parametres identiques a la premiere

*4eme transformation : parametres identiques a la premiere

*5eme transformation
MS5 = 380. ;
KM5 = 0.0247 ;

*6eme transformation
PEQ6 = EVOL 'MANU' 'T'   (PROG 375 380 405 600 )
                   'PEQ' (PROG 0.  1.  1.  0.  );

F6  = 'EVOL' 'MANU' 'TPOI' (PROG -100. -80. -60. -50. -40. -30. -25. -20. -18. -15. -12. -10. -9. -5. -1. -0.05 0.D0 1.D-6)
                    'F'    (PROG 0.005 1.573 2.857 3.417 3.982 4.583 4.833 5.26 5.472 6.033 7.675 11.4 18.45 17.1 0.328 0.00238 0.00238 0.) ;

TAU6 = EVOL 'MANU' 'T'   (PROG 375  380)
                   'TAU' (PROG 1.D6 20 );

*7eme transformation
PEQ7 = 'EVOL' 'MANU' 'T' (PROG 625 630 730 735 )
                     'PEQ' (PROG 0   1   1   0   );

F7  = 'EVOL' 'MANU' 'TPOI' (PROG -8.5  -6.2 -4.7  0.  1.D-6)
                    'F'    (PROG  0.001 0.13 1.5  1.5 0.   );

TAU7 = EVOL 'MANU' 'T'   (PROG 625  630 )
                   'TAU' (PROG 1.D6 5   );


*Calcul automatique de la liste des vitesses de refroidissement
NBVI  = 30.       ;
VIT1  = 250.      ;
VIT2  = 2.        ;
VLOG1 = 'LOG' VIT1;
VLOG2 = 'LOG' VIT2;

PASLOG = (VLOG2 - VLOG1) / (NBVI - 1);
LRELOG ='PROG' VLOG1 'PAS' PASLOG VLOG2;
LISTREF='EXP' LRELOG;

Seuil = 0.005 ;


ALPHA = 1. ;


NBPHA = 'DIME' LISTPHA ;
*initialisation des TABLES :
EVO_TAB = 'TABL' ;
TSEUIL = 'TABL' ;
EVOT_TAB = 'TABL' ;
PROP = 'TABL' ;
TRC = 'VIDE' 'EVOLUTIO' ;
REPE BOU_INI NBPHA ;

    indexINI = &BOU_INI;
    EVO_TAB . indexINI = 'TABL' ;
    TSEUIL . indexINI = 'TABL' ;
    TSEUIL . indexINI . 'tpsS1' = 'PROG' ;
    TSEUIL . indexINI . 'tpsS2' = 'PROG' ;
    TSEUIL . indexINI . 'TS1' = 'PROG' ;
    TSEUIL . indexINI . 'TS2' = 'PROG' ;

FIN BOU_INI ;


REPE BOU_VIT (DIME LISTREF) ;

  vit = &BOU_VIT;

* Declaration de la vitesse de refroidissement : en Degres/s
  DT_vit = -1 * ( 'EXTR' LISTREF vit ) ;

* F2 et F3 dependent de la vitesse :
  F1L = 'IPOL' DT_vit F1 ;
  F6L = 'IPOL' DT_vit F6 ;
  F7L = 'IPOL' DT_vit F7 ;

* Calcul de la duree du refroidissement :
  DUREE = -1. * (TINI - TFIN) / DT_vit ;

* Declaration du Temps initial :
  TEMPINI = 1. ;
* Calcul du Temps final :
  TEMPFIN = TEMPINI + DUREE ;
* Calcul du pas de temps en fonction de la vitesse de refroidissement :
  DTPS    = -10. / DT_vit ;

  LTCAL   ='PROG' TEMPINI 'PAS' DTPS TEMPFIN ;
  T       = TINI + (DT_vit * (LTCAL - TEMPINI));

* Initialisation de CHE1 : prop de phases initiales :
  PROP . 'AUST' = 'PROG' ALPHA ;
  PROP . 'MART' = 'PROG' 0. ;
  PROP . 'BAIN' = 'PROG' 0. ;
  PROP . 'FERR' = 'PROG' 0. ;
  PROP . 'MB  ' = 'PROG' 0. ;
  CTYP = 'CHAI' 'VARIABLES_INTERNES' ;
  CHE1 ='MANU' 'CHML' MODCP1 'AUST' ('EXTR' (PROP . 'AUST') 1)
                             'MART' ('EXTR' (PROP . 'MART') 1)
                             'BAIN' ('EXTR' (PROP . 'BAIN') 1)
                             'FERR' ('EXTR' (PROP . 'FERR') 1)
                             'MB  ' ('EXTR' (PROP . 'MB  ') 1)
               'TYPE' CTYP 'NOEUD' ;


  nbpas = (DIME LTCAL) - 1;
  REPE CTEMP nbpas;

*   On recupere le temps debut de pas et le temps fin de pas :
    temp0   = EXTR LTCAL  &CTEMP     ;
    temp1   = EXTR LTCAL (&CTEMP + 1);
    CHETPS0 = 'MANU' 'CHML' MAILT 'TEMP' temp0 ;
    CHETPS1 = 'MANU' 'CHML' MAILT 'TEMP' temp1 ;
*   On calcule la temperature debut de pas et la temperature fin de pas :
    T0      = EXTR T  &CTEMP             ;
    T1      = EXTR T (&CTEMP + 1)        ;
    CHT0    = 'MANU' 'CHML' MAILT 'T' T0 ;
    CHT1    = 'MANU' 'CHML' MAILT 'T' T1 ;

*   On determine les differents parametres materiaux :
    PEQ11L = 'IPOL' T0 PEQ1 ;
    PEQ12L = 'IPOL' T1 PEQ1 ;

    PEQ61L = 'IPOL' T0 PEQ6 ;
    PEQ62L = 'IPOL' T1 PEQ6 ;

    PEQ71L = 'IPOL' T0 PEQ7 ;
    PEQ72L = 'IPOL' T1 PEQ7 ;

    TAU11L = 'IPOL' T0 TAU1 ;
    TAU12L = 'IPOL' T1 TAU1 ;

    TAU61L = 'IPOL' T0 TAU6 ;
    TAU62L = 'IPOL' T1 TAU6 ;

    TAU71L = 'IPOL' T0 TAU7 ;
    TAU72L = 'IPOL' T1 TAU7 ;

*   On definit le Materiaux debut de pas et le materiaux fin de pas :
    MATCP1 = 'MATE' MODCP1 'PEQ1' PEQ11L 'TAU1' TAU11L 'F1' F1L
                           'PEQ2' PEQ11L 'TAU2' TAU11L 'F2' F1L
                           'PEQ3' PEQ11L 'TAU3' TAU11L 'F3' F1L
                           'PEQ4' PEQ11L 'TAU4' TAU11L 'F4' F1L
                           'MS5'  MS5    'KM5'  KM5
                           'PEQ6' PEQ61L 'TAU6' TAU61L 'F6' F6L
                           'PEQ7' PEQ71L 'TAU7' TAU71L 'F7' F7L;

    MATCP2 = 'MATE' MODCP1 'PEQ1' PEQ12L 'TAU1' TAU12L 'F1' F1L
                           'PEQ2' PEQ12L 'TAU2' TAU12L 'F2' F1L
                           'PEQ3' PEQ12L 'TAU3' TAU12L 'F3' F1L
                           'PEQ4' PEQ12L 'TAU4' TAU12L 'F4' F1L
                           'MS5'  MS5    'KM5'  KM5
                           'PEQ6' PEQ62L 'TAU6' TAU62L 'F6' F6L
                           'PEQ7' PEQ72L 'TAU7' TAU72L 'F7' F7L;

    CHE1 = COMP MODCP1 (CHE1 'ET' CHETPS0 'ET' CHT0 'ET' MATCP1 ) ( CHETPS1 'ET' CHT1 'ET' MATCP2 ) ;

*   Extraction pour tracer :
    Pi     ='EXCO' CHE1 AUST 'AUST';
    PiEvol = 'EXTR' Pi AUST 1 1 1;
    PROP . 'AUST' = PROP . 'AUST' 'ET' PiEvol ;

    Pi     ='EXCO' CHE1 MART 'MART';
    PiEvol = 'EXTR' Pi MART 1 1 1;
    PROP . 'MART' = PROP . 'MART' 'ET' PiEvol ;

    Pi     ='EXCO' CHE1 BAIN 'BAIN';
    PiEvol = 'EXTR' Pi BAIN 1 1 1;
    PROP . 'BAIN' = PROP . 'BAIN' 'ET' PiEvol ;

    Pi     ='EXCO' CHE1 FERR 'FERR';
    PiEvol = 'EXTR' Pi FERR 1 1 1;
    PROP . 'FERR' = PROP . 'FERR' 'ET' PiEvol ;

    Pi     ='EXCO' CHE1 MB 'MB  ';
    PiEvol = 'EXTR' Pi MB 1 1 1;
    PROP . 'MB  ' = PROP . 'MB  ' 'ET' PiEvol ;

  FIN CTEMP;

  EVO_TAB . 1 . vit = 'EVOL' 'BOUT' 'MANU' 'TEMP' LTCAL 'PROP' PROP . 'AUST';
  EVO_TAB . 2 . vit = 'EVOL' 'VERT' 'MANU' 'TEMP' LTCAL 'PROP' PROP . 'MART';
  EVO_TAB . 3 . vit = 'EVOL' 'CYAN' 'MANU' 'TEMP' LTCAL 'PROP' PROP . 'BAIN';
  EVO_TAB . 4 . vit = 'EVOL' 'BRIQ' 'MANU' 'TEMP' LTCAL 'PROP' PROP . 'FERR';
  EVO_TAB . 5 . vit = 'EVOL' 'VIOL' 'MANU' 'TEMP' LTCAL 'PROP' PROP . 'MB  ';

  EVOT = 'EVOL' 'BLEU' 'MANU' 'TEMP' LTCAL 'T' T;
  EVOT_TAB . vit = EVOT ;

  LTCAL = LTCAL '+' ('VALEUR' 'PREC') ;
  TRC = TRC 'ET' ('EVOL' 'NOIR' 'MANU' 'TEMP' LTCAL 'T' T) ;

FIN BOU_VIT ;



*PostTraitement :
REPE BOU_VIT (DIME LISTREF) ;

  vit = &BOU_VIT ;
  DT_vit = -1 * ( 'EXTR' LISTREF vit ) ;

  EVO = 'VIDE' 'EVOLUTIO' ;
  REPE BOU_PHA NBPHA ;
      indexPHA = &BOU_PHA;
      EVO = EVO 'ET' (EVO_TAB . indexPHA . vit) ;
  FIN BOU_PHA ;
  TL = TABL ;
  TL . 1 = 'MARQ ETOI REGU' ;
  TL . 2 = 'MARQ PLUS REGU' ;
  TL . 3 = 'MARQ MOIN REGU' ;
  TL . 4 = 'MARQ BARR REGU' ;
  TL . 5 = 'MARQ CROI REGU' ;
  TL . 'TITRE' = TABL ;
  TL . 'TITRE' . 1 = '1-Metal de base' ;
  TL . 'TITRE' . 2 = '2-Martensite' ;
  TL . 'TITRE' . 3 = '3-Bainite' ;
  TL . 'TITRE' . 4 = '4-Ferrite' ;
  TL . 'TITRE' . 5 = '5-Austenite' ;
  TITRE = 'CHAI' 'Vitesse : ' ( 'ENTI' 'PROC' DT_vit ) ' Deg.s-1' ;
  DESS EVO 'GRIL' 'TIRC' 'GRIS' 'LEGE' TL 'TITR' TITRE ;

  EVOT = EVOT_TAB . vit ;

  REPE BOUP 4 ;

    NumPROP = &BOUP + 1 ;

    EVO = EVO_TAB . NumPROP . vit ;

*   Traitement de EVO
    Seuil1  = 5.D-3 ;
    TEMPS   = 'EXTR' EVO 'ABSC' 1;
    PROP    = 'EXTR' EVO 'ORDO' 1;
    T       = 'EXTR' EVOT 'ORDO' 1;
    Seuil2 = ( 'MAXI' PROP) - (Seuil1 * 4.D0) ;

    SI (('MAXI' PROP) '>' Seuil1);

*     tpsS1 : temps debut apparition de PROP
      tpsS1  ='IPOL' Seuil1 ('ORDO' PROP 'CROI')  TEMPS ;
*     TS1 : T°C debut apparition de PROP
      TS1    ='IPOL' tpsS1          TEMPS         T   ;

      SI (TS1 '>' TFIN);

*       tpsS2 : temps fin apparition de PROP
        tpsS2='IPOL' Seuil2 ('ORDO' PROP 'CROI') TEMPS ;
*       TS2 : T°C fin apparition de PROP
        TS2  ='IPOL' tpsS2          TEMPS         T   ;

        SI (TS2 '<' TS1);
          TSEUIL . NumPROP . 'tpsS1'  = TSEUIL . NumPROP . 'tpsS1' 'ET' tpsS1 ;
          TSEUIL . NumPROP . 'TS1'    = TSEUIL . NumPROP . 'TS1' 'ET' TS1 ;

          TSEUIL . NumPROP . 'tpsS2'  = TSEUIL . NumPROP . 'tpsS2' 'ET' tpsS2 ;
          TSEUIL . NumPROP . 'TS2'    = TSEUIL . NumPROP . 'TS2' 'ET' TS2 ;

        FINS ;

      FINS ;

    FINS ;

  FIN BOUP ;


FIN BOU_VIT ;


*On trace le diagramme TRC avec les courbes de vitesse
EVO = 'VIDE' 'EVOLUTIO' ;
REPE BOUP 3 ;
  NumPROP = &BOUP + 1 ;
  COULEUR = 'EXTR' LESCOUL NumPROP ;
  EVO = EVO 'ET' ( 'EVOL' COULEUR 'MANU' 'TEMP' (TSEUIL . NumPROP . 'tpsS1') 'T' (TSEUIL . NumPROP . 'TS1') )
            'ET' ( 'EVOL' COULEUR 'MANU' 'TEMP' (TSEUIL . NumPROP . 'tpsS2') 'T' (TSEUIL . NumPROP . 'TS2') ) ;
FIN BOUP ;

TL = TABL ;
TL . 'TITRE' = TABL ;
REPE BOU_VIT (DIME LISTREF) ;
  vit = &BOU_VIT ;
  DT_vit = -1 * ( 'EXTR' LISTREF vit ) ;
  TL . 'TITRE' . vit = 'CHAI' ('ENTI' 'PROC' DT_vit) 'C/s' ;
  TL . vit = 'TIRR' ;
FIN BOU_VIT ;

TITRE = 'CHAI' 'Diagramme TRC' ;
DESS ( TRC 'ET'  EVO ) 'LOGX' 'XBOR' 1. 1E3
                       'GRIL' 'TIRC' 'GRIS'
                       'LEGE' TL
                       'TITR' TITRE ;

*On trace le diagramme TRC avec les courbes debut/fin d'apparition des proportions de phases
TL = TABL ;
TL . 'TITRE' = TABL ;
TL . 'TITRE' . 1 = 'MART debut' ;
TL . 'TITRE' . 2 = 'MART fin' ;
TL . 'TITRE' . 3 = 'BAIN debut' ;
TL . 'TITRE' . 4 = 'BAIN fin' ;
TL . 'TITRE' . 5 = 'FERR debut' ;
TL . 'TITRE' . 6 = 'FERR fin' ;
TITRE = 'CHAI' 'Diagramme TRC' ;
DESS EVO 'LOGX' 'XBOR' 1. 1E2
         'GRIL' 'TIRC' 'GRIS'
         'LEGE' TL
         'TITR' TITRE ;



fin;


