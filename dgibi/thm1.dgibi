* fichier : thm1.dgibi
*---------------------------------------------------------------------*
*                                                                     *
*             Cas-test du modele THERMOHYDRIQUE SCHREFLER             *
*                                                                     *
* Description : Simulation d'une structure composite constituee d'un  *
* ------------- modele THM et d'un modele THERMIQUE classique en 2D   *
*               Deformation Plane. De plus, CL de CONVECTION appli-   *
*               -quee a une extremite du milieu THM.                  *
*                                                                     *
*               Par defaut, comportement mecanique endommageable pour *
*               verifier possibilite couplage avec endommagement.     *
*                                                                     *
*               Test de non-regression sur resultats du 7/10/2008.    *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
*
*                                                                     *
'OPTI' 'DIME' 2 'ELEM' 'QUA8' 'MODE' 'PLAN' 'DEFO' ;
*                                                                     *
* Calcul sur tous les pas de temps : ICOMPLET = VRAI ;
ICOMPLET = FAUX ;
*                                                                     *
* Simulation avec un modele d'endommagement : IMAZARS = VRAI ;
IMAZARS  = VRAI ;
*                                                                     *
* Traces : ITRAC1 = VRAI ;
ITRAC1   = FAUX ;
*                                                                     *
'OPTI' 'ECHO' 1 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                  I. Geometrie et Donnees Initiales                  *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*--------------------------- I.1. Geometrie --------------------------*
*                                                                     *
PA1      = 0. 0. ;
PB1      = 0. 0.01 ;
PC1      = 0.3 0.01 ;
PD1      = 0.3 0. ;
*                                                                     *
* Nombre d'elements suivant Ox et Oy :                                *
NBEX1    = 60 ;
NBEY1    = 1 ;
* Droites contour de la geometrie :                                   *
DAB1     = PA1 'DROI' NBEY1 PB1 ;
DCD1     = PC1 'DROI' NBEY1 PD1 ;
DBC1     = PB1 'DROI' NBEX1 PC1 ;
DDA1     = PD1 'DROI' NBEX1 PA1 ;
DFE1     = DCD1 'PLUS' (0.01 0.) ;
*                                                                     *
SURF1    = 'DALL' DAB1 DBC1 DCD1 DDA1 ;
SURF2    = DCD1 'REGL' 1 DFE1 ;
*                                                                     *
*-------------------- I.2. Discretisation en Temps -------------------*
*                                                                     *
'SI' ICOMPLET ;
  LTPSCAL1 = PROG 0. PAS 100. 36000. ;
'SINO' ;
  LTPSCAL1 = PROG 0. PAS 100. 300. ;
'FINS' ;
*                                                                     *
*--------------------- I.3. Conditions Initiales ---------------------*
*                                                                     *
* Temperature (TK0), Pression de Gaz (PG0), Humidite Relative (HR0)   *
* et Pression Capillaire (PC0) Initiales :                            *
TK0      = 298.15 ;
PG0      = 101325. ;
HR0      = 0.60895 ;
PC0      = 68.3596E6 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                           II. Modelisation                          *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*---------------- II.1. Modeles et Caracteristiques ------------------*
*                                                                     *
MODTH1   = 'MODE' SURF1 'THERMOHYDRIQUE' ;
MODTH2   = 'MODE' SURF2 'THERMIQUE' ;
MODCO1   = 'MODE' DAB1 'THERMIQUE' 'CONVECTION' ;
'SI' IMAZARS ;
  MODM1    = 'MODE' SURF1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
                                      'ENDOMMAGEMENT' 'MAZARS' ;
'SINO' ;
  MODM1    = 'MODE' SURF1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
'FINS' ;
MODM2    = 'MODE' SURF2 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MOD0     = MODTH1 'ET' MODM1 'ET' MODTH2 'ET' MODM2 ;
MOD0     = MODTH1 'ET' MODM1 'ET' MODTH2 'ET' MODM2 'ET' MODCO1 ;
*                                                                     *
* Module d'Young (MY1), Coefficient de Poisson (NU1) et Dilatation    *
* Thermique (ALPH1) :                                                 *
MY1      = 'EVOL' 'MANU'
  'T   ' ('PROG' 20. 60. 110. 250.)
  'YOUN' ('PROG' 38.5E9 33.E9 29.8E9 20.4E9) ;
NU1      = 0.25 ;
ALPH1    = 11.E-6 ;
*                                                                     *
* Parametres pour modele MAZARS :                                     *
EPSD1    = 2.E-04 ;
AT1      = 1. ;
BT1      = 1.E4 ;
AC1      = 1.2 ;
BC1      = 1.5E3 ;
BETA1    = 1.06 ;
*                                                                     *
CH0      = 'MANU' 'CHPO' SURF1 3 'PG' PG0 'PC' PC0 'T' TK0 ;
MATTH1 F0  = @MATETHM MODTH1 CH0 ;
MATTH2   = 'MATE' MODTH2 'RHO' 1. 'C' 0. 'K' 1.E-6 ;
MATCO1   = 'MATE' MODCO1 'H' 1.E6 ;
'SI' IMAZARS ;
  MATM1    = 'MATE' MODM1 'YOUN' MY1 'NU' NU1 'ALPH' ALPH1 'RHO' 2.E3
'KTR0' EPSD1 'ATRA' AT1 'BTRA' BT1 'ACOM' AC1 'BCOM' BC1 'BETA' BETA1 ;
'SINO' ;
  MATM1    = 'MATE' MODM1 'YOUN' MY1 'NU' NU1 'ALPH' ALPH1 ;
'FINS' ;
MATM2    = 'MATE' MODM2 'YOUN' 1.E9 'NU' 0.3 'ALPH' 1.E-6 ;
MAT0     =  MATTH1 'ET' MATM1 'ET' MATTH2 'ET' MATM2 ;
MAT0     =  MATTH1 'ET' MATM1 'ET' MATTH2 'ET' MATM2 'ET' MATCO1 ;
*                                                                     *
*--------------------- II.2. C.L. et Chargements ---------------------*
*                                                                     *
* II.2.1. ThermoHydriques :                                           *
CLPG1    = 'BLOQ' 'PG' DAB1 ;
CLPC1    = 'BLOQ' 'PC' DAB1 ;
CLTH0    = CLPG1 'ET' CLPC1 ;
*                                                                     *
CHPG1    = 'DEPI' CLPG1 1. ;
CHPC1    = 'DEPI' CLPC1 1. ;
CHTK1    = 'MANU' 'CHPO' DAB1 1 'T   ' 1. ;
EVPG1    = 'EVOL' 'MANU' 'TEMPS' ('PROG'  0. 300. 36000.)
  ('PROG' 101325. 431325. 758925.) ;
EVPC1    = 'EVOL' 'MANU' 'TEMPS' ('PROG'  0. 300. 36000.)
  ('PROG' 6.8362E7 4.39004E7 4.08575E7) ;
EVTK1    = 'EVOL' 'MANU' 'TEMPS' ('PROG' 0. 300. 36000.)
  ('PROG' 298.15 418. 440.) ;
CGPG1    = 'CHAR' 'TIMP' CHPG1 EVPG1 ;
CGPC1    = 'CHAR' 'TIMP' CHPC1 EVPC1 ;
CGTK1    = 'CHAR' 'TECO' CHTK1 EVTK1 ;
CG0      = CGPG1 'ET' CGPC1 'ET' CGTK1 ;
*                                                                     *
* Mecaniques :                                                        *
CLME1    = 'BLOQ' 'UX' DAB1 ;
CLME2    = 'BLOQ' 'UY' DBC1 ;
CLME3    = 'BLOQ' 'UY' DDA1 ;
CLME0    = CLME1 'ET' CLME2 'ET' CLME3 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                       III. Resolution PASAPAS                       *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*---------------------- III.1. Table de Donnees ----------------------*
*                                                                     *
TPAS1                         = 'TABL' ;
TPAS1 . 'MODELE'              = MOD0 ;
TPAS1 . 'CARACTERISTIQUES'    = MAT0 ;
TPAS1 . 'BLOCAGES_THERMIQUES' = CLTH0 ;
TPAS1 . 'BLOCAGES_MECANIQUES' = CLME0 ;
TPAS1 . 'CHARGEMENT'          = CG0 ;
TPAS1 . 'TEMPS_CALCULES'      = LTPSCAL1 ;
TPAS1 . 'PROCEDURE_THERMIQUE' = 'NONLINEAIRE' ;
TPAS1 . 'RELAXATION_THETA'    = 1.0 ;
TPAS1 . 'MOVA'                = 'D' ;
*                                                                     *
TPAS1 . 'TEMPERATURES'        = 'TABL' ;
TPAS1 . 'TEMPERATURES' . 0    = CH0 ;
*                                                                     *

*------------------------- III.2. Resolution -------------------------*
*                                                                     *
PASAPAS TPAS1 ;
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                         IV. Post-traitement                         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*------------------- IV.1. Solution de comparaison -------------------*
*                                                                     *
EVPGR1   = 'EVOL' 'TURQ' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'PG' ('PROG' 1.01325E+05 1.01325E+05 1.01325E+05) ;
EVPCR1   = 'EVOL' 'TURQ' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'PC' ('PROG' 6.83596E+07 6.83596E+07 6.83596E+07) ;
EVTKR1   = 'EVOL' 'TURQ' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'T ' ('PROG' 2.98150E+02 2.98150E+02 2.98150E+02) ;
EVPGR2   = 'EVOL' 'VERT' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'PG' ('PROG' 4.31325E+05 1.03086E+05 1.01345E+05) ;
EVPCR2   = 'EVOL' 'VERT' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'PC' ('PROG' 4.39004E+07 6.75367E+07 6.83477E+07) ;
EVTKR2   = 'EVOL' 'VERT' 'MANU'
  'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
  'T ' ('PROG' 4.17982E+02 3.01067E+02 2.98192E+02) ;
'SI' ICOMPLET ;
  EVPGR3   = 'EVOL' 'JAUN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PG' ('PROG' 5.11160E+05 2.70857E+05 1.21151E+05) ;
  EVPCR3   = 'EVOL' 'JAUN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PC' ('PROG' 4.31589E+07 4.31737E+07 5.23776E+07) ;
  EVTKR3   = 'EVOL' 'JAUN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'T ' ('PROG' 4.23359E+02 3.83641E+02 3.52063E+02) ;
  EVPGR4   = 'EVOL' 'ORAN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PG' ('PROG' 6.76337E+05 5.09370E+05 3.86782E+05) ;
  EVPCR4   = 'EVOL' 'ORAN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PC' ('PROG' 4.16246E+07 3.94484E+07 4.15511E+07) ;
  EVTKR4   = 'EVOL' 'ORAN' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'T ' ('PROG' 4.34453E+02 4.08195E+02 3.86481E+02) ;
  EVPGR5   = 'EVOL' 'ROUG' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PG' ('PROG' 7.58925E+05 6.08924E+05 5.02148E+05) ;
  EVPCR5   = 'EVOL' 'ROUG' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'PC' ('PROG' 4.08575E+07 3.79207E+07 3.89898E+07) ;
  EVTKR5   = 'EVOL' 'ROUG' 'MANU'
    'X ' ('PROG' 0.00000E+00 5.00000E-02 1.00000E-01)
    'T ' ('PROG' 4.39999E+02 4.16870E+02 3.97931E+02) ;
  EVPGR0   = EVPGR1 'ET' EVPGR2 'ET' EVPGR3 'ET' EVPGR4 'ET' EVPGR5 ;
  EVPCR0   = EVPCR1 'ET' EVPCR2 'ET' EVPCR3 'ET' EVPCR4 'ET' EVPCR5 ;
  EVTKR0   = EVTKR1 'ET' EVTKR2 'ET' EVTKR3 'ET' EVTKR4 'ET' EVTKR5 ;
'SINO' ;
  EVPGR0   = EVPGR1 'ET' EVPGR2 ;
  EVPCR0   = EVPCR1 'ET' EVPCR2 ;
  EVTKR0   = EVTKR1 'ET' EVTKR2 ;
'FINS' ;
*                                                                     *
*------------ IV.2. Depouillement de la solution calculee ------------*
*                                                                     *
TTEMPS1  = TPAS1 . 'TEMPS' ;
IDIM1    = 'DIME' TTEMPS1 ;
TINDEX1  = 'TABL' ;
'REPE' BOU1 IDIM1 ;
  IM1      = &BOU1 - 1 ;
  TPSI1    = TTEMPS1 . IM1 ;
  TINDEX1 . TPSI1 = IM1 ;
'FIN' BOU1 ;
*                                                                     *
TSOL1    = TPAS1 . 'TEMPERATURES' ;
*                                                                     *
PX1      = DBC1 'POIN' 'PROC' (0.05 0.) ;
PX2      = DBC1 'POIN' 'PROC' (0.10 0.) ;

'OPTI' ELEM 'SEG2';
DX1      = (PB1 'DROI' 1 PX1) 'ET' (PX1 'DROI' 1 PX2) ;

*                                                                     *
SOLPG1   = 'EVOL' 'TURQ' 'CHPO' (TSOL1 . (TINDEX1 . 0.)) 'PG' DX1 ;
SOLPC1   = 'EVOL' 'TURQ' 'CHPO' (TSOL1 . (TINDEX1 . 0.)) 'PC' DX1 ;
SOLTK1   = 'EVOL' 'TURQ' 'CHPO' (TSOL1 . (TINDEX1 . 0.)) 'T ' DX1 ;
SOLPG2   = 'EVOL' 'VERT' 'CHPO' (TSOL1 . (TINDEX1 . 300.)) 'PG' DX1 ;
SOLPC2   = 'EVOL' 'VERT' 'CHPO' (TSOL1 . (TINDEX1 . 300.)) 'PC' DX1 ;
SOLTK2   = 'EVOL' 'VERT' 'CHPO' (TSOL1 . (TINDEX1 . 300.)) 'T ' DX1 ;
'SI' ICOMPLET ;
  SOLPG3 = 'EVOL' 'JAUN' 'CHPO' (TSOL1 . (TINDEX1 . 9.E3)) 'PG' DX1 ;
  SOLPC3 = 'EVOL' 'JAUN' 'CHPO' (TSOL1 . (TINDEX1 . 9.E3)) 'PC' DX1 ;
  SOLTK3 = 'EVOL' 'JAUN' 'CHPO' (TSOL1 . (TINDEX1 . 9.E3)) 'T ' DX1 ;
  SOLPG4 = 'EVOL' 'ORAN' 'CHPO' (TSOL1 . (TINDEX1 . 27.E3)) 'PG' DX1 ;
  SOLPC4 = 'EVOL' 'ORAN' 'CHPO' (TSOL1 . (TINDEX1 . 27.E3)) 'PC' DX1 ;
  SOLTK4 = 'EVOL' 'ORAN' 'CHPO' (TSOL1 . (TINDEX1 . 27.E3)) 'T ' DX1 ;
  SOLPG5 = 'EVOL' 'ROUG' 'CHPO' (TSOL1 . (TINDEX1 . 36.E3)) 'PG' DX1 ;
  SOLPC5 = 'EVOL' 'ROUG' 'CHPO' (TSOL1 . (TINDEX1 . 36.E3)) 'PC' DX1 ;
  SOLTK5 = 'EVOL' 'ROUG' 'CHPO' (TSOL1 . (TINDEX1 . 36.E3)) 'T ' DX1 ;
'FINS' ;
*                                                                     *
'SI' ICOMPLET ;
  EVPGTOT1 = SOLPG1 'ET' SOLPG2 'ET' SOLPG3 'ET' SOLPG4 'ET' SOLPG5 ;
  EVPCTOT1 = SOLPC1 'ET' SOLPC2 'ET' SOLPC3 'ET' SOLPC4 'ET' SOLPC5 ;
  EVTPTOT1 = SOLTK1 'ET' SOLTK2 'ET' SOLTK3 'ET' SOLTK4 'ET' SOLTK5 ;
'SINO' ;
  EVPGTOT1 = SOLPG1 'ET' SOLPG2 ;
  EVPCTOT1 = SOLPC1 'ET' SOLPC2 ;
  EVTPTOT1 = SOLTK1 'ET' SOLTK2 ;
'FINS' ;
*                                                                     *
*--------------------------- IV.3. Traces ----------------------------*
*                                                                     *
'SI' ITRAC1 ;
  TDESS1   = 'TABL' ;
  TDESS1 . 1  = 'MOT' 'MARQ SS CROI NOLI' ;
  TDESS1 . 2  = 'MOT' 'MARQ SS CROI NOLI' ;
  TDESS1 . 'TITRE'   = 'TABL' ;
  TDESS1 . 'TITRE' . 1  = 'MOT' 'PAS DE LEGENDE' ;
  TDESS1 . 'TITRE' . 2  = 'MOT' 'PAS DE LEGENDE' ;
  TDESS1 . 'TITRE' . 6  = 'MOT' 'Initial' ;
  TDESS1 . 'TITRE' . 7  = 'MOT' 't = 5 mn' ;
  'SI' ICOMPLET ;
    TDESS1 . 3  = 'MOT' 'MARQ SS CROI NOLI' ;
    TDESS1 . 4  = 'MOT' 'MARQ SS CROI NOLI' ;
    TDESS1 . 5  = 'MOT' 'MARQ SS CROI NOLI' ;
    TDESS1 . 'TITRE' . 3  = 'MOT' 'PAS DE LEGENDE' ;
    TDESS1 . 'TITRE' . 4  = 'MOT' 'PAS DE LEGENDE' ;
    TDESS1 . 'TITRE' . 5  = 'MOT' 'PAS DE LEGENDE' ;
    TDESS1 . 'TITRE' . 8  = 'MOT' 't = 2 h 30' ;
    TDESS1 . 'TITRE' . 9  = 'MOT' 't = 7 h 30 mn' ;
    TDESS1 . 'TITRE' . 10 = 'MOT' 't = 10 h' ;
  'FINS' ;
*                                                                     *
  TIT1     = 'MOT'
    'Simulation (lignes) Vs. Solution de Refence (points).' ;
  'DESS' TDESS1 (EVPGR0 'ET' EVPGTOT1) 'TITR' TIT1 ;
  'DESS' TDESS1 (EVPCR0 'ET' EVPCTOT1) 'TITR' TIT1 ;
  'DESS' TDESS1 (EVTKR0 'ET' EVTPTOT1) 'TITR' TIT1 ;
'FINS' ;
*                                                                     *
*------------------------ IV.4. Calcul erreur ------------------------*
*                                                                     *
ERRPG1   = ('MAXI' 'ABS' ('EXTR' (EVPGR1 - SOLPG1) 'ORDO'))
  / ('MAXI' ('EXTR' EVPGR1 'ORDO')) ;
ERRPC1   = ('MAXI' 'ABS' ('EXTR' (EVPCR1 - SOLPC1) 'ORDO'))
  / ('MAXI' ('EXTR' EVPCR1 'ORDO')) ;
ERRTK1   = ('MAXI' 'ABS' ('EXTR' (EVTKR1 - SOLTK1) 'ORDO'))
  / ('MAXI' ('EXTR' EVTKR1 'ORDO')) ;
ERRPG2   = ('MAXI' 'ABS' ('EXTR' (EVPGR2 - SOLPG2) 'ORDO'))
  / ('MAXI' ('EXTR' EVPGR2 'ORDO')) ;
ERRPC2   = ('MAXI' 'ABS' ('EXTR' (EVPCR2 - SOLPC2) 'ORDO'))
  / ('MAXI' ('EXTR' EVPCR2 'ORDO')) ;
ERRTK2   = ('MAXI' 'ABS' ('EXTR' (EVTKR2 - SOLTK2) 'ORDO'))
  / ('MAXI' ('EXTR' EVTKR2 'ORDO')) ;
'SI' ICOMPLET ;
  ERRPG3   = ('MAXI' 'ABS' ('EXTR' (EVPGR3 - SOLPG3) 'ORDO'))
    / ('MAXI' ('EXTR' EVPGR3 'ORDO')) ;
  ERRPC3   = ('MAXI' 'ABS' ('EXTR' (EVPCR3 - SOLPC3) 'ORDO'))
    / ('MAXI' ('EXTR' EVPCR3 'ORDO')) ;
  ERRTK3   = ('MAXI' 'ABS' ('EXTR' (EVTKR3 - SOLTK3) 'ORDO'))
    / ('MAXI' ('EXTR' EVTKR3 'ORDO')) ;
  ERRPG4   = ('MAXI' 'ABS' ('EXTR' (EVPGR4 - SOLPG4) 'ORDO'))
    / ('MAXI' ('EXTR' EVPGR4 'ORDO')) ;
  ERRPC4   = ('MAXI' 'ABS' ('EXTR' (EVPCR4 - SOLPC4) 'ORDO'))
    / ('MAXI' ('EXTR' EVPCR4 'ORDO')) ;
  ERRTK4   = ('MAXI' 'ABS' ('EXTR' (EVTKR4 - SOLTK4) 'ORDO'))
    / ('MAXI' ('EXTR' EVTKR4 'ORDO')) ;
  ERRPG5   = ('MAXI' 'ABS' ('EXTR' (EVPGR5 - SOLPG5) 'ORDO'))
    / ('MAXI' ('EXTR' EVPGR5 'ORDO')) ;
  ERRPC5   = ('MAXI' 'ABS' ('EXTR' (EVPCR5 - SOLPC5) 'ORDO'))
    / ('MAXI' ('EXTR' EVPCR5 'ORDO')) ;
  ERRTK5   = ('MAXI' 'ABS' ('EXTR' (EVTKR5 - SOLTK5) 'ORDO'))
  / ('MAXI' ('EXTR' EVTKR5 'ORDO')) ;
'FINS' ;
*                                                                     *
'SI' ICOMPLET ;
  ERRPG0   = 'MAXI' ('PROG' ERRPG1 ERRPG2 ERRPG3 ERRPG4 ERRPG5) ;
  ERRPC0   = 'MAXI' ('PROG' ERRPC1 ERRPC2 ERRPC3 ERRPC4 ERRPC5) ;
  ERRTK0   = 'MAXI' ('PROG' ERRTK1 ERRTK2 ERRTK3 ERRTK4 ERRTK5) ;
'SINO' ;
  ERRPG0   = 'MAXI' ('PROG' ERRPG1 ERRPG2) ;
  ERRPC0   = 'MAXI' ('PROG' ERRPC1 ERRPC2) ;
  ERRTK0   = 'MAXI' ('PROG' ERRTK1 ERRTK2) ;
'FINS' ;
ERR0     = 'MAXI' ('PROG' ERRPG0 ERRPC0 ERRTK0) ;
*                                                                     *
'SI' (ERR0 '>' 1.E-3) ;
  'MESS' ERR0;
  'ERRE' 5 ;
  'FIN' ;
'SINO' ;
  'MESS' ' ****** Ecart relatif max. solution de reference =' ERR0 ;
  'SAUT' 1 'LIGN' ;
  'MESS'
'----------------------- CAS-TEST THM1 REUSSI ! -----------------------'
  ;
  'SAUT' 1 'LIGN' ;
'FINS' ;
*                                                                     *
'FIN' ;










