*
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*
*                        VIBR14_FOURIER
*
* Objectif  : Calcul des modes propres d'un tube mince orthotrope
*             axisymetrique encastre - encastre
* Elements  : coque mince DKT
* Creation  : BP, 2017-03-01
* Reference : Intercomparaison avec modele 3D CAST3M (vibr14_3d)
*
************************************************************************
*
* DIMENSION dans le plan (XOY)
*
*              solide
*      Y
*  Y=L | . . . D __ B
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      | . . .  |__|   -----> X
*      O       C    A
*             R=Ri R=Re
*
*               |  |
*               <-->
*                 t
*
* la coque mince est definie sur Rmoy=(Re+Ri)/2
*
*
* ORTHOTROPIE dans le plan (Y,Theta)
*
*                    Y
*                     |
*            V2  beta |
*               \     |
*                \    |
*                 \   |
*                  \  |
*                   \ |
*                    \|
*       Z-------------+X
*
************************************************************************

  GRAPH = FAUX ;
*   GRAPH = VRAI ;

************************************************************************
* OPTIONS ET DONNEES
************************************************************************

* options
  typEF = mot 'QUA8';
  OPTI DIME 2 MODE FOUR NOHA ELEM typEF EPSI LINEAIRE;
  OPTI TRAC PSC POTR HELVETICA_16 EPTR 15;


* geometrie
  L = 0.3048;
  t = 0.254E-3;
  Re = 0.0762;
  Ri = Re - t;
  Rmoy = 0.5 * (Ri + Re);

* nz et nR = nombre d elements selon Z et R
   nz    = 20;  nR    = 1;

* materiau
  Eyl  = 206.E9;
  nult = 0.3;
  rho1 = 7850.;
  Eyt  = Eyl / 10.;
  Glt  = Eyt / (2. * (1. + nult));
  Gtt  = Glt;
  nutt = 0.3;
  nutl = nult * Eyt / Eyl;
  mess Eyl Eyt Glt Gtt nutt;
  beta=30.;

* nombre de modes calcules
  nmod = 2;


************************************************************************
* MAILLAGE
************************************************************************

  p0   = 0. 0. ;
  vaxe = 0. 1. ;
  vx = 1. 0. ;

* maillage solide
  p1i = Ri 0. ;
  p1e = Re 0. ;
  p2i = Ri L  ;
  p2e = Re L  ;
  d1  = p1i droi nR p1e;
  d2  = p2i droi nR p2e;

  mesh8 = REGL d1 nZ d2 ;

* maillage coq2
  OPTI ELEM SEG2;
  p1 = Rmoy 0.;
  p2 = Rmoy L;
  mesh2 = p1 droi nZ p2;


************************************************************************
* MODELEs
************************************************************************

  mod2 = MODE mesh2 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE' 'COQ2'
                    'CONS' 'couche 1';
  mod8 = MODE mesh8 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE';


*=======================================================================
*===> BOUCLE SUR LES HARMONIQUES DE FOURIER
*=======================================================================
nHlect = lect 3 4 5 6 7 8 9 10;
nnH = DIME nHlect;

w2vibr = prog;
w8vibr = prog;

REPE BnH nnH;

  nH = extr nHlect &BnH;
  MESS ' ';
  MESS '______________________________________________';
  MESS ' ';
  MESS '>>>>>>>>>>>>>>>>  nH=' nH '<<<<<<<<<<<<<<<<<<<';
  MESS '______________________________________________';
  MESS ' ';
  OPTI MODE FOUR nH;

  MESS '==================== \b=' beta '====================';
  TITRE (chai '\b =' (enti beta) '° - n_{H}='nH);



************************************************************************
* MATERIAUx
************************************************************************

  mat2 = MATE mod2 'YG1' Eyl 'YG2' Eyt 'NU12' nult 'G12' Glt
                   'RHO' rho1 'EPAI' t
                   'DIRE' vaxe 'INCL' beta ;

  mat8 = MATE mod8 'YG1'  Eyt  'YG2'  Eyl  'YG3'  Eyt
                   'NU12' nutl 'NU13' nutt 'NU23' nult
                   'G12'  Glt  'G13'  Gtt  'G23'  Glt
                   'RHO'  rho1 'DIRE' vx 'INCL' 0. beta;

* verification graphique de l'orientation des fibres
  v2 = chan (VLOC mod2 mat2) 'GRAVITE' mod2;
  v8 = chan (VLOC mod8 mat8) 'GRAVITE' mod8;

  SI GRAPH;
    xv2 = 0.1 / nZ;
    vv2 = vect v2 xv2 mod2 (mots V1R V1Z V2R V2Z V3R V3Z)
                      (mots 'BLEU' 'BRON' 'ROUG');
    TRAC vv2 mesh2  ;
    vv8 = vect v8 xv2 mod8 (mots V1R V1Z V2R V2Z V3R V3Z)
                      (mots 'BLEU' 'BRON' 'ROUG');
    TRAC vv8 mesh8  ;
  FINSI;


************************************************************************
* MATRICES
************************************************************************

* solide :
  cl1  = BLOQ 'DEPL'  d1;
  cl2y = BLOQ 'UZ' d2;
  cl2  = BLOQ 'UR' 'UT' d2;
  cl_8 = cl1 et cl2 et cl2y;
  K8 = RIGI mod8 mat8;
  M8 = MASS mod8 mat8;
  Ktot8 = K8 et cl_8;

* coque :
  cl1  = BLOQ 'DEPL'  p1;
  cl2y = BLOQ 'UZ'    p2;
  cl2  = BLOQ 'UR' 'UT' p2;
  cl_2 = cl1 et cl2 et cl2y;
  K2 = RIGI mod2 mat2;
  M2 = MASS mod2 mat2;
  Ktot2 = K2 et cl_2;


************************************************************************
* ANALYSE MODALE
************************************************************************

  TMOD_2 = VIBR 'IRAM' 100. 2  Ktot2 M2 ;
  TMOD_8 = VIBR 'IRAM' 100. 2  Ktot8 M8 ;

* stockage
  w2vibr = w2vibr et (TMOD_2 . 'MODES' . 1 . 'FREQUENCE');
  w8vibr = w8vibr et (TMOD_8 . 'MODES' . 1 . 'FREQUENCE');


FIN BnH;
*=======================================================================



************************************************************************
* TEST DE BON FONCTIONNEMENT
************************************************************************
* REM :  On teste que les harmoniques de Fourier sont 6, 7, 5 et 8.
nHref = lect 6 7 5 8;

w_2 nHjj_2 = ORDO w2vibr nHlect;
list nHjj_2;
list w_2;

w_8 nHjj_8 = ORDO w8vibr nHlect;
list nHjj_8;
list w_8;

* on ne teste que les 4 premiers modes
nHdiff_2 = (extr nHjj_2 (lect 1 2 3 4)) - nHref;
nHdiff_8 = (extr nHjj_8 (lect 1 2 3 4)) - nHref;

SI ( ((MAXI nHdiff_2 'ABS') >EG 1)
  OU ((MAXI nHdiff_8 'ABS') >EG 1) );
  ERRE 5;
SINON;
  ERRE 0;
FINSI;

FIN ;






