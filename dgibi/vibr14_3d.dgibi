*
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*
*                        VIBR14_3D
*
* Objectif  : Calcul des modes propres d'un tube mince orthotrope
*             axisymetrique encastre - encastre
* Elements  : coque mince DKT
* Creation  : BP, 2017-03-01
* Reference : Intercomparaison avec modele 2D Fourier CAST3M
*
************************************************************************
*
* DIMENSION dans le plan (XOY)
*
*              solide
*      Y
*  Y=L | . . . D __ B
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      |        |  |
*      | . . .  |__|   -----> X
*      O       C    A
*             R=Ri R=Re
*
*               |  |
*               <-->
*                 t
*
* la coque mince est definie sur Rmoy=(Re+Ri)/2
*
*
* ORTHOTROPIE dans le plan (Y,Theta)
*
*                    Y
*                     |
*            V2  beta |
*               \     |
*                \    |
*                 \   |
*                  \  |
*                   \ |
*                    \|
*       Z-------------+X
*
************************************************************************

  GRAPH = FAUX ;
*   GRAPH = VRAI ;

************************************************************************
* OPTIONS ET DONNEES
************************************************************************

* options
  OPTI DIME 3 MODE TRID ELEM CUB8 EPSI LINEAIRE;
  OPTI TRAC PSC POTR HELVETICA_16 EPTR 15;

* geometrie
  L = 0.3048;
  t = 0.254E-3;
  Re = 0.0762;
  Ri = Re - t;
  Rmoy = (Ri + Re) / 2.;

* nombre d elements selon Z et R
  ncirc = 2**8; nz = 20; nR = 1;
*   ncirc = 2**8; nz = 40; nR = 1;
*   ncirc = 2**9;  nz = 80;  nR = 1;

* materiau
  Ey1  = 206.E9;
  nu12 = 0.3;
  rho1 = 7850.;
  Ey2  = Ey1 / 10.;
  G12  = Ey2 / (2. * (1. + nu12));
  mess Ey1 Ey2 G12;
  beta=30.;

* nombre de modes calcules
  nmod = 2*4;
* nmod = 2*7;


************************************************************************
* MAILLAGE
************************************************************************

  p0   = 0. 0. 0.;
  vaxe = 0. 1. 0.;
  vx = 1. 0. 0.;
  vz = 0. 0. 1. ;

* MAILLAGE COQUE MINCE

  p1 = Rmoy 0. 0.;
  p2 = Rmoy L  0.;
  d12 = p1 droi nZ p2;
  OPTI ELEM TRI3;
  mesh3 = ROTA d12 360. ncirc p0 vaxe;
  ELIM mesh3 (1.E-3 * t);
  OPTI ELEM CUB8;

* recup
  y3 = COOR mesh3 2;
  py1  = POIN y3 EGINFE      1.E-6;
  py2  = POIN y3 EGSUPE (L - 1.E-6);
  p4 = poin  py2 'PROCH' (  0.        L (-1.*Rmoy));
  p6 = poin  py2 'PROCH' ((-1.*Rmoy)  L   0.      );
  p8 = poin  py2 'PROCH' (  0.        L   Rmoy    );
  cy2 = (cer3 p2 p4 p6 (ncirc/2)) et (cer3 p6 p8 p2 (ncirc/2));
  cy2 = cy2 coul BRUN;
  elim cy2 py2 1.E-8;
  cyMIL = cy2 moin (0. (0.5*L) 0.);
  elim cyMIL mesh3 1.E-8;
  cyMIL2 = cy2 moin (0. (0.25*L) 0.);
  elim cyMIL2 mesh3 1.E-8;

* chpoints angulaires
  cos1 = (coor mesh3 3) / Rmoy;
  sin1 = (coor mesh3 1) / Rmoy;
  cosMIL = (coor cyMIL 3) / Rmoy;
  sinMIL = (coor cyMIL 1) / Rmoy;
  cosMIL2 = (coor cyMIL2 3) / Rmoy;
  sinMIL2 = (coor cyMIL2 1) / Rmoy;



************************************************************************
* MODELEs ET MATERIAUx
************************************************************************

* coque mince dkt
  mod3 = MODE mesh3 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE' 'DKT';
  mat3 = MATE mod3  'YG1' Ey1 'YG2' Ey2 'NU12' nu12 'G12' G12
                    'RHO' rho1 'EPAI' t
                    'DIRE' vaxe 'INCL' beta ;

* verification graphique de l'orientation des fibres
  v3 = chan (VLOC mod3 mat3) 'GRAVITE' mod3;
  SI GRAPH;
    xv3 = 0.1 / nZ;
    vv3 = vect v3 xv3 mod3 (mots V1X V1Y V1Z V2X V2Y V2Z V3X V3Y V3Z)
                      (mots 'BLEU' 'BRON' 'ROUG');
    vv3_1 = vect v3 xv3 mod3 (mots V1X V1Y V1Z )
                      (mots 'BLEU' );
    TRAC (0. 2. -10.) vv3_1 mesh3 'BOIT' py2 'CACH';
    TRAC (0. 2. -10.) vv3 mesh3 'BOIT' py2 'CACH';
  FINSI;

************************************************************************
* MATRICES
************************************************************************

* coque mince dkt
  cl3 = BLOQ 'DEPL' (py1 et py2);
  K3  = RIGI mod3 mat3;
  M3  = MASS mod3 mat3;
  Ktot3 = K3 et cl3;


************************************************************************
* ANALYSE MODALE
************************************************************************
  SI GRAPH;
    mopost = mots 'TABL' 'DEFO';
  SINO;
    mopost = mots 'TABL' ;
  FINSI;

* coque mince dkt
  TMOD_3 = VIBR 'IRAM' 300. nmod  Ktot3 M3 ;
  POSTVIBR TMOD_3 mopost;



************************************************************************
* TEST DE BON FONCTIONNEMENT
************************************************************************
* REM :  On teste que les harmoniques de Fourier sont 6, 7, 5 et 8.
nHref = lect 6 7 5 8;
nHjj = lect;

* boucles sur les modes
I= -1 ;
REPE BMOD 4; I = I + 2;
    phi1 = TMOD_3 . 'MODES' . I . 'DEFORMEE_MODALE';
    phi1MIL = REDU phi1 cyMIL;
    phi1MIL = (cosMIL * (exco phi1MIL 'UZ' 'SCAL'))
            + (sinMIL * (exco phi1MIL 'UX' 'SCAL'));
    ev1MIL = EVOL CHPO  phi1MIL 'SCAL' cyMIL;
    s1MIL = EXTR  ev1MIL 'ABSC';
    u1MIL = EXTR  ev1MIL 'ORDO';
    u1moy = ((mini u1MIL) + (maxi u1MIL)) / 2.;
    s1moy = IPOL  u1moy u1MIL s1MIL 'TOUS';
    nH1 = (DIME s1moy) / 2;
    nHjj = nHjj et nH1;
FIN BMOD  ;

list nHjj;

nHdiff = nHjj - nHref;

SI ((maxi nHdiff 'ABS') >eg 1);
  ERRE 5;
SINON;
  ERRE 0;
FINSI;

FIN ;






