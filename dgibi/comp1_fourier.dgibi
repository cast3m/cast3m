* fichier :  comp1_fourier.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

************************************************************************
*                CYLINDRE COMPOSITE BICOUCHE
*           FIBRES ENROULEES -45/+45 AUTOUR DE L'AXE
*                     PRESSION INTERNE
*
*
* Un cylindre bloqué à sa base en déplacement suivant
* l'axe Z est soumis à une pression interne.
* 2D Fourier COQ2
* Ref : Rapport CEA , DEMT 85-482 , M. Hittinger, 1985
* Remise a plat : BP, 2017-01-23
*
************************************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;
* GRAPH = 'O';

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
*   OPTI TRAC X ;
  OPTI TRAC 'PSC' EPTR 5;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

TITRE 'CYLINDRE COMPOSITE BICOUCHE SOUS PRESSION INTERNE';
OPTION DIME 2 MODE FOUR 0 ELEM SEG2;

DENS 0.1 ;

 NAX = 1;
 NAX = 4;

*______________________________________________________
*   GEOMETRIE
*______________________________________________________

R = 1.05 ; H = 1. ;
PA = R 0.; PB = R  H ;
O1 = 0. 0. ; O2 = 0. 1.;

CYL = PA DROIT NAX PB ;

SI (NEG GRAPH 'N') ;
  TRAC CYL 'QUAL' ;
FINSI ;
*______________________________________________________
*   DESCRIPTION DU MATERIAU ORTHOTROPE : COUCHE 1
*______________________________________________________

MOD1 = MODE CYL MECANIQUE ELASTIQUE ORTHOTROPE COQ2  'CONS' 'couche 1';
MAT1 = MATE MOD1 'DIRE' O2 'INCL' 45
                 'YG1' 7.E6 'YG2' 1.3E6 NU12 0.28 G12 5.E5
                 'EPAI' 0.05 'EXCE' 0.025 ;

* verification graphique de l'orientation :
* 2D fourier coque => 3 vecteurs : V1, V2 et V3
v123 = VLOC MOD1 MAT1;
* mo123 = mots 'V1R' 'V1Z' 'V1T' 'V2R' 'V2Z' 'V2T' 'V3R' 'V3Z' 'V3T';
mo123 = mots 'V1R' 'V1Z'  'V2R' 'V2Z'  'V3R' 'V3Z' ;
ve123 = VECT v123 MOD1 0.07 mo123 (mots 'AZUR' 'OR' 'POUR');
SI (NEG GRAPH 'N');
  TITRE 'COMP1 couche 1 : V1(AZUR)  V2 (JAUNE)  V3(ROUG)';
  TRACE ve123 CYL 'CACH';
FINSI ;

*______________________________________________________
*   DESCRIPTION DU MATERIAU ORTHOTROPE : COUCHE 2
*______________________________________________________
MOD2 = MODE CYL MECANIQUE ELASTIQUE ORTHOTROPE COQ2 'CONS' 'couche 2' ;
MAT2 = MATE MOD2 'DIRE' O2 'INCL' -45
                 'YG1' 7.E6 'YG2' 1.3E6 NU12 0.28 G12 5.E5
                 'EPAI' 0.05 'EXCE' -0.025 ;

* verification graphique de l'orientation :
v123 = VLOC MOD2 MAT2;
ve123 = VECT v123 MOD2 0.07 mo123 (mots 'AZUR' 'BRON' 'POUR');
SI (NEG GRAPH 'N');
  TITRE 'COMP1 couche 2 : V1(AZUR)  V2 (JAUNE)  V3(ROUG)';
  TRACE ve123 CYL 'CACH';
FINSI ;

* chargement
MOP = 'MODE' CYL 'CHARGEMENT' 'PRESSION' COQ2 ;
* on met -1 pour etre orienté vers les r>0
MAP = 'PRES' MOP 'PRES' -1. ;

*______________________________________________________
*   CREATION DE LA RIGIDITE
*______________________________________________________


MODORT = MOD1 ET MOD2 'ET' MOP ;
MATORT = MAT1 ET MAT2 ;

RIG12 = RIGI MODORT MATORT ;


*______________________________________________________
*   CONDITIONS AUX LIMITES
*______________________________________________________


CDL1 = BLOQ 'UZ' pB ;
CDL2 = BLOQ 'UT' pB ;
CDL = CDL1 ET CDL2 ;

*______________________________________________________
*   CALCUL
*______________________________________________________
RIGITOT = RIG12 ET CDL ;
FP = 'BSIG' MOP MAP ;
*
DEP1 = RESO RIGITOT FP ;

DEFO0= DEFO CYL 0.0 DEP1 GRIS;
DEFO1= DEFO CYL DEP1 (EXCO DEP1 'UR');

SI (NEG GRAPH 'N') ;
 TRAC  (DEFO1 et DEFO0) ;
FINSI;

* ref = calcul element LC8 tire de [DEMT 85-482]
uref = (0.63615E-5 + 0.64702E-5) /2.;

* VALEUR pour le TEST
ur1A  = EXTR DEP1 'UR' PA ;
ur1B  = EXTR DEP1 'UR' PB ;
MESS ' DEPLACEMENT RADIAL REFERENCE : ' uref;
MESS ' DEPLACEMENT RADIAL EN PA CALCULE :' ur1A ur1B;
RES1= ABS((ur1A - uref) / uref);
MESS 'ECART RELATIF : ' RES1 ;
MESS '************************************************';
SAUT 2 LIGN ;
TEMPS ;
*______________________________________________________
*                    CODE BON FONCTIONNEMENT
*______________________________________________________

SI (RES1 <EG 5.E-2);
   ERRE 0 ;
SINO;
   ERRE 5 ;
FINSI ;
FIN;




