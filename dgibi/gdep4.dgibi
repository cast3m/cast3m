*----------------------------------------------------------------------*
************************************************************************
* Section : Mecanique Plastique
* Section : Mecanique Elastique
************************************************************************
* fichier gdep4.dgibi                                                  *
*                                                                      *
* Exemple de la donnée d'un état mécanique initial avec :              *
*   - Comportement mécanique non linéaire (élasto-plastique) ;         *
*   - Grands déplacements.                                             *
*                                                                      *
* On simule 2 fois un essai de traction simple :                       *
*   - 1ere simulation sert de reference.                               *
*   - 2nd simulation permet de tester la donnee de l'état initial,     *
*     puis de comparer la solution a la 1ere.                          *
*                                                                      *
*----------------------------------------------------------------------*
* IG1 = VRAI : traces ;                                                *
IG1      = FAUX ;
*                                                                      *
'OPTI' 'DIME' 2 'ELEM' 'QUA8' 'MODE' 'PLAN' 'CONT' ;
*                                                                      *
S1       = ((0 0) 'DROI' 1 (1 0)) 'TRAN' 1 (0 1) ;
L1       = S1 'COTE' 1 ;
L2       = S1 'COTE' 2 ;
L3       = S1 'COTE' 3 ;
L4       = S1 'COTE' 4 ;
'SI' IG1 ;
  'TRAC' 'QUAL' S1 'TITR'
' Cas-test de la donnee du deplacement initial en grands deplacements' ;
'FINS' ;
*                                                                      *
*---------------------- MODELE / CARACTERISTIQUES ---------------------*
*                                                                      *
MOD1     = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'CINEMATIQUE' ;
MAT1     = 'MATE' MOD1 'YOUN' 200.E9 'NU' 0.3 'SIGY' 150.E6 'H' 200.E6 ;
*                                                                      *
*-------------------------- BLOCAGES / C.L. ---------------------------*
*                                                                      *
* Blocages traction simple suivant (O,y) ;                             *
CL1      = 'BLOQ' L1 'UY' ;
CL3      = 'BLOQ' L3 'UY' ;
CL4      = 'BLOQ' L4 'UX' ;
CL0      = CL1 'ET' CL3 'ET' CL4 ;
'SI' IG1 ;
  'TRAC' 'QUAL' (S1 'ET' ((L1 'ET' L4) 'COUL' 'ROUG')) 'TITR'
    ' On bloque L1 en (O,y) et L4 en (O,x) ' ;
'FINS' ;
*                                                                      *
* Deplacement impose :                                                 *
EPYY1    = 1.E-3 ;
DCL3     = 'DEPI' CL3 EPYY1 ;
'SI' IG1 ;
  'TRAC' 'QUAL' (S1 'ET' (L3 'COUL' 'ROUG')) 'TITR'
    ' On impose un deplacement sur L3 suivant (O,y) ' ;
'FINS' ;
*                                                                      *
* Chargement associe :                                                 *
LTPS1    = 'PROG' 0. 1. ;
EV1      = 'EVOL' 'MANU' 'TEMP' LTPS1 'AMPL' LTPS1 ;
CG1      = 'CHAR' 'DIMP' DCL3 EV1 ;
*                                                                      *
*------------------------------ PASAPAS -------------------------------*
*                                                                      *
XPREC1   = 1.E-10 ;
*                                                                      *
TAB1                         = 'TABL' ;
TAB1 . 'MODELE'              = MOD1 ;
TAB1 . 'CARACTERISTIQUES'    = MAT1 ;
TAB1 . 'BLOCAGES_MECANIQUES' = CL0 ;
TAB1 . 'CHARGEMENT'          = CG1 ;
TAB1 . 'TEMPS_CALCULES'      = 'PROG' 0. 'PAS' 0.1 1. ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'PRECISION'           = XPREC1 ;
*                                                                      *
PASAPAS TAB1 ;
*                                                                      *
TAB2                         = 'TABL' ;
TAB2 . 'MODELE'              = MOD1 ;
TAB2 . 'CARACTERISTIQUES'    = MAT1 ;
TAB2 . 'BLOCAGES_MECANIQUES' = CL0 ;
TAB2 . 'CHARGEMENT'          = CG1 ;
TAB2 . 'TEMPS_CALCULES'      = 'PROG' 0.9 1. ;
TAB2 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB2 . 'PRECISION'           = XPREC1 ;
TAB2 . 'TEMPS'               = 'TABL' ;
TAB2 . 'TEMPS'        . 0    = 0.9 ;
TAB2 . 'DEPLACEMENTS'        = 'TABL' ;
TAB2 . 'DEPLACEMENTS' . 0    = TAB1 . 'DEPLACEMENTS' . 9 ;
TAB2 . 'CONTRAINTES'         = 'TABL' ;
TAB2 . 'CONTRAINTES'  . 0    = TAB1 . 'CONTRAINTES'  . 9 ;
TAB2 . 'VARIABLES_INTERNES'  = 'TABL' ;
TAB2 . 'VARIABLES_INTERNES' . 0 = TAB1 . 'VARIABLES_INTERNES' . 9 ;
TAB2 . 'DEFORMATIONS_INELASTIQUES' = 'TABL' ;
TAB2 . 'DEFORMATIONS_INELASTIQUES'  . 0 =
  TAB1 . 'CONTINUATION' . 'DEFORMATIONS_INELASTIQUES' ;
*                                                                      *
PASAPAS TAB2 ;
*                                                                      *
*-------------------------------- TEST --------------------------------*
*                                                                      *
DDEP10   = (TAB1 . 'DEPLACEMENTS' . 10) - (TAB2 . 'DEPLACEMENTS' . 1) ;
DSIG10   = (TAB1 . 'CONTRAINTES'  . 10) - (TAB2 . 'CONTRAINTES'  . 1) ;
NDEP10   = 'MAXI' 'ABS' (TAB1 . 'DEPLACEMENTS' . 10 'ENLE' 'LX') ;
NSIG10   = 'MAXI' 'ABS' (TAB1 . 'CONTRAINTES'  . 10) ;
DDEP10   = DDEP10 'ENLE' 'LX' ;
ERRDEP10 = 'MAXI' 'ABS' (DDEP10 / NDEP10) ;
ERRSIG10 = 'MAXI' 'ABS' (DSIG10 / NSIG10) ;
'SI' IG1 ;
  'TRAC' ('CHAN' 'CHAM' (DDEP10 / NDEP10) MOD1) MOD1 'TITR' ('CHAI'
' Erreur relative sur les deplacements =' 'FORMAT' '(E8.1)' ERRDEP10) ;
  'TRAC' (DSIG10 / NSIG10) MOD1 'TITR' ('CHAI'
    ' Erreur relative sur la contrainte =' 'FORMAT' '(E8.1)' ERRSIG10) ;
'FINS' ;
ERR0     = 'MAXI' ('PROG' ERRDEP10 ERRSIG10) ;
*                                                                      *
'SI' (ERR0 > XPREC1) ;
  'ERRE' 5 ;
'FINS' ;
*                                                                      *
'FIN' ;




