* fichier :  relacori.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
graph='N';
****************************************
*
*
*  poutre maillée en coques et en massifs
*
* test de la relation de corps rigide
***********************************************
opti dime 3  ;
opti elem cu20;
p1 = 0 0 0 ; p2 = 0 1. 0 ;
l1 = p1 d 2 p2 ;
surf1 = l1 tran  5 (5 0 0 ) ;

l2 = cote 3 surf1 ;
surf2 = l2 tran 1 (0 0 .5) ;
surf2 = surf2 et (l2 tran 1 (0 0 -.5));
vol1 = volu surf2 tran 8 (8. 0 0) ;
vol2 = coul vol1 jaun ;
mail1 = surf1 et vol2 ;
pp = poin mail1 proche (13. .5 0) ;

* on tourne la poutre pour ne pas privilegier un repere particulier
ax1 = -1 0.5 0 ; ax2 = -1 0.5 1 ; ax3 = -1 1 0 ;
ax4 = 1 .5 0 ;
depl mail1 tour 55. ax1 ax4 ;
depl mail1 tour 30. ax1 ax2 ;

modl1 = mode surf1 mecanique coq8 ;
mat1 = mate modl1 youn 2e11 nu .3 rho 8000. epai 1.;
modl2 = mode vol2 mecanique cu20 ;
mat2 = mate modl2 youn 2e11 nu .3 rho 8000.;

modlt = modl1 et modl2 ;
matt = mat1 et mat2 ;
rig1 = rigi modlt  matt ;
mass1 = mass modlt  matt ;
ch1 = manu chpo mail1 1 uZ 1. ;
f1 = mass1 * ch1 ;

blo1 = bloq l1 depl rota ;
blo2 = rela cori depl rota l2 surf2 ;
rigt = blo1 et blo2 et rig1 ;

dep1 = reso f1 rigt ;
def1 = (defo dep1 surf1 1000.) et
                   (defo dep1 vol2 1000. jaun) ;
si (ega 'O' graph);
trac def1 cach;
finsi;

x1 = extr dep1 uz pp ;
** solution analytique
i1 = 1./12 ; q = 1.*8000;
x2 = q*(13.**4)/2e11/8./i1 ;
xe = (abs ((x1 - x2)/x2))*100 ;

mess 'deplacement suivant z';
mess 'analytique'x2 'C2000' x1 'erreur' xe '%' ;
 si (xe > .3) ; erre 5 ; sinon ; erre 0 ;finsi;
fin ;







