* fichier : adve2.dgibi
************************************************************************
* Section : Thermique Advection
* Section : Thermique Statique
************************************************************************
* Cas-test de l'operateur ADVEction.                                   *
*                                                                      *
* Ce cas-test verifie que le produit de la rigidite d'advection avec un*
* champ de temperature donne : v.gradT est egal a la solution attendue.*
*                                                                      *
* Attention, il s'agit du champ solution integre sur l'element fini    *
* et non pas la solution analytique.                                   *
*                                                                      *
*----------------------------------------------------------------------*
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
*                                                                      *
* Commentez cette ligne pour voir les traces :                         *
'OPTI' 'TRAC' 'PSC' ;
*                                                                      *
O1       = 0 0 ;
X1       = 1 0 ;
Y1       = 0 1 ;
*                                                                      *
S1       = (O1 'DROI' 1 X1) 'TRAN' 1 Y1 ;
L1       = S1  'COTE' 1 'COUL' 'ROUG' ;
L3       = S1 'COTE' 3 ;
L4       = S1 'COTE' 4 'COUL' 'ROUG' ;
'TITR' ' Maillage : carre de cote 1...' ;
'TRAC' 'QUAL' S1 ;
*                                                                      *
mo1      = 'MODE' S1 thermique advection ;
ma1      = 'MATE' mo1 'K' 1. 'RHO' 1.5 'C' 1. 'UX' 0.22 'UY' 1.0 ;
*                                                                      *
cht1     = (s1 'COOR' 1) + (S1 'COOR' 2) 'NOMC' T ;
*                                                                      *
KA1      = 'ADVE' mo1 ma1 ;
chq1     = ka1 * cht1 ;
*                                                                      *
chqref   = 'MANU' 'CHPO' s1 1 'Q' 0.305 'NATURE' 'DISCRET' ;
*                                                                      *
err1     =  'MAXI' 'ABS' (chq1 - chqref / chqref) ;
*                                                                      *
'OPTI' 'ECHO' 0 ;
'MESS' ;
'MESS'
' > Ecart relatif entre champs calcule et reference =' err1 ;
'MESS' ;
'OPTI' 'ECHO' 1 ;
*                                                                      *
'SI' (err1 > 1.e-5) ;
  'ERRE' 5 ;
'SINO' ;
  'OPTI' 'ECHO' 0 ;
  'MESS' ;
  'MESS' ' > Test reussi ! ' ;
  'MESS' ;
'OPTI' 'ECHO' 1 ;

'FINS' ;
*                                                                      *
'FIN' ;


