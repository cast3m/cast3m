* fichier :  orth6.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST
SAUT PAGE;

*****************************************************************
*   TEST ORTH6                                                  *
*   PLAQUE CARREE ORTHOTROPE ENCASTREE                          *
*                                                               *
*   Test V.P.C.S. numero SSLS33/90                              *
*                 Groupe : Statique lineaire                    *
*                          Structure assemblee                  *
*                                                               *
*****************************************************************
*
OPTION  ECHO 0;
*
OPTION DIME 2 ELEM QUA4;
OPTION MODE PLAN CONT;
*
GRAPH = 'N';
* GRAPH = 'O';

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

*----------------------------------------------------------------*
*                geometrie : maillage                            *
*----------------------------------------------------------------*
*
PA =  0.5  0.5;
PB = -0.5  0.5;
PC = -0.5 -0.5;
PD =  0.5 -0.5;
PE =  0.   0.;
*
D1 = PA DROIT 10 PB;
D2 = PB DROIT 10 PC;
D3 = PC DROIT 10 PD;
D4 = PD DROIT 10 PA;
*
SUR1 = DALLAGE D1 D2 D3 D4 PLAN;
SUR = SUR1 ET PE;
*
ELIM SUR 0.001;
*
SI (NEG GRAPH 'N');
  TITRE 'ORTH6 : PLAQUE CARREE ORTHOTROPE';
  TRACE SUR 'QUAL';
FINSI ;
*
NBNOSUR = NBNO SUR1;
NBELSUR = NBEL SUR1;
*
*----------------------------------------------------------------*
*                 calcul de la rigidite                          *
*----------------------------------------------------------------*
*
MOD = MODE SUR1 MECANIQUE ELASTIQUE ORTHOTROPE QUA4;
*
DIR1 = (COS 30.) (SIN 30.);
MAT = MATE MOD DIRE DIR1 YG1 2.5737E10 YG2 7.377E9 YG3 7.377E9
NU12 0.31 NU13 0.31 NU23 0.31 G12 2.319E9;

* verification graphique de l'orientation :
* 2D massif => 2 vecteurs : V1 et V2
v12 = VLOC MOD MAT;
ve12 = VECT v12 MOD (mots 'V1X' 'V1Y' 'V2X' 'V2Y') 0.02
                    (mots 'AZUR' 'BRON');
SI (NEG GRAPH 'N');
  TITRE 'ORTH6 : V1(AZUR) V2 (JAUNE)';
  TRACE ve12 SUR ;
FINSI ;

RI = RIGI MOD MAT ;
*
*----------------------------------------------------------------*
*                   conditions aux limites                       *
*----------------------------------------------------------------*
*
CL = BLOQUE DEPL ROTA D2;
RIG = RI ET CL;
*
*----------------------------------------------------------------*
*                     chargement                                 *
*----------------------------------------------------------------*
*
FTOT = 1.E6;
NBNOD4 = NBNO D4;
F5 = FTOT / ((2 * NBNOD4) - 2);
F50 = F5 0.;
I = 1;
NBI = NBNOD4 - 1;
CHA = FORC (0. 0.) (D4 POIN 1);
REPETER BLOC1 NBI;
  CHA1 = FORC F50 (D4 POIN I);
  CHA2 = FORC F50 (D4 POIN (I+1));
  CHA = CHA ET CHA1 ET CHA2;
  I = I + 1;
FIN BLOC1;
*
*----------------------------------------------------------------*
*              resolution : calcul elastique                     *
*----------------------------------------------------------------*
*
* Champs de deplacements
*
DE = RESO RIG CHA;
*
* Contraintes : interpolation aux noeuds du maillage
*
CHAM1 = SIGM MOD MAT DE;
CHAM2 = CHAN NOEUD MOD CHAM1;
CHPO1 = CHAN CHPO MOD CHAM2;
*
* Trace facultatif du champ de d√©placement.
*
SI (NEG GRAPH 'N');
   TITR 'ORTH6 : Deplacements UX';
   TRAC CACH SUR1 (EXCO DE UX);
   TITR 'ORTH6 : Deplacements UY';
   TRAC CACH SUR1 (EXCO DE UY);
FINSI;
*
*----------------------------------------------------------------*
*                  extraction des resultats                      *
*----------------------------------------------------------------*
*
UA = EXTR DE UX PA; UA1 = 1.10E-4;
ERGUA = 100 * (ABS ((UA1 - UA) / UA1));
*
UD = EXTR DE UX PD; UD1 = 9.95E-5;
ERGUD = 100 * (ABS ((UD1 - UD) / UD1));
*
UE = EXTR DE UX PE; UE1 = 4.77E-5;
ERGUE = 100 * (ABS ((UE1 - UE) / UE1));
*
VA = EXTR DE UY PA; VA1 = -1.31E-4;
ERGVA = 100 * (ABS ((VA1 - VA) / VA1));
*
VD = EXTR DE UY PD; VD1 = -7.17E-5;
ERGVD = 100 * (ABS ((VD1 - VD) / VD1));
*
VE = EXTR DE UY PE; VE1 = -4.98E-5;
ERGVE = 100 * (ABS ((VE1 - VE) / VE1));
*
SMXXE = EXTR CHPO1 SMXX PE;
SMXXE = SMXXE / 1.E6;
SMXXE1 = 1.02;
ERGXX = 100 * (ABS ((SMXXE1 - SMXXE) / SMXXE1));
*
SMYYE = EXTR CHPO1 SMYY PE;
SMYYE = SMYYE / 1.E6;
SMXYE = EXTR CHPO1 SMXY PE;
SMXYE = SMXYE / 1.E6;
*
*----------------------------------------------------------------*
*                 affichage des resultats                        *
*----------------------------------------------------------------*
*
* Trace facultatif de la deformees
*
SI (NEG GRAPH 'N');
  TITR ' PLAQUE CARREE ORTHOTROPE : DEFORMEE .';
  AMPVEC = 1E-6;
  VECT1= VECT CHA AMPVEC FX FY ROUGE;
  REA2 = REAC DE RIG;
  VECT2= VECT REA2 1e-6 FX FY BLEU;
  DEF0 = DEFO SUR  DE 0.;
  DEF1 = DEFO SUR  DE 30. (VECT1 ET VECT2) VERT;
  TRAC (DEF0 ET DEF1);
FINSI;
*
SAUT LIGN;
MESS 'Resultats : test orth6.dgibi';
MESS '----------------------------';
SAUT 1 LIGN;
MESS ' Deplacements';
MESS ' ------------';
SAUT 1 LIGN;
*
MESS '   Point A :';
MESS '   ---------';
SAUT 1 LIGN;
MESS '     Deplacement theorique horizontal : ' UA1 'm';
MESS '     Deplacement calcule horizontal   : ' UA 'm';
MESS '                     Soit un ecart de : ' ERGUA '%';
SAUT 1 LIGN;
MESS '     Deplacement theorique vertical   : ' VA1 'm';
MESS '     Deplacement calcule vertical     : ' VA 'm';
MESS '                     Soit un ecart de : ' ERGVA '%';
SAUT 1 LIGN;
*
MESS '   Point D :';
MESS '   ---------';
SAUT 1 LIGN;
MESS '     Deplacement theorique horizontal : ' UD1 'm';
MESS '     Deplacement calcule horizontal   : ' UD 'm';
MESS '                     Soit un ecart de : ' ERGUD '%';
SAUT 1 LIGN;
MESS '     Deplacement theorique vertical   : ' VD1 'm';
MESS '     Deplacement calcule vertical     : ' VD 'm';
MESS '                     Soit un ecart de : ' ERGVD '%';
SAUT 1 LIGN;
*
MESS '   Point E :';
MESS '   ---------';
SAUT 1 LIGN;
MESS '     Deplacement theorique horizontal : ' UE1 'm';
MESS '     Deplacement calcule horizontal   : ' UE 'm';
MESS '                     Soit un ecart de : ' ERGUE '%';
SAUT 1 LIGN;
MESS '     Deplacement theorique vertical   : ' VE1 'm';
MESS '     Deplacement calcule vertical     : ' VE 'm';
MESS '                     Soit un ecart de : ' ERGVE '%';
SAUT 1 LIGN;
*
MESS ' Contraintes au point E';
MESS ' ----------------------';
SAUT 1 LIGN;
*
MESS '     Contrainte theorique horizontale : ' SMXXE1 'MPa';
MESS '     Contrainte calculee horizontale  : ' SMXXE 'MPa';
MESS '                     Soit un ecart de : ' ERGXX '%';
SAUT 1 LIGN;
MESS '     Contrainte calculee verticale    : ' SMYYE 'MPa';
SAUT 1 LIGN;
MESS '  Contrainte calculee de cisaillement : ' SMXYE 'MPa';
SAUT 1 LIGN;
*
MESS ' Maillage : ';
MESS ' ---------- ';
SAUT LIGN;
MESS '    Nombre de noeuds  : ' NBNOSUR;
MESS '    Nombre d elements : ' NBELSUR 'QUA4';
SAUT LIGN;
*----------------------------------------------------------------*
*                  code fonctionnement                           *
*----------------------------------------------------------------*
*
ERGMAX = MAXI ( PROG ERGUA ERGUD ERGUE ERGVA ERGVD ERGVD ERGXX);
*
* Il y a erreur si un des pourcentages ERG* est suprieur  1.%
*
SAUT 2 LIGN;
SI (ERGMAX <EG 1.0);
    ERRE  0 ;
SINO;
    ERRE  5 ;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






