* fichier : ETG_MELEME.dgibi
************************************************************************
* Section : Maillage Autres
************************************************************************

*******************************************************
*           Test ETG_MELEME.dgibi: Jeux de données    *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 2017/03/29

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*___________________________________________________________________
*   TEST ETG_MELEME                                                *
*                                                                  *
*   Fusion d'une TABLE ESCLAVE de MAILLAGES                        *
*                                                                  *
*       A partir   d'un MAILLAGE   partitionné dans  une  TABLE de *
*   SOUS-TYPE ESCLAVE, la fusion pour ne faire qu'un objet MAILLAGE*
*   unique se fait en une seule opération avec l'opérateur ETG     *
*___________________________________________________________________
*

* MAILLAGE
OPTI DIME 3 ELEM CUB8;

P1 = 0. 0. 0. ;
P2 = 1. 0. 0. ;

NBE1 = 25;
L1   = DROI NBE1 P1 P2;
L2   = L1 PLUS (0. 1. 0.);

S1   = REGL NBE1 L1 L2;
S2   = S1 PLUS (0. 0. 1.);

V1   = VOLU NBE1 S1 S2;

* Mailtot est un MAILLAGE COMPLEXE contenant toutes les primitives de
* fabrication de V1
MAILTOT = V1 ET S1 ET S2 ET L1 ET L2;
NBEL1   = NBEL MAILTOT;
TYPE1   = MAILTOT 'ELEM' 'TYPE';

* PARTITIONNEMENT de MAILTOT en NBPART
NBPART  = 50000;
MAILP   = PART 'ARLE' NBPART MAILTOT;

* Vérification du bon fonctionnement de ETG
* FUSION du MAILLAGE partitionné en une seule opération
MAILTO2 = ETG MAILP;


*___________________________________________________________________
* Validation en comparant MAILTOT a MAILTO2
*___________________________________________________________________
* Nombre d'éléments total
NBEL2   = NBEL MAILTO2;LIST NBEL2;
SI ('NEG' NBEL2 NBEL1);
  MESS 'Erreur  1 ETG de MAILLAGE : ' NBEL1 NBEL2;
FINS;

* Nombre de type d'éléments
TYPE2   = MAILTO2 'ELEM' 'TYPE';
SI ('NEG' (DIME TYPE1) (DIME TYPE2));
  MESS 'Erreur  2 ETG de MAILLAGE : ' (DIME TYPE1) (DIME TYPE2);
FINS;

* Boucle sur les MELEME SIMPLE
REPE SURi (DIME TYPE1);
  II     = &SURi;
  NOMELM = 'EXTR' TYPE1 II  ;
  MAI1   = 'ELEM' MAILTOT NOMELM;
  MAI2   = 'ELEM' MAILTO2 NOMELM;

* Nombre d'éléments
  NBEL1  = 'NBEL' MAI1;
  NBEL2  = 'NBEL' MAI2;
  SI ('NEG' NBEL1 NBEL2);
    MESS 'Erreur  3 ETG de MAILLAGE : ' II NOMELM NBEL1 NBEL2;
  FINS;

* Nombre de noeuds
  NBNO1  = 'NBNO' MAI1;
  NBNO2  = 'NBNO' MAI2;
  SI ('NEG' NBNO1 NBNO2);
    MESS 'Erreur  4 ETG de MAILLAGE : ' II NOMELM NBNO1 NBNO2;
  FINS;

* différence symétrique
  DIFF1 = DIFF MAI1 MAI2;
  SI (('NEG' ('NBEL' DIFF1) 0) 'ET' ('NEG' ('NBNO' DIFF1) 0));
    MESS 'Erreur  5 ETG de MAILLAGE : ' II NOMELM (NBEL DIFF1);
  FINS;
FIN SURi;

* Mesure du temps passé dans les opérateurs
TEMP 'IMPR';

FIN;



