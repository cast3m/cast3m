* fichier :  plas11.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Plas11.dgibi: Jeux de données        *
*           ---------------------------------         *
*                                                     *
OPTION ECHO 1;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;
GRAPH = 'N';
SAUT PAGE;
*
*******************************************************
*                                                     *
*       TEST : PLAS11                                 *
*       DEFORMATIONS PLANES GENERALISEES              *
*                                                     *
*    une poutre de section rectangulaire est          *
*    soumise à une rotation imposée RX :              *
*                                                     *
*     Hauteur:4 Largeur:2                             *
*                                                     *
*    '''''''''                                        *
*    '   *   '                                        *
*    '   |   '           cette poutre est modélisée   *
*    ---------          avec une partie massive (1x1) *
*    |       |          une autre avec des éléments   *
*    |    -------->@RX  COQ2  (long :.5 epais 2.) et  *
*    |       |          la dernière avec des éléments *
*    ---------          point POI1 : 4 point, 2 en    *
*    '   |   '          haut, 2 en bas (sect .5 )     *
*    '   *   '                                        *
*    '''''''''                                        *
*                                                     *
*    On étudie le comportement du moment résultant    *
*    de cette rotation imposée jusqu'au domaine       *
*    plastique                                        *
*                                                     *
*******************************************************
TITRE 'PLAS11 - SECTION DE POUTRE SOUMISE A UNE  ROTATION IMPOSEE';
OPTION ECHO 1;
TEMPS;
*
*
OPTI DIME 2;
OPTI ELEM QUA8;
*
* on définit le point support des déformations
* planes généralisées
*
PG = 0. 0.;PG1=PG PLUS (0. 0.);PG2 = PG PLUS (0. 0.);
*
OPTI MODE PLAN GENE  ;
*
* quelques initialisations :
*
I = 1;
EVL1 = PROG 0. ;
EVL2 = PROG 0. ;
EVL3 = PROG 0. ;
*
* et quelques constantes pour le calcul théorique :
*
* demi hauteur de la section
H = 2.;
* demi largeur de la section
B = 1. ;
* le module de young
E1 = 2E11;
* la limite élastique du matériau
SILIM = 4.83E8;
* le moment d'inertie par rapport a l'axe des X
IX = (2.*B)*((2.*H)**3)/12.;
* la valeur limite de la déformation élastique
EPSEL = SILIM/E1;
* la valeur limite du moment élastique
MOMEL = E1*IX*EPSEL/H;
* le moment plastique ultime
MOLIM = 3*MOMEL/2.;
*
*======================================================
* géometrie : maillage
*======================================================
*
* dimension en mètres
*
*----------maillage partie massive-----------*
*
* points
*
P1 = 1. -1.;
P2 = 1. 1.;
P3 = -1. 1.;
P4 = -1. -1.;
*
* droite et lignes
*
* coefficients de finesse du maillage
*
N = 4;
M = 1;
*
D1 = P1 D N P2 ;
D2 = P2 D M P3 ;
D3 = P3 D N P4 ;
D4 = P4 D M P1 ;
*
SURF1 = DALL D1 D2 D3 D4;
*
*----------maillage éléments coq2------------*
*
OPTI ELEM SEG2 ;
*
P5 = 0. 1. ;
P6 = 0. 1.5 ;
P7 = 0. -1. ;
P8 = 0. -1.5 ;
*
D5 = P5 D 2 P6;
D6 = P7 D 2 P8;
COQU1 = D5 ET D6 ;
*
P9 = 0. 1.625;
P10 = 0. 1.875;
P11 = 0. -1.625;
P12 = 0. -1.875;
*
*----------maillage éléments poi1------------*
*
PO11 = MANU POI1 P9;
PO12 = MANU POI1 P10;
PO13 = MANU POI1 P11;
PO14 = MANU POI1 P12;
PO1 = PO11 ET PO12 ET PO13 ET PO14;
*
SURTOT = SURF1 ET COQU1 ET PO1 ;
*
SI (NEG GRAPH 'N');
   TITR 'PLAS12 : MAILLAGE';
   TRAC QUAL SURTOT;
FINSI;
*
ELTOT = NBEL SURTOT;
NOTOT = NBNO SURTOT;
*
*
*======================================================
* modèle - matériau
* rigidité
*======================================================
*
*----------------partie massive-----------------*
*
MO1 = MODE SURF1 MECANIQUE ELASTIQUE PLASTIQUE PARFAIT  QUA8 DPGE PG;
MA1 = MATE MO1 YOUN E1 NU 0. SIGY SILIM;
ma2=  MATE MO1 YOUN 'TOTO' NU 0. SIGY SILIM;
list ma2;
*
*----------------éléments coq2------------------*
*
MO2 = MODE COQU1 MECANIQUE ELASTIQUE PLASTIQUE PARFAIT  COQ2 DPGE PG1;
MAT2 = MATE MO2 YOUN E1 NU 0. SIGY SILIM;
CAR2 = CARA MO2 EPAI 2. ;
MA2 = MAT2 ET CAR2 ;
*
*-----------------éléments poi1-----------------*
*
MO3 = MODE PO1 MECANIQUE ELASTIQUE PLASTIQUE PARFAIT  POI1   DPGE PG1;
MAT3 = MATE MO3 YOUN E1 NU 0. SIGY SILIM;
CAR3 = CARA MO3 SECT .5 ;
MA3 = MAT3 ET CAR3;
*
MODE1 = MO1 ET MO2 ET MO3;
MATE1 = MA1 ET MA2 ET MA3;
*
RI1 = RIGI MODE1 MATE1;

*
*======================================================
* conditions aux limites
*======================================================
*
P15 = SURF1 POINT PROCHE (0. 0.);
P16 = SURF1 POINT PROCHE (0. 1.);
B1 = BLOQ DEPL P15;
B2 = BLOQ UX P16;
B3 = BLOQ DEPL PO1 ;
B4 = BLOQ UX COQU1 ;
B5 = BLOQ UY (P5 ET P7) ;
B6 = BLOQ UZ RY (PG et pg1 );
B7 = BLOQ RX ( PG et pg1 );
BTOT = B1 ET B2 ET B3 ET B4 ET B5 ET B6 ET B7;
RI = RI1 ET BTOT;
*
*======================================================
* chargement
*======================================================
*
TEMP1 = (PROG 4. PAS 2. 30.) ET (PROG 50.);
LAM1 = (PROG 4. PAS 2. 30.) ET (PROG 50.);
FAT1 = .0002;
TITRE 'PLAS11 : CHARGEMENT';
EV1 = EVOL MANU 'TEMPS' TEMP1 'RX(T)' LAM1 ;
DE1 = DEPI B7 FAT1 ;
CHRIX = CHAR 'DIMP' DE1 EV1 ;
*
SI (NEG GRAPH 'N');
  DESS EV1 ;
FINSI;
*
*======================================================
* calcul non linéaire
*======================================================
*
opti sauv 'plas11.sauv';
TAB1 = TABLE;
TAB1.'BLOCAGES_MECANIQUES' = BTOT;
TAB1.'MODELE' = MODE1;
TAB1.'CARACTERISTIQUES' = MATE1;
TAB1.'CHARGEMENT' = CHRIX;
TAB1.'TEMPS_CALCULES' = TEMP1;
tab1.'TEMPS_SAUVEGARDES' = TEMP1;
tab1.'PRECISION' = 1.e-6;

PASAPAS TAB1 ;




*
*  recalcul des trois dernier pas (pour tester l'option reprise de
* pasapas
*
tab1.REPRISE=26.;
tab1.'TEMPS_CALCULES'=prog 28. 30. 50.;
tab1.'TEMPS_SAUVEGARDES'=prog 28. 30. 50.;
pasapas tab1;


*
*======================================================
* post-traitement : tracé d'une courbe et comparaison
* avec la solution analytique
*======================================================
*
DIM1 = DIMENSION TEMP1;
*
REPETER BLOC1 DIM1;
*
TPS = EXTR TEMP1 I;
CH1 = PECHE TAB1 DEPLACEMENTS TPS;
MOIMP = REAC RI CH1;
*
*EXTRACTION DU MOMENT MX CORRESPONDANT À LA ROTATION RX
*
MMX = EXTR MOIMP MX PG;MMX1= EXTR MOIMP MX PG1;MMX=MMX+MMX1;
RRX = EXTR CH1 RX PG;RRX1= EXTR CH1 RX PG1;RRX=RRX+RRX1;
EVL1 = EVL1 ET (PROG MMX );
EVL2 = EVL2 ET (PROG RRX );
*
*---------------SOLUTION THEORIQUE--------------------*
*
EPSN = 2.*RRX ;
*
* quand on est dans le domaine plastique :
*
SI (EPSN > EPSEL) ;
 MOMN = (1 - ((( EPSEL / EPSN) ** 2) / 3)) * MOLIM ;
 EVL3 = EVL3 ET (PROG MOMN);
*
* quand on est dans le domaine élastique :
*
SINON;
 MOMN = E1 * IX * RRX ;
 EVL3 = EVL3 ET (PROG MOMN) ;
FINSI;
*
I = I+1;
*
FIN BLOC1;
*
SI (NEG GRAPH 'N');
*
COURB = TABLE;
COURB.1 = 'MARQ ETOI NOLI' ;
*
titre 'PLAS11 : COURBE MX EN FONCTION DE RX ';
EV1 = EVOL MANU 'RX' EVL2 'MX' EVL1 ;
EV2 = EVOL MANU 'RX' EVL2 'MX' EVL3 ;
*  courbe théorique : traits continus
* point calculés    : marques étoiles
DESS  (EV1 ET EV2) COURB LOGO;
FINSI;
*
* calcul de l'écart avec la solution de référence
*
ERG1 = 100*(ABS((MOMN-MMX)/MOMN));
*
*======================================================
* affichage des résultats
*======================================================
*
MESS ' RESULTATS : TEST PLAS11 ';
MESS '---------------------------';
SAUT 2 LIGN;
*
MESS 'on cherche la valeur du moment calculé dans le ';
MESS 'domaine plastique';
MESS 'elle est comparée a une valeur obtenue analytiquement';
SAUT 1 LIGN;
MESS ' moment théorique :' MOMN 'MPa';
SAUT 1 LIGN;
MESS ' moment calculé :' MMX 'MPa';
SAUT 1 LIGN;
MESS '          Soit un écart de :' ERG1 '%';
SAUT 2 LIGN;
*
MESS '    Nombre d elements :' ELTOT;
MESS '    Nombre de noeuds  :' NOTOT;
MESS '       soit ' (NOTOT * 3) 'd.d.l.';
*
*
*======================================================
*                  code de fonctionnement
*======================================================
*
*     L'écart maximum entre valeur théorique et
* calculée doit être inférieur a 0.5%.
*
SAUT 2 LIGN;
SI (ERG1 <EG 0.5);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*======================================================
*                 Temps de calcul et fin
*======================================================
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;
*























