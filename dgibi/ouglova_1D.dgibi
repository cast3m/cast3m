* fichier : ouglova_1D.dgibi
opti epsi lineaire ;
*******************************************************
*******************************************************
*******************************************************
*  Cas test de l'implantation numerique du modele
*  OUGLOVA 1D
*******************************************************
* Developpe par :
*     Romili PAREDES
*     Benjamin RICHARD
* Contact :
*     Romili.Paredes@cea.fr
*     Benjamin.Richard@cea.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
******************************************************

*******************************************************
* Options
*******************************************************

OPTI DIME 2 ELEM SEG2 TRAC PSC
DENSITE 1;

******************************************************
*** Parametres geometriques
******************************************************

Ra  = 1;
sc1 = 3.141592/4;
h   = 1;

******************************************************
*** Definition de la geometrie
******************************************************

P1 = 0.  0.;
P2 = h   0.;

LT = D P1 P2;

******************************************************
*** Définition du modele
******************************************************

MOD1 = MODE LT MECANIQUE ELASTIQUE ISOTROPE
                         PLASTIQUE OUGLOVA BARRE;
*MOD1 = MODE LT MECANIQUE ELASTIQUE ISOTROPE BARRE;
******************************************************
*** Parametres materiaux
******************************************************

* Module d'Young
ygn = 200.e9;

* Coefficient de Poisson
nun = 0.33;

* Limite elastique
sigym = 500.e6;

* Coefficient d'ecrouissage
km = 500.e6;

* Exposant d'ecrouissage
mm = 2.786;

* Taux de corrosion en percentage
tcm = 15;

* Endommagement critique
dcm = 0.2;


MAT1 = MATE MOD1 'YOUN' ygn 'NU' nun 'SECT' sc1
                 'SIGY' sigym 'K   ' km 'M   ' mm
                 'TC  ' tcm 'DC  ' dcm;
*MAT1 = MATE MOD1 YOUN ygn NU nun SECT sc1;

******************************************************
*** Definition des conditions aux limites
*** et des déplacements imposés
******************************************************

CL1 = BLOQ P1 UX UY;
CL2 = BLOQ LT UY;
CL11 = BLOQ P2 UX;
D1   = DEPI CL11 1;

CLTOT = CL1 ET CL2 ET CL11;

******************************************************
*** Definition de chargement
******************************************************

LI1  = PROG 0. 1.;
LI2  = PROG 0. 2.5e-1;
LIS1 = PROG 0. PAS 0.01 1;

EV   = EVOL MANU LI1 LI2;

CHA1 = CHAR 'DIMP' D1 EV;

CHTOT = CHA1;

******************************************************
*** Resolution
******************************************************

TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CLTOT;
TAB1.'MODELE' = MOD1;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'MAXITERATION' = 1000;

PASAPAS TAB1;

******************************************************
*** Verification
******************************************************

EV2 = @GLOBAL TAB1 cl1 EV FX;
liste EV2;
dess ev2;

vref = EXTR EV2 'ORDO';
vref = EXTR 29 vref;

err =  abs(vref  + 5.68172e8) / 5.68172e8;
message ' erreur relative ';
list err;

si (err > 1.e-3);
   erreur (5);
finsi;

FIN;























