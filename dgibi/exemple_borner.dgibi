* fichier : exemple_borner.dgibi
************************************************************************
* Section : Mathematiques Fonctions
* Section : Mecanique Elastique
************************************************************************
* EXEMPLES D'UTILISATION DE L'OPERATEUR BORNER :
*-----------------------------------------------

* EVOLUTION :
*------------
lr_1 = 'PROG' -5. 'PAS' 0.5 5. ;
lr_2 = 'EXP' lr_1 ;
ev1 = evol roug manu 'X' lr_1 'Y' lr_2 ;
ev2 = (borner ev1 1 'MAXI' 5.) coul bleu ;
ev3 = (borner ev1 1 'MINI' 60.) 'COUL' VERT ;
ev4 = (borner ev1 1 'COMPRIS' 40. 1.) 'COUL' VERT ;
ev4 = (borner ev1 1 'COMPRIS' 40. 9.) 'COUL' jaun ;
ev5 = ev1 et ev2 et ev3 et ev4 ;
** dess ev5 ;
ev6 = borner ev5 2 'MAXI' 100.  4 'COMPRIS' 30. 30. ;
** dess ev6 ;

* CHPOINT :
*----------
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;

P0 = 0. 0. ;
Px = 1. 0. ;
Py = 0. 1. ;

Dr1 = 'DROITE' 5 P0 Px ;
Surf1 = 'TRANS' 4 Dr1 Py ;

Chp1 = 'EXP' (5. '*' ('COOR' 1 Dr1)) ;

Chp1_b = 'BORNER' Chp1 'SCAL' 'MINI' 100. ;
* La ligne ci-dessous a l'erreur 281
* Chp1_b = 'BORNER' Chp1 'TOTO' 'MINI' 100. ;
'LISTER' Chp1 ; 'LISTER' Chp1_b ;
*'TRACER' Chp1 Dr1 ; 'TRACER' Chp1_b Dr1 ;

Chp_ux = 'NOMC' 'UX  ' ('COOR' 1 Surf1) ;
Chp_uy = 'NOMC' 'UY  ' ('COOR' 2 Surf1) ;
Chp_u  = Chp_ux 'ET' Chp_uy ;

Chp_u_b = 'BORNER' Chp_u 'UY' 'MINI' 0.123
                         'UX' 'COMPRIS' 0.1986 0.7890 ;
'LISTER' Chp_u ; 'LISTER' Chp_u_b ;

Chp_t = Chp_u '+' Chp1 ;

Chp_t_b = 'BORNER' Chp_t 'SCAL' 'MAXI' 50 ;

'LISTER' Chp_t ; 'LISTER' Chp_t_b ;

* MCHAML :
*---------
'OPTION' 'DIME' 1 'ELEM' 'SEG2' ;

P0 = 'POINT' 0. ;
Px = 'POINT' 1. ;
Dr1 = 'DROITE' 5 P0 Px ;
Mod1 = 'MODELE' Dr1 'THERMIQUE' ;

Chmx1 = 'MANU' 'CHML' Dr1 'SCAL' 1. ;
Chm1 = 'EXP' (5. '*' ('COOR' 1 Chmx1)) ;
Chm1_b = 'BORNER' Chm1 'SCAL' 'MINI' 100. ;
* La ligne ci-dessous a l'erreur 280
* Chm1_b = 'BORNER' Chm1 'TOTO' 'MINI' 100. ;
'LISTER' Chm1 ; 'LISTER' Chm1_b ;

Chmx1 = 'MANU' 'CHML' Mod1 'SCAL' 1. 'MASSE' ;
Chm1 = 'EXP' (5. '*' ('COOR' 1 Chmx1)) ;
Chm1_b = 'BORNER' Chm1 'SCAL' 'COMPRIS' 5. 59. ;
'LISTER' Chm1 ; 'LISTER' Chm1_b ;
** 'TRACER' Chm1 Mod1 ; 'TRACER' Chm1_b Mod1 ;

'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;

Py = 0. 1. ;

Surf1 = 'TRANS' 4 Dr1 Py ;
Mod2 = 'MODELE' Surf1 'MECANIQUE' 'ELASTIQUE' ;

Chmx2  = 'MANU' 'CHML' Surf1 'SCAL' 1. ;
Chm_ux = 'NOMC' 'UX  ' ('COOR' 1 Chmx2) ;
Chm_uy = 'NOMC' 'UY  ' ('COOR' 2 Chmx2) ;
Chm_u  = Chm_ux 'ET' Chm_uy ;

Chm_u_b = 'BORNER' Chm_u 'UY' 'MINI' 0.123
                         'UX' 'COMPRIS' 0.1986 0.7890 ;
'LISTER' Chm_u ; 'LISTER' Chm_u_b ;
** 'TRACER' Chm_u Mod2 ; 'TRACER' Chm_u_b Mod2 ;

Chm_t = Chm_u 'ET' Chm1 ;

Chm_t_b = 'BORNER' Chm_t 'SCAL' 'MAXI' 50 ;
'LISTER' Chm_t ; 'LISTER' Chm_t_b ;

Chm_sl = 'MANU' 'CHML' Mod2 'SCAL' ('LECT' 1 100) 'STRESSES' ;
Chm_t2 = Chm_u 'ET' chm_sl 'ET' Chm1 ;

Chm_t2_b = 'BORNER' Chm_t2 'SCAL' 'MAXI' 50 ;
'LISTER' Chm_t2 ; 'LISTER' Chm_t2_b ;

'FIN' ;





