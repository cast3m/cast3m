* fichier : test_AMITEX.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*23456789123456789123456789123456789123456789123456789123456789123456789
*
* CAS TESTS POUR L'UTILISATION DES PROCEDURES D'APPLICATION DES
* CONDITIONS AUX LIMITES ET DE CALCUL DE COMPORTEMENT MOYEN
*
* CAS CONSIDERES :
*      CAS HOMOGENE ANISOTROPE
*      CAS "MATRICE/INCLUSION SPHERIQUE" SUR MAILLAGE REGULIER
*
* L. GELEBART, DECEMBRE 2009
*=======================================================================
*
*                                                             PARAMETRES
*=======================================================================
NEL0 = 8;
NEL1 = 2;
VISU0 = 1;
* proprietes elastiques de l'inclusion et de la matrice
EI0 = 100.;
EM0 = 400.;
NUI0 = 0.3;
NUM0 = 0.1;
* Position et rayon de l'inclusion
DX0 = 0.3;
DY0 = 0.4;
DZ0 = 0.6;
R0 = 0.25;
E0 = 0.1;
* Chargement
CTAB = TABLE;
CTAB . 1 = 1.; CTAB . 2 = 2.; CTAB . 3 = 3.;
CTAB . 4 = 4.; CTAB . 5 = 5.; CTAB . 6 = 6.;

* Tolerance sur les tests
tol0 = 1e-9;

*
*                                                 GEOMETRIE ET MATERIAUX
*=======================================================================
*
* cas matrice/inclusion sur maillage regulier
*----------------------------------------------
OPTI DIME 3;opti epsilon lineaire;
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;
OPTI ELEM SEG2;
P1P2 = DROI P1 P2 NEL0;
P2P3 = DROI P2 P3 NEL0;
P3P4 = DROI P3 P4 NEL0;
P4P1 = DROI P4 P1 NEL0;
OPTI ELEM QUA4;
SURF0 = DALL P1P2 P2P3 P3P4 P4P1;
OPTI DIME 3 ELEM CUB8;;
VOL0 = SURF0 VOLU NEL0 TRANS (0. 0. 1.);
*
MODTOT0 = MODE VOL0 MECANIQUE ELASTIQUE;
UN0 = MANU CHML VOL0 SCAL 1.;
X0 = (COOR 1 VOL0) - DX0;
Y0 = (COOR 2 VOL0) - DY0;
Z0 = (COOR 3 VOL0) - DZ0;
DIST0 = ((X0 * X0 ) + (Y0 * Y0) + (Z0 * Z0)) ** 0.5;
DIST0 = CHAN CHAM DIST0 MODTOT0 RIGIDITE;
INCL0 = MASQUE DIST0 INFERIEUR R0;
MATR0 = MASQUE DIST0 SUPERIEUR R0;
YOUN0 = (EI0 * INCL0) + (EM0 * MATR0);
NU0 = (NUI0 * INCL0) + (NUM0 * MATR0);
MATTOT0 = MATE MODTOT0 YOUN YOUN0 NU NU0;
RIGMAT0 = RIGI MODTOT0 MATTOT0;
*
* cas homogene anisotrope
*-------------------------------------------
OPTI DIME 3;
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;
OPTI ELEM SEG2;
P1P2 = DROI P1 P2 NEL1;
P2P3 = DROI P2 P3 NEL1;
P3P4 = DROI P3 P4 NEL1;
P4P1 = DROI P4 P1 NEL1;
OPTI ELEM QUA4;
SURF0 = DALL P1P2 P2P3 P3P4 P4P1;
OPTI DIME 3 ELEM CUB8;;
VOL1 = SURF0 VOLU NEL1 TRANS (0. 0. 1.);
*
DIR0 = POIN 1. 0. 0.;
DIR1 = POIN 0. 1. 0.;
MODTOT1 = MODE VOL1 MECANIQUE ELASTIQUE ANISOTROPE;
D110 = 123;
D210 = 45;
D220 = 678;
D310 = 91;
D320 = 23;
D330 = 456;
D410 = 10;
D420 = 11;
D430 = 12;
D440 = 222;
D510 = 2;
D520 = 4;
D530 = 6;
D540 = 8;
D550 = 777;
D610 = 10;
D620 = 14;
D630 = 16;
D640 = 18;
D650 = 13;
D660 = 321;
MATTOT1= MODTOT1 MATE DIRECTION DIR0 DIR1 PARALLELE D11 D110 D21 D210
D22 D220 D31 D310 D32 D320 D33 D330 D41 D410 D42 D420 D43 D430 D44
D440 D51 D510 D52 D520 D53 D530 D54 D540 D55 D550 D61 D610 D62
D620 D63 D630 D64 D640 D65 D650 D66 D660;
K0 = TABLE;
K0 . 1 = TABLE;
K0 . 1 . 1 = D110; K0 . 1 . 2 = D210; K0 . 1 . 3 = D310;
K0 . 1 . 4 = D410; K0 . 1 . 5 = D510; K0 . 1 . 6 = D610;
K0 . 2 = TABLE;
K0 . 2 . 1 = D210; K0 . 2 . 2 = D220; K0 . 2 . 3 = D320;
K0 . 2 . 4 = D420; K0 . 2 . 5 = D520; K0 . 2 . 6 = D620;
K0 . 3 = TABLE;
K0 . 3 . 1 = D310; K0 . 3 . 2 = D320; K0 . 3 . 3 = D330;
K0 . 3 . 4 = D430; K0 . 3 . 5 = D530; K0 . 3 . 6 = D630;
K0 . 4 = TABLE;
K0 . 4 . 1 = D410; K0 . 4 . 2 = D420; K0 . 4 . 3 = D430;
K0 . 4 . 4 = D440; K0 . 4 . 5 = D540; K0 . 4 . 6 = D640;
K0 . 5 = TABLE;
K0 . 5 . 1 = D510; K0 . 5 . 2 = D520; K0 . 5 . 3 = D530;
K0 . 5 . 4 = D540; K0 . 5 . 5 = D550; K0 . 5 . 6 = D650;
K0 . 6 = TABLE;
K0 . 6 . 1 = D610; K0 . 6 . 2 = D620; K0 . 6 . 3 = D630;
K0 . 6 . 4 = D640; K0 . 6 . 5 = D650; K0 . 6 . 6 = D660;

RIGMAT1 = RIGI MODTOT1 MATTOT1;
*
*                            VALIDATION DES CHARGEMENTS MOYENS IMPOSES
*                                    SUR CAS TEST "MATRICE INCLUSION"
*=======================================================================
* Definition des chargements pour differents types de CL
*-------------------------------------------------------
RIGCL1 DEPI1 = @CLIM @CLPC VOL0 CTAB;
RIGCL2 DEPI2 = @CLIM @CLDHC VOL0 CTAB;
RIGCL3 DEPI3 = @CLIM @CLMI1C VOL0 CTAB;
RIGCL4 DEPI4 = @CLIM @CLMI2C VOL0 CTAB;
RIGCL5 DEPI5 = @CLIM @CLCH VOL0 CTAB;
RIGCL6 DEPI6 = @CLIM @CLPD VOL0 CTAB;
RIGCL7 DEPI7 = @CLIM @CLDH VOL0 CTAB;
*                                    Resolution elastique
*--------------------------------------------------------
DEPT = TABLE;
DEPT . 1 = RESOU (RIGCL1 ET RIGMAT0) DEPI1;
DEPT . 2 = RESOU (RIGCL2 ET RIGMAT0) DEPI2;
DEPT . 3 = RESOU (RIGCL3 ET RIGMAT0) DEPI3;
DEPT . 4 = RESOU (RIGCL4 ET RIGMAT0) DEPI4;
DEPT . 5 = RESOU (RIGCL5 ET RIGMAT0) DEPI5;
DEPT . 6 = RESOU (RIGCL6 ET RIGMAT0) DEPI6;
DEPT . 7 = RESOU (RIGCL7 ET RIGMAT0) DEPI7;
*                                              Validation
*      pour les chargements en contrainte moyenne imposee
*--------------------------------------------------------
V0 = MESU VOL0;
i = 0;
REPETE BOU0 5;
i = i + 1;
SIG0 = SIGMA MODTOT0 MATTOT0 (DEPT . i);
SXX0 = (INTG SIG0 MODTOT0 SMXX) / V0;
SYY0 = (INTG SIG0 MODTOT0 SMYY) / V0;
SZZ0 = (INTG SIG0 MODTOT0 SMZZ) / V0;
SXY0 = (INTG SIG0 MODTOT0 SMXY) / V0;
SXZ0 = (INTG SIG0 MODTOT0 SMXZ) / V0;
SYZ0 = (INTG SIG0 MODTOT0 SMYZ) / V0;
*MESS SXX0;
*MESS SYY0;
*MESS SZZ0;
*MESS SXY0;
*MESS SXZ0;
*MESS SYZ0;
SI ((ABS (SXX0 - (CTAB . 1))) > tol0);ERRE 5;FINSI;
SI ((ABS (SYY0 - (CTAB . 2))) > tol0);ERRE 5;FINSI;
SI ((ABS (SZZ0 - (CTAB . 3))) > tol0);ERRE 5;FINSI;
SI ((ABS (SXY0 - (CTAB . 4))) > tol0);ERRE 5;FINSI;
SI ((ABS (SXZ0 - (CTAB . 5))) > tol0);ERRE 5;FINSI;
SI ((ABS (SYZ0 - (CTAB . 6))) > tol0);ERRE 5;FINSI;
FIN BOU0;
*                                              Validation
*     pour les chargements en deformation moyenne imposee
*--------------------------------------------------------
i = 5;
REPETE BOU0 2;
i = i + 1;
DEF0 = EPSI MODTOT0 MATTOT0 (DEPT . i);
EXX0 = (INTG DEF0 MODTOT0 EPXX) / V0;
EYY0 = (INTG DEF0 MODTOT0 EPYY) / V0;
EZZ0 = (INTG DEF0 MODTOT0 EPZZ) / V0;
EXY0 = (INTG DEF0 MODTOT0 GAXY) / (2 * V0);
EXZ0 = (INTG DEF0 MODTOT0 GAXZ) / (2 * V0);
EYZ0 = (INTG DEF0 MODTOT0 GAYZ) / (2 * V0);
*MESS EXX0;
*MESS EYY0;
*MESS EZZ0;
*MESS EXY0;
*MESS EXZ0;
*MESS EYZ0;
SI ((ABS (EXX0 - (CTAB . 1))) > tol0);ERRE 5;FINSI;
SI ((ABS (EYY0 - (CTAB . 2))) > tol0);ERRE 5;FINSI;
SI ((ABS (EZZ0 - (CTAB . 3))) > tol0);ERRE 5;FINSI;
SI ((ABS (EXY0 - (CTAB . 4))) > tol0);ERRE 5;FINSI;
SI ((ABS (EXZ0 - (CTAB . 5))) > tol0);ERRE 5;FINSI;
SI ((ABS (EYZ0 - (CTAB . 6))) > tol0);ERRE 5;FINSI;
FIN BOU0;
*
*                            VALIDATION DU CALCUL DE COMPOTEMENT MOYEN
*                                    SUR CAS TEST HOMOGENE ANISOTROPE
*=======================================================================
VISU1 = TABLE;
VISU1 . 1 = 0;
VISU1 . 2 = 0;
VISU1 . 3 = 0;
CONV0 = 0;
AMPL0 = 1.;
*
TK = TABLE;
TK . 1 = TABLE;
TK . 2 = TABLE;
TK . 3 = TABLE;
TK . 4 = TABLE;
TK . 5 = TABLE;
TK . 6 = TABLE;
TK . 7 = TABLE;
K C D = @KEFF MODTOT1 MATTOT1 @CLPC AMPL0 CONV0 VISU1;
TK . 1 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLPD AMPL0 CONV0 VISU1;
TK . 2 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLDH AMPL0 CONV0 VISU1;
TK . 3 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLDHC AMPL0 CONV0 VISU1;
TK . 4 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLCH AMPL0 CONV0 VISU1;
TK . 5 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLMI1C AMPL0 CONV0 VISU1;
TK . 6 = K;
K C D = @KEFF MODTOT1 MATTOT1 @CLMI2C AMPL0 CONV0 VISU1;
TK . 7 = K;
*
ERR_K = 0;
i=0;
REPETE BOU0 7;
i = i + 1;
j = 0;
REPETE BOUJ0 6;
j = j + 1;
k = 0;
REPETE BOUK0 6;
k = k + 1;
ERR_K = ERR_K + (abs ((K0 . j . k) - (TK . i . j . k)));
FIN BOUK0;
FIN BOUJ0;
MESS 'ERR_K = ' ERR_K;
SI (ERR_K > tol0); ERRE 5; FINSI;
ERR_K = 0;
FIN BOU0;

*
*  Raouter les comparaison pilotage C et D pour DH et P
*
*
FIN;













