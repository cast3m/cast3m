* fichier : test_dess.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************
* Teste quelques fonctionnalités décoratives de DESSIN
*-----------------------------------------------------

* Mettre GRAPH a VRAI pour tracer a l'ecran, sinon tracer dans un .ps
GRAPH = FAUX  ;
'SI' ('NON' GRAPH) ;
  'OPTION' 'TRACER' 'PSC';
'FINSI' ;

* test des couleurs de remplissage (problème) et des différents marqueurs
* ceux qui sont sur les bords doivent apparaître
a=        'EVOL' 'BLEU'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .3 .81) ;
a=a 'ET' ('EVOL' 'ROUGE' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .4 .83));
a=a 'ET' ('EVOL' 'ROSE'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .5 .85));
a=a 'ET' ('EVOL' 'VERT'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .6 .89));
a=a 'ET' ('EVOL' 'TURQ'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .7 .93));
a=a 'ET' ('EVOL' 'JAUNE' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .8 .96));
a=a 'ET' ('EVOL' 'BLANC' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .9 1.0));

t   = 'TABLE'  ;
t.1 = 'MOT' 'MARQ PLEI XL CARR';
t.2 = 'MOT' 'MARQ PLEI L ROND';
t.3 = 'MOT' 'MARQ PLEI M LOSA';
t.4 = 'MOT' 'MARQ PLEI S CARR';
t.5 = 'MOT' 'MARQ PLEI SS CARR';
t.6 = 'MOT' 'MARQ MOIN';
t.7 = 'MOT' 'MARQ BARR';
t.titre = 'TABLE' ;
t.titre.1 = 'MOT' 'taille XL' ;
t.titre.2 = 'MOT' 'taille L' ;
t.titre.3 = 'MOT' 'taille M' ;
t.titre.4 = 'MOT' 'taille S' ;
t.titre.5 = 'MOT' 'taille SS' ;
t.titre.6 = 'MOT' 'MOINS' ;
t.titre.7 = 'MOT' 'BARRE' ;
'DESSIN' a t 'LEGE' 'TITRE' 'Remarquez les taquets sur les bords' ;

* test des nouvelles couleurs d'évolution
a=        'EVOL' 'ROSE'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .1 .81) ;
a=a 'ET' ('EVOL' 'BLANC' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .2 .83));
a=a 'ET' ('EVOL' 'AZUR'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .3 .83));
a=a 'ET' ('EVOL' 'ORANG' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .4 .83));
a=a 'ET' ('EVOL' 'VIOLET' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .5 .85));
a=a 'ET' ('EVOL' 'OCEAN' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .6 .89));
a=a 'ET' ('EVOL' 'CYAN'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .7 .93));
a=a 'ET' ('EVOL' 'OLIVE' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .8 .96));
a=a 'ET' ('EVOL' 'GRIS'  'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .9 1.0));
t   = 'TABLE'  ;
t.1 = 'MOT' 'MARQ PLEIN L ROND';
t.2 = 'MOT' 'MARQ PLEIN L ROND';
t.3 = 'MOT' 'MARQ PLEIN L ROND';
t.4 = 'MOT' 'MARQ PLEIN L ROND';
t.5 = 'MOT' 'MARQ PLEIN L ROND';
t.6 = 'MOT' 'MARQ PLEIN L ROND';
t.7 = 'MOT' 'MARQ PLEIN L ROND';
t.8 = 'MOT' 'MARQ PLEIN L ROND';
t.9 = 'MOT' 'MARQ PLEIN L ROND';
t.titre = 'TABLE' ;
t.titre.1 = 'MOT' 'rose' ;
t.titre.2 = 'MOT' 'blanc' ;
t.titre.3 = 'MOT' 'azur' ;
t.titre.4 = 'MOT' 'orange' ;
t.titre.5 = 'MOT' 'violet';
t.titre.6 = 'MOT' 'ocean';
t.titre.7 = 'MOT' 'cyan';
t.titre.8 = 'MOT' 'olive';
t.titre.9 = 'MOT' 'gris';
'DESSIN' a t 'LEGE' 'TITRE' 'Joli, non ?';


* test taille de titre (il est tronqué) et logo
tit = 'CHAINE'
'aujourd hui je vais a l ecole et je vais bien m éclater car il y a '
'plein de copainssympaavecquijevaisjoueràchat' ;
'DESSIN' a t 'LEGE' 'TITRE' tit logo ;

* test de l'impression d'un label en chaque point d'une évolution
ev  = 'EVOL' 'VERT' 'MANU' 'X' (prog 0 .5 1) 'Y' (prog 0 .3 1);
t   = 'TABLE'  ;
t.1 = 'MOT' 'LABEL Coucou';
'DESSIN' ev t 'TITRE' 'Test impression de labels' ;

* test de l'impression des taquets en abscisses et vignettes contenant
* le nom des points
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;
pt1 = 0 0;
pt2 = 1.01 0;
d   = 'DROIT' pt1 pt2 10 ;
x   = 'COOR' 1 d ;
e   = 'EVOL' 'TURQ' 'CHPO' x d ;
'DESSIN' e 'TITRE' 'Test des vignettes de points en bas' ;

'TEMPS' 'SGAC' 'IMPR' ;

* Test des nouvelles couleurs sur les maillages
* ---------------------------------------------
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;

'OPTION' 'COULEUR' 'BLEU' ;
m1 = 'MANU' 'QUA4' (0 0) (0 1) (1 1) (1 0) ;
m2 = 'COULEUR' 'ROUG' ('PLUS' m1 (1 0)) ;
m3 = 'COULEUR' 'ROSE' ('PLUS' m2 (1 0)) ;
m4 = 'COULEUR' 'JAUN' ('PLUS' m3 (1 0)) ;
m5 = 'COULEUR' 'VERT' ('PLUS' m4 (1 0)) ;
m6 = 'COULEUR' 'TURQ' ('PLUS' m5 (1 0)) ;
m7 = 'COULEUR' 'BLAN' ('PLUS' m6 (1 0)) ;

n1 = 'COULEUR' 'AZUR' ('PLUS' m1 (0 -1)) ;
n2 = 'COULEUR' 'ORAN' ('PLUS' n1 (1 0)) ;
n3 = 'COULEUR' 'VIOL' ('PLUS' n2 (1 0)) ;
n4 = 'COULEUR' 'OCEA' ('PLUS' n3 (1 0)) ;
n5 = 'COULEUR' 'CYAN' ('PLUS' n4 (1 0)) ;
n6 = 'COULEUR' 'OLIV' ('PLUS' n5 (1 0)) ;
n7 = 'COULEUR' 'GRIS' ('PLUS' n6 (1 0)) ;

'TITRE' 'BLEU ROUG ROSE JAUN VERT TURQ BLAN ,'
        'AZUR ORAN VIOL OCEA CYAN OLIV GRIS' ;
'TRACER' (m1 'ET' m2 'ET' m3 'ET' m4 'ET' m5 'ET' m6 'ET' m7 'ET'
          n1 'ET' n2 'ET' n3 'ET' n4 'ET' n5 'ET' n6 'ET' n7) 'FACE' ;

'ERREUR' 0 ;
'FIN' ;








