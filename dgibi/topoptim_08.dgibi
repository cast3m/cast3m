* fichier topoptim_08.dgibi
************************************************************************
************************************************************************

************************************************************************
** Topology optimization of a simple 2D structure subjected to a
** mechanical loading, with an axial periodicity restriction.
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V1.0 2017/04/18 Original version compatible with TOPOPTIM V2.1
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' QUA4 ;

* Mesh
nelx0 = 120 ;
nely0 = 30 ;
p0 = 0.0 0.0 ;
p1 = 0.0 (FLOT nely0) ;
p2 = (FLOT nelx0) 0.0 ;
lgn0 = DROI nely0 p0 p1 ;
msh0 = TRAN lgn0 nelx0 p2 ;
p2 = msh0 POIN 'PROC' p2 ;
msh1 = msh0 ELEM 'APPU' 'STRI' (msh0 POIN 'DROI' p0 p1 90.1) ;
DEPL 'TOUR' 20.0 p0 msh0 ;

* Model and material
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' ;
mat0 = MATE mod0 'YOUN' 210.0e9 'NU' 0.3 ;

* Boundary conditions and loading
bc0 = (BLOQ 'UX' lgn0) ET (BLOQ 'UY' p2) ;
load0 = FORC (0.0 -1.0) p1 ;

* Factor evolutions
pfct0 = EVOL 'MANU' 'CYCLES' (PROG  5  15) 'P' (PROG 1.0 3.0) ;
qfct0 = EVOL 'MANU' 'CYCLES' (PROG 25 105) 'Q' (PROG 1.0 5.0) ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ;
mdl0.'BLOCAGES_MECANIQUES' = bc0 ;
mdl0.'CHARGEMENT'          = load0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_LINEAIRE' = mdl0 ;
tab0.'FRACTION_VOLUME'     = 0.5 ;
tab0.'FACTEUR_P'           = pfct0 ;
tab0.'FACTEUR_Q'           = qfct0 ;
tab0.'RESTRICTIONS'        = TABL ;
tab0.'RESTRICTIONS'.(1)            = TABL ;
tab0.'RESTRICTIONS'.(1).'ZONE'     = msh1 ;
tab0.'RESTRICTIONS'.(1).'TYPE'     = 'PERIODICITE_AXIALE' ;
tab0.'RESTRICTIONS'.(1).'PERIODES' = 3 ;
tab0.'RESTRICTIONS'.(1).'POIN1'    = p0 ;
tab0.'RESTRICTIONS'.(1).'POIN2'    = p2 / 4.0 ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Plot to screen
topo0 = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
SI graph0 ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;


