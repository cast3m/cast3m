* fichier :  t_visk2.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
graph='N';
saut page ;
*------------------------
* test loi visk2
*------------------------
opti dime 3 elem cub8 ;
p_ori = 0. 0. 0.; e_x = 1. 0. 0. ; e_y = 0. 1. 0. ;
e_z = 0. 0. 1. ;
*
p1 = p_ori ;
l1 = p1 d 1 (p1 plus e_x) ;
s1 = l1 trans 1 e_y ;
v1 = s1 volu trans 1 e_z ;
s2 = face 2 v1 ;
*
misopoin = vrai ;
* ATTENTION le cas faux appelle un peu de travail de l operateur !
*
si faux ;
mo1 = mode v1 mecanique elastique viscoplastique visk2 ;
ma1 = mate mo1 young 2.e11 nu 0.3 rho 7.8e9 alpha 1.5e-5
  sigy 200.e6 h (0.1*2.e11) eta (2.*0.1*2.e11) hvis (0.1*2.e11) n 5. ;
finsi ;
si vrai ;
mo1 = mode v1 mecanique elastique viscoplastique visk2 ;
ma1 = mate mo1 young 150.e9 nu 0.3 rho 7.8e9 alpha 1.5e-5
        sigy 200.e6 h (0.1*2.e11) eta  0.2e11 hvis (2*0.2e11) n 1 ;
ma2 = mate mo1 young 20.e9 nu 0.3 rho 7.8e9 alpha 1.5e-5
        sigy 75.0e6 h (0.05*2.e11) eta 0.2e11 hvis (2*0.2e11) n 1 ;
finsi ;
si faux ;
mo1 = mode v1 mecanique elastique plastique cinematique ;
ma1 = mate mo1  young 150.e9 nu 0.3 rho 7.8e9 alpha 1.5e-5
         sigy 200.e6 h (0.1*2.e11) ;
ma2 = mate mo1  young 20.e9 nu 0.3 rho 7.8e9 alpha 1.5e-5
         sigy 75.0e6 h (0.05*2.e11) ;
finsi ;
*
chsig1 = zero mo1 contrain ;
chvar1 = zero mo1 varinter ;
*
t_ev = table ;
t_prog = table ;
si misopoin ;
t_prog . 1 =  (prog 0.  pas 1. 8.)*1. ;
t_prog . 2 =  (prog 0.  pas 1. 8.)*10. ;
sinon ;
t_prog . 1 =  prog 0. 1. 2. 2.3 2.6 3. pas 1. 20.  ;
t_prog . 2 =  prog 0. 10. 20. 23. 26. 30. pas 1. 40. pas 10. 60. ;
finsi ;
*
*opti impr 'poub' ;
*
si misopoin ;
 n_tra = 1 ;
sinon ;
 n_tra = 2 ;
finsi ;
repeter b_tra n_tra ;

bl1 = (bloq s1 uz) et (bloq p1 depla) et (bloq l1 uy) ;
bl2 = bloq s2 uz ;
chdimp1 = depi bl2 1.e-3 ;
l_temps = t_prog . &b_tra ;
si misopoin ;
 l_ordo = prog 0. 1. 2. ((dime l_temps) - 3)*3. ;
 l_ordo = l_ordo / 1. ;
sinon ;
 l_ordo = prog 0. 1. 2. 2.3 2.6 ((dime l_temps) - 5)* 3. ;
finsi ;
ev_t = evol manu l_temps l_ordo ;
cha1 = char dimp ev_t chdimp1 ;
*
tpas = table ;
tpas . modele = mo1 ;
tpas . caracteristiques = ma1 ;
tpas . chargement = cha1 ;
tpas . blocages_mecaniques = bl1 et bl2 ;
tpas . temps_calcules = prog 0. pas 1. 5. ;
*tpas . temps_calcules = prog 0. 1. 2.;
*
TMASAU=table;
tpas . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
pasapas tpas ;
*
tpas . caracteristiques = ma2 ;
tpas . temps_calcules = prog 6. pas 1. 8. ;
pasapas tpas ;
*
* post-traitement
o_res = prog ;

repeter b_evo (dime tpas . temps) ;
rea1 = reac bl2 tpas . deplacements . (&b_evo-1) ;
si (&b_evo ega 1) ;
 res1 = manu chpo 1 p1 fz 0. ;
sinon ;
 res1 = resu rea1 ;
finsi ;
pe1 = point 1 (extr res1 mail) ;
o_res = o_res et (prog (extr res1 fz pe1)) ;
fin b_evo ;

t_ev . &b_tra = evol manu 'temps(s)' l_temps 'F(1.e3*T)' (o_res/1.e7) ;

fin b_tra ;
*
tabsymb = table ;
tabsymb . titre = table ;
tabsymb . 1 = 'MARQ CROI' ;
tabsymb . titre . 1 = 'taux defo 0.001/s' ;
tabsymb . 2 = 'MARQ LOSA' ;
tabsymb . titre . 2 = 'taux defo 0.01/s' ;
si (neg graph 'N') ;
 si misopoin ;
 dess (t_ev . 1) lege tabsymb
 titre ' visk2  / resultante / traction et relaxation' ;
 sinon ;
 dess (t_ev . 1 et t_ev . 2) lege tabsymb
 titre ' visk2  / resultante / traction et relaxation' ;
 finsi ;
finsi ;
*
si misopoin ;
  si ( abs((extr o_res 6) - 2.41830E+08) > 1.e5) ;
    erre 5 ;
  sinon ;
    erre 0 ;
  finsi ;
fin  ;






