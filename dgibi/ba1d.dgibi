* fichier : ba1d.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
******************************************************
* DESCRIPTION
******************************************************
* Local test - BA1D model
*
* Loading path considered:
*
* - : Cyclic bending
*
******************************************************
* AUTHOR
******************************************************
* Developped by : Benjamin RICHARD
*                 CEA-DEN/DANS/DM2S/SEMT/EMSI
*                 Benjamin.Richard@cea.fr
*
*                 Laurent MOUTOUSSAMY
*                 CEA-DEN/DANS/DM2S/SEMT/EMSI
*                 Laurent.Moutoussamy@cea.fr
******************************************************
graph = 'N';
******************************************************

******************************************************
* OPTIONS
******************************************************
OPTI ELEM SEG2 DIME 2;

******************************************************
* GEOMETRICAL PARAMETERS
******************************************************
DENSITE 1.0;

P1 = 0.0 0.0;
P2 = 0.0 1.0;

L1 = D 1 P1 P2;

CL1 = BLOQ DEPL ROTA P1;
CL2 = BLOQ UX        P2;

******************************************************
* MODEL
******************************************************
MO1 = MODE L1 MECANIQUE ELASTIQUE PLASTIQUE BA1D POUT;

******************************************************
* MATERIAL
******************************************************
MA1 = MATE MO1 YOUN 39.0E3 NU 0.0001 UELA 1.0E-3 FPLA 100
PFIS 0.3  HCIN 10.0 QFRA 0.5 APIH 1.0
BPIH 5.0E10 SECT 1.0 INRZ 1.0 ;

******************************************************
* LOADING
******************************************************
LI1 = PROG 0. 1. 2. 3.;
LI2 = PROG 0. 2.5E-3 0. 2.5E-3;
EV1 = EVOL MANU LI1 LI2;

DEP = DEPI CL2 1.0;
CH1 = CHAR DIMP DEP EV1;

******************************************************
* COMPUTED PSEUDO TIMES
******************************************************
LIC = PROG 0. PAS 0.01 3.0;

******************************************************
* PASAPAS PROCEDURE
******************************************************
TAB1 = TABLE;
TAB1.'BLOCAGES_MECANIQUES' = CL1 ET CL2;
TAB1.'MODELE' = MO1;
TAB1.'CHARGEMENT'=CH1;
TAB1.'CARACTERISTIQUES' = MA1;
TAB1.'TEMPS_CALCULES' = LIC;
TAB1.'MOVA' = ((MOTS DP0) ET (MOTS DM0));

PASAPAS TAB1;

******************************************************
* POST TREATMENT
******************************************************
NN = DIME TAB1.DEPLACEMENTS;
NN = NN - 1;

******************************************************
* DEFORMED MESH
******************************************************
DEF1 = DEFO L1 TAB1.DEPLACEMENTS.NN 0. BLEU;
DEF2 = DEFO L1 TAB1.DEPLACEMENTS.NN    ROUG;

si (ega graph 'O');
TRAC (DEF1 ET DEF2);
finsi;

******************************************************
* LOAD/DISPLACEMENT CURVE
******************************************************
EV2 = @GLOBAL TAB1 EV1 CL2 FX;

si (ega graph 'O');
dess ev2;
finsi;

******************************************************
* IMPLEMENTATION TEST
******************************************************
aa = EXTR EV2 'ORDO';
aaa = EXTR aa 290;

message 'erreur relative';
err = ABS(aaa - 98.981) / 98.981;
list err;

si (> err 1E-3);
erreur(5);
finsi;

FIN;






















