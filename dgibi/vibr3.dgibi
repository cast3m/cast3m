* fichier :  vibr3.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*           Test vibr3.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test vibr3.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
******************************************************************
*                               VIBR3
******************************************************************
*                                                                *
*  Mots-clés : Vibrations, calcul modal, 2D                      *
*                                                                *
*  TEST VIBR3                                                    *
*                                                                *
*  CAS TEST DU 91/06/13     PROVENANCE : TEST                    *
*                                                                *
*  POUTRE DE SECTION VARIABLE : VIBRATION DANS LE PLAN           *
*                                                                *
*  Test NAFEMS numero fv32                                       *
*                                                                *
*      La structure est constituee d'une poutre de section       *
*   variable. Sa longueur est de 10.0 m, sa plus grande largeur  *
*   est de 5.0 m et sa plus petite largeur est de 1.0 m.         *
*                                                                *
*      On s'interesse aux vibrations dans le plan xOy, c'est-a-  *
*   dire que le deplacemnt uz de tous les noeuds est nul. On     *
*   travaille donc en mode plan (dimension 2).                   *
*                                                                *
*      La poutre est bloquee le long de l'axe des ordonnees pour *
*   les deplacements ux et uy.                                   *
*                                                                *
******************************************************************
OPTI DIME 2;
OPTI ELEM QUA8;
OPTI EPSI LINEAIRE;

TEMPS;
*
*================================================================*
*                  geometries et maillages                       *
*================================================================*
*
* Dimensions en metres
*
* Points
*
P1 = 0.  -2.5;
P2 = 0.   2.5;
P3 = 10.  0.5;
P4 = 10. -0.5;
*
* Droites
*
K  = 1 ;
N1 = 4 * K; N2 = 8 * K;
*
D12 = P1 DROI N1 P2;
D23 = P2 DROI N2 P3;
D34 = P3 DROI N1 P4;
D41 = P4 DROI N2 P1;
*
SUR1 = DALL D12 D23 D34 D41 PLAN;
*
* Trace facultatif du maillage
*
SI (NEG GRAPH 'N');
  TITR ' VIBR3 : MAILLAGE';
  TRAC 'QUAL' SUR1;
FINSI;
*
*================================================================*
*      modl- materiau - rigidite - masse                         *
*================================================================*
*
MOD1 = MODE SUR1 MECANIQUE QUA8;
MAT1 = MATE MOD1 YOUN 2.E11 NU 0.3 RHO 8.E3;
RIG1 = RIGI MOD1 MAT1;
MAS1 = MASS MOD1 MAT1;
*
*==============================================================*
*                  conditions aux limites                      *
*==============================================================*
*
CLI1 = BLOQ DEPL D12;
RIG1 = RIG1 ET CLI1;
*
*==============================================================*
*              calcul des frequences propres                   *
*                           et                                 *
*               extraction des resultats                       *
*==============================================================*
*
* Utilisation de l'operateur VIBR.
*
FRE1 = TABLE;
FRE1.1 = 44.623;
FRE1.2 = 130.03;
FRE1.3 = 162.70;
FRE1.4 = 246.05;
FRE1.5 = 379.90;
FRE1.6 = 391.44;
*
RESUL = VIBR INTERVALLE 40 450 BASSE 6 RIG1 MAS1;
*
*==============================================================*
*                   affichache des resultats                   *
*==============================================================*
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
FRE2 = TABLE; MOD = TABLE; DEF = TABLE;ERG = TABLE;
*
I = 0;
REPETER BLOC1 6;
  I = I + 1;
  FRE2.I =  RESUL . MODES . I . FREQUENCE;
  ERG.I = 100 * (ABS ((FRE1.I - FRE2.I) / FRE1.I));
  MESS ' MODE ' I;
  MESS ' ----------';
  MESS ' Frequence theorique :' FRE1.I 'Hz';
  MESS ' Frequence calculee  :' FRE2.I 'Hz';
  MESS '    Soit un ecart de :' ERG.I '%';
  SAUT 1 LIGN;
*
* Extraction et trace des modes propres et calcul des deformees.
*    (facultatif)
  SI (NEG GRAPH 'N');
    MOD.I =  RESUL . MODES . I . DEFORMEE_MODALE;
    DEF0  = DEFO SUR1 0. MOD.I;
    DEF.I = DEFO SUR1 MOD.I BLEU;
    TITR 'VIBR3 : MODES PROPRES';
    TRAC (DEF0 ET DEF.I);
  FINSI;
FIN BLOC1;
*
*==============================================================*
*                   code fonctionnement                        *
*==============================================================*
*
ERGMAX = MAXI (PROG ERG.1 ERG.2 ERG.3 ERG.4 ERG.5 ERG.6);

SI (ERGMAX <EG 5.5);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






