* fichier :  cube.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
SAUT PAGE;
************************************************************************
*     CUBE EN TRACTION UNIAXIALE
*
*     TEST ELEMENTAIRE DU GROUPE DE TRAVAIL 'STATIQUE NON LINEAIRE'
*     COMMISSION VPCS
*
*     LE CALCUL MARCHE ET DONNE EXACTEMENT LES RESULTATS THEORIQUES
*     LE 05/05/90
*
*     Un cube constitu{ de 2 {l{ments CUB8 est soumis @ une traction
*     uniaxiale (blocages des d{placements suivant Y et Z et d{place-
*     ment impos{ suivant X aux deux extr{mit{s (surfaces SU1 et SU2))
*
*     Les contraintes obtenues sont compar{es aux r{sultats th{oriques
*     et donnent les r{sultats pr{cis{s plus bas.
*
*     Ref: Hinton Ezatt .Fundamentals tests for two and three dimensio-
*      -nal small strain, elastoplastic finite element analysis. April
*      1987 NAFEMS
**********************************************************************
opti echo 0;

SAUT PAGE;
TITRE 'CUBE EN TRACTION UNIAXIALE';
OPTION DIME 3 ECHO 0;

graph = 'N';

TEMPS ;
DENS 1;
*-----------------------------------------------------------------------
***** DEFINITION DE LA GEOMETRIE
*-----------------------------------------------------------------------
A = 1.;
A1 = -1.;
A2 = .5;
B = 1.;
C = 1.;
P1 = A 0 0; Q1 = A2 0 0; R1 = 0 0 0;
P2 = A B 0; Q2 = A2 B 0; R2 = 0 B 0;
P3 = A B C; Q3 = A2 B C; R3 = 0 B C;
P4 = A 0 C; Q4 = A2 0 C; R4 = 0 0 C;
TR = A1 0 0;
OPTION ELEM SEG2;
LI1 = P1 DROIT 1 P2;
LI2 = P2 DROIT 1 P3;
LI3 = P3 DROIT 1 P4;
LI4 = P4 DROIT 1 P1;
CO1 = LI1 ET LI2 ET LI3 ET LI4;
LJ1 = Q1 DROIT 1 Q2;
LJ2 = Q2 DROIT 1 Q3;
LJ3 = Q3 DROIT 1 Q4;
LJ4 = Q4 DROIT 1 Q1;
CO2 = LJ1 ET LJ2 ET LJ3 ET LJ4;
LK1 = R1 DROIT 1 R2;
LK2 = R2 DROIT 1 R3;
LK3 = R3 DROIT 1 R4;
LK4 = R4 DROIT 1 R1;
CO3 = LK1 ET LK2 ET LK3 ET LK4;
OPTION ELEM QUA4;
SU1 = SURFACE CO1 PLANE;
SU2 = SURFACE CO2 PLANE;
SU3 = SURFACE CO3 PLANE;
OPTION ELEM CUB8;
VOL1 = SU1 VOLU 1 SU2;
VOL2 = SU2 VOLU 1 SU3;
VOL = VOL1 ET VOL2;
ELIM .001 VOL;
*-----------------------------------------------------------------------
***** TRACE
*-----------------------------------------------------------------------

SI ( NEG graph 'N');
OEIL = (30 20 20);
TRAC OEIL VOL;
finsi;

*-----------------------------------------------------------------------
***** CONDITIONS AUX LIMITES
*-----------------------------------------------------------------------
CL1 = BLOQUE SU3 UX;
CL2 = BLOQUE VOL UY UZ;
CL3 = BLOQUE SU2 UX;
CL4 = BLOQ SU1 UX;
CL = CL1 ET CL2 ET CL3 ET CL4;
*-----------------------------------------------------------------------
* VALEURS IMPOSEES DES DEPLACEMENTS
*-----------------------------------------------------------------------
EPS0 = .25E-4;
COEF1 = A * EPS0 * .5;
COEF2 = A * EPS0;
D1 = DEPI CL3 COEF1;
D2 = DEPI CL4 COEF2;
DEP = D1 ET D2;
*-----------------------------------------------------------------------
***** CALCUL MODELE
*-----------------------------------------------------------------------
MO = MODE VOL 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'PARFAIT' CUB8;

MAT1 = MATE MO YOUN 2.5E11 SIGY 5.0E6 NU .25;

*-----------------------------------------------------------------------
***** CHAMPS RELATIFS A L'OPERATEUR PASAPAS
*-----------------------------------------------------------------------
LP1 = PROG 0. 1. 2. 3. 4. ;
LP2 = PROG 0. 1. 2. 1. 0.;
EV = EVOL MANU T LP1 F(T) LP2;
CHA1 = CHAR 'DIMP' DEP EV;
LIS1 = PROG 1. 2. 3. 4. ;
*-----------------------------------------------------------------------
*****  CALCUL NON LINEAIRE
*-----------------------------------------------------------------------
TAB1 = TABLE;
TAB1.'BLOCAGES_MECANIQUES' = CL;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'MODELE' = MO;
TAB1.'CHARGEMENT' = CHA1;
PASAPAS TAB1;
*-----------------------------------------------------------------------
***** EXTRACTION DES RESULTATS
*-----------------------------------------------------------------------
SI1 = TAB1.CONTRAINTES.1;
SI2 = TAB1.CONTRAINTES.2;
SI3 = TAB1.CONTRAINTES.3;
SI4 = TAB1.CONTRAINTES.4;
SXX1 = EXTR SI1 SMXX 1 1 1;
SYY1 = EXTR SI1 SMYY 1 1 1;
SZZ1 = EXTR SI1 SMZZ 1 1 1;
SXX2 = EXTR SI2 SMXX 1 1 1;
SYY2 = EXTR SI2 SMYY 1 1 1;
SZZ2 = EXTR SI2 SMZZ 1 1 1;
SXX3 = EXTR SI3 SMXX 1 1 1;
SYY3 = EXTR SI3 SMYY 1 1 1;
SZZ3 = EXTR SI3 SMZZ 1 1 1;
SXX4 = EXTR SI4 SMXX 1 1 1;
SYY4 = EXTR SI4 SMYY 1 1 1;
SZZ4 = EXTR SI4 SMZZ 1 1 1;

***** RESULTATS THEORIQUES POUR CHAQUE PAS *****

sigxx1 = 7.5e6; sigxx2 = 11.66e6;  sigxx3 = 4.16e6;
sigyy1 = 2.5e6; sigyy2 =  6.66e6;  sigyy3 = 4.16e6;
sigzz1 = 2.5e6; sigzz2 =  6.66e6;  sigzz3 = 4.16e6;

sigxx4 = -3.33e6;
sigyy4 =  1.66e6;
sigzz4 =  1.66e6;


*-----------------------------------------------------------------------
***** AFFICHAGE DES RESULTATS
*-----------------------------------------------------------------------
SAUT PAGE;
*////////// PARAMETRE D'EVOLUTION  T : 1 //////////';
*-----------------------------------------------------------------------
MESS '                 T = 1 ';
MESS ' RESULTATS THEORIQUES : SIGMAXX =  7.5 E 6' ;
MESS '                        SIGMAYY =  2.5 E 6' ;
MESS '                        SIGMAZZ =  2.5 E 6' ;
MESS '                                          ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX1 ;
MESS '                        SIGMAYY = 'SYY1 ;
MESS '                        SIGMAZZ = 'SZZ1 ;
ergxx1 = abs((sigxx1 -sxx1)/sxx1);
ergyy1 = abs((sigyy1 -syy1)/syy1);
ergzz1 = abs((sigzz1 -szz1)/szz1);

MESS 'ergxx1 =' ergxx1;
MESS 'ergyy1 =' ergyy1;
MESS 'ergzz1 =' ergzz1;
SAUT 1 LIGNE ;
*
*////////// PARAMETRE D'EVOLUTION  T : 2 //////////';
*-----------------------------------------------------------------------
MESS '                 T = 2 ';
MESS ' RESULTATS THEORIQUES : SIGMAXX = 11.66 E 6' ;
MESS '                        SIGMAYY =  6.66 E 6' ;
MESS '                        SIGMAZZ =  6.66 E 6' ;
MESS '                                           ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX2 ;
MESS '                        SIGMAYY = 'SYY2 ;
MESS '                        SIGMAZZ = 'SZZ2 ;

ergxx2 = abs((sigxx2 -sxx2)/sxx2);
ergyy2 = abs((sigyy2 -syy2)/syy2);
ergzz2 = abs((sigzz2 -szz2)/szz2);

MESS 'ergxx2 =' ergxx2;
MESS 'ergyy2 =' ergyy2;
MESS 'ergzz2 =' ergzz2;

SAUT 1 LIGNE ;
*
*////////// PARAMETRE D'EVOLUTION  T : 3 //////////';
*-----------------------------------------------------------------------
MESS '                 T = 3 ' ;
MESS ' RESULTATS THEORIQUES : SIGMAXX =  4.16 E 6' ;
MESS '                        SIGMAYY =  4.16 E 6' ;
MESS '                        SIGMAZZ =  4.16 E 6' ;
MESS '                                           ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX3 ;
MESS '                        SIGMAYY = 'SYY3 ;
MESS '                        SIGMAZZ = 'SZZ3 ;

ergxx3 = abs((sigxx3 -sxx3)/sxx3);
ergyy3 = abs((sigyy3 -syy3)/syy3);
ergzz3 = abs((sigzz3 -szz3)/szz3);

MESS 'ergxx3 =' ergxx3;
MESS 'ergyy3 =' ergyy3;
MESS 'ergzz3 =' ergzz3;


SAUT 1 LIGNE ;
*
*////////// PARAMETRE D'EVOLUTION  T : 4 //////////';
*-----------------------------------------------------------------------
MESS '                 T = 4 ' ;
MESS ' RESULTATS THEORIQUES : SIGMAXX = -3.33 E 6' ;
MESS '                        SIGMAYY =  1.66 E 6' ;
MESS '                        SIGMAZZ =  1.66 E 6' ;
MESS '                                           ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX4 ;
MESS '                        SIGMAYY = 'SYY4 ;
MESS '                        SIGMAZZ = 'SZZ4 ;
MESS '                                  ' ;

ergxx4 = abs((sigxx4 -sxx4)/sxx4);
ergyy4 = abs((sigyy4 -syy4)/syy4);
ergzz4 = abs((sigzz4 -szz4)/szz4);

MESS 'ergxx4 =' ergxx4;
MESS 'ergyy4 =' ergyy4;
MESS 'ergzz4 =' ergzz4;




***** TEST DE FONCTIONNEMENT *****

lise = prog ergxx1 ergyy1 ergzz1 ergxx2 ergyy2 ergzz2
ergxx3 ergyy3 ergzz3 ergxx4 ergyy4 ergzz4 ;

lima = maxi lise;

si ( lima < .05 );
         erre 0;
sinon;
         erre 5;
finsi;

lima = 100*lima;
MESS 'L ECART MAXIMUM EST DE ' lima '%';
temps;
fin;













