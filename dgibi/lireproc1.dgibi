************************************************************************
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************
*
* CAS-TEST DE LA DIRECTIVE : LIRE 'PROC'
*
************************************************************************
*
* TESTS POUR LE FICHIER "MAPROC1"
DEBP VERIF1 TAB1*'TABLE' ;
    SI ((NEG TAB1.'A' 1) OU
        (NEG TAB1.'B' 'JE SUIS DANS MAPROC1')  OU
        (NEG (MAXI TAB1.'C') 7)) ;
        ERRE 5 ;
    FINS ;
FINP ;
*
************************************************************************
*
* TEST 1 = IMPORTATION DES LIGNES CONTENUES DANS LE FICHIER "MAPROC1"
*
LIRE 'PROC' '../divers/MAPROC1' 'FOO' ;
SI (NEG (TYPE FOO) 'PROCEDUR') ;
    ERRE 5 ;
FINS ;
OPTI 'LOCA' VRAI ;
FOO ;
OPTI 'LOCA' FAUX ;
LIST &FOO ;
VERIF1 &FOO ;
*
* TEST 2 = TEST 1 SAUF QUE LE NOM DE LA PROCEDURE EST DEJA AFFECTE
*
BOU = 5 ;
LIRE 'PROC' '../divers/MAPROC1' 'BOU' ;
SI (NEG (TYPE BOU) 'PROCEDUR') ;
    ERRE 5 ;
FINS ;
OPTI 'LOCA' VRAI ;
BOU ;
OPTI 'LOCA' FAUX ;
VERIF1 &BOU ;
*
* TEST 3 = TEST 2 SAUF QUE LIRE 'PROC' EST APPELE DEPUIS UNE PROCEDURE
*
KAI = 5 ;
DEBP BLOB ;
    LIRE 'PROC' '../divers/MAPROC1' 'KAI' ;
    SI (NEG (TYPE KAI) 'PROCEDUR') ;
        ERRE 5 ;
    FINS ;
    OPTI 'LOCA' VRAI ;
    KAI ;
    OPTI 'LOCA' FAUX ;
FINP &KAI ;
TAB1 = BLOB ;
VERIF1 TAB1 ;
* on verifie aussi que KAI etait bien un objet local dans BLOB
SI (NEG KAI 5) ;
    ERRE 5 ;
FINS ;
*
************************************************************************
*
* TESTS POUR LE FICHIER "MAPROC2"
ENTI1 = 5 ;
ENTI2 = -9 ;
SOMM1 = -4 ;
PROD1 = -45 ;
FACT1 = 120 ;
*
************************************************************************
*
* TEST 4 = LECTURE DE TOUTES LES PROCEDURES DE "MAPROC2"
*
LIRE 'PROC' '../divers/MAPROC2' ;
SI ((NEG (TYPE SOMME  ) 'PROCEDUR') OU
    (NEG (TYPE PRODUIT) 'PROCEDUR') OU
    (NEG (TYPE FACTO  ) 'PROCEDUR')) ;
    ERRE 5 ;
FINS ;
SI ((NEG (SOMME   ENTI1 ENTI2) SOMM1) OU
    (NEG (PRODUIT ENTI1 ENTI2) PROD1) OU
    (NEG (FACTO   ENTI1      ) FACT1)) ;
    ERRE 5 ;
FINS ;
************************************************************************
*
* TEST 5 = TEST 4 SAUF QUE LES NOMS DES PROCEDURES SONT DEJA AFFECTES
*
SOMME   = 0 ;
PRODUIT = 0 ;
FACTO   = 0 ;
LIRE 'PROC' '../divers/MAPROC2' ;
SI ((NEG (TYPE SOMME  ) 'PROCEDUR') OU
    (NEG (TYPE PRODUIT) 'PROCEDUR') OU
    (NEG (TYPE FACTO  ) 'PROCEDUR')) ;
    ERRE 5 ;
FINS ;
SI ((NEG (SOMME   ENTI1 ENTI2) SOMM1) OU
    (NEG (PRODUIT ENTI1 ENTI2) PROD1) OU
    (NEG (FACTO   ENTI1      ) FACT1)) ;
    ERRE 5 ;
FINS ;
************************************************************************
*
* TEST 6 = TEST 5 SAUF QUE LIRE 'PROC' EST APPELE DEPUIS UNE PROCEDURE
*
SOMME   = 0 ;
PRODUIT = 0 ;
FACTO   = 0 ;
DEBP BLOB ;
    LIRE 'PROC' '../divers/MAPROC2' ;
    SI ((NEG (TYPE SOMME  ) 'PROCEDUR') OU
        (NEG (TYPE PRODUIT) 'PROCEDUR') OU
        (NEG (TYPE FACTO  ) 'PROCEDUR')) ;
        ERRE 5 ;
    FINS ;
    RES1 = SOMME   ENTI1 ENTI2 ;
    RES2 = PRODUIT ENTI1 ENTI2 ;
    RES3 = FACTO   ENTI1       ;
FINP RES1 RES2 RES3 ;
I1 I2 I3 = BLOB ;
SI ((NEG I1 SOMM1) OU
    (NEG I2 PROD1) OU
    (NEG I3 FACT1)) ;
    ERRE 5 ;
FINS ;
************************************************************************
*
* TEST 7 = TEST 5 SAUF QUE L'ON IMPORTE UNE SEULE PROCEDURE
*
SOMME   = 0 ;
PRODUIT = 0 ;
FACTO   = 0 ;
LIRE 'PROC' '../divers/MAPROC2' 'PRODUIT' ;
SI ((NEG (TYPE SOMME  ) 'ENTIER'  ) OU
    (NEG (TYPE PRODUIT) 'PROCEDUR') OU
    (NEG (TYPE FACTO  ) 'ENTIER'  )) ;
    ERRE 5 ;
FINS ;
SI ((NEG SOMME 0) OU
    (NEG (PRODUIT ENTI1 ENTI2) PROD1) OU
    (NEG FACTO 0)) ;
    ERRE 5 ;
FINS ;
************************************************************************
*
* TESTS POUR LE FICHIER "MAPROC3"
*
************************************************************************
*
* ON ECRASE UNE PROCEDURE DE BASE DE CAST3M (ICI PASAPAS)
LIRE 'PROC' '../divers/MAPROC3' ;
VAL1 = PASAPAS ;
SI (NEG VAL1 123) ;
    ERRE 5 ;
FINS ;


FIN ;





