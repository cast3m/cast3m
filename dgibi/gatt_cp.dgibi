* fichier :  gatt_cp.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test gatt_cp.dgibi: Jeux de données
*     -----------------------------------
*
'OPTI' 'DIME' 2 'MODE' 'PLAN' 'CONT' ;
'OPTI' 'ELEM' 'QUA8'                 ;
'OPTI' 'TRAC' 'PSC'                  ;
'TEMPS' 'ZERO'                       ;
L = 'MOT' LIST ; F = 'MOT' FIN       ;
*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*               MODELE GATT_MONERIE                   *
*  UO2 STANDARD COMPRESSIBLE AVEC COUPLAGE STATIQUE   *
*                                                     *
*  MAILLAGE:                                          *
*     EPROUVETTE RECTANGULAIRE                        *
*                                                     *
*  CHARGEMENT:                                        *
*     DEPLACEMENT VERTICAL IMPOSE                     *
*       TRACTION - COMPRESSION                        *
*     TEMPERATURE CONSTANTE                           *
*     DENSITE DE FISSIONS CONSTANTE                   *
*     PRIMAIRE - MECANISMES 1 ET 2 - IRRADIATION      *
*     DENSIFICATION - GONFLEMENT                      *
*******************************************************
*
GRAPH    = FAUX ;
LISTCOUR = VRAI ;
*
NA   = 1                             ;
NR   = 1                             ;
H    = 6.2e-3                        ;
D    = 5.e-3                         ;
R    = D/2.                          ;
HS2  = H/2.                          ;
*
OO   = 0.              0.            ;
A1   = R               0.            ;
A2   = R               HS2           ;
A3   = 0.              HS2           ;
*
LB   = 'DROIT' NR  OO A1             ;
LD   = 'DROIT' NA  A1 A2             ;
LH   = 'DROIT' NR  A2 A3             ;
LG   = 'DROIT' NA  A3 OO             ;
*
SU1  = 'DALL' LB  LD  LH  LG  'PLAN' ;
*
MODL1= MODE SU1 MECANIQUE ELASTIQUE
  VISCOPLASTIQUE GATT_MONERIE        ;
*
* ----------------------*
* Materiau GATT_MONERIE *
* ----------------------*
TE1  = 1400. + 273.  ;
PO   = 4.52E-2       ;
DF1  = 1.E18         ;
TA = @GATTPAR '../divers/fichier_gatt' ;
*
* fluage primaire rendu significatif pour UO2 standard
 TA.'KPUO'   = 1E3 * TA.'KPUO' ;
*
MATREE = MATE MODL1 'YOUN' (TA.'YOUN') ;
PP = 'MANU' 'CHML' MODL1 'T' TE1 'PORO' PO RIGIDITE ;
EE = 'VARI' 'NUAG' MODL1 MATREE PP ;
*
MATR11 = 'MATE' MODL1  'YOUN' EE  'NU' 0.3  'RHO' 10950.  'ALPH' 0. ;
MATR12 = 'MATE' MODL1
 'R'    (TA.'R')    'DG0' (TA.'DG0') 'DG'  (TA.'DG')
 'K1'   (TA.'K1')   'M1'  (TA.'M1')  'Q1'  (TA.'Q1')  'N1'   (TA.'N1')
 'K2'   (TA.'K2')   'M2'  (TA.'M2')  'Q2'  (TA.'Q2')  'N2'   (TA.'N2')
 'OMEG' (TA.'OMEG') 'H'   (TA.'H')   'Q'   (TA.'Q')   'BETA' (TA.'BETA')
 'K'    (TA.'K')    'A'   (TA.'A')   'Q3'  (TA.'Q3')  'N3'   (TA.'N3')
 'CR'   (TA.'CR')   'CR1' (TA.'CR1') 'CR2' (TA.'CR2') 'CR3'  (TA.'CR3');
*
MATR13 = 'MATE' MODL1  'KP' (TA.'KPUO')   'AP' (TA.'AP')
                       'BP' (TA.'BP')     'QP' (TA.'QP') ;
*
MATR14 = 'MATE' MODL1
 'ADEN' (TA.'ADEN') 'KGON' (TA.'KGON')
 'POR0' (TA.'POR0') 'BUMI' (TA.'BUMI')  'EFIS' (TA.'EFIS') ;
*
* TYPE = 0. combustible UO2            sinon combustible AFA3GLAA
* COMP = 0. combustible incompressible sinon compressible
* DYN  = 0. couplage dynamique         sinon statique
*
MATR1 = MATR11 'ET' MATR12 'ET' MATR13 'ET' MATR14 ;
*
* -----------------------------------------------------*
* Conditions aux limites + deplacement impose sur CLYH *
* -----------------------------------------------------*
CLYB = 'BLOQ' 'UY' LB                  ;
CLYH = 'BLOQ' 'UY' LH                  ;
CLXG = 'BLOQ' 'UX' LG                  ;
CLT  = CLYB 'ET' CLYH 'ET' CLXG        ;
*
TMIL = 1.E6 ;
TFIN = 2.E6 ;
*
RHO0 = 1. - TA.'POR0' ;
AKBU = TA.'EFIS'*270./238./10950./RHO0*DF1 ;
* CE QUI SUIT EST VRAI CAR LE FLUX DE FISSIONS EST CONSTANT
TBU = (TA.'BUMI' / AKBU) - 1. ;
TTBU = ('ENTI' (TBU/10000.)) * 10000. ;
*-------------------*
* Instants calcules *
*-------------------*
'SI' LISTCOUR ;
*--------------
LIST1 = 'PROG' 0   'PAS' 50  300  'PAS' 100  1000 'PAS' 1000  10000
                   'PAS' 10000. TTBU
                   TBU (TTBU+10000.) (TTBU+20000.)
                   'PAS' 10000. TMIL ;
'SINON' ;
*--------
LIST1 = 'PROG' 0   'PAS' 50  300  'PAS' 100  1000 'PAS' 1000  10000
                   'PAS' 10000. TTBU
                   TBU (TTBU+10000.) (TTBU+20000.)
                   'PAS' 10000. TMIL
                   'PAS' 100. (TMIL + ((TFIN - TMIL) / 200.))
                   'PAS' 1000. (TMIL + ((TFIN - TMIL) / 20.))
                   'PAS' 2000. (TMIL + ((TFIN - TMIL) / 4.))
                   'PAS' 10000. TFIN ;
'FINSI' ;
*--------
* --------------------------*
* Chargement en temperature *
* --------------------------*
CHTEMP   = 'MANU' 'CHPO' SU1 1 'T' 1.           ;
EVT      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                         ('PROG' TE1 TE1)       ;
CHARTEMP = 'CHAR' 'T' CHTEMP EVT                ;
* ---------------------------------*
* Chargement en densite de fission *
* ---------------------------------*
CHFISS   = 'MANU' 'CHPO' SU1 1 'DFIS' 1.        ;
EVF      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                    ('PROG' DF1 DF1)            ;
CHARFISS = 'CHAR' 'DFIS' CHFISS EVF             ;
* ---------------------------------*
* Chargement en deplacement impose *
* ---------------------------------*
DEPZ     = 'DEPI' CLYH 1                        ;
DELTAZ   = HS2/200. ;
EVEPS    = 'EVOL' 'MANU'
            ('PROG' 0. TMIL   TFIN (2.*TFIN))
            ('PROG' 0. DELTAZ 0.    0.)         ;
CHARMECA = 'CHAR' 'DIMP' DEPZ EVEPS             ;
*
CHARTOT  = CHARMECA 'ET' CHARTEMP 'ET' CHARFISS ;
* ---------------------------------------*
* Variables internes initiales 'PORO'=PO *
* ---------------------------------------*
VAR00 = 'ZERO' MODL1 'VARINTER'                                    ;
VAR01 = 'MANU' 'CHML' MODL1 'PORO' PO
                            'TYPE' 'VARIABLES INTERNES' 'STRESSES' ;
VAR0  = VAR00 + VAR01                                              ;
*
*
TAB1 = TABLE                           ;
TAB1.'TEMPERATURES' = TABLE            ;
TAB1.'VARIABLES_INTERNES'= TABLE       ;
TAB1.'BLOCAGES_MECANIQUES' = CLT       ;
TAB1.'MODELE' = MODL1                  ;
TAB1.'CHARGEMENT' = CHARTOT            ;
TAB1.'TEMPERATURES' . 0 = CHTEMP       ;
TAB1.'VARIABLES_INTERNES' . 0 = VAR0   ;
TAB1.'CARACTERISTIQUES' = MATR1        ;
TAB1.'TEMPS_CALCULES' = LIST1          ;
TAB1.'TEMPS_SAUVES' = LIST1            ;
*
TMASAU                   = TABLE       ;
tab1 . 'MES_SAUVEGARDES' = TMASAU      ;
TMASAU .'DEFTO' = VRAI                 ;
TMASAU .'DEFIN' = VRAI                 ;
*
PASAPAS TAB1                           ;
*
*
AP    = TA.'AP'        ;
BP    = TA.'BP'        ;
N1    = TA.'N1'        ;
N2    = TA.'N2'        ;

* Facteurs multiplicatifs dus au dopage par le Chrome
WC1 = 'TANH' ( (TA.'CR' - TA.'CR2') / TA.'CR3' ) ;
WC1 = 1. + ( 0.5 * TA.'CR1' * (1. + WC1) )       ;
CV  = 180. / PI                                  ;
WC2 = 1. - ( 'COS' (CV * TA.'DG' / TA.'DG0') )   ;
WC2 = 2. * (TA.'DG0'**TA.'M2') * WC2             ;

BUMI = TA.'BUMI' ;
AKEVD = AKBU*TA.'KGON' ;
ADEN = TA.'ADEN' ;
KGON = TA.'KGON' ;
* -------------------------------------------------------------------*
* Calcul de la cte AAAA intervenant ds le calcul de la def. de dens. *
* -------------------------------------------------------------------*
BUMAX0=60.D0*BUMI ;
CRIT=1.D-10 ;
'REPE' BLOC 100 ;
  BUMAX = BUMI* ('EXP' (1. - (ADEN/(KGON*BUMAX0)))) ;
  BUMAX = (0.2*BUMAX) + (0.8*BUMAX0) ;
  TEST='ABS' ((BUMAX-BUMAX0)/BUMAX0) ;
  'SI' ('<' TEST CRIT) ;
    'QUIT' BLOC ;
  'FINS' ;
  BUMAX0=BUMAX ;
'FIN' BLOC ;
AAAA = (-1.D0*RHO0*(ADEN-(KGON*BUMAX))) /
                ((1.D0+ADEN)*(LOG(BUMAX/BUMI)))  ;
*
* -----------------------*
* Controle des resultats *
* -----------------------*
SS    = TAB1 . 'CONTRAINTES'                ;
VV    = TAB1 . 'VARIABLES_INTERNES'         ;
IN    = TAB1 . 'DEFORMATIONS_INELASTIQUES'  ;
NCONT = ('DIME' (TAB1 . 'CONTRAINTES')) - 1 ;
*
T1     = 0. ;
SMYY1  = 0. ;
FF1    = TA.'POR0' ;
ERMAX1 = 0. ;
ERMAX2 = 0. ;
*
LTT     = 'PROG' ;
LDFF    = 'PROG' ;
LEVFF   = 'PROG' ;
LEVP2   = 'PROG' ;
LEVTH   = 'PROG' ;
LEVCALC = 'PROG' ;
LSMYY   = 'PROG' ;
*
'REPE' BLOC NCONT ;
*
* -------------------------------------
* VITESSE DE DEFORMATION THEORIQUE EVTH
* -------------------------------------
  T2   = 'EXTR' (TAB1.'TEMPS_SAUVES') (&BLOC + 1);
  EVTH = DELTAZ / HS2 / TMIL                     ;
  'SI' (> T2 (TMIL+0.1))                         ;
    EVTH = -1. * DELTAZ / HS2 / (TFIN - TMIL)    ;
  'FINS'                                         ;
* ----------------------------------------------------
* SMYY EST PREPONDERANTE DEVANT LES AUTRES CONTRAINTES
* ----------------------------------------------------
  SMYY = 'MAXI' ('EXCO' (SS . &BLOC) 'SMYY') ;
  SSM  = SMYY / 3.                           ;
  SSEQ = 'ABS' SMYY                          ;
* Deviateur des contraintes
  SSPRIM1 = -1. * SMYY / 3.                  ;
  SSPRIM2 = 2. * SMYY / 3.                   ;
  SSPRIM3 = -1. * SMYY / 3.                  ;
* ---------------------------------------------
* DEFORMATION VISCO-PLASTIQUE EQUIVALENTE EVPEQ
* ---------------------------------------------
  EVXX  = 'MAXI' ('EXCO' IN.&BLOC 'EIXX')                           ;
  EVYY  = 'MAXI' ('EXCO' IN.&BLOC 'EIYY')                           ;
  EVZZ  = 'MAXI' ('EXCO' IN.&BLOC 'EIZZ')                           ;
  EVH   = (EVXX+EVYY+EVZZ) / 3.                                     ;
  EVPEQ = ((EVXX - EVH)**2) + ((EVYY - EVH)**2) + ((EVZZ - EVH)**2) ;
  EVPEQ = ((2./3.) * EVPEQ)**0.5                                    ;
* ------------------------------------------
* VITESSE DE DEFORMATION VISCO-PLASTIQUE EVP
* ------------------------------------------
*-- fluage primaire
*
'SI' ('<' EVPEQ 1E-10) ; EVPEQ = 1E-10 ; 'FINSI' ;
*
  EVP01 = 1.5 * TA.'KPUO' *
  ('EXP' (-1.*TA.'QP'/(TA.'R'*TE1))) *
  ( (SSEQ**(AP-1)) / (EVPEQ**BP) ) * SSPRIM1 ;
*
  EVP02 = 1.5 * TA.'KPUO' *
  ('EXP' (-1.*TA.'QP'/(TA.'R'*TE1))) *
  ( (SSEQ**(AP-1)) / (EVPEQ**BP) ) * SSPRIM2 ;
*
  EVP03 = 1.5 * TA.'KPUO' *
  ('EXP' (-1.*TA.'QP'/(TA.'R'*TE1))) *
  ( (SSEQ**(AP-1)) / (EVPEQ**BP) ) * SSPRIM3 ;
*
*-- fluage secondaire (2 mecanismes)
*
  PP = 'MAXI' ('EXCO' VV . &BLOC 'PORO') ;
*
  A1  = (N1* (PP**(-1./N1) - 1.)**(-2.*N1/(N1+1.)))      ;
  B1  = (1. + (2.*PP/3.))/ ((1. - PP) **(2.*N1/(N1+1.))) ;
  AB1 = (9. * A1) / ( (4.*B1) + A1 )                     ;
  BA1 = B1 / ( B1 + (A1/4.) )                            ;
*
  GSSEQ = ((BA1 * (SSEQ**2)) + (AB1 * (SSM**2))) ** 0.5  ;
*
  A2 = (N2* (PP**(-1./N2) - 1.)**(-2.*N2/(N2+1.))) ;
  B2 = (1. + (2.*PP/3.))/ ((1. - PP) **(2.*N2/(N2+1.))) ;
*
  EVP11 = 0.5 * (TA.'K1' * (TA.'DG'**TA.'M1') * WC1 *
  ('EXP' (-1.*TA.'Q1'/(TA.'R'*TE1))) *
  (((A1*((1.5*SSM)**2)) + (B1*(SSEQ ** 2)))**((N1-1.)/2.)) *
  ((A1*1.5*SSM) + (3.*B1*SSPRIM1))) ;
*
  EVP21 = 0.5 * (TA.'K2' * WC2 * ('EXP' (-1.*TA.'Q2'/(TA.'R'*TE1))) *
  (((A2*((1.5*SSM)**2)) + (B2*(SSEQ ** 2)))**((N2-1.)/2.)) *
  ((A2*1.5*SSM) + (3.*B2*SSPRIM1))) ;
*
  EVP12 = 0.5 * (TA.'K1' * (TA.'DG'**TA.'M1') * WC1 *
  ('EXP' (-1.*TA.'Q1'/(TA.'R'*TE1))) *
  (((A1*((1.5*SSM)**2)) + (B1*(SSEQ ** 2)))**((N1-1.)/2.)) *
  ((A1*1.5*SSM) + (3.*B1*SSPRIM2))) ;
*
  EVP22 = 0.5 * (TA.'K2' * WC2 * ('EXP' (-1.*TA.'Q2'/(TA.'R'*TE1))) *
  (((A2*((1.5*SSM)**2)) + (B2*(SSEQ ** 2)))**((N2-1.)/2.)) *
  ((A2*1.5*SSM) + (3.*B2*SSPRIM2))) ;
*
  EVP13 = 0.5 * (TA.'K1' * (TA.'DG'**TA.'M1') * WC1 *
  ('EXP' (-1.*TA.'Q1'/(TA.'R'*TE1))) *
  (((A1*((1.5*SSM)**2)) + (B1*(SSEQ ** 2)))**((N1-1.)/2.)) *
  ((A1*1.5*SSM) + (3.*B1*SSPRIM3))) ;
*
  EVP23 = 0.5 * (TA.'K2' * WC2 * ('EXP' (-1.*TA.'Q2'/(TA.'R'*TE1))) *
  (((A2*((1.5*SSM)**2)) + (B2*(SSEQ ** 2)))**((N2-1.)/2.)) *
  ((A2*1.5*SSM) + (3.*B2*SSPRIM3))) ;
*
  PSI1 = TA.'K1' * WC1 * (TA.'DG'**TA.'M1')/(N1+1.) *
  ('EXP' (-1.*TA.'Q1'/(TA.'R'*TE1))) *
  (((A1*((1.5*SSM)**2)) + (B1*(SSEQ ** 2)))**((N1+1.)/2.)) ;
*
  PSI2 = TA.'K2' * WC2/(N2+1.) * ('EXP' (-1.*TA.'Q2'/(TA.'R'*TE1))) *
  (((A2*((1.5*SSM)**2)) + (B2*(SSEQ ** 2)))**((N2+1.)/2.)) ;
*
  EVPC1 = 0.5 * TA.'BETA' / TA.'H' * TA.'Q' * TA.'OMEG' *
  (GSSEQ**(-1.*TA.'Q' - 2.)) *
      (1. -
  (('TANH' ((TE1 - (TA.'OMEG' * (GSSEQ**(-1.*TA.'Q'))))  / TA.'H'))**2))
   * (PSI2 - PSI1) * ( ((AB1*SSM)/3.) +  (1.5*BA1*SSPRIM1) ) ;
*
  EVPC2 = 0.5 * TA.'BETA' / TA.'H' * TA.'Q' * TA.'OMEG' *
  (GSSEQ**(-1.*TA.'Q' - 2.)) *
      (1. -
  (('TANH' ((TE1 - (TA.'OMEG' * (GSSEQ**(-1.*TA.'Q'))))  / TA.'H'))**2))
   * (PSI2 - PSI1) * ( ((AB1*SSM)/3.) +  (1.5*BA1*SSPRIM2) ) ;
*
  EVPC3 = 0.5 * TA.'BETA' / TA.'H' * TA.'Q' * TA.'OMEG' *
  (GSSEQ**(-1.*TA.'Q' - 2.)) *
      (1. -
  (('TANH' ((TE1 - (TA.'OMEG' * (GSSEQ**(-1.*TA.'Q'))))  / TA.'H'))**2))
   * (PSI2 - PSI1) * ( ((AB1*SSM)/3.) +  (1.5*BA1*SSPRIM3) ) ;
*
  TETA = 0.5 * TA.'BETA' *
      (1. +
      ('TANH' ((TE1 - (TA.'OMEG' * (GSSEQ**(-1.*TA.'Q')))) / TA.'H'))) ;
*
  EVP1 = EVP01 + ((1. - TETA) * EVP11) + (TETA * EVP21) + EVPC1 ;
  EVP2 = EVP02 + ((1. - TETA) * EVP12) + (TETA * EVP22) + EVPC2 ;
  EVP3 = EVP03 + ((1. - TETA) * EVP13) + (TETA * EVP23) + EVPC3 ;
* -------------------------------------
* VITESSE DE DEFORMATION ELASTIQUE EVEL
* -------------------------------------
  DSMYY = (SMYY - SMYY1) / (T2 - T1) ;
  EVEL  = DSMYY / ('MAXI' EE)        ;
* ----------------------------------
* VITESSE DE DEFORMATION IRRADIATION
* ----------------------------------
  EVIR1 = 1.5 * TA.'A' * DF1 * (SSEQ **(TA.'N3' - 1.)) *
       ('EXP' (-1.*TA.'Q3'/(TA.'R'*TE1))) * SSPRIM1 ;
  EVIR2 = 1.5 * TA.'A' * DF1 * (SSEQ **(TA.'N3' - 1.)) *
       ('EXP' (-1.*TA.'Q3'/(TA.'R'*TE1))) * SSPRIM2 ;
  EVIR3 = 1.5 * TA.'A' * DF1 * (SSEQ **(TA.'N3' - 1.)) *
       ('EXP' (-1.*TA.'Q3'/(TA.'R'*TE1))) * SSPRIM3 ;
* ------------------------------------
* VITESSE DE DEFORMATION DENSIFICATION
* ------------------------------------
* CE QUI SUIT EST VRAI CAR LE FLUX DE FISSIONS EST CONSTANT
  BU = AKBU * T2 ;
  'SI' (BU '>' BUMI) ;
    NUMER = (RHO0 * RHO0 * AKEVD) - (RHO0 * AAAA * AKEVD) +
            (RHO0 * AAAA * AKEVD * ('LOG' (BU / BUMI))) -
            (RHO0 * AAAA / T2) ;
    DENOM = (RHO0 + (AAAA * ('LOG' (BU / BUMI)))) *
            (RHO0 + (AAAA * ('LOG' (BU / BUMI)))) ;
    EVD = -1. * (AKEVD - (NUMER/DENOM)) / 3. ;
  'SINO' ;
    EVD = 0. ;
  'FINS' ;
* ---------------------------------
* VITESSE DE DEFORMATION GONFLEMENT
* ---------------------------------
* CE QUI SUIT EST VRAI CAR LE FLUX DE FISSIONS EST CONSTANT
  EVG = AKEVD/3. ;
* -------------------------------
* VITESSE DE DEFORMATION SELON YY
* -------------------------------
  EVCALC = ((1.+(TA.'K'*DF1))*EVP2) + EVEL + EVIR2 +
                       EVD + EVG ;
  ECART1 = (EVCALC - EVTH) / EVTH ;
  ERMAX1 = 'MAXI' ('PROG' ('ABS' ECART1) ERMAX1) ;
* -------------------
* VITESSE DE POROSITE
* -------------------
  FF = 'MAXI' ('EXCO' (VV . &BLOC) 'PORO') ;
  DFF = (FF - FF1) / (T2 - T1) ;
  EVFF = (3.*EVD) +
         ((1. - FF) * (1.+(TA.'K'*DF1))*(EVP1+EVP2+EVP3)) +
         ((1. - FF) * (EVIR1 + EVIR2 + EVIR3)) ;
  ECART2 = ('ABS' (DFF - EVFF)) / FF ;
  ERMAX2 = 'MAXI' ('PROG' ('ABS' ECART2) ERMAX2) ;
*
  LTT     = LTT     'ET' ('PROG' T2)     ;
  LEVP2   = LEVP2   'ET' ('PROG' EVP2)   ;
  LEVCALC = LEVCALC 'ET' ('PROG' EVCALC) ;
  LEVTH   = LEVTH   'ET' ('PROG' EVTH)   ;
  LDFF    = LDFF    'ET' ('PROG' DFF)    ;
  LEVFF   = LEVFF   'ET' ('PROG' EVFF)   ;
  LSMYY   = LSMYY   'ET' ('PROG' SMYY)   ;
*
  FF1   = FF   ;
  SMYY1 = SMYY ;
  T1    = T2   ;
*
'FIN' BLOC ;
*
'SI' ( ERMAX1 '<EG' 0.05 );
   'ERRE' 0;
'SINO';
   'MESS' 'ERREUR MAXIMALE VITESSE DEFORMATION :'
           ERMAX1 '> 0.05 ' ;
   'ERRE' 5;
'FINS';
*
'SI' ( ERMAX2 '<EG' 0.05 );
   'ERRE' 0;
'SINO';
   'MESS' 'ERREUR MAXIMALE POROSITE :'
           ERMAX2 '> 0.05 ' ;
   'ERRE' 5;
'FINS';
*
'SI' GRAPH ;
  'TITR' 'Contraintes planes : Vitesses de Deformation' ;
  EVEVP  = 'EVOL' 'MANU' LTT 'Temps'  LEVP2   'Vitesse' ;
  EVEVC  = 'EVOL' 'MANU' LTT 'Temps'  LEVCALC 'Vitesse' ;
  EVEVTH = 'EVOL' 'MANU' LTT 'Temps'  LEVTH   'Vitesse' ;
  TAD     = TABLE                                       ;
  TAD . 1 = 'TIRC'                                      ;
  TAD . 3 = 'TIRR'                                      ;
  TAD . 'TITRE'     = TABLE                             ;
  TAD . 'TITRE' . 1 = 'Viscoplastique Analytique'       ;
  TAD . 'TITRE' . 2 = 'Totale Calculee'                 ;
  TAD . 'TITRE' . 3 = 'Totale Analytique'               ;
  'DESS' (EVEVP 'ET' EVEVC 'ET' EVEVTH) TAD 'LEGE'      ;
*
  'TITR' 'Contraintes planes : Vitesses de variation de la porosite' ;
  EVDFF  = 'EVOL' 'MANU' LTT 'Temps' LDFF 'Vitesse'                  ;
  EVEVFF = 'EVOL' 'MANU' LTT 'Temps' LEVFF 'Vitesse'                 ;
  TAF     = TABLE                                                    ;
  TAF . 2 = 'TIRR'                                                   ;
  TAF . 'TITRE'     = TABLE                                          ;
  TAF . 'TITRE' . 1 = 'Calculee'                                     ;
  TAF . 'TITRE' . 2 =  'Analytique'                                  ;
  'DESS' (EVDFF 'ET' EVEVFF) TAF 'LEGE'                              ;
*
  'TITR' 'Contraintes planes : Contraintes'                ;
  EVSMYY = 'EVOL' 'MANU' LTT 'Temps'   LSMYY 'Contrainte'  ;
  'DESS' EVSMYY                                            ;
'FINS' ;
*
'FIN';








