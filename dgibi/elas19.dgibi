* fichier :  elas19.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
* CAS TEST DU 93/11/4
SAUT PAGE;
OPTI TRAC BENS;
************************************************************
*
*                   TEST ELAS19
*
*     modelisation de la section rectangulaire d'une poutre
*  soumise a 2 moments de flexion mfx et mfy et a un effort
*  normal
*    le calcul est effectue en deformation plane generalisee
*    on recherche la contrainte en un point p8 de la section
*  on compare ensuite au resultat theorique
*
************************************************************
*
OPTI ECHO 1;
*
OPTI DIME 2 ELEM QUA8 MODE PLAN DEFO ;
opti epsilon lineaire;
*
GRAPH = 'N';
*
*===========================================================
* geometrie : maillage
*===========================================================
*
* dimension en metres
*
* points
*
P1 = 1. -2.;
P2 = 1. 2.;
P3 = -1. 2.;
P4 = -1. -2.;
*
* droite et lignes
*
* coefficient de finesse du maillage
*
N = 6;
M = 4;
*
D1 = P1 D N P2 ;
D2 = P2 D M P3 ;
D3 = P3 D N P4 ;
D4 = P4 D M P1 ;
*
* maillage
*
SURF1 = DALL D1 D2 D3 D4;
*
SI (NEG GRAPH 'N');
   TITR 'PLANGENE1 : MAILLAGE';
   TRAC QUAL SURF1;
FINSI;
*
* on definit le point support de la deformation
* plane generalisee
*
PG = 0. 0.;
OPTI MODE PLAN GENE ;
*
ELSURF1 = NBEL SURF1;
NOSURF1 = NBNO SURF1;
*
*
*===========================================================
* modele - materiau
* rigidite
*===========================================================
*
MO = MODE SURF1 MECANIQUE ELASTIQUE QUA8 DPGE PG;
MA = MATE MO YOUN 2E11 NU 0.3;
RI1 = RIGI MO MA;
*
*===========================================================
* bloquages - conditions aux limites
*===========================================================
*
P5 = SURF1 POINT PROC (0. 2.);
P6 = SURF1 POINT PROC (0. 0.);
B1 = BLOQ DEPL P6;
B2 = BLOQ UX P5;
RI = RI1 ET B1 ET B2;
*
*===========================================================
* forces
*===========================================================
*
F1 = FORC FZ 1E10 PG;
F2 = MOME MX 1E9 PG;
F3 = MOME MY 1E8 PG;
F = F1 ET F2 ET F3;
*
*===========================================================
* resolution : champs de deplacement
*===========================================================
*
DEP1 = RESO RI F;
*
*===========================================================
* extraction d'une contrainte
* et comparaison avec solution analytique
*===========================================================
*
* calcul du champs de contraintes totales
*
SIG1 = SIGM MO MA DEP1;
*
SI (NEG GRAPH 'N');
   SZZ1 = EXCO SMZZ SIG1;
   TITR 'PLANGENE1 : CONTRAINTES NORMALES';
   TRAC QUAL SZZ1 MO SURF1;
FINSI;
*
CHAM1 = CHAN NOEUD MO SIG1;
CHPO1 = CHAN CHPO MO CHAM1;
*
* extraction d'une contrainte au point P8
* et comparaison avec la solution analytique
*
P8 = SURF1 POINT PROC (.5 .5);
*
SMZZC = EXTR CHPO1 SMZZ P8;
*
* calcul de la contrainte theorique
*
X2 = COOR 1 P8;
X3 = COOR 2 P8;
FNORM = 1E10;
MFX = 1E9;
MFY = 1E8;
SMZZT =(FNORM/8)-(X2*MFY/2.6666666666666) +(X3*MFX/10.6666666666666);
*
* calcul de l'ecart avec la solution de reference
*
ERGZZC = 100*(ABS((SMZZT-SMZZC)/SMZZT));
*
*==========================================================
* affichage des resultats
*==========================================================
*
MESS ' RESULTATS : TEST PLANGENE ';
MESS '---------------------------';
SAUT 2 LIGN;
*
MESS 'on cherche la contrainte en un point du maillage';
MESS 'elle est comparee a une valeur obtenue analytiquement en ce même point';
SAUT 1 LIGN;
MESS ' Contrainte normale theorique :' SMZZT 'MPa';
SAUT 1 LIGN;
MESS ' Contrainte normale calculee :' SMZZC 'MPa';
SAUT 1 LIGN;
MESS '          Soit un ecart de :' ERGZZC '%';
SAUT 2 LIGN;
*
MESS '    Nombre d elements :' ELSURF1;
MESS '    Nombre de noeuds  :' NOSURF1;
MESS '       soit ' (NOSURF1 * 3) 'd.d.l.';
*==================================================================*
*
*==================================================================
*                  code fonctionnement
*==================================================================
*
*     L'ecart maximum entre valeur theorique et calculee doit etre
* inferieure a 0.0005%.
*
SAUT 2 LIGN;
SI (ERGZZC <EG 0.0005);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*==================================================================
*                 Temps de calcul et fin
*==================================================================
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;
*



















