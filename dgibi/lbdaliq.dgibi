*
*
* Conductivité thermique de l'eau
* Valeurs de référence : tables VDI
* Comparaisons de différentes formules et tests de 'VARI' 'LBDALIQ'
*
*
'OPTI' 'DIME' 2 'ELEM' 'QUA4' 'TRAC' 'PSC' 'EPTR' 10 'DENS' 1. ;
graph = faux ;
*
*- Calcul de la conductivité thermique de l'eau (formule Cathare)
*
'DEBP' lambdak TW0*'LISTREEL' PW0*'LISTREEL' ;
*
UN  = TW0 / TW0 ;
PS1 = 'VARI' 'PSATT' TW0 ;
HL1 = 'VARI' 'HLS'   PW0 TW0 ;
*
a0 = 0.573738622   ;
a1 = 0.2536103551  ;
a2 = -0.145468269   ;
a3 = 0.01387472485 ;
*
x  = 1.7196904D-6 * HL1 ;
L1 = (a0 * un) + (a1 * x) + (a2 * x * x) + (a3 * x * x * x) ;
*
'FINP' L1 ;
*
*- Calcul de la conductivité thermique de l'eau (formule VDI)
*
'DEBP' lambdav TW0*'LISTREEL' PW0*'LISTREEL' ;
*
UN  = TW0 / TW0 ;
HL1 = 'VARI' 'HLS'   PW0 TW0 ;
*
a0 = -0.92247   ;
a1 =  6.728934102  ;
a2 =-10.11230521 ;
a3 =  6.996953832 ;
a4 = -2.316062510 ;
*
b0 =-0.20954276 ;
b1 = 1.320227345 ;
b2 =-2.485904388 ;
b3 = 1.517081933 ;
*
c0 = 0.08104183147 ;
c1 =-0.4513858027 ;
c2 = 0.8057261332 ;
c3 =-0.4668315566 ;
*
PC0 = 221.2D5 * UN ;
TC0 = (374.15 + 273.15) * UN ;
BETA = PW0 / PC0 ;
TETA = TW0 / TC0 ;
PS1 = 'VARI' 'PSATT' TW0 ;
BETAS = PS1 / PC0 ;
*
L1 = (a0*un) + (a1 * TETA) + (a2 * TETA * TETA)
   + (a3 * TETA * TETA * TETA) + (a4 * TETA * TETA * TETA * TETA) ;
L2 = (b0*un) + (b1 * TETA) + (b2 * TETA * TETA)
   + (b3 * TETA * TETA * TETA) ;
L3 = (c0*un) + (c1 * TETA) + (c2 * TETA * TETA)
   + (c3 * TETA * TETA * TETA) ;
L4 = BETA - BETAS ;
L5 = L1 + (L4*L2) + (L4*L4*L3) ;
*
'FINP' L5 ;
*
* Température et pression
*
TW1 = ('PROG' 20. 40. 60. 80. 90.) + 273.15 ;
UN  = TW1 / TW1 ;
PW1 = 1.D5 * UN ;
LREF1 = 'PROG' 0.604 0.632 0.654 0.670 0.676 ;
L10 = LAMBDAK TW1 PW1 ;
L11 = LAMBDAV TW1 PW1 ;
*
TW2 = ('PROG' 10. 30. 50. 100. 130. 150. 170.) + 273.15 ;
UN  = TW2 / TW2 ;
PW2 = 10.D5 * UN ;
LREF2 = 'PROG' 0.588 0.619 0.644 0.681 0.688 0.687 0.681 ;
L20 = LAMBDAK TW2 PW2 ;
L21 = LAMBDAV TW2 PW2 ;
*
TW3 = ('PROG' 10. 50. 100. 150. 200. 220.) + 273.15 ;
UN  = TW3 / TW3 ;
PW3 = 25.D5 * UN ;
LREF3 = 'PROG' 0.589 0.645 0.682 0.688 0.665 0.649 ;
L30 = LAMBDAK TW3 PW3 ;
L31 = LAMBDAV TW3 PW3 ;
*
* Test de VARI avec T sous forme de FLOTTANT
'DEBP' testf p*'LISTREEL' t*'LISTREEL' ;
   nb     = 'DIME' t ;
   listlb = 'PROG' ;
   'REPE' blo1 nb ;
      valp   = 'EXTR' p &blo1 ;
      valt   = 'EXTR' t &blo1 ;
      vallb  = 'VARI' 'LBDALIQ' valp valt ;
      listlb = listlb 'ET' ('PROG' vallb) ;
   'FIN' blo1 ;
'FINP' listlb ;
test_f1 = testf pw1 tw1 ;
test_f2 = testf pw2 tw2 ;
test_f3 = testf pw3 tw3 ;
*
* Test de VARI avec T sous forme de LISTREEL
test_l1 = vari 'LBDALIQ' pw1 tw1 ;
test_l2 = vari 'LBDALIQ' pw2 tw2 ;
test_l3 = vari 'LBDALIQ' pw3 tw3 ;
*
* Test de VARI avec T sous forme de CHPO
'DEBP' testc p*'CHPOINT' t*'CHPOINT' mail1*'MAILLAGE' ;
   test1  = 'VARI' 'LBDALIQ' p t ;
   listlb = 'EXTR' test1 'VALE' mail1 ;
'FINP' listlb ;
P1 = 1. 0. ; P2 = 2. 0. ;
GEO1 = P1 'DROI' 4 P2 ; PT1 = 'CHAN' 'POI1' GEO1 ;
GEO2 = P1 'DROI' 6 P2 ; PT2 = 'CHAN' 'POI1' GEO2 ;
GEO3 = P1 'DROI' 5 P2 ; PT3 = 'CHAN' 'POI1' GEO3 ;
T1 = 'MANU' 'CHPO' PT1 'SCAL' TW1 ;
T2 = 'MANU' 'CHPO' PT2 'SCAL' TW2 ;
T3 = 'MANU' 'CHPO' PT3 'SCAL' TW3 ;
P1 = 'MANU' 'CHPO' PT1 'SCAL' PW1 ;
P2 = 'MANU' 'CHPO' PT2 'SCAL' PW2 ;
P3 = 'MANU' 'CHPO' PT3 'SCAL' PW3 ;
test_c1 = testc p1 t1 pt1 ;
test_c2 = testc p2 t2 pt2 ;
test_c3 = testc p3 t3 pt3 ;
*
* Comparaisons mise en oeuvre Esope/Gibi pour Cathare
d10 = 'EVOL'        'MANU' tw1 (l10 - test_L1 / l10 * 100.) ;
d20 = 'EVOL' 'VERT' 'MANU' tw2 (l20 - test_l2 / l20 * 100.) ;
d30 = 'EVOL' 'ROUG' 'MANU' tw3 (l30 - test_l3 / l30 * 100.) ;
*
*-------------------------------------
*- Sorties Graphiques
*-------------------------------------
*
'SI' graph ;
*
ev10   = 'EVOL'        'MANU' tw1 l10 ;
ev20   = 'EVOL' 'VERT' 'MANU' tw2 l20 ;
ev30   = 'EVOL' 'ROUG' 'MANU' tw3 l30 ;
ev11   = 'EVOL'        'MANU' tw1 l11 ;
ev21   = 'EVOL' 'VERT' 'MANU' tw2 l21 ;
ev31   = 'EVOL' 'ROUG' 'MANU' tw3 l31 ;
evr1   = 'EVOL'        'MANU' tw1 lref1 ;
evr2   = 'EVOL' 'VERT' 'MANU' tw2 lref2 ;
evr3   = 'EVOL' 'ROUG' 'MANU' tw3 lref3 ;
rela10 = 'EVOL'        'MANU' tw1 (l10 - lref1 / lref1 * 100.) ;
rela20 = 'EVOL' 'VERT' 'MANU' tw2 (l20 - lref2 / lref2 * 100.) ;
rela30 = 'EVOL' 'ROUG' 'MANU' tw3 (l30 - lref3 / lref3 * 100.) ;
rela11 = 'EVOL'        'MANU' tw1 (l11 - lref1 / lref1 * 100.) ;
rela21 = 'EVOL' 'VERT' 'MANU' tw2 (l21 - lref2 / lref2 * 100.) ;
rela31 = 'EVOL' 'ROUG' 'MANU' tw3 (l31 - lref3 / lref3 * 100.) ;
*
TAB1 = TABLE ;
TAB1 . 'TITRE' = TABLE ;
TAB1 .  1 = ' MARQ PLEIN ROND NOLI' ;
TAB1 .  2 = ' MARQ PLEIN CARR NOLI' ;
TAB1 .  3 = ' MARQ PLEIN LOSA NOLI' ;
TAB1 .  4 = ' MARQ ROND NOLI' ;
TAB1 .  5 = ' MARQ CARR NOLI' ;
TAB1 .  6 = ' MARQ LOSA NOLI' ;
*
TIT1 = 'Thermal conductivity of water (Cathare)' ;
TIT2 = ' C' ;
TIT3 = 'W/m2/K' ;
TAB1 . 'TITRE' .  1 = 'MOT' ' 1 bar' ;
TAB1 . 'TITRE' .  2 = 'MOT' '10 bar' ;
TAB1 . 'TITRE' .  3 = 'MOT' '25 bar' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'ref  1' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'ref 10' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'ref 25' ;
'DESS' (ev10 'ET' ev20 'ET' ev30 'ET' evr1 'ET' evr2 'ET' evr3)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Thermal conductivity of water (VDI)' ;
TIT2 = ' C' ;
TIT3 = 'W/m2/K' ;
TAB1 . 'TITRE' .  1 = 'MOT' ' 1 bar' ;
TAB1 . 'TITRE' .  2 = 'MOT' '10 bar' ;
TAB1 . 'TITRE' .  3 = 'MOT' '25 bar' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'ref  1' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'ref 10' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'ref 25' ;
'DESS' (ev11 'ET' ev21 'ET' ev31 'ET' evr1 'ET' evr2 'ET' evr3)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Thermal conductivity of water. Relative error (x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Cat.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Cat. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Cat. (25 bar)' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'VDI.  (1 bar)' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'VDI. (10 bar)' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'VDI. (25 bar)' ;
'DESS' (rela10 'ET' rela20 'ET' rela30 'ET'
        rela11 'ET' rela21 'ET' rela31)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Thermal conductivity of water. Relative error (Cath., x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Cat.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Cat. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Cat. (25 bar)' ;
'DESS' (rela10 'ET' rela20 'ET' rela30)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Thermal conductivity of water. Relative error (VDI, x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'VDI.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'VDI. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'VDI. (25 bar)' ;
'DESS' (rela11 'ET' rela21 'ET' rela31)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 =
'Esope/Gibi relative error for Cathare formula (x100)' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Esope/Gibi (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Esope/Gibi (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Esope/Gibi (25 bar)' ;
'DESS' (d10 'ET' d20 'ET' d30)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
'FINSI' ;
*
*----------------------------------------------------------------------
*- Tests de bon fonctionnement
*- (Comparaisons des différents types et de la mise en oeuvre en gibi)
*----------------------------------------------------------------------
*
diff1 = 'MAXI' (test_f1 - test_l1 / l10) 'ABS' ;
diff2 = 'MAXI' (test_f2 - test_l2 / l20) 'ABS' ;
diff3 = 'MAXI' (test_f3 - test_l3 / l30) 'ABS' ;
diff4 = 'MAXI' (test_f1 - test_c1 / l10) 'ABS' ;
diff5 = 'MAXI' (test_f2 - test_c2 / l20) 'ABS' ;
diff6 = 'MAXI' (test_f3 - test_c3 / l30) 'ABS' ;
diff7 = 'MAXI' (test_f1 - l10 / l10)     'ABS' ;
diff8 = 'MAXI' (test_f2 - l20 / l20)     'ABS' ;
diff9 = 'MAXI' (test_f3 - l30 / l30)     'ABS' ;
didi  = diff1 + diff2 + diff3 + diff4 + diff5
      + diff6 + diff7 + diff8 + diff9 ;
'MESS' 'Sommes du max des différences relatives ' didi ;
'SI' (didi > 1.D-14) ;
   'ERRE' 5 ;
'FINSI' ;
*
'FIN' ;






