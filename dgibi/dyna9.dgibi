* fichier :  dyna9.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*           Test Dyna9.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test dyna9.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
************************************************************************
*                            DYNA9
*
*           CALCUL DES FREQUENCES PROPRES D'UNE PLAQUE-CONSOLE
*
* Dans cet exemple on calcule les frequences propres d'une plaque
* encastree sur un cote et libre sur les trois autres cotes.
*
* Les solutions calcules sont comparees a celles publiees par
* M.V. BARTON dans l'article :
*
*           VIBRATIONS OF RECTANGULAR AND SHEAR PLATES
*           JOU. OF APPL. MECH. , VOL.18 , 1951
*
************************************************************************
TITR 'FREQUENCES PROPRES D UNE PLAQUE CONSOLE' ;

OPTI DIME 3 ELEM QUA4 ;
OPTI EPSI LINEAIRE;

TEMPS ;
*
*------------------------- GEOMETRIE -----------------------------------
*
P0 = 0 0 0 ;
P1 = 0 1 0 ;
P2 = 2 1 0 ;
P3 = 2 0 0 ;
L1 = D 8 P0 P1 ;
L2 = D 16 P1 P2 ;
L3 = D 8 P2 P3 ;
L4 = D 16 P3 P0 ;
S = L1 L2 L3 L4 DALL PLAN ;
*
OEIL1 = 10 10 10 ;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' OEIL1 S ;
FINSI;
*
*------------------------- MATERIAU ------------------------------------
*
MM = MODE S MECANIQUE COQ4 ;

MA = MATE MM YOUN 30E6 NU 0.3 RHO 7.32E-4 ;
CA = CARA MM EPAI 0.01 ;
MA =MA ET CA;
*
*-------------- CONDITIONS AUX LIMITES - CALCUL DES MATRICES -----------
*
INC = BLOQ DEPL ROTA L1 ;
K   = RIGI MM MA ;
KT  = K ET INC ;
M   = MASS MM MA;
*
*------------------------ CALCUL ---------------------------------------
PROGREF = PROG 84.6 363.8 526.6 1187  ;
PROG1   = PROG 85.  360.  530.  1200. ;
AUTO    = VIBR PROCHE PROG1 KT M      ;

*
*-------------------------- TRACE ET SORTIES ---------------------------
PROGERR = VIDE 'LISTREEL';
REPE SURMOD (DIME PROG1);
  INDICE = &SURMOD ;
  FREQEN = AUTO . MODES . INDICE . FREQUENCE ;
  FREQREF= EXTR PROGREF INDICE;

  MESS ' MODE ' INDICE
        'FREQUENCE REF :' (EXTR PROGREF INDICE)
        'FREQUENCE CAL :' FREQEN ;
  MESS '  ERREUR RELATIVE : ' ERREUR ;
  DEPLA = AUTO . MODES . INDICE . DEFORMEE_MODALE ;
  DEPLZ = EXCO DEPLA 'UZ';
  SI (NEG GRAPH 'N');
    TRAC OEIL1 DEPLZ (DEFO 0.5 DEPLA S);
  FINSI;
  SAUT 1 LIGNE ;

  ERREUR = 100 * ((ABS(FREQREF -FREQEN) / FREQREF ));
  PROGERR = PROGERR ET (PROG ERREUR);

FIN SURMOD;

*---------------- CODE DE FONCTIONNEMENT -------------------------------
ERRMAX = MAXI PROGERR;

SI (ERRMAX < 1);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;

TEMPS ;
FIN;






