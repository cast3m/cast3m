* fichier : t_@PASHIST.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
*---------------------------------------------------------------------*
*                       Test Procedure @PASHIST                       *
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*
* IDESS1 = VRAI : dessin actifs :                                     *
IDESS1   = FAUX ;
*                                                                     *
'OPTI' 'ECHO' 0 ;
*                                                                     *
*---------------------- Construction Champ Test ----------------------*
*                                                                     *
'OPTI' 'DIME' 2 'ELEM' 'QUA8' ;
*                                                                     *
* Maillage :                                                          *
O1       = 0. 0. ;
OX1      = 1. 0. ;
OY1      = 0. 1. ;
NE1      = 1000 ;
L1       = O1  'DROI' NE1 OX1 ;
S1       = L1 'TRAN' 1 ((1. / ('FLOT' NE1)) * OY1) ;
*Trac S1 ; Opti Donn 5 ;
*                                                                     *
* Modele / Champ :                                                    *
MOD0     = 'MODE' S1 'MECANIQUE' ;
CHPO1    = 'CHAN' 'ATTRIBUT' ((S1 'COOR' 1) 'NOMC' 'X')
  'NATURE' 'DISCRET' ;
CHPO2    = ('BRUI' 'BLAN' 'GAUS' 5. 1.3 S1) 'NOMC' 'GAUS' ;
CHPO3    = ('BRUI' 'BLAN' 'EXPO' -2. 1. S1) 'NOMC' 'EXPO' ;
CHPO0    = CHPO1 'ET' CHPO2 'ET' CHPO3 ;
CHAM0    = 'CHAN' 'CHAM' MOD0 CHPO0 'STRESSES' ;
*Trac MOD0 CHAM0 ;
*                                                                     *
*------------------------ Tests fonctionnement -----------------------*
*                                                                     *
* Synthaxe par defaut :                                               *
LHIST1   = @PASHIST MOD0 CHAM0 ;
DDSIG1   = 'HIST' 'VERT' MOD0 CHAM0 LHIST1 ;
'SI' IDESS1 ;
  'DESS' DDSIG1 'TITR' ' Verif. fonctionnement @PASHIST ' ;
'FINS' ;
*                                                                     *
* Donnee Noms de composantes par LISTMOTS :                           *
LMOT1    = 'MOTS' 'EXPO' 'GAUS' ;
LHIST1   = @PASHIST MOD0 CHAM0 LMOT1 ;
DDSIG1   = 'HIST' 'VERT' MOD0 CHAM0 LHIST1 LMOT1 ;
'SI' IDESS1 ;
  'DESS' DDSIG1 'TITR'
    ' Verif. donnee Noms de composantes par LISTMOTS' ;
'FINS' ;
*                                                                     *
* Donnee Nom de composantes par un MOT :                              *
MOT1     = 'MOT' 'X' ;
LHIST1   = @PASHIST MOD0 CHAM0 MOT1 ;
DDSIG1   = 'HIST' 'VERT' MOD0 CHAM0 LHIST1 MOT1 ;
'SI' IDESS1 ;
  'DESS' DDSIG1 'TITR'
    ' Verif. donnee Nom de composante par MOT (ici Coor 1 maillage)' ;
'FINS' ;
*                                                                     *
* Visualisation (donnee cachee) :                                     *
'SI' IDESS1 ;
  LHIST1   = @PASHIST MOD0 CHAM0 VRAI ;
'FINS' ;
*                                                                     *
'FIN' ;




