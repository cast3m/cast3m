* fichier : syco_3D_contpla.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*******************************************************
**-------------------------------------------------------------------
MESS '' ;
MESS '      COMPARAISON 3D et 2D contraintes planes ';
MESS '      pour lois SYCO1 et SYCO2 ';
MESS '      CALCUL DYNAMIQUE COMPORTEMENT VISCOPLASTIQUE SYCO1 ';
MESS '                        en 3D ' ;
MESS '  avec 1 face bloquee pour comparer a 2D cont plane' ;
MESS '                    Unites: N  Pa  m  kg  s' ;
MESS '' ;
**--------------------------------------------------------------------
*******************************************************
*** Options de calcul
opti dime 3 elem CUB8 mode trid;

*******************************************************
*** maillage
  pa1 = 0. 0. 0. ;
  pa2 = 0.0001 0. 0. ;
  pa3 = 0.0001 5. 0. ;
  pa4 = 0. 5. 0. ;
  li12 = pa1 droi 1 pa2;
  li23 = pa2 droi 1 pa3;
  li34 = pa3 droi 1 pa4;
  li41 = pa4 droi 1 pa1;
  su1 = dall li12 li23 li34 li41;
  vol1 = su1  volu tran 1 (0. 0. 3.);
  elim 0.00001 vol1;
*
   pa5 = vol1 poin proch (0. 0. 3.);
   pa6 = vol1 poin proch (3. 0. 3.);
   pa7 = vol1 poin proch (3. 5. 3.);
   pa8 = vol1 poin proch (0. 5. 3.);

**********************************************************
***   MODELE & MATERIAU   ***
**********************************************************
*
ep1 = prog 0. 0.003151475  0.072983512 0.147777886 ;
si1 = (prog 0. 666. 851.6513994 951.65139) * 1.e6;
lcxx = 'EVOL' 'MANU' 'Epsilon' ep1 'Sigma' si1 ;
ab1 = extr lcxx 'ABSC';
ord1 = extr lcxx 'ORDO';
eey1 = (extr ord1 2)/(extr ab1 2);
*
nu0 = 0.3 ;
rho0= 7800.;
**********************************************************
*** Modele & materiau  ***
*
* elements standards
  mod1 = 'MODELISER' vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
  'VISCOPLASTIQUE' 'SYCO1' 'CUB8';
  mat1 = 'MATERIAU' mod1'YOUN' eey1 'NU' nu0 'RHO' rho0
  'TRAC' lcxx 'PSYC' 12. 'DSYC' 1.d8 ;
*

**********************************************************
***   CL et DEPLACEMENTS IMPOSES   ***
**********************************************************

cl1 = bloq (pa1 et pa4 et pa8 et pa5)  'UX';
cl2 = bloq (pa1 et pa2 et pa5 et pa6)  'UY';
cl4 = bloq (pa1 et pa2 et pa3 et pa4)  'UZ';
cl5 = bloq (pa3 et pa4 et pa7 et pa8)  'UY';
cltot =  (cl1 'ET' cl2 'ET' cl4 'ET' cl5) ;
*
*... chargement en deplacement impose
umax = 1.;
uimp = depi cl5 umax;

*
xx1 = prog 0. 1.;
yy1 = prog 0. 10.;
evochar = evol manu 'temps' xx1 'charge' yy1;
char1 = char meca uimp evochar;
*

*=======================================================*
*                                                       *
*                     RESOLUTION                        *
*                                                       *
*=======================================================*

dt1 = 2.e-4;
xx0 = prog 0.             pas dt1 'NPAS' 30;
*
tab1 = table;
tab1.dynamique = vrai;
tab1.modele = mod1;
tab1.caracteristiques = mat1;
tab1.blocages_mecaniques = cltot;
tab1.chargement = char1;
tab1.temps_calcules = xx0;
tab1.precision = 1.d-10;
tab1.convergence_forcee = faux;
pasapas tab1;
*
*post traitement
ut1 = tab1.deplacements. 30;
var1 = tab1.variables_internes. 30;
*
val1 = extr var1 'EPSE' 1 1 3;
val2 = extr var1 'VP' 1 1 3;
val3 = extr ut1 'UZ' pa8;
**********************************************************
**-------------------------------------------------------------------
MESS '' ;
MESS '      CALCUL DYNAMIQUE COMPORTEMENT VISCOPLASTIQUE SYCO1 ';
MESS '                        en  2D cont plane ' ;
MESS '                    Unites: N  Pa  m  kg  s' ;
MESS '' ;
**--------------------------------------------------------------------
*** Options de calcul
opti dime 2 elem QUA4 mode plan cont;

*******************************************************
*** maillage
  pa1 = 0. 0.;
  pa2 = 5. 0.;
  pa3 = 5. 3.;
  pa4 = 0. 3.;
  li12 = pa1 droi 1 pa2;
  li23 = pa2 droi 1 pa3;
  li34 = pa3 droi 1 pa4;
  li41 = pa4 droi 1 pa1;
  su1 = dall li12 li23 li34 li41;
  elim 0.001 su1;
*

**********************************************************
***   MODELE & MATERIAU   ***
**********************************************************
ep1 = prog 0. 0.003151475  0.072983512 0.147777886 ;
si1 = (prog 0. 666. 851.6513994 951.65139) * 1.e6;
lcxx = 'EVOL' 'MANU' 'Epsilon' ep1 'Sigma' si1 ;
*
ab1 = extr lcxx 'ABSC';
ord1 = extr lcxx 'ORDO';
eey1 = (extr ord1 2)/(extr ab1 2);
*
nu0 = 0.3 ;
rho0= 7800.;
**********************************************************
*** Modele & materiau  ***
*
* elements standards
  mod1 = 'MODELISER' su1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
  'VISCOPLASTIQUE' 'SYCO1' 'QUA4';
  mat1 = 'MATERIAU' mod1 'YOUN' eey1 'NU' nu0 'RHO' rho0
  'TRAC' lcxx 'PSYC' 12. 'DSYC' 1.d8 ;
*

**********************************************************
***   CL et DEPLACEMENTS IMPOSES   ***
**********************************************************

cl1 = bloq (pa1 et pa4) 'UX';
cl2 = bloq (pa1 et pa2) 'UY';
cl3 = bloq (pa2 et pa3) 'UX';
cltot =  (cl1 'ET' cl2 'ET' cl3 ) ;
*
*... chargement en deplacement impose
umax = 1.;
uimp = depi cl3 umax;

*
xx1 = prog 0. 1.;
yy1 = prog 0. 10.;
evochar = evol manu 'temps' xx1 'charge' yy1;
char1 = char meca uimp evochar;
*
*=======================================================*
*                                                       *
*                     RESOLUTION                        *
*                                                       *
*=======================================================*

dt1 = 2.e-4;
*
tab2 = table;
tab2.dynamique = vrai;
tab2.modele = mod1;
tab2.caracteristiques = mat1;
tab2.blocages_mecaniques = cltot;
tab2.chargement = char1;
tab2.temps_calcules = xx0;
tab2.precision = 1.d-10;
tab2.convergence_forcee = faux;
pasapas tab2;
*
*post traitement
ut1 = tab2.deplacements. 30;
var1 = tab2.variables_internes. 30;
*
xal1 = extr var1 'EPSE' 1 1 3;
xal2 = extr var1 'VP' 1 1 3;
xal3 = extr ut1 'UY' pa3;
*
aa1 = ABS(val1 - xal1);
aa2 = ABS(val2 - xal2);
aa3 = ABS(val3 - xal3);
*list aa1;
*list aa2;
*list aa3;
*opti donn 5;
*
si (aa1 >EG 5.e-5);
erre 5;
finsi;
si (aa2 >EG 5.e-3);
erre 5;
finsi;
si (aa3 >EG 5.e-4);
erre 5;
finsi;

*******************************************************
**-------------------------------------------------------------------
MESS '' ;
MESS '      CALCUL DYNAMIQUE COMPORTEMENT VISCOPLASTIQUE SYCO2 ';
MESS '                        en 3D ' ;
MESS '  avec 1 face bloquee pour comparer a 2D cont plane' ;
MESS '                    Unites: N  Pa  m  kg  s' ;
MESS '' ;
**--------------------------------------------------------------------
*******************************************************
*** Options de calcul
opti dime 3 elem CUB8 mode trid;

*******************************************************
*** maillage
  pa1 = 0. 0. 0. ;
  pa2 = 0.01  0. 0. ;
  pa3 = 0.01  5. 0. ;
  pa4 = 0. 5. 0. ;
  li12 = pa1 droi 1 pa2;
  li23 = pa2 droi 1 pa3;
  li34 = pa3 droi 1 pa4;
  li41 = pa4 droi 1 pa1;
  su1 = dall li12 li23 li34 li41;
  vol1 = su1  volu tran 1 (0. 0. 3.);
  elim 0.001 vol1;
*
   pa5 = vol1 poin proch (0. 0. 3.);
   pa6 = vol1 poin proch (3. 0. 3.);
   pa7 = vol1 poin proch (3. 5. 3.);
   pa8 = vol1 poin proch (0. 5. 3.);

**********************************************************
***   MODELE & MATERIAU   ***
**********************************************************
*
ep1 = prog 0. 0.003151475  0.072983512 0.147777886 ;
si1 = (prog 0. 666. 851.6513994 951.65139) * 1.e6;
lcxx = 'EVOL' 'MANU' 'Epsilon' ep1 'Sigma' si1 ;
ab1 = extr lcxx 'ABSC';
ord1 = extr lcxx 'ORDO';
eey1 = (extr ord1 2)/(extr ab1 2);
*
nu0 = 0.3 ;
rho0= 7800.;
**********************************************************
*** Modele & materiau  ***
*
         TEMP01 = -125.;
         PSY9 = 4.25 - (0.015*TEMP01);
         ASY9 = 0.07592 - (0.07182*TEMP01/273.);
         BSY9 = 0.06352 - (0.23970*TEMP01/273.);
         CSY9 = 0.02528 - (0.04124*TEMP01/273.);
*message 'P A B C' psy9 asy9 bsy9 csy9;
* elements standards
  mod1 = 'MODELISER' vol1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
  'VISCOPLASTIQUE' 'SYCO2' 'CUB8';
  mat1 = 'MATERIAU' mod1'YOUN' eey1 'NU' nu0 'RHO' rho0
  'TRAC' lcxx 'PSYC' psy9 'ASYC' asy9 'BSYC' bsy9 'CSYC' csy9;
*
**********************************************************
***   CL et DEPLACEMENTS IMPOSES   ***
**********************************************************

cl1 = bloq (pa1 et pa4 et pa8 et pa5)  'UX';
cl2 = bloq (pa1 et pa2 et pa5 et pa6)  'UY';
cl4 = bloq (pa1 et pa2 et pa3 et pa4)  'UZ';
cl5 = bloq (pa3 et pa4 et pa7 et pa8)  'UY';
cltot =  (cl1 'ET' cl2 'ET' cl4 'ET' cl5) ;
*
*... chargement en deplacement impose
umax = 1.;
uimp = depi cl5 umax;

*
xx1 = prog 0. 1.;
yy1 = prog 0. 10.;
evochar = evol manu 'temps' xx1 'charge' yy1;
char1 = char meca uimp evochar;
*
*
*=======================================================*
*                                                       *
*                     RESOLUTION                        *
*                                                       *
*=======================================================*

dt1 = 2.e-4;
*
tab1 = table;
tab1.dynamique = vrai;
tab1.modele = mod1;
tab1.caracteristiques = mat1;
tab1.blocages_mecaniques = cltot;
tab1.chargement = char1;
tab1.temps_calcules = xx0;
tab1.precision = 1.d-10;
tab1.convergence_forcee = faux;
pasapas tab1;
*
*post traitement

sig1 = tab1.contraintes. 30;
ut1 = tab1.deplacements. 30;
vt1 = tab1.vitesses. 30;
tt1 = tab1.temps. 30;
var1 = tab1.variables_internes. 30;
*
*
val1 = extr var1 'EPSE' 1 1 3;
val2 = extr var1 'VP' 1 1 3;
val3 = extr ut1 'UZ' pa7;

*******************************************************
**-------------------------------------------------------------------
MESS '' ;
MESS '      CALCUL DYNAMIQUE COMPORTEMENT VISCOPLASTIQUE SYCO2 ';
MESS '                        en  2D cont plane ' ;
MESS '                    Unites: N  Pa  m  kg  s' ;
MESS '' ;
**--------------------------------------------------------------------
*** Options de calcul
opti dime 2 elem QUA4 mode plan cont;
    opti 'TRAC' X ;

*******************************************************
*** maillage
  pa1 = 0. 0.;
  pa2 = 5. 0.;
  pa3 = 5. 3.;
  pa4 = 0. 3.;
  li12 = pa1 droi 1 pa2;
  li23 = pa2 droi 1 pa3;
  li34 = pa3 droi 1 pa4;
  li41 = pa4 droi 1 pa1;
  su1 = dall li12 li23 li34 li41;
  elim 0.001 su1;
*

**********************************************************
***   MODELE & MATERIAU   ***
**********************************************************
ep1 = prog 0. 0.003151475  0.072983512 0.147777886 ;
si1 = (prog 0. 666. 851.6513994 951.65139) * 1.e6;
lcxx = 'EVOL' 'MANU' 'Epsilon' ep1 'Sigma' si1 ;
*
ab1 = extr lcxx 'ABSC';
ord1 = extr lcxx 'ORDO';
eey1 = (extr ord1 2)/(extr ab1 2);
*
nu0 = 0.3 ;
rho0= 7800.;
**********************************************************
*** Modele & materiau  ***
*
         TEMP01 = -125.;
         PSY9 = 4.25 - (0.015*TEMP01);
         ASY9 = 0.07592 - (0.07182*TEMP01/273.);
         BSY9 = 0.06352 - (0.23970*TEMP01/273.);
         CSY9 = 0.02528 - (0.04124*TEMP01/273.);
*message 'P A B C' psy9 asy9 bsy9 csy9;
* elements standards
  mod1 = 'MODELISER' su1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
  'VISCOPLASTIQUE' 'SYCO2' 'QUA4';
  mat1 = 'MATERIAU' mod1'YOUN' eey1 'NU' nu0 'RHO' rho0
  'TRAC' lcxx 'PSYC' psy9 'ASYC' asy9 'BSYC' bsy9 'CSYC' csy9;
*

**********************************************************
***   CL et DEPLACEMENTS IMPOSES   ***
**********************************************************

cl1 = bloq (pa1 et pa4) 'UX';
cl2 = bloq (pa1 et pa2) 'UY';
cl3 = bloq (pa2 et pa3) 'UX';
cltot =  (cl1 'ET' cl2 'ET' cl3 ) ;
*
*... chargement en deplacement impose
umax = 1.;
uimp = depi cl3 umax;

*
xx1 = prog 0. 1.;
yy1 = prog 0. 10.;
evochar = evol manu 'temps' xx1 'charge' yy1;
char1 = char meca uimp evochar;
*
*=======================================================*
*                                                       *
*                     RESOLUTION                        *
*                                                       *
*=======================================================*

dt1 = 2.e-4;
*
tab2 = table;
tab2.dynamique = vrai;
tab2.modele = mod1;
tab2.caracteristiques = mat1;
tab2.blocages_mecaniques = cltot;
tab2.chargement = char1;
tab2.temps_calcules = xx0;
tab2.precison = 1.d-10;
tab2.convergence_forcee = faux;
pasapas tab2;
*
*post traitement
sig1 = tab2.contraintes. 30;
ut1 = tab2.deplacements. 30;
vt1 = tab2.vitesses. 30;
tt1 = tab2.temps. 30;
var1 = tab2.variables_internes. 30;
*
*
xal1 = extr var1 'EPSE' 1 1 3;
xal2 = extr var1 'VP' 1 1 3;
xal3 = extr ut1 'UY' pa3;

aa1 = ABS(val1 - xal1);
aa2 = ABS(val2 - xal2);
aa3 = ABS(val3 - xal3);
*list aa1;
*list aa2;
*list aa3;
*opti donn 5;
*
si (aa1 >EG 5.e-5);
erre 5;
finsi;
si (aa2 >EG 5.e-3);
erre 5;
finsi;
si (aa3 >EG 5.e-4);
erre 5;
finsi;
fin;






