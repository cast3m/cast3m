* fichier :  rayo-2D-1-trans.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************

complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
***************************************************
****************************************************************
*
*  test 2D couplage conduction-rayonnement
*  REFERENCE: SPARROW CESS "Radiation Heat Transfer" 1978 p.189
*
*  DONNEES
*  cas de 2 ailettes
*
*  angle entre les ailettes :  45 degres
*  longueur                 :  L=0.1m
*  emissivite               :  0.75
*
*  conductivite lineique                     : LAM=4.536 W/K
*  conditions aux limites  base d ailette    : TB= 2000 K
*                          extremite ailette : flux nul
*  Nombre caracteristique rayonnemnent/conduction  XC=1.
*
*  RESULTATS
*  efficacite de l ailette             0.591
*  puissance rayonnee                  41 kW
*  temperature en milieu d ailette     1820 K
*  temperature en extremite d ailette  1751 K
*
*  Ce jeu de données est analogue   au rayo-2D-1.dgibi sauf que
*  l'on obtient la solution par une simulation transitoire avec
*  un chargement constant (comme par une méthode de relaxation
*  en mécanique).
*
*  on teste les 2 méthodes: 1/sans ou 2/avec calcul de la
*  matrice de rayonnement (cf. notice PASAPAS)
****************************************************************

*** Options ...

    OPTION DIME 2 ELEM QUA4 ;

    graph = faux ;

*** Paramètres ...

*   AL angle entre les ailettes ...
    AL = 45. ;

    L = 0.1;  C = COS(AL) ; S = SIN(AL) ;
    A = SIN(AL/2.) ;

*   Nombre de segments / ailette ...
si complet;
    N = 20 ;
sinon;
    N = 4;
finsi;


*   Épaisseur des ailettes : 1mm
    dz = 0.001 ;
    dzp = dz ;
    dzn = -1 * dz ;

*   Propriétés physiques ...
    sig=5.67E-8 ; l=0.1; tb=2000. ; emis = 0.75 ;

*   Efficacite de l'ailette
    xc = 1.0 ;

*** Points ...

    P1 = 0. 0. ; P2 = L  0. ;
    Q1 = 0. 0. ; Q2 = (L*C) (L*S) ;

*** Lignes ...

    L1= D N  P1 P2 ; L2 = D N  Q2 Q1 ;

*** Surfaces ...

    z1 = l1 tran 1 (0. dzn) ;
    z2 = l2 tran 1 ((dzn*s) (dzp*c)) ;

    lim1 = cote 4 z1 ;
    lim2 = cote 2 z2 ;

    cavite = l1 et l2 ;
    tout = z1 et z2 ;
    titr 'Le maillage d ailette' ;
    si(graph) ; trac tout ; finsi ;

*** Modélisation ...

*   conduction
*   calcul de la conductivité lineique
    kr   = sig*tb*tb*tb *l*l ;
    lam  = kr/xc ;
    lamb = lam/dz ;

    mcd1 = modeli z1   thermique  ;
    mcd2 = modeli z2   thermique  ;
    mcd  = mcd1 et mcd2  ;
    k = mate mcd 'K' lamb 'RHO' 5000. 'C' 10. ;
    cnd = cond mcd k ;

    mr1 = modeli l1 thermique rayonnement  'CAVITE' CONVEXE CONS 'CAV1';
    mr2 = modeli l2 thermique rayonnement  'CAVITE' CONVEXE CONS 'CAV1';
    mrt = mr1 et mr2  ;

    e1 = mate mr1 'EMIS' emis ;
    e2 = mate mr2 'EMIS' emis ;
    e = e1 et e2  ;


*** Conditions aux limites ...

    c1 = bloq lim1 'T' ;
    tim1 = depi c1 tb ;
    c2 = bloq lim2 'T' ;
    tim2 = depi c2 tb ;

*--------------------------------------------------------------
* méthode par défaut (avec calcul de la matrice de rayonnement)

*** Préparation à la simulation avec PASAPAS ...

    tabnl = table ;

    tabnl . 'MODELE' =  mcd et mrt;
    tabnl . 'CARACTERISTIQUES' = k et e ;

    listtemp = prog 0  5 100 ;
    listval  = prog 1  1   1 ;
    evchar   = evol manu 't' listtemp 'f(t)' listval ;
    tabnl . 'CHARGEMENT' = char 'TIMP' evchar (tim1 et tim2) ;

    tabnl . 'BLOCAGES_THERMIQUES' = c1 et c2 ;

*    tabnl . 'RAYONNEMENT' = table ;
*    tabnl . 'RAYONNEMENT' . 1 = table ;
**    tabnl . 'RAYONNEMENT' . 1 . 'TYPE' = 'CAVITE' ;
*    tabnl . 'RAYONNEMENT' . 1 . 'MODELE' = mrt ;
*    tabnl . 'RAYONNEMENT' . 1 . 'CONVEXE' = vrai ;
*    tabnl . 'RAYONNEMENT' . 1 . 'FERME' = faux ;

    tabnl . 'TEMPS_CALCULES' = prog 0 pas 0.5 10 ;
*    tabnl . 'TEMPS_SAUVES'   = extr (tabnl . 'TEMPS_CALCULES')
*              (lect 1 pas 10 (dime (tabnl . 'TEMPS_CALCULES'))) ;

    tabnl . 'PROCEDURE_THERMIQUE' = 'DUPONT' ;

*** Résolution (avec PASAPAS) ...

    pasapas tabnl ;

*** Petit post-traitement ...

    nbpas = dime (tabnl . TEMPS) ;
    listtime = prog ;
    listtemp = prog ;
    repeter surpas nbpas ;
       lindice = &surpas - 1 ;
       listtime = listtime et (prog (tabnl . TEMPS . lindice)) ;
       valtemp = extr (tabnl . TEMPERATURES . lindice) 'T' p2 ;
       listtemp = listtemp et (prog valtemp) ;
    fin surpas ;

    titr 'Evolution de temperature a l extremite de l ailette' ;
    evtemp = evol manu 't' listtime 'T' listtemp ;
    si(graph) ; dess evtemp ; finsi ;


    valref = 1751. ;
    valobt = extr listtemp (dime listtemp) ;
    errre1 = 100 * (valobt - valref) / valref ;

opti echo 0 ;
    saut 1 lign ;
    mess 'Méthode avec calcul de la matrice de rayonnement' ;
    mess 'Solution exacte  : ' valref ;
    mess 'Solution obtenue : ' valobt ;
    mess 'soit une erreur de ' errre1  ' %' ;


*** Test de bon fonctionnement

    si ((abs errre1) > 1.);
       mess ' méthode avec matrice de rayonnement';
       erre 5 ;
    sinon;
       erre 0;
    finsi ;

*--------------------------------------------------------------
* méthode sans calcul de la matrice de rayonnement
  mrt2= model (l1 et l2) thermique 'RAYONNEMENT' 'CAVITE' FERME
    TRAYO ;
    e2 = mate mrt2 'EMIS' emis;
    tabnl = table ;
    tabnl . 'MODELE' =  mcd  et mrt2;
    tabnl . 'CARACTERISTIQUES' = k et e2 ;

    listtemp = prog 0  5 100 ;
    listval  = prog 1  1   1 ;
    evchar   = evol manu 't' listtemp 'f(t)' listval ;
    tabnl . 'CHARGEMENT' = char 'TIMP' evchar (tim1 et tim2) ;

    tabnl . 'BLOCAGES_THERMIQUES' = c1 et c2 ;

*    tabnl . 'RAYONNEMENT' = table ;
*    tabnl . 'RAYONNEMENT' . 1 = table ;
*    tabnl . 'RAYONNEMENT' . 1 . 'TYPE' = 'CAVITE' ;
*    tabnl . 'RAYONNEMENT' . 1 . 'MODELE' = mrt ;
**    tabnl . 'RAYONNEMENT' . 1 . 'CONVEXE' = vrai ;
*    tabnl . 'RAYONNEMENT' . 1 . 'FERME' = faux ;
*    tabnl . 'RAYONNEMENT' . 1 . 'TRAYO'= vrai;

    tabnl . 'TEMPS_CALCULES' = prog 0 pas 0.5 10 ;
*    tabnl . 'TEMPS_SAUVES'   = extr (tabnl . 'TEMPS_CALCULES')
*              (lect 1 pas 10 (dime (tabnl . 'TEMPS_CALCULES'))) ;

    tabnl . 'PROCEDURE_THERMIQUE' = 'DUPONT' ;

*** Résolution (avec PASAPAS) ...

    pasapas tabnl ;

*** Petit post-traitement ...

    nbpas = dime (tabnl . TEMPS) ;
    listtime = prog ;
    listtemp = prog ;
    repeter surpas nbpas ;
       lindice = &surpas - 1 ;
       listtime = listtime et (prog (tabnl . TEMPS . lindice)) ;
       valtemp = extr (tabnl . TEMPERATURES . lindice) 'T' p2 ;
       listtemp = listtemp et (prog valtemp) ;
    fin surpas ;

    titr 'Evolution de temperature a l extremite de l ailette' ;
    evtemp = evol manu 't' listtime 'T' listtemp ;
    si(graph) ; dess evtemp ; finsi ;


    valref = 1751. ;
    valobt = extr listtemp (dime listtemp) ;
    errre2 = 100 * (valobt - valref) / valref ;

opti echo 0 ;
    saut 1 lign ;
    mess 'Methode sans calcul de la matrice de rayonnement' ;
    mess 'Solution exacte  : ' valref ;
    mess 'Solution obtenue : ' valobt ;
    mess 'soit une erreur de ' errre2  ' %' ;



*** Test de bon fonctionnement

    si ((abs errre2) > 1.);
       mess ' méthode sans matrice de rayonnement';
       erre 5 ;
    sinon;
       erre 0;
    finsi ;

   fin ;



