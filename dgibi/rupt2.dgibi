* fichier :  rupt2.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*           Test Rupt2.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
OPTION echo 1                                         ;
GRAPH = 'N'                                           ;
SAUT PAGE                                             ;
*                                                     ;
******************************************************;
*                                                     ;
*                                                     ;
*              QUALIFICATION DU CALCUL DE G           ;
*              EN THERMO-ELASTICITE LINEAIRE          ;
*              SUR UNE PLAQUE A FISSURE LATERALE      ;
*              EVALUATION DU FACTEUR DE FORME         ;
*                                                     ;
*                                                     ;
* le calcul est compare a celui obtenu par            ;
* WILSON et YU                                        ;
* dans The use of J-Integral in thermal stress crack  ;
* problems international Journal of Fracture (1979)   ;
*                                                     ;
******************************************************;
*                                                     ;
OPTION DIME 2 ELEM QUA8 MODE PLAN DEFO                ;
*                                                     ;
*----------------- DEFINITION DU MAILLAGE ------------;
*                                                     ;
a = 100. ; b = 200. ; h = 400. ;t = a / 100.          ;
densite  t ; pf = (a 0.)                              ;
c1 = (c ( pf moin (t 0.)) pf ( pf plus (0. t)))
      c pf ( pf plus (t 0.))                          ;
sf = cout pf c1;
r1 = t ; rr1 = t                                      ;
repeter bhomo 7;
   ri  =  r1 + ( 0.3 * r1 )                           ;
   rri = rr1 + ri                                     ;
   dens ri                                            ;
   ci = ( c ( pf moin (rri 0.)) pf ( pf plus (0. rri)))
          c pf ( pf plus (rri 0.))                    ;
   sf = sf et ( cout c1 ci )                          ;
   c1 = ci ; r1 = ri ; rr1 = rri                      ;
fin bhomo                                             ;
dens (a / 3.)                                         ;
p0  = (0. 0.) ; p1 = (b 0.)                           ;
p2  = p0 plus (0. a) ; p3 = p1 plus (0. a)            ;
pi1 = ci poin 1  ; l1  = pi1 d p0                     ;
n = (nbel l1) * -1                                    ;
pi2 = ci poin 4  ; l2  = pi2 d n p2                   ;
pi3 = ci poin 10 ; l3  = pi3 d n p3                   ;
pi4 = ci poin 13 ; l4  = pi4 d   p1                   ;
ci  = inve ci    ;ligh = p2  d   p3                   ;
sc1 = dall l1 (p0 d p2) (inve l2) (ci ELEM comp pi2 pi1)   ;
sc2 = dall l2 ligh (inve l3) (ci ELEM comp pi3 pi2)        ;
sc3 = dall l3 (p3 d p1) (inve l4) (ci ELEM comp pi4 pi3)   ;
sc  = sc1 et sc2 et sc3                               ;
dens (a / 2.)                                         ;
mrest  = ligh tran (0. (h - a)) dini (a / 2.)         ;
lihaut = mrest cote 3                                 ;
phd    = lihaut poin init                             ;
ccp    = sf et sc et mrest                            ;
elim  ccp  0.001                                      ;
cccp   = cont ccp                                     ;
lifis  = cccp ELEM comp pf p0                              ;
libas  = cccp ELEM comp p1 pf                              ;
*                                                     ;
*------------------- CREATION DU MODELE --------------;
*                                                     ;
objaf = MODE ccp mecanique elastique isotrope         ;
*                                                     ;
*---------- DEFINITION DES CARACTERISTIQUES ----------;
*----------   MATERIELLES ET GEOMETRIQUES   ----------;
*                                                     ;
mat = MATE objaf  YOUN 2.e4  NU 0.3 ALPH 5.e-6        ;
*                                                     ;
*---------- CALCUL DES RIGIDITES ELEMENTAIRES --------;
*---------     ET DEFINITION DES BLOCAGES    ---------;
*                                                     ;
rig  = rigi objaf mat                                 ;
cdl1 = bloq Uy libas                                  ;
cdl2 = bloq Uy lihaut                                 ;
cdl3 = bloq Ux phd                                    ;
*                                                     ;
*-------- DEFINITION DU CHAMP DE TEMPERATURES --------;
*                                                     ;
chx = coor 1 ccp                                      ;
cha = manu chpo ccp 1 scal a                          ;
chx = nomc 'T' (chx - cha)                            ;
cht = chx * (100. / a)                                ;
ch0 = 0 * cht                                         ;
sigth = thet MAT objaf cht                            ;

*                                                     ;
*-------- RESOLUTION ET CALCUL DES CONTRAINTES -------;
*                                                     ;
U    = reso (rig et cdl1 et cdl2 et cdl3 )
       (bsig objaf sigth)   ;
SIG  = sigm mat objaf U                               ;
SIG  = SIG - sigth                                    ;
sigy = exco SIG smyy                                  ;
SI ( NEG GRAPH 'N' )                                  ;
     TRAC  CCP                                        ;
     TRAC SIGY OBJAF CCP                              ;
FINSI                                                 ;
*                                                     ;
*------ INITIALISATION DE LA TABLE EN ENTREE ---------;
*----       DE LA PROCEDURE G_THETA         ----------;
*                                                     ;
SUPTAB = TABLE ;
SUPTAB.'OBJECTIF' = MOT 'J';
SUPTAB.'LEVRE_SUPERIEURE' = lifis;
SUPTAB.'FRONT_FISSURE' = PF ;
SUPTAB.'MODELE' = objaf;
SUPTAB.'CARACTERISTIQUES' = mat;
SUPTAB.'SOLUTION_RESO' = u;
SUPTAB.'TEMPERATURES' = cht;
SUPTAB.'COUCHE' = 4;
*                                                     ;
*----------- APPEL A LA PROCEDURE G_THETA ------------;
*                                                     ;
SAUT PAGE                                             ;
G_THETA suptab                                        ;
*                                                     ;
*------ RECUPERATION DU RESULTAT ET COMPARAISONS -----;
*                                                     ;
G = suptab.'RESULTATS'                                ;
K = (20000. * G / (1 - (0.3**2)))**0.5                ;
F = K * (1 - 0.3) /
    ( 20000 * 5.e-6 * 100 * ((100*pi)**0.5))          ;
mess 'taux de restitution energetique        G :' G   ;
mess 'coefficient d intensite de contrainte  K :' K   ;
mess 'facteur de forme  calcule           Fcal :' F   ;
mess
'facteur de forme analytique         Fthe :' 0.514    ;
*                   CODE FONCTIONNEMENT               ;
RESI = abs (( F - 0.514 ) / 0.514 )                   ;
SI (RESI <  5E-2)                                     ;
    ERRE  0                                           ;
SINO                                                  ;
    ERRE  5                                           ;
FINSI                                                 ;
fin;
















