* fichier :  gdef2.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
********************************************************************
*                                                                  *
*  TEST GDEF2                                                      *
*                                                                  *
*  CISAILLEMENT PUR EN GRANDES DEFORMATIONS PLASTIQUES             *
*  ( MODELE DE PLASTICITE ISOTROPE )                               *
*      On compare avec les valeurs obtenues a une solution         *
*      analytique                                                  *
*                                                                  *
********************************************************************
*
OPTI ECHO 0;
TITRE   ' TEST GDEF2  ' ;
GRAPH = 'N';
OPTI DIME 2 ELEM QUA4 MODE PLAN CONT;
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
P1 = 0 0 ;
P2 = 1. 0. ;
P3 = 1. 1. ;
P4 = 0. 1. ;
L1 = P1 D 1 P2 ;
L2 = P2 D 1 P3;
L3 = P3 D 1 P4 ;
L4 = P4 D 1 P1 ;
SU = DALLER L1 L2 L3 L4 ;
SI (NEG GRAPH 'N');
   TRAC SU    ;
FINSI;
*
*==================================================================*
*           modele - materiau - caracteristiques en Pa             *
*                      conditions aux limites                      *
*==================================================================*
*
MO = MODE SU MECANIQUE ELASTIQUE PLASTIQUE ISOTROPE
 epsilon jaumann ;
YU = 1000. ;   XNU = 0.3 ;
LTRAC = EVOL MANU 'EPS' ( PROG 0. 1.E-3 10.001 )
                  'SIG' ( PROG 0. 1. 30.91026920);
MA = MATE MO YOUN YU     NU XNU TRAC LTRAC ;
BL1 = BLOQ  DEPL P1 ;
BL2 = BLOQ  DEPL P2 ;
BL3 = BLOQ UX L3 ;
BL4 = BLOQ UY L3 ;
FF = DEPI BL3 1. ;
*
*==================================================================*
*         Initialisation de la table pour appel a PASAPAS          *
*==================================================================*
*
TAB1 = TABLE;
tab1.grands_deplacements=vrai;
TAB1 . BLOCAGES_MECANIQUES= BL1 ET BL2 ET BL3 ET BL4 ;
TAB1 . MODELE = MO;
TAB1 . CARACTERISTIQUES = MA ;
EV1 = EVOL MANU X (PROG 0  5 10 15 20 )
 Y  (PROG 0  5 10 5 0   );
TAB1 . CHARGEMENT = CHAR MECA FF EV1 ;
TAB1 . TEMPS_CALCULES = PROG 0.1   PAS 0.1 5.;
PASAPAS TAB1 ;
*
*==================================================================*
*                   affichage des resultats                        *
*==================================================================*
*
MESS ' RESULTATS TEST GDEF2 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS '   On cherche le cisaillement ';
MESS '   Il est compare a une solution analytique   ';
SAUT 2 LIGN;
*
*----------- r{sultats --------------------------------------------------
*
AA = TAB1 . DEPLACEMENTS ;
BB = TAB1 . CONTRAINTES  ;
N = DIME AA ;
SP12= PROG ;
SPTH12= PROG ;
EP = PROG ;
EP = TAB1 . TEMPS_CALCULES ;
REPE BOU (N - 1 ) ;
CSX = CHAN CHPO  MO ( BB . &BOU ) ;
DX = EXTR ( AA . &BOU ) UX P3 ;
S12 = EXTR CSX SMXY P1 ;
SP12 = SP12 ET ( PROG S12 ) ;
SDX = 0.577350269 +  DX  ;
SPTH12= SPTH12 ET ( PROG  SDX ) ;
FIN BOU ;
EV12 = EVOL ROSE  MANU 'DEPLA' EP 'SIG12' SP12 ;
FVTH12 = EVOL TURQ  MANU 'DEPLA' EP 'SIG12' SPTH12 ;
SI (NEG GRAPH 'N');
 DESS  ( FVTH12 ET EV12) ;
FINSI;
*
*   COMPARAISON SOL ANALYTIQUE
*   ecart
*
ER12 =(MAXI ABS ( SP12 - SPTH12 ))/(MAXI ABS SPTH12);
*
SAUT 1 LIGN;
MESS ' Ecart  relatif en contraintes  sig12     :' ER12 ;
*
*
*==================================================================*
*                  code fonctionnement                             *
*==================================================================*
*
* L'ecart maximum entre valeurs de reference et calculee doit etre
* inferieur a 5.d-2
*
SAUT 2 LIGNE;
    SI (ER12 <EG 0.05 ) ;
        ERRE 0;
    SINON;
        ERRE 5;
    FINSI;
*
SAUT 1 LIGN;
*
FIN;










