* fichier :  elas20.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*           Test Elas20.dgibi: Jeux de données      *
*           ----------------------------------      *
*                                                   *
SAUT PAGE ;
*****************************************************
*                                                   *
* TEST TUYAU DROIT ET COUDE SOUS PRESSION           *
*                                                   *
* description :                                     *
* -----------                                       *
*                                                   *
*   Test en statique lineaire, chargement pression. *
*                                                   *
* La structure est modelisee en elements TUYAU :    *
*   - 2 parties droites de longueur 3.048m maillées *
*     par 5 éléments chacune.                       *
*   - 1 coude entre ces deux parties droites de     *
*     rayon de courbure égal à 0.61m, maillé par    *
*     10 éléments.                                  *
*                                                   *
* Propriétés des matériaux :                        *
*   - Module d'Young         : YOUN1 = 2.e11        *
*   - Coefficient de Poisson : NU1   = 0.3          *
*                                                   *
* Chargement :                                      *
*   - Pression               : PRES1 = 15 Mpa       *
*                                                   *
* Conditions limites :                              *
*   - Encastrement a l'extrémité PA                 *
*                                                   *
*****************************************************
OPTI ECHO 1 ;
*
OPTI DIME 3 ELEM SEG2 ;
*
GRAPH = 'N' ;
*
*****************************************************
*            GEOMETRIE : MAILLAGE                   *
*****************************************************
*
OEIL = 0. 0. 10. ;
*
LDRO1 = 3.048 ;
RCO1  = 0.61 ;
LTOT1 = LDRO1 + RCO1 ;
*
PA  = 0. 0. 0. ;
PAP = 0. LDRO1 0. ;
PBP = RCO1 LTOT1 0. ;
PB  = LTOT1 LTOT1 0. ;
*
LIG1 = PA D 5 PAP ;
LIG2 = PBP D 5 PB ;
LCOU1 = CERC 10 PAP ( RCO1 LDRO1 0. ) PBP ;
*
LIGNE = LIG1 ET LCOU1 ET LIG2 ;
*
TASS LIGNE ;
ELIM LIGNE 1.E-5 ;
*
'SI' ( 'NEG' GRAPH 'N' ) ;
   'TITRE' 'TUYPRES : MAILLAGE' ;
   TRAC LIGNE OEIL QUAL ;
'FINSI' ;
*
*****************************************************
*                MODELES                            *
*****************************************************
*
OBLI1 = MODE LIG1 MECANIQUE ELASTIQUE TUYAU ;
OBLI2 = MODE LIG2 MECANIQUE ELASTIQUE TUYAU ;
OBCO1 = MODE LCOU1 MECANIQUE ELASTIQUE TUYAU ;
OBLIGN = OBLI1 ET OBLI2 ET OBCO1 ;
*
*****************************************************
*          VALEUR DE LA PRESSION                    *
*****************************************************
*
PRES1 = 15.E6 ;
*
*****************************************************
*               MATERIAUX                           *
*****************************************************
*
MATLI1 = 'MATE' ( OBLI1 ET OBLI2 ) 'YOUN' 2.1E11 'NU' 0.3 ;
MATCO1 = 'MATE' OBCO1 'YOUN' 2.1E11 'NU' 0.3 ;
MATLIGN = MATLI1 ET MATCO1 ;
*
*****************************************************
*             CARACTERISTIQUES                      *
*****************************************************
*
CARLI1 = 'CARA' OBLI1 'EPAI' 26.187E-3 'RAYO' 203.2E-3
         'VECT' ( 0. 0. -1. ) 'PRES' PRES1 ;
CARLI2 = 'CARA' OBLI2 'EPAI' 26.187E-3 'RAYO' 203.2E-3
         'VECT' ( 0. 0. -1. ) 'PRES' PRES1 ;
CARCO1 = 'CARA' OBCO1 'EPAI' 26.187E-3 'RAYO' 203.2E-3
         'RACO' 0.61 'VECT' ( 0. 0. -1. ) 'PRES' PRES1 ;
CARLIGN = CARLI1 ET CARLI2 ET CARCO1 ;
*
CARTOT = MATLIGN ET CARLIGN ;
*
*****************************************************
*          CONDITIONS LIMITES                       *
*****************************************************
*
RIGBLOQ = 'BLOQ' 'DEPL' 'ROTA' PA ;
*
*****************************************************
*          CALCUL DE LA RIGIDITE                    *
*****************************************************
*
RIGLIGN = 'RIGI' OBLIGN CARTOT ;
*
RIGTOT = RIGLIGN ET RIGBLOQ ;
*
*****************************************************
*             CHARGEMENT                            *
*****************************************************
*
FPRES1 = pres tuya OBLIGN CARTOT ;
*
*****************************************************
*             RESOLUTION                            *
*****************************************************
*
DEPL1 = 'RESO' RIGTOT FPRES1 ;
*
*****************************************************
*        EXTRACTION DES RESULTATS                   *
*****************************************************
*
VXB1 = 0.822E-3 ;
UXB1 = 'EXTR' DEPL1 'UX' PB ;
ERG1 = 100. * ( 'ABS' (( VXB1 - UXB1 ) / VXB1 )) ;
*
VYB1 = VXB1 ;
UYB1 = 'EXTR' DEPL1 'UY' PB ;
ERG2 = 100. * ( 'ABS' (( VYB1 - UYB1 ) / VYB1 )) ;
*
VXBP1 = 0.137E-3 ;
UXBP1 = 'EXTR' DEPL1 'UX' PBP ;
ERG3 = 100. * ( 'ABS' (( VXBP1 - UXBP1 ) / VXBP1 )) ;
*
VYBP1 = 0.822E-3 ;
UYBP1 = 'EXTR' DEPL1 'UY' PBP ;
ERG4 = 100. * ( 'ABS' (( VYBP1 - UYBP1 ) / VYBP1 )) ;
*
VYAP1 = 0.685E-3 ;
UYAP1 = 'EXTR' DEPL1 'UY' PAP ;
ERG5 = 100. * ( 'ABS' (( VYAP1 - UYAP1 ) / VYAP1 )) ;
*
*****************************************************
*         AFFICHAGE DES RESULTATS                   *
*****************************************************
*
'MESS' 'RESULTATS : TEST TUYPRES' ;
'MESS' '------------------------' ;
SAUT LIGN ;
*
'MESS' ' deplacement ux (m) du point PB' ;
'MESS' ' ------------------------------' ;
'MESS' '         Valeur theorique : ' VXB1 'm' ;
'MESS' '         Valeur calculee  : ' UXB1 'm' ;
'MESS' '                   Ecart  : ' ERG1 '%' ;
SAUT LIGN ;
*
'MESS' ' deplacement uy (m) du point PB' ;
'MESS' ' ------------------------------' ;
'MESS' '         Valeur theorique : ' VYB1 'm' ;
'MESS' '         Valeur calculee  : ' UYB1 'm' ;
'MESS' '                   Ecart  : ' ERG2 '%' ;
SAUT LIGN ;
*
'MESS' ' deplacement ux (m) du point PBP' ;
'MESS' ' -------------------------------' ;
'MESS' '         Valeur theorique : ' VXBP1 'm' ;
'MESS' '         Valeur calculee  : ' UXBP1 'm' ;
'MESS' '                   Ecart  : ' ERG3 '%' ;
SAUT LIGN ;
*
'MESS' ' deplacement uy (m) du point PBP' ;
'MESS' ' -------------------------------' ;
'MESS' '         Valeur theorique : ' VYBP1 'm' ;
'MESS' '         Valeur calculee  : ' UYBP1 'm' ;
'MESS' '                   Ecart  : ' ERG4 '%' ;
SAUT LIGN ;
*
'MESS' ' deplacement uy (m) du point PAP' ;
'MESS' ' -------------------------------' ;
'MESS' '         Valeur theorique : ' VYAP1 'm' ;
'MESS' '         Valeur calculee  : ' UYAP1 'm' ;
'MESS' '                   Ecart  : ' ERG5 '%' ;
*
ERGMAX = 'MAXI' ( 'PROG' ERG1 ERG2 ERG3 ERG4 ERG5 ) ;
*
'SI' ( ERGMAX <EG 5.) ;
  ERRE 0 ;
'SINON' ;
  ERRE 5 ;
'FINSI' ;
SAUT LIGN ;
*
TEMPS ;
*
FIN ;











