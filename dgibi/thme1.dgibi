* fichier :  thme1.dgibi
*           Test Thme1.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
*------------------------------------------------------
*
* Calcul thermo-mécanique ( mécanique et thermique
* linéaire ). Utilisation de la procédure PASAPAS
*
*
*   |
*   |_____________________________________
*   |                                     |
*   |            T_initial = 20°C         | x1 = 0.1 m
*   |_____________________________________| T1 = 500°C
*   | x0 = 0 m
*   | T0 = 20°C
*     Encastrement
*
*   on étudie la diffusion thermique dans le barreau
*   et sa dilatation
*
*   caracteristiques du barreau :
*
*         l = 0.1m
*         K = 14.6
*         C = 460
*         RHO = 7800
*         ALPHA = 1E-5
*         YOUNG = 2.E11
*         NU = 0.00001
*
*
*   Mailles de 0.01 m de cote
*
*   On compare les résultats à une solution analytique
*
*------------------------------------------------------

OPTION ECHO 1;
OPTION DIME 2 ELEM QUA8 MODE PLAN DEFO;
*
*-------------------- Creation du maillage ------------
*
dens 1;
PA = 0.   0.  ;
PB = 0.1   0.  ;
PC = 0.1   0.01 ;
PD = 0.   0.01 ;
pp8 = 0.08 0.;
D1 = DROI 10 PA PB ;
D2 = DROI 1  PB PC ;
D3 = DROI 10 PC PD ;
D4 = DROI 1  PD PA ;
L1 = D1 ET D2 ET D3 ET D4;
SU1 = SURF L1 PLANE ;
pp8 = su1 point proc pp8;
*
*-------------------- Définition du modèle ------------
*
MOD1 = MODE SU1 THERMIQUE ISOTROPE CONS UN;
MOD2 = MODE SU1 MECANIQUE ELASTIQUE ISOTROPE CONS DEUX;
MOD = MOD1 ET MOD2 ;
*
*------------------- Définition des matériaux ---------
*
MAT1 = MATE MOD1 K 14.6 'C' 460. RHO 7800.  ;
MAT2 = MATE MOD2 ALPHA 1E-5 YOUN 2E+11 NU 0.00001;
MAT = MAT1 ET MAT2 ;
*
*------------------- blocages mécaniques --------------
*
CL1 = BLOQ UY SU1 ;
CL2 = BLOQ UX D4 ;
CL_ME = CL1 ET CL2;
*
*------------------- blocages thermiques --------------
*
CL3 = BLOQ T D2;
CL4 = BLOQ T D4;
CL_TH = CL3 ET CL4;
*
*------------------- températures imposées ------------
*
TEM1 = DEPI CL3 500.;
TEM2 = DEPI CL4 20.;
EV1 = EVOL MANU x (PROG 0. 200.) y (prog 1. 1.);
CHA = CHAR TIMP (TEM1 + TEM2) EV1;
*
*------------------- température initiale -------------
*
TEM0 = MANU CHPO SU1 1 T 20.;
*
*--------------- creation de la table pour pasapas ----
*
ta = table;
ta.modele = mod;
ta.caracteristiques = mat;
ta.blocages_mecaniques = cl_me;
ta.blocages_thermiques = cl_th;
ta.temperatures = table;
ta.temperatures . 0 = tem0;
ta.chargement = cha;
ta.procedure_thermique = LINEAIRE;
ta.temps_calcules = prog 0. pas 5. 150.;
*
pasapas ta;
*
*----------------------- Depouillement ----------------
*
ltref1 = prog 10. pas 10. 150.;
a = (14.6 / 7800.)/460.;
x = 0.08;
er1 = 0.;
er2 = 0.;
nb = 0;
tpr1 = prog;
tpr2 = prog;
tpr3 = prog;
tep_th = prog;
dep_th = prog;
idim = 'DIME' ltref1 ;
solteenr = prog
   32.779       76.142       116.26       148.47
   174.30       195.40       212.98       227.88
   240.70       251.85       261.65       270.33
   278.08       285.03       291.30  ;
 soldeenr = prog
 4.51064E-07  3.18778E-06  7.26909E-06  1.18402E-05
 1.65525E-05  2.12583E-05  2.58885E-05  3.04069E-05
 3.47909E-05  3.90251E-05  4.30987E-05  4.70048E-05
 5.07398E-05  5.43030E-05  5.76962E-05;
repeter bou idim;
   ite = &bou;
   te = 'EXTR' ltref1 ite ;
   dep = PECHE ta deplacements te ;
   tem = PECHE ta temperatures te ;
   tep1 = extr tem T pp8;
   dep1 = extr dep UX pp8;
   tpr1 = tpr1 et (prog tep1);
   tpr2 = tpr2 et (prog dep1);
   tpr3 = tpr3 et (prog te);
si complet;
   co1 = erf((0.1 - x)/(2 * (( a * te )**0.5)));
   co2 = erf(0.1/(2.*(( a * te )**0.5)));
   sol_te = 500. - ((500. - 20.)*co1/co2);
sinon;
    sol_te = extr solteenr ite;
finsi;
   tep_th = tep_th et (prog sol_te);
si complet;
   mi = prog;
   mi1 = prog ;
   repeter bou2  21;
      ite2 = &bou2 - 1;
      te1 = x * ite2 / 20;
      mi = mi et (prog te1);
      y1 = erf((0.1 - te1)/(2.*(( a * te )**0.5)));
      mi1 = mi1 et (prog y1);
   fin bou2;
   ev5 = evol manu xx mi y mi1;
   res = extr (INTG ev5) 1;
   sol_de = 1E-5*(500. - 20.)/co2*((x * co2) - res);
sinon;
    sol_de = extr soldeenr ite;
finsi;
   dep_th = dep_th et (prog sol_de);
   si ((flot ite) >  ((flot idim)/2) );
     er1 = er1 + (abs ((sol_te - tep1)/sol_te) * 100.);
     er2 = er2 + (abs ((sol_de - dep1)/sol_de) * 100.);
      nb = nb + 1;
   finsi;
fin bou;
*
er1 = er1/nb;
er2 = er2/nb;
mess 'erreur sur la température :'er1'%';
mess 'erreur sur le deplacement :'er2'%';
*
*lvar =text 'EVOLUTION DE LA TEMPERATURE AU POINT'x'm';
*titre lvar;
*ev_te = evol manu temps tpr3 temperature tpr1;
*ev_tet = evol manu temps tpr3 temperature tep_th;
*dess (ev_te et ev_tet);
*lvar = text 'EVOLUTION DU DEPLACEMENT AU POINT'x'm';
*titre lvar;
*ev_de = evol manu temps tpr3 deplacement tpr2;
*ev_det = evol manu temps tpr3 deplacement dep_th;
*dess (ev_de et ev_det);
*
si ( er1 > 2. ) ;
  mess 'Résultat incorrect' ;
  erreur 5 ;
finsi ;
si ( er2 > 5. ) ;
  mess 'Résultat incorrect' ;
  erreur 5 ;
finsi ;
*
fin;











