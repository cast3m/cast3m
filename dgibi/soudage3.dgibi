* fichier : soudage3.dgibi
* Section : Mecanique Plastique
* Section : Thermique Conduction
OPTI ECHO 0 ;
OPTI TRAC 'PSC' ;
GRAPH = FAUX ;
************************************************************************
* Debut du jeu de donnees de soudage multipasse H. Pommier 12/03/2017
*
*  OPTIONS GENERALES
*  - 3D
*  - Grande Deformations
*
* THERMIQUE
*  - Source de chaleur volumique (Goldak) dépendant du temps
*  - Conduction dans la matière
*  - Convection les bords hors cordon de soudure
*  - Proprietes matériaux qui dépendent de T
*
* MECANIQUE
*  - Dilatation thermique
*  - Proprietes matériaux qui dépendent de T
*  - Materiaux ELASTIQUE "mou" dans les zones inactives
*  - Matériaux elastoplastique dans les zones actives
*  - Perte des proprietés mecaniques à l'état liquide           (PERSO1)
*  - Activation des élèments dès qu'ils ont fondus une 1ère fois(PERSO2)
*
************************************************************************

***PROCEDURES

'DEBP' PERSO1   TAB1*'TABLE'                   ;
************************************************************************
* PROCEDURE PERSO1 : Apres la MECANIQUE dans PASAPAS
*   - Annnule la deformation plastique cumulee
*   - Annnule la deformation inelastique
* lorsque les elements sont à l'etat Liquide (T > Tfusion)
************************************************************************

  NB_PAS  = (DIME TAB1. 'TEMPS')                    ;

  WTAB    = TAB1.'WTABLE' ;
  MODMECA = EXTR WTAB.'MO_TOTAL' 'FORM' 'MECANIQUE' ;
  MODTHER = EXTR WTAB.'MO_TOTAL' 'FORM' 'THERMIQUE' ;
  MODTCOND= EXTR MODTHER         'COMP' 'CONDUCTION';

  EIDITH  = REDU TAB1.'ESTIMATION'.'DEFORMATIONS_INELASTIQUES' MODMECA ;
  EPSETH  = REDU TAB1.'ESTIMATION'.'VARIABLES_INTERNES'        MODMECA ;

* Recuperation des ELEMENTS LIQUIDES dans le Champs de TEMPERATURE
  CHPTEMP = TAB1.'ESTIMATION'.'TEMPERATURES';
  CHATEMP = CHAN 'CHAM' CHPTEMP MODMECA 'SCALAIRE'              ;
  ELEMSUP = ELEM CHATEMP 'SUPE' T_FUSION 'STRI'                      ;

  SI ((NBEL ELEMSUP) '>' 0 );
    MODSUP  = REDU MODMECA ELEMSUP ;
    MODINF  = REDU MODMECA (DIFF (EXTR MODMECA 'MAIL' ) ELEMSUP) ;

*   Deformations inelastiques
    LMOTS2 = EXTR EIDITH 'COMP' ;
    CHELNEW = VIDE 'MCHAML' ;
    CHELCAL = EIDITH        ;
    REPE ICOMP (DIME LMOTS2);
      MOT4   = EXTR LMOTS2 &ICOMP ;
      CHELSUP= MANU 'CHML'  MODSUP MOT4 0.D0
                        'TYPE' 'DEFORMATIONS_INELASTIQUES' 'STRESSES' ;
      CHELNEW= CHELNEW ET CHELSUP ;
    FIN ICOMP;
    CHELINF= REDU CHELCAL MODINF  ;
    EIDITH = CHELNEW 'ET' CHELINF ;

*   Variables internes
    LMOTS2 = EXTR EPSETH 'COMP' ;
    CHELNEW = VIDE 'MCHAML' ;
    CHELCAL = EPSETH        ;
    REPE ICOMP (DIME LMOTS2);
      MOT4   = EXTR LMOTS2 &ICOMP ;
      CHELSUP= MANU 'CHML'  MODSUP MOT4 0.D0
                        'TYPE' 'VARIABLES_INTERNES'        'STRESSES' ;
      CHELNEW= CHELNEW ET CHELSUP ;
    FIN ICOMP;
    CHELINF= REDU CHELCAL MODINF  ;
    EPSETH = CHELNEW 'ET' CHELINF ;
*   Mise a jour dans la TABLE
    TAB1.'ESTIMATION'.'DEFORMATIONS_INELASTIQUES' = EIDITH ;
    TAB1.'ESTIMATION'.'VARIABLES_INTERNES'        = EPSETH ;
  FINS;

'FINP' TAB1;

'DEBP' PERSO2   TAB1*'TABLE'                  ;
************************************************************************
* PROCEDURE PERSO2 : Apres la THERMIQUE dans TRANSNON
* - met a jour le MODELE et le MATERIAU MECANIQUE : DEGRADEES -> NOMINALES
* - met a jour la TEMPERATURE_REFERENCE pour les elements fondus
* - ATTENTION : La temperature a jour est dans l'indice WTAB.'THER_COURANT'
************************************************************************

  MPASSE = 'CHAI' 'PERSO_2 : PASSE numero ' I ;

  WTAB = TAB1 . 'WTABLE' ;

  NB_PAS  = (DIME TAB1.'TEMPS')                     ;

  MODMECA = EXTR WTAB.'MO_TOTAL' 'FORM' 'MECANIQUE' ;
  MODTHER = EXTR WTAB.'MO_TOTAL' 'FORM' 'THERMIQUE' ;
  MODTCOND= EXTR MODTHER         'COMP' 'CONDUCTION';

  MAIMECA = EXTR MODMECA 'MAILLAGE'                 ;

  MATTOT  = TAB1.'CARACTERISTIQUES'                 ;

  MAIPASS = EXTR SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'. I
                'MAILLAGE'                                            ;
  MOMECNT = SOUDAGE.'MODELE'   .'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL';
  MOMECDT = SOUDAGE.'MODELE'   .'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL';

  MODPIECE= SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'.'TOTAL'              ;
  MAIPIECE= EXTR MODPIECE 'MAILLAGE'                                  ;

  MAILNTOT= EXTR MOMECNT'MAILLAGE'                                    ;
  MODTHAP = SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'. I                  ;

* Recuperation des ELEMENTS LIQUIDES dans le Champs de TEMPERATURE
  CHATEMP = CHAN 'CHAM' WTAB.'THER_COURANT' MODMECA 'TEMPERATURES'  ;
  CHATEMP = BORN CHATEMP 'T' 'MINI' T_INIT                            ;
  ELEMSUP = ELEM CHATEMP 'SUPE' T_FUSION 'LARGEMENT'                ;

************************************************************************
* Traitement du MODELE NOMINAL / DEGRAGE
************************************************************************
  ELEMOUT = INTE (DIFF MAIMECA MAIPASS) ELEMSUP ;
  ELSUPAS = DIFF ELEMSUP ELEMOUT                ;

* Construction du MAILLAGE des ELEMENTS actifs nouveaux
  MAILINI = TAB1 .'MAILLAGE_ACTIF'. (NB_PAS-1)  ;
  MAILPOT = INTE MAIPASS ELSUPAS                ;
  MAILINT = INTE MAILPOT MAILINI                ;
  MAILADDI= (DIFF ELSUPAS MAILINT) COUL BLEU    ;
  MAILNOMI= MAILINI ET MAILADDI                 ;

   BTEST = FAUX;

* Mise a jour du MODELE et du MATERIAUX si on a des ELEMENTS ADDITIONNELS
  SI ((NBEL MAILADDI) '>' 0);
    MODNOMI = REDU MOMECNT MAILNOMI  ;
    MODMECA = MODPIECE               ;
    MAILDEGR= DIFF MAILNTOT MAILNOMI ;
    MODMECA = MODMECA ET MODNOMI     ;

    SI ((NBEL MAILDEGR) '>' 0);
      MODMECA = MODMECA ET (REDU MOMECDT MAILDEGR);
    FINS;

    MATMECA = REDU MATTOT MODMECA ;

*   Mise a jour dans la TABLE pour le PAS suivant
    WTAB. 'MOD_MEC'         = MODMECA            ;
    WTAB. 'MAT_MEC'         = MATMECA            ;

    WTAB. 'MO_TOT'          = MODMECA            ;
    WTAB. 'MA_TOT'          = MATMECA            ;

*   Indice obligatoire a renseigner car PAS_ETAT travaille sur MO_TOTAL
    WTAB. 'MO_TOTAL'       = MODMECA ET MODTHER ;
    WTAB. 'MA_TOTAL'       = MATMECA ET MATTHER ;

*   Enregistrement du MAILLAGE_ACTIF
    TAB1.'MAILLAGE_ACTIF'. NB_PAS = MAILNOMI         ;
    TAB1.'MES_MODEL'     . NB_PAS = WTAB. 'MO_TOTAL' ;
*ajout HP 14/04/2017
    TAB1.'MODELE'      = WTAB. 'MO_TOTAL' ;

    BTEST = VRAI;
  SINO;
    SI (NON (EXIS TAB1.'MAILLAGE_ACTIF' NB_PAS));
*     Recuperation depuis le PAS precedent
      TAB1.'MAILLAGE_ACTIF'. NB_PAS = MAILINI                     ;
      TAB1.'MES_MODEL'     . NB_PAS = TAB1.'MES_MODEL'.(NB_PAS-1) ;
    FINS;
  FINS;

************************************************************************
* Traitement des ELEMENTS FONDUS
************************************************************************
* Construction du MAILLAGE des ELEMENTS fondus nouveaux
  MAILOK  =(DIFF MAIMECA MAILNTOT) 'ET' MAILNOMI                   ;
  MODOK   = REDU MODMECA MAILOK                                    ;
  CHATEMP = CHAN 'CHAM' WTAB.'THER_COURANT' MODMECA 'TEMPERATURES' ;
  CHATEMP = REDU CHATEMP MODOK                                     ;
  ELEMSUP = ELEM CHATEMP 'SUPE' T_FUSION 'LARGEMENT'             ;
  MAILINI = TAB1.'MAILLAGE_FONDU'. (NB_PAS-1)                      ;
  MAILINT = INTE ELEMSUP MAILINI                                   ;
  MAILADDI=(DIFF ELEMSUP MAILINT) COUL BLEU                        ;

* Recalcul de 'TEMPERATURE_REFERENCE' sur le nouveau modele
  SI ((NBEL MAILADDI) '>' 0 );
    MAILFOND = MAILINI ET MAILADDI                                     ;
    MAILNORM = DIFF MAIMECA  MAILFOND                                  ;
    MODFOND  = REDU MODMECA  MAILFOND                                  ;
    MODNORM  = REDU MODMECA  MAILNORM                                  ;
    CHATEMP  = CHAN 'CHAM' WTAB.'THER_COURANT' MODMECA 'TEMPERATURES'  ;
    TREFSUP  = BORN (REDU CHATEMP MODFOND) 'T' 'MINI' T_FUSION       ;
    TREFDIF  = MANU 'CHML'  MODNORM 'T' T_INIT   'TYPE' 'TEMPERATURES' ;
    WTAB.'TEMPERATURE_REFERENCE' = TREFSUP 'ET' TREFDIF                ;
    WTAB.'ETAT_REF'              ='MOT' 'INCONNU'                      ;

*   Enregistrement du MAILLAGE_ACTIF du MODELE et du MAILLAGE_FONDU
    TAB1.'MAILLAGE_FONDU'. NB_PAS = MAILFOND ;
  SINO;
*  Recuperation depuis le PAS precedent
TAB1.'MAILLAGE_FONDU'. NB_PAS = MAILINI  ;

*  Ajout HP 18/04/2017
*  Diminution TREF jusqua temperature fusion 1400 au refroidissement
* Construction du MAILLAGE des ELEMENTS refroidis
SI ((NBEL TAB1.'MAILLAGE_FONDU'. NB_PAS) '>EG' 1) ;
*MODRE est le modele meca reduit au modele fondu
MODRE = REDU MODMECA (TAB1.'MAILLAGE_FONDU'. NB_PAS) ;
*CHTRE est le champ par elem des temperatures de MODRE
CHTRE = CHAN 'CHAM' WTAB.'THER_COURANT' MODRE 'TEMPERATURES' ;
*ELEMRE est l ensemble des elements du maillage fondu
*dont CHTRE est strict inf a Tfus
ELEMRE  = ELEM CHTRE 'INFE' T_FUSION 'STRI'                       ;
*MODRE est le modele meca reduit a ELEMRE, ie a l ensemble des elements
*du maillage fondu dont la temp est strict inf a Tfus
MODRE  = REDU MODMECA  ELEMRE                                  ;
*TREFRE est le champ par elements de Tfus sur MODRE
TREFRE  = MANU 'CHML'  MODRE 'T' T_FUSION   'TYPE' 'TEMPERATURES' ;
*MTREFF est le maillage fondu comprenant uniquement les elements
*dont la temp est sup ou eg a Tfus
MTREFF = DIFF (TAB1.'MAILLAGE_FONDU'. NB_PAS) ELEMRE ;
*MODTOL est le modele meca reduit aux elements du maillage fondu
*de T >= Tfus
MODTOL = REDU MODMECA MTREFF ;
OLDTREF = REDU (WTAB.'TEMPERATURE_REFERENCE') MODTOL ;
MAIL0 =  EXTR MODMECA 'MAILLAGE' ;
MAIL1 = DIFF MAIL0 (TAB1.'MAILLAGE_FONDU'. NB_PAS) ;
MOD0 = REDU MODMECA MAIL1 ;
TREF0  = MANU 'CHML'  MOD0 'T' T_INIT   'TYPE' 'TEMPERATURES' ;
WTAB.'TEMPERATURE_REFERENCE' =
(OLDTREF 'ET' TREFRE) ET TREF0    ;
FINS;
* Fin ajout HP 18/04/2017
FINS;
'FINP' TAB1;

************************************************************************
* NOM         : SOUGOLD0
* DESCRIPTION : Source normalisée GOLDAK
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Olivier ASSERIN (CEA/DEN/DM2S/SEMT/LTA)
*               mél : olivier.asserin@cea.fr
************************************************************************
* VERSION    : v1, 17/07/2008, version initiale
* HISTORIQUE : modification pour PASAPAS H. POMMIER 02-2017
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
************************************************************************
DEBP SOUGOLD0 mo_sou*'MMODEL' P_Sour*'LISTREEL' pos_sou*'POINT'
theta1*'FLOTTANT' wd*'FLOTTANT' tt*'FLOTTANT' dirs*'LISTREEL' ;
xglo yglo zglo = 'COORDONNEE' ( EXTR mo_sou 'MAILLAGE' ) ;
*** repere local(xs,ys,zs)
xsou ysou zsou = COOR pos_sou  ;
xxloc = xglo - xsou ;
yyloc = yglo - ysou ;
zzloc = zglo - zsou ;

*** Rotation de theta1 autour de l'axe des X
*** puis rotation de theta2 autour de l'axe du nouvel axe des Z
cos1 = COS theta1 ;
sin1 = SIN theta1 ;
theta2 = '*' (ACOS (EXTR dirs 1)) (SIGN (EXTR dirs 2)) ;
theta2 = 0. ;
cos2 = COS theta2 ;
sin2 = SIN theta2 ;

xloc = (cos2*xxloc)+((cos1*sin2)*yyloc)+((sin1*sin2)*zzloc) ;
yloc = ((-1.*sin2)*xxloc)+(cos1*(cos2*yyloc))+((sin1*cos2)*zzloc) ;
zloc = ((-1.*sin1)*yyloc)+(cos1*zzloc) ;

Q =  EXTR P_Sour 1 ;
a =  EXTR P_Sour 2 ;
b =  EXTR P_Sour 3 ;
Cf = EXTR P_Sour 4 ;
Cr = EXTR P_Sour 5 ;
*** * contribution de la source en y et z
cy = EXP ( (-3./(a**2.)) * (yloc * yloc) ) ;
cz = EXP ( (-3./(b**2.)) * (zloc * zloc) ) ;
*** * on calcule la source en avant du front
fil_av = xloc MASQ egsupe (-1.e-10) ;
fil_ar = fil_av MASQ inferieur (1.e-6) ;
cx_av = EXP ( ( -3. / ( Cf ** 2. ) ) * (xloc * xloc) ) ;
cx_ar = EXP ( ( -3. / ( Cr ** 2. ) ) * (xloc * xloc) ) ;
cx_av = fil_av * cx_av ;
cx_ar = fil_ar * cx_ar ;
cx = cx_av + cx_ar ;
Rf = 2. / (1. + ( Cr / Cf ) ) ;
Pi1 = (3.14159265358979**1.5) ;
qmax = (6 * (3.**0.5) * Rf * Q) / (Pi1 * Cf * a * b) ;
cz2 = zloc MASQ 'EGINFE'  ( 0. ) ;
CHPO_SOM = qmax * cx * cy * cz  ;

SI (tt '>' wd) ;

CHPO_SOM = '*' CHPO_SOM 0. ;
FINSI ;
FINP  CHPO_SOM ;

************************************************************
*********CHARTHER
DEBP charther HTAB*'TABLE' tt*'FLOTTANT' ;
TAA=TABLE ; ETAB=PRECED.'WTABLE' ;

modther = EXTR HTAB.'MES_MODEL'. 0 'FORM' 'MECANIQUE' ;

*position source
xsu = '*' ('-' tt ('*' (I-1) tfin)) vit0 ;
vecp = xsu 0. 0. ;
pos_sou = 'PLUS' SOUDAGE.'CHARGEMENT'.'XYZ'. I vecp ;
ang_sou = 0. ;
dirs = PROG 0. 0. 1. ;
pvol = SOUGOLD0 modther P_sour pos_sou ang_sou wdur tt dirs ;

**Correction puissance
Puivo = CHAN 'CHAM' pvol modcond ;
qvol = (INTG modcond Puivo) ;
qsours = qvol ;
facteur = 1. ;
QVol0 = EXTR P_sour 1 ;
QSurf0 = 0. ;
SI (qsours 'NEG' 0.) ;
puiseff = '/' ('+' QVol0 QSurf0) facteur ;
rfsour =  qvol  ;
cf = '/' puiseff qsours ;
pvol = '*' pvol cf ;
Puivo2 = CHAN 'CHAM' pvol modcond ;
qvol2 = (INTG modcond Puivo2) ;
qsours2 = qvol2 ;
FINS ;

*CHARGEMENT
charsou = SOUR pvol modcond ;

* sortie du second membre
TAA . 'ADDI_SECOND' = charsou ;

FINP TAA;

************************************************************************
************************************************************************
************************************************************************
************************************************************************
************************************************************************
**************DEBUT MISE EN DONNES
************************************************************************
************************************************************************
* OPTIONS GENERALES DU CALCUL
OPTI DIME 3         ;
OPTI ELEM 'CUB8' ;

*GEOMETRIE
N = 1 ; COMM 'Nombre de couche' ;
P = 1 ; COMM 'Nombre de passe par couche' ;
*Construction carre elementaire
l0 = 0.005 ; COMM 'cote carre' ;
nl0 = 2 ;
vtran = ('*' nl0 l0) 0. 0. ; COMM 'longueur plaque' ;
vit0 = 0.001 ; COMM 'Vitesse soudage' ;
tfin = 3600. ; COMM 'Temps final' ;
wdur = '/' ('*' ('-' nl0 1) l0) vit0 ;


************************************************************************
*                         PARAMETRES DU SOUDAGE                        *
************************************************************************
N_PIECE = 1       ;
N_PASSE = '*' N P ;
T_INIT  = 20.     ;
TAREF   = -273.15 ;
T_CONV  = T_INIT  ;
T_FUSION = 1400.  ;

Q =  2000. ;
a =  0.005 ;
b =  0.005 ;
Cf = 0.005 ;
Cr = 0.01 ;
P_sour = PROG Q a b Cf Cr ;

* Construction de la TABLE SOUDAGE pour stocker l'etude
SOUDAGE                               = TABL ;
SOUDAGE.'CONSTITUANTS'                = TABL ;
SOUDAGE.'CONSTITUANTS'.'PIECE'        = TABL ;
SOUDAGE.'CONSTITUANTS'.'APPORT'       = TABL ;

* Creation de la TABLE des CONSTITUANTS
SOUDAGE. 'CONSTITUANTS'. 'PIECE'. 1 = MOT '316L'   ;

'REPETER' INDICE N_PASSE ;
   I = &INDICE ;
   SOUDAGE.'CONSTITUANTS'.'APPORT'.I = MOT '316L' ;
'FIN' INDICE ;

* Creation de la TABLE des sequences de la source
SOUDAGE. 'CHARGEMENT'                 = TABL ;
SOUDAGE. 'CHARGEMENT'.'MECANIQUE'     = TABL ;
SOUDAGE. 'CHARGEMENT'.'THERMIQUE'     = TABL ;
SOUDAGE . 'TEMPS_CALCULES' = TABL ;
SOUDAGE . 'TEMPS_SAUVES' = TABL ;

'REPETER' INDICE N_PASSE ;
I = &INDICE ;
pas1 = 0.5 ;
pas2 = 5. ;
pas3 = 500. ;
ltcal = PROG pas1 'PAS' pas1  wdur 'PAS' pas2
('+' wdur 60.) 'PAS' pas3 tfin ;
SOUDAGE.'TEMPS_CALCULES'. I = tfin '*' (I-1) '+' ltcal ;
'FIN' INDICE ;

************************************************************************
*                               MAILLAGES                              *
************************************************************************
*Tables pour couleur passes
TABCOUL1 = TABLE;
TABCOUL2 = TABLE;
TABCOUL1.1 = ROSE ;
TABCOUL1.2 = VERT ;
TABCOUL1.3 = JAUN ;
TABCOUL1.4 = OCEA ;
TABCOUL1.5 = ROSE ;
TABCOUL1.6 = TURQ ;
TABCOUL1.7 = ROUG ;
TABCOUL1.8 = BLEU ;
TABCOUL1.9 = TURQ ;
TABCOUL1.10 = AZUR ;
TABCOUL1.11 = ORAN ;
TABCOUL1.12 = ROUG ;
***************************************

d0 = 0.0025 ;
P1 = 0. 0. 0. ;
P2 = 0. ('*' l0 -1.) 0. ;
P3 = 0. ('*' l0 -1.) l0 ;
P4 = 0. 0. l0 ;

L12 = DROI P1 P2 'DINI' d0 'DFIN' d0 ;
L23 = DROI P2 P3 'DINI' d0 'DFIN' d0 ;
L34 = DROI P3 P4 'DINI' d0 'DFIN' d0 ;
L41 = DROI P4 P1 'DINI' d0 'DFIN' d0 ;

S1 = DALL L12 L23 L34 L41 ;
MAILP = VIDE 'MAILLAGE' ;

TMAIL = 'TABLE' ;
*PASSES
K = 1 ;
REPE BLOCN N ;
I = '-' &BLOCN 1 ;
REPE BLOCP P ;
J = '-' &BLOCP 1 ;

NPASSE = CHAI 'PASSE_' K ;
****COUL
'REPETER' BCOUL ('*' N P) ;
H = &BCOUL;
HH = '+' (@MOD H 12) 1 ;
TABCOUL2.H = TABCOUL1.HH ;
'FIN' BCOUL ;
****
JJ = '*' J -1. ;
SPASSE =
(S1 'PLUS' (0. ('*' l0 JJ) ('*' l0 I))) COUL TABCOUL2.K ;
TMAIL.NPASSE =  SPASSE  VOLU  'DINI' d0 'DFIN' d0 'TRAN' vtran ;
MAILP = MAILP ET TMAIL.NPASSE ;
K = '+' K 1 ;
FIN BLOCP ;
FIN BLOCN;

*PIECE
MPIECE1 = VIDE 'MAILLAGE' ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' (0. ('*' l0 2.) ('*' l0 -1.)))
COUL 'ROUG' ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' (0. ('*' l0 1.) ('*' l0 -1.)))
 COUL 'ROUG' ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' (0. ('*' l0 2.) ('*' l0 -2.)))
 COUL 'ROUG' ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' (0. ('*' l0 1.) ('*' l0 -2.)))
 COUL 'ROUG' ;

vec0 = 0. ('*' l0 (('*' (P + 1) -1.))) ('*' l0 -1.);
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' l0 (('*' P -1.))) ('*' l0 -1.) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' l0 (('*' (P + 1) -1.))) ('*' l0 -2.) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' l0 (('*' P -1.))) ('*' l0 -2.) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;


REPE BLOCN N ;
I = '-' &BLOCN 1 ;
vec0 = 0. l0 ('*' l0 I) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' l0 2) ('*' l0 I) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;

vec0 = 0. ('*' l0 ('*' (P + 1) -1.)) ('*' l0 I) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' l0 ('*' P -1.)) ('*' l0 I) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
FIN BLOCN;

REPE BLOCP P ;
J = '-' &BLOCP 1 ;
vec0 = 0. ('*' ('*' l0 J) -1.) ('*' l0 -1) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
vec0 = 0. ('*' ('*' l0 J) -1.) ('*' l0 -2) ;
MPIECE1 = MPIECE1 ET (S1 'PLUS' vec0) COUL 'ROUG' ;
FIN BLOCP ;
MPIECE1 = MPIECE1  VOLU  'DINI' d0 'DFIN' d0 'TRAN' vtran ;
MAIL1 = MAILP ET MPIECE1 ;

ELIM MAIL1 1.e-6 ;
AXES = @repere (0. 0. 0.) ('PROG' 0.05 0.05 0.05) 'BLAN' ;

SI GRAPH ;
MESS (CHAI 'Nombre elements : ' (NBEL MAILP) );
TRAC FACE CACHE (MAIL1 ET MPIECE1 ET AXES) ;
FINS ;

* Creation de la TABLE des MAILLAGES
SOUDAGE. 'MAILLAGE'                  = 'TABL' ;
SOUDAGE. 'MAILLAGE'. 'APPORT'        = 'TABL' ;
SOUDAGE. 'MAILLAGE'. 'PIECE'         = 'TABL' ;

* Creation de la TABLE des MAILLAGES
mail0 = ENVE MAIL1 ;
SOUDAGE.'MAILLAGE'.'PIECE'. 1 = MPIECE1   ;
MAILT = SOUDAGE.'MAILLAGE'.'PIECE'. 1 ;

* PASSES MECA
'REPETER' INDICE N_PASSE ;
   I = &INDICE ;
   NOMP = CHAI 'PASSE_' I ;
   PASSTEMP = TMAIL.NOMP ;
   SOUDAGE.'MAILLAGE'.'APPORT'. I =  PASSTEMP   ;
   MAILT = MAILT 'ET' (SOUDAGE.'MAILLAGE'.'APPORT'. I)  ;
'FIN' INDICE ;

SOUDAGE.'MAILLAGE'.'MTOT' = MAILT ;

* Verification que toutes les parties ont ete renseignees
SI ((DIME SOUDAGE. 'MAILLAGE'    . 'PIECE') NEG N_PIECE );
 MESS '    Toutes les PIECES n ont pas de MAILLAGE';
 ERRE 5;
FINSI;

SI ((DIME SOUDAGE. 'MAILLAGE'    . 'APPORT') < N_PASSE );
 MESS '    Tous les APPORTS n ont pas de MAILLAGE';
 ERRE 5;
FINSI;


SI ((DIME SOUDAGE. 'CONSTITUANTS'. 'PIECE') NEG N_PIECE );
 MESS '    Toutes les PIECES n ont pas de CONSTITUANT';
 ERRE 5;
FINSI;

SI ((DIME SOUDAGE. 'CONSTITUANTS'. 'APPORT') < N_PASSE );
 MESS '    Tous les APPORTS n ont pas de CONSTITUANT';
 ERRE 5;
FINSI;

SOUDAGE.'CHARGEMENT'.'XYZ' = 'TABL' ;
****Position soudage
K = 1 ;
REPE BLOCN N ;
I = &BLOCN ;
REPE BLOCP P ;
J = &BLOCP ;
yloc = -0.5 * l0 * (2 * J - 1) ;
zloc = '*' l0 I ;

posloc = ('/' l0 2.) yloc zloc ;
SOUDAGE.'CHARGEMENT'.'XYZ'. K = posloc ;
K = '+' K 1 ;
FIN BLOCP ;
FIN BLOCN;

************************************************************************
*                               MODELES                                *
************************************************************************
SOUDAGE.'MODELE'                                = TABLE;
SOUDAGE.'MODELE'.'PIECE'                        = TABLE;
SOUDAGE.'MODELE'.'PIECE' .'MECANIQUE'           = TABLE;
SOUDAGE.'MODELE'.'PIECE' .'THERMIQUE'           = TABLE;

SOUDAGE.'MODELE'.'APPORT'                       = TABLE;
SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'           = TABLE;
SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE' = TABLE;
SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL' = TABLE;
SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'           = TABLE;

SOUDAGE.'MODELE'.'THERMIQUE'                    = TABLE;

* PIECES a souder
SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'.'TOTAL' = VIDE 'MMODEL'   ;
SOUDAGE.'MODELE'.'PIECE'.'THERMIQUE'.'TOTAL' = VIDE 'MMODEL'   ;
REPE INDICE (DIME (SOUDAGE. 'MAILLAGE'. 'PIECE'))              ;
  I=&INDICE                                                    ;
  MAILMECA = SOUDAGE.'MAILLAGE'.'PIECE'. I                     ;
  MAILTHER = MAILMECA                                        ;

* MODELE MECANIQUE ET THERMIQUE
  CONSTI = CHAI 'PE' I ;

  MOMEC  = MODE MAILMECA 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
                                     'PLASTIQUE' 'ISOTROPE'
*                                     'TRI6' 'Q8RI'
*                                     'TRI3' 'Q4RI'
                                     'CONS' CONSTI             ;

  MOTHE  = MODE MAILTHER 'THERMIQUE' 'CONDUCTION'
                               'CONS' 'THER'             ;

  SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'. I = MOMEC              ;
  SOUDAGE.'MODELE'.'PIECE'.'THERMIQUE'. I = MOTHE              ;

  SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'.'TOTAL' =
      SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'.'TOTAL' ET MOMEC    ;
  SOUDAGE.'MODELE'.'PIECE'.'THERMIQUE'.'TOTAL' =
      SOUDAGE.'MODELE'.'PIECE'.'THERMIQUE'.'TOTAL' ET MOTHE    ;
FIN INDICE;



* APPORTS durant les differentes passes
SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL'= VIDE 'MMODEL';
SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL'= VIDE 'MMODEL';
SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL'          = VIDE 'MMODEL';
REPE INDICE (DIME (SOUDAGE. 'MAILLAGE'. 'APPORT'))                    ;
  I=&INDICE                                                           ;
  MAILMECA = SOUDAGE.'MAILLAGE'.'APPORT'. I                           ;
  MAILTHER = MAILMECA                                                 ;

* MODELE MECANIQUE ET THERMIQUE
  CONSTI = CHAI 'PD'                                            ;
  MOMECD = MODE MAILMECA 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
*                                 'TRI6' 'Q8RI'
*                                 'TRI3' 'Q4RI'
                                 'CONS' CONSTI                        ;

  CONSTI = CHAI 'PN'                                            ;
  MOMECN = MODE MAILMECA 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
                              'PLASTIQUE' 'ISOTROPE'
*                                 'TRI6' 'Q8RI'
*                                 'TRI3' 'Q4RI'
                                 'CONS' CONSTI                        ;

  MOTHE  = MODE MAILTHER 'THERMIQUE' 'CONDUCTION' 'CONS' 'THER'       ;

  SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'. I = MOMECD         ;
  SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'. I = MOMECN         ;
  SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'. I           = MOTHE          ;

  SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' =
    SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' ET MOMECD ;
  SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' =
    SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' ET MOMECN ;
  SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL' =
    SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL' ET MOTHE            ;
FIN INDICE;


* CONVECTION : Construction Automatique du CONTOUR demande
LCONT1 = ENVE (EXTR SOUDAGE.'MODELE'.'PIECE'. 'THERMIQUE'.'TOTAL'
              'MAILLAGE') ;

LCONT2 = ENVE (EXTR SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL'
              'MAILLAGE') ;
INTER1 = INTE LCONT1 LCONT2 ;
LEXTT  = DIFF LCONT1 INTER1 ;
SI GRAPH ;
TRAC LEXTT ;
FINS;
MOCONV = MODE LEXTT 'THERMIQUE' 'CONVECTION' 'CONS' 'CONVECTION';
SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL' =
  SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL' ET MOCONV       ;

************************************************************************
*                               MATERIAUX                              *
************************************************************************
*Procédure pour calculer YOUN à partir d'une évolution
'DEBP' CALYO    EV1*EVOLUTION;
X1 = EXTR EV1 'ABSC' ;
Y1 = EXTR EV1 'ORDO' ;
YOU1 = '/' (EXTR Y1 2) (EXTR X1 2) ;
'FINP' YOU1 ;
******************************************

SOUDAGE.'MATERIAUX'                                = TABLE;
SOUDAGE.'MATERIAUX'.'PIECE'                        = TABLE;
SOUDAGE.'MATERIAUX'.'PIECE' .'MECANIQUE'           = TABLE;
SOUDAGE.'MATERIAUX'.'PIECE' .'THERMIQUE'           = TABLE;

SOUDAGE.'MATERIAUX'.'APPORT'                       = TABLE;
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'           = TABLE;
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE' = TABLE;
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL' = TABLE;
SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'           = TABLE;

SOUDAGE.'MATERIAUX'.'THERMIQUE'                    = TABLE;

* Lecture des MATERIAUX de la bibliotheque
* BIBLIOTHEQUE disponible : '316L' '16MND5'

* Initialisation du CONSTITUANT precedent
NOMCONSP = 'NONDEFIN';

* PIECES a souder
SOUDAGE.'MATERIAUX'.'PIECE'.'MECANIQUE'.'TOTAL' = VIDE 'MCHAML';
SOUDAGE.'MATERIAUX'.'PIECE'.'THERMIQUE'.'TOTAL' = VIDE 'MCHAML';
REPE INDICE (DIME (SOUDAGE. 'MAILLAGE'. 'PIECE'));
  I=&INDICE;
  NOMCONS = SOUDAGE. 'CONSTITUANTS'. 'PIECE' . I ;

*   Chargement des proprietes materiaux que si le constituant change
    NOMCONSP = NOMCONS;

*   Lecture des proprietes dans la bibliotheque
*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_E.csv'            ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20. 200. 300. 400. 500. 600.
700. 800. 900. 1000. 1500. 3000. ;
        TTMP. 2 = PROG 197e9 184e9 176e9 168e9
160e9 151e9 142e9 130e9 108e9 81e9 1e9 1e9 ;
    EVYOUN  = EVOL MANU 'T' (TTMP. 1) 'YOUN' (TTMP. 2)                 ;
    MAXIT   = MAXI (TTMP. 1)                                           ;


*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Nu.csv'           ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 25 100 200 400 500 600 700 800
900 1000 1100 1200 1300 1390 1420 1450 3000 ;
        TTMP. 2 = PROG 0.296 0.298 0.304 0.315 0.32
0.323 0.326 0.33 0.336 0.339 0.346 0.349 0.353 0.353 0.353 0.353 0.353 ;
        DIMNU = DIME TTMP. 1 ;
        VALNU = TTMP. 2 ;
    EVNU    = EVOL MANU 'T' (TTMP. 1) 'NU' (TTMP. 2)                   ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Rho.csv'          ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 200 400 600 800 1000 1200 1400 1500 3000 ;
        TTMP. 2 = PROG 8.0e3 7.93e3 7.84e3 7.75e3 7.65e3 7.55e3
7.45e3 7.35e3 7.30e3 7.30e3 ;
    EVRHO   = EVOL MANU 'T' (TTMP. 1) 'RHO' (TTMP. 2)                  ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Limite_Elas.csv'  ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 200 300 400 500 600 700 800 900 1000 1500 3000 ;
        TTMP. 2 = PROG 287e6 198e6 172e6 157e6 152e6 145e6 136e6
127e6 115e6 79e6 5e6 1e6 ;
    EVSIGY  = EVOL MANU 'T' (TTMP. 1) 'R0' (TTMP. 2)                 ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Epsi_Th.csv'      ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 0 100 200 300 400 500 600 700 800 900 1000 1100
1200 1420 1460 3000 ;
        TTMP. 2 = PROG -3.17e-4 0.0013 0.003 0.0048 0.0066 0.0086 0.0106
0.0126 0.0147 0.0168 0.019 0.0212 0.0235 0.029 0.0298 0.0298 ;
    EVDEFT  = EVOL MANU 'T' (TTMP. 1) 'EPTH' (TTMP. 2)                 ;
    LALPHA  = TTMP. 2 / (TTMP. 1 - TAREF)                             ;
    EVALPHA = EVOL MANU 'T' (TTMP. 1) 'ALPH' LALPHA                    ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Ecrou_T.csv'      ;
*    TTMP1   = LIRE 'CSV' NOMDATA                                       ;
        TTMP1 = TABL ;
        TTMP1. 1 = PROG  20 200 300 400 500 600 700 800 900 1000 1500 3000 ;
*    NOMDATA = CHAI 'Materiaux/'NOMCONS'/'NOMCONS'_Ecrou_Cont_Def.csv ' ;
*    TTMP2   = LIRE 'CSV' NOMDATA                                       ;
        TTMP2   = TABL ;
        TTMP2. 1   = PROG 0. 0.01 0.05 0.2 1. 2. ;
        TTMP2. 2   = PROG 0 28e6 132e6 274e6 913e6 1369e6 ;
        TTMP2. 3   = PROG 0 33e6 136e6 272e6 802e6 1203e6 ;
        TTMP2. 4   = PROG 0 29e6 135e6 279e6 728e6 1092e6 ;
        TTMP2. 5   = PROG 0 29e6 137e6 278e6 442e6 663e6 ;
        TTMP2. 6   = PROG 0 27e6 130e6 267e6 398e6 597e6 ;
        TTMP2. 7   = PROG 0 25e6 117e6 242e6 355e6 532e6 ;
        TTMP2. 8   = PROG 0 25e6 76e6 204e6 264e6 396e6 ;
        TTMP2. 9   = PROG 0 25e6 72e6 167e6 223e6 334e6 ;
        TTMP2. 10  = PROG 0 22e6 46e6 54e6 65e6 97e6 ;
        TTMP2. 11  = PROG 0 3e6 18e6 21e6 26e6 39e6 ;
        TTMP2. 12  = PROG 0 2e6 3e6 4e6 5e6 6e6 ;
        TTMP2. 13  = PROG 0 1e6 2e6 3e6 4e6 5e6 ;

    EVTOT   = VIDE 'EVOLUTION';
    LTEMPER = VIDE 'LISTREEL' ;
    LYOU2   = VIDE 'LISTREEL' ;
    LYOU3   = VIDE 'LISTREEL' ;
    REPE ITEMPER (DIME TTMP1. 1);
      J=&ITEMPER;
      TEMPER = EXTR (TTMP1 . 1) J;
      LTEMPER= LTEMPER ET TEMPER ;

*     Interpolation du module de YOUNG et SIGY a la temperature TEMPER
      YOUNIPOL = IPOL TEMPER EVYOUN;
      SIGYIPOL = IPOL TEMPER EVSIGY;
      LYOU2    = LYOU2 ET YOUNIPOL ;

*     Calcul de EPSIY a partir de YOUNIPOL et SIGYIPOL
      EPSIY = SIGYIPOL / YOUNIPOL;

*   Construction de la courbe de traction Complete pour Cast3M
       LESPI = (PROG 0.) ET ((TTMP2. 1) + EPSIY   );
       LSIGM = (PROG 0.) ET ((TTMP2. (J+1)) + SIGYIPOL);
       EVTRAC= EVOL MANU 'EPS' LESPI 'CONT' LSIGM           ;
       EVTOT = EVTOT ET EVTRAC;
       LYOU3 = LYOU3 ET (CALYO EVTRAC) ;

*   Construction du NUAGE qui associe une EVOLUTION a une TEMPERATURE
      SI (J EGA 1);
        NUATRAC =             NUAGE  'COMP' 'T   '  TEMPER
                                     'COMP' 'TRAC'  EVTRAC        ;
      SINO;
        NUATRAC = NUATRAC ET (NUAGE  'COMP' 'T   '  TEMPER
                                     'COMP' 'TRAC'  EVTRAC )      ;
      FINS;
    FIN ITEMPER;

   EVYOUN  = EVOL MANU 'T' LTEMPER 'YOUN'   LYOU3 ;

*   Parametres thermiques
*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_k.csv';
*    TTMP    = LIRE 'CSV' NOMDATA;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 100 200 300 400 500 600 700 800 900 1000
1200 1400 3000 ;
        TTMP. 2 = PROG 0.0147e3 0.0158e3 0.0172e3 0.0186e3 0.02e3
0.0211e3 0.0222e3 0.0232e3 0.0241e3 0.0248e3 0.0255e3
0.0269e3 0.0283e3 0.0283e3 ;
    EVK     = EVOL MANU 'T' (TTMP. 1) 'K' (TTMP. 2);

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Cp.csv';
*    TTMP    = LIRE 'CSV' NOMDATA;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 100 200 300 400 600 800 1000 1200 1500 3000 ;
        TTMP. 2 = PROG 450 490 525 545 560 580 625 660 670 690 690 ;
    EVCP    = EVOL MANU 'T' (TTMP. 1) 'C' (TTMP. 2);

    SI GRAPH ;
      DESS EVYOUN  TITR 'Module de YOUNG';
      DESS EVNU    TITR 'Coefficient de POISSON';
      DESS EVRHO   TITR 'Masse Volumique';
      DESS EVSIGY  TITR 'Limite Elastique';
      DESS EVALPHA TITR 'Coefficient de dilatation Thermique';
      DESS EVTOT   TITR 'Courbe de Traction a differentes temperatures';
      DESS EVK     TITR 'Conductivite Thermique';
      DESS EVCP    TITR 'Chaleur Specifique';
    FINS;


* MATERIAUX MECANIQUE ET THERMIQUE
  MOMEC = SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'. I                      ;
  MOTHE = SOUDAGE.'MODELE'.'PIECE'.'THERMIQUE'. I                      ;

MAMEC = MATE MOMEC 'YOUN' EVYOUN 'NU' EVNU 'RHO' EVRHO 'ALPH' EVALPHA
                     'TRAC' NUATRAC                                    ;

  SOUDAGE.'MATERIAUX'.'PIECE'.'MECANIQUE'. I = MAMEC                   ;

  MATHE = MATE MOTHE 'K' EVK 'RHO' EVRHO 'C' EVCP                      ;
  SOUDAGE.'MATERIAUX'.'PIECE'.'THERMIQUE'. I = MATHE                   ;

  SOUDAGE.'MATERIAUX'.'PIECE'.'MECANIQUE'.'TOTAL' =
    SOUDAGE.'MATERIAUX'.'PIECE'.'MECANIQUE'.'TOTAL' ET MAMEC           ;
  SOUDAGE.'MATERIAUX'.'PIECE'.'THERMIQUE'.'TOTAL' =
    SOUDAGE.'MATERIAUX'.'PIECE'.'THERMIQUE'.'TOTAL' ET MATHE           ;

FIN INDICE;

MOMECT = SOUDAGE.'MODELE'.'PIECE'.'MECANIQUE'.'TOTAL';

***********************************************************************
* APPORTS durant les differentes passes
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL'=
                                                          VIDE 'MCHAML';
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL'=
                                                          VIDE 'MCHAML';
SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'.'TOTAL'          =
                                                          VIDE 'MCHAML';
REPE INDICE (DIME (SOUDAGE. 'MAILLAGE'. 'APPORT'));
  I=&INDICE;
  NOMCONS = SOUDAGE. 'CONSTITUANTS'. 'APPORT' . I ;
*   Chargement des proprietes materiaux que si le constituant change
    NOMCONSP = NOMCONS;

*   Lecture des proprietes dans la bibliotheque
*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_E.csv'            ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20. 200. 300. 400. 500. 600.
700. 800. 900. 1000. 1500. 3000. ;
        TTMP. 2 = PROG 197e9 184e9 176e9 168e9
160e9 151e9 142e9 130e9 108e9 81e9 1e9 1e9 ;
    EVYOUN  = EVOL MANU 'T' (TTMP. 1) 'YOUN' (TTMP. 2)                 ;
    MAXIT   = MAXI (TTMP. 1)                                           ;


*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Nu.csv'           ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 25 100 200 400 500 600 700 800
900 1000 1100 1200 1300 1390 1420 1450 3000 ;
        TTMP. 2 = PROG 0.296 0.298 0.304 0.315 0.32
0.323 0.326 0.33 0.336 0.339 0.346 0.349 0.353 0.353 0.353 0.353 0.353 ;
        DIMNU = DIME TTMP. 1 ;
        VALNU = TTMP. 2 ;
    EVNU    = EVOL MANU 'T' (TTMP. 1) 'NU' (TTMP. 2)                   ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Rho.csv'          ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 200 400 600 800 1000 1200 1400 1500 3000 ;
        TTMP. 2 = PROG 8.0e3 7.93e3 7.84e3 7.75e3 7.65e3 7.55e3
7.45e3 7.35e3 7.30e3 7.30e3 ;
    EVRHO   = EVOL MANU 'T' (TTMP. 1) 'RHO' (TTMP. 2)                  ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Limite_Elas.csv'  ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 200 300 400 500 600 700 800 900 1000 1500 3000 ;
        TTMP. 2 = PROG 287e6 198e6 172e6 157e6 152e6 145e6 136e6
127e6 115e6 79e6 5e6 1e6 ;
    EVSIGY  = EVOL MANU 'T' (TTMP. 1) 'R0' (TTMP. 2)                 ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Epsi_Th.csv'      ;
*    TTMP    = LIRE 'CSV' NOMDATA                                       ;
        TTMP = TABL ;
        TTMP. 1 = PROG 0 100 200 300 400 500 600 700 800 900 1000 1100
1200 1420 1460 3000 ;
        TTMP. 2 = PROG -3.17e-4 0.0013 0.003 0.0048 0.0066 0.0086 0.0106
0.0126 0.0147 0.0168 0.019 0.0212 0.0235 0.029 0.0298 0.0298 ;
    EVDEFT  = EVOL MANU 'T' (TTMP. 1) 'EPTH' (TTMP. 2)                 ;
    LALPHA  = TTMP. 2 / (TTMP. 1 - TAREF)                             ;
    EVALPHAN = EVOL MANU 'T' (TTMP. 1) 'ALPH' LALPHA                  ;

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Ecrou_T.csv'      ;
*    TTMP1   = LIRE 'CSV' NOMDATA                                       ;
        TTMP1 = TABL ;
        TTMP1. 1 = PROG  20 200 300 400 500 600 700 800 900 1000 1500 3000 ;
*    NOMDATA = CHAI 'Materiaux/'NOMCONS'/'NOMCONS'_Ecrou_Cont_Def.csv ' ;
*    TTMP2   = LIRE 'CSV' NOMDATA                                       ;
        TTMP2   = TABL ;
        TTMP2. 1   = PROG 0. 0.01 0.05 0.2 1. 2. ;
        TTMP2. 2   = PROG 0 28e6 132e6 274e6 913e6 1369e6 ;
        TTMP2. 3   = PROG 0 33e6 136e6 272e6 802e6 1203e6 ;
        TTMP2. 4   = PROG 0 29e6 135e6 279e6 728e6 1092e6 ;
        TTMP2. 5   = PROG 0 29e6 137e6 278e6 442e6 663e6 ;
        TTMP2. 6   = PROG 0 27e6 130e6 267e6 398e6 597e6 ;
        TTMP2. 7   = PROG 0 25e6 117e6 242e6 355e6 532e6 ;
        TTMP2. 8   = PROG 0 25e6 76e6 204e6 264e6 396e6 ;
        TTMP2. 9   = PROG 0 25e6 72e6 167e6 223e6 334e6 ;
        TTMP2. 10  = PROG 0 22e6 46e6 54e6 65e6 97e6 ;
        TTMP2. 11  = PROG 0 3e6 18e6 21e6 26e6 39e6 ;
        TTMP2. 12  = PROG 0 2e6 3e6 4e6 5e6 6e6 ;
        TTMP2. 13  = PROG 0 1e6 2e6 3e6 4e6 5e6 ;

    EVTOT   = VIDE 'EVOLUTION'   ;
    LTEMPER = VIDE 'LISTREEL'    ;
    LYOU2   = VIDE 'LISTREEL'    ;
          LYOU3   = VIDE 'LISTREEL'    ;

    REPE ITEMPER (DIME TTMP1. 1) ;
      J=&ITEMPER                 ;
      TEMPER = EXTR (TTMP1 . 1) J;
      LTEMPER= LTEMPER ET TEMPER ;

*     Interpolation du module de YOUNG et SIGY a la temperature TEMPER
      YOUNIPOL = IPOL TEMPER EVYOUN;
      SIGYIPOL = IPOL TEMPER EVSIGY;
      LYOU2    = LYOU2 ET YOUNIPOL ;

*     Calcul de EPSIY a partir de YOUNIPOL et SIGYIPOL
      EPSIY = SIGYIPOL / YOUNIPOL;

*   Construction de la courbe de traction Complete pour Cast3M
       LESPI = (PROG 0.) ET ((TTMP2. 1) + EPSIY   )      ;
       LSIGM = (PROG 0.) ET ((TTMP2. (J+1)) + SIGYIPOL)  ;
       EVTRAC= EVOL MANU 'EPSI' LESPI 'CONT' LSIGM       ;
       EVTOT = EVTOT ET EVTRAC                           ;
       LYOU3 = LYOU3 ET (CALYO EVTRAC)                   ;

*   Construction du NUAGE qui associe une EVOLUTION a une TEMPERATURE
      SI (J EGA 1);
        NUATRACN =              NUAGE  'COMP' 'T   '  TEMPER
                                       'COMP' 'TRAC'  EVTRAC        ;
      SINO;
        NUATRACN = NUATRACN ET (NUAGE  'COMP' 'T   '  TEMPER
                                       'COMP' 'TRAC'  EVTRAC )      ;
      FINS;
    FIN ITEMPER;

          EVYOUN  = EVOL MANU 'T' LTEMPER 'YOUN'   LYOU3 ;

*   Parametres thermiques
*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_k.csv';
*    TTMP    = LIRE 'CSV' NOMDATA;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 100 200 300 400 500 600 700 800 900 1000
1200 1400 3000 ;
        TTMP. 2 = PROG 0.0147e3 0.0158e3 0.0172e3 0.0186e3 0.02e3
0.0211e3 0.0222e3 0.0232e3 0.0241e3 0.0248e3 0.0255e3
0.0269e3 0.0283e3 0.0283e3 ;
    EVK     = EVOL MANU 'T' (TTMP. 1) 'K' (TTMP. 2);

*    NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Cp.csv';
*    TTMP    = LIRE 'CSV' NOMDATA;
        TTMP = TABL ;
        TTMP. 1 = PROG 20 100 200 300 400 600 800 1000 1200 1500 3000 ;
        TTMP. 2 = PROG 450 490 525 545 560 580 625 660 670 690 690 ;
    EVCP    = EVOL MANU 'T' (TTMP. 1) 'C' (TTMP. 2);

    SI GRAPH ;
      DESS EVYOUN   TITR 'Module de YOUNG Nominal';
      DESS EVNU     TITR 'Coefficient de POISSON';
      DESS EVRHO    TITR 'Masse Volumique';
      DESS EVSIGY   TITR 'Limite Elastique';
      DESS EVALPHAN TITR 'Coefficient de dilatation Thermique';
     DESS EVTOT    TITR 'Courbe de Traction a differentes temperatures';
      DESS EVK      TITR 'Conductivite Thermique';
      DESS EVCP     TITR 'Chaleur Specifique';
    FINS;

* MATERIAUX MECANIQUE ET THERMIQUE
  MOMECD= SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'. I           ;
  MOMECN= SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'. I           ;
  MOTHE = SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'. I                     ;
  YOUND = EXTR LYOU3 (DIME LTEMPER);
  NUD   = EXTR VALNU DIMNU;

  MAMECD= MATE MOMECD 'YOUN' YOUND   'NU' NUD  'ALPH' 0.D0             ;
  MAMECN= MATE MOMECN 'YOUN' EVYOUN  'NU' EVNU 'ALPH' EVALPHAN
                      'TRAC' NUATRACN                                  ;
  MATHE = MATE MOTHE 'K' EVK 'RHO' EVRHO 'C' EVCP                      ;

  SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'. I = MAMECD ;
  SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'. I = MAMECN ;
  SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'. I           = MATHE  ;

  SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'.'TOTAL'      =
              SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'.'TOTAL' ET MATHE;

  SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' =
   SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' ET MAMECD;

  SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' =
   SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' ET MAMECN;
FIN INDICE;

MOMECDT = SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL';
MOMECNT = SOUDAGE.'MODELE'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL';
MOTHT   = SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.'TOTAL';

SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'.'TOTAL' = REDU
SOUDAGE.'MATERIAUX'.'APPORT'.'THERMIQUE'.'TOTAL' MOTHT;

SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' = REDU
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' MOMECDT;

SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' = REDU
SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL' MOMECNT;

* Materiaux pour la CONVECTION
MACONV = MATE MOCONV 'H' 23.e-6     ;

************************************************************************
*                          BLOCAGES PERMANENTS                         *
************************************************************************
SOUDAGE.'BLOCAGES'                         = 'TABL';
SOUDAGE.'BLOCAGES'.'PERMANENT'             = 'TABL';

* BLOCAGES MECANIQUES
zblo = '*' l0 -2. ;
pt0 = MPIECE1 POIN 'PLAN' (0. 0. zblo )
 (1. 0. zblo ) (0. 1. zblo ) 1.e-4 ;
BLOQ0 = 'BLOQ' pt0 'UX' 'UY' 'UZ'                           ;
SOUDAGE.'BLOCAGES'.'PERMANENT'.'MECANIQUE' = BLOQ0 ;

************************************************************************
*                              CHARGEMENT                              *
************************************************************************

* CHARGEMENT Thermique : Source de chaleur dans l'APPORT

REPE INDICE N_PASSE ;
* Boucle sur toutes les PASSES
I=&INDICE;
MOD1 = SOUDAGE.'MODELE'  .'APPORT'.'THERMIQUE'. I     ;
MAIL = SOUDAGE.'MAILLAGE'.'APPORT'. I                 ;
EVO1   = EVOL 'MANU' (PROG 0. 1.D10) (PROG 1. 1.)     ;
CHTCONV = MANU 'CHPO' LEXTT 1 'T' T_CONV              ;
CHA1= CHAR 'TECO' CHTCONV EVO1                        ;
SOUDAGE. 'CHARGEMENT'.'THERMIQUE'. I = CHA1           ;
FIN INDICE                                            ;

* Chargement Thermique : FLUX pour la CONVECTION et RAYO
EVO1   = EVOL 'MANU' (PROG 0. 1.D10) (PROG 1. 1.)       ;
CHTCONV = MANU 'CHPO' LEXTT 1 'T' T_CONV ;
CHACONV= CHAR 'TECO' CHTCONV EVO1                        ;

************************************************************************
*                               PASAPAS                                *
************************************************************************

* Assemblage des MODELES, CARACTERISTIQUES, BLOCAGES, CHARGEMENTS
MODMECA0 = SOUDAGE.'MODELE'   .'PIECE' .'MECANIQUE'          .'TOTAL' ET
           SOUDAGE.'MODELE'   .'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' ;

MATMECA0 = SOUDAGE.'MATERIAUX'.'PIECE' .'MECANIQUE'          .'TOTAL' ET
           SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'DEGRADE'.'TOTAL' ET
           SOUDAGE.'MATERIAUX'.'APPORT'.'MECANIQUE'.'NOMINAL'.'TOTAL'  ;
MODTHER0 = SOUDAGE.'MODELE'   .'PIECE' .'THERMIQUE'          .'TOTAL'  ;
MATTHER0 = SOUDAGE.'MATERIAUX'.'PIECE' .'THERMIQUE'          .'TOTAL' ET
           SOUDAGE.'MATERIAUX'.'APPORT' .'THERMIQUE'         .'TOTAL';


* Ajout de la CONVECTION
MODTHER  = MODTHER0 ET MOCONV  ;
MATTHER  = MATTHER0 ET MACONV  ;
MATTOT   = MATMECA0 ET MATTHER  ;

* BLOCAGES PERMANENTS
BLOMECA0 = SOUDAGE.'BLOCAGES'.'PERMANENT'.'MECANIQUE';
BLOTOT   = BLOMECA0 ;

I = 1;
* Chargement Thermique de la PASSE 1
CHARTOT = SOUDAGE.'CHARGEMENT'.'THERMIQUE'. 1 ;
* On ajoute le MODELE et le MATERIAUX THERMIQUE de l'APPORT
MODTHER = MODTHER  ET SOUDAGE.'MODELE'   .'APPORT'.'THERMIQUE'. 1;
MODTOT  = MODMECA0 ET MODTHER                                    ;

* Initialisations des CHAMPS
*Les CHPOINTS
MAILTHER= EXTR MODTHER 'MAILLAGE' ;
MAILMECA= EXTR MODMECA0 'MAILLAGE';
CHPTINI = MANU 'CHPO' MAILTHER 1 'T'  T_INIT     'NATU' 'DIFFUS' ;
DEPLINI = MANU 'CHPO' MAILMECA 3
'UX' 0. 'UY' 0. 'UZ' 0. 'NATU' 'DIFFUS';
REACINI = VIDE 'CHPOINT';

*Les MCHAML
CONTINI = ZERO MODMECA0 'CONTRAIN' ;
VINTINI = ZERO MODMECA0 'VARINTER' ;
DEININI = ZERO MODMECA0 'DEFINELA' ;

*Definition de TEMPERATURE_REFERENCE et TALPHA_REFERENCE
TREF    = MANU 'CHML' MODMECA0 'T' T_INIT 'TYPE' 'TEMPERATURES' ;
MAILACT = VIDE 'MAILLAGE' ;
MAILFON = VIDE 'MAILLAGE' ;

* Declaration de la TABLE avant PASAPAS
TPASAP                                 ='TABL'   ;
TPASAP.'MODELE'                        = MODTOT  ;
TPASAP.'CARACTERISTIQUES'              = MATTOT  ;
TPASAP.'BLOCAGES_MECANIQUES'           = BLOTOT  ;
TPASAP.'CHARGEMENT'                    = CHARTOT ;
TPASAP.'TEMPS_CALCULES'              = ltcal   ;
*TPASAP.'TEMPS_SAUVES'                =    ;
TPASAP.'TEMPS'                         ='TABL'   ;
TPASAP.'TEMPERATURES'                  ='TABL'   ;
TPASAP.'DEPLACEMENTS'                  ='TABL'   ;
TPASAP.'CONTRAINTES'                   ='TABL'   ;
TPASAP.'REACTIONS'                     ='TABL'   ;
TPASAP.'VARIABLES_INTERNES'            ='TABL'   ;
TPASAP.'DEFORMATIONS_INELASTIQUES'     ='TABL'   ;
TPASAP.'MES_MODEL'                     ='TABL'   ;
TPASAP.'MAILLAGE_ACTIF'                ='TABL'   ;
TPASAP.'MAILLAGE_FONDU'                ='TABL'   ;
TPASAP.'MES_SAUVEGARDES'               ='TABL'   ;
TPASAP.'MES_DATA'                      ='TABL'   ;
TPASAP.'MES_DATA'.'TREF'               ='TABL'   ;
TPASAP.'MES_DATA'.'TAREF'              ='TABL'   ;
TPASAP.'MES_DATA'.'MA'                 ='TABL'   ;
TPASAP.'TEMPS'                     . 0 = 0.D0    ;
TPASAP.'TEMPERATURES'              . 0 = CHPTINI ;
TPASAP.'DEPLACEMENTS'              . 0 = DEPLINI ;
TPASAP.'CONTRAINTES'               . 0 = CONTINI ;
TPASAP.'REACTIONS'                 . 0 = REACINI ;
TPASAP.'VARIABLES_INTERNES'        . 0 = VINTINI ;
TPASAP.'DEFORMATIONS_INELASTIQUES' . 0 = DEININI ;
TPASAP.'MES_MODEL'                 . 0 = MODTOT  ;
TPASAP.'MAILLAGE_ACTIF'            . 0 = MAILACT ;
TPASAP.'MAILLAGE_FONDU'            . 0 = MAILFON ;
TPASAP.'MES_SAUVEGARDES'.'DEFIN'       = VRAI    ;
TPASAP.'MES_SAUVEGARDES'.'DEFTO'       = VRAI    ;
TPASAP.'TALPHA_REFERENCE'              = TAREF   ;
TPASAP.'TEMPERATURE_REFERENCE'         = TREF    ;
TPASAP . 'PROCEDURE_PERSO1'            = VRAI    ;
TPASAP . 'PROCEDURE_PERSO2'            = VRAI    ;
TPASAP . 'PROCEDURE_CHARTHER'              = VRAI ;
TPASAP . 'CELSIUS'                                 = VRAI ;
TPASAP . 'PAS_AJUSTE'                      = FAUX ;
TPASAP . 'PRECISION'                   =  1.e-1  ;
TPASAP . 'PROCESSEURS'                 = 'MOT' 'MONOPROCESSEUR' ;
TPASAP . 'GRANDS_DEPLACEMENTS'         = VRAI    ;
TPASAP . 'LAGRANGIEN'                  = 'MOT' 'REACTUALISE'   ;
TPASAP . 'PREDICTEUR'                  = 'MOT' 'HPP'           ;

* Boucle sur toutes les PASSES (Reprise pour PASAPAS)
REPE INDICE N_PASSE ;
  INDI0 = &INDICE ;
  PASAPAS TPASAP ;
  MENA 'OBLI';

SI (INDI0 '<' N_PASSE) ;
* Incrementation du compteur de PASSES
  I=&INDICE + 1  ;

* On ajoute le MODELE et le MATERIAUX THERMIQUE de l'APPORT
  NB_PAS   = DIME (TPASAP.'TEMPS');
  MODTHER  = MODTHER  ET SOUDAGE.'MODELE'   .'APPORT'.'THERMIQUE'. I;
  MAILTHER = EXTR MODTHER 'MAILLAGE'                                ;
  MODMECA0 = EXTR (TPASAP.'MES_MODEL'.(NB_PAS-1)) 'FORM' 'MECANIQUE';
  MODTOT   = MODMECA0 ET MODTHER                                    ;

* Chargement Thermique de la PASSE en QUESTION
CHARTOT = SOUDAGE.'CHARGEMENT'.'THERMIQUE'. I ;

* Partie nouvelle dans la THERMIQUE
  MAIPASS  = EXTR SOUDAGE.'MODELE'.'APPORT'.'THERMIQUE'.I 'MAIL'     ;
  MAILSANS = DIFF MAILTHER MAIPASS                                   ;
  MAILCONI = CHAN 'POI1' (ENVE MAILSANS)                             ;
  MAILCONP = CHAN 'POI1' (ENVE MAIPASS )                             ;
  MAILINTE = INTE MAILCONI MAILCONP                                  ;
  MAILREST = DIFF (CHAN 'POI1' MAIPASS ) MAILINTE                    ;
  CHPTORI  = TPASAP.'TEMPERATURES'.(NB_PAS-1)                        ;
  CHPCONT  = REDU CHPTORI MAILINTE ;
  CHPTNEW  = MANU 'CHPO' MAILREST 1 'T' (MAXI CHPCONT) 'NATU''DIFFUS';
  CHPTINI  = CHPTORI 'ET' CHPTNEW                                    ;

* Liste des Temps Calcules (Recuperation dans la TABLE SOUDAGE)
  SI (EGA (DIME SOUDAGE. 'TEMPS_CALCULES') 1);
    list1 = SOUDAGE. 'TEMPS_CALCULES'. 1 ;
  SINO;
    list1 = SOUDAGE. 'TEMPS_CALCULES'. I ;
  FINS;

* On fait les modifications dans la table
  TPASAP.'MODELE'                        = MODTOT  ;
  TPASAP.'CHARGEMENT'                    = CHARTOT ;
  TPASAP.'TEMPS_CALCULES'                = list1   ;

 CHPTINI = MANU 'CHPO' MAILTHER 1 'T' T_INIT 'NATU' 'DIFFUS' ;
  TPASAP.'TEMPERATURES' . (NB_PAS-1) = CHPTINI ;

* Il faut redefinir TEMPERATURE_REFERENCE & TALPHA_REFERENCE en reprise
  TPASAP.'TEMPERATURE_REFERENCE'         =
                                TPASAP.'WTABLE'.'TEMPERATURE_REFERENCE';
  TPASAP.'MES_DATA'.'TREF'.NB_PAS = TPASAP.'TEMPERATURE_REFERENCE' ;
  TPASAP.'TALPHA_REFERENCE'              =
                                TPASAP.'WTABLE'.'TALPHA_REFERENCE';
  TPASAP.'MES_DATA'.'TAREF'.NB_PAS = TPASAP.'TALPHA_REFERENCE' ;

  TPASAP.'MES_MODEL'        . NB_PAS   = MODTOT  ;
  TPASAP.'MAILLAGE_ACTIF'. NB_PAS  = TPASAP.'MAILLAGE_ACTIF'.(NB_PAS-1);
  TPASAP.'MES_DATA'.'MA'.NB_PAS = TPASAP.'MAILLAGE_ACTIF' ;
FINSI ;
FIN INDICE;

SI GRAPH ;
N = '-' (DIME TPASAP.'TEMPS') 1 ;
MODT = TPASAP.'MES_MODEL'.N ;
MODM = EXTR MODT 'FORMULATION' 'MECANIQUE' ;
MAILM = EXTR MODM 'MAILLAGE' ;
MAILF = TPASAP.'MAILLAGE_FONDU'. N ;
CONT0 = TPASAP.'CONTRAINTES'.N ;
CHT0 = TPASAP.'TEMPERATURES'. 10 ;
TRAC CONT0 MODM ;
TRAC CHT0 MAILM ;
TRAC MAILF ;
FINS ;

FIN ;




