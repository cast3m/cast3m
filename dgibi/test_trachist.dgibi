* fichier :  test_trachist.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************
GRAPH = vrai  ;
******************* CAS TEST : test_trachist.dgibi ********************

*-------------------------------------------------------------
*
* Test des procédures  TRACHIT et TRACHIS
*
*-------------------------------------------------------------
*'OPTION' 'ECHO' 0 ;
'SAUTER' 'PAGE';

'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;
'OPTION' 'TRACER' 'PSC' ;

************************************************************************
*
*                           Début Programme
*
************************************************************************
*-- maillage :
a1 = 0. 0.;
a2 = 1. 0. ;
a3 = 1. 1. ;
a4 = 0. 1.;
d1 = 'DROIT' a1 2 a2;
d2 = 'DROIT' a2 2 a3;
d3 = 'DROIT' a3 2 a4;
d4 = 'DROIT' a4 2 a1;
dom = 'ORIENTER' ('DALLER' d1 d2 d3 d4);

hysom = 'CHANGER' dom 'POI1' ;
chpo1 = ('COOR' hysom 1) + (10.*('COOR' hysom 2)) ;
chpo2 = 0.0 - chpo1;
chpo1 = 'NOMC' chpo1 'CIL';
chpo2 = 'NOMC' chpo2 'LAB';
chpo0 = chpo1 'ET' chpo2 ;

tsort       = 'TABLE';
tsort.temps = 'TABLE' ;
tsort.val   = 'TABLE' ;
'REPETER' bcl 10;
  i = &bcl;
  tsort.temps.i = i;
  tsort.val.i   = chpo0 * i * i;
'FIN' bcl;

*-- Test de TRACHIT (évolutions en certains points au cours du temps)
*   ---------------

* Maillage des points de suivi :
mailpevl  = a1 'ET' a2 'ET' a3 'ET' a4 ;

*- tables de noms
* nom des composantes
tnomcomp   = 'TABLE' ;
tnomcomp.'SOUSTYPE' = 'MOT' 'NOM_COMPOSANTE';
tnomcomp.1 = 'CHAINE' 'H2O' ;
tnomcomp.2 = 'CHAINE' 'Na+' ;

* nom des points
tnomnoeu   = 'TABLE' ;
tnomnoeu.'SOUSTYPE' = 'MOT' 'NOM_POINT';
tnomnoeu.1 = 'CHAINE' 'BG' ;
tnomnoeu.2 = 'CHAINE' 'BD' ;
tnomnoeu.3 = 'CHAINE' 'HD' ;
tnomnoeu.4 = 'CHAINE' 'HG' ;
prefleg   = 'yo' ;

*- Evolutions
Tit     = 'CHAINE' '(x + y) t*t' ;
Tab1a   = TRACHIT TSORT 'VAL' mailpevl tnomnoeu tnomcomp 'PREF' prefleg;

*- Tracé
'SI' GRAPH ;
   DESTRA Tab1a 'MIMA' 'AXES' 'TITR' Tit  'TITX' 't' 'TITY' 'Val';
'FINSI' ;

*-- Test de TRACHIS (évolutions en certains temps le long d'une ligne)
*   ---------------

*- ligne de mesure :
MAILPEVL = d1;

*- liste des indices des temps à tracer :
LiIndevl = 'LECT' 1 5 10 ;

*- Evolutions
Tit     = 'CHAINE' '(x + y) t*t ' ;
Tab1b   = TRACHIS TSORT 'VAL' mailpevl LiIndevl ('MOTS' 'CIL' 'LAB')
                  ('MOTS' 'Bou' 'Diu') 'PREF' prefleg 'UNIT' 's';

*- Tracé
'SI' GRAPH ;
   DESTRA Tab1b 'MIMA' 'AXES' 'TITR' Tit 'TITX' 't' 'TITY' 'Val';
'FINSI' ;

'ERREUR' 0;

'FIN';






