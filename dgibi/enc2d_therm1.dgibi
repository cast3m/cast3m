* fichier :  enc2d_therm1.dgibi
*
*  Enceinte 2D Axisymetrique
*  Refroidissement d'un gaz chaud (AIR)
*  Interaction GAz/paroi/temperature exterieure
*  Teste thermique paroi et coefficient d'echange

'OPTI' 'DIME' 2 'ELEM' 'CU20' ;
 'OPTI' 'MODE' 'AXIS' ;

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

 'SI' COMPLET ;
  nbit=200    ;
  DT0 = 2.    ;
  n1=3 ;
  n2=5;
  n3=10;
  ds1= 0.02 ;
  ds2= 0.2  ;
 'SINON'      ;
  nbit=20     ;
  DT0 = 2.    ;
  n1=3 ;
  n2=5;
  n3=10;
  ds1= 0.02 ;
  ds2= 0.2  ;
 'FINSI'      ;


p1=5.e-4 0 ;
p2=0.5 0. ;
p3=1 0 ;
p4=1 2;
p41=0.5 2. ;
p5=5.e-4 2 ;


bas= p1 'DROI' DINI ds1 DFIN ds2 p2 d DINI ds2 DFIN ds1 p3 ;
paroid=p3 'DROI' n3 p4 ;
plaf=inve (bas plus (0. 2.) );
axe = p5 'DROI' n3 p1 ;
elim (bas et plaf et paroid et axe) 1.e-5 ;
vtp = (paroid trans 2 (0.1  0.)) coul bleu;
parext = (cote 3 vtp) coul rouge ;


mt= 'DALLER'  bas paroid plaf axe ;
Si GRAPH;
trace (mt et vtp);
Finsi ;
rxt = 'TABLE' ;
rxt.'vtf'=mt ;
rxt.'axe'   = axe ;

 rxt.'TF0'    =       100.                        ;
 rxt.'PT0'    =       1.0e5                       ;

 rxt.'THERMP'=VRAI ;
 rxt.'vtp'=vtp;
 rxt.'ROCP'=1.e4 ;
 rxt.'LAMBDA'=40.  ;
 rxt.'Tp0'     = 30. ;
 rxt.'ECHAN'=50. ;

 rxt.'ECHEXT'  = VRAI ;
 rxt.'HEXT'=50. ;
 rxt.'TPEXT'=20. ;
 rxt.'parext'=parext ;

rxt.'epsi'= 1.e-4 ;
rxt.'pi'  = 0.5 0.5 ;
rxt.'DT0'  = DT0     ;
rxt.'RENU'  = 'RIEN' ;
rxt.'DISCR'= 'QUAF';
rxt.'KPRE' = 'MSOMMET';
rxt.'MODTURB'  = 'NUTURB'  ;
rxt.'NUT'  = 0.005  ;
rxt.'GRAPH'  = GRAPH ;

EXECRXT  nbit rxt ;

un=rxt.'TIC'.'UN';
tf =rxt.'TIC'.'TF' ;

$vtf=rxt.'GEO'.'$vtf' ;
$vtp=rxt.'GEO'.'$vtp' ;
vtf=doma $vtf maillage;
vtp=doma $vtp maillage;

rho=rxt.'TIC'.'RHO' ;
un =rxt.'TIC'.'UN';
tp =rxt.'TIC'.'TP';

ixe=inve axe ;
evauz = evol chpo (exco un 'UY') ixe ;
evarh = evol chpo rho ixe ;
evatf = evol chpo tf  ixe ;


Si (NON COMPLET) ;
list rxt.TIC.'Tfm' ;
list rxt.TIC.'PT'  ;
list rxt.TIC.'LMAXU';


ltfm=Prog
   100.00       79.731       77.454       71.786       67.833
   64.355       61.029       57.925       55.136       52.636
   50.398       48.391       46.573       44.903       43.359
   41.927       40.602       39.376       38.240       37.187
   36.207;

lPT =Prog
  1.00000E+05   98068.       92332.       91994.       91665.
   90512.       89494.       88730.       88003.       87312.
   86707.       86175.       85692.       85248.       84837.
   84455.       84099.       83770.       83464.       83182.
   82918.;

Lmaxu=Prog
   0.0000       0.0000      0.29154      0.46506      0.52465
   0.48430      0.42198     0.35131      0.28534      0.22824
   0.19253      0.17409     0.16273      0.15506      0.14841
   0.14168      0.13494     0.12844      0.12257      0.11747
   0.11309;

tic=rxt.'TIC' ;
ERtf=somm( abs (ltfm - tic.'Tfm') )/ 80. ;
ERPT=somm( abs (lPT  - tic.'PT' ) ) /1.e5 ;
ERum=somm( abs (Lmaxu  - tic.'LMAXU' ) )  ;

Mess ' ERtf=' ERtf ' ERPT=' ERPT ' ERum=' ERum ;

 Si (ERtf '>' 2.e-4) ; erreur 5 ; Finsi ;
 Si (ERPT '>' 2.e-4) ; erreur 5 ; Finsi ;
 Si (ERum '>' 2.e-3) ; erreur 5 ; Finsi ;

evuz= extr evauz ordo ;
list evuz ;
lrr= prog
  4.88090E-38  1.63432E-02  5.27145E-02  8.21404E-02  0.10017
  0.11203      0.11314      0.10972      9.63041E-02  8.71612E-02
  6.94869E-02  6.24003E-02  4.82692E-02  4.89579E-02  3.89615E-02
  4.48514E-02  3.37560E-02  3.37986E-02  2.10628E-02  2.61349E-03
 -1.42428E-37;

ER=somm( (evuz - lrr)*(evuz - lrr) ) *0.05;
 mess ' Ecart  ' er ;
 Si ( er > 2.e-4) ; erreur 5 ; finsi ;

Finsi ;


Si GRAPH ;

 dess evauz 'TITRE' 'Vitesse sur l axe';
 dess evarh 'TITRE' 'Densite melange sur l axe';
 dess evatf 'TITRE' 'Temperature sur l axe';

ung= vect un 0.1 ux uy jaune;
trace ung vtf;
 trace tp  vtp 'TITRE' ' Tp ' ;
 opti isov suli ;
trace tf  vtf 'TITRE' ' Tf ' ;
trace rho vtf 'TITRE' ' Rho' ;
Finsi ;

 FIN ;






