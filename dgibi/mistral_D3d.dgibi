* fichier :  mistral_D3d.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test mistral_D3d.dgibi: Jeux de donnees         *
*     ---------------------------------------         *
*                                                     *
  opti echo 0 ;
  opti dime 3 mode tridim elem cu20 ;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;

*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*  MODELE MISTRAL                                     *
*    ELASTICITE ET PLASTICITE INSTANTANEE             *
*                                                     *
*  MAILLAGE:                                          *
*     EPROUVETTE CUBIQUE                              *
*                                                     *
*  CHARGEMENT:                                        *
*     DEPLACEMENT IMPOSE MONOTONE CROISSANT           *
*******************************************************
*
* Geometrie
*
* ------------------------------------------
  XX = 1. ; YY = 1. ; ZZ = 1. ;
* ------------------------------------------
*
   PB00 = 0 0 0    ;  PB10 = XX 0 0 ;
   PB11 = XX YY 0  ;  PB01 = 0 YY 0 ;
   VECZ = 0 0 ZZ ;
*
   LBX0 = droi 1 PB00 PB10 ;
   LBY1 = droi 1 PB10 PB11 ;
   LBX1 = droi 1 PB11 PB01 ;
   LBY0 = droi 1 PB01 PB00 ;
*
* Maillage
*
   PLB = dall LBX0 LBY1 LBX1 LBY0 plan ;
   EPROU = PLB volu 1 tran VECZ ;
   PLH = face 2 EPROU ;
   PL0YZ = EPROU poin plan PB00 PB01 VECZ 1E-3 ;
   PLX0Z = EPROU poin plan PB00 PB10 VECZ 1E-3 ;
*   trac EPROU ;
*
* Modele et materiau
*
   MOD3 = mode EPROU mecanique elastique orthotrope
                               viscoplastique mistral ;
*
* ------------------------------------------------------------
  VEC1 = 1. 1. 0. ; VEC2 = -1. 1. 0. ; TETA = 45. ;
  SENSIP1 = -2 ; SENSIP2 = 1 ; ICBASE = 0 ;
  fichier = '../divers/mimatD3d_par' ;
  PDILT    E1 E2 E3 NU12 NU23 NU13 MU12 MU23 MU13
  PNBRE PCOHI PECOU PEDIR PRVCE PECRX PDVDI PCROI PINCR
    = @mistpar fichier SENSIP1 SENSIP2 ;
* ------------------------------------------------------------
  MAT3 = mate MOD3  'YG1 ' E1    'YG2 ' E2   'YG3 ' E3
                    'NU12' NU12  'NU23' NU23 'NU13' NU13
                    'G12 ' MU12  'G13 ' MU13 'G23 ' MU23
                    'ALP1' 0.    'ALP2' 0.   'ALP3' 0.
 'DILT' PDILT 'NBRE' PNBRE 'COHI' PCOHI 'ACOU' PECOU 'EDIR' PEDIR
 'RVCE' PRVCE 'ECRX' PECRX 'DVDI' PDVDI 'CROI' PCROI 'INCR' PINCR
                    'SIP1' SENSIP1  'SIP2' SENSIP2  'IBAS' ICBASE
                    'DIRECTION' VEC1 VEC2 'INCLINE' TETA ;
*
* Conditions aux limites
*
   CLB = bloq uz PLB ;
   CLH = bloq uz PLH ;
   CLYZ = bloq ux PL0YZ ;
   CLXZ = bloq uy PLX0Z ;
   CL = CLB et CLH et CLYZ et CLXZ ;
*
* Chargement
*
* -------------------------------------------------------
   T1 = 100. ; DT1 = 10. ;
   EPSZZ1 = 0.02 ;
   TT1 = 300. ;
   PHIT0 = 0. ;
* -------------------------------------------------------
   TEMPS = prog 0. T1 ;
   TEMPSCAL = prog 0. pas DT1 T1 ;
*
   DEPZ = depi CLH ZZ ;
   EVEPS = evol manu TEMPS (prog 0. EPSZZ1) ;
   CHADE = char dimp DEPZ EVEPS ;
*
   TT = manu chpo EPROU 1 'T' 1. ;
   EVTT = evol manu TEMPS (prog TT1 TT1) ;
   CHTT = char 'T' TT EVTT ;
*
   PHI = manu chpo EPROU 1 'FI' 1. ;
   EVFI = evol manu TEMPS (prog 0. 0.) ;
   CHFI = char 'FI' PHI EVFI ;
*
   CHA = CHADE et CHTT et CHFI ;
*
* Valeurs initiales
*
   VINT0 = zero MOD3 'VARINTER' ;
   FIT0 = manu chml MOD3 'FIT ' PHIT0 type 'SCALAIRE' 'STRESSES' ;
   VINT0 = VINT0 + FIT0 ;
*
* Calcul
*
   TAB = TABLE ;
   TAB.'VARIABLES_INTERNES' = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL ;
   TAB.'CARACTERISTIQUES' = MAT3 ;
   TAB.'MODELE' = MOD3 ;
   TAB.'CHARGEMENT' = CHA ;
   TAB.'VARIABLES_INTERNES' . 0 = VINT0 ;
   TAB.'TEMPS_CALCULES' = TEMPSCAL ;
*
   PASAPAS TAB ;
*
* Traitement des resultats
*
   SIG = TAB.'CONTRAINTES' ;
   DEP = tab.'DEPLACEMENTS' ;
   VI  = tab.'VARIABLES_INTERNES' ;
   NT = dime TEMPSCAL-1 ;
* ----------------
   ERMAX = 0.005 ;
* ----------------
*
   SIGT = SIG.NT ;
   SIGT_PO = chang chpo MOD3 SIGT ;
   SIGXX = extr SIGT_PO SMXX PB00 ;
   SIGYY = extr SIGT_PO SMYY PB00 ;
   SIGZZ = extr SIGT_PO SMZZ PB00 ;
   SIGXY = extr SIGT_PO SMXY PB00 ;
   SIGXZ = extr SIGT_PO SMXZ PB00 ;
   SIGYZ = extr SIGT_PO SMYZ PB00 ;
*
   DEPT = DEP.NT ;
   EPSIT = epsi DEPT MOD3 ;
   EPSIT_PO = chang chpo MOD3 EPSIT ;
   EPSXX = extr EPSIT_PO EPXX PB00 ;
   EPSYY = extr EPSIT_PO EPYY PB00 ;
   EPSZZ = extr EPSIT_PO EPZZ PB00 ;
   EPSXY = (extr EPSIT_PO GAXY PB00)/2. ;
   EPSXZ = (extr EPSIT_PO GAXZ PB00)/2. ;
   EPSYZ = (extr EPSIT_PO GAYZ PB00)/2. ;
*
   VIT = VI.NT ;
   VIT_PO = chang chpo MOD3 VIT ;
   EPSP11 = extr VIT_PO EP01 PB00 ;
   EPSP22 = extr VIT_PO EP02 PB00 ;
   EPSP33 = extr VIT_PO EP03 PB00 ;
   EPSP12 = extr VIT_PO EP04 PB00 ;
   EPSP13 = extr VIT_PO EP05 PB00 ;
   EPSP23 = extr VIT_PO EP06 PB00 ;
*
   mess ;
   mess 'SIGXX SIGYY SIGZZ :    ' SIGXX SIGYY SIGZZ ;
   mess 'SIGXY SIGXZ SIGYZ :    ' SIGXY SIGXZ SIGYZ ;
   mess ;
   mess 'EPSXX EPSYY EPSZZ :    ' EPSXX EPSYY EPSZZ ;
   mess 'EPSXY EPSXZ EPSYZ :    ' EPSXY EPSXZ EPSYZ ;
   mess ;
   mess 'EPSP11 EPSP22 EPSP33 : ' EPSP11 EPSP22 EPSP33 ;
   mess 'EPSP12 EPSP13 EPSP23 : ' EPSP12 EPSP13 EPSP23 ;
   mess ;
*
* -----------------------------------------------------
   SIGZZ1 = 486970389. ;
   EPSP331 = 0.01513029 ;
   YOUNGX = 1.E11 ; YOUNGY = 1.E11 ; YOUNGZ = 1.E11 ;
   POISYZ = 0.2 ; POISZX = 0.4 ;
* -----------------------------------------------------
   EPSEZZ1 = SIGZZ1/YOUNGZ ;
   EPSEXX1 = (-1.)*(POISZX/YOUNGZ)*SIGZZ1 ;
   EPSEYY1 = (-1.)*(POISYZ/YOUNGY)*SIGZZ1 ;
   EPSP111 = (-0.6)*EPSP331 ;
   EPSP221 = (-0.4)*EPSP331 ;
   EPSXX1 = EPSEXX1 + EPSP111 ;
   EPSYY1 = EPSEYY1 + EPSP221 ;
   EPSZZ1 = EPSEZZ1 + EPSP331 ;
*
   ERSIGZZ = ABS(SIGZZ/SIGZZ1 - 1.) ;
*
   EREPSXX = EPSXX/EPSXX1 - 1. ;
   EREPSYY = EPSYY/EPSYY1 - 1. ;
   EREPSZZ = EPSZZ/EPSZZ1 - 1. ;
   P_ER = prog EREPSXX EREPSYY EREPSZZ ;
   EREPSMA = maxi P_ER 'ABS' ;
*
   EREPSP11 = EPSP11/EPSP111 - 1. ;
   EREPSP22 = EPSP22/EPSP221 - 1. ;
   EREPSP33 = EPSP33/EPSP331 - 1. ;
   P_ER = prog EREPSP11 EREPSP22 EREPSP33 ;
   EREPSPMA = maxi P_ER 'ABS' ;
*
   si (ERSIGZZ > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur la contrainte > ' ERMAX ;
    mess  ERSIGZZ ;
   finsi ;
   si (EREPSMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur une deformation totale > ' ERMAX ;
    mess EREPSXX EREPSYY EREPSZZ ;
   finsi ;
   si (EREPSPMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur une deformation plastique > ' ERMAX ;
    mess EREPSP11 EREPSP22 EREPSP33 ;
    mess ;
   finsi ;
   P_ER = prog ERSIGZZ EREPSMA EREPSPMA ;
   ERMA = maxi P_ER ;
   si (ERMA > ERMAX) ;
    ERRE 5 ;
   finsi ;

   fin ;







