* fichier :  snap.dgibi
************************************************************************
* Section : Mecanique Non-lineaire
* Section : Mecanique Elastique
************************************************************************
*************************************************************************
*  TEST SNAP
*
*      EXEMPLE D UTILISATION DE LA PROCEDURE PASAPAS ( ET INCREME)
*
*      PROBLEME DE GRANDS DEPLACEMENTS
*
*      PROBLEME DU SNAP une seule barre est maillee et calculee
*
*
*
*         ||
*         ||
*         \/ F
*         /\
*        /  \
*     | /    \ |
*     |/      \|
*     |        |
*************************************************************************
*
*
TITRE ' SNAP ' ;
*
*TEMPS ;
*
*------------- geometrie   ligne ST formee d' 1  SEG2 -------------------
*              PAR SYMETRIE, ON N'ETUDIE QUE LA MOITIE
*
OPTI ECHO 0 ;
OPTI DIME 2 ELEM SEG2 MODE PLAN CONT ;DENS 1. ;
P1 = 0.    1.    ; P2 = 10. 0.  ;
ST = P1 D 1  P2 ;
*
*------------ calcul mecanique .  ------------------
*
 MO = MODE ST  MECANIQUE  BARR     ;
*
*   MATERIAU ET CARACTERISTIQUES
*
*
MA1 = MATE MO YOUN 2.1E11  NU 0.3 ;
CAR1 = CARA MO SECT 0.05 ;
MACA= MA1 ET CAR1;
*
*----------- calcul de la rigidite --------------------------------------
*
RI1 =    RIGI MACA  MO ;
*
*----------- definition des conditions aux limites ----------------------
*
CL1 = BLOQ UX P1 ;
CL2 = BLOQ UX P2 ;
CL3 = BLOQ UY P2 ;
CL4 = BLOQ UY p1;
CL = CL1 ET CL2 ET CL3 ;
*
*----------- definition du chargement -----------------------------------
*
FP11 = FORCE (  0 -12.5e5 ) p1;
LIX1 = PROG 0. 40. ;
LIY1 = PROG 0. 40. ;
EV1 = EVOL MANU T LIX1 F(T) LIY1 ;
*
*----------- resolution par la procedure NONLIN -------------------------
*            CALCUL EN GRANDS DEPLACEMENTS
*
TAB2 = TABLE ;
CHA1 = CHAR MECA FP11   EV1 ;
TAB2.'GRANDS_DEPLACEMENTS'=VRAI;
TAB2.'AUTOMATIQUE' = VRAI;
*
* ---------- pilotage suivant le deplacement du point P1
*
'DEBPROC' AUTOPILO DELT*'CHPOINT' DELA*'CHPOINT' ZMOD*'MMODEL'
          ZMAT*'MCHAML' TTT*TABLE ;
      NORM1 =     (extraire  delt p1 'UY' ) *-1 ;
*      mess ' norm1 '  norm1;
FINPROC   NORM1;
*--------------------------------------------------------
*
TAB2.'AUTOPAS' = 200;
TAB2.'AUTOCRIT' = 0.075;
TAB2.'AUTORESU' = 1;
TAB2.'BLOCAGES_MECANIQUES' = CL;
TAB2.'MODELE' = MO;
TAB2.'CARACTERISTIQUES' = MACA;
TAB2.'CHARGEMENT' = CHA1;
LIS11 =   PROG 0. 2. ;
TAB2.'TEMPS_CALCULES' = LIS11;
tab2.'REAC_GRANDS'=200.;
PASAPAS TAB2 ;
*
*----------- resultats --------------------------------------------------
*        courbe de snap through : montee descente montee
PGX = PROG 0.;
PGY = PROG 0.;
NDIM = (DIME ( TAB2 . DEPLACEMENTS )) - 1 ;

REPETER TBOU2 NDIM ;

LEDEP = TAB2 . DEPLACEMENTS.  (&TBOU2);
REA1 = REAC CL3 LEDEP ;
V   = EXTR LEDEP UY P1 ;
PGX = PGX ET ( PROG V ) ;
VV  = EXTR REA1  FY P2 ;
PGY = PGY ET ( PROG VV ) ;
FIN TBOU2 ;
*
pgx = pgx * -1.;
strou = mini pgy;
sref = -2.e+6;
smp= sref * 1.05;smb = sref * 0.95;
mess ' borne min ' smp 'valeur trouvée' strou 'borne max' smb;
*
si (( strou > smb ) ou ( strou < smp) ) ;
   erreur (5);
finsi;
*
*EV5 = EVOL TURQ MANU  'Deplacement' PGX 'Force de reaction' PGY ;
*DESS EV5 ;
fin;











