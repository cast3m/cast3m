* fichier :  flslic4.dgibi
******************************************************************
*                                                                *
*       test de validation de l' élément de raccord fluide -     *
*                                                                *
*                        structure LIC4.                         *
*                                                                *
*         calcul du mode n=3 m=1 d'un système de coques          *
*                                                                *
*          concentriques. La coque exterieure est supposée       *
*                                                                *
*            encastrée, la coque interieure est libre.           *
*                                                                *
*         valeurs de comparaison: calcul en mode fourier n=3     *
*                                                                *
*          Les valeurs sont Fréquence              = 47.491      *
*                           Masse généralisée      = 2563.1      *
*                                                                *
*        On ne modélise qu'un quart de modèle compte tenu des    *
*                                                                *
*        symetries.                                              *
*                                                                *
*      Pour la coque externe, les conditions sont ux=uy=uz=0     *
*                                                 rx=ry=rz=0     *
*      Pour la coque interne  les conditions sont                *
*                                          plan xz SYMETRIE      *
*                                          plan yz ANTISYMETRIE  *
*                                          uz=0                  *
*      Pour le fluide les conditions sont                        *
*                                          plan xz dP/dn=0       *
*                                          plan yz  P=0          *
*                                          plan x=0 et x=h dP/dn *
*                                                                *
******************************************************************
*
opti dime 3                                                      ;
opti elem cub8                                                   ;
opti echo 0                                                      ;
OPTI EPSI LINEAIRE;
*
****************************************
* definition des donnees parametriques *
****************************************
*
ri =1.00;         comm rayon de la coque interne                 ;
re =1.30;         comm rayon de la coque externe                 ;
h  =1.00;         comm hauteur du modele                         ;
hc =0.04;         comm epaisseur des coques                      ;
lc0=5.3;          comm longueur caracteristique du fluide        ;
y0 =2.1e11;       comm module d YOUNG des coques                 ;
roc=7900;         comm masse volumique de l acier                ;
decouz= 5;       comm nombre d elements suivant z               ;
decout= 10;       comm nombre d elements suivant teta            ;
decouf= 5;       comm nombre d elements sur le rayon            ;
vect= 0. 0. 0.0;  comm vect translation pour raccord fluide
structure                                                        ;
*
*************************
* definition des points *
*************************
*
pci1= ri 0.00 0.00                                               ;
pci2= ri 0.00 h                                                  ;
pce1= re 0.00 0.00                                               ;
pce2= re 0.00 h                                                  ;
ce1=0.00 0.00 0.00                                               ;
ce2=0.00 0.00 h                                                  ;
*
*************************
* definition des coques *
*************************
*
lci= pci1 d decouz pci2                                          ;
lce= pce1 d decouz pce2                                          ;
sci= lci rota decout 90 ce1 ce2                                  ;
sce= lce rota decout 90 ce1 ce2                                  ;
*
************************
* definition du fluide *
************************
*
lcf1 = lci plus vect                                             ;
lcf2 = lce moins vect                                            ;
sflu1= lcf1 rota decout 90 ce1 ce2                               ;
sflu2= lcf2 rota decout 90 ce1 ce2                               ;
vflu = volu sflu1 decouf sflu2                                   ;
*
***************************************
* definition des elements de raccords *
***************************************
*
opti elem lia4                                                   ;
raci= liaison 0.001 sflu1 sci                                    ;
race= liaison 0.001 sflu2 sce                                    ;
*
*****************************************
* definition des modeles elements finis *
*****************************************
*
moci = modele sci 'MECANIQUE' 'ELASTIQUE' coq4                   ;
moce = modele sce 'MECANIQUE' 'ELASTIQUE' coq4                   ;
mocri= modele raci 'MECANIQUE' 'LIQUIDE'  lic4                   ;
mocre= modele race 'MECANIQUE' 'LIQUIDE'  lic4                   ;
mocf = modele vflu 'LIQUIDE' lcu8                                ;
*
****************************
* definition des materiaux *
****************************
*
maci = mate moci youn y0 nu 0.3 rho roc epai hc                  ;
mace = mate moce youn y0 nu 0.3 rho roc epai hc                  ;
macf = mate mocf rho 1000 rorf 1000 cson 1435 cref 1435
                       lcar lc0 g 9.81                           ;
macri= mate mocri rho 1000 rorf 1000 cson 1435 cref 1435
                      lcar lc0 g 9.81                            ;
macre= mate mocre rho 1000 rorf 1000 cson 1435 cref 1435
                      lcar lc0 g 9.81                            ;
cacri= carac mocri 'LIQUIDE' vflu                                ;
cacre= carac mocre 'LIQUIDE' vflu                                ;
macri= macri et cacri                                            ;
macre= macre et cacre                                            ;
macr = macri et macre                                            ;
*
************************
* calcul des rigidites *
************************
*
rigci= rigi moci maci                                            ;
rigce= rigi moce mace                                            ;
rigcf= rigi mocf macf                                            ;
rigcr= rigi (mocri et mocre) macr;
rigt = rigci et rigce et rigcf et rigcr                          ;
*
********************************
* calcul des matrices de masse *
********************************
*
masci= masse moci maci                                           ;
masce= masse moce mace                                           ;
mascf= masse mocf macf                                           ;
mascr= masse (mocri et mocre) macr                               ;
mast = masci et masce et mascf et mascr                          ;
*
*****************************************
* definition des conditions aux limites *
*****************************************
*
pcr1= pci1 tour 90 ce1 ce2                                       ;
pcr2= pci2 tour 90 ce1 ce2                                       ;
pcr3= pce2 tour 90 ce1 ce2                                       ;
pnx= sci point plan ce1 pci1 pci2 0.001                          ;
vpny= vflu point plan pcr1 pcr2 pcr3 0.001                       ;
pnz= sci point plan pcr1 ce1 pci1 0.001                          ;
*
blo1= bloque depla rota sce                                      ;
blo2= symt 'DEPL' ce1 pci1 pci2  sci 0.001                       ;
blo3= anti 'DEPL' pcr1 pcr2 pcr3 sci 0.001                       ;
blo4= symt 'ROTA' ce1 pci1 pci2  sci 0.001                       ;
blo5= anti 'ROTA' pcr1 pcr2 pcr3 sci 0.001                       ;
blo6= bloque uz sci                                              ;
blop = bloque 'P' 'PI' vpny                                      ;
blot= blo1 et blo2 et blo3 et blo4 et blo5 et blo6 et blop       ;
*
********************************
* calcul de la rigidite totale *
********************************
*
rigt= rigt et blot                                               ;
*
************************************
* utilisation de vibre option proc *
************************************
*
tmx1= vibr proch (prog 40.) rigt mast                      ;
*
****************************
* extraction des resultats *
****************************
*
kfois=1                                                          ;
freq = tmx1.modes.kfois.frequence                                ;
utot = tmx1.modes.kfois.deformee_modale                          ;
mn   = tmx1.modes.kfois.masse_generalisee                        ;
*
*************************************************
* recalcul par modification de la normalisation *
*         maxi du deplacement ux =1             *
*************************************************
*
uxm = maxi (exco ux utot)                                        ;
unox= utot/uxm                                                   ;
udx1= manu 'CHPOINT' (sci et sce et raci et race) 6
      ux 1.0 uy 0.0 uz 0.0 rx 0.0 ry 0.0 rz 0.0 nature diffus    ;
udx2= manu 'CHPOINT' (raci et race et vflu) 2
      'P' 0.0 'PI' 0.0  nature diffus                            ;
udx = udx1 + udx2                                                ;
qx  = ytmx unox udx mast                                         ;
mnx = ytmx unox unox mast                                        ;
mnx = mnx                                                        ;
*
*****************************************
* comparaison aux valeurs de références *
*****************************************
*
iuer=0.05                                                        ;
tvalth= table                                                    ;
tvalth.1=47.491                                                  ;
tvalth.2=2563.1                                                  ;
tvalca= table                                                    ;
tvalca.1= freq                                                   ;
tvalca.2= mnx                                                    ;
ierc=table                                                       ;
i=1                                                              ;
iercmax= 0.0                                                     ;
repeter bverif 2                                                 ;
 ierc.i= ((tvalth.i)-(tvalca.i))                                 ;
 ierc.i= (ierc.i)/(tvalth.i)                                     ;
 si ((ierc.i) < 0.0)                                             ;
     xvali= ierc.i                                               ;
     ierc.i=abs xvali                                            ;
 finsi                                                           ;
 si (iercmax < (ierc.i))                                         ;
     iercmax= (ierc.i)                                           ;
 finsi                                                           ;
i=i+1                                                            ;
fin bverif                                                       ;
si (iercmax < iuer)                                              ;
 mess 'bonne fin du calcul'                                      ;
 sinon                                                           ;
 mess 'ecart maximal vaut ' iercmax 'il y a un probleme'         ;
finsi                                                            ;
fin                                                              ;







