* fichier :  mazars2.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*   Christian La Borderie 20 janvier 2010
*
*
*
*TESTE LA VERSION MAZARS MODIFIÉE AVEC PRISE EN COMPTE DE GF
*ON SOLLICITE UN ÉLÉMENT DE TAILLE VARIABLE EN TRACTION ET ON VÉRIFIE
*QUE L'ÉNERGIE DE FISSURATION OBTENUE CORRESPOND À CELLE QUI ÉTAIT VOULUE
*
OPTI ECHO 0;
COMPLET=FAUX;
GRAPH=FAUX;
*
*
*
OPTI DIME 2 ELEM QUA4 MODE PLAN CONT ;
SI COMPLET;N=3;SINON;N=1;FINSI;
TAB2=TABLE;
TAB2.BTRA=TABLE;
TAB2.TAILLE=TABLE;
TAB2.EVTRAC=TABLE;
TAB2.GFCAL=TABLE;
TAB2.VARI=TABLE;
PROGG=PROG;
PROGD=PROG;
REPETER BOU1 N;
  A=10.**(-1*&BOU1);
  TAB2.TAILLE.I=A;
  PROGD=INSE PROGD &BOU1 A;
  DENSITE A;
  P1=0. 0.;
  P2=A 0.;
  P3=A A;
  P4=0. A;
  D1=D P1 P2;D2=D P2 P3;D3=D P3 P4;D4=D P4 P1;
  S1=DALLER D1 D2 D3 D4;
  MOD1=MODE S1 MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT MAZARS;
  FT=3.D+6;
  YG=30.D+9;
  GF=100.;
  XNU=0.2;
  EPS0=FT/YG;
  CHAM1=MANU CHML MOD1 BTRA 1. TYPE CARACTERISTIQUES RIGIDITE;
  CHAM2=INTG CHAM1 MOD1 ELEM BTRA;
  CHH=CHAM2**(1./2.);
  CHAM31= CHH*FT/GF ;
  MAT1=MATE MOD1 YOUN YG NU 0.2D0 ATRA -1. BTRA CHAM31 KTR0 EPS0
                            ACOM 2000. BCOM 20000. RHO 2.5E+3 ;
  CL1=BLOQ P1 UY;
  CL2=BLOQ D2 UX;
  CL3=BLOQ D4 UX;
  CLTOT=CL1 ET CL2 ET CL3;
  F1=DEPI CL2 1.;
  EVOL1=EVOL MANU (PROG 0. 1.)   (PROG 0.  1.);
  CHAR1=CHARGEMENT F1 EVOL1 DIMP;
  TAB1=TABLE;
  TAB1.BLOCAGES_MECANIQUES=CLTOT;
  TAB1.CARACTERISTIQUES=MAT1;
  TAB1.GRAND_DEPLACEMENTS=VRAI;
  TAB1.MODELE=MOD1;
  TAB1.MOVA='D';
  TAB1.CHARGEMENT=CHAR1;
  TAB1.MAXITERATION=100;

  SI (&BOU1 EGA 1);
  TAB1.TEMPS_CALCULES=PROG 0. (A*EPS0) PAS (A*0.1*EPS0) (A*10*EPS0)
                                      PAS (A*EPS0)    (A*30*EPS0);

  FINSI;
  SI (&BOU1 EGA 2);
  TAB1.TEMPS_CALCULES=PROG 0. (A*EPS0) PAS (A*0.1*EPS0) (A*10*EPS0)
                                      PAS (A*EPS0)    (A*100*EPS0)
                                      PAS (A*10*EPS0)  (A*300*EPS0);

  FINSI;
  SI (&BOU1 EGA 3);
  TAB1.TEMPS_CALCULES=PROG 0. (A*EPS0) PAS (A*0.1*EPS0) (A*10*EPS0)
                                      PAS (A*EPS0)    (A*100*EPS0)
                                      PAS (A*10*EPS0)  (A*300*EPS0)
                                      PAS (A*100*EPS0) (A*3000*EPS0);
  FINSI;


  PASAPAS TAB1;

  N1=DIME TAB1.TEMPS_CALCULES;
  PROGT=TAB1.TEMPS_CALCULES;
  TITRE 'COURBE CONTRAINTE - DEPLACEMENT';
  EV1=(@GLOBAL TAB1 EVOL1 CL2 FX) / A;
  SI (&BOU1 EGA 1);
    EVT=EV1;
    SINO;
    EVT=EV1 ET EVT;
  FINSI;
  L1=ENLE (EXTR EV1 ABSC) 1;
  L2=ENLE (EXTR EV1 ORDO) 1;
  EV1=EVOL MANU L1 L2;
  S1=INTG EV1;
  G2=EXTR S1 1;
  PROGG=INSE PROGG &BOU1 G2;
  TAB2.GFCAL.I=G2;
FIN BOU1;

*
*
*
MESSAGE 'VALEURS DE GF OBTENUES';
LIST PROGG;
IERR=0;
REPETER BOU3 N;
 XERR=((EXTR PROGG &BOU3) - GF)/GF;
 SI (XERR > 0.01); IERR=5;FINSI;
 MESSAGE 'TAILLE  10-' &BOU3 '  ERREUR DE  CALCUL : ' (100*XERR) '%';
FIN BOU3;
ERREUR IERR;
SI GRAPH;
  DESS EVT;
FINSI;
FIN;






