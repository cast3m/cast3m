* fichier : amor.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Dynamique
************************************************************************

************************************************************************
*           Test de frontiere LYSMER et KULHEMEYER
************************************************************************
*
* Dans ce test, une onde de compression est generee par une impulsion
* a une extremite d'une barre maille en elements massifs 3D. Cette onde
* est absorb√©e lorsqu'elle arrive a l'autre extremite de la barre munie
* de la frontiere absorbante de Lysmer et Kulhemeyer.
*
* L'absorption est totale si la direction de propagation de l'onde est
* perpendiculaire a la frontiere, ce qui est le cas ici.
*
* F. WANG DM2S/SEMT/EMSI 02/2014
************************************************************************
* Pour les traces, mettre IG1 a VRAI ;                                 *
IG1      = FAUX ;
*                                                                      *
OPTI DIME 3 ELEM CUB8 ;

NE  = 80 ;
DIS = 80. ;
DX  = DIS / NE ;

P0  =  0 0 0 ;
P1  =  0 5 0 ;
L1  =  DROI 1 P0 P1 ;
S1  =  TRAN 1 L1 (0 0 5) ;
SOL =  VOLU NE S1 TRAN (DIS 0 0) ;
S2  =  FACE 2 SOL ;
TITR 'MAILLAGE 3D D UNE BARRE' ;
ELIM SOL 0.001 ;
'SI' IG1 ;
  TRAC SOL ;
'FINS' ;

NU1  = 1./3. ;
VS   = 400. ;
VP   = 800. ;
RHO1 = 2500 ;
G1   = VS*VS*RHO1 ;
E1   = 2*(1 + NU1)*G1 ;

MOD_S  = MODE SOL MECANIQUE ELASTIQUE ISOTROPE CUB8 ;
MAT_S  = MATE MOD_S YOUN E1 NU NU1 RHO RHO1 ;
MAS_S  = MASS MOD_S MAT_S ;
RIG_S  = RIGI MOD_S MAT_S ;

* APPEL AMOR

AMO_S  = AMOR S2 MOD_S MAT_S ;

CL1 = BLOQ UZ UY SOL ;

TITR 'FORCE APPLIQUEE A UNE EXTREMITE' ;
DT       = 0.0125 / 2 ;
NT       = 300 ;
L_TIM    = 'PROG' 0.0 'PAS' DT 'NPAS' NT ;
L_FOR    = 'PROG' 0.  1.E5   0.  -1.E5   297 * 0. ;
EV_FOR   = 'EVOL' 'MANU' 'TEMPS(S)' L_TIM 'FORCE(N)' L_FOR ;
'SI' IG1 ;
  DESS EV_FOR ;
'FINS' ;

MODP = 'MODE' S1 'CHARGEMENT' 'PRESSION' ;
MATP = 'PRES' MODP 'PRES' 1.0 ;
PP   = 'BSIG' MODP MATP ;
P_S1 = 'PRES' 'MASS' MOD_S 1.0 S1  ;
F_S1 = 'EXCO' 'FX' P_S1 'FX' ;
CHA1 = CHAR F_S1 EV_FOR ;

*-----------------------------------------------------------------------
*                 TABLE POUR LA PROCEDURE DYNAMIQUE
*-----------------------------------------------------------------------

CH_DEPI = MANU CHPO 3 SOL UX 0.0 UY 0.0 UZ 0.0 ;
CH_VITI = MANU CHPO 3 SOL UX 0.0 UY 0.0 UZ 0.0 ;

TAB_DYN = TABLE                                         ;
TAB_DYN.'DEPL' = CH_DEPI                                ;
TAB_DYN.'VITE' = CH_VITI                                ;
TAB_DYN.'RIGI' = RIG_S ET CL1                           ;
TAB_DYN.'MASS' = MAS_S                                  ;
TAB_DYN.'AMOR' = AMO_S                                  ;
TAB_DYN.'CHAR' = CHA1                                   ;
TAB_DYN.'FREQ' = 250                                    ;
TAB_DYN.'DEBU' = 0.0                                    ;
NB_PAS         = ENTI (0.3 / 0.001)                     ;
TAB_DYN.'INST' = PROG 0. PAS 0.001 NPAS (NB_PAS - 1)    ;
RESU = DYNAMIC TAB_DYN ;
TEMPS ;

*-----------------------------------------------------------------------
*                          POST-TRAITEMENT
*-----------------------------------------------------------------------

POI = TABLE ;
POI.1 = P0  ;
POI.2 = SOL POIN PROC (40 0 0) ;
POI.3 = SOL POIN PROC (80 0 0) ;

NP = DIME POI ;
L_TEM = PROG NB_PAS * 0.0 ;
L_ENR = PROG NB_PAS * 0.0 ;
L_D = TABLE ;

I = 1 ;
REPE B1 NP ;
L_D.I  = PROG NB_PAS * 0.0 ;

I = I + 1 ;
FIN B1 ;

I_TEM = 0 ;
REPETER B2 NB_PAS ;
  I_TEM = I_TEM + 1           ;
  TEM_I = RESU.I_TEM.'TEMP'   ;
  REMP L_TEM I_TEM TEM_I      ;

  CHD_I = RESU.I_TEM.'DEPL'   ;
  EPS_I = EPSI MOD_S CHD_I ;
  SIG_I = SIGM MOD_S MAT_S CHD_I ;
  ENR_I = ENER MOD_S EPS_I SIG_I ;
  ENR_I = INTG MOD_S ENR_I ;
  I = 1 ;
    REPE B2_1 NP ;
    REMP L_D.I I_TEM (EXTR CHD_I POI.I UX)  ;
    REMP L_ENR I_TEM ENR_I ;
  I = I + 1 ;
  FIN B2_1 ;

FIN B2 ;

EV_DEP = TABLE ;

I = 1 ;
REPE B3 NP ;
Z_I = COOR 1 POI.I ;

TITRE 'DEPLACEMENT POINT' I ' Z =' Z_I ' M' ;
EV_DEP.I = EVOL MANU 'TEMPS (S)' L_TEM 'DEPL (M)' L_D.I ;
I = I + 1 ;
FIN B3 ;

TITR 'DEPLACEMENTS DES POINTS A 0, 40 ET 80 M' ;
'SI' IG1 ;
  DESS ((COUL ROUG EV_DEP.1) ET (COUL VERT EV_DEP.2) ET
        (COUL BLEU EV_DEP.3)) MIMA ;
'FINS' ;

TITRE 'EVOLUTION D ENERGIE DE DEFORMATION DANS LA BARRE' ;
EV_ENR = EVOL ROSE MANU 'TEMPS (S)' L_TEM 'ENERGIE' L_ENR ;
'SI' IG1 ;
  DESS EV_ENR MIMA ;
'FINS' ;

E_MAX = MAXI L_ENR ;
E_FIN = EXTR L_ENR (DIME L_ENR) ;
E_RES = E_FIN / E_MAX ;
*
*             CODE FONCTIONNEMENT
*
SI (E_RES < 1.E-4) ;
  MESS 'TAUX D ENERGIE RESIDUELLE = ' E_RES ;
SINON ;
   ERRE 5 ;
FINSI ;
*                                                                      *
FIN ;










