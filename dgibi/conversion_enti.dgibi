************************************************************************
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
* NOM         : conversion_enti.dgibi
*
* DESCRIPTION : Comparaison entre les differentes fonctions pour
*               convertir un reel en entier
*
* MOTS-CLÉS   : troncature,partie entiere,arrondi,proche,floor,ceiling,
*               plancher,plafond,round
*
************************************************************************
* HISTORIQUE : 18/12/2014 : JCARDO : creation du jeu de données
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
* Prière de PRENDRE LE TEMPS DE COMPLÉTER LES COMMENTAIRES
* en cas de modification de ce cas test afin de faciliter
* la maintenance !
************************************************************************

OPTI 'ECHO' 0 ;


* GRAINE ALEATOIRE
SEED1 = 235 ;

* NOMBRE DE VALEURS DANS LES LISTES ALEATOIRES
NB1 = 1000 ;
* NOMBRE D'ELEMENTS PAR COTE DU CUBE
NB2 = 5 ;

* TRACER LES RESULTATS ?
GRAPH = VRAI ;
OPTI 'TRAC' 'PSC' ;



* boucle sur les options
LOPTI = MOTS 'TRON' 'INFE' 'SUPE' 'PROC' ;
NOPTI = DIME LOPTI ;
REPE BLOC1 NOPTI ;
MOPTI = EXTR LOPTI &BLOC1 ;

SI (EGA MOPTI 'TRON') ;
    EMIN1 = -1 ;
    EMAX1 = 1 ;
FINS ;
SI (EGA MOPTI 'INFE') ;
    EMIN1 = -1 ;
    EMAX1 = 0 ;
FINS ;
SI (EGA MOPTI 'SUPE') ;
    EMIN1 = 0 ;
    EMAX1 = 1 ;
FINS ;
SI (EGA MOPTI 'PROC') ;
    EMIN1 = -0.5 ;
    EMAX1 = 0.5 ;
FINS ;

SAUT 3 'LIGNES' ;
MESS '+----------------------------------+' ;
MESS '|                                  |' ;
MESS '|      O P T I O N   " ' MOPTI ' "      |' ;
MESS '|                                  |' ;
MESS '+----------------------------------+' ;




* ===============================
* TEST 1 POUR LES OBJETS LISTREEL
* ===============================

SAUT 1 'LIGNE' ;
MESS 'TEST 1 LISTREEL' ;
MESS '===============' ;

LREE1 = BRUI 'BLAN' 'UNIF' 0 100 NB1 SEED1 ;
LENT1 = ENTI MOPTI LREE1 ;

LECA1 = LENT1 - LREE1 ;
EMIN2 = MINI LECA1 ;
EMAX2 = MAXI LECA1 ;

MESS (CHAI 'MINI{ (ENTI "' MOPTI '" LREE1) - LREE1 } = ' EMIN2) ;
MESS (CHAI 'MAXI{ (ENTI "' MOPTI '" LREE1) - LREE1 } = ' EMAX2) ;

SI GRAPH ;
    EVOL1 = EVOL 'ROUG' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               LECA1 ;
    EVOL2 = EVOL 'VERT' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               (PROG (DIME LREE1)*0.5) ;
    EVOL3 = EVOL 'VERT' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               (PROG (DIME LREE1)*-0.5) ;
    EVOL4 = EVOL 'BLEU' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               (PROG (DIME LREE1)*1.) ;
    EVOL5 = EVOL 'BLEU' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               (PROG (DIME LREE1)*-1.) ;
    EVOL6 = EVOL 'BLAN' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE1))
                               (PROG (DIME LREE1)*0.) ;
    DESS (EVOL1 ET EVOL2 ET EVOL3 ET EVOL4 ET EVOL5 ET EVOL6)
         'TITR' (CHAI '(ENTI "' MOPTI '" LREE1) - LREE1')
         'TITY' 'Ecart absolu'
         'YBOR' -1.5 1.5 'YGRA' 0.5 ;
FINS ;

********* VALIDATION *********
SI ((EMAX2 >EG EMAX1) OU (EMIN2 <EG EMIN1)) ;
    ERRE 5 ;
FINS ;
******************************




* ===============================
* TEST 2 POUR LES OBJETS LISTREEL
* ===============================

SAUT 1 'LIGNE' ;
MESS 'TEST 2 LISTREEL' ;
MESS '===============' ;

LREE2 = PROG 0.5 'PAS' 1. 'NPAS' NB1 ;
LENT2 = ENTI MOPTI LREE2 ;

LECA2 = LENT2 - LREE2 ;
EMIN2 = MINI LECA2 ;
EMAX2 = MAXI LECA2 ;

MESS (CHAI 'MINI{ (ENTI "' MOPTI '" LREE2) - LREE2 } = ' EMIN2) ;
MESS (CHAI 'MAXI{ (ENTI "' MOPTI '" LREE2) - LREE2 } = ' EMAX2) ;

* SI GRAPH ;
*     EVOL1 = EVOL 'ROUG' 'MANU' (PROG 1. 'PAS' 1. (DIME LREE2)) LECA2 ;
*     DESS EVOL1 'TITR' (CHAI '(ENTI "' MOPTI '" LREE2) - LREE2')
*                'TITY' 'Ecart absolu' ;
* FINS ;

********* VALIDATION *********
SI (EGA MOPTI 'TRON') ;
    EMIN1 = -1 ;
    EMAX1 = 1 ;
    EVAL1 = -0.5 ;
FINS ;
SI (EGA MOPTI 'INFE') ;
    EMIN1 = -1 ;
    EMAX1 = 0 ;
    EVAL1 = -0.5 ;
FINS ;
SI (EGA MOPTI 'SUPE') ;
    EMIN1 = 0 ;
    EMAX1 = 1 ;
    EVAL1 = 0.5 ;
FINS ;
SI (EGA MOPTI 'PROC') ;
    EMIN1 = -0.5 ;
    EMAX1 = 0.5 ;
    EVAL1 = 0.5 ;
FINS ;

SI ((EMAX2 NEG EVAL1 1.E-7) OU (EMIN2 NEG EVAL1 1.E-7)) ;
    ERRE 5 ;
FINS ;
******************************




* ===========================
* TEST POUR LES OBJETS ENTIER
* ===========================

SAUT 1 'LIGNE' ;
MESS 'TEST ENTIER' ;
MESS '===========' ;

ENTI1 = SEED1 ;
ENTI2 = ENTI ENTI1 ;

MESS (CHAI 'ENTI1 = ' ENTI1 '      (ENTI "' MOPTI '" ENTI1) = ' ENTI2) ;

********* VALIDATION *********
SI (NEG ENTI1 ENTI2) ;
    ERRE 5 ;
FINS ;
******************************




* =============================
* TEST POUR LES OBJETS FLOTTANT
* =============================

SAUT 1 'LIGNE' ;
MESS 'TEST FLOTTANT' ;
MESS '=============' ;

FLOT1 = EXTR LREE1 1 ;
ENTI1 = ENTI MOPTI FLOT1 ;

MESS (CHAI 'FLOT1 = ' FLOT1 '      (ENTI "' MOPTI '" FLOT1) = ' ENTI1) ;

********* VALIDATION *********
ECAR1 = ENTI1 - FLOT1 ;
SI ((ECAR1 <EG EMIN1) OU (ECAR1 >EG EMAX1)) ;
    ERRE 5 ;
FINS ;
******************************



* ============================
* TEST POUR LES OBJETS CHPOINT
* ============================

SAUT 1 'LIGNE' ;
MESS 'TEST CHPOINT' ;
MESS '============' ;

OPTI 'DIME' 3 'ELEM' 'CUB8' ;
MAIL2 = (0. 0. 0.) DROI        NB2 (1. 0. 0.)
                   TRAN        NB2 (0. 1. 0.)
                   VOLU 'TRAN' NB2 (0. 0. 0.5) ;
MAIL3 = CHAN 'TET4' (MAIL2 PLUS (0. 0. 1.)) ;
MAIL1 = MAIL2 ET MAIL3 ;

COMP1 = BRUI 'BLAN' 'UNIF' 0. 100. MAIL1 SEED1 ;
COMP2 = BRUI 'BLAN' 'UNIF' 0. 100. MAIL1 SEED1 ;
CHPO1 = (NOMC COMP1 'UX' 'NATURE' 'DISCRET') ET
        (NOMC COMP2 'UY' 'NATURE' 'DISCRET') ;
CHPO2 = ENTI MOPTI CHPO1 ;

CECA1 = CHPO2 - CHPO1 ;
EMIN2 = MINI CECA1 ;
EMAX2 = MAXI CECA1 ;

MESS (CHAI 'MINI{ (ENTI "' MOPTI '" CHPO1) - CHPO1 } = ' EMIN2) ;
MESS (CHAI 'MAXI{ (ENTI "' MOPTI '" CHPO1) - CHPO1 } = ' EMAX2) ;

SI GRAPH ;
    TRAC MAIL1 (EXCO CECA1 'UX')
        'TITR' (CHAI '(ENTI "' MOPTI '" CHPO1) - CHPO1 (comp. UX)') ;
    TRAC MAIL1 (EXCO CECA1 'UY')
        'TITR' (CHAI '(ENTI "' MOPTI '" CHPO1) - CHPO1 (comp. UY)') ;
FINS ;

********* VALIDATION *********
SI ((EMAX2 >EG EMAX1) OU (EMIN2 <EG EMIN1)) ;
    ERRE 5 ;
FINS ;
******************************




* TEST 1 POUR LES OBJETS MOT
* ==========================

SAUT 1 'LIGNE' ;
MESS 'TEST 1 MOT' ;
MESS '==========' ;

MOT1 = CHAI SEED1 ;
ENTI1 = ENTI MOT1 ;

MESS (CHAI 'MOT1 = "' MOT1 '"            '
           '10*(ENTI "' MOPTI '" MOT1) = ' (10*ENTI1)) ;

********* VALIDATION *********
SI (NEG ENTI1 SEED1) ;
    ERRE 5 ;
FINS ;
******************************




* TEST 2 POUR LES OBJETS MOT
* ==========================

SAUT 1 'LIGNE' ;
MESS 'TEST 2 MOT' ;
MESS '==========' ;

MOT2 = '-2.98716E+2' ;
ENTI2 = ENTI MOPTI MOT2 ;

MESS (CHAI 'MOT2 = "' MOT2 '"            '
           '10*(ENTI "' MOPTI '" MOT2) = ' (10*ENTI2)) ;

********* VALIDATION *********
SI (EGA MOPTI 'TRON') ;
    ENTI20 = -298 ;
FINS ;
SI (EGA MOPTI 'INFE') ;
    ENTI20 = -299 ;
FINS ;
SI (EGA MOPTI 'SUPE') ;
    ENTI20 = -298 ;
FINS ;
SI (EGA MOPTI 'PROC') ;
    ENTI20 = -299 ;
FINS ;

SI (NEG ENTI2 ENTI20) ;
    ERRE 5 ;
FINS ;
******************************




* TEST POUR LES OBJETS LISTMOTS
* =============================

SAUT 1 'LIGNE' ;
MESS 'TEST LISTMOTS' ;
MESS '=============' ;

LMOT1 = MOTS '412' '5.98' '1.E2' '-9.3' '10.5' ;
LENT1 = ENTI MOPTI LMOT1 ;

REPE BLOC2 (DIME LMOT1) ;
    MESS (CHAI 'LMOT1[' &BLOC2 '] = "' (EXTR LMOT1 &BLOC2) '"         '
               '10*(ENTI "' MOPTI '" LMOT1[' &BLOC2 ']) = '
               (10*(EXTR LENT1 &BLOC2))) ;
FIN BLOC2 ;

********* VALIDATION *********
SI (EGA MOPTI 'TRON') ;
    LENT10 = LECT 412 5 100 -9 10 ;
FINS ;
SI (EGA MOPTI 'INFE') ;
    LENT10 = LECT 412 5 100 -10 10 ;
FINS ;
SI (EGA MOPTI 'SUPE') ;
    LENT10 = LECT 412 6 100 -9 11 ;
FINS ;
SI (EGA MOPTI 'PROC') ;
    LENT10 = LECT 412 6 100 -9 11 ;
FINS ;

SI ((MAXI 'ABS' (LENT10 - LENT1)) NEG 0) ;
    ERRE 5 ;
FINS ;
******************************




FIN BLOC1 ;


********************************************************
* Visualisation des 4 fonctions : TRON, INFE, SUPE, PROC
********************************************************
SI GRAPH ;
    LX  = PROG -3. 'PAS' 0.001 3. ;
    LY1 = ENTI LX ;
    LY2 = ENTI 'INFERIEUR' LX ;
    LY3 = ENTI 'SUPERIEUR' LX ;
    LY4 = ENTI 'PROCHE'    LX ;
    LY5 = LX ;
    EVOL1 = EVOL 'VERT' 'MANU' LX (FLOT LY1) ;
    EVOL2 = EVOL 'ROUG' 'MANU' LX (FLOT LY2) ;
    EVOL3 = EVOL 'BLEU' 'MANU' LX (FLOT LY3) ;
    EVOL4 = EVOL 'ORAN' 'MANU' LX (FLOT LY4) ;
    EVOL5 = EVOL 'BLAN' 'MANU' LX LY5 ;
    TAB1 = TABL ;
    TAB1 . 'TITRE' = TABL ;
    TAB1 . 'TITRE' . 1 = MOT 'Y=TRONCATURE(X)' ;
    TAB1 . 'TITRE' . 2 = MOT 'Y=INFERIEUR(X)' ;
    TAB1 . 'TITRE' . 3 = MOT 'Y=SUPERIEUR(X)' ;
    TAB1 . 'TITRE' . 4 = MOT 'Y=PROCHE(X)' ;
    TAB1 . 'TITRE' . 5 = MOT 'Y=X' ;
    TAB1 . 2 = 'TIRR' ;
    TAB1 . 3 = 'TIRR' ;
    TAB1 . 5 = 'TIRL' ;
    DESS (EVOL1 ET EVOL2 ET EVOL3 ET EVOL4 ET EVOL5)
         'TITX' 'X' 'TITY' 'Y' 'LEGE' TAB1
         'XBOR' -3.5 3.5 'XGRA' 0.5
         'YBOR' -4. 4. 'YGRA' 1. ;
FINS ;
********************************************************



* OPTI 'ECHO' 1 'TRAC' 'X' 'DONN' 5;

FIN ;





