* fichier :  elas13.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas13.dgibi: Jeux de données        *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*______________________________________________________
*   TEST ELAS13                                       *
*                                                     *
*   POUTRE SUR SOL ELASTIQUE, EXTREMITES ARTICULEES   *
*                                                     *
*   Test V.P.C.S. numero SSLL16/89                    *
*                                                     *
*   Reference : M. Courtand et P. Lebelle, Formulaire *
*         du beton arme, T. 2, Paris, Eyrolles, 1976  *
*                                                     *
*   La structure est constituee d'une poutre articulee*
*   aux extremites et reposant sur un sol elastique.  *
*                                                     *
*      On se propose de calculer la rotation et la    *
*   reaction a une extremite, ainsi que la fleche et  *
*   le moment de flexion au centre de la poutre.      *
*                                                     *
*     Les valeurs trouvees sont ensuites comparees    *
*   aux valeurs de reference obtenues par solution    *
*   analytique.                                       *
*                                                     *
*______________________________________________________
*
OPTI DIME 3;
OPTI ELEM SEG2;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  maillage - geometrie               *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
L = PI * (10 ** .5) / 2.;
*
PA = (-.5 * L) 0. 0.;
PB = (0.5 * L) 0. 0.;
*
SUR1 = PA DROI 8 PB;
PD = SUR1 POIN PROC (0. 0. 0.);
*
SI (NEG GRAPH 'N');
   TITR 'ELAS13 : MAILLAGE';
   TRAC (0. 0. 1000.) QUAL SUR1;
FINSI;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               calcul de la rigidite                 *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE POUT;
MATR1 = MATE MODL1 YOUN 2.E11 NU 0.3 TORS 1000.
                   SECT 1000 INRY 1.E-4 INRZ 1.E-4;
RIGI1 = RIGI MODL1 MATR1;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*              conditions aux limites                 *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*  - Rotule en A et B,
*  - Appui continu elastique : sol de raideur lineique
*     constante : K = 840 000 N/m2 ,
*  - Conditions d'un probleme plan.
*
CL1 = BLOQ UX (PA ET PB);
CL2 = BLOQ UY (PA ET PB);
*
CL3 = BLOQ UZ SUR1;
CL4 = BLOQ RX SUR1;
*
NEL1 = NBEL SUR1;
K = 840000;
KNOEUD = K * L / (2. * NEL1);
I = 0;
CL5 = APPUI DEPL 0. SUR1;
REPETER BLOC2 NEL1;
  I = I + 1;
  POI1 = SUR1 POIN I;
  POI2 = SUR1 POIN (I + 1);
  APP1 = APPUI DEPL KNOEUD POI1;
  APP2 = APPUI DEPL KNOEUD POI2;
  CL5 = CL5 ET APP1 ET APP2;
FIN BLOC2;
*
RIGI1 = RIGI1 ET CL1 ET CL2 ET CL3 ET CL4 ET CL5;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                      chargement                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*  - Force ponctuelle verticale de -10000 N au point D,
*  - Force lineique repartie verticale de -5000 N/m,
*  - Moments opposes aux extremites de 15000 N.m .
*
CHA1 = FORC (0. -10000. 0.) PD;
*
FTOTAL = -5000. * L;
FPOINT = FTOTAL / (2. * NEL1);
CHA2 = FORC FY 0. PA;
I = 0;
REPETER BLOC1 NEL1;
  I = I + 1;
  POI1 = SUR1 POIN I;
  POI2 = SUR1 POIN (I + 1);
  FOR1 = FORC FY FPOINT POI1;
  FOR2 = FORC FY FPOINT POI2;
  CHA2 = CHA2 ET FOR1 ET FOR2;
FIN BLOC1;
*
CHA3 = (MOMENT MZ -15000 PA) ET (MOMENT MZ 15000 PB);
*
CHAR1 = CHA1 ET CHA2 ET CHA3;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*           resolution : champs de deplacements       *
*                        champ de contraintes         *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
DEPL1 = RESO RIGI1 CHAR1;
*
* Trace facultatif de la deformee
*
REAC1 = REAC RIGI1 DEPL1;
SI (NEG GRAPH 'N');
   TITR 'ELAS13 : DEFORMEE, CHARGEMENT, REACTIONS';
   VECT1 = VECT CHAR1 1.E-4 FX FY FZ ROUGE;
   VECT2 = VECT REAC1 1.E-4 FX FY FZ BLEU;
   DEF0 = DEFO SUR1 DEPL1 0.;
   DEF1 = DEFO SUR1 DEPL1 20. (VECT1 ET VECT2) VERT;
   TRAC (0. 0. 1000.) SUR1 (DEF0 ET DEF1);
FINSI;
*
SIGM1 = SIGM MODL1 MATR1 DEPL1;
SIGM2 = CHAN NOEUD MODL1 SIGM1;
SIGM3 = CHAN CHPO MODL1 SIGM2;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*              extraction des resultats               *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
* Rotation du point A :
RZA1 = EXTR DEPL1 RZ PA;
RZA2 = -3.045E-3;
ERG1 = 100 * (ABS ((RZA2 - RZA1) / RZA2));
*
* Reaction au point A :
VA1  = EXTR REAC1 FY PA;
VA2  = 1.1674E4;
ERG2 = 100 * (ABS ((VA2 - VA1) / VA2));
*
* Fleche au point D :
UYD1 = EXTR DEPL1 UY PD;
UYD2 = -4.23326E-3;
ERG3 = 100 * (ABS ((UYD2 - UYD1) / UYD2));
*
* Moment de flexion au point D :
MD1  = EXTR SIGM3 MOMZ PD;
MD2  = 3.3840E4;
ERG4 = 100 * (ABS ((MD1 - MD2) / MD2));
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               affichage des resultats               *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MESS 'RESULTATS : TEST ELAS13';
MESS '-----------------------';
SAUT LIGN;
*
MESS ' Rotation du point A ';
MESS ' ------------------- ';
MESS '      Valeur theorique :' RZA2 'rad';
MESS '      Valeur calculee  :' RZA1 'rad';
MESS '                 Ecart :' ERG1 '%';
SAUT LIGN;
*
MESS ' Reaction au point A ';
MESS ' ------------------- ';
MESS '      Valeur theorique :' VA2 'N';
MESS '      Valeur calculee  :' VA1 'N';
MESS '                 Ecart :' ERG2 '%';
SAUT LIGN;
*
MESS ' Fleche au point D ';
MESS ' ----------------- ';
MESS '      Valeur theorique :' UYD2 'm';
MESS '      Valeur calculee  :' UYD1 'm';
MESS '                 Ecart :' ERG3 '%';
SAUT LIGN;
*
MESS ' Moment de flexion au point D ';
MESS ' ---------------------------- ';
MESS '      Valeur theorique :' MD2 'N.m';
MESS '      Valeur calculee  :' MD1 'N.m';
MESS '                 Ecart :' ERG4 '%';
SAUT LIGN;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                code fonctionnement                  *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++*
ERGMAX = MAXI (PROG ERG1 ERG2 ERG3 ERG4);
SI (ERGMAX <EG 5.);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;

SAUT 1 LIGN ;
TEMPS;

FIN;




