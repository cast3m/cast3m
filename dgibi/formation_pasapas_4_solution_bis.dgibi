************************************************************************
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Variation d'un jeu sous l'action d'une sollicitation thermique en    *
* regime transitoire                                                   *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - l'utilisation d'un modele de convection pour representer l'echange *
*   thermique, avec un coefficien td'echange variable)                 *
* - la definition du chargement                                        *
* - la mise a jour du coefficient d'echange selon la force de contact  *
*   (avec la procedure REEV_MEC)                                       *
* - la resolution avec PASAPAS                                         *
* - le post traitement                                                 *
************************************************************************


** Options generales
OPTI 'DIME' 2 'ELEM' 'QUA4' 'MODE' 'PLAN' 'DEFO' 'ECHO' 0 ;
ITRAC = FAUX ;


** Parametres geometriques, materiaux et CL
L       = 1. ;
EPAI    = 0.1 ;
JEU_INI = 0.01*L ;
LAMBDA1 = 60. ;
LAMBDA2 = 80. ;
CP1     = 800. ;
CP2     = 100. ;
RHO1    = 7800. ;
RHO2    = 7800. ;
ALPHA1  = 12.E-6 ;
ALPHA2  = 12.E-6 ;
YOUNG1  = 200.E9 ;
YOUNG2  = 200.E9 ;
NU1     = 0.3 ;
NU2     = 0.3 ;
T_INI = 20. ;
T1    = 500. ;
T4    = 20. ;
TEMPS_F = 5.E5 ;
XPAS = 5.E3 ;


** Maillage
P1 = 0. 0. ;
P2 = L 0. ;
L0 = DROI 10 P1 P2 ;
S1 = L0 TRAN 1 (0. EPAI) ;
L1 = S1 COTE 4 ;
L2 = S1 COTE 2 ;
S2 = S1 PLUS ((L + JEU_INI) 0.) ;
L3 = S2 COTE 4 ;
L4 = S2 COTE 2 ;
P3 = S2 POIN 'PROC' ((L + JEU_INI) 0.) ;
P4 = S2 POIN 'PROC' (((2. * L) + JEU_INI) 0.) ;
MAILT  = S1 ET S2 ;
MCONT1 = IMPO 'MAIL' L2 L3 ;
SI ITRAC ;
  TRAC MAILT 'TITR' 'Maillage' ;
FINSI ;


** Modeles et materiaux
MODM1 = MODE S1    'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MATM1 = MATE MODM1 'ALPHA' ALPHA1 'YOUN' YOUNG1 'NU' NU1 ;
MODM2 = MODE S2    'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MATM2 = MATE MODM2 'ALPHA' ALPHA2 'YOUN' YOUNG2 'NU' NU2 ;
MODC  = MODE MCONT1 'CONTACT' 'UNILATERAL' ;
MODT1 = MODE S1    'THERMIQUE' 'ISOTROPE' ;
MATT1 = MATE MODT1 'K' LAMBDA1 'C' CP1 'RHO' RHO1 ;
MODT2 = MODE S2    'THERMIQUE' 'ISOTROPE' ;
MATT2 = MATE MODT2 'K' LAMBDA2 'C' CP2 'RHO' RHO2 ;
MRACC = RACC (1.1 * JEU_INI) L2 L3 ;
MODRACC = MODE MRACC 'THERMIQUE' 'CONVECTION' ;
HVSP = EVOL 'MANU' 'PC' (PROG 0. 5.E8 5.1E8) 'H' (PROG 0. 1.E4 1.E4) ;
MATRACC = MATE MODRACC 'H' HVSP ;
MOD_TH = MODT1 ET MODT2 ET MODRACC ;
MAT_TH = MATT1 ET MATT2 ET MATRACC ;
MOD_ME = MODM1 ET MODM2 ET MODC ;
MAT_ME = MATM1 ET MATM2 ;


** Blocages mecaniques
CL1   = BLOQ 'UY' MAILT ;
CL2   = BLOQ 'UX' L1 ;
CL3   = BLOQ 'UX' L4 ;
CL_ME = CL1 ET CL2 ET CL3 ;


** Blocages thermiques
CL4   = BLOQ 'T' L1 ;
CL5   = BLOQ 'T' L4 ;
CL_TH = CL4 ET CL5 ;


** Chargements en temperatures imposees
TEM1 = DEPI CL4 T1 ;
TEM2 = DEPI CL5 T4 ;
EV1  = EVOL 'MANU' (PROG 0. TEMPS_F) (PROG 1. 1.) ;
CHA1 = CHAR 'TIMP' (TEM1 ET TEM2) EV1 ;


** Chargement decrivant le parametre PC (pression de contact)
CH_PC = MANU 'CHPO' MRACC 'PC' 0. ;
CHA2 = CHAR 'PC' CH_PC EV1 ;


** Champ de temperature initiale
TEM0 = MANU 'CHPO' MAILT 1 'T' T_INI ;


** Procedure REEV_MEC pour actualiser le chargement decrivant la
*  pression de contact
DEBP REEV_MEC T1*'TABLE' ;
  R1 = T1 . 'ESTIMATION' . 'REACTIONS' ;
  WT = T1 . 'WTABLE' ;
  TAM = T1 . 'AMOI' ;
  L3 = TAM . 'LIGNE_3' ;
  MR = TAM . 'MAIL' ;
  SI ((T1 . 'ESTIMATION' . 'TEMPS') NEG 0.) ;
* calcul de la pression de contact
    PARA_P = (MAXI 'ABS' (RESU (REDU R1 L3))) / (MESU L3) ;
* chargment decrivant le parametre 'PC'
    CH_PC = MANU 'CHPO' MR 'PC' PARA_P ;
    EV1 = TAM . 'EVOL' ;
    CHA3 = CHAR 'PC' CH_PC EV1 ;
* on ecrase le chargement global de WTABLE
    WT . 'CHARGEMENT' = (TAM . 'CHAR_0') ET CHA3 ;
  FINS ;
FINP ;


** Resolution avec PASAPAS
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MOD_ME ET MOD_TH ;
TAB1 . 'CARACTERISTIQUES'    = MAT_ME ET MAT_TH ;
TAB1 . 'BLOCAGES_MECANIQUES' = CL_ME ;
TAB1 . 'BLOCAGES_THERMIQUES' = CL_TH ;
TAB1 . 'TEMPERATURES'        = TABL ;
TAB1 . 'TEMPERATURES' . 0    = TEM0 ;
TAB1 . 'CHARGEMENT'          = CHA1 ET CHA2 ;
TAB1 . 'TEMPS_CALCULES'      = PROG XPAS 'PAS' XPAS TEMPS_F ;
TAB1 . 'CONVERGENCE_MEC_THE' = VRAI ;
TAB1 . 'PROCEDURE_REEV_MEC'  = VRAI ;
TAB1 . 'AMOI'                = TABL ;
TAB1 . 'AMOI' . 'LIGNE_3'    = L3 ;
TAB1 . 'AMOI' . 'MAIL'       = MRACC ;
TAB1 . 'AMOI' . 'EVOL'       = EV1 ;
TAB1 . 'AMOI' . 'CHAR_0'     = CHA1 ;
PASAPAS TAB1 ;


** Post-traitement
LTPS = PROG ;
LJEU = PROG ;
LT2  = PROG ;
LT3  = PROG ;
NB1  = DIME (TAB1 . 'TEMPS') ;
MESS '  Temps          Jeu              T2              T3' ;
* listage du jeu et des temperatures a chaque pas de temps
CONF0 = FORM ;
REPE B1 NB1 ;
  TPS1 = TAB1 . 'TEMPS' . (&B1 - 1) ;
  LTPS = LTPS ET TPS1 ;
  TE1 = TAB1 . 'TEMPERATURES' . (&B1 - 1) ;
  U1 = TAB1 . 'DEPLACEMENTS' . (&B1 - 1) ;
  CONF1 = FORM U1 ;
  J1 = NORM (P3 MOIN P2) ;
  T2 = EXTR TE1 'T' P2 ;
  T3 = EXTR TE1 'T' P3 ;
  MESS TPS1 J1 T2 T3 ;
  LJEU = LJEU ET J1 ;
  LT2 = LT2 ET T2 ;
  LT3 = LT3 ET T3 ;
  SI ITRAC ;
    TRAC TE1 MAILT (PROG T4 PAS ((T1 - T4) / 20.) T1) 'TITR' (CHAI 'Temperature au temps' TPS1) ;
  FINSI ;
  FORM CONF0 ;
FIN B1 ;
* temperature VS temps aux points P2 et P3
JEUVST = EVOL 'ROUG' 'MANU' 'Temps' LTPS 'Jeu' LJEU ;
T2VST = EVOL 'ROUG' 'MANU' 'Temps' LTPS 'Temperature' LT2 ;
T3VST = EVOL 'BLEU' 'MANU' 'Temps' LTPS 'Temperature' LT3 ;
TLEG               = TABL ;
TLEG . 'TITRE'     = TABL ;
TLEG . 'TITRE' . 1 = 'Point P2';
TLEG . 'TITRE' . 2 = 'Point P3' ;
SI ITRAC ;
  DESS JEUVST 'TITR' 'Jeu VS temps' ;
  DESS (T2VST ET T3VST) 'TITR' 'Temperature VS temps' 'LEGE' TLEG ;
FINSI ;

FIN ;









