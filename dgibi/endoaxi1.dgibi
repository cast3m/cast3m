* fichier :  endoaxi1.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*           Test Endoaxi1.dgibi: Jeux de données      *
*           ------------------------------------      *
*                                                     *
*
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
*******************************************************
*                                                     *
* TEST;  MATERIAU ELASTO-PLASTIQUE ENDOMMAGEABLE      *
*                                                     *
* EPROUVETTE EN TRACTION AVEC DEPLACEMENTS IMPOSES.   *
* MAILLAGE AXISYMETRIQUE.                             *
*                                                     *
* LES RESULTATS OBTENUS SONT COMPARES  A LA SOLUTION  *
* ANALYTIQUE OBTENUE PAR ALGORITHME.                  *
* ON TESTE - LA DEFORMATION PLASTIQUE CUMULEE         *
*            (composante EPSE dans VARIABLES_INTERNES)*
*          - D, LA VARIABLE D'ENDOMMAGEMENT           *
*            (DANS VARIABLES_INTERNES).               *
*          - SIGMAzz ( dans CONTRAINTES ).            *
*                                                     *
* Test effectue par Mr STRUB (cf rapport DMT/91 .558).*
* Test modifie pour evolution 2/06/92.                *
*******************************************************

GRAPH = 'N';

OPTI ECHO 1 DIME 2 ELEM QUA8 MODE AXIS;
opti trac bens;
*------------------------------------------------------
   P1 = 0 0;          P2 = 3E-3 0;
   P3 = 3E-3 30E-3;   P4 = 0 30E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 3 ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 3 ;
*
   EPROU = L1 L2 L3 L4 DALLER PLAN ;

SI ( NEG GRAPH 'N');
   trac eprou;
FINSI;

   CL1 = BLOQ L1 UZ ;
   CL2 = BLOQ L3 UZ ;
   CL3 = BLOQ L4 UR ;
   CL = CL1 ET CL2 ET CL3 ;
   DEP = DEPI CL2 1E-3 ;
*
   MO = MODE EPROU 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE'
'ENDOMMAGEABLE';
*
  PROGEPS = PROG 0. .47285E-2 .76066E-2 .89226E-2
   1.0533E-2 1.4011E-2 1.9096E-2 2.6081E-2 3.4654E-2
   4.5828E-2 6.0813E-2 7.2835E-2 8.3328E-2 9.5526E-2
   10.691E-2 11.808E-2 12.784E-2 13.891E-2 14.936E-2
   15.700E-2 16.746E-2 17.720E-2 18.946E-2 19.860E-2
   20.703E-2 21.607E-2 22.802E-2 23.685E-2 24.919E-2
   26.083E-2 ;
  PROGSIG = PROG 0. 350.E06 370.59E06 379.18E06
   387.37E06 400.52E06 414.22E06 427.94E06 440.69E06
   453.63E06 467.26E06 476.30E06 483.24E06 490.46E06
   496.56E06 502.06E06 506.53E06 511.28E06 515.51E06
   518.45E06 522.29E06 525.71E06 529.81E06 532.73E06
   535.33E06 538.03E06 541.47E06 543.92E06 547.23E06
   550.24E06 ;
   CTRAC = EVOL MANU EPS PROGEPS TRAC PROGSIG ;

SI ( NEG GRAPH 'N');
   DESS CTRAC;
FINSI;

  MAT = MATE MO YOUN 74020.E06 NU 0.3 RHO 7800. ALPH 0.
               TRAC CTRAC EPSD 0.03 DC 0.23 EPSR 0.25 ;
*
* LISTE DES PAS DE CALCUL :
*                         0.13 MM INCREMENT ELASTIQUE
*                         0.16 MM INCREMENT PLASTIQUE
si complet;
    LIS = PROG 0.13 0.16
               0.5 1. 1.25 1.5 2. 2.5 3. 3.5 4. 4.5
               5. 5.5 6. 6.5 7. 7.5 8.;
sinon;
     LIS = prog  0.13 0.16  0.5 ;
finsi;
*
    LI1 = PROG 0. 30. ; LI2 = PROG 0. 30. ;
    EV = EVOL MANU T LI1 DEP(T) LI2 ;
    CHA = CHAR 'DIMP' DEP EV ;
    TAB = TABLE ;

    TAB.'BLOCAGES_MECANIQUES' = CL;
    TAB.'CARACTERISTIQUES' = MAT;
    TAB.'CHARGEMENT' = CHA;
    TAB.'MODELE' = MO;
    TAB.'TEMPS_CALCULES' = LIS;
    TAB.'PRECISION' = 1E-08;
*
 PASAPAS  TAB ;

*******************************************************
***************     TEST DE FONCTIONNEMENT  ***********
*******************************************************

dpc0 = extr TAB.VARIABLES_INTERNES.2 EPSE 1 1 1;
dpc1 = extr TAB.VARIABLES_INTERNES.3 EPSE 1 1 1;
dpc0t = 5.46e-4;dpc1t =1.12694E-02;
si complet;
dpc2 = extr TAB.VARIABLES_INTERNES.7 EPSE 1 1 1;
dpc4 = extr TAB.VARIABLES_INTERNES.11 EPSE 1 1 1;
dpc6 = extr TAB.VARIABLES_INTERNES.15 EPSE 1 1 1;
dpc75 =extr TAB.VARIABLES_INTERNES.18 EPSE 1 1 1;

 dpc2t = 6.03e-2; dpc4t = 12.65e-2; dpc6t = 19.29e-2;
dpc75t = 24.27e-2;
finsi;
D0 =  extr TAB.VARIABLES_INTERNES.2 'VHWD' 1 1 1;
D1 =  extr TAB.VARIABLES_INTERNES.3 'VHWD' 1 1 1;
D0T = 0.; D1T= 0.0;
si complet;
D2 =  extr TAB.VARIABLES_INTERNES.7 'VHWD' 1 1 1;
D4 =  extr TAB.VARIABLES_INTERNES.11 'VHWD' 1 1 1;
D6 =  extr TAB.VARIABLES_INTERNES.15 'VHWD' 1 1 1;
D75 =  extr TAB.VARIABLES_INTERNES.18 'VHWD' 1 1 1;

 D2T= 0.0257; D4T= 0.0901; D6T= 0.1622; D75T= 0.2199;
  finsi;

SZZ0 = EXTR TAB.CONTRAINTES.2 SMZZ 1 1 1;
SZZ1 = EXTR TAB.CONTRAINTES.3 SMZZ 1 1 1;
SZZ0T = 354.3E6; SZZ1T =  4.07984E+08;
si complet;
SZZ2 = EXTR TAB.CONTRAINTES.7 SMZZ 1 1 1;
SZZ4 = EXTR TAB.CONTRAINTES.11 SMZZ 1 1 1;
SZZ6 = EXTR TAB.CONTRAINTES.15 SMZZ 1 1 1;
SZZ75 = EXTR TAB.CONTRAINTES.18 SMZZ 1 1 1;

SZZ2T = 459.3E6; SZZ4T = 461.4E6; SZZ6T = 443.3E6;
SZZ75T = 422.4E6;
finsi;

EDPC0 = ABS((DPC0-DPC0T)/DPC0T);
EDPC1 = ABS((DPC1-DPC1T)/DPC1T);
si complet;
EDPC2 = ABS((DPC2-DPC2T)/DPC2T);
EDPC4 = ABS((DPC4-DPC4T)/DPC4T);
EDPC6 = ABS((DPC6-DPC6T)/DPC6T);
EDPC75 = ABS((DPC75-DPC75T)/DPC75T);
finsi;
ED0 = ABS((D0-D0T)/(D0T+0.001));
ED1 = ABS((D1-D1T)/(D1T+0.001));
si complet;
ED2 = ABS((D2-D2T)/D2T);
ED4 = ABS((D4-D4T)/D4T);
ED6 = ABS((D6-D6T)/D6T);
ED75 = ABS((D75-D75T)/D75T);
finsi;

ESZZ0 = ABS((SZZ0-SZZ0T)/SZZ0T);
ESZZ1 = ABS((SZZ1-SZZ1T)/SZZ1T);
si complet;
ESZZ2 = ABS((SZZ2-SZZ2T)/SZZ2T);
ESZZ4 = ABS((SZZ4-SZZ4T)/SZZ4T);
ESZZ6 = ABS((SZZ6-SZZ6T)/SZZ6T);
ESZZ75 = ABS((SZZ75-SZZ75T)/SZZ75T);

finsi;
si complet;
LERR = PROG EDPC0 EDPC1 EDPC2 EDPC4 EDPC6 EDPC75 ED0
 ED1 ED2 ED4 ED6 ED75
       ESZZ0 ESZZ1 ESZZ2 ESZZ4 ESZZ6 ESZZ75;
sinon;
lerr =  PROG EDPC0 EDPC1 ED0 ED1 ESZZ0 ESZZ1;
finsi;
list LERR;

ermax = maxi LERR;
LIST ermax;
temps;
SI ( ERMAX <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

FIN;
 FIN;
















