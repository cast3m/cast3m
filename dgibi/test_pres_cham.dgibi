************************************************************************
* Utilisation de l'operateur PRES avec un MCHAML                       *
*                                                                      *
* Une comparaison est faite entre les utilisation dans PRES            *
* - d'un CHPOINT (CHAR1)                                               *
* - d'un MCHAML  (CHAR2)                                               *
*                                                                      *
* Calcul mecanique avec d'une poutre avec section en T :               *
* - encastrement de la face gauche                                     *
* - pression variable repartie sur la face superieure                  *
************************************************************************
  'OPTI' 'DIME' 3 'ELEM' 'CUB8' 'TRAC' 'X' 'ECHO' 0 ;

* Indicateur pour traces
  ITRAC = FAUX ;
*
*-----------------------------------------------------------------------*
*                              Maillage                                 *
*-----------------------------------------------------------------------*
* Dimensions section
  LA = 0.004 ; LB = 0.02 ; H1 = 0.03 ;
* Longueur de la poutre
  LONG = 0.2 ;
* Taille de maille cible
  OPTI 'DENS' LA ;
*
  P0 = 0. 0. 0. ;
  P1 = P0 PLUS (0. LA 0.) ;
  P2 = P1 PLUS (0. 0. (H1 - (2*LA))) ;
  P3 = P2 PLUS (0. (LB - LA) 0.) ;
  P4 = P3 PLUS (0. 0. (2*LA)) ;
  P5 = P0 PLUS (0. 0. H1) ;
  LIG1 = D P0 P1 ;
  LIG1 = D LIG1 P2 ;
  LIG1 = D LIG1 P3 ;
  LIG1 = D LIG1 P4 ;
  LIG1 = D LIG1 P5 ;
  LIG1 = D LIG1 P0 ;
  SUR1 = SURF LIG1 'PLAN' ;
  SUR1 = SUR1 ET (SUR1 SYME 'DROI' P0 P5) ;
  ELIM SUR1 1.E-9 ;
*
  VOL1 = SUR1 VOLU 'TRAN' 50 (LONG 0. 0.) ;
  'SI' ITRAC ;
    TRAC 'CACH' VOL1 'TITR' 'Maillage' ;
  'FINSI' ;
*
  ENV1 = ENVE VOL1 ;
  PHAUT = ENV1 POIN 'PLAN' P4 P5 (P4 PLUS (1. 0. 0.)) ;
  SHAUT = ENV1 ELEM 'APPU' 'STRI' PHAUT ;
*
*-----------------------------------------------------------------------*
*                              Modeles                                  *
*-----------------------------------------------------------------------*
* Modele mecanique
  MOMEC = 'MODE' VOL1 'MECANIQUE' 'ELASTIQUE' ;
  MAMEC = 'MATE' MOMEC 'YOUN' 200.E9 'NU' 0.3 ;
*
*-----------------------------------------------------------------------*
*                       Conditions aux limites                          *
*-----------------------------------------------------------------------*
* Encastrement
  BL1 = BLOQ 'DEPL' SUR1 ;
*
*-----------------------------------------------------------------------*
*                             Chargements                               *
*-----------------------------------------------------------------------*
* Pression repartie sur la face superieure
  PMAX = 2.E6 ;
  CHX = 'COOR' SHAUT 1 ;
  CHP = PMAX * ((CHX - ('MINI' CHX)) / (('MAXI' CHX) - ('MINI' CHX))) ;
  LTPS = 'PROG' 0. 5. 10. ; LCOE = 'PROG' 0. 1. 2. ;
  EV1 = EVOL 'MANU' 'TEMP' LTPS 'COEF' LCOE ;
*
* Chargement PRES avec un CHPOINT
  F1 = 'PRES' 'MASS' MOMEC CHP ;
  CHAR1 = 'CHAR' 'MECA' F1 EV1 ;
*
* Chargement PRES avec un MCHAML
  F2 = 'PRES' 'MASS' MOMEC ('CHAN' 'CHAM' CHP SHAUT) ;
  CHAR2 = 'CHAR' 'MECA' F2 EV1 ;
*
* Trace
  V1 = 'VECT' F1 'FORC' 'ROUG' ;
  'SI' ITRAC ;
    TITR_Z = 'CHAI' 'Pression repartie' ;
    TRAC 'CACH' V1 (SHAUT ET ('ARETE' ENV1)) 'TITR' TITR_Z ;
  'FINSI' ;
*
*-----------------------------------------------------------------------*
*                            Resolution                                 *
*-----------------------------------------------------------------------*
* Resolution avec PASAPAS
  TAB1 = TABL ;
  TAB1.'MODELE'              = MOMEC ;
  TAB1.'CARACTERISTIQUES'    = MAMEC ;
  TAB1.'BLOCAGES_MECANIQUES' = BL1 ;
  TAB1.'CHARGEMENT'          = CHAR1 ;
  TAB1.'TEMPS_CALCULES'      = LTPS ;
  PASAPAS TAB1 ;
*
  TAB2 = TABL ;
  TAB2.'MODELE'              = MOMEC ;
  TAB2.'CARACTERISTIQUES'    = MAMEC ;
  TAB2.'BLOCAGES_MECANIQUES' = BL1 ;
  TAB2.'CHARGEMENT'          = CHAR2 ;
  TAB2.'TEMPS_CALCULES'      = LTPS ;
  PASAPAS TAB2 ;
*
*-----------------------------------------------------------------------*
*                             Visualisation                             *
*-----------------------------------------------------------------------*
* Deformee de la poutre
  DEP1 = TAB1.'DEPLACEMENTS' ;DEP2 = TAB2.'DEPLACEMENTS' ;
  DEF_T0 = 'DEFO' VOL1 DEP1. 0 1 VERT ;
  'REPE' BDEP ('DIME' DEP1) ;
    DEF1_TI = 'DEFO' VOL1 DEP1. (&BDEP - 1) 10. ROUGE ;
    DEF2_TI = 'DEFO' VOL1 DEP2. (&BDEP - 1) 10. BLEU ;
    'SI' ITRAC ;
      TITR_Z = 'CHAI' 'Instant ' TAB1.'TEMPS'. (&BDEP - 1) ;
      TRAC (DEF_T0 'ET' DEF1_TI 'ET' DEF2_TI) 'TITR' TITR_Z 'CACH' ;
    'FINSI' ;
  'FIN' BDEP ;
*
*-----------------------------------------------------------------------*
*                       Comparaison des resultats                       *
*-----------------------------------------------------------------------*
  PT = VOL1 POIN PROC (P5 PLUS (LONG 0. 0.)) ;
  DEP1_PT = 'EXTR' (TAB1.'DEPLACEMENTS'. (('DIME' DEP1) - 1)) 'UZ' PT ;
  DEP2_PT = 'EXTR' (TAB2.'DEPLACEMENTS'. (('DIME' DEP2) - 1)) 'UZ' PT ;
  ERRMAX = 'ABS' (DEP1_PT - DEP2_PT) ;
  'SI' (ERRMAX '>' 1.E-3) ;
    'ERRE' 5 ;
  'FINSI' ;
*
'FIN' ;


