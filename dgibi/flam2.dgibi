* fichier :  flam2.dgibi
************************************************************************
* Section : Mecanique Flambage
************************************************************************


************************************************************************
* COMPARAISON DES CALCULS DE FLAMBAGE D'UN TUBE
* SOUS PRESSION EXTERNE (D'APRËS R.J. GIBERT)
************************************************************************

*------------------- OPTIONS -----------------------

* OPTI DIME 3 ELEM QUA4 ECHO 0;
OPTI DIME 3 ELEM QUA4 ;
OPTI EPSI LINEAIRE;
MESS '  COMPARAISON DES CALCULS DE FLAMBAGE';
MESS 'D UN TUBE SOUS PRESSION EXTERNE MODÈLISÈ';
MESS '           AVEC COQ3 ET COQ4';

*------------- DEFINITION DU MAILLAGE -----------------

P0 = 0. 0. 0.;
P1 = 0. 1. 0.;
P2 = 0. 0. 1.;
P3 = 0.08 0. 0.;
P4 = 0.08 1. 0.;
P5 = 0.08 0. 1.;
C1 = CERC 20 P1 P0 P2;
C2 = CERC 20 P4 P3 P5;
L1 = D 1 P1 P4;
L2 = D 1 P2 P5;
OBJ4 = C1 TRAN 1 P3;
ELIM (OBJ4 ET L1 ET L2 ET C1 ET C2) 1.E-3;
OBJ3 = CHAN 'TRI3' OBJ4;

*-------------  CONDITIONS AUX LIMITES  -------------

BC1 = BLOQ C1 'UX' 'RY' 'RZ';
BC2 = BLOQ C2 'UX' 'RY' 'RZ';
* BL1 = BLOQ L1 'UZ' 'RX' 'RY';
* BL2 = BLOQ L2 'UY' 'RX' 'RZ';
BL1 = BLOQ L1 'UZ' 'RX';
BL2 = BLOQ L2 'UY' 'RX';
CL = BC1 ET BC2 ET BL1 ET BL2;

*-------------  MODELE, MATERIAU, MATRICES -------------

*  MODELE
MOD3 = MODE OBJ3 MECANIQUE ELASTIQUE ISOTROPE COQ3;
* MOD3 = MODE OBJ3 MECANIQUE ELASTIQUE ISOTROPE DKT;
MOD4 = MODE OBJ4 MECANIQUE ELASTIQUE ISOTROPE COQ4;
*
MOP1 = 'MODE' OBJ3 'CHARGEMENT' 'PRESSION' COQ3 ;
MOP2 = 'MODE' OBJ4 'CHARGEMENT' 'PRESSION' COQ4 ;
*
*  MATERIAU
MAT3 = MATE MOD3 YOUN 2.E11 NU 0.3 EPAI 0.05;
MAT4 = MATE MOD4 YOUN 2.E11 NU 0.3 EPAI 0.05;
*
MAP1 = 'PRES' MOP1 'PRES' -1000000. ;
MAP2 = 'PRES' MOP2 'PRES' -1000000. ;
*
MOD3 = MOD3 ET MOP1 ; MAT3 = MAT3 ;
MOD4 = MOD4 ET MOP2 ; MAT4 = MAT4 ;
*  RIGIDITÈ
RIG3 = RIGI MOD3 MAT3;
RIG4 = RIGI MOD4 MAT4;

*------------- CALCUL ELASTIQUE DES PRECONTRAINTES -------------

*  CHARGEMENT
PR3 = 'BSIG' MOP1 MAP1 ;
PR4 = 'BSIG' MOP2 MAP2 ;
*
*  CALCUL ELASTIQUE
DEP3 = RESO (RIG3 ET CL) PR3;
DEP4 = RESO (RIG4 ET CL) PR4;

*  CALCUL DES CONTRAINTES
SIG3 = SIGM MAT3 MOD3 DEP3;
SIG4 = SIGM MAT4 MOD4 DEP4;

*  MATRICE KS ET KP
CHP3 = MANU 'CHPO' OBJ3 1 'SCAL' -1000000.;
CHP4 = MANU 'CHPO' OBJ4 1 'SCAL' -1000000.;
KS3 = KSIG SIG3 MOD3 mat3 FLAM;
KS4 = KSIG SIG4 MOD4 mat4 FLAM;
KP3 = KP MOP1 CHP3 FLAM;
KP4 = KP MOP2 CHP4 FLAM;
RAI3 = KP3 ET ((-1.)*KS3);
RAI4 = KP4 ET ((-1.)*KS4);
RAS3 = (-1.)*KS3;
RAS4 = (-1.)*KS4;

*-------------  RECHERCHE DE LA CHARGE CRITIQUE -------------
wshift = 0.4;
*REM: difficulte a converger pour VIBR IRAM pour wshift < 0.2 ...;

* calcul via VIBR PROCHE :
* en prenant en compte Ksigma et KP :
* wshift = 0.417;
XMOD3 = VIBR PROCHE (PROG wshift) (RIG3 ET CL) RAI3 ;
XMOD4 = VIBR PROCHE (PROG wshift) (RIG4 ET CL) RAI4 ;
* en prenant en compte Ksigma seulement :
* wshift = 0.482;
XMOD3S = VIBR PROCHE (PROG wshift) (RIG3 ET CL) RAS3 ;
XMOD4S = VIBR PROCHE (PROG wshift) (RIG4 ET CL) RAS4 ;

* calcul via VIBR IRAM :
* en prenant en compte Ksigma et KP :
*  opti impi 5;
TMOD3 = VIBR 'IRAM' wshift 1 (RIG3 ET CL) RAI3 ;
TMOD4 = VIBR 'IRAM' wshift 1 (RIG4 ET CL) RAI4 ;
* en prenant en compte Ksigma seulement :
TMOD3S = VIBR 'IRAM' wshift 1 (RIG3 ET CL) RAS3 ;
TMOD4S = VIBR 'IRAM' wshift 1 (RIG4 ET CL) RAS4 ;
*  opti impi 0;

* POST-TRAITEMENT :
* solution theorique :
XTH = ((2.E11/(4*(1 - 0.09)))*((0.05/1.)**3))/1000000.;
* solutions calculees :
FF3  = XMOD3.'MODES'. 1 .'FREQUENCE';
FF4  = XMOD4.'MODES'. 1 .'FREQUENCE';
FF3S = XMOD3S.'MODES'. 1 .'FREQUENCE';
FF4S = XMOD4S.'MODES'. 1 .'FREQUENCE';
IFF3  = TMOD3.'MODES'. 1 .'FREQUENCE';
IFF4  = TMOD4.'MODES'. 1 .'FREQUENCE';
IFF3S = TMOD3S.'MODES'. 1 .'FREQUENCE';
IFF4S = TMOD4S.'MODES'. 1 .'FREQUENCE';
X2PI = 2.D0 * PI;
* solutions coq3 :
XCOQ3 = (X2PI * (PROG FF3 IFF3 FF3S IFF3S)) ** 2;
* solutions coq4 :
XCOQ4 = (X2PI * (PROG FF4 IFF4 FF4S IFF4S)) ** 2;
* erreur relative
ERR3 = ABS (100*((extr XCOQ3 (lect 1 2)) - XTH)/XTH);
ERR4 = ABS (100*((extr XCOQ4 (lect 1 2)) - XTH)/XTH);

MESS '--------------------------------------------------------------';
MESS '--- VALEUR THEORIQUE               : ' XTH;
MESS '--------------------------------------------------------------';
MESS '--- COQ3 :                         PROCH           IRAM';
MESS '  VALEUR CALCULÈE AVEC KP      : ' (extr XCOQ3 1) (extr XCOQ3 2);
MESS '                    ERREUR (%) : ' (extr ERR3 1) (extr ERR3 2) ;
MESS '  VALEUR CALCULÈE SANS KP      : ' (extr XCOQ3 3) (extr XCOQ3 4);
MESS '  VARIATION (THÈORIQUE : 4/3)  : '
  ((extr XCOQ3 3)/(extr XCOQ3 1)) ((extr XCOQ3 4)/(extr XCOQ3 2)) ;
MESS '--------------------------------------------------------------';
MESS '--- COQ3 :                         PROCH           IRAM';
MESS '  VALEUR CALCULÈE AVEC KP      : ' (extr XCOQ4 1) (extr XCOQ4 2);
MESS '                    ERREUR (%) : ' (extr ERR3 1) (extr ERR3 2) ;
MESS '  VALEUR CALCULÈE SANS KP      : ' (extr XCOQ4 3) (extr XCOQ4 4);
MESS '  VARIATION (THÈORIQUE : 4/3)  : '
  ((extr XCOQ4 3)/(extr XCOQ4 1)) ((extr XCOQ4 4)/(extr XCOQ4 2)) ;
MESS '--------------------------------------------------------------';

*------------------ CODE DE BON FONCTIONNEMENT -------------------------
SI (((MAXI ERR3 'ABS') < 5) ET ((MAXI ERR4 'ABS') < 5));
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

FIN;
















