* fichier :  endoaxi3.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*          Test Endoaxi3.dgibi: Jeux de données       *
*          ------------------------------------       *
*                                                     *
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
***************************************************
*******************************************************
*                                                     *
* TEST;  MATERIAU ELASTO-PLASTIQUE ENDOMMAGEABLE      *
* MATERIAU DEPENDANT DE LA TEMPERATURE :              *
*                          COURBE DE TRACTION         *
*                          COEFFICIENT DE DILATATION  *
*                          THERMIQUE                  *
* TEMPERATURE DE REFERENCE = 30°C                     *
* TEMPERATURE DE REFERENCE POUR COEF. DE DILATATION   *
* THERMIQUES               = 10°C                     *
* TEMPERATURE INITIALE     = 50°C                     *
*                                                     *
* EPROUVETTE EN TRACTION AVEC DEPLACEMENTS IMPOSES    *
*                           + CHARGEMENT THERMIQUE    *
* MAILLAGE AXISYMETRIQUE.                             *
*                                                     *
* LES RESULTATS OBTENUS SONT COMPARES  A LA SOLUTION  *
* ANALYTIQUE OBTENUE PAR ALGORITHME.                  *
* ON TESTE - LA DEFORMATION PLASTIQUE CUMULEE         *
*            (composante EPSE dans VARIABLES_INTERNES)*
*          - D, LA VARIABLE D'ENDOMMAGEMENT           *
*            DANS VARIABLES_INTERNES .                *
*          - SIGMAzz ( DANS CONTRAINTES ).            *
*******************************************************                                                       *


OPTI ECHO 0 DIME 2 ELEM QUA8 MODE AXIS;
*------------------------------------------------------
   P1 = 0 0;          P2 = 3E-3 0;
   P3 = 3E-3 30E-3;   P4 = 0 30E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 1  ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 1  ;
*
   EPROU = L1 L2 L3 L4 DALLER PLAN ;

   CL1 = BLOQ L1 UZ ;
   CL2 = BLOQ L3 UZ ;
   CL3 = BLOQ L4 UR ;
   CL = CL1 ET CL2 ET CL3 ;
   DEP = DEPI CL2 -1E-3 ;
*
   MO = MODE EPROU 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE'
'ENDOMMAGEABLE';
*
* module tangent valant 783MPa
*-----------------------------
  PROGEPS1 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG1 = PROG 0. 350.E06 581.1976E06 816.0976E06 ;
   CTRAC1 = EVOL MANU EPS PROGEPS1 SIG PROGSIG1 ;
*
* module tangent valant 491MPa
*-----------------------------
  PROGEPS2 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG2 = PROG 0. 350.E06 494.9783E06 642.2783E06 ;
   CTRAC2 = EVOL MANU EPS PROGEPS2 SIG PROGSIG2 ;
*
* module tangent valant 199MPa
*-----------------------------
  PROGEPS3 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG3 = PROG 0. 350.E06 408.7590E06 468.4590E06 ;
   CTRAC3 = EVOL MANU EPS PROGEPS3 SIG PROGSIG3 ;
*
* module tangent variable
*------------------------
CTRAC = NUAGE 'T'*'FLOTTANT' 'TRAC'*'EVOLUTION'
                  0. CTRAC1 100. CTRAC2 200. CTRAC3  ;
*
* coefficient de dilatation thermique vaiable
*--------------------------------------------
*
  PROGTEMP = PROG 0. 100. 200. ;
  PROGALFA = PROG 0. .25E-3 .5E-3 ;
  EVALPH = EVOL MANU 'T' PROGTEMP 'ALPH' PROGALFA ;
*
   MATvar = MATE MO YOUN 74020.E06 NU 0.3 RHO 7800.
   ALPH EVALPH TRAC CTRAC EPSD 0. DC 0.23 EPSR 0.25 ;
*
************** DEFINITION DU CHARGEMENT ***************
*
   LI1 = PROG 0. 1 31. ; LI2 = PROG 0. 0. 30. ;
   EV = EVOL MANU T LI1 DEP(T) LI2 ;
   CHA1 = CHAR 'DIMP' DEP EV ;
 TEMPREF = MANU CHPO EPROU 1 'T' 30.    ;
   TEMP0 = MANU CHPO EPROU 1 'T' 50.    ;
   TEMP200 = MANU CHPO EPROU 1 'T' 200. ;
   t0 = 0. ; T1 = 1. ; t20 = 20.        ;
   TEMP = TABLE; TEMPERA = TABLE;
   TEMP.0 = t0 ; TEMP.1 = t1 ; TEMP.2 = t20 ;
   TEMPERA.0 = TEMPREF;TEMPERA.1 = TEMP0;
   TEMPERA.2 = TEMP200;
   CHA2 = CHAR 'T' TEMP TEMPERA;
   CHA = CHA1 ET CHA2;
*
* LISTE DES PAS DE CALCUL :
*                1.035 MM PREMIER INCREMENT PLASTIQUE
*
si complet ;
  LIS = PROG 1.035 1.1 pas 0.1 7;
sinon;
LIS =   PROG 1.035 1.1 pas 0.1 1.6;
finsi;

*
TAB = TABLE ;
TAB.'PRECISION' = 1E-3;
TAB.'BLOCAGES_MECANIQUES' = CL;
TAB.'CARACTERISTIQUES' = MATvar;
TAB.'CHARGEMENT' = CHA;
TAB.'TEMPS_CALCULES' = LIS;
TAB.'MODELE' = MO;
TAB.'TALPHA_REFERENCE' = 10.      ;
TAB.'TEMPERATURE_REFERENCE' =  TEMPREF ;
PASAPAS TAB ;
*
*------------------------------------------------------
*  CONTROLE DES RESULTATS AVEC DE LA SOLUTION
*     DE REFERENCE OBTENUE PAR ALGORITHME
*------------------------------------------------------
*
si complet;
 TA = 4.; TB = 7.;
 REF_D = PROG 7.5E-2 17.7E-2      ;
 REF_P = PROG 10.3E-2 20.9E-2     ;
 REF_S = (PROG -376.9 -369.1)*1E6 ;
SINON;
 TA = 1.2;  TB = 1.6;
 REF_D = PROG 3.52404E-03     1.24384E-02      ;
 REF_P = PROG 5.75205E-03     1.97372E-02   ;
 REF_S = PROG    -3.52473E+08    -3.57838E+08;
finsi;
*
D1=EXTR (PECHE TAB VARIABLES_INTERNES TA) 'VHWD' 1 1 1;
D2=EXTR (PECHE TAB VARIABLES_INTERNES TB) 'VHWD' 1 1 1;
*
P1=EXTR (PECHE TAB VARIABLES_INTERNES TA) 'EPSE' 1 1 1;
P2=EXTR (PECHE TAB VARIABLES_INTERNES TB) 'EPSE' 1 1 1;
*
S1 = EXTR (PECHE TAB CONTRAINTES TA) 'SMZZ' 1 1 1;
S2 = EXTR (PECHE TAB CONTRAINTES TB) 'SMZZ' 1 1 1;
*
EF_D = PROG D1 D2  ;
EF_P = PROG P1 P2  ;
EF_S = PROG S1 S2  ;
*
PROG_ERD = MAXI (ABS ((EF_D  - REF_D) / REF_D)) ;
PROG_ERP = MAXI (ABS ((EF_P  - REF_P) / REF_P)) ;
PROG_ERS = MAXI (ABS ((EF_S  - REF_S) / REF_S)) ;
*
ERR = MAXI (PROG PROG_ERD PROG_ERP PROG_ERS)    ;
*
temps;
SI ( ERR <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
FIN;















