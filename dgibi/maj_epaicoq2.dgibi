* fichier : maj_epaicoq2.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*----------------------------------------------------------------------*
* Exemple de calcul elastoplastique avec des coques prenant en compte  *
* la diminution de l'epaisseur des coques au cours du calcul.          *
*                                                                      *
* Pour cela, on fait appel a la procedure PERSO1 de PASAPAS.           *
* En cours de calcul, l'epaisseur est calculee en considerant que      *
* l'ecoulement plastique se fait a volume contant.                     *
*                                                                      *
* Le cas de calcul est une plaque vue par sa tranche (ligne en 2D),    *
* dont on bloque la base et sur laquelle on impose un effort en tete.  *
* La comportement materiau est 'ELASTIQUE' 'PLASTIQUE' 'ISOTROPE'.     *
*                                                                      *
* On compare la solution "coque" a la solution avec des elts. massifs. *
*                                                                      *
*----------------------------------------------------------------------*
*                                                                      *
* ITRAC1   = VRAI : traces actives                                     *
* ISMPLAN1 = VRAI : calcul en contraintes planes                       *
ITRAC1   = FAUX ;
ISMPLAN1 = VRAI ;
*                                                                      *
*-------------------------- Procedure PERSO1 --------------------------*
*                                                                      *
'DEBP' PERSO1 TU1*'TABLE' ;
* Appel avant 1er calcul de MECANIQUE (indicateur 'EGAL' a 1) :*;
* => on passe                                                          *
'SI' (TU1 . 'PERSO1_APPEL' 'EGA' 1) ;
  'QUIT' PERSO1 ;
'FINS' ;
* DEP1   : Les deplacements en fin de PAS (courants) :                 *
DEP1     = TU1 . 'ESTIMATION' . 'DEPLACEMENTS' ;
* On sauve la configuration courante :                                 *
CONFC1   = 'FORM' ;
* On se place dans la configuration intiale (FOR0), puis dans la       *
* la config. en  fin de PAS grace a DEP1 :                             *
'FORM' (TU1 . 'WTABLE' . 'FOR0') ;
'FORM' DEP1 ;
* Calcul surface courante des coques : on integre un champ de 1 dans   *
* la configuration deformee (DEP1) :                                   *
MODX1    = TU1 . 'DATA_PERSO' . 'MODX1' ;
CHAM1    = TU1 . 'DATA_PERSO' . 'CHAM1' ;
S1       = 'INTG' MODX1 CHAM1 'ELEM' ;
* On se remet dans la config. courante :                               *
'FORM' CONFC1 ;
* Surface initiale des coques (S0) :                                   *
S0       = TU1 . 'DATA_PERSO' . 'S0' ;
'SI' ('EGA' ('VALE' 'MODE') 'PLANCONT') ;
  CHPAR1   = (S0 / (S0 + (0.5 * (S1 - S0)))) ;
'SINO' ;
  CHPAR1   = S0 / S1 ;
'FINS' ;
CHPAR1   = CHPAR1 'NOMC' 'PAR1' ;
* Mise a jour du parametre dans le chargement :                        *
EV0      = TU1 . 'DATA_PERSO' . 'EV0' ;
CGP1     = 'CHAR' 'PAR1' CHPAR1 EV0 ;
* On reconstruit le chargement en ajoutant le nouveau CGP1 aux autres  *
* chargements du probleme passes a l'indice  DATA_PERSO.CG0 :          *
WTAB.'CHARGEMENT_I' = TU1 . 'DATA_PERSO' . 'CG0' 'ET' CGP1 ;
WTAB.'CHARGEMENT'   = TU1 . 'DATA_PERSO' . 'CG0' 'ET' CGP1 ;
'FINP' ;
*                                                                      *
*----------------------------------------------------------------------*
*                                 COQ2                                 *
*----------------------------------------------------------------------*
*                                                                      *
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
*                                                                      *
'SI' ISMPLAN1 ;
  'OPTI' 'MODE' 'PLAN' 'CONT' ;
'FINS' ;
*                                                                      *
*------------------------------ Maillage ------------------------------*
*                                                                      *
P1       = 0 0 ;
P2       = 0 1 ;
L1       = P1 'DROI' 10 P2 ;
*                                                                      *
'SI' ITRAC1 ;
  'TRAC' 'QUAL' (L1 'ET' P1 'ET' P2)
    'TITR' ' Modelisation coque 1D : P1 bloque, Fy imposee en P2 ' ;
'FINS' ;
*                                                                      *
*--------------------- Modele - Caracteristiques ----------------------*
*                                                                      *
CTRA1    = 'EVOL' 'MANU' 'EPSI' ('PROG' 0. 0.0015 0.1)
                         'SIGM' ('PROG' 0. 300.E6  1000.E6) ;
EVEP1    = 'EVOL' 'MANU' 'PAR1' ('PROG' 0.1 1. 2.)
                         'EPAI' ('PROG' 0.001 0.01 0.02) ;
MO1      = 'MODE' L1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'COQ2' ;
MA1      = 'MATE' MO1 'YOUN' 200.E9 'NU' 0.3 'TRAC' CTRA1
                      'EPAI' EVEP1 ;
*                                                                      *
*------------------------- C.L. - Chargements -------------------------*
*                                                                      *
CL1      = 'BLOQ' 'DEPL' 'ROTA' P1 ;
*                                                                      *
VF1      = 8.E6 ;
F1       = 'FORC' (0. VF1) P2 ;
EV1      = 'EVOL' 'MANU' 'TEMP' ('PROG' 0. 1.) ('PROG' 0. 1.) ;
CG1      = 'CHAR' 'MECA' EV1 F1 ;
*                                                                      *
* L'epaisseur depend du parametre PAR1 : on donne sa valeur initiale   *
CHAM1    = 'MANU' 'CHML' MO1 'PAR1' 1. 'RIGIDITE' ;
EV0      = 'EVOL' 'MANU' ('PROG' 0. 1.) ('PROG' 1. 1.) ;
CGEP1    = 'CHAR' 'PAR1' EV0 CHAM1 ;
*
* "Surface" initiale des coques :
CHS0     = 'INTG' MO1 CHAM1 'ELEM' ;
*
*----------------------------- Resolution -----------------------------*
*                                                                      *
* Liste des temps de calcul :                                          *
'SI' ('EGA' ('VALE' 'MODE') 'PLANDEFO') ;
  LTPS1    = 'PROG' 0. 0.3 'PAS' 0.015 1. ;
'FINS' ;
'SI' ('EGA' ('VALE' 'MODE') 'PLANCONT') ;
  LTPS1    = 'PROG' 0. 0.3 'PAS' 0.025 1. ;
'FINS' ;
*                                                                      *
* Table PASAPAS :                                                      *
TAB1                         = 'TABL' 'PASAPAS' ;
TAB1 . 'MODELE'              = MO1 ;
TAB1 . 'CARACTERISTIQUES'    = MA1 ;
TAB1 . 'BLOCAGES_MECANIQUES' = CL1 ;
TAB1 . 'CHARGEMENT'          = CG1 'ET' CGEP1 ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'TEMPS_CALCULES'      = LTPS1 ;
*                                                                      *
* Donnees pour laprocedure PERSO1 :                                    *
TAB1 . 'PROCEDURE_PERSO1'    = VRAI ;
TAB1 . 'DATA_PERSO'          = 'TABL' ;
TAB1 . 'DATA_PERSO' . 'S0'   = CHS0 ;
TAB1 . 'DATA_PERSO' . 'MODX1'= MO1 ;
TAB1 . 'DATA_PERSO' . 'CHAM1'= CHAM1 ;
TAB1 . 'DATA_PERSO' . 'EV0'  = EV0 ;
TAB1 . 'DATA_PERSO' . 'CG0'  = CG1 ;
*                                                                      *
PASAPAS TAB1 ;
*                                                                      *
*----------------------------------------------------------------------*
*                                MASSIFS                               *
*----------------------------------------------------------------------*
*                                                                      *
*------------------------------ Maillage ------------------------------*
*                                                                      *
S1       = L1 'TRAN' 1 (0.01 0.) ;
L2       = (S1 'COTE' 2) 'COUL' 'ROUG' ;
L4       = (S1 'COTE' 4) 'COUL' 'VERT' ;
*                                                                      *
'SI' ITRAC1 ;
  CHVISU1  = ((S1 'COOR' 1) * 5.) 'NOMC' 'UX' ;
  'FORM' CHVISU1 ;
  'TRAC' 'QUAL' (S1 'ET' L2 'ET' L4)
    'TITR' ' Modelisation 2D : L4 bloquee, Fy imposee en L2 ' ;
  'FORM' (-1. * CHVISU1) ;
'FINS' ;
*                                                                      *
*--------------------- Modele - Caracteristiques ----------------------*
*                                                                      *
MO2      = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' ;
MA2      = 'MATE' MO2 'YOUN' 200.E9 'NU' 0.3 'TRAC' CTRA1 ;
*                                                                      *
*------------------------- C.L. - Chargements -------------------------*
*                                                                      *
CL2      = ('BLOQ' 'UY' L4) 'ET' ('BLOQ' 'UX' P1) ;
*                                                                      *
F2       = 'FORC' (0. VF1) L2 ;
CG2      = 'CHAR' 'MECA' EV1 F2 ;
*                                                                      *
*----------------------------- Resolution -----------------------------*
*                                                                      *
TAB2                         = 'TABL' 'PASAPAS' ;
TAB2 . 'MODELE'              = MO2 ;
TAB2 . 'CARACTERISTIQUES'    = MA2 ;
TAB2 . 'BLOCAGES_MECANIQUES' = CL2 ;
TAB2 . 'CHARGEMENT'          = CG2 ;
TAB2 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB2 . 'TEMPS_CALCULES'      = LTPS1 ;
*                                                                      *
PASAPAS TAB2 ;
*                                                                      *
*----------------------------------------------------------------------*
*                              VALIDATION                              *
*----------------------------------------------------------------------*
*                                                                      *
* Evolution du deplacement Uy du point P2 :                            *
EVUP2_1  = 'EVOL' 'ROUG' 'TEMP' TAB1 'DEPLACEMENTS' 'UY' P2 ;
EVUP2_2  = 'EVOL' 'VERT' 'TEMP' TAB2 'DEPLACEMENTS' 'UY' P2 ;
*                                                                      *
* calcul de l'erreur relative entre COQ2 / Massifs                     *
ERR1     = 'MAXI' 'ABS' ('EXTR' (EVUP2_1 - EVUP2_2) 'ORDO') ;
ERR1     = ERR1 / ('MAXI' 'ABS' ('EXTR' EVUP2_2'ORDO')) ;
*                                                                      *
VECH1    = 'VALE' 'ECHO' ;
'OPTI' 'ECHO' 0 ;
'SAUT' 1 'LIGN' ;
'MESS' ' ****** Erreur relative sur deplacement Uy(P2) = '
  (100. * ERR1) ' %' ;
'SAUT' 1 'LIGN' ;
'OPTI' 'ECHO' VECH1 ;
*                                                                      *
'SI' ITRAC1 ;
  TDESS1   = 'TABL' ;
  TDESS1 . 2 = 'MOT' 'MARQ CROI NOLI' ;
  TDESS1 . 'TITRE' = 'TABL' ;
  TDESS1 . 'TITRE' . 1 = 'Modele coque 1D ' ;
  TDESS1 . 'TITRE' . 2 = 'Modele massif 2D ' ;
  MOT1     = 'CHAI'
    ' Uy(P2/L2) : modele coque (rouge) / massif (vert) (err.:'
    'FORMAT' '(F5.2)' (100. * ERR1) '%)' ;
  'DESS' TDESS1 (EVUP2_1 'ET' EVUP2_2) 'TITR' MOT1 'LEGE' ;
'FINS' ;
*                                                                      *
'FIN' ;
'OPTI' 'ECHO' 1 ;






