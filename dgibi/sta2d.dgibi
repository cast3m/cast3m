* fichier :  sta2d.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Plastique
************************************************************************
complet = faux ;
*                      PROGRAMMME STATIONNAIRE 2D                      *

* 29/11/1998                     Marta DRAGON/LMS-Ecole Polytechnique  *

*                  RAIL SOUMIS AU PASSAGE D'UNE ROUE :

*                   chargement mobile, de type Hertz
*   on cherche l'etat stationnaire atteint par le rail apres un grand
*    nombre de passages de la roue, en particulier les deformations
*                             plastiques

* maillage :   MAISTA : pave maille regulierement par des quadrangles,
*                       finement dans la zone superieure
*
*
*  V : deplacement du chargement
*     ---------------------->
*             Y
*             |
*     ________|_________________________ X
*      P4|    |P0           | |     | |P1    } maillage fin
*        |    |             | |     | |      } hauteur : haut
*        |____|_____________|_|_____|_|      } nb elts : Ncote1
*        |    |             | |     | |         }
*        |    |             | |     | |         }
*        |    |             | |     | |         } nb elts : Ncote2
*        |    |             | |     | |         }
*      P3|____|_____________|_|_____|_|P2       }
*             |              i       1
*                             \_________ colonne d'elements i
*
*               nb elts : Nbase
*
*
* modele :     MODSTA : modele entier
*              MOD    : tableau contenant les Nbase colonnes d'elts, nu-
*                       merotees de droite a gauche (sens oppose a V)
*
* materiau :   MATSTA : materiau du pave, elasto-plastique a ecrouissage
*                       cinematique
*              YO :     module d'Young
*              NN :     coefficient de Poisson
*              SY :     limite d'elasticite
*              HH :     module d'ecrouissage
*
* blocages :   BLOSTA : tout deplacement des noeuds de la base (P2-P3)
*
* chargement : CHASTA : pression du contact de Hertz ; au point distant
*                       de x du point de contact :
*                                p(x) = PMAX * [1 - (x/a)^2]^0.5
*              a :      demi-longueur du contact
*              PC :     point central du contact
*              PMAX :   pression maximale
*
*
* PROCEDURE DE CALCUL :
* l'ensemble des donnees est stocke dans le tableau CALST :
*
*   - CALST . MODELE                : MODSTA
*   - CALST . MODELE_TABLE          : MOD
*   - CALST . CARACTERISTIQUES      : MATSTA
*   - CALST . BLOCAGES_MECANIQUES   : BLOSTA
*   - CALST . CHARGEMENT            : CHASTA (type CHPOINT)
*   - CALST . PRECISION             : precision dans le test de
*                                     plasticite (ici a 0.001)
*   - CALST . MAXITERATION          : nb maximal d'iterations (ici a 20)
*
* puis pour chaque iteration i :
*
*   - CALST . DEPLACEMENTS            . i : deplacements
*   - CALST . DEFORMATIONS            . i : deformations
*   - CALST . CONTRAINTES             . i : contraintes
*   - CALST . CRITERE_PLASTICITE      . i : tableau issu de la procedure
*                                           de calcul du critere
*   - CALST . DEFORMATIONS_PLASTIQUES . i : deformations plastiques
*   - CALST . VARIABLES_INTERNES      . i : variables internes
*   - CALST . CONTRAINTES_PLASTIQUES  . i : contraintes plastiques
*                                           (L:epsp)
*   - CALST . FORCES_PLASTIQUES       . i : forces plastiques
*   - CALST . CONVERGENCE             . i : logique (faux tant que le
*                                           calcul n'a pas converge)

OPTI ECHO 1 DIME 2 MODE PLAN DEFO ELEM QUA8 ;
GRAPH = 'N';

****           MAILLAGE FONDAMENTAL DE LA PREMIERE COLONNE          ****

A    =   1.E-3 ;
haut = -2. * A ;

N      =     10 ;
Ncote1 =  2 * N ;
Ncote2 =      N ;
Nbase  = 10 * N ;

P0 =        0.        0. ;
P1 =  (5. * A)        0. ;
P2 =  (5. * A) (-5. * A) ;
P3 = (-5. * A) (-5. * A) ;
P4 = (-5. * A)        0. ;

AXES = coul roug
       ((((-5.1 * A) 0.) droi 1 ((5.1 * A) 0.)) et
        ((0. (-5.1 * A)) droi 1 (0. (0.1 * A)))) ;

tra0 = ((coor 1 P1) - (coor 1 P4)) / Nbase ;
lig  = P2 droi Ncote2 ((coor 1 P1) haut) droi Ncote1 P1 ;
part = lig tran 1 ((-1. * tra0) 0.) ;


****            MAILLAGE TOTAL PAR TRANSLATION ET MODELE            ****

repeter colon Nbase ;
  tra = tra0 * (&colon - 1) ;
  si (&colon ega 1) ;
    MAISTA = part ;
    MOD = tabl ;
    MOD . &colon = mode part mecanique elastique isotrope
                                       plastique cinematique cons un ;
    MODSTA = MOD . &colon ;
  sinon ;
    morc = part moin (tra 0.) ;
    MAISTA = MAISTA et morc ;
    elim 1.e-6 MAISTA ;
    MOD . &colon = mode morc mecanique elastique isotrope
                                       plastique cinematique cons un ;
    MODSTA = MODSTA et (MOD . &colon) ;
  finsi ;
fin colon ;

*mess '** NB DE NOEUDS  DU MAILLAGE : ' (nbno MAISTA) ' **' ;
*mess '** NB D ELEMENTS DU MAILLAGE : ' (nbel MAISTA) ' **' ;
*trace cach (MAISTA et AXES) ;


****                            MATERIAU                            ****

YO = 2.E11  ;
NN = 0.34   ;
SY = 200.E6 ;
HH = 60.E3  ;
MATSTA = mate MODSTA youn YO nu NN sigy SY h HH ;


****                            BLOCAGES                            ****

BAS = MAISTA poin droi P2 P3 1.E-6 ;
*trace (MAISTA et (COUL BLEU BAS) et AXES);
BLOSTA = bloq depl BAS ;


****                           CHARGEMENT                           ****

*A = 1.E-3 ;
PC = 0. 0. ;
PMAX = 400.E6 ;

X0CH    = coor 1 PC ;
Y0CH    = 0. ;
SUR     = MAISTA poin droi (0. 0.) (1. 0.) 1.E-6 ;
P0CH    = MAISTA poin proc (X0CH Y0CH) ;
XCH     = (coor 1 SUR) - (1. * X0CH) ;
XYCH    = 1 - ((XCH / A) ** 2) ;
ZONCH   = XYCH masq egsupe 0. ;
P_HERTZ = (PMAX * ((XYCH * ZONCH) ** 0.5)) + 1.E-20 ;
CHASTA  = pres mass MODSTA P_HERTZ ;

*vec = vect CHASTA 0.01E-6 fx fy bleu ;
*trace cach vec (MAISTA et AXES) ;


****               INITIALISATION DU TABLEAU DE CALCUL              ****

CALST = table ;
CALST . 'MODELE'              = MODSTA ;
CALST . 'MODELE_TABLE'        = MOD    ;
CALST . 'CARACTERISTIQUES'    = MATSTA ;
CALST . 'BLOCAGES_MECANIQUES' = BLOSTA ;
CALST . 'CHARGEMENT'          = CHASTA ;
CALST . 'PRECISION'           = 1.E-3  ;
si complet ;
CALST . 'MAXITERATION'        = 20     ;
sinon ;
CALST . 'MAXITERATION'        = 1     ;
finsi ;

****            CALCUL EN ELASTO-PLASTICITE STATIONNAIRE            ****

@STATIO CALST ;


****    VISUALISATION DES ISOVALEURS DES DEFORMATIONS PLASTIQUES    ****

defpl = CALST . 'DEFORMATIONS_PLASTIQUES' ;
ii    = (dime defpl) - 1 ;
SI (NEG GRAPH 'N');
'TITR' 'deformations plastiques : EIXX' ;
trace (chan 'CHPO' MODSTA (exco 'EIXX' (defpl . ii))) MAISTA ;

'TITR' 'deformations plastiques : EIYY' ;
trace (chan 'CHPO' MODSTA (exco 'EIYY' (defpl . ii))) MAISTA ;

'TITR' 'deformations plastiques : EIZZ' ;
trace (chan 'CHPO' MODSTA (exco 'EIZZ' (defpl . ii))) MAISTA ;

'TITR' 'deformations plastiques : GIXY' ;
trace (chan 'CHPO' MODSTA (exco 'GIXY' (defpl . ii))) MAISTA ;
FINSI ;

****    LIEU DES POINTS OU LA DEFORMATION PLASTIQUE EST MAXIMALE    ****

plmax = (exco 'EIYY' (defpl . ii)) poin 'MAXI' 'ABS' ;
yymax = extr (coor 2 plmax) 'SCAL' (plmax poin 1) ;

VCAL = (-1. * YYMAX / A) ;
VTHE =  0.7 ;
mess ;
mess '*********************************************************' ;
mess ;
mess '    Deformation plastique maximale a la profondeur : ' ;
mess ;
mess '                  '     VCAL          ' *    a' ;
mess ;
mess '              (valeur theorique : 0.7 * a)' ;
mess ;
mess '*********************************************************' ;

ERG = ( VCAL - VTHE ) / VTHE ;
SI ( ERG <EG 0.02);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;
FIN;










