************************************************************************
* NOM         : TRAC
* DESCRIPTION : Tester TRAC avec les QUAFs compares aux autres elements
*               suite aux modifications pour gerer correctement les
*               faces TRI7/QUA9.
*               Les modifs ont touche pas mal de subroutine de trace
*               celles issues de envvol (operateur ENVELOPPE) qui
*               tracent les isovaleurs.
*               On a simplifie la programmation, corrige des bugs mais
*               peut-etre introduit d'autres.
*               Voir operquaf.dgibi pour le test des operateurs de
*               maillage (ENVE, etc...)
*
*               On pourrait egalement ajouter d'autres tests SECT, etc.
*
*               Le test tel que identifie un certain nombre de bugs
*               visibles en sortie PSC :
*               1) TET4 PRI6 : l'arete situe derriere est trace
*                  ex: pages 132, 198
*               2) OPTI ISOV LIGNE en 3D ne gere pas totalement
*                  correctement les parties cachees et les labels
*                  particulierement sur les lineaires.
*                  Sur les QUAD et surtout QUAFs, c'est mieux grace aux
*                  noeuds centre face rajoute et au decoupage en
*                  triangle barycentrique de chaque face
*                  ex: pages 134, 152, 170, 272, 279
*               3) TRAC COUP + chpo/cham : facette non visible
*                  tracee en chpo mais pas en cham
*                 ex: pages 360, 361
*               4) Lorsqu'il y a a la fois des lignes et des surfaces
*                  les segments apparaissent derriere les faces meme
*                  s'ils sont devant. Mais cela semble etre voulu dans
*                  faced2 ??? (cf. le -TDIST dans KFAC)
*                  ex: pages 270, 288
*               5) Trace des champs sur des segments non geres en OPTI
*                  ISOV LIGN
*                  ex: pages 84, 82
*               5b) Trace des champs sur des surfaces mal gere en OPTI
*                  ISOV LIGN
*                  ex: page 290, 615
*               6) L'option COUP ne gere pas les segments
*                  ex: page 131
*               7) Les traces chpo+vect ne trace pas le chpo sur le
*                  maillage de segments
*                  mais les traces cham+vect si ! (sauf option isov lign)
*                  ex: pages 594, 595, 567
*               8) Option BOIT, le clipping Postcript tel que fait dans
*                  strini introduit des effets
*                  étranges sur certaines isovaleurs : iso et contour
*                  non coherent.
*                  ex: page 720
*                  On peut régler ce pb en modifiant strini, au prix
*                  d'un passage de I4 en I5 sur l'écriture des entiers
*                  dans  le Postscript
*
*               Il y a un logique sautquaf utilise pour tracer
*               uniquement les elements standard. Ceci a servi pour
*               tester la non-regression au moment de l'incorporation
*               des quafs
*
*               Inspire de orieelem.dgibi
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 05/07/2016, version initiale
* HISTORIQUE : v1, 05/07/2016, création
* HISTORIQUE : 23/08/2016 : modif maillage complexe : melange ligne,
*                  surface, volume suite fiche 9050
* HISTORIQUE : 29/11/2016 : ajout option boite pour zoomer un peu
*                           (fiche 9218)
* HISTORIQUE : 09/10/2018 : ajout champ constant strictement ou bruite
*                           (fiche 9956)
* HISTORIQUE :
************************************************************************
*
*
complet = FAUX ;
interact= FAUX ;
sautquaf= FAUX ;
'OPTION' 'DIME' 3 'ELEM' CUB8  ;
'SI' ('NON' interact) ;
  'OPTION' 'TRAC' 'PS' ;
'SINON' ;
  'OPTION' 'TRAC' 'X' ;
'FINSI' ;
*
* Paramètres globaux
*
lisquaf = 'MOTS' 'TRI7' 'QUA9' 'TE15' 'PY19' 'PR21' 'CU27' ;
lisel2  = 'MOTS' 'TRI3' 'TRI6' 'TRI7' 'QUA4' 'QUA8' 'QUA9' ;
lisel3  = 'MOTS' 'TET4' 'TE10' 'TE15' 'PYR5' 'PY13' 'PY19'
   'PRI6' 'PR15' 'PR21' 'CUB8' 'CU20' 'CU27' ;
*
* Liste de paramètres à tester :
* lsort : canaux de sortie
* lisov : type d'isovaleurs à tracer
* ltypcha : quand on trace un champ, on peut choisir de le tracer comme
*           chpoint ou chamelem
* lforcha : forme du champ à tracer :
*           1) un champ valant 1 en un point bien choisi et zéro ailleurs
*           2) une combinaison linéaire des coordonnées des noeuds
* couvec : couleur des champs de vecteurs
* ltypma2 : nom des maillages 2D à traiter (mis dans une table)
* ltypma3 : nom des maillages 2D à traiter (mis dans une table)
*


'SI' complet ;
   'SI' interact ;
      lsort = 'MOTS' 'PS' 'PSC' 'X' 'OPEN' ;
   'SINON' ;
      lsort = 'MOTS' 'PS' 'PSC' ;
   'FINSI' ;
   lisov   = 'MOTS' 'SURF' 'LIGN' 'SULI' ;
   ltypcha = 'MOTS' 'chpo' 'cham' ;
   lforcha = 'LECT' 1 2 ;
'SINON' ;
   'SI' interact ;
*      lsort = 'MOTS' 'PS' 'PSC' 'X' 'OPEN' ;
      lsort = 'MOTS' 'PSC' 'X' ;
*      lsort = 'MOTS' 'X' ;
   'SINON' ;
*      lsort = 'MOTS' 'PS' 'PSC' ;
      lsort = 'MOTS' 'PSC' ;
   'FINSI' ;
   lisov = 'MOTS' 'SURF' 'LIGN' 'SULI' ;
*   lisov   = 'MOTS' 'SURF' ;
   ltypcha = 'MOTS' 'chpo' 'cham' ;
   lforcha = 'LECT' 1 2 ;
*   ltypcha = 'MOTS' 'chpo' ;
*   lforcha = 'LECT' 1 ;
'FINSI' ;
'SI' interact ;
   couvec = 'JAUN' ;
'SINON' ;
   couvec = 'ROUG' ;
'FINSI' ;
*
* Les maillages à traiter en 2D et 3D : les maillages à un élément ont le même nom
* que l'élément du même type
* MCO2 : maillage "complexe" 2D ; MC2Q : le meme en quadratique : MC2F le meme
*    en quaf
* MCO3 : maillage "complexe" 3D ...
*
ltypma2 = lisel2 'ET' ('MOTS' 'MCO2' 'MC2Q' 'MC2F') ;
ltypma3 = lisel3 'ET' ('MOTS' 'MCO3' 'MC3Q' 'MC3F' 'MCOP') ;
*
* Procedure pour les traces de champ en 3D
*
'DEBPROC' vtch3 ;
'ARGUMENT' el*'MAILLAGE' ;
   lcoup = faux ; lvect = faux ; lboit = faux ;
   lmotcle = 'MOTS' 'COUP' 'SECT' 'VECT' 'BOIT' ;
   'REPETER' imotcle ;
      'ARGUMENT' motcle/'MOT' ;
      'SI' ('NON' ('EXISTE' motcle)) ; 'QUITTER' imotcle ; 'FINSI' ;
      'SI' ('NON' ('EXISTE' lmotcle motcle)) ;
         cherr = 'CHAINE' 'Keyword' ' ' motcle ' unknown.' ;
         'ERREUR' cherr ;
      'FINSI' ;
      'SI' ('EGA' motcle 'COUP') ;
         lcoup = vrai ; mocoup = 'CHAINE'  'COUP' ;
      'FINSI' ;
      'SI' ('EGA' motcle 'SECT') ;
         lcoup = vrai ; mocoup = 'SECT' ;
      'FINSI' ;
      'SI' ('EGA' motcle 'VECT') ;
         lvect = vrai ;
      'FINSI' ;
      'SI'  ('EGA' motcle 'BOIT') ;
         lboit= vrai ;
      'FINS' ;
   'FIN' imotcle ;
   'SI' ('ET' lboit ('OU' lvect lcoup)) ;
      cherr = 'CHAI' 'Option BOIT non compatible avec COUP et VECT' ;
      'ERRE' cherr ;
   'FINS' ;
*
   pc1 = 0.6 0. 0. ; pc2 = 0.47 0. 1. ; pc3 = 0.54 1. 0. ;
*   pc1 = 1. 0. 0. ; pc2 = 0.47 0. 1. ; pc3 = 0.54 1. 0. ;
   nomel = 'EXTRAIRE' ('ELEM' el 'TYPE') 1 ;
   lquaf = 'EXISTE' lisquaf nomel ;
   'SI' ('ET' sautquaf lquaf) ;
      'MESSAGE' 'On saute lelement' ' ' nomel ;
      'QUITTER' vtch3 ;
   'FINSI' ;
   'SI' lvect ;
      xel yel zel = 'COORDONNEE'  el ;
      vel = ('NOMC' 'UX' ('+' xel 1.))
        '+' ('NOMC' 'UY' ('+' yel 0.5)) '+'
            ('NOMC' 'UZ' ('+' zel 0.33)) ;
      vvel = 'VECT' vel 'DEPL' couvec ;
   'FINSI' ;
* Zoom * 2
   'SI' lboit ;
      boitel = BOITE ('HOMO' el ('BARY' el) 0.5) ;
   'FINS' ;
   'SI' ('NEG' ('NBEL' el) 1) ;
      lcle = 'LECT' 2 ;
   'SINON' ;
      lcle = lforcha ;
   'FINSI' ;
*    TRAC cham chpo
   'REPETER' iicle ('DIME' lcle) ;
      icle = 'EXTRAIRE' lcle &iicle ;
      'SI' ('EGA' icle 1) ;
         evel = 'ENVELOPPE' el ;
         yevel = 'COORDONNEE' 2 evel ;
         ppp = 'POIN' yevel 'EGINFE' 1.D-10 ;
         fac = 'ELEM' evel 'APPUYE' 'STRICTEMENT' ppp ;
         'SI' ('NEG' ('NBEL' fac) 1) ;
            'LISTE' fac ;
            'ERREUR' 'on devrait avoir un element dans fac' ;
         'FINSI' ;
         p1  = 1. 0.  0. ;
         pb  = 'BARYCENTRE' fac ;
         pb1 = 'PLUS' ('*' p1 0.1) ('*' pb 0.9) ;
         pel = 'POIN' evel 'PROC' pb1 ;
         cel = 'MANU' 'CHPO' pel 1 'SCAL' 1. ;
      'FINSI' ;
      'SI' ('EGA' icle 2) ;
         xel yel zel = 'COORDONNEE' el ;
         cel = xel '+' ('*' 0.1 yel) '+' ('*' 0.12 zel) ;
      'FINSI' ;
      ccel = 'CHANGER' 'CHAM' cel el ;
      'REPETER' isort ('DIME' lsort) ;
         mosort = 'EXTRAIRE' lsort &isort ;
         'OPTI' 'TRAC' mosort ;
         'REPETER' iisov ('DIME' lisov) ;
            moisov = 'EXTRAIRE' lisov &iisov ;
            'OPTI' 'ISOV' moisov ;
            'REPETER' icham ('DIME' ltypcha) ;
               iicham = &icham ;
               mocham = 'EXTRAIRE' ltypcha iicham ;
               'SI' ('EGA' mocham 'chpo') ;
                  tcel = cel ;
                  tm   = el ;
               'FINSI' ;
               'SI' ('EGA' mocham 'cham') ;
                  tcel = ccel ;
                  'SI' lquaf ;
                     tm   = 'MODE' el 'NAVIER_STOKES' 'QUAF' ;
                  'SINON' ;
                     tm   = 'MODE' el 'THERMIQUE' ;
                  'FINSI' ;
               'FINSI' ;
               ch = 'CHAINE' 'TRAC' ;
               'SI' lcoup ;
                  ch = 'CHAINE' ch ' ' mocoup ;
               'FINSI' ;
               'SI' lvect ;
                  ch = 'CHAINE' ch ' VECT' ;
               'FINSI' ;
               'SI' lboit ;
                  ch = 'CHAINE' ch ' BOIT' ;
               'FINSI' ;
               ch = 'CHAINE' ch ' ' mocham ' ' mosort ' ' moisov ' '
                   nomel ;
               'MESSAGE' ch ;
               lsaut = 'ET' ('EGA' nomel 'PY19') ('EGA' mocham 'cham') ;
               'SI' lsaut ;
                  'MESSAGE' ' Pas de trace de chamelem en PY19 ' ;
                  'MESSAGE' ' Il manque les fns de forme du PY19 ' ;
               'SINON' ;
                  'SI' lcoup ;
                     'SI' lvect ;
                     'TRACER' 'CACH' tcel tm el vvel mocoup pc1 pc2 pc3
                        'TITR' ch ;
                     'SINON' ;
                     'TRACER' 'CACH' tcel tm mocoup pc1 pc2 pc3
                        'TITR' ch ;
                     'FINSI' ;
                  'SINON' ;
                     'SI' lvect
                     'TRACER' 'CACH' tcel tm el vvel 'TITR' ch ;
                     'SINON' ;
                        'SI' lboit ;
                        'TRACER' 'CACH' tcel tm 'TITR' ch
                                                'BOIT' boitel ;
                        'SINO' ;
                        'TRACER' 'CACH' tcel tm 'TITR' ch ;
                        'FINS' ;
                     'FINSI' ;
                  'FINSI' ;
               'FINSI' ;
            'FIN' icham ;
         'FIN' iisov ;
      'FIN' isort ;
   'FIN' iicle ;
'FINPROC' ;
*
* Procedure pour les traces de champ en 2D
* Pourrait etre rassemblee avec vtch3
*
'DEBPROC' vtch2 ;
'ARGUMENT' el*'MAILLAGE' ;
   nomel = 'EXTRAIRE' ('ELEM' el 'TYPE') 1 ;
   lquaf = 'EXISTE' lisquaf nomel ;
   'SI' ('ET' sautquaf lquaf) ;
      'MESSAGE' 'On saute lelement' ' ' nomel ;
      'QUITTER' vtch2 ;
   'FINSI' ;
   'SI' ('NEG' ('NBEL' el) 1) ;
      lcle = 'LECT' 2 ;
   'SINON' ;
      lcle = lforcha ;
   'FINSI' ;
*    TRAC cham chpo
   'REPETER' iicle ('DIME' lcle) ;
      icle = 'EXTRAIRE' lcle &iicle ;
      'SI' ('EGA' icle 1) ;
         p1  = 1. 0. ;
         pb  = 'BARYCENTRE' el ;
         pb1 = 'PLUS' ('*' p1 0.1) ('*' pb 0.9) ;
         pel = 'POIN' el 'PROC' pb1 ;
*   pel = 'POIN' ('NBNO' el) ('CHANGER' 'POI1' el) ;
         cel = 'MANU' 'CHPO' pel 1 'SCAL' 1. ;
      'FINSI' ;
      'SI' ('EGA' icle 2) ;
         xel yel = 'COORDONNEE' el ;
         cel = xel '+' ('*' 0.1 yel) ;
      'FINSI' ;
      ccel = 'CHANGER' 'CHAM' cel el ;
*
      'REPETER' isort ('DIME' lsort) ;
         mosort = 'EXTRAIRE' lsort &isort ;
         'OPTI' 'TRAC' mosort ;
         'REPETER' iisov ('DIME' lisov) ;
            moisov = 'EXTRAIRE' lisov &iisov ;
            'OPTI' 'ISOV' moisov ;
            'REPETER' icham ('DIME' ltypcha) ;
               iicham = &icham ;
               mocham = 'EXTRAIRE' ltypcha iicham ;
               'SI' ('EGA' mocham 'chpo') ;
                  tcel = cel ;
                  tm   = el ;
               'FINSI' ;
               'SI' ('EGA' mocham 'cham') ;
                  tcel = ccel ;
                  'SI' lquaf ;
                     tm   = 'MODE' el 'NAVIER_STOKES' 'QUAF' ;
                  'SINON' ;
                     tm   = 'MODE' el 'THERMIQUE' ;
                  'FINSI' ;
               'FINSI' ;
               ch = 'CHAINE' 'TRAC' ' ' mocham ' ' mosort ' ' moisov ' '
                   nomel ;
               'MESSAGE' ch ;
               'TRACER' tcel tm 'TITR' ch ;
            'FIN' icham ;
         'FIN' iisov ;
      'FIN' isort ;
   'FIN' iicle ;
'FINPROC' ;
*
* Procedure pour les traces de faces
*
'DEBPROC' vtfac ;
'ARGUMENT' el*'MAILLAGE' ;
   nomel = 'EXTRAIRE' ('ELEM' el 'TYPE') 1 ;
   lquaf = 'EXISTE' lisquaf nomel ;
   'SI' ('ET' sautquaf lquaf) ;
      'MESSAGE' 'On saute lelement' ' ' nomel ;
      'QUITTER' vtfac ;
   'FINSI' ;
   'MESSAGE' 'TRAC FACE' ' ' nomel ;
*    TRAC FACE
   'REPETER' isort ('DIME' lsort) ;
      mosort = 'EXTRAIRE' lsort &isort ;
      'OPTI' 'TRAC' mosort ;
      ch = 'CHAINE' 'TRAC FACE' ' ' mosort ' ' nomel ;
      'MESSAGE' ch ;
      'TRACER' 'FACE' el 'TITR' ch ;
   'FIN' isort ;
'FINPROC' ;
*
* Procedure pour le trace d'un champ constant + un bruit
*
'DEBPROC' vtccst ;
'ARGUMENT' el*'MAILLAGE' ;
'ARGU' val*'FLOTTANT' ;
'ARGU' bru/'FLOTTANT' ;
*
   nomel = 'EXTRAIRE' ('ELEM' el 'TYPE') 1 ;
   lquaf = 'EXISTE' lisquaf nomel ;
   'SI' ('ET' sautquaf lquaf) ;
      'MESSAGE' 'On saute lelement' ' ' nomel ;
      'QUITTER' vtccst ;
   'FINSI' ;
   cel ='MANU' 'CHPO' el 1 'SCAL' val 'NATURE' 'DISCRET' ;
   tit = 'CHAI' 'TRAC CSTE' ;
   'SI' ('EXIS' bru) ;
      cel = '+' cel
            ('BRUI' 'BLAN' 'UNIF' 0. bru el) ;
      tit = 'CHAI' tit '+BRUI=' val '+' bru ;
      'MESSAGE' tit ' ' nomel  ;
   'SINO' ;
      tit = 'CHAI' tit '=' val ;
      'MESSAGE' tit ' ' nomel ;
   'FINS' ;

*    TRAC CSTE
   'REPETER' isort ('DIME' lsort) ;
      mosort = 'EXTRAIRE' lsort &isort ;
      'OPTI' 'TRAC' mosort ;
      'REPETER' iisov ('DIME' lisov) ;
         moisov = 'EXTRAIRE' lisov &iisov ;
         'OPTI' 'ISOV' moisov ;
         ch = 'CHAINE' tit ' ' mosort ' ' moisov ' ' nomel ;
         'MESSAGE' ch ;
         'TRACER' cel el 'TITR' ch ;
      'FIN' iisov ;
   'FIN' isort ;
'FINPROC' ;
*
* Procedure pour les traces de vecteur + coupe
* Pourrait etre rassemblee avec vtch3
*
'DEBPROC' vtcouvec ;
'ARGUMENT' el*'MAILLAGE' ;
   nomel = 'EXTRAIRE' ('ELEM' el 'TYPE') 1 ;
   lquaf = 'EXISTE' lisquaf nomel ;
   'SI' ('ET' sautquaf lquaf) ;
      'MESSAGE' 'On saute lelement ' nomel ;
      'QUITTER' vtcouvec ;
   'FINSI' ;
   pc1 = 0.6 0. 0. ; pc2 = 0.47 0. 1. ; pc3 = 0.54 1. 0. ;
   xel yel zel = 'COORDONNEE'  el ;
   vel = ('NOMC' 'UX' ('+' xel 1.))
     '+' ('NOMC' 'UY' ('+' yel 0.5)) '+' ('NOMC' 'UZ' ('+' zel 0.33)) ;
   vvel = 'VECT' vel 'DEPL' couvec ;
*    TRAC COUP vec
   'REPETER' isort ('DIME' lsort) ;
      mosort = 'EXTRAIRE' lsort &isort ;
      'OPTI' 'TRAC' mosort ;
      ch = 'CHAINE' 'TRAC COUP vec' ' ' mosort ' ' nomel ;
      'MESSAGE' ch ;
      'TRACER' 'CACH' vvel el 'COUP' pc1 pc2 pc3 'TITR' ch ;
   'FIN' isort ;
'FINPROC' ;
*
*'OPTI' debu 1 ;
*
* Debut du jeu de donnee
*
tmail = 'TABLE' ;
*
'REPETER' idim 3 ;
   dim = &idim  ;
*dim=3 ;
'SI' ('EGA' dim 1) ;
   'OPTI' 'DIME' 1 'ELEM' 'SEG2' ;
*
   p1 = 'POIN' 0. ; p2 = 'POIN' 1. ;
*   p3 = 'POIN' 0.5 ;
   se2 = 'MANU' 'SEG2' p1 p2 ;
   se3 = 'CHANGER' se2 'QUAD' ;
   se2i = 'MANU' 'SEG2' p2 p1 ;
   se3i = 'CHANGER' se2i 'QUAD' ;
* On ne teste rien en dimension 1 : on devrait :)
'FINSI' ;
'SI' ('EGA' dim 2) ;
   'OPTI' 'DIME' 2 'ELEM' 'TRI3' ;
   p1 = 0. 0. ; p2 = 1. 0. ; p3 = 1. 1. ; p4 = 0. 1. ;
   tr3 = 'MANU' 'TRI3' p1 p2 p3 ;
   tr6 = 'CHANGER' tr3 'QUAD' ;
   tr7 = 'CHANGER' tr6 'QUAF' ;
*
   qu4 = 'MANU' 'QUA4' p1 p2 p3 p4 ;
   qu8 = 'CHANGER' qu4 'QUAD' ;
   qu9 = 'CHANGER' qu8 'QUAF' ;
*
   tmail . 'TRI3' = tr3 ; tmail . 'TRI6' = tr6 ; tmail . 'TRI7' = tr7 ;
   tmail . 'QUA4' = qu4 ; tmail . 'QUA8' = qu8 ; tmail . 'QUA9' = qu9 ;


   mcon1 = 'CHANGER' 'TRI3' tr7 ;
   mcon2 =  ('CHANGER' 'QUA4' qu9) 'PLUS' (1. 0.);
   'ELIMINATION' mcon1 mcon2 1.D-6 ;
   mcons =  mcon1 'ET' mcon2 ;
* Suite a la fiche 9050, on trace sur un maillage composite surface+ligne
   mconl = ('CHANGER' mcons 'LIGN') 'PLUS' (0. 1.) ;
   'ELIMINATION' mcons mconl 1.D-6 ;
   mcon  = mcons 'ET' mconl ;

   mcon   = 'CHANGER' mcon  'LINE' ;
   mconq  = 'CHANGER' mcon  'QUAD' ;
   mconqf = 'CHANGER' mconq 'QUAF' ;
*
   tmail . 'MCO2' = mcon   ;
   tmail . 'MC2Q' = mconq  ;
   tmail . 'MC2F' = mconqf ;

   'SI' faux ;
* TEST 2D hors boucle
      lsort = 'MOTS' 'PSC' 'X' ;
      lisov   = 'MOTS' 'SURF' ;
      ltypcha = 'MOTS' 'chpo' ;
      lforcha = 'LECT' 1 2 ;
     VTCH2 tr7 ; VTCH2 tr6 ;
     'ERREUR' stop ;
   'FINSI' ;


* TRAC FACE
   'REPETER' i ('DIME' ltypma2) ;
      VTFAC (tmail . ('EXTRAIRE' ltypma2 &i)) ;
   'FIN' i ;
* TRAC CHPO CHAM
   'REPETER' i ('DIME' ltypma2) ;
      VTCH2 (tmail . ('EXTRAIRE' ltypma2 &i)) ;
   'FIN' i ;
'FINSI' ;
'SI' ('EGA' dim 3) ;
   'OPTI' 'DIME' 3 'ELEM' 'TET4' ;
   p1 = 0. 0. 0. ; p2 = 1. 0. 0. ; p3 = 1. 1. 0. ; p4 = 0. 1. 0. ;
   p5 = 0. 0. 1. ; p6 = 1. 0. 1. ; p7 = 1. 1. 1. ; p8 = 0. 1. 1. ;
   tetr4 = 'MANU' 'TET4' p1 p2 p3 p5 ;
   tetr10 = 'CHANGER' tetr4 'QUAD' ;
   tetr15 = 'CHANGER' tetr10 'QUAF' ;
   pyra5 = 'MANU' 'PYR5' p1 p2 p3 p4 p5 ;
   pyra13 = 'CHANGER' pyra5 'QUAD' ;
   pyra19 = 'CHANGER' pyra13 'QUAF' ;
   pris6 = 'MANU' 'PRI6' p1 p2 p3 p5 p6 p7 ;
   pris15 = 'CHANGER' pris6 'QUAD' ;
   pris21 = 'CHANGER' pris15 'QUAF' ;
   cube8 = 'MANU' 'CUB8' p1 p2 p3 p4 p5 p6 p7 p8 ;
   cube20 = 'CHANGER' cube8 'QUAD' ;
   cube27 = 'CHANGER' cube20 'QUAF' ;
*
   c1 = 'CHANGER' 'TET4' tetr15 ;
   c2 = 'CHANGER' 'QUAF' c1 ;
   c3_1 = 'CHANGER' 'CUB8' c2 ;
   c3_1 = 'ORIENTER' c3_1 ;

* Maillage composite (volume + surface + ligne)
   c3_2 = 'PLUS' ('CHANGER' c3_1 'LIGNE') (0. 0. 1.1) ;
*   c3_2 = 'PLUS' ('CHANGER' c3_1 'LIGNE') (0. 1.1 0.) ;
   c3_3 = 'CHANGER' c3_1 'SURFACE' ;
   c3_3 = 'PLUS' c3_3 (0. 1.1 0.) ;
   c3 = c3_1 'ET' c3_2 'ET' c3_3 ;
   'ELIMINATION' c3 1.D-6 ;
   tt = c3 ;
   ttq  = 'CHANGER' 'QUAD' tt ;
   ttqf = 'CHANGER' 'QUAF' ttq ;
*  Maillage composite 2 (volume + surface + ligne) plus simple
   t3_1 = tetr4 ;
   t3_2 = 'PLUS' ('CHANGER' t3_1 'LIGNE') (0. 0. 1.1) ;
   t3_3 = 'CHANGER' t3_1 'SURFACE' ;
   t3_3 = 'PLUS' t3_3 (0. 1.1 0.) ;
   t3 = t3_1 'ET' t3_2 'ET' t3_3 ;
*
   tmail . 'TET4' = tetr4  ;   tmail . 'TE10' = tetr10 ;
   tmail . 'TE15' = tetr15 ;   tmail . 'PYR5' = pyra5  ;
   tmail . 'PY13' = pyra13 ;   tmail . 'PY19' = pyra19 ;
   tmail . 'PRI6' = pris6  ;   tmail . 'PR15' = pris15 ;
   tmail . 'PR21' = pris21 ;   tmail . 'CUB8' = cube8  ;
   tmail . 'CU20' = cube20 ;   tmail . 'CU27' = cube27 ;
   tmail . 'MCO3' = tt ; tmail . 'MC3Q' = ttq ;
   tmail . 'MC3F' = ttqf ; tmail . 'MCOP' = t3 ;

   'SI' faux ;
* TEST 3D hors boucle
      lsort = 'MOTS' 'X' ;
      lisov   = 'MOTS' 'SURF' ;
      ltypcha = 'MOTS' 'chpo' ;
      lforcha = 'LECT' 1 ;
      'OPTI' debu 1 ;
      VTCCST (tmail . 'TET4') PI  ;
*      VTCCST (tmail . 'MCO3') PI  ;
*      VTCH3 tetr4  'COUP' 'VECT' ;   VTCOUVEC tetr4 ;
      'ERREUR' stop ;
   'FINSI' ;
* TRAC FACE
   'REPETER' i ('DIME' ltypma3) ;
      VTFAC (tmail . ('EXTRAIRE' ltypma3 &i)) ;
   'FIN' i ;
* TRAC COUP VEC
   'REPETER' i ('DIME' ltypma3) ;
      VTCOUVEC (tmail . ('EXTRAIRE' ltypma3 &i)) ;
   'FIN' i ;
* TRAC CHAM CHPO
   'REPETER' i ('DIME' ltypma3) ;
      VTCH3 (tmail . ('EXTRAIRE' ltypma3 &i)) ;
   'FIN' i ;
* TRAC CHAM CHPO COUP
   'REPETER' i ('DIME' ltypma3) ;
      VTCH3 (tmail . ('EXTRAIRE' ltypma3 &i)) 'COUP' ;
   'FIN' i ;
* TRAC CHAM CHPO COUP VECT
   'REPETER' i ('DIME' ltypma3) ;
      VTCH3 (tmail . ('EXTRAIRE' ltypma3 &i)) 'COUP' 'VECT' ;
   'FIN' i ;
* TRAC CHAM CHPO BOIT
   'REPETER' i ('DIME' ltypma3) ;
      VTCH3 (tmail . ('EXTRAIRE' ltypma3 &i)) 'BOIT' ;
   'FIN' i ;
* TRAC CHPO CSTE Test un champ constant...
   mpi = pi '*' -1. ;
   s180 = 'SIN' 180. ;
   ms180 = '*' s180 -1. ;
   VTCCST (tmail . 'MCO3') PI ;
   VTCCST (tmail . 'MCO3') mPI ;
   VTCCST (tmail . 'MCO3') s180 ;
   VTCCST (tmail . 'MCO3') ms180 ;
   VTCCST (tmail . 'MCO3') 0. ;
   VTCCST (tmail . 'MCO3') PI ('*' s180 10) ;
   VTCCST (tmail . 'MCO3') mPI ('*' s180 10) ;
   VTCCST (tmail . 'MCO3') s180 s180 ;
   VTCCST (tmail . 'MCO3') ms180 s180 ;
   VTCCST (tmail . 'MCO3') 0. s180 ;
'FINSI' ;
'FIN' idim ;
*




'SI' interact ;
   'OPTION' 'DONN' 5 'ECHO' 1 ;
'FINSI' ;

*
* End of dgibi file TRAC
*
'FIN' ;













