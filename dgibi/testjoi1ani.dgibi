* fichier : testjoi1ani.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
opti elem seg2 dime 3 mode trid;
option epsilon lineaire;
*****************************
*                           *
*    SOLUTION ANALYTIQUE    *
*                           *
*****************************
      UXANA = 0.00700;
      UYANA = -86.7515;
      UZANA = 2.38900;
*                           *
      RXANA = -0.36000;
      RYANA = -0.12000;
      RZANA = -4.33563;
*                           *
*****************************
*
*
*********CREATION DES POUTRES****
*
*****PROPRIETES GEOMETRIQUES*****
*
l1=10.;
lt=30.;
torp = 5;
iyp = 3;
izp = 4;
secp = 0.2;
*
*****PROPRIETES MATERIELLES*****
*
youp = 32;
nup = 0.2;
*
dir1 = 0 1 0;
dir2 = 1 0 0;
*
****POUTRE GAUCHE****
*
pi1 = 0 0 0;
pi2 = l1 0 0;
li = pi1 d 1 pi2;
*
*
****POUTRE DROITE****
*
ps1 = l1 0 0;
ps2 = lt 0 0;
ls = ps1 d 1 ps2;
*
lt = ls et li;
*trac lt;
*
mop =  modeli lt mecanique elastique poutre;
map = mate mop YOUN youp NU nup TORS torp INRY iyp INRZ izp SECT secp;
*
********JOINT*********
*
lj = pi2 d 1 ps1;
moj =  modeli lj mecanique elastique anisotrope joi1;
maj = mate moj direction dir1 dir2
         D11 30
         D21 10 D22 40
         D31 10 D32 10 D33 30
         D41 0 D42 0 D43 0 D44 40
         D51 0 D52 0 D53 0 D54 10 D55 20
         D61 0 D62 0 D63 0 D64 10 D65 10 D66 30;
*
*********************************
*********************************
*
mo1 = moj et mop;
ma1 = maj et map;
*
cl1 = bloq depl rota pi1;
*
**************
* CHARGEMENT *
**************
fo1 = force (0. -1. 0.) ps2;
*
LI1 = PROG 0. PAS 0.2 1. ;
LI2 = PROG 0. PAS 0.2 1. ;
EV1 = EVOL MANU T LI1 F(T) LI2;
*
CHA1 = CHAR 'MECA' fo1 EV1 ;
*
**************
*
TAB1 = TABLE ;
*
TAB1.'BLOCAGES_MECANIQUES' = cl1;
TAB1.'MODELE' = mo1;
TAB1.'CARACTERISTIQUES' = ma1;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'TEMPS_CALCULES' = LI1;
*
PASAPAS TAB1 ;
*
* ----------    CODE DE FONCTIONNEMENT       ----------
*
RES = TAB1 . DEPLACEMENTS . 5 ;
DEPA1 = EXTR res UY ps2 ;
RESI = ABS( (DEPA1-UYANA)/UYANA ) ;
*
SI (RESI <EG 1E-3 ) ;
  ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;
*
FIN;








