* fichier : rupt17.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
**************************************************************
*
*    rupt17.dgibi
*
*    CAS TEST SUR LE CALCUL DE J EN THERMOPLASTICITE
*    POUR FISSURE PROCHE (ou SUR) INTERFACE LIAISON BIMETALLIQUE
*
*    CREATION : BP, 11 Aout 2011
*    MODIFS   :
*
*    REM : - la presence de decharges induit une legere dependance au contour
*          - fissure parallele a linterface => pas besoin du 'MODELES_COMPOSITES'
*
**************************************************************


*******************************************************
*** Options de calcul et de tracés

 OPTI DIME 2 ELEM QUA8 MODE plan defo ;

*deformation lineaire
 OPTI 'EPSI' 'LINEAIRE';

*calcul elastique?
*  calel = vrai ;
 calel = faux ;

* Complet => modif de la finesse du maillage + du pas de chargement
 COMPLET = faux;
*  COMPLET = vrai;

* options de tracé
 GRAPH   = vrai;
 opti 'TRAC' 'PSC' 'EPTR' 5;


*******************************************************
*** Géométrie

 dxelim = 1e-6 ;
 h = 100. ;
 W = 100. ;
 hint = 50. ;
 hfis = 34. ;
* hfis = 50. ;
* avec hfis = 50., on a la fissure dans l'interface (à tester + tard)
 asW = 0.5 ;


**********************************************************************
*** Données matériau

*--- Données de l'acier inox 304L (20 et 350°C)
 lT11 = prog 20. 350. ;
 lE1 = prog 197. 172. ;
 evE1 = evol manu 'T' lT11 'YOUN' (lE1*1e3) ;
*
 lep201 = prog 0. 0.0006935 0.00104  0.00140
 0.00198 0.00305 0.00511 0.00716 0.0112 0.0163 0.0315
 0.0516 0.1020 0.152 0.303 0.503 1.004 5.006 ;
 lsi201 = prog 0. 136.6 165.6 178.0 192.5 207.0
 219.4 227.7 240.1 254.6 287.7 323.2 391.8 456.2 545.3
 630.4 767.5 1212.0 ;
 ta201 = evol manu lep201 lsi201 ;
*
 le3501 = prog 0. 0.0006347 0.00087 0.00119
 0.00171 0.00274 0.00478 0.00682 0.0109 0.0159 0.0311
 0.0513 0.1017 0.152 0.303 0.503 1.004 5.006 ;
 ls3501 = prog 0. 109.2 115.6 118.1 121.9 127.0
 134.6 141.0 151.1 161.3 189.2 221.8 290.5 354.8 443.9
 529.0 666.1 1110.6 ;
 ta3501 = evol manu le3501 ls3501 ;
*
 CTrac1 = NUAGE  'COMP' 'T' 20. 350.
      'COMP' 'TRAC' ta201 ta3501 ;

*--- Données des aciers 16 et 18MND5 (20 - 350°C)
 lT12 = prog 20. 350. ;
 lE2 = prog 204. 180. ;
 evE2 = evol manu 'T' lT12 'YOUN' (lE2*1e3) ;
* dans le cas COMPLET pb avec la pente de lsi202
*  lep202 = prog 0. 0.001691 0.001891 0.00219 0.00269
*  0.00369 0.00569 0.00769 0.00969 0.0117 0.0168 0.0219
*  0.0320 0.0421 0.0522 0.0623 0.0723 0.0823 0.0924
*  0.102 0.303 1.00 5.00 ;
*  lsi202 = prog 0. 345 345 345 345 345 345 345 345 355.4
*  372.6 386.4 410.6 434.2 448.1 461.2 470.8 477.7
*  484.6 491.6 570.9 671.5 834.2 ;
*  lep202 = prog 0. 0.001691 0.00969 0.0117 0.0168 0.0219
*  0.0320 0.0421 0.0522 0.0623 0.0723 0.0823 0.0924
*  0.102 0.303 1.00 5.00 ;
*  lsi202 = prog 0. 345 346 355.4
*  372.6 386.4 410.6 434.2 448.1 461.2 470.8 477.7
*  484.6 491.6 570.9 671.5 834.2 ;
 lep202 = prog 0. (0.300/204.) 0.002 0.0025 0.008 0.0117
 0.0168 0.0219
 0.0320 0.0421 0.0522 0.0623 0.0723 0.0823 0.0924
 0.102 0.303 1.00 5.00 ;
 lsi202 = prog 0. 300. 320. 330. 345. 355.4
 372.6 386.4 410.6 434.2 448.1 461.2 470.8 477.7
 484.6 491.6 570.9 671.5 834.2 ;
 ta202 = evol manu lep202 lsi202 ;
*
 le3502 = prog 0. 0.001423 0.001679 0.00206 0.0026
 0.00366 0.00573 0.00778 0.00983 0.0119 0.0169 0.0220
 0.0321 0.0422 0.0523 0.0623 0.0724 0.0824 0.0925
 0.102 0.303 1.00 5.00 ;
 ls3502 = prog 0. 256.2 266.1 281.0 290.0 299.0 310.9
 319.9 328.9 334.9 349.8 364.8 385.7 400.2 412.3 422.9
 429.0 435.0 441.0 447.0 509.0 586.7 709.3 ;
 ta3502 = evol manu le3502 ls3502 ;
*
 CTrac2 = NUAGE  'COMP' 'T' 20. 350.
               'COMP' 'TRAC' ta202 ta3502 ;

 si(GRAPH);
    dess ((coul ta201 bleu) et (ta3501 coul roug)
       et (coul ta202 turq) et (ta3502 coul rose));
    dess ((coul ta201 bleu) et (ta3501 coul roug)
       et (coul ta202 turq) et (ta3502 coul rose))
       'XBOR' 0. 0.02;
 fins;


*******************************************************
*** Maillage

 si(COMPLET); nW = 50 ;
 sino;        nW = 24;
 fins;
 nhF = enti (nW * (h - hint) / h) ;

 P1 = 0. (-0.5 * h) ;
 P2 = W (-0.5 * h) ;
 l1 = P1 d nW p2 ;

 si (hfis ega hint) ;
   nhA = enti (nW * hint / h) ;
   SA = l1 tran nhA (0. hint) ;
   l2 = l1 plus (0. hint) ;
   SF = l2 tran nhF (0. (h - hint)) ;
   pfis = l2 point proc ((asW * W) (hint - (0.5 * h))) ;
   p3 = l2 point proc (W (hint - (0.5 * h))) ;
   llig = l2 elem comp pfis p3 ;
   elim dxelim (llig et SA) ;
   l3 = l1 plus (0. h) ;
   elim dxelim (SF et l3) ;
   levF = diff l2 llig ;
   levA = levF plus (0. 0.) ;
   elim dxelim (SA et levA) ;
 sinon ;
   nhA = enti (nW * hfis / h) ;
   SA1 = l1 tran nhA (0. hfis) ;
   l2 = l1 plus (0. hfis) ;
   SA2 = l2 tran (nhF - nhA) (0. (hint - hfis)) ;
   l4 = l1 plus (0. hint) ;
   SF = l4 tran nhF (0. (h - hint)) ;
   pfis = l2 point proc ((asW * W) (hint - (0.5 * h))) ;
   p3 = l2 point proc (W (hint - (0.5 * h))) ;
   llig = l2 elem comp pfis p3 ;
   elim dxelim (llig et SA1) ;
   l3 = l1 plus (0. h) ;
   elim dxelim (SF et l3) ;
   levF = diff l2 llig ;
   levA = levF plus (0. 0.) ;
   elim dxelim (SA1 et levA) ;
   elim dxelim (SA2 et SF) ;
   SA = SA1 et SA2 ;
 finsi ;

 plaq = (SA coul bleu) et (SF coul roug) ;
 si(GRAPH);trac plaq ;fins;


*******************************************************
*** MODELE MECANIQUE


*--- Modèle elastique ou elasto plastique ?
si calel ;
    mo316 = MODELE sa MECANIQUE ELASTIQUE ISOTROPE ;
    mo508 = MODELE sf MECANIQUE ELASTIQUE ISOTROPE ;
    ma316 = materiau mo316 young evE1 nu 0.3 alph 1E-5 ;
    ma508 = materiau mo508 young evE2 nu 0.3 alph 1E-5 ;
sinon ;
    mo316 = MODELE sa MECANIQUE ELASTIQUE plastique ;
    mo508 = MODELE sf MECANIQUE ELASTIQUE plastique ;
    ma316 = materiau mo316 young evE1 nu 0.3 alph 1E-5 trac CTrac1 ;
    ma508 = materiau mo508 young evE2 nu 0.3 alph 1E-5 trac CTrac2 ;
 finsi ;
 mo = mo316 et mo508 ;
 ma = ma316 et ma508 ;

* ----- CONDITIONS LIMITES
 cd1 = bloq l1 UY  ;
 cd2 = RELA ENSE UY l3 ;
 cd3 = bloq pfis UX ;
 cdl = cd1 et cd2 et cd3 ;

*-----  Chargement
  lx1 = prog 0. 1. ;
  ly1 = prog 0. 1. ;
  ev1 = evol manu lx1 ly1 ;
  PRES1 = PRES MASS mo (-100.) l3 ;
*PRES1 = PRES MASS mo dxelim l3 ;
  CHAP = CHAR MECA PRES1 ev1 ;

* ----- Définition de la temperature
 Ti = 20. ; Tf = 350. ;
 chT1 = manu chpo plaq 1 'T' Ti ;
 chx = plaq coor 1 ;
 chT2 = chT1 - (nomc 'T' (chx / W * (Ti - Tf))) ;
 TT1 = table ; TT1 . 0 = 0. ; TT1 . 1 = 1. ;
 TT2 = table ; TT2 . 0 = chT1 ; TT2 . 1 = chT2 ;
 CHAT = CHAR 'T' TT1 TT2 ;

* si (COMPLET); tcha = prog 0. 0.1 pas 0.05 1. ;
si (COMPLET); tcha = prog 0. pas 0.05 1. ;
sino;         tcha = prog 0. pas 0.20 1. ;
fins;


*******************************************************
*** PROCEDURE PAS A PAS

 TAB2 = TABLE;
 TAB2.'MODELE' = mo ;
 TAB2.'CARACTERISTIQUES' = ma ;
 TAB2.'BLOCAGES_MECANIQUES' = cdl ;
 TAB2.'CHARGEMENT'= CHAP et CHAT ;
 TAB2.'TEMPS_CALCULES' = tcha ;

 PASAPAS TAB2 ;

 si(GRAPH);

  ii = (dime tcha) - 1 ;
  def = defo tab2.deplacements.ii plaq ;
  trac chT2 def 'TITR' 'T et deformee finale';

  si (non calel);
   var2 = TAB2 . 'VARIABLES_INTERNES' . ii;
   var2prg = (prog 0.) et (exp ((prog -3. PAS 0.1 -1.) * (log 10.)));
   trac var2 mo def var2prg 'TITR' 'variables_internes finales';
  fins;

 fins;


*******************************************************
*** CALCUL DU TAUX DE RESTITUTION D'ENERGIE

*--- boucle sur les contours lnc ----------------------
 si(COMPLET);
   lnc = lect 1 2 3 5 7 8 9 11 ;
   lcoul = mots VIOL BLEU TURQ VERT OLIV JAUN ORAN ROUG;
 sino;
   lnc = lect 1 2 3 5 7 ;
   lcoul = mots BLEU TURQ VERT JAUN ROUG;
 fins;
 nlc = dime lnc ; i = 0 ;
 tath = table ;
 lJM = prog ;
 tath.0 = tcha ;

 repeter bloc1 nlc ;
    i = i + 1 ;
    ncou = extr lnc i;

    SUPTAB = TABLE ;
    SUPTAB.'OBJECTIF'= 'J';
    SUPTAB.'LEVRE_SUPERIEURE' = levF ;
    SUPTAB.'LEVRE_INFERIEURE' = levA;
    SUPTAB.'COUCHE' = ncou ;
    SUPTAB.'FRONT_FISSURE' = pfis ;
    SUPTAB.'SOLUTION_PASAPAS' = TAB2;
    G_THETA SUPTAB ;

    dess suptab.EVOLUTION_RESULTATS
      'TITR' (chai i 'couche' ncou'(G_THETA)');

    mocoul1 = extr lcoul i;
    si(ega i 1);
      evotot = coul suptab.EVOLUTION_RESULTATS mocoul1;
    sino;
      evotot = evotot et (coul suptab.EVOLUTION_RESULTATS mocoul1);
    fins;

    tath.i = extr (suptab.EVOLUTION_RESULTATS) ordo ;
    Jmaxi = extr tath.i (dime tath.i);
    lJM  = lJM  et Jmaxi ;

 fin bloc1 ;
*--- fin de boucle sur les contours lnc ----------------------


 lel = W / nW ;
 evJ = evol 'BLEU' manu 'dimcouche' (lel*lnc) 'J' lJM ;
 dess (evJ) 'TITR' (chai 'stabilite de J');

 ybor0 = 0.;
 ybor1 = maxi (extr evJ 'ORDO');
 ybor1 = (1.1 * ybor1);
 tdess1 = tabl;
 tdess1 . 1 = mot 'MARQ PLUS';
 dess evJ 'YBOR' ybor0 ybor1 tdess1
   'TITR' (chai 'stabilite de J');

 dess evotot 'YBOR' ybor0 ybor1 'TITR' (chai 'evolution de J');

* opti donn 5  trac X ;


*******************************************************
*** TEST DE BON FONCTIONNEMENT

* valeurs testees
Jmax1 = maxi lJM; Jmin1 = mini lJM;
mess 'Jmax1 Jmin1=' Jmax1 Jmin1;

* bornes de references (au 11 aout 2011)
si(COMPLET); Jmax0 = 75.; Jmin0 = 65.;
sino;        Jmax0 = 80.; Jmin0 = 70.;
fins;

*test
SI ((Jmax1 < Jmax0) et (Jmin1 > Jmin0));
  ERRE  0;
SINON;
  ERRE  5;
FINSI;



FIN ;








