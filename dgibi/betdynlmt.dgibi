* fichier : betdynlmt.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*** essai de traction uniaxiale


OPTI DIME 3 ELEM CUB8 ;

* Defintion du maillage *
* cube à 8 noeuds *

P1 = 0. 0.   0.  ;
P2 = 0. 1.   0.   ;
L1 = DROI 1 P1 P2 ;
COTEG = L1 TRAN (0. 0. 1) DINI 1. DFIN 1.;
GEOM = VOLU COTEG  TRAN (1. 0. 0.)  DINI 1. DFIN 1.;
COTED=FACE GEOM  2 ;

*** Modèle et matériau

MOD1= MODE GEOM MECANIQUE ELASTIQUE PLASTIQUE_ENDOM
         BETON_DYNAR_LMT ;
MAT1 = MATE MOD1 'YOUN' 57.e9 'NU' 0.2 'RHO' 2500
                 'F0' 0.3 'Q1' 1.5 'Q2' 0.8 'Q3' 1.0
                 'SGM0' 70.e6 'XN' 15 'NVP' 1.5
                 'MVP' 1.e-4 'K' 15 'MDT' 0.5e-4
                 'NDT' 3. 'AT' 30000 'BT' 1.6
                 'MDC' 0.5e-3 'NDC' 20 'AC' 3000
                 'BC' 4. 'ED0' 9.5e-5;


*** Conditions aux limites

CL1 = BLOQ P1 UZ UY;
CL2 = BLOQ L1 UZ ;
CL3 = BLOQ COTEG UX ;
CLDEPL = BLOQ COTED UX ;

CLTOT = CL1 et CL2 et CL3 et CLDEPL ;

*** chargement

FO1 = DEPI CLDEPL 1. ;
PROG1 = PROG 0.  20. ;
PROG2 = PROG 0. 2.e-4 ;
EVOL1  = EVOL MANU PROG1 PROG2 ;
LIST1 = PROG 0. PAS 1. 20. ;

CHAR1 = CHARGEMENT FO1 EVOL1 DIMP ;

*** préparation du pasapas

tab1 = TABLE ;
tab1 . PLASTIQUE = vrai ;
tab1 . MODELE = mod1 ;
tab1 . CARACTERISTIQUES=mat1 ;
tab1 . CHARGEMENT = char1 ;
tab1 . BLOCAGES_MECANIQUES = cltot ;
tab1 . TEMPS_CALCULES = list1 ;
tab1 . TEMPS_SAUVES = list1 ;
tab1 . DYNAMIQUE = vrai ;
tab1 . MOVA = 'D' ;

PASAPAS tab1 ;

CONTR = tab1 . CONTRAINTES . 13 ;
SIXX1 = EXTR CONTR SMXX 1 1 1 ;
CONTR = tab1 . CONTRAINTES . 20 ;
SIXX2 = EXTR CONTR SMXX 1 1 1 ;

S1 = 3.43818E6 ;
S2 = 1.04167E6 ;

err1 = abs ((SIXX1 - S1)/S1 * 100) ;
err2 = abs ((SIXX2 - S2)/S2 * 100) ;

si ((err1 > .1) ou (err2 > .1));
 erre 5;
finsi ;

fin ;





