* fichier :  soret_8.dgibi
************************************************************************
* Section : Diffusion Soret
* Section : Diffusion Fick
************************************************************************

************************************************************************
*                        CAS TEST soret_8.dgibi                        *
************************************************************************
*   Test effet Soret
* - 2D AXIS
* - ELEMENTS FINIS TESTES : 'TRI6'
* - Regime permanent
* - C(R0) = C0
* - C(R1) = C1
* - GRAD(T) choisi en 1/R (T=A*ln(|R|)   ==> GRAD(T)= A/R)
* - CONDUCTION et SORET sont en concurrence
************************************************************************


************************************************************************
* OPTIONS & REGLAGES
************************************************************************
  GRAPH = 'N' ;
 'OPTI' 'DIME' 2 'ELEM' 'TRI6'  'MODE' 'AXIS';

 'SI' ('NEG' GRAPH 'N');
   'OPTI' 'TRAC' 'X';
 'SINO';
   'OPTI' 'TRAC' 'PSC';
 'FINS';

  NBE1 = 15   ;
  NBE2 = 15   ;

  K    = -7.D0;
  A    =  1.D0;

  R0   =  0.5 ;
  R1   =  1.5 ;

  C0   = 1.   ;
  C1   = 2.   ;

************************************************************************
* MAILLAGE
************************************************************************
  P1 = R0 0. ;
  P2 = R0 1. ;
  P3 = R1 1. ;
  P4 = R1 0. ;

  L1 ='DROI' NBE1 P1 P2;
  L2 ='DROI' NBE1 P4 P3;

  S1 ='REGL' NBE2 L1 L2;
  L3 ='COTE' 2 S1;

 'TRAC' 'QUAL' S1;

************************************************************************
* MODELE & MATERIAU
************************************************************************
  MOD1 ='MODE'  S1   'DIFFUSION' 'FICK' 'INCO' 'HI  ' 'QHI ';
  MAT1 ='MATE'  MOD1 'KD  ' 1.D0 ;
  MAT2 ='MANU' 'CHML' MOD1 'SCAL' K 'RIGIDITE' ;

************************************************************************
* Champ de temperature pour que le GRAD(T) soit constant
************************************************************************
  COO1  ='COOR' 1 S1 ;
  EVO1  ='EVOL' 'BLEU' 'CHPO' COO1 'SCAL' L3  ;
  LRR   ='EXTR'  EVO1  'ORDO' 1 ;
  TEMP1 ='CHAN' 'COMP' (A * ('LOG' ('ABS' COO1) )) 'T' ;
 'TITR' 'Evolution de la TEMPERATURE avec (abscisse curviligne)';
  EVOT  ='EVOL' 'BLEU' 'CHPO' TEMP1 'T' L3  ;
 'DESS' EVOT ;

************************************************************************
* RIGIDITE
************************************************************************
  RIG1 ='COND' MOD1 MAT1;
  RIG2 ='SORE' MOD1 MAT1 MAT2 TEMP1;

************************************************************************
* CONDITIONS AUX LIMITES
************************************************************************
  CLIM1 ='BLOQ' 'HI'  L1   ;
  CLIM2 ='BLOQ' 'HI'  L2   ;


  CIMP1 ='DEPI' CLIM1 C0   ;
  CIMP2 ='DEPI' CLIM2 C1   ;

************************************************************************
* RESOLUTION
************************************************************************
  SOL1  ='RESO' (RIG1 ET RIG2 ET CLIM1 ET CLIM2) (CIMP1 ET CIMP2);
 'TITR' 'Isovaleurs de concentration en HI';
 'TRAC' SOL1 S1;

************************************************************************
* POST-TRAITEMENT
************************************************************************
* Solution NUMERIQUE
  EVONUM ='EVOL' 'ROUG' 'CHPO' SOL1 'HI  ' L3  ;
  LCNUM  ='EXTR' EVONUM 'ORDO' 1 ;
  EVONUM ='EVOL' 'ROUG' 'MANU' 'X' LRR 'HI' LCNUM;

* Solution ANALYTIQUE
  EVO2 ='EVOL' 'CHPO' TEMP1 'HI' L3  ;
  FLO1 = -1.*K*A ;

  LCANA=(((R0**FLO1)*C1) - ((R1**FLO1)*C0) + ((LRR**FLO1)*(C0 - C1))) /
        ( (R0**FLO1)     -  (R1**FLO1));

  EVOANA ='EVOL' 'BLEU' 'MANU' 'X' LRR 'HICA' LCANA;
 'DESS' (EVONUM ET EVOANA);

* Comparaison
  LCDIFF = LCNUM - LCANA ;
 'TITR' 'Difference entre la Solutions NUMERIQUE et ANALYTIQUE';
  EVODIF ='EVOL' 'VERT' 'MANU' 'X' LRR 'HICA' LCDIFF;
 'DESS' EVODIF;


* Critere d'erreur
 'OPTI' 'ECHO' 0;
  ERRABS ='MAXI' 'ABS' LCDIFF;
  ERRREL = ERRABS / (('MAXI' LCANA) - ('MINI' LCANA))      ;
 'MESS' '*********************************************' ;
 'MESS' '* Erreur MAXIMALE absolue  :' ERRABS '*';
 'MESS' '* Erreur MAXIMALE relative :' ERRREL '*';
 'MESS' '*********************************************' ;
 'SI' (ERRREL >EG 1.72D-4);
   'ERRE' 5;
 'FINS';

 'FIN';


