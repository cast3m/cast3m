* fichier : gamma.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************


************************************************************************
* NOM         : GAMMA
* DESCRIPTION : Teste l'operateur 'FONC' 'GAMMA' sur des LISTREEL
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Serge PASCAL       (CEA/DEN/DM2S/SEMT/LM2S)
* REVISION    : Clément BERTHINIER (CEA/DEN/DM2S/SEMT/LM2S)
**********************************************************************
* VERSION    : v1, 28/10/2009, version initiale
* HISTORIQUE : v1, ??/??/????, création
* HISTORIQUE : v2, 11/02/2015, Des valeurs Inf apparaissaient...
*                              Correction de la plage de validite
* HISTORIQUE :
************************************************************************
* Prière de PRENDRE LE TEMPS de compléter les commentaires
* en cas de modification de ce sous-programme afin de faciliter
* la maintenance !
************************************************************************



* Test de quelques valeurs remarquables :
L1       = 'PROG' -1.5 -0.5 0.5 1. 1.5 2. 2.5 3. 3.5 4. ;
RCPI1    = PI ** 0.5 ;
L2       = 'PROG' (4. * RCPI1 / 3.) (-2. * RCPI1) RCPI1 1.
                  (0.5 * RCPI1) 1. (0.75 * RCPI1) 2.
                  (0.125 * 15. * RCPI1) 6. ;
L3       = 'FONC' 'GAMMA' L1 ;
ERR1     = 'MAXI' 'ABS' ((L3 - L2) / L2) ;

* Test egalite : G(x)G(1-x)=PI/sin(PI*x) :
E1       = EXP 1. ;
L1       = 'PROG' (-1.D0 * (E1** 5.05)) PAS (E1 / 140.) (E1 ** 5.1) ;
L2       = SIN (180. * L1)  ;
L3       = 'FONC' 'GAMMA' (1 - L1) ;
L4       = 'FONC' 'GAMMA' (L1) ;
L5       = L2 * L3 * L4 - ('PROG' ('DIME' L1) * PI);

ERR2     = 'MAXI' 'ABS' L5 ;
'LIST' (ABS L5) ;
'LIST' ERR2 ;
*'DESS' ('EVOL' 'MANU' L1 L23) ;

ERR0     = 'MAXI' ('PROG' ERR1 ERR2) ;
'SI' (ERR0 '>' 2.E-10) ;
  'ERRE' 5 ;
'FINS' ;

'FIN' ;




