* fichier :  soret_1.dgibi
************************************************************************
* Section : Diffusion Soret
* Section : Thermique Conduction
************************************************************************

****************** CAS TEST  soret_1.dgibi *****************************
*   test  effet Soret  sur un disque plan avec trou central
*                en modéle axisymetrique
*     le potentiel agissant sur la  concentration donne un gradient
*     radial constant
*     Concentration fixées sur les bords interne et externe
*
************************************************************************
  GRAPH = 'N' ;
  'OPTION' 'DIME' 2 'ELEM' 'QUA4'  ;
  'OPTION' 'MODE' 'AXIS' ;
  nbb = 100 ;l = 100. ;
  rint = 100. ;rext = rint + l ;

*---- solution analytique  calculée a partir d'une integrale-------
*-----donnée par MATLAB pour  pour v = .5   ----------------------
*-  pour ri = 100. re= 200. diffusivité  du materiau  1.2
*--------- concentrations interne 20.  externe 100. ---------------
l1 = 'PROG'0. 'PAS' 1. 11. 15. 20. 'PAS' 10. 100. ;
l2 = 'PROG'20. 80.15 119.15 144.21 160.09 169.94 175.83 179.11
 180.68 181.14 180.88 180.15 175.19 168.03 154.89 143.63 133.89
 125.39 117.27 111.27 105.35 100. ;
 evana = 'EVOL' 'MANU' 'ABSC' l1 'ORDO' l2 ;
**
*------ on definit un maillage  au pas de l1----------------------

  p1 = rint 0  ; p2 = rext   0. ; p3 = rint 1.;
  pp1 = 111. 0. ; pp2 = 115. 0 ; pp3 = 120. 0. ;
  nr = 1 ;
  su1 = ('DROI' 1 p3 p1 ) 'TRAN' (11 * nr)  (pp1 'MOINS' p1 )
                   'TRAN'  nr   (pp2 'MOINS' pp1)
                   'TRAN'  nr   (pp3 'MOINS' pp2)
                   'TRAN'   (8 * nr)   (p2  'MOINS' pp3) ;

  'TITRE' ' traitement axi ' ;
  'SI' ('NEG' GRAPH 'N') ;
  'TRAC' su1 ;
  'FINSI' ;
  v = .5 ;
  lig1 = su1 'COTE' 2 ;
  cext = su1 'COTE' 3 ;
  cint = su1 'COTE' 1 ;
  kdif  = 1.2 ;
  r = coor 1 su1 ;
*  Cacul du potentiel T donnant  T,R = v  constante
  TT = CHAN 'COMP' 'T' ( r * v ) 'NATU' 'DIFFUS';
  kv = manu chpo su1 1 SCAL (1./kdif ) ;
*
  mod1 = su1 'MODE' 'THERMIQUE' 'ISOTROPE'   ;
  mat1 = 'MATE' mod1 'K' kdif ;
*------------- matrice de diffusion  normale -------------------------
  rig1 = 'CONDUC' mod1 mat1 ;
*--------------matrice effet Soret -----------------------------------
  kvv = chan cham kv mod1 'RIGIDITE' ;
*  rig2 = 'SORE'   mod1 mat1 TT ;
  rig2 = sore   mod1 mat1 kvv  TT    ;
*
  clime= 'BLOQUER' 'T'  cext    ;
  clims= 'BLOQUER' 'T'  cint    ;
  ff = ('DEPIMP' clime 100. ) 'ET' ( 'DEPIMP' clims 20.);
*
  sol1 = 'RESO' ( rig1 et rig2 et clime et clims) ff  ;
  'TITR' 'Comparaison solution analytique / F.E. ' ;
  ev3 = 'EVOL'  roug 'CHPO' sol1 'T' lig1 ;
  r1 = sol1 clime 'REAC' 'RESU' 'MAXI' ;
  r2 = sol1 clims 'REAC' 'RESU' 'MAXI' ;
   tabu = table ;
  tabu.1= 'MARQ CROI ' ;
  tabu.2= 'MARQ TRIA ' ;
   tabu.'TITRE' = table ;
  tabu.'TITRE'. 1 = MOT 'ANALYTIQUE' ;
  tabu.'TITRE'. 2 = MOT 'AXISYMETRIQUE' ;

  'SI' ('NEG'  GRAPH 'N' ) ;
  'DESS' ( evana  et ev3 ) lege tabu xbor 0. 100. ;
  finsi ;
*----------------calcul de l'erreur ---------------------------------
 l3 = 'EXTR' ev3 'ORDO' ;
    si (( dime l2 ) neg (dime (ev3 extr absc )))  ;
 l3 = ipol (extr evana absc ) (ev3 extr absc ) ( ev3 extr ordo ) ;
    finsi ;
    delta = (l3 - l2) ;
 eeee = 'ABS' (delta / l2 ) ;
 'TITR' 'Erreur absolue ' ;
 ever1 = 'EVOL' 'MANU' 'ABSC' l1 'ORDO' delta ;

 'TITR' 'Erreur relative ' ;
 ever2 = 'EVOL' 'MANU' 'ABSC' l1 'ORDO' eeee ;
 ermax = 'MAXI' eeee ;

  'SI' ('NEG'  GRAPH 'N' ) ;
  'DESS' ever1 'XBOR' 0. 100. ;
  'DESS' ever2 'XBOR' 0. 100. ;
  finsi ;


 'MESS' ' FLUX  en ENTREE  ' R2 ;
 'MESS' ' FLUX  en SORTIE  ' R1 ;

'SI' (ermax > 1.e-2 ) ;
   'ERRE' 5 ;
'FINS'      ;
'FIN' ;


