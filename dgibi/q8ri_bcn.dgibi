* fichier :  q8ri_bcn.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 1 ;
******************************************************************
* TEST Q8RI     BCN
*
* Verification of element Q8RI (QUA8 with 2x2 Gauss points)
*
* Elastic analysis of a square subjected to biaxial extension
*
******************************************************************
TITRE 'VERIFICATION OF ELEMENT Q8RI' ;
OPTI DIME 2 ELEM QUA8 MODE PLAN DEFO ;
opti epsilon lineaire;

GRAPH='N';
*
*------------ Geometry definition --------------------------------
*
pa = 0. 0. ; pb = 10. 0. ; pc = 10. 10. ; pd = 0. 10 ;
lab = pa DROIT 10 pb ;lbc = pb DROIT 10 pc ;
lcd = pc DROIT 10 pd ;lda = pd DROIT 10 pa ;
mesh = DALLER lab lbc lcd lda ;
*
SI (NEG GRAPH 'N');
  TRACE mesh ;
FINSI ;
*
*------------ Model and material ---------------------------------
*
mod1 = MODE mesh MECANIQUE ELASTIQUE Q8RI ;
mat1 = MATE mod1 YOUN 1.E7 NU 0 ;
*
*------------ Stiffness and boundary conditions ------------------
*
rig1 = RIGI mod1 mat1 ;
bc_lab = BLOQ UY lab ; bc_lbc = BLOQ UX lbc ;
bc_lcd = BLOQ UY lcd ; bc_lda = BLOQ UX lda ;
bc_tot = bc_lab ET bc_lbc ET bc_lcd ET bc_lda ;
rigtot = rig1 ET bc_tot ;
*
*------------ Prescribed displacements: dilatation ---------------
*
di_lbc = DEPI bc_lbc 0.01 ;
di_lcd = DEPI bc_lcd 0.01 ;
ftot = di_lbc ET di_lcd ;
*
*------------ Linear system --------------------------------------
*
desp = RESO rigtot ftot ;
*=================================================================
*                         Postprocess
*=================================================================
*
* Deformed mesh
*
SI (NEG GRAPH 'N') ;
  def0 = DEFO mesh desp 0.  BLAN ;
  def1 = DEFO mesh desp 10. ROUG ;
  TRACE (def0 ET def1) ;
FINSI ;
*
* Stresses computed with SIGM
*
stress1 = SIGM mod1 mat1 desp ;
*
* Stresses computed with EPSI and ELAS
*
strain  = EPSI mod1 desp ;
stress2 = ELAS mod1 strain mat1 ;
*
* Gradient of displacements computed with GRAD
*
grad1 = GRAD mod1 desp ;
*
* Verifications
ERRE 0 ;
* 1) stress1 = stress2
*
difstres = MAXI (stress1-stress2) ABS ;
SI (difstres > 1.E-4) ; ERRE 5 ; FINSI  ;
*
* 2) smxx = smyy = 1.e4 , smzz = smxy = 0
*
smxxmin = MINI (EXCO stress1 SMXX) ; dxxmin = ABS (smxxmin-1.E4) ;
smxxmax = MAXI (EXCO stress1 SMXX) ; dxxmax = ABS (smxxmax-1.E4) ;
smyymin = MINI (EXCO stress1 SMYY) ; dyymin = ABS (smyymin-1.E4) ;
smyymax = MAXI (EXCO stress1 SMYY) ; dyymax = ABS (smyymax-1.E4) ;
smzzmin = MINI (EXCO stress1 SMZZ) ; dzzmin = ABS smzzmin ;
smzzmax = MAXI (EXCO stress1 SMZZ) ; dzzmax = ABS smzzmax ;
smxymin = MINI (EXCO stress1 SMXY) ; dxymin = ABS smxymin ;
smxymax = MAXI (EXCO stress1 SMXY) ; dxymax = ABS smxymax ;
dif = PROG dxxmin dxxmax dyymin dyymax
           dzzmin dzzmax dxymin dxymax ;
SI ( (MAXI dif ABS)> 1.E-4) ;
   ERRE 5 ;
FINSI ;
FIN ;








