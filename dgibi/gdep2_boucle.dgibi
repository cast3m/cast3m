* fichier :  gdep2_boucle.dgibi
******************************************************************
*           anneau sous pression non uniforme
******************************************************************
*
* position du probleme
* --------------------
* il s agit de determiner la deformee d un anneau
* sous pression non uniforme suiveuse en grands
* deplacements
* reference:
* ----------
* Argyris J., Mlejnek H. P., Dynamics of structure, North Holland
* 1991, p545
******************************************************************

option echo 1;
option dime 2 elem seg2 ;
opti mode 'PLAN' 'CONT' ;

graph = faux ;

*-------------------------------
* definition de la geometrie
*-------------------------------

r = 100 ;
a = 0 0  ;
b = 100 0  ;
c = 0 100 ;
lig1 = b cerc 18 a c ;

*--------------------------------------
* definition des conditions aux limites
*--------------------------------------
cl1 = bloq rota (b et c) ;
cl2 = bloq uy b ;
cl3 = bloq ux c ;
cl = cl1 et cl2 et cl3 ;

*---------------------------------------------------------
* definition du modele, du materiau et des caracteristiques
*---------------------------------------------------------

valyoun = 2.1e7 ;
mo1 = modele lig1 mecanique elastique isotrope coq2 ;
ma  = mate mo1 youn valyoun nu 0. rho 7.8e3 ;
ep1 = 1. ;
ca  = cara mo1 epai ep1 ;
ma  = ma et ca;

*-----------------------------
* calcul de l objet chargement
*-----------------------------

lf = prog 0  1000. ;
lt = prog 0  1000. ;
ls = prog 0 pas 0.025 1. pas 0.0125 3. ;

p0bar = r * r * r * 12 / valyoun ;
chx1 = lig1 coor 1 ;
chp1 = chx1 ** 2 / r / r * 2. / p0bar ;
chp1 = chp1 nomc pr ;
*
* Modele Chargement de pression
  MOPRE = 'MODE' LIG1 'CHARGEMENT' 'PRESSION' 'COQ2' ;
  CHMZ = pres mopre (chan cham mopre chp1) ;
*
  TTPS = 'TABL' ; TTPS. 0 = 0. ; TTPS. 1 = 1000. ;
  TPRE = 'TABL' ; TPRE. 0 = 0.*CHMZ ; TPRE. 1 = 1000.*CHMZ ;
  CHAA = 'CHAR' 'PRES' TTPS TPRE ;
*
*------------------------------
*  calcul non lineaire
*------------------------------

tab2 = table ;
tab2.grands_deplacements = vrai ;
tab2.blocages_mecaniques = cl;
tab2.modele = MO1 'ET' MOPRE ;
tab2.caracteristiques = MA ;
tab2.chargement = CHAA ;
tab2.temps_calcules = ls;
tab2.'PRECISION'= 1e-8;





pasapas tab2 ;
*------------------------------
* comparaison avec la reference
*------------------------------

xxx = peche tab2 deplacements;
uxb = (extr xxx ux b)  ;
uxbth = -86. ;
err = (abs ((uxbth - uxb)/uxbth))*100 ;
mess 'deplacement du point b, p0 = 3 ' ;
mess 'reference 'uxbth 'castem' uxb '%erreur' err ;

si (err < 2) ;
   erre 0 ;
sinon ;
   erre 5 ;
finsi ;

si(graph) ;
   defo0 = defo lig1 xxx 0. VERT ;
   defo1 = defo lig1 xxx 1. ROUG ;
   trac (defo0 et defo1) ;
finsi ;

fin ;








