* fichier : ouglova_CP.dgibi
opti epsi lineaire ;
*******************************************************
*******************************************************
*******************************************************
*  Cas test de l'implantation numerique du modele
*  OUGLOVA 2D en contraintes planes
*******************************************************
* Developpe par :
*     Romili PAREDES
*     Benjamin RICHARD
* Contact :
*     Romili.Paredes@cea.fr
*     Benjamin.Richard@cea.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
******************************************************

*******************************************************
* Options
*******************************************************

OPTI DIME 2 ELEM TRI3 TRAC PSC MODE PLAN CONT;
DENSITE 1;

******************************************************
*** Definition de la geometrie
******************************************************

P1 = 0.  0.;
P2 = 1.  0.;
P3 = 1.  1.;
P4 = 0.  1.;

L1 = D P1 P2;
L2 = D P2 P3;
L3 = D P3 P4;
L4 = D P4 P1;

ST = SURF 'PLAN' (L1 ET L2 ET L3 ET L4);

******************************************************
*** Définition du modele
******************************************************

MOD1 = MODE ST MECANIQUE ELASTIQUE ISOTROPE PLASTIQUE OUGLOVA;

******************************************************
*** Parametres materiaux
******************************************************

* Module d'Young
ygn = 200.e9;

* Coefficient de Poisson
nun = 0.33;

* Limite elastique
sigym = 500.e6;

* Coefficient d'ecrouissage
km = 500.e6;

* Exposant d'ecrouissage
mm = 2.786;

* Taux de corrosion en percentage
tcm = 15;

* Endommagement critique
dcm = 0.2;


MAT1 = MATE MOD1 'YOUN' ygn 'NU' nun
                 'SIGY' sigym 'K   ' km 'M   ' mm
                 'TC  ' tcm 'DC  ' dcm;

******************************************************
*** Definition des conditions aux limites
*** et des déplacements imposés
******************************************************

CL1 = BLOQ L1 UY;
CL2 = BLOQ P1 UX;

CL11 = BLOQ L3 UY;
D1   = DEPI CL11 1;

CLTOT = CL1 ET CL2 ET CL11;

******************************************************
*** Definition de chargement
******************************************************

LI1  = PROG 0. 1.;
LI2  = PROG 0. 3.5e-1;
LIS1 = PROG 0. PAS 0.01 1;

EV   = EVOL MANU LI1 LI2;

CHA1 = CHAR 'DIMP' D1 EV;

CHTOT = CHA1;

******************************************************
*** Resolution
******************************************************

TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CLTOT;
TAB1.'MODELE' = MOD1;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'MAXITERATION' = 1000;

PASAPAS TAB1;

******************************************************
*** Verification
******************************************************

EV2 = @GLOBAL TAB1 cl1 EV FY;
liste EV2;

vref = EXTR EV2 'ORDO';
vref = EXTR 21 vref;

err =  abs(vref  + 5.37658e8) / 5.37658e8;
message ' erreur relative ';
list err;

si (err > 1.e-3);
   erreur (5);
finsi;

FIN;




