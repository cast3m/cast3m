* fichier :  bobiproc.dgibi
DEBPROC INDUCTAN BOBINE*'MAILLAGE' TBIOT*'TABLE';
*********************************************************
* CALCUL DE LA MUTUELLE INDUCTANCE ENTRE UN INDUCTEUR
* DECRIT ANALYTIQUEMENT PAR LA TABLE TBIOT ET UN
* INDUIT DE TYPE MAILLAGE.
*    TBIOT.'SOUSTYPE' = INDUCTEUR
*    TBIOT.1 = TABLE DECRIVANT L'INDUCTEUR 1
*     ...
*    TBIOT.N = TABLE DECRIVANT L'INDUCTEUR N
*    POUR L'INDUCTEUR 1 PAR EXEMPLE :
*    TBIOT.1.'SOUSTYPE' = INDUCTEUR1
*    TBIOT.1.'GEOTYPE' = 'BARR' OU 'ARC' OU 'CIRC'
*    CAS 'ARC' OU 'CIRC':
*      TBIOT.1.'POINT1' = CENTRE DE LA SPIRE
*      TBIOT.1.'POINT2' = PREMIER POINT DEFINISSANT LA SPIRE
*      TBIOT.1.'POINT3' = DEUXIEME POINT DEFINISSANT LA SPIRE
*      TBIOT.1.'FLOT1' = RAYON INTERIEUR DE LA BOBINE
*      TBIOT.1.'FLOT2' = RAYON EXTERIEUR DE LA BOBINE
*      TBIOT.1.'FLOT3' = HAUTEUR DE LA BOBINE
*    CAS 'BARR':
*      TBIOT.1.'POINT1' = EXTREMITE 1 DE LA BARRE
*      TBIOT.1.'POINT2' = EXTREMITE 2 DE LA BARRE
*      POINT1 ET POINT2 DEFINISSENT L'AXE X LOCAL
*      TBIOT.1.'POINT3' = POINT DEFINISSANT L'AXE Y LOCAL
*      TBIOT.1.'FLOT1' = DY DE LA BOBINE
*      TBIOT.1.'FLOT2' = DZ DE LA BOBINE
*    TBIOT.1.'SECTION' = 'RECTANGLE' OU 'TRAPEZE'
*    DANS LE CAS 'TRAPEZE', ON A DE PLUS :
*    TBIOT.1.'PENTEBASSE' = PENTE BASSE
*    TBIOT.1.'PENTEHAUTE' = PENTEHAUTE
*
*    SYNTAXE : MUTU = INDUCTAN BOBINE TBIOT;
*
********************************************************
*
* MAILLAGE DE POINTS CONSTITUE DES
* BARYCENTRES DES ELEMENTS DE 'BOBINE'
*
  NE = NBEL BOBINE;
  IEL = 1;
  GEO1 = BOBINE ELEM IEL;
  P0 = BARY GEO1;
  GEO2 = P0 ET P0;
  GEO2 = GEO2 ELEM POI1 1;
  NEM1=NE-1;
  REPETER BOUC NEM1;
    IEL = IEL + 1;
    GEO1 = BOBINE ELEM IEL;
    PC = BARY GEO1;
    GEO3 = PC ET PC;
    GEO3 = GEO3 ELEM POI1 1;
    GEO2 = GEO2 ET GEO3;
  FIN BOUC;
  ELIM 0.01 GEO2;
*
* CALCUL DU POTENTIEL VECTEUR DU A CHAQUE
* INDUCTEUR ET DU POTENTIEL VECTEUR TOTAL
*
  PI = 3.14159265;
  MU0 = 4.*PI*1.E-7;
*
* INITIALISATION DU POTENTIEL VECTEUR TOTAL
  ATOT = MANU 'CHPO' GEO2 3 AX 0. AY 0. AZ 0.;
*
* BOUCLE SUR LES INDUCTEURS
  IND = 0;
  NIND = DIME TBIOT;
  NIND = NIND-1;
  REPETER BOUC NIND;
    IND = IND+1;
    TYPI = TBIOT.IND.'GEOTYPE';
    Q1 = TBIOT.IND.'POINT1';
    Q2 = TBIOT.IND.'POINT2';
    Q3 = TBIOT.IND.'POINT3';
*
*   CAS DES CERCLES ET DES ARCS
    SI ((EGA TYPI 'CIRC') OU (EGA TYPI 'ARC'));
      RI = TBIOT.IND.'FLOT1';
      RE = TBIOT.IND.'FLOT2';
      H  = TBIOT.IND.'FLOT3';
      S = (RE-RI)*H;
      DNS = 1./S;
      SECT = TBIOT.IND.'SECTION';
*
*     SECTION RECTANGULAIRE
      SI (EGA SECT 'RECTANGLE');
        SI (EGA TYPI 'CIRC');
          AIND = BIOT 'POTE' GEO2 'CERC' Q1 Q2 Q3
                 RI RE H DNS MU0;
        FINSI;
        SI (EGA TYPI 'ARC');
          AIND = BIOT 'POTE' GEO2 'ARC' Q1 Q2 Q3
                 RI RE H DNS MU0;
        FINSI;
      FINSI;
*
*     SECTION TRAPEZOIDALE
      SI (EGA SECT 'TRAPEZE');
        PENT1 = TBIOT.IND.'PENTEBASSE';
        PENT2 = TBIOT.IND.'PENTEHAUTE';
        SI (EGA TYPI 'CIRC');
          AIND = BIOT 'POTE' GEO2 'CERC' Q1 Q2 Q3
                 RI RE H 'TRAP' PENT1 PENT2 DNS MU0;
        FINSI;
        SI (EGA TYPI 'ARC');
          AIND = BIOT 'POTE' GEO2 'ARC' Q1 Q2 Q3
                 RI RE H 'TRAP' PENT1 PENT2 DNS MU0;
        FINSI;
      FINSI;
*
*   CAS DES BARRES
    SINON;
      DY = TBIOT.IND.'FLOT1';
      DZ = TBIOT.IND.'FLOT2';
      S = DY*DZ;
      DNS = 1./S;
      SECT = TBIOT.IND.'SECTION';
*
*     SECTION RECTANGULAIRE;
      SI (EGA SECT 'RECTANGLE');
        AIND = BIOT 'POTE' GEO2 'BARR' Q1 Q2 Q3 DY DZ
               DNS MU0;
      FINSI;
*
*     SECTION TRAPEZOIDALE
      SI (EGA SECT 'TRAPEZE');
        PENT1 = TBIOT.IND.'PENTEBASSE';
        PENT2 = TBIOT.IND.'PENTEHAUTE';
        AIND = BIOT 'POTE' GEO2 'BARR' Q1 Q2 Q3
               DY DZ 'TRAP' PENT1 PENT2 DNS MU0;
      FINSI;
    FINSI;
    ATOT = ATOT + AIND;
  FIN BOUC;
*
* CALCUL DES VOLUMES ELEMENTAIRES
*
  LVOL = PROG 0.;
  IE = 0;
  REPETER BOUC NE;
    IE = IE+1;
    GEO1 = BOBINE ELEM IE;
    MOD1 = MODE GEO1 MECANIQUE ELASTIQUE;
    MAT1 = MATE MOD1 'RHO' 1. 'YOUN' 1. 'NU' 1.;
    VMAS = MASSE MOD1 MAT1;
    CUNI = MANU 'CHPO' GEO1 1 UZ 1.;
    VOEL = VMAS*CUNI;
    VOLU = @TOTAL VOEL GEO1 FZ;
    LVOL = LVOL ET (PROG VOLU);
  FIN BOUC;
  NEP1=NE+1;
  LENT = LECT 2 PAS 1 NEP1;
  LVOL = EXTR LVOL (LENT);
  CVOL = MANU 'CHPO' GEO2 1 SCAL LVOL;
*
* CALCUL DE LA NORME DU
* POTENTIEL VECTEUR
*
  LCOM = MOTS AX AY AZ;
  ANOR = PSCA AIND AIND LCOM LCOM;
  ANOR = ANOR**0.5;
*
* PONDERATION PAR LE VOLUME
* ELEMENTAIRE : CALCUL DE LA MUTUELLE
*
  LCOM = MOTS SCAL;
  MUTUELLE=PSCA ANOR CVOL LCOM LCOM;
  MUTUELLE = @TOTAL MUTUELLE GEO2 SCAL;
  MUTUELLE = MUTUELLE/S;
FINPROC MUTUELLE;
***************************************************
***************************************************
OPTI DIME 3 ELEM CUB8 MODE TRIDIM COUL ROUG ;
DENS 0.1;
O0 = 0. 0. 0. ;
O1 = 0. 0. 1. ;
O2 = 1. 0. 0. ;
O3 = 0. 1. 0. ;
P1 = 0.9 0. -0.1;
P2 = 1.1 0. -0.1;
P3 = 1.1 0. 0.1;
P4 = 0.9 0. 0.1 ;
N1 = 3;
N2 = 3;
NC = 16;
L1 = P1 D N1 P2 ;
L2 = P2 D N2 P3 ;
L3 = P3 D N1 P4 ;
L4 = P4 D N2 P1 ;
S1 = L1 L2 L3 L4 DALL PLAN;
BOBINE = VOLU S1 ROTA NC 360 O0 O1;
ELIM 0.01 BOBINE;
OEIL = 1000. 1000. 1000.;
*TRAC OEIL BOBINE CACHE;
*
* L'INDUCTEUR EST UNE BOBINE CIRCULAIRE :
* ON MODELISE UN SEUL INDUCTEUR
*
TBIOT = TABLE 'INDUCTEUR';
TBIOT . 1 = TABLE 'INDUCTEUR1';
TBIOT . 1 . 'GEOTYPE' = 'CIRC';
TBIOT . 1 . 'POINT1' = O0;
TBIOT . 1 . 'POINT2' = O2;
TBIOT . 1 . 'POINT3' = O3;
TBIOT . 1 . 'FLOT1' = 0.9;
TBIOT . 1 . 'FLOT2' = 1.1;
TBIOT . 1 . 'FLOT3' = 0.2;
TBIOT . 1 . 'SECTION' = 'RECTANGLE';
SELF = INDUCTAN BOBINE TBIOT;
LIST SELF;
SOLREF = 3.15e-6;
ERREL = ABS(SOLREF-SELF)/SOLREF;
LIST ERREL;
SI (ERREL > 0.02);
   ERREUR 5;
FINSI;
FIN;


