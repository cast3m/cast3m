**********************************
*Section: Mecanique Nonlineaire  *
*Section: Mecanique Transitoire  *
**********************************
*
* CAS TEST DU 13/11/14     PROVENANCE : TEST
SAUT PAGE;
*
*******************************************************
*                                                     *
*  TEST PROCEDURE OUVCOR POUR UN PANEL EN CISSAILEMENT*
*  4. * 1. * 0.2                                      *
*                                                     *
*                                                     *
*  Le maillage est en 3D a l'aide d'elements          *
*  massif CUB8. L'acier est maille a l'aide d'elements*
*  barre.                                             *
*                                                     *
*******************************************************


*******************************************************
* MAILLAGE
*******************************************************
opti dime 3 ;
opti elem cub8 ;


dfi = 0.15 ;

P1 = 0. 0. 0 ;
P2 = 4. 0. 0 ;

DT = DROI P1 P2 dini dfi dfin dfi ;

SM = DT TRANS (0 1. 0) dini dfi dfin dfi ;
DT = SM COTE 3 ;

VT = VOLU SM TRANS (0 0 0.2) dini dfi dfin dfi ;

elim 1.e-4 VT ;
***
*** definition des renforcements dans la pertie centrale
** barre verticale
PA1 = 2. 0.05 0.05 ;
PA2 = PA1 PLUS (0 (1. - 0.05) 0) ;

DV = DROI PA1 PA2 dini dfi dfin dfi ;

** barres horizontales

P1 = 0.05 0.5 0.055 ;
P2 = P1 PLUS ((4. - 0.05) 0 0) ;

DH = DROI P1 P2 dini dfi dfin dfi ;

STRUCT = vt et dv et dh ;

MODB = MODE VT MECANIQUE ELASTIQUE ISOTROPE
                 ENDOMMAGEMENT DAMAGE_TC ;

* aciers

MODA = MODE (DH ET DV) MECANIQUE ELASTIQUE ISOTROPE
                PLASTIQUE ISOTROPE BARR ;

*
MODTOT = MODB et MODA;

* propriétés mécaniques

 yg = 22.e9 ;
 nub = 0.19 ;
 rhom = 2410 ;
 Gvalm = 300. ;
 ftulm = 3.3e6 ;
 redcm = 1.7e6 ;
 fc01m = -25.e6 ;
 rt45m = 1.18 ;
 fcu1m = -42.5e6 ;
 extum = -0.015;
 strpm = -22.e6 ;
 extpm = -0.001 ;
 ext1m = -0.006 ;
 str1m = -35.e6 ;
 ext2m = -0.008 ;
 str2m = -22.e6 ;
ncrim = 1 ;

jaco1 = jaco modb ;
jaco2 = chan 'RIGIDITE' modb jaco1 ;
hlenm = jaco2**(1./3.) ;

matb = mate modb  YOUN  yg NU nub RHO rhom HLEN hlenm
                   GVAL Gvalm FTUL ftulm REDC redcm FC01 fc01m
                   RT45 rt45m FCU1 fcu1m STRU extum EXTP extpm
                   STRP strpm EXT1 ext1m STR1 str1m EXT2 ext2m
                   STR2 str2m NCRI ncrim ;

* aciers renforcement

EA = 189274e6 ;
NUA = 0.3 ;

fe=554.e6;
ETa=3245.e6;

epsel=fe/Ea;
epsfin=2.7e-2 .;
leps=prog 0. epsel epsfin;
sigfin=fe + (eta*(epsfin - epsel));
lsig=prog 0. fe sigfin;
evsig=evol manu leps lsig;


MATA = MATE MODA YOUN EA NU NUA RHO 7850
                   TRAC evsig SECT 7.85e-5  ;


*
MATOT = MATB et MATA;

*******************************************************
* CONDITIONS AUX LIMITES
*******************************************************
p1 = vt poin proc (0 0 0);
p2 = vt poin proc (1 0 0);
p3 = vt poin proc (1 0 1);
sb = vt poin plan p1 p2 p3 1e-4;

p1 = vt poin proc (p1 plus (0 1 0));
p2 = vt poin proc (p2 plus (0 1 0));
p3 = vt poin proc (p3 plus (0 1 0));
sh = vt poin plan p1 p2 p3 1e-4;

pp1 = vt poin proc (0 0 0);
pp2 = vt poin proc (1 0 0);
pp3 = vt poin proc (0 4 0);
ss = vt poin plan pp1 pp2 pp3 1e-4;

cl1 = BLOQ sh ux;
cl2 = BLOQ sb ux uy;
cl3 = BLOQ sh uy ;
cl4 = BLOQ ss uz ;
cl5 = RELA ACCRO (DV ET DH) VT  1.e-4 ;


cltot = cl1 et cl2 et cl3 et cl4 et cl5;


* chargement en déplacement

FO1 = DEPI cl1 (1) ;

list1 = prog 0 1;
list2 = prog 0 1.5e-3 ;

evol1 = evol manu list1 list2 ;

char1= CHAR MECA FO1 evol1;

*******************************************************
* RESOLUTION : PASAPAS
*******************************************************

listt = prog 0 pas 0.05 1 ;

tab1 = TABLE;

tab1 . MODELE = modtot ;

tab1 . CARACTERISTIQUES = matot ;

tab1 . CHARGEMENT = char1 ;

tab1 . BLOCAGES_MECANIQUES = cltot ;

tab1 . TEMPS_CALCULES = listt ;

tab1 . MOVA = 'RIEN' ;


PASAPAS tab1 ;

*******************************************************
* POST-TRAITEMENT AVEC OUVCOR
*******************************************************

* initou
tab1.geo = vt;
tab1.poi = vt poin proc (0. 0. 0.);
tab1.hor = vrai;
tab1.pla = 'XY';
tab1.pas = 20;
tab1.lh = 2.;
tab1.lb = 0.;
tab1.lg = 0.;
tab1.ld = 4.;
tab1.crito = 1.e-9;
tab1.critp = 1.e-5;

initou tab1;


* zonfis
tab1.droi = faux;
hau = 2.;
bas = 0.;
nha = 4;
nba = 2;
pha = 1;
pba = 1;
gau = -0.05;
dro = 0.05;
alpha = 0.;
zonfis tab1 pha pba nha nba hau bas gau dro alpha;


* postou
dist = 0.03;
tab1.critt = 0.5;
*postou tab1 dist;

fin;







