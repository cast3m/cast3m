* fichier :  test_cisailnl.dgibi
************************************************************************
* Section : Mecanique Plastique
* Section : Mecanique Elastique
************************************************************************
********************************
*
* Cas test modele global cisail_nl
*
*  D. COMBESCURE - EMSI 1999
*
********************************
opti dime 2 elem seg2;
*
p0 = 0.  0.;
p1 = 0.  1.;
bar1 = p0 d 1 p1;
*
Long = 0.25;
Larg = 0.25;
*
younbar = 30000.;
*
nubar = 0.;
Gbar = younbar/(2.*(1+nubar));
*
Sbar = Larg*Long;
Ibar = (1./12.)*(Long**3)*Larg;
*
* Courbe contrainte de cisaillement - déformation
*
Fmax = Sbar*2.1;
UXX1 = prog 0. (0.5*Fmax/(Gbar*Sbar)) (2.*Fmax/(Gbar*Sbar))
       (10.*Fmax/(Gbar*Sbar));
FXX1 = prog 0. (0.5*Fmax/Sbar) (Fmax/Sbar) (1.2*Fmax/Sbar);
DXFXTh = evol manu UXX1 (Sbar*FXX1);
*
DMAXP = 1. - ((extr FXX1 3 )/(Gbar*(extr UXX1 3 )));
DMAXN = DMAXP;
DELAP = (extr UXX1 2);
DELAN = DELAP;
Efiss = (1. - DMAXP)*Gbar;
*
XMONOP = PROG;
YMONOP = PROG;
j0 = 2;
repeter LAB1 ((DIME FXX1) - 2 );
   j0=j0+1;
   YY = (extr FXX1 j0);
   XX = (extr UXX1 j0) - ((extr FXX1 j0)/Efiss);
   XMONOP = INSE XMONOP (j0 - 2) (maxi (prog XX 0.));
   YMONOP = INSE YMONOP (j0 - 2) YY;
fin lab1;
MONOP = EVOL MANU XMONOP (Sbar*YMONOP);
MONON = EVOL MANU XMONOP (Sbar*YMONOP);
*
modbar1 = mode bar1 mecanique elastique plastique cisail_nl timo;
matbar1 = mate modbar1 YOUN younbar NU nubar SECT Sbar
     INRZ Ibar DELP delap DMAP dmaxp DELN delan DMAN dmaxn
     BETA 0.2 ALFA 0.5 TETA 1.  MONP monop MONN monon
     SECY Sbar RHO 0.;
*
BL0 = BLOQ DEPL ROTA p0;
BL1 = BLOQ ROTA p1;
BL2 = BLOQ UX p1;
DEPP1 = DEPI BL2 1.;
Protemp = prog 0. 10. 20. 101.;
ProHori = PROG 0. 0.001 -0.001 0.;
Procalc = PROG 0. pas 0.25 10.;
CharHori = CHAR DIMP DEPP1 (evol manu Protemp ProHori);
*
TAB1 = TABLE;
TAB1 . 'MOVA'          = 'RIEN';
TAB1 . 'TEMPS_CALCULES' = ProCalc ;
*TAB1 . 'CONVERGENCE_FORCEE ' = 'FAUX';
TAB1 .'MAXITERATION'  = 50;
*TAB1.'PRECISION' = 1.D-5;
TAB1 .'CHARGEMENT' = Charhori;
TAB1 .'DYNAMIQUE' = faux;
TAB1 .'MODELE' = modbar1;
TAB1 .'CARACTERISTIQUES'= matbar1;
TAB1 .'BLOCAGES_MECANIQUES' = BL0 ET BL1 ET BL2;
*
PASAPAS TAB1;
*
*
DEPT = TAB1.DEPLACEMENTS;
CONT = TAB1.CONTRAINTES;
TIME = INDEX DEPT;
DIMT = DIME TIME;
*
DEPX = PROG;
GXYT = prog;
EFFX = prog;
*
EFFXXX = PROG;
REPETER BOU1 (DIMT - 1);
  DEP1 = DEPT.(TIME.(&BOU1 + 1));
  CON1 = CONT.(TIME.(&BOU1 + 1));
  FOR1 = REAC (BL0 et BL1 et BL2) DEP1;
  DEPX = DEPX ET (PROG  (extr DEP1 UX p1));
  EFFX = EFFX ET (PROG (extr FOR1 FX p0));
FIN BOU1;
*
DXFX = EVOL MANU DEPX ((-1.)*EFFX);
*
dreal = extr (ipol (prog 0.0005) DEPX ((-1.)*EFFX)) 1;
dtheo =  extr (ipol (prog 0.0005) UXX1 (Sbar*FXX1)) 1;
prec = abs (dreal - dtheo);
*
xlim0 = 1.D-7;
SI (prec > xlim0);
  'ERRE' 5;
SINON;
   'ERRE' 0;
FINSI;
*
FIN;
*










