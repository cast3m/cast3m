* fichier :  rupt9.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*           Test Rupt9.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
****************************************************
*                                                  *
*   VALIDATION DES PROCEDURE G_THETA ET T_PITETA   *
*     D'UNE PLAQUE EN TRACTION PURE. SOLUTION      *
*  DE REFERENCE : Compendium of STRESS INTENSITY   *
*  FACTORS by Rooke & Cartwright.                  *
*                                                  *
*                                                  *
*  GEOMETRIE :                                     *
*                                                  *
*  longueur de la plaque     : 250. MM             *
*  largeur de la plaque      : 100. MM             *
*  lonqueur de la fissure    : 50.  MM             *
*  Inclinaison de la fissure : 45.0 degrés         *
*                                                  *
*  MATERIAU :                                      *
*                                                  *
*  Module d'Young         : 2.E5 MPA               *
*  Coefficient de poisson : 0.3                    *
*                                                  *
*  CHARGEMENT :                                    *
*                                                  *
*  Traction pure          : 1.0 daN/MM**2          *
*                                                  *
*  SOLUTION ANALYTIQUE :                           *
*                                                  *
*  Les facteurs d'intensité de contraintes         *
*                K1 = 150.37 daN/mm**(-3/2)        *
*                K2 = -71.4 daN/mm**(-3/2)         *
*                                                  *
****************************************************
'OPTION' 'DIME' 2 'ELEM' 'QUA8'
         'MODE' 'PLAN' 'DEFO'
         'ECHO'  1 ;
*** Erreur géométrique pour les éliminations
*** Plus petite que la fissure !
egeom = 1.D-6 ;
***
*** DD : Ouverture initiale de la fissure
***
DD = 1.D-3 ;
ddsur2 = DD '/' 2.0D0 ;
p11 = (125.D0 '-' ddsur2) 0.D0 ;
p12 = (125.D0 '+' ddsur2) 0.D0 ;
p0  = 25.D0  0.D0 ;
p2  = 275.D0 0.D0 ;
p3  = 25.D0  100.D0 ;
ppp = 125.D0 0.D0 ;
L = 50.D0 ;  theta = 45.D0 ;
cth = 'COS' theta ;
sth = 'SIN' theta ;
tth = sth '/' cth ;
pf1 = ppp 'PLUS' (L '*' (cth sth)) ;
p4  = ((100.D0 '/' tth) '+' 125.D0)  100.D0 ;
p5  = 275.D0 100.D0 ;

*******************************************************
*          CONSTRUCTION DU MAILLAGE                   *
*        AUTOUR DE LA POINTE DE FISSURE               *
*******************************************************
pd = 'PROG' 1.D0  2.5D0 5.D0  9.D0  13.D0 19.D0 25.D0 ;
cim11 = pf1 ;  cim2 = pf1 ;  cim3 = pf1 ;
cim12 = pf1 ;
plfim1 = pf1 ;  plfim2 = pf1 ;  pim3 = pf1 ;

*******************************************************
'REPETER' bbbb 7 ;
  v = 'EXTRAIRE' pd &bbbb ;
  dd1 = (v '/' L) '*' ddsur2 ;
  xx1 = v '*' cth ;  mxx1 = (-1.0D0 '*' xx1) ;
  yy1 = v '*' sth ;
  xx2 = dd1 '*' sth '*' sth ;
  yy2 = dd1 '*' sth '*' cth ;  myy2 = (-1.0D0 '*' yy2);
  pi1 = pf1 'MOIN' (xx1  yy1) ;
  pi2 = pf1 'PLUS' (mxx1 yy1) ;
  pi3 = pf1 'PLUS' (xx1  yy1) ;
  pi4 = pf1 'MOIN' (mxx1 yy1) ;
  plfi1 = pi1 'MOIN' (xx2 myy2) ;
  plfi2 = pi1 'PLUS' (xx2 myy2) ;
  'SI' (&bbbb '>EG' 6) ;
    n = 4 ;
  'SINON'  ;
    n = 2 ;
  'FINSI' ;
*******************************************************
  ci11 = 'CERCLE' n plfi1 pf1 pi2   ;
  ci2  = 'CERCLE' n pi2   pf1 pi3   ;
  ci3  = 'CERCLE' n pi3   pf1 pi4   ;
  ci12 = 'CERCLE' n pi4   pf1 plfi2 ;
  sui11 = 'COUTURE' cim11 ci11 ;
  sui2  = 'COUTURE' cim2  ci2  ;
  sui3  = 'COUTURE' cim3  ci3  ;
  sui12 = 'COUTURE' cim12 ci12 ;
  'ELIMINATION' egeom (sui11 'ET' sui2) ;
  'ELIMINATION' egeom (sui12 'ET' sui3) ;
*******************************************************
  cim11 = ci11 ;  cim2 = ci2 ;  cim3 = ci3 ;
  cim12 = ci12 ;  plfim1 = plfi1 ;
  plfim2 = plfi2 ;  pim3 = pi3 ;
  sut1 = 'ET' sui11 sui2  ;
  sut2 = 'ET' sui3  sui12 ;
  'SI' (&bbbb 'EGA' 1) ;
    sutt1 = sut1 ;
    sutt2 = sut2 ;
  'SINON' ;
    sutt1 = 'ET' sutt1 sut1 ;
    sutt2 = 'ET' sutt2 sut2 ;
  'FINSI' ;
'FIN' bbbb ;
'OUBLIER' cim11  ;  'OUBLIER' cim2   ;  'OUBLIER' cim3;
'OUBLIER' cim12  ;
'OUBLIER' plfim1 ;  'OUBLIER' plfim2 ;  'OUBLIER' pim3;
'MENAGE' ;

*********************************
*  CONSTRUCTION DU MAILLAGE TOTAL
*********************************

lg   = 'DROIT'  6  p0  p3 ;
ld   = 'DROIT'  6  p2  p5 ;
lhc  = 'DROIT' -5  p4  pi3 'DINI' 65 'DFIN' 50 ;
lig1 = 'ET' lg   ('ET' ('DROIT' 13 p3 p4)  lhc) ;
lig1 = 'ET' lig1 ('INVERSE' ('ET' ci2 ci11)) ;
lig1 = 'ET' lig1 ('ET' ('DROIT' 3 plfi1 p11)
                       ('DROIT' 6 p11   p0)) ;
su1  = 'SURFACE' lig1 'PLANE' ;
obj1 = 'ET' su1 sutt1 ;
*
lig2 = 'ET' ld   ('ET' ('DROIT' 3 p5 p4) lhc) ;
lig2 = 'ET' lig2 ('ET' ci3 ci12) ;
lig2 = 'ET' lig2 ('ET' ('DROIT'  3 plfi2 p12)
                 ('DROIT' -9 p12   p2 'DINI' 40
                                      'DFIN' 50)) ;
su2  = 'SURFACE' lig2 'PLANE' ;
obj2 = 'ET' su2 sutt2 ;
*
obj  = 'ET' obj1 obj2 ;
'ELIMINATION' egeom obj ;
cobj = 'CONTOUR' obj ;
*
* Levres de la fissure
*
ls = cobj 'ELEM' 'COMP' p11 pf1 ;
li = cobj 'ELEM' 'COMP' pf1 p12 ;
*-----------DEFINITION DU MATERIAU-------------------;
objmod = 'MODELISER' obj 'MECANIQUE' 'ELASTIQUE'
         'ISOTROPE' ;
objmat = 'MATERIAU'  objmod 'YOUN' 2.D5 'NU  ' 0.3D0 ;
*-----------MATRICE DE RIGIDITE----------------------;
rig = 'RIGIDITE' objmat objmod ;
*--------------CONDITIONS AUX LIMITES----------------;
cal = 'ET' ('BLOQUE' 'UX' 'UY' p0)
           ('BLOQUE'      'UY' p2) ;
rigtot = 'ET' rig cal ;
*-----------DEFINITION DU CHARGEMENT-----------------;
f1 = 'PRES' 'MASS' objmod -10.D0 lg ;
f2 = 'PRES' 'MASS' objmod -10.D0 ld ;
f0 = 'ET' f1 f2 ;
*-----------RESOLUTION ET CONTRAINTES----------------;
u0 = 'RESO' rigtot f0 ;
sig0 = 'SIGMA' objmod objmat u0 ;

**********************************************
** fabrication de déplacements THETA et PI ***
**********************************************

SUPTAB = 'TABLE' ;
SUPTAB . 'OBJECTIF' = 'MOT' 'DECOUPLAGE' ;
SUPTAB . 'LEVRE_SUPERIEURE' = ls ;
SUPTAB . 'LEVRE_INFERIEURE' = li ;
SUPTAB . 'FRONT_FISSURE' = pf1 ;
SUPTAB . 'MODELE' = objmod ;
SUPTAB . 'CARACTERISTIQUES' = objmat ;
SUPTAB . 'SOLUTION_RESO' = u0 ;
SUPTAB . 'CHARGEMENTS_MECANIQUES' = f0 ;
SUPTAB . 'COUCHE' = 6 ;
G_THETA SUPTAB ;

**********************************************
************ Affichage des résultats *********
**********************************************

K1 = SUPTAB . 'RESULTATS' . 'I' ;
K2 = SUPTAB . 'RESULTATS' . 'II' ;
'MESSAGE'  'K1 = ' K1 'K2 = ' K2 ;
ER1 = 'ABS' (((K1 '-' 150.37D0) '/' 150.37D0) '*'
      100.D0) ;
ER2 = 'ABS' (((K2 '+'   71.4D0) '/'   71.4D0) '*'
      100.D0) ;
'MESSAGE' 'ER1 = ' ER1 'ER2 = ' ER2 ;
'SI'  ((ER1 '>' 2.D0) 'OU' (ER2 '>' 2.D0)) ;
  'ERREUR' 5 ;
'SINON' ;
  'ERREUR' 0 ;
'FINSI' ;
'FIN' ;























