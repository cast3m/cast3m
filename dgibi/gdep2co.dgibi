* fichier :  gdep2co.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*
*  portes tournant autour d'un axe
*
 opti dime 2 elem  TRI3 ; dens 4;
 pa = 0 0 ; pb = 4 0 ; liab = pa d pb;
 suto = liab trans  4 ( 0 10);
pq =   cote 3 suto  ;
opti dime 3;
mo= suto  model mecanique elastique  dkt;
EP = 1.;
ma= mater mo  young 10000 nu 0.3     epai EP;
bli = bloq  pa UX  ;
blf = bloq liab   UY UZ  ;
blro = bloq suto RY RZ ; bl = bli et blf et  blro;
bly  = bloq pq uy;
blz  = bloq  pq  uz;
ang = 11.25;
gtz =10. *( prog   0. ( sin ang ) ( sin ( 2. * ang)  )
 ( sin ( 3. * ang) )
 ( sin ( 4. * ang))  ( sin ( 5. * ang))  ( sin ( 6. * ang))
 ( sin ( 7. * ang))  ( sin ( 8. * ang))  ( sin ( 9. * ang))
 ( sin ( 10. * ang)) )   ;

gty =10. *( prog   0. ( cos ang - 1.) ( cos ( 2. * ang) - 1.)
 ( cos ( 3. * ang) - 1.)
 (cos (4.*ang) - 1.) (cos (5.*ang) - 1.) (cos (6.*ang) - 1.)
(cos (7.*ang) - 1.) (cos (8.*ang) - 1.) (cos (9.*ang) - 1.)
 (cos (10.*ang) - 1.) )  ;
fz = depimp blz 1.  ;
fy = depimp bly 1.;
ft = prog 0. 1. 2. 3. 4. 5. 6. 7. 8. 9. 10.;
evz = evol manu abs ft ord gtz;
evy = evol manu abs ft ord gty;
chz = charge dimp fz evz;chy = charge dimp fy evy;
ch = chz et chy;
tb = table;
tb . grands_deplacements = vrai;
tb . blocages_mecaniques =  blz et bly et bl;
*tb . automatique = vrai; tb . autocrit = 100000;
lit = prog 1. 2. 3. 4.  ;
tb . chargement = ch;
tb . modele = mo;
tb . caracteristiques = ma;
tb . temps_calcules = lit;
pasapas tb;
co1 = tb . contraintes . 4;
cma = maxi co1;
mess ' contrainte maximum ' cma;
si ( cma  >  1.e-2);
       erre  5;
finsi;
mess '************* test correct  **************';
fin;








