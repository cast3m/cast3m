* fichier :  ddi.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*             Test Ddi.dgibi: Jeux de données         *
*             -------------------------------         *
*                                                     *
OPTI ECHO 0 DIME 2 ELEM QUA8 MODE AXIS;
*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*        MATERIAU VISCO-PLASTIQUE ENDOMMAGEABLE       *
*     MODELE A DEUX DEFORMATIONS INELASTIQUES (DDI)   *
*                                                     *
*  MAILLAGE:                                          *
*     EPROUVETTE CYLINDRIQUE (AXISYMETRIQUE)          *
*                                                     *
*  CHARGEMENT:                                        *
*     DEPLACEMENT IMPOSE MONOTONE CROISSANT           *
*******************************************************
*
*
   P1 = 0 0;          P2 = 5E-3 0;
   P3 = 5E-3 50E-3;   P4 = 0 50E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 1 ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 1 ;
*
* Maillage
* --------
   EPROU = L1 L2 L3 L4 DALLER PLAN ;
*
* Conditions aux limites
* ----------------------
   CL1 = BLOQ L1 UZ ;
   CL2 = BLOQ L3 UZ;
   CL3 = BLOQ L4 UR ;
   CL = CL1 ET CL2 ET CL3 ;
*
* Modele de calcul
* ----------------
mo = 'MODE' eprou 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
       'VISCOPLASTIQUE' 'DDI' ;
*
* Materiau: Zirconium alpha 200°C
* -------------------------------
ma = 'MATE' mo 'YOUN' 8.2D10 'NU' 0.36
       'RP0' 135.D6 'QP' -60.D6 'BP' 120
       'CP1' 34000.D6 'DP1' 250.
       'CP2' 60000.D6 'DP2' 3000. 'KS' 960.D6 'N' 3.4
       'RV0' 70.D6 'QV' -20.D6 'BV' 10.
       'CV1' 24000.D6 'DV1' 300.
       'CV2' 9000.D6 'DV2' 3000. 'CVP1' 0. 'CVP2' 0.;
*
* Temps
* -----
   EPSMAX=0.01;
   EPSPT=0.001;
   NCALC=20;
   TFIN=EPSMAX/EPSPT;
   DT0=TFIN/NCALC;
*
* Chargement
* ----------
   DEP0=DEPI CL2 (EPSMAX*0.05);
   LI1 = PROG 0. tfin ;
   LI2 = PROG 0. 1. ;
   EV = EVOL MANU T LI1 LOAD LI2 ;
   CHA = CHAR 'DIMP' DEP0 EV ;
*
* Calcul
* ------
   TAB = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL;
   TAB.'CARACTERISTIQUES' = MA;
   TAB.'MODELE' = MO;
   TAB.'CHARGEMENT' = CHA;
   LIS=PROG 0. pas dt0 tfin;
   TAB.'TEMPS_CALCULES' = LIS;
   PASAPAS TAB ;
*
*  Comparaison avec les valeurs de reference
*  -----------------------------------------
  dim0 = 'DIME' ( tab .'TEMPS' ) - 1;
  var0 = tab .'VARIABLES_INTERNES'. dim0 ;
  sigm0= tab .'CONTRAINTES'.dim0;
  p0 =  MAXI (EXCO var0 'VIPW');
  v0 =  MAXI ( EXCO var0 'V');
  sig0 = MAXI ('EXCO' sigm0 'SMZZ');
*
  p_th = 4.17896E-03;
  v_th = 3.14432E-03;
  sig_th = 2.19491E+08;
  errp = 100. * (ABS((p_th - p0)/p_th));
  errv = 100. * (ABS((v_th - v0)/v_th));
  errsig = 100. * (ABS((sig_th - sig0)/sig_th));
*
  SI ( (errp > 0.1) OU (errv > 0.1) OU (errsig > 0.1));
    ERRE 5 ;
  SINON;
    ERRE 0 ;
  FINSI;
*
FIN;










