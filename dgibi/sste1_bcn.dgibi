* fichier :  sste1_bcn.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
* VERTICAL MOVEMENT OF A PILE
* TEST: Rounded Hyperbolic Mohr-Coulomb model
*
*
*-------------  OPCIONES GENERALES  --------------------------------
*
GRAPH = 'N' ;
OPTION DIME 2 TRAC x ELEM qua4 MODE axis;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
*
*-------------  CREACION DE LA GEOMETRIA  -------------------------
*
p1 = 7.5 0.;
p2 = 345. 0.;
p3 = 345. 5.;
p4 = 7.5  5.;
npoi1=1;
l23 = d npoi1 p2 p3;
l41 = d npoi1 p4 p1;
xkk = 0.1*1.;
ykk = 40.*1.;
*xkk = 10.*1.;
*ykk = 100.*1.;
l12 = d p1 p2 dini xkk dfin ykk;
l34 = d p3 p4 dini ykk dfin xkk;
l43 = inve l34;
malla = daller l12 l23 l34 l41;
*
*--------------  MODELO  ------------------------------------------
*
 E_ELAS = 1040.;
 NUHOR  = 0.3;
 COHES  = 1.;
* 30 grados
 ANGFR  = 0.52359878D0;
 MOD1   =  modeli malla mecanique elastique plastique rh_coulomb;
 MAT1   =  mater mod1 youn E_ELAS nu NUHOR cohe COHES  phi ANGFR;
*
*--------------  MATRICES DE RIGIDEZ  -----------------------------
*
 rig1 = rigi mod1 mat1;
*
*--------------  CONDICIONES DE CONTORNO  -------------------------
*
 rigcont1 = (bloq uz l23) et (bloq ur (l23 et l41));
 rig1b    = rig1 et rigcont1;
*
 rigco2a  = rela ur l12 - ur l43;
 rigco2b  = rela uz l12 - uz l43;
 rigcont2 = rigco2a et rigco2b;
 rigcont  = rigcont1 et rigcont2;
 rig1     = rig1b et rigcont2;
*
*--------------  MOVIMIENTO IMPUESTO  -----------------------------
*
 rigsupe = (bloq uz l41);
 valor   = 0.2D0;
 movyy   = (depi RIGsupe valor);
 evol1   = EVOL MANU t (PROG 0. 1.) level (PROG 0. 1.);
 evfut   = CHAR DIMP evol1 movyy;
*
*--------------  RESOLUCION  --------------------------------------
*
 maxiter = 15;
 t0 = table;
 t0.MODELE              = mod1;
 t0.CARACTERISTIQUES    = mat1;
 t0.BLOCAGES_MECANIQUES = rigcont et rigsupe;
 t0.CHARGEMENT          = evfut;
 t0.ACCELERATION        = maxiter;
 t0.MAXITERATION        = maxiter;
 t0.PRECISION           = 1.E-10;
 t0.CONVERGENCE_FORCEE  = faux;
 t0.K_TANGENT           = vrai;
 t0.SUBSTEPPING         = vrai;
 t0.NMAXSUBSTEPS        = 1000;
 t0.PRECISINTER         = 1.E-12;
 t0.NITERINTER_MAX      = 5;
 t0.TEMPS_CALCULES      = prog 0. pas 0.3 0.3 pas 0.05 1.;
 t0.TEMPS_SAUVES        = t0.TEMPS_CALCULES;
 pasapas t0;
*
*--------------  POSTPROCESO  -------------------------------------
*
 imax   = (dime t0.TEMPS_SAUVES) - 1;
 fuer   = prog 0.;
 i      = 1;
 repeter blocdefi imax;
   aux  = reac rigsupe t0.deplacements.i;
   aux  = resu aux;
   pbas = extr aux MAIL ;
   pbas = POIN 1 pbas ;
   aux  = extr aux fz pbas;
   fuer = fuer et (prog aux);
   i=i+1;
 fin blocdefi;
 law = evol manu t t0.TEMPS_SAUVES f(t) fuer;
 SI (NEG GRAPH 'N') ;
   dessin law;
 FINSI ;
 err = (aux - 4.71961E+02)/4.71961E+02 ;
 list (aux - 4.71961E+02);
 list err;
 SI ((ABS err) < 0.5e-5) ;
   ERRE 0 ;
 SINON ;
   ERRE 5 ;
 FINSI ;
 FIN;









