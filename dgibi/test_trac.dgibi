* fichier : test_trac.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************

* Teste TRAC avec les nouvelles couleurs
*---------------------------------------

* Mettre GRAPH a VRAI si trace en interactif, sinon trace en Postscript
GRAPH = FAUX ;
'SI' ('NON' GRAPH) ;  OPTI 'TRAC' 'PSC' ;  'FINSI' ;

OPTI 'DIME' 2 'ELEM' 'QUA4' ;

p1 = 0 0 ;
p2 = 1 0 ;
p3 = 1 1 ;
p4 = 0 1 ;

*-- teste l'option 'COUL'
OPTI 'COULEUR' 'BLEU' ;
mesh = 'MANU' 'QUA4' p1 p2 p3 p4 ;
TRAC mesh 'COULEUR' 'BLEU' 'NCLK'
'TITRE' 'on doit voir un carre bleu';

*-- teste l'une des nouvelles couleurs
TRAC ('COULEUR' mesh 'ORAN') 'NCLK'
'TITRE' 'on doit voir un carre orange';

*-- teste les isovaleurs
bchpo = 'COOR' 1 mesh ;
TRAC bchpo mesh 'NCLK'
'TITRE' 'on doit voir un dégradé surfacique';
* le long d'une ligne
TRAC bchpo ('CHANGER' mesh 'LIGNE') 'NCLK'
'TITRE' 'on doit voir un dégradé le long des lignes';
* avec un nombre inférieur à 16
TRAC bchpo mesh ('PROG' 0 'PAS' .1 1.) 'NCLK' ;
* avec un nombre inférieur à 8
TRAC bchpo mesh ('PROG' 0 'PAS' .2 1.) 'NCLK' ;
* avec des lignes
OPTI 'ISOV' 'LIGNE' ;
TRAC bchpo mesh 'NCLK'
'TITRE' 'avec courbes isovaleurs';

*-- teste les vecteurs (en 3D car ET foire en 2D)
OPTI 'DIME' 3 ;
OPTI oeil (0. 0. 1.d30) ;
licoul = 'MOTS' 'DEFAUT' 'BLEU' 'ROUGE' 'ROSE' 'JAUNE' 'VERT' 'TURQ'
              'AZUR' 'ORANGE' 'VIOLET' 'OCEAN' 'CYAN' 'OLIVE' 'GRIS';
ncoul  = 'DIME' licoul ;
'REPETER' bcl ncoul ;
  i     = &bcl ;
  icoul = 'EXTR' licoul i ;
  ang   = (i-1.) * 360. / ncoul ;
  c     = 'MANU' 'CHPO' mesh 3 'UX' (('COS' ang))
                            'UY' (('SIN' ang)) 'UZ' 0.;
  v     = 'VECTEUR' 0.1 c 'UX' 'UY' 'UZ' icoul ;
  'SI' (i 'EGA' 1) ;
     vtot = v ;
  'SINON' ;
     vtot = vtot 'ET' v ;
  'FINSI' ;
'FIN' bcl;

'TITRE' 'on doit voir plein de vecteurs colorés en cercle';
TRAC mesh vtot ;

*-- teste les déformées
defanim = ANIME 20 mesh ('NOMC' bchpo 'UX') 1. ;
*bp TRAC defanim 'NCLK'
*bp TRAC defanim
*bp 'TITRE' 'on doit voir toutes les amplitudes jusqu a 1';
TRAC defanim 'ANIME'
'TITRE' 'clic sur animation : visu deformees jusqu a 1';
OPTI 'ISOV' 'SURFACE' ;
defanim = ANIME 3 mesh ('NOMC' bchpo 'UX') 1. bchpo ;
*bp TRAC defanim 'NCLK'
TRAC defanim 'ANIME'
'TITRE' 'ok ?';

*-- teste les déformées multiples avec/sans isovaleurs
d12 = droi p1 10 p2;
ychpo = (sin (180.* (coor d12 1))) NOMC 'UY';
amp1 = 0.5;
def1 = defo d12 ychpo amp1 ychpo;
def0 = defo d12 (0.*ychpo) 'GRIS';
def05 = defo d12 (0.5*ychpo) (0.5*amp1) (0.5*ychpo);
def08 = defo d12 (0.8*ychpo) (0.8*amp1) 'GRIS';
trac (def0 et def05 et def08 et def1)
TITRE '0 et 0.8 en gris - 0.5 et 1 avec isovaleurs';

*-- teste les déformées avec vecteur
vec1 = VECT ychpo amp1 'DEPL';
def00 = defo d12 (0.*ychpo) amp1 vec1 'GRIS';
trac (def00 et def1)
TITRE '0 gris + vecteur bleu = 1 avec isovaleurs';

* opti donn 5 ;
'FIN' ;








