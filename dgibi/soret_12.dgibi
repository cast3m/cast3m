* fichier :  soret_13.dgibi
************************************************************************
* Section : Diffusion Soret
* Section : Diffusion Fick
************************************************************************

************************************************************************
*                        CAS TEST soret_13.dgibi                       *
************************************************************************
*   Test effet Soret
* - 3D TRID
* - ELEMENTS FINIS TESTES : 'PRI6'
* - Regime permanent
* - C(Z0) = C0
* - C(Z1) = C1
* - GRAD(T) choisi lineaire (T=A*(Z**2) / 2.D0   ==> GRAD(T)= A*Z)
* - CONDUCTION et SORET sont en concurrence
************************************************************************


************************************************************************
* OPTIONS & REGLAGES
************************************************************************
  GRAPH = 'N' ;
 'OPTI' 'DIME' 3 ;
 'OPTI' 'ELEM' 'TRI3'  ;

 'SI' ('NEG' GRAPH 'N');
   'OPTI' 'TRAC' 'X';
 'SINO';
   'OPTI' 'TRAC' 'PSC';
 'FINS';

  NBE1 =  5   ;
  NBE2 =  5   ;
  NBE3 = 15   ;

  K    = -7.D0;
  A    =  1.D0;

  Z0   = -0.5 ;
  Z1   =  1.  ;

  C0   = 1.   ;
  C1   = 2.   ;

************************************************************************
* MAILLAGE
************************************************************************
  P1 = 0. 0. Z0;
  P2 = 0. 1. Z0;
  P3 = 1. 1. Z0;
  P4 = 1. 0. Z0;

  L1 ='DROI' NBE1 P1 P2;
  L2 ='DROI' NBE1 P4 P3;

  S1 ='REGL' NBE2 L1 L2;
  S2 = S1 'PLUS' (0. 0. (Z1 - Z0));
  L4 ='COTE' 4 S2;
  P5 ='POIN' L4 'FINAL';

 'OPTI' 'ELEM' 'PRI6';
  V1  ='VOLU' NBE3 S1 S2 ;
  ENV1='ENVE' V1;
  ARE1='ARET' ENV1;
  PAR1='CHAN' 'POI1' ARE1 ;
  M1  ='POIN' PAR1 'DROIT' P1 P5 1.D-5;

  L6  = 'ELEM' ARE1 'APPU' 'STRI' M1;
 'TRAC' 'QUAL' ENV1;
 'TRAC' 'QUAL' ARE1;

************************************************************************
* MODELE & MATERIAU
************************************************************************
  MOD1 ='MODE'  V1   'DIFFUSION' 'FICK' 'INCO' 'HI  ' 'QHI ';
  MAT1 ='MATE'  MOD1 'KD  ' 1.D0 ;
  MAT2 ='MANU' 'CHML' MOD1 'SCAL' K 'RIGIDITE' ;

************************************************************************
* Champ de temperature pour que le GRAD(T) soit constant
************************************************************************
  COO3  ='COOR' 3 V1 ;
  EVO1  ='EVOL' 'BLEU' 'CHPO' COO3 'SCAL' L6  ;
  LRZ   ='EXTR'  EVO1  'ORDO' 1 ;
  TEMP1 ='CHAN' 'COMP' (A / 2.D0 * (COO3 ** 2)) 'T' ;
 'TITR' 'Evolution de la TEMPERATURE avec (abscisse curviligne)';
  EVOT  ='EVOL' 'BLEU' 'CHPO' TEMP1 'T' L6  ;
 'DESS' EVOT ;

************************************************************************
* RIGIDITE
************************************************************************
  RIG1 ='COND' MOD1 MAT1;
  RIG2 ='SORE' MOD1 MAT1 MAT2 TEMP1;

************************************************************************
* CONDITIONS AUX LIMITES
************************************************************************
  CLIM1 ='BLOQ' 'HI'  S1   ;
  CLIM2 ='BLOQ' 'HI'  S2   ;


  CIMP1 ='DEPI' CLIM1 C0   ;
  CIMP2 ='DEPI' CLIM2 C1   ;

************************************************************************
* RESOLUTION
************************************************************************
  SOL1  ='RESO' (RIG1 ET RIG2 ET CLIM1 ET CLIM2) (CIMP1 ET CIMP2);
 'TITR' 'Isovaleurs de concentration en HI';
 'TRAC' SOL1 V1;

************************************************************************
* POST-TRAITEMENT
************************************************************************
* Solution NUMERIQUE
  EVONUM ='EVOL' 'ROUG' 'CHPO' SOL1 'HI  ' L6  ;
  LCNUM  ='EXTR' EVONUM 'ORDO' 1 ;
  EVONUM ='EVOL' 'ROUG' 'MANU' 'X' LRZ 'HI' LCNUM;

* Solution ANALYTIQUE
  EVO2   ='EVOL' 'CHPO' TEMP1 'HI' L6  ;
  FLO1 = ((-2*K*A)**0.5)/2. ;
  XDENO= ( 'EXP'(K/2.*A*(LRZ**2))            ) *
         (('ERF'(FLO1*Z1)) - ('ERF'(FLO1*Z0)));
  LNUME= (('ERF'(FLO1*LRZ))*C1*('EXP'(K/2.*A*(Z1**2))))-
         (('ERF'(FLO1*LRZ))*C0*('EXP'(K/2.*A*(Z0**2))))+
         (('ERF'(FLO1*Z1 ))*C0*('EXP'(K/2.*A*(Z0**2))))-
         (('ERF'(FLO1*Z0 ))*C1*('EXP'(K/2.*A*(Z1**2))));

  LCANA  =  LNUME / XDENO;

  EVOANA ='EVOL' 'BLEU' 'MANU' 'X' LRZ 'HICA' LCANA;
 'DESS' (EVONUM ET EVOANA);

* Comparaison
  LCDIFF = LCNUM - LCANA ;
 'TITR' 'Difference entre la Solutions NUMERIQUE et ANALYTIQUE';
  EVODIF ='EVOL' 'VERT' 'MANU' 'X' LRZ 'HICA' LCDIFF;
 'DESS' EVODIF;


* Critere d'erreur
 'OPTI' 'ECHO' 0;
  ERRABS ='MAXI' 'ABS' LCDIFF;
  ERRREL = ERRABS / (('MAXI' LCANA) - ('MINI' LCANA))      ;
 'MESS' '*********************************************' ;
 'MESS' '* Erreur MAXIMALE absolue  :' ERRABS '*';
 'MESS' '* Erreur MAXIMALE relative :' ERRREL '*';
 'MESS' '*********************************************' ;
 'SI' (ERRREL >EG 1.47D-2);
   'ERRE' 5;
 'FINS';

 'FIN';


