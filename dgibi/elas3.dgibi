* fichier :  elas3.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas3.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

*______________________________________________________
* TEST ELAS3
*
* Meme probleme que DFICH10 mais avec une extrémité
* libre au lieu d'etre encastrée (la géométrie est
* différente).
*
* Un cylindre est pincé à une extrémité, l'autre reste
* libre.
*
* Pour des raisons de symétrie seul 1/4 de la structure
* est discrétisé.
* Les conditions aux limites respectent cette symétrie.
*
* Une force ponctuelle est appliquée diamétralement.
*
* La déflexion suivant Z au point d'application de la
* force est comparée à la déflexion théorique (-0.452).
*______________________________________________________

OPTION DIME 3 ELEM TRI3 ;DENS 0.75 ;
TITR 'CYLINDRE PINCE A BORDS LIBRES' ;
TEMPS ;

*----------------------------------------------------
* DEFINITION DE LA GEOMETRIE
*----------------------------------------------------
P1=4.953 0 0 ;P2=0 0 4.953 ;C1=0 0 0 ;VECT=0 5.175 0 ;
LIG1=C   P1 C1 P2 ;SURF=LIG1 TRAN    VECT ;
LIG1 LIG2 LIG3 LIG4=COTE SURF ;P3=LIG2 POIN FINA ;
LIST P3;

*----------------------------------------------------
* TRACE
*----------------------------------------------------
OEIL1 = 100 100 100;
SI(NEG GRAPH 'N');
   TRAC OEIL1 SURF 'QUAL';
FINSI;

*----------------------------------------------------
* DEFINITION DU MODELE ET DES CARACTERISTIQUES
*----------------------------------------------------

MOD1=MODE SURF MECANIQUE ELASTIQUE DKT;
MAT1=MATE MOD1 YOUN 10.5E6 NU 0.3125 RHO 1. EPAI 0.094 ;

*----------------------------------------------------
* CALCUL RIGIDITE ET MASSE ;
*----------------------------------------------------
*
RIG1=RIGI MOD1 MAT1;

*----------------------------------------------------
* CONDITIONS AUX LIMITES
*----------------------------------------------------
ENC1=BLOQ LIG4 UZ ;ENC2=BLOQ LIG4 RX ;ENC3=BLOQ LIG4 RY ;
ENC4=BLOQ LIG2 UX ;ENC5=BLOQ LIG2 RY ;ENC6=BLOQ LIG2 RZ ;
ENC7=BLOQ LIG3 UY ;ENC8=BLOQ LIG3 RX ;ENC9=BLOQ LIG3 RZ ;
ENC10=ENC1 ET ENC2 ET ENC3 ET ENC4 ET ENC5 ET ENC6 ET ENC7 ET ENC8
ET ENC9 ;

*----------------------------------------------------
* FORCE
*----------------------------------------------------
F1=0 0 -100 ;
EFOR=FORC F1 P3 ;
RIG2=RIG1 ET ENC10;

*----------------------------------------------------
* RESOLUTION
*----------------------------------------------------
DE1 = RESO RIG2 EFOR;
D3Z = EXTR DE1 UZ P3;

*----------------------------------------------------
* RESULTATS (isovaleurs de deplacement en Z)
*----------------------------------------------------
SAUT PAGE ;

SURF1= DEFO SURF DE1;
DZ   = EXCO DE1  UZ;
SI(NEG GRAPH 'N');
    TRAC OEIL1 'CACH' SURF1 DZ ;
FINSI;

MESS ' DEPLACEMENT EN Z THEORIQUE :   -0.452 ';
MESS ' DEPLACEMENT EN Z CALCULE   :   ' D3Z ;
TEMPS ;
*            CODE FONCTIONNEMENT
DZREF=-0.452;
RESI=ABS((D3Z-DZREF)/DZREF);
SI(RESI <EG 4E-4);
     ERRE  0;
SINO;
     ERRE  5;
FINSI;
FIN;




