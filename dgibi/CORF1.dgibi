* fichier :  CORF1.dgibi
OPTI DIME 3 ELEM TRI3 ;
P0 = 0. 0. 0. ;
P1 = 1. 0. 0. ;
L1 = P0 D 5 P1;
P2 = 0. 0. 1.;
P3 = 0. 1. 0. ;
L1 = CERC 5 P1 P0 P3 ;
OEIL = 1000. 1000. 1000.;
L2 = L1 TOUR 90. P0 P2 ;
L3 = L2 TOUR 90. P0 P2 ;
L4 = L3 TOUR 90. P0 P2 ;
L0 = L1 ET L2 ET L3 ET L4;
ELIM 0.001 L0;
S1 = SURF L0 'PLANE';
ELIM 0.001 L0 S1;
MOD1 = 'MODELE' S1 'MAGNETODYNAMIQUE'
 'POTENTIEL_VECTEUR' 'ISOTROPE' 'ROT3';
*OPTI DONN 5;
MAT1 = 'MATE' MOD1 'ETA' 1.E-4 'PERM' 1. 'EPAI' 1.;
M1 = 'MUTU' MOD1 MAT1 S1;
R1 = 'RESI' MOD1 MAT1 ;
*******************************
* CONSTITUTION DU SYSTEME *****
*******************************
* DONNEE DU CHAMP INUCTEUR : 100T/s ****
B1Z = MANU CHPO S1 1 SCAL 0.;
B2Z = MANU CHPO S1 1 SCAL 100.;
DTB=1.;
DBZDT = (B2Z-B1Z)/DTB;
X1 = (COOR 1 S1) ;
Y1 = (COOR 2 S1) ;
X2 = X1**2;
Y2 = Y1**2;
RAY = (X2+Y2)**0.5 ;
DAPHIDT = 0.5*RAY*DBZDT;
* ON PASSE EN CARTESIEN *
ANGL1 = 'ATG' Y1 X1 ;
PIS4 = 'ATG' 1. 1. ;
PIS2 = 2.*PIS4;
ANGL2 = ANGL1 + PIS2 ;
COSA1 = 'COS' ANGL2 ;
SINA1 = 'SIN' ANGL2 ;
DAXDT = DAPHIDT*COSA1;
DAXDT = EXCO DAXDT SCAL AX;
DAXDT = CHAN DAXDT ATTRIBUT NATURE DISCRET;
DAYDT = DAPHIDT*SINA1;
DAYDT = EXCO DAYDT SCAL AY;
DAYDT = CHAN DAYDT ATTRIBUT NATURE DISCRET;
DAZDT = MANU CHPO S1 1 AZ 0.;
DAZDT = CHAN DAZDT ATTRIBUT NATURE DISCRET;
DADT = DAXDT ET DAYDT ET DAZDT ;
VADT = VECT DADT AX AY AZ 0.005 ROUGE;
DADTN=CNEQ MOD1 DADT;
LIST DADT;
LIST DADTN;
* DONNEE DES OPERATEURS ********
TETA=0.5;
DT = 1.E-5;
TEMPS=0.;
OPE1 = M1 ET (R1*(TETA*DT)) ;
OPE2 = BLOQ 'FC' L0;
OPE3 = OPE1 ET OPE2;
SOU1 = M1 ET (R1*((TETA-1.)*DT)) ;
SOU2 = SOU1 ;
*SOU2 = SOU1 ET OPE2 ;
SOL1 = MANU CHPO S1 1 FC 0. ;
NDT = 1000;
NSORT = NDT/5 ;
REPE BOUC NDT;
IDT = &BOUC ;
TEMPS = TEMPS+DT;
SMB11 = SOU2*SOL1;
SMB12 = DT*DADTN;
SMB1 = SMB11 - SMB12;
SOL2 = RESO OPE3 SMB1;
SOL0 = OPE3*SOL2 ;
SOL1 = SOL2 ;
OUT = (IDT - (NSORT*(IDT/NSORT)));
SI ( OUT EGA 0);
MESS 'PAS DE TEMPS' IDT;
MESS '   TEMPS = ' TEMPS;
LIST SMB1;
LIST SOL1 ;
FINSI;
FIN BOUC;
MESS ' ******** INSTANT FINAL *******';
LIST SOL1 ;
CURR = DECO MOD1 MAT1 SOL1;
CURX = EXCO 'FC,X' CURR ;
CURX = CHAN 'CHPO' CURX MOD1;
CRX2 = CURX**2;
CURY = EXCO 'FC,Y' CURR ;
CURY = CHAN 'CHPO' CURY MOD1;
CRY2 = CURY**2;
CURZ = EXCO 'FC,Z' CURR ;
CURZ = CHAN 'CHPO' CURZ MOD1;
CRZ2 = CURZ**2;
CURP = CURX ET CURY ET CURZ ;
LIST CURP;
CURN = (CRX2+CRY2+CRZ2)**0.5;
JMAX = MAXI CURN;
LIST JMAX;
JREF = 0.42E6;
ERRJ = ABS(JREF-JMAX)/JREF;
LIST ERRJ;
SI (ERRJ > 0.02);
   ERREUR 5;
FINSI;
CURP = CURP/JMAX;
LIST CURP;
CURV = VECT CURP 'FC,X' 'FC,Y' 'FC,Z' 0.2 ROUGE;
*OPTI DONN 5;
FIN;



