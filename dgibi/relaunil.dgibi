* fichier :  relaunil.dgibi
************************************************************************
* Section : Langage Objets
* Section : Mecanique Elastique
************************************************************************
* test de trois poutres parralleles encastrées a une extremite
* ayant des relations de contact unilaterales avec jeux
*
*                      P |
*      |________________\|/_________________________
*      |        |                 |                 |
*      |     jeu 1               jeu 2            jeu 3
*      |________|________________ |_________________|
*      |        |                 |
*      |     jeu 4               jeu 5
*      |________|_________________|__________________
*      |                /|\
*      |               P |
*
*
*      |   10   |        20       |       20        |
*
*
* On pince les poutres exterieures a l'aide des forces P
*
*   Le jeu 3 se ferme en premier puis le jeu 5
* en continuant a charger le jeu 2 se ferme  puis le 3 se rouvre
* Le probleme redevient symetrique et les jeux 1 et 4 se ferment
* simultanenement
*



opti dime 3 elem seg2 mode trid echo 0;
rayon = 0.45 ;
d = 2*rayon ;
epais = 0.1 ;
*lcontact =
*
* Points...
*
p0 = 0. 6. 0. ;
p1 = 0. 3. 0. ;
p2 = 0. 0. 0. ;
*
p10= 10. 6. 0. ;
p11= 10. 3. 0. ;
p12= 10. 0. 0. ;
*
p30= 30. 6. 0. ;
p31= 30. 3. 0. ;
p32= 30. 0. 0. ;
*
p50= 50. 6. 0.;
p51= 50. 3. 0.;
p52= 50. 0. 0.;
*
* no of elements...
*
n1 = 1 ;
n2 = 2 ;
*
li01     = p0       droite n1 p10  ;
li02     = p10      droite n2 p30 ;
li03     = p30      droite n2 p50 ;
litot0   = li01 et li02 et li03 ;
*
li11     = p1       droite n1 p11 ;
li12     = p11      droite n2 p31 ;
li13     = p31      droite n2 p51 ;
litot1   = li11 et li12 et li13 ;
*
li21     = p2       droite n1 p12 ;
li22     = p12      droite n2 p32 ;
li23     = p32      droite n2 p52 ;
litot2   = li21 et li22 et li23 ;
*
pf1 = litot0 poin 'PROC' (20. 6. 0.) ;
pf2 = litot2 poin 'PROC' (20. 0. 0.) ;
*
litot = litot0 et litot1 et litot2 ;
elim 1e-05 litot ;
*trac litot;
*opti donn 5;
*
* Finite Element Model & Material...
*
valyoun = 1e+8 ;
valnu = 0.1 ;
racin3 = 3.0 ** 0.5 ;
s1 = epais * 6 * rayon;
iy = rayon * rayon * rayon * epais * 2.5;
iz = iy;
ts = iy + iz;
mess  ' section des poutres  ' s1;
mess ' mmoment d inertie des poutres ' iy;
*
mo1 = modele litot MECANIQUE ELASTIQUE pout ;
ma1 = mate mo1 YOUN valyoun NU valnu  ;
ca1 = CARA mo1 sect s1 inry iy inrz iz tors ts ;
*
*  Rigidite et CL ...
*
ri1 = RIGI mo1 (ma1 et ca1) ;
*
cl1 = bloq rota depl p0 ;
cl2 = bloq rota depl p1 ;
cl3 = bloq rota depl p2 ;
cl4 = bloq uz ry rx litot ;
*
rig2 = ri1 et cl1 et cl2 et cl3 et cl4 ;
*
* Contact Relation....
*
map10 = manu POI1 p10 ;
map11 = manu POI1 p11 ;
map12 = manu POI1 p12 ;
map30 = manu POI1 p30 ;
map31 = manu POI1 p31 ;
map32 = manu POI1 p32 ;
map50 = manu POI1 p50 ;
map51 = manu POI1 p51 ;
*
rela1   = rela 'MINI' 1.0  UY map10 - 1.0 UY map11 ;
fodepi1 = depi rela1 -0.01 ;
*
rela2   = rela 'MINI' 1.0 UY map30 - 1.0 UY map31 ;
fodepi2 = depi rela2 -0.01 ;
*
rela3   = rela 'MINI' 1.0 UY map50 - 1.0 UY map51 ;
fodepi3 = depi rela3 -0.01 ;
*
rela4   = rela 'MINI' 1.0 UY map11 - 1.0 UY map12 ;
fodepi4 = depi rela4 -0.01 ;
*
rela5   = rela 'MINI' 1.0 UY map31 - 1.0 UY map32 ;
fodepi5 = depi rela5 -0.01 ;
*
reltot = rela1 et rela2 et rela3 et rela4 et rela5;
ritot = rig2 et rela1 et rela2 et rela3 et rela4 et rela5 ;
*trac ritot ;
*
* Loading....
*
fo1 = force FY -20. pf1 ;
fo2 = force FY  20. pf2 ;
fo3 = fo2 et fo1 ;
fincre = fo3 ;
fodepi = fodepi1 et fodepi2 et fodepi3 et fodepi4 et fodepi5 ;
*
*   test
*
* detest = reso rig2 fo3 ;
* opti donn 5 ;
*
tabdepl = table ;
tabre1 = table ;
tabre2 = table ;
tabre3 = table ;
tabre4 = table ;
tabre5 = table ;
listr1 = prog ;
listr2 = prog;
listr3 = prog;
listr4 = prog ;
listr5 = prog ;
i = 1 ;
*opti donn 5;
na=10;
repeter surligne   na ;
      fotot = (i * fo3) et fodepi  ;
      dep1 = reso ( rig2 et reltot) fotot ;
      tabdepl . i = dep1 ;
      mess'=================================' ;
      mess 'increment ' i ;
*      list dep1;
      valfo1 = extr fotot FY pf1 ;
*      list valfo1 ;
      valfo2 = extr fotot FY pf2 ;
*     list valfo2 ;
*
      reac1 = reac rela1 dep1 ;
      tabre1 . i = reac1 ;
      listr1 = listr1 et (prog (extr reac1 FY p10)) ;
      reac2 = reac rela2 dep1 ;
      tabre2 . i = reac2 ;
      listr2 = listr2 et (prog (extr reac2 FY p30)) ;
      reac3 = reac rela3 dep1 ;
      tabre3 . i = reac3 ;
      listr3 = listr3 et (prog (extr reac3 FY p50)) ;
      reac4 = reac rela4 dep1 ;
      tabre4 . i = reac4 ;
      listr4 = listr4 et (prog (extr reac4 FY p11)) ;
      reac5 = reac rela5 dep1 ;
      tabre5 . i = reac5 ;
      listr5 = listr5 et (prog (extr reac5 FY p31)) ;
*
      i = i + 1 ;
fin surligne ;
*
listiter = prog 1.  pas 1. na ;

evolr1 = evol BLAN manu 'Numiter' listiter 'Reac1' listr1 ;
evolr2 = evol ROUG manu 'Numiter' listiter 'Reac2' listr2 ;
evolr3 = evol BLEU manu 'Numiter' listiter 'Reac3' listr3 ;
evolr4 = evol JAUN manu 'Numiter' listiter 'Reac4' listr4 ;
evolr5 = evol TURQ manu 'Numiter' listiter 'Reac5' listr5 ;
*
* les listri sont les reactions des appuis aux niveaux des
* relations unilaterales par pas de temps
*list listr1;list listr2;list listr3;list listr4;list listr5;
*  dess (evolr1 et evolr2 et evolr3 et evolr4 et evolr5) ;
ltest = prog   6.1718       18.209       28.580
38.950 49.321   59.691      69.548       76.821       84.094
  91.366 ;
dif = ltest - listr2;
poce= abs (dif / ltest);
maxerr = maxi poce;
*
optio echo 0;
mess ' **********************************************************';
mess ' *                                                        *';
mess ' *                                                        *';
mess ' *        erreur max sur reaction au contact 2 (en %)     *';
mess ' *                                                        *';
mess ' *                       ' maxerr    '                 *';
mess ' *                                                        *';
mess ' *                                                        *';
mess ' **********************************************************';
si ( maxerr > 0.01) ; erreur 5; finsi;

fin;




