* fichier : compression.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
* Type & Elements de representation *
opti dime 3 elem cub8 ;

* Caracteristiques geometriques *
la=1.;
mla=1. ;
crit = 0.00002 ;

* Defintion des noeuds *
P1 = 0. 0.   0.  ;
P2 = 0. la   0.   ;

* Definition des contours *
L1 = droit 1 P1 P2 ;

* Generation de contours *
coteg = L1 TRAN (0. 0. la) DINI mla DFIN mla;

* Creation du volume *
geom = VOLU coteg  TRAN (la 0. 0.)  DINI la DFIN la ;

* Defintion des surfaces supplementaires *
coted=FACE geom  2 ;

* Epurer le volume *
elim crit geom ;
elim crit (geom et coteg);
elim crit (geom et coted);

* Affichage *
coted = coted coul rouge ;
coteg = coteg coul bleu ;

*** Modele

mod1 = MODE geom MECANIQUE ELASTIQUE PLASTIQUE_ENDOM 'ENDO_PLAS' ;

mat1 = MATE mod1 'YOUN' 20.74e9 'NU' 0.2 'AC' 2.75
'AT' 1. 'BC' 2470. 'BT' 15000. 'EPD0' 10.e-5
'RC' 120.e6 'RT' 10.e6 'P' .4 'GAMA' 0.99 'ALFA' 0.5
'A' -1 'AH' 7.e-5 'BH' 2.e-2 'CH' 3.e-6 'BETA' 1. 'K0' 0.1;


*** Conditions aux limites

cl1 = BLOQ P1 UZ UY;
cl2 = BLOQ L1 UZ ;
cl3 = BLOQ COTEG UX ;
cldepl = BLOQ COTED UX ;

cltot = cl1 et cl2 et cl3 et cldepl ;


*** Chargement

fo1 = DEPI cldepl 1. ;
prog1 = PROG 0. 1. ;
prog2 = PROG 0. -2.e-3 ;

step1 = 1./20. ;
list1 = PROG 0. PAS step1 1. ;

evol1 = EVOL MANU prog1 prog2 ;

char1 = CHARGEMENT fo1 evol1 DIMP ;


tab1 = TABLE ;
tab1 . PLASTIQUE = vrai ;
tab1 . MODELE = mod1 ;
tab1 . CARACTERISTIQUES = mat1 ;
tab1 . CHARGEMENT = char1 ;
tab1 . BLOCAGES_MECANIQUES = cltot ;
tab1 . TEMPS_CALCULES = list1 ;
tab1 . TEMPS_SAUVES = list1 ;
tab1 . MOVA = 'D' ;
tab1 . PRECISION = 1.e-8;

PASAPAS tab1 ;

ind = inde (tab1.contraintes);
nbind1 = dime ind;
nbind1 = nbind1 - 1 ;
************************************************************

*definition des fonctions *

sigxx1=prog;
dex1=prog;
dev = prog ;

i = 1;

repeter bou1 nbind1;

  cont1 = (tab1).contraintes.i;

  sigxx1=sigxx1 et (prog(extr cont1 SMXX 1 1 1));

  dep1 = (tab1) . deplacements . i ;

  eps0 = EPSI dep1 mod1;
  depx1 = (EXTR eps0 EPXX 1 1 1);
  depy1 = (EXTR eps0 EPYY 1 1 1);
  depz1 = (EXTR eps0 EPZZ 1 1 1) ;

*  opti donn 5 ;
************************************************************
* deformations suivant les 3 composantes *

  devv = (depx1 + depy1 + depz1) ;
  dev = dev et (prog devv) ;
  dex1 = dex1 et (prog depx1);
 i = i+1;

fin bou1;

sixx = evol manu 'depl_xx' dex1 'sigxx' sigxx1;
sivv = evol manu 'defo_vol' dev 'sigxx' sigxx1 ;

*dess sivv;
*dess sixx;

fin ;




