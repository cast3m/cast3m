* fichier : rupt28.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
opti trac psc eptr 5 ;
*
GRAPH = 'O' ;
*
opti dime 3 elem cu20 ;
opti epsi lineaire;

************************
*Données paramètriques :
************************
* a : profondeur de la fissure *
* t : epaisseur du tube *
* ri, re : rayon interne/externe *
* h : hauteur du tube *

h = 1. ;
t = 60.e-3 ;
a = t/5 ;
ri = t*5;
re = ri+t;

*POINTS POUR L'AXE DE REVOLUTION
p0 = (0. 0. 0.);
py = (0. 1. 0.);

*COORDONNEE DE LA POINTE DE LA FISSURE

pf = (a + ri) 0. 0.;

*NOMBRE D'ELEMENTS AUTOUR DE LA POINTE DE LA FISSURE (1 et 2 COUT)
n_fiss = 10;

*TAILLE D'UN ELEMENT DE LA 1ERE ET 2EME COUTURE*
t_el = 200e-6 ; t_el2 = 400e-6 ;
*Facteur d'agrandissement de la taille du derafinement
tt_el2 = 4.*t_el2 ;

*LONGUEUR DE LA 1ERE ET 2EME COUTURE*
lc1 = n_fiss * t_el ; lc2 = t_el2 * n_fiss;

*NIVEAU DE CHARGEMENT
p0T = -400. ; p0P = 400. ; dt0 = 300.;

*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                    DEBUT DU MAILLAGE
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


**************************************************************
**********************  1ERE COUTURE  ************************
************  (Autour de la pointe de la fissure)  ***********
**************************************************************

p1cbd = pf plus (lc1 0. 0.) ;
p1chd = pf plus (lc1 lc1 0.) ;
pf1 = pf plus (0. lc1 0.) ;
p1chg = pf1 moin (lc1 0. 0.) ;
p1cbg = pf moin (lc1 0. 0.) ;

d1ch = droi (2*n_fiss) p1chg p1chd;
d1cg = droi (n_fiss) p1cbg p1chg ;
d1cd = droi (n_fiss) p1cbd p1chd ;
d1cbg = droi (n_fiss) p1cbg pf ;
d1cbd = droi (n_fiss) pf p1cbd ;

cout1 = regl n_fiss d1ch (d1cbg et d1cbd) ;
*cout1 = coul jaun cout1 ;

**************************************************************
**********************  2EME COUTURE  ************************
************  (Autour de la pointe de la fissure)  ***********
**************  Partie au-dessus de la fissure  **************
**************************************************************

p2cbd = pf plus (lc2 0. 0.) ;
p2chd = pf plus (lc2 lc2 0.) ;
pf2 = pf plus (0. lc2 0.) ;
p2chg = pf2 moin (lc2 0. 0.) ;
p2cbg = pf moin (lc2 0. 0.) ;

d2ch = droi (2*n_fiss) p2chg p2chd;
d2cg = droi (n_fiss) p2cbg p2chg ;
d2cd = droi (n_fiss) p2cbd p2chd ;

cout2 = regl n_fiss (d1cd et d1ch et d1cg) (d2cd et d2ch et d2cg) ;
*cout2 = coul vert cout2 ;

cout1et2 = cout1 et cout2;

**************************************************************
*****************  DERAFINEMENT DES COUTURES  ****************
**************  Partie au-dessus de la fissure  **************
**************************************************************

*------------------( DERAF A 4 ELEMENT )-------------------

pid1 = p2chg moin (0. tt_el2 0.) ;
pid2 = pid1 plus (0. t_el2 0.) ;
pid3 = pid2 plus (0. t_el2 0.) ;
pid4 = pid3 plus (0. t_el2 0.) ;
pid5 = pid4 plus (0. t_el2 0.) ;
pid6 = pid2 moin (t_el2 0. 0.) ;
pid7 = pid3 moin (t_el2 0. 0.) ;
pid8 = pid4 moin (t_el2 0. 0.) ;
pid9 = pid1 moin (tt_el2 0. 0.) ;
pid10 = pid3 moin (tt_el2 0. 0.) ;
pid11 = pid5 moin (tt_el2 0. 0.) ;

did1 = droi 1 pid1 pid2 ;
did2 = droi 1 pid2 pid3 ;
did3 = droi 1 pid3 pid4 ;
did4 = droi 1 pid4 pid5 ;
did5 = droi 1 pid9 pid6 ;
did6 = droi 1 pid6 pid7 ;
did7 = droi 1 pid7 pid8 ;
did8 = droi 1 pid11 pid8 ;
did9 = droi 1 pid10 pid7 ;

si1 = (regl 1 did1 did5) et (regl 1 did2 did6) et
      (regl 1 did3 did7) et (regl 1 did4 (inve did8)) et
      (regl 1 did8 did9) et (regl 1 did9 did5) ;
elim si1 1.e-5 ;

*------------------( DERAF A 3 ELEMENT )-------------------

pad1 = pf moin (lc2 0. 0.) ;
pad2 = pad1 plus (0. t_el2 0.) ;
pad3 = pad2 plus (0. t_el2 0.) ;
pad4 = pad3 plus (0. t_el2 0.) ;
pad5 = pad2 moin (t_el2 0. 0.) ;
pad6 = pad3 moin (t_el2 0. 0.) ;
pad7 = pad1 moin (tt_el2 0. 0.) ;
pad8 = pad4 moin (tt_el2 0. 0.) ;

dad1 = droi 1 pad1 pad2 ;
dad2 = droi 1 pad2 pad3 ;
dad3 = droi 1 pad3 pad4 ;
dad4 = droi 1 pad7 pad5 ;
dad5 = droi 1 pad5 pad6 ;
dad6 = droi 1 pad6 pad8 ;

sa1 = (regl 1 dad1 dad4) et (regl 1 dad2 dad5) et
      (regl 1 dad3 dad6) et (regl 1 dad4 (inve dad6));
saa1 = sa1 ;
repe i0 1 ;
ssa1 = sa1 plus (0. (3.*&i0*t_el2) 0.) ;
fin i0 ;
sa1 = sa1 et ssa1 ;
elim sa1 1.e-5 ;

*---------------------- PARTIE GAUCHE -----------------------
sig = sa1 et si1 ; elim sig 1.e-5 ;

*---------------------- PARTIE DROITE -----------------------
sid = sig syme droi ((coor 1 pf) 0. 0.) ((coor 1 pf) lc2 0.) ;
elim sid 1.e-5 ;

*---------------------- PARTIE HAUTE -----------------------

*lignes diagonales pour la symetrie
p_diagod = p2chd plus (lc1 lc1 0.);
p_diago = p2chg moin (lc1 0. 0.);
p_diagog = p_diago plus (0. lc1 0.);

d_diagog = droi 1 p1chg p_diagog;
d_diagod = droi 1 p1chd p_diagod;

sihg = sig syme droi p1chg p_diagog ;
elim sihg 1.e-5 ;
sihd = sid syme droi p1chd p_diagod ;
elim sihd 1.e-5 ;

sih = sihd et sihg ; elim sih 1.e-5 ;

*---------------------- PARTIE COIN -----------------------

dg = droi 1 pid11 p2chg;
dcg = dg tran 1 (0. tt_el2 0.);
dcd = dcg syme droi ((coor 1 pf) 0. 0.) ((coor 1 pf) lc2 0.) ;
sic = dcd et dcg; elim sic 1.e-5;
cout3 = sig et sid et sih et sic ; elim cout3 1.e-5 ;

cout_tot = cout1 et cout2 et cout3;
elim d1cbd cout_tot 1.e-5 ;

**************************************************************
*********************  RESTE DU MAILLAGE  ********************
**************  Partie au-dessus de la fissure  **************
**************************************************************

*Partie de gauche
*----------------
pt1 = mini (coor 1 cout_tot) 0. 0.;
pt2 = (mini (coor 1 cout_tot)) (maxi (coor 2 cout_tot)) 0.;
pt_partg = cout3 poin droit pt1 pt2 1.e-5 ;
d_partg = (cont cout_tot) elem appuye strictement pt_partg ;
p_ri = ri 0. 0.;
pg = d_partg tran
(((coor 1 p_ri)-(mini(coor 1 cout_tot))) 0. 0.) dini 1.6e-3 dfin 3.2e-3;

*Partie de droite
*----------------
pt3 = maxi (coor 1 cout_tot) 0. 0.;
pt4 = (maxi (coor 1 cout_tot)) (maxi (coor 2 cout_tot)) 0.;
pt_partd = cout_tot poin droit pt3 pt4 1.e-5 ;
d_partd = (cont cout_tot) elem appuye strictement pt_partd ;
p_re = re 0. 0.;
pd = d_partd tran
(((coor 1 p_re)-(maxi(coor 1 cout_tot))) 0. 0.) dini 1.6e-3 dfin 3.2e-3;
bas_cout = pg et pd et cout_tot ; elim bas_cout 1.e-5 ;

*Partie du haut
*--------------
p5 = (mini (coor 1 bas_cout)) (maxi (coor 2 bas_cout)) 0.;
p6 = (maxi (coor 1 bas_cout)) (maxi (coor 2 bas_cout)) 0.;
pt_parth = bas_cout poin droit p5 p6 1.e-5 ;
d_partd = (cont bas_cout) elem appuye strictement pt_parth ;
ph = d_partd tran (0. ((h/2.) - (maxi(coor 2 bas_cout))) 0.)
dini 1.6e-3 dfin t ;

*Structure totale EN AXI
*************************************
s0 = ph et bas_cout ; elim s0 1.e-5 ;
*************************************

pa = s0 poin proc (ri 0. 0.);
pb = s0 poin proc (re 0. 0.) ;

lvsup = (cont s0) elem compris pa pf ;

*Definition des bords du maillage AXI
l_hau = cote 3 ph;
l_bas = (cont s0) elem compris pf pb ;
p_gau = s0 poin droit (ri (mini (coor 2 s0)) 0.)
(ri (maxi (coor 2 s0)) 0.) 1.e-5 ;
l_gau = (cont s0) elem appuye strictement p_gau ;

n1 = 1;
deg1 = 0.5 ;
*Structure totale EN 3D
*************************************************
v0 = s0 volu n1 rota deg1 p0 py ; elim v0 1.e-5 ;
*************************************************

f1 f2 f3 = face v0;
f1 = f1 coul bleu;
f2 = f2 coul roug;

* Creation des surfaces inferieure et superieure.
surliga = l_bas rota n1 deg1 p0 py;
sursup = l_hau rota n1 deg1 p0 py;
surlev = lvsup rota n1 deg1 p0 py;
*surbas = (l_bas et lvsup)rota n1 deg1 p0 py;

*Defintion du front de fissure
pfx1 = (coor 1 pf)*(cos (-1.*deg1));
pfx3 = (coor 1 pf)*(sin (-1.*deg1));
pfx  = pfx1 0. pfx3;
fr_fiss = cerc n1 pf p0 pfx;
fr_fiss = fr_fiss coul cyan;

elim (v0 et surliga et sursup et surlev et
pfx et fr_fiss et f1 et f2) 1.e-5;

si (ega GRAPH 'O') ;
  trac (f3 et fr_fiss et pf et pfx);
fins ;

*Trois points sur la surface f2
zm = mini (f2 coor 3) ;
PC = f2 poin proc (ri 0. zm) ;
PD = f2 poin proc (re 0. zm) ;
PE = f2 poin proc (ri (h / 2.) zm) ;

si (ega GRAPH 'O') ;
  trac V0 qual cach ;
fins ;

*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                    FIN DU MAILLAGE
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                       PARTIE CALCULS
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

* PROPRIETES MATERIAUX A 300°C

coefEP = 5. ;
E0 = 185e3; nu0 = 0.4999 ; alfa0 = 13.08e-6;

*ldc elasto plastique
dsig = 0.01 ;
ls1 = prog 50. pas 500. 10000. ;
ls3002 = (prog 0. dsig) et ls1 ;
le3002 = (prog 0. (dsig / E0)) et (coefEP * ls1 / E0) ;
ta3002 = evol manu le3002 ls3002 ;

mo0 = mode v0 mecanique elastique plastique isotrope ;
ma0 = mate mo0 YOUN E0 nu nu0 alph alfa0 trac ta3002 ;
*mo0 = mode v0 mecanique elastique isotrope ;
*ma0 = mate mo0 YOUN E0 nu nu0 alph alfa0 ;

* CONDITIONS AUX LIMITES

cl1 = bloq uy surliga ;
cl2 = rela ense uy sursup ;
cl3 = bloq uz f1 ;
cl4 = v0 symt depl PC PD PE 1.e-6 ;
cl0 = cl1 et cl2 et cl3 et cl4 ;

* CHARGEMENT
*Traction uniaxiale
f0T = press mass mo0 p0T sursup ;
ev0 = evol manu (prog 0. 1.) (prog 0. 1.)  ;
cha0 = char meca f0T ev0 ;

*Gradient de temperature
chx = v0 coor1 1 ; chz = v0 coor 3 ;
chrr = ((chx ** 2) + (chz ** 2)) ** 0.5 ;
chr = (chrr - ri) / (re - ri) ;
cht0 = nomc 'T' (0. * chr) ;
cht1 = nomc 'T' (dt0 * chr);

tt1 = table ; tt2 = table ;
tt1 . 0  = 0. ; tt1 . 1 = 1. ;
tt2 . 0 = cht0 ; tt2 . 1 = cht1 ;
chat = CHAR 'T' tt1 tt2 ;

*Contraintes résiduelles
epsimp = epth cht1 mo0 ma0 ;
chsr = char DEFI epsimp ev0 ;

***********************************************
*CALCUL ELASTOPLASTIQUE : Contraintes résiduelles
***********************************************

tcha = prog 0.01 0.05 pas 0.05 1. ;
tab_meca = table ;
tab_meca . 'MODELE'              = mo0  ;
tab_meca . 'CARACTERISTIQUES'    = ma0  ;
tab_meca . 'BLOCAGES_MECANIQUES' = cl0  ;
tab_meca . 'CHARGEMENT'          = chSR ;
tab_meca . 'TEMPS_CALCULES'      = tcha ;
pasapas tab_meca ;

************************************
* CALCUL DE J ELASTO-PLASTIQUE
************************************

tabJpl = table ;
tabJpl . 'SOLUTION_PASAPAS'       = tab_meca ;
tabJpl . 'OBJECTIF'               = mot 'J' ;
tabjpl . 'MAILLAGE' = v0 ;
tabJpl . 'LEVRE_SUPERIEURE'       = surlev ;
tabJpl . 'FRONT_FISSURE'          = fr_fiss ;
tabJpl . 'COUCHE'                 = 4 ;
g_theta tabJpl ;

np = (dime tabJpl.resultats) - 1 ;
JplRS = tabJpl.resultats.np.global ;
np = (dime tab_meca.deplacements) - 1 ;
CMOD_RS = extr tab_meca.deplacements.np UY PA ;

***********************************************
*CALCUL ELASTOPLASTIQUE : Traction uniaxiale
***********************************************

tcha = prog 0.01 0.05 pas 0.05 1. ;
tab_meca = table ;
tab_meca . 'MODELE'              = mo0  ;
tab_meca . 'CARACTERISTIQUES'    = ma0  ;
tab_meca . 'BLOCAGES_MECANIQUES' = cl0  ;
tab_meca . 'CHARGEMENT'          = cha0 ;
tab_meca . 'TEMPS_CALCULES'      = tcha ;
pasapas tab_meca ;

************************************
* CALCUL DE J ELASTO-PLASTIQUE
************************************

tabJpl = table ;
tabJpl . 'SOLUTION_PASAPAS'       = tab_meca ;
tabJpl . 'OBJECTIF'               = mot 'J' ;
tabJpl . 'LEVRE_SUPERIEURE'       = surlev ;
tabJpl . 'FRONT_FISSURE'          = fr_fiss ;
tabJpl . 'COUCHE'                 = 4 ;
g_theta tabJpl ;

np = (dime tabJpl.resultats) - 1 ;
JplT = tabJpl.resultats.np.global ;

***********************************************
*CALCUL ELASTOPLASTIQUE : Chargement thermique
***********************************************

tcha = prog 0.01 0.05 pas 0.05 1. ;
tab_ther = table ;
tab_ther . 'MODELE'              = mo0  ;
tab_ther . 'CARACTERISTIQUES'    = ma0  ;
tab_ther . 'BLOCAGES_MECANIQUES' = cl0  ;
tab_ther . 'CHARGEMENT'          = chaT ;
tab_ther . 'TEMPS_CALCULES'      = tcha ;
pasapas tab_ther ;

************************************
* CALCUL DE J ELASTO-PLASTIQUE
************************************

tabJpl = table ;
tabJpl . 'SOLUTION_PASAPAS'       = tab_ther ;
tabJpl . 'OBJECTIF'               = mot 'J' ;
tabJpl . 'LEVRE_SUPERIEURE'       = surlev ;
tabJpl . 'FRONT_FISSURE'          = fr_fiss ;
tabJpl . 'COUCHE'                 = 4 ;
g_theta tabJpl ;

np = (dime tabJpl.resultats) - 1 ;
JplTH = tabJpl.resultats.np.global ;
CMOD_TH = extr tab_meca.deplacements.np UY PA ;


****************************************
********* SOLUTIONS ANALYTIQUES *********
*****************************************

*Fonctions d'influence
i0 = 1.2208 ;
i1 = 0.7205 ;

*Contraintes imposées pour le gradient de temperature
sig0 = ((E0*alfa0*dt0)/(1-nu0)) * (ri/(3*t)) *
((2*(re**2))/(ri*(re+ri)) - 1);
sig1 = -1. * ((E0*alfa0*dt0)/(1-nu0));

* J analytiques
JT = coefEP * (1-(nu0**2)) * ((i0*(-1.*p0T)*((pi*a)**(1./2.)))**2) / E0;
JTH = (1-(nu0**2)) / coefEP *
((((i0*sig0)+ (i1*sig1*(a/t)))*((pi*a)**(1./2.)))   **2)/E0;

*Erreurs sur J entre la solution analytique et le MEF
errT = ((JplT-JT)/JT)*100.;
errTH = ((JplTH-JTH)/JTH)*100.;
errRS = ((JplRS-JTH)/JTH)*100.;

mess 'Solution MEF       : ' JplT JplTH JplRS ;
mess 'Solution Theorique : ' JT JTH JTH;
mess 'Erreur en %        : ' errT errTH errRS  ;

* Test sur les erreurs
si ((abs errT) > 3.1) ;
  erre 'Erreur sur le calcul de JplT' ;
fins ;
si ((abs errTH) > 3.7) ;
  erre 'Erreur sur le calcul de JplTH' ;
fins ;
si ((abs errRS) > 3.7) ;
  erre 'Erreur sur le calcul de JplRS' ;
fins ;


FIN ;




