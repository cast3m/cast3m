* fichier :  joi24.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 0 ;
SAUT PAGE ;
MESS'!==============================================================!';
MESS'!                                                              !';
MESS'!                        TEST JOI24                            !';
MESS'!                                                              !';
MESS'!              ESSAI DE TRACTION SUR UN JOINT 2D               !';
MESS'!                                                              !';
MESS'!  Un joint 2D JOI2 a son bord inferieur encastre.             !';
MESS'!  Son bord superieur est libre. Un effort de traction est     !';
MESS'!  exerce sur son bord superieur,perpendiculairement au plan   !';
MESS'!  du joint. Le joint est incline de 20 degres par rapport     !';
MESS'!  a l axe X. Les noeuds du joint sont A1, B1, IB1, IA1.       !';
MESS'!                                                              !';
MESS'!     Y                                                        !';
MESS'!      !                                                       !';
MESS'!B1,IB1!.                                                      !';
MESS'!      !    .                                                  !';
MESS'!      !        .                                              !';
MESS'!      !            . <---- joint                              !';
MESS'!      !                .                                      !';
MESS'!      !________________o__(_._ X                              !';
MESS'!                             A1,IA1                           !';
MESS'!                                                              !';
MESS'!       angle o = 20 degres                                    !';
MESS'!       longueur du joint = L = 2                              !';
MESS'!       X(A1) = 1.879385242                                    !';
MESS'!       Y(B1) = 0.684040286                                    !';
MESS'!                                                              !';
MESS'!  Force exercee sur le bord superieur = 100000                !';
MESS'!> Deplacement de chacun des noeuds libres : Delta             !';
MESS'!        Force = Raideur * Delta * Aire                        !';
MESS'!  ou                                                          !';
MESS'!        Raideur = raideur du joint = 4.2E10                   !';
MESS'!        Delta   = deplacement des noeuds du joint             !';
MESS'!        Aire    = aire du joint                               !';
MESS'!                = Longueur * largeur                          !';
MESS'!        L       = 2                                           !';
MESS'!        l       = 1 ( contraintes ou deformations planes )    !';
MESS'!                                                              !';
MESS'!        Delta   =  100000 / (4.2E10 * 2 * 1)                  !';
MESS'!                =  1.190476E-6                                !';
MESS'!                                                              !';
MESS'!     1/ Deplacement suivant X :                               !';
MESS'!                                                              !';
MESS'!        Deltax = delta * cos(70)                              !';
MESS'!               = 4.0716677E-7                                 !';
MESS'!                                                              !';
MESS'!                                                              !';
MESS'!     2/ Deplacement suivant Y :                               !';
MESS'!                                                              !';
MESS'!        Deltay = delta * sin(70)                              !';
MESS'!               = 1.1186815E-6                                 !';
MESS'!                                                              !';
MESS'!==============================================================!';
OPTION DIME 2 ;
OPTION ELEM SEG2 MODE PLAN CONT  ;
*
SOLAY = 1.1186815E-6 ;
SOLAX = 4.0716677E-7 ;
*
* ---------- DEFINITION DE LA SURFACE TOP DU JOINT ----------
*
A1 = 1.879385242 0.00 ;
B1 = 0.00 0.684040286 ;
*
* ----------          MAILLAGE          ----------
*
H1 = A1 DROIT 1 B1 ;
*
L1 = H1 ;
*
* ---------- DEFINITION DE LA SURFACE BOT DU JOINT ----------
*
IA1 = 1.879385242 0.00 ;
IB1 = 0.00 0.684040286 ;
*
* ----------          MAILLAGE          ----------
*
IH1 = IA1 DROIT 1 IB1 ;
*
IL1 = IH1 ;
*
* ----------   CREATION DU JOINT JOI2   ----------
*
OPTION ELEM RAC2 ;
VOL = RACCORD 0.01 (INVE L1) (INVE IL1) ;
*
* ---------- DEFINITION DES CONDITIONS LIMITES ----------
*
CL11 = BLOQ IA1 UX ;
CL12 = BLOQ IA1 UY ;
CL1 = CL11 ET CL12 ;
*
CL21 = BLOQ IB1 UX ;
CL22 = BLOQ IB1 UY ;
CL2 = CL21 ET CL22 ;
*
CL = CL1 ET CL2 ;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MOD1 = MODE VOL 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' JOI2 ;
MA1 = MATE MOD1 KS 4.2E08 KN 4.2E10 ;
*
* ----------       MATRICE DE RIGIDITE        ----------
*
RI1 = RIGI MOD1 MA1 ;
RI2 = RI1 ET CL ;
*
* ----------        FORCE DE TRACTION         ----------
*
FO1 = FORCE ( 34202.01433  93969.26209 ) L1 ;
*
* ----------            RESOLUTION            ----------
*
RE = RESO RI2 FO1 ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS ' Solution Analytique :'                                    ;
MESS '                                                         ' ;
MESS '    UX =' SOLAX  ;
MESS '    UY =' SOLAY  ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS ' Solution Calculee :'                                      ;
MESS '                                                             ' ;
LIST RE ;

*
* ----------        CODE DE FONCTIONNEMENT          ----------
*
DEPA1 = EXTR RE UX A1 ;
DEPA2 = EXTR RE UY A1 ;
RESIX = ABS( (DEPA1-SOLAX)/SOLAX ) ;
RESIY = ABS( (DEPA2-SOLAY)/SOLAY ) ;
*
SI ( (RESIX <EG 1E-4 ) ET (RESIY <EG 1E-4 ) ) ;
   ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;

*
* ----------        CALCUL DES DEFORMATIONS         ----------
*
EPS1 = EPSI MOD1 RE ;
LIST EPS1 ;
*
* ----------        CALCUL DES CONTRAINTES         ----------
*
SIG1 = SIGMA MOD1 MA1 RE ;
LIST SIG1 ;
*
FIN ;











