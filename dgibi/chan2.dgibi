* fichier : chan2.dgibi

************************************************************************
* Section : Langage Fonctionnement
************************************************************************

************************************************************************
* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;
OPTI ECHO 1 ;

SAUT PAGE         ;
SI (NEG GRAPH 'N');
  OPTI TRAC X     ;
SINO              ;
  OPTI TRAC PSC   ;
FINSI             ;

SAUT PAGE;

************************************************************************
* NOM         : CHAN2
* DESCRIPTION : Teste l'operateur CHANGER pour les cas suivants :
*                 - changer un MCHAML  en MCHAML  avec CHAN 'CHAM' ...
*                 - changer un CHPOINT en CHPOINT avec CHAN 'CHPO' ...
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Clément BERTHINIER (CEA/DEN/DM2S/SEMT/LM2S)
*               mél : clement.berthinier@cea.fr
**********************************************************************
* VERSION    : v1, 02/02/2015, version initiale
* HISTORIQUE : v1, 02/02/2015, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
* Prière de PRENDRE LE TEMPS de compléter les commentaires
* en cas de modification de ce sous-programme afin de faciliter
* la maintenance !
************************************************************************


************************************************************************
* Création d'un maillage
************************************************************************
OPTI DIME 3   ;
OPTI ELEM SEG2;

NEZ = 2 ;

P1= 0. 0. 0. ;
P2= 1. 0. 0. ;

L1= DROI NEZ P1 P2     ;
L2= L1 PLUS (0. 1. 0.);

OPTI ELEM QUA4    ;
S1= REGL L1 NEZ L2 ;

OPTI ELEM CUB8                ;
V1= VOLU S1 TRAN NEZ (0. 0. 1.);


SI(NEG GRAPH 'N');
  TRAC CACH V1   ;
FINSI            ;


************************************************************************
* Création d'un CHPOINT
************************************************************************
X Y Z = COOR V1             ;
CHPO1 = X + (Y**2) + (Z**3) ;

list resu chpo1 ;

************************************************************************
* Création d'un MMODEL de MECANIQUE
************************************************************************
MODE1 = MODE V1 MECANIQUE ELASTIQUE ISOTROPE;

************************************************************************
* Création de différents types de MCHAML : Supports différents
************************************************************************
CHAM1 = CHAN 'CHAM'     CHPO1 MODE1 ;

CHAM2 = CHAN 'NOEUD'    CHAM1 MODE1 ;
CHAM3 = CHAN 'GRAVITE'  CHAM1 MODE1 ;
CHAM4 = CHAN 'MASSE'    CHAM1 MODE1 ;
CHAM5 = CHAN 'STRESSES' CHAM1 MODE1 ;

lister resu cham1 ;
lister resu cham2 ;
lister resu cham3 ;
lister resu cham4 ;
lister resu cham5 ;


************************************************************************
* Passage d'un MCHAML a un MCHAML avec la syntaxe : CHAN 'CHAM' ...
*    Ce cas peut se présenter en thermique avec des chargements sous
*    forme de CHPOINT ou MCHAML indifféremment
*    Par défaut le support est aux 'NOEUD'
************************************************************************
CHAM12 = CHAN 'CHAM'     CHAM1 MODE1 ;
CHAM22 = CHAN 'CHAM'     CHAM2 MODE1 ;
CHAM32 = CHAN 'CHAM'     CHAM3 MODE1 ;
CHAM42 = CHAN 'CHAM'     CHAM4 MODE1 ;
CHAM52 = CHAN 'CHAM'     CHAM5 MODE1 ;

lister resu cham12 ;
lister resu cham22 ;
lister resu cham32 ;
lister resu cham42 ;
lister resu cham52 ;
zz = 'ABS' (cham12 '-' cham1) ;
lister resu zz ; list (mini zz) ; list (maxi zz) ;
zz = 'ABS' (cham22 '-' cham1) ;
lister resu zz ; list (mini zz) ; list (maxi zz) ;
zz = 'ABS' (cham42 '-' cham1) ;
lister resu zz ; list (mini zz) ; list (maxi zz) ;
zz = 'ABS' (cham52 '-' cham1) ;
lister resu zz ; list (mini zz) ; list (maxi zz) ;

************************************************************************
* Passage d'un MCHAML a un MCHAML avec la syntaxe : CHAN 'CHAM' ...
*    Ce cas peut se présenter en thermique avec des chargements sous
*    forme de CHPOINT ou MCHAML indifféremment
*    Par défaut le support est aux 'NOEUD'
************************************************************************

*Changement d'un MCHAML vers MCHAML en changeant Support
CHAM13 = CHAN 'CHAM' CHAM1 MODE1 'RIGIDITE' ;
CHAM23 = CHAN 'CHAM' CHAM2 MODE1 'RIGIDITE' ;
CHAM33 = CHAN 'CHAM' CHAM3 MODE1 'RIGIDITE' ;
CHAM43 = CHAN 'CHAM' CHAM4 MODE1 'RIGIDITE' ;
CHAM53 = CHAN 'CHAM' CHAM5 MODE1 'RIGIDITE' ;

lister resu cham13 ;
lister resu cham23 ;
lister resu cham33 ;
lister resu cham43 ;
lister resu cham53 ;

*Changement d'un MCHAML vers MCHAML en changeant Support ordre changé
CHAM14 = CHAN 'CHAM' 'RIGIDITE' CHAM1 MODE1 ;
CHAM24 = CHAN 'CHAM' 'RIGIDITE' CHAM2 MODE1 ;
CHAM34 = CHAN 'CHAM' 'RIGIDITE' CHAM3 MODE1 ;
CHAM44 = CHAN 'CHAM' 'RIGIDITE' CHAM4 MODE1 ;
CHAM54 = CHAN 'CHAM' 'RIGIDITE' CHAM5 MODE1 ;

lister resu cham14 ;
lister resu cham24 ;
lister resu cham34 ;
lister resu cham44 ;
lister resu cham54 ;

*Changement d'un MCHAML vers MCHAML en changeant Support et Type (vide)
CHAM15 = CHAN 'CHAM' CHAM1 MODE1 'RIGIDITE' 'SCAL_A1';
CHAM25 = CHAN 'CHAM' CHAM2 MODE1 'RIGIDITE' 'SCAL_B1';
CHAM35 = CHAN 'CHAM' CHAM3 MODE1 'RIGIDITE' 'SCAL_C1';
CHAM45 = CHAN 'CHAM' CHAM4 MODE1 'RIGIDITE' 'SCAL_D1';
CHAM55 = CHAN 'CHAM' CHAM5 MODE1 'RIGIDITE' 'SCAL_E1';

lister resu cham15 ;
lister resu cham25 ;
lister resu cham35 ;
lister resu cham45 ;
lister resu cham55 ;

*Changement d'un MCHAML vers MCHAML en changeant Support et Type (plein)
CHAM16 = CHAN 'CHAM' CHAM15 MODE1 'STRESSES' 'SCAL_A2';
CHAM26 = CHAN 'CHAM' CHAM25 MODE1 'STRESSES' 'SCAL_B2';
CHAM36 = CHAN 'CHAM' CHAM35 MODE1 'STRESSES' 'SCAL_C2';
CHAM46 = CHAN 'CHAM' CHAM45 MODE1 'STRESSES' 'SCAL_D2';
CHAM56 = CHAN 'CHAM' CHAM55 MODE1 'STRESSES' 'SCAL_E2';

lister resu cham16 ;
lister resu cham26 ;
lister resu cham36 ;
lister resu cham46 ;
lister resu cham56 ;

*Changement d'un CHPOINT vers MCHAML en spécifiant le Support
CHAM17 = CHAN 'CHAM' CHPO1 MODE1 'NOEUD' ;
CHAM27 = CHAN 'CHAM' CHPO1 MODE1 'GRAVITE' ;
CHAM37 = CHAN 'CHAM' CHPO1 MODE1 'STRESSES' ;
CHAM47 = CHAN 'CHAM' CHPO1 MODE1 'MASSE' ;
CHAM57 = CHAN 'CHAM' CHPO1 MODE1 'RIGIDITE' ;

lister resu cham17 ;
lister resu cham27 ;
lister resu cham37 ;
lister resu cham47 ;
lister resu cham57 ;

*Changement d'un CHPOINT vers MCHAML en spécifiant le Support et Type
CHAM18 = CHAN 'CHAM' CHPO1 MODE1 'NOEUD'    'SCALAIRE';
CHAM28 = CHAN 'CHAM' CHPO1 MODE1 'GRAVITE'  'SCALAIRE';
CHAM38 = CHAN 'CHAM' CHPO1 MODE1 'STRESSES' 'SCALAIRE';
CHAM48 = CHAN 'CHAM' CHPO1 MODE1 'MASSE'    'SCALAIRE';
CHAM58 = CHAN 'CHAM' CHPO1 MODE1 'RIGIDITE' 'SCALAIRE';

lister resu cham18 ;
lister resu cham28 ;
lister resu cham38 ;
lister resu cham48 ;
lister resu cham58 ;

*Changement d'un MCHAML vers CHPOINT en spécifiant un MAILLAGE
CHAM19 = CHAN 'CHAM' CHPO1 V1;

lister resu cham19 ;

************************************************************************
* Passage d'un CHPOINT a un CHPOINT avec la syntaxe : CHAN 'CHPO' ...
*    Ce cas peut se présenter en thermique avec des chargements sous
*    forme de CHPOINT ou MCHAML indifféremment
*    Le MMODEL est alors OPTIONNEL
************************************************************************
*Changement d'un CHPOINT vers CHPOINT sans le modèle (inutile en fait)
CHPO2 = CHAN 'CHPO' CHPO1       ;

*Changement d'un CHPOINT vers CHPOINT en donnant le Modèle (inutilisé)
CHPO3 = CHAN 'CHPO' CHPO1 MODE1 ;

*Changement d'un CHPOINT vers MCHAML sans la méthode (MOYE, SOMM)
CHPO4 = CHAN 'CHPO' CHAM1 MODE1 ;

*Changement d'un CHPOINT vers MCHAML avec la méthode (MOYE, SOMM)
CHPO5 = CHAN 'CHPO' CHAM1 MODE1 'MOYE';
LIST RESU CHPO5;

*Changement d'un CHPOINT vers MCHAML sans la méthode (MOYE, SOMM)
CHPO6 = CHAN 'CHPO' CHAM1 MODE1 'SOMM';
LIST RESU CHPO6;


FIN;



