* fichier :  ccar4.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************
**
**  --- 10 OCTOBRE 1998 ---
**
**   TEST CAVITE CARRE
**
**   teste LAPL KONV(SUPG) KCCT en IMPL
**   Formulation MACRO CENTREP1
**   Formulation QUADR CENTREP1

OPTION ISOV 'SULI' ;
GRAPH = 'N'                   ;
COMPLET = FAUX ;

SI ( COMPLET ) ;
ds1=0.02 ; ds2=0.2 ;
SINON        ;
err1=2.e-3 ;
ds1=0.04 ; ds2=0.3 ;
FINSI ;



 DEBPROC TEST KPRESS*MOT TYPK*MOT MACRO*MOT GRAPH*MOT ;
 option dime 2 elem TYPK ;

 p1= 0 0 ; p12=0.5 0. ; p2= 1 0 ;

 ab=p1 d dini ds1 dfin ds2 p12 d dini ds2 dfin ds1 p2 ;
 mt= ab trans dini ds1 dfin ds2 (0 0.5) trans dini ds2
 dfin ds1 (0 0.5) ;
 bc=cote 2 mt ; cd=cote 3 mt ; da=cote 4 mt ;
 AA=bc moin (0.5 0.) ;
 BB=ab plus (0. 0.5) ;
 elim (AA et BB et mt) 1.e-3 ;

 mt= chan mt QUAF ;
 $mt=mode mt 'NAVIER_STOKES' MACRO ;

MU  =1. ;
RO= 400. ;
DT=1. ;

* La cavité est fermée il faut imposer la pression en 1 point !
prep1=doma $mt kpress;
bcp=elem  prep1 POI1 (lect 1) ;
*

CD1= chan CD POI1 ;
CD = elem CD1 (lect 2 pas 1 ((nbel cd1) - 1) );


   RV= eqex  'OMEGA' 0.7  'NITER' 5
* 'OPTI' 'EF' 'IMPL' 'SUPG' 'NODIV' KPRESS
  'OPTI' 'EF' 'IMPL' 'SUPG'  KPRESS
  ZONE  $mt    OPER  KBBT 1.            INCO 'UN' 'PRES'
  ZONE  $mt    OPER  LAPN MU            INCO 'UN'
  ZONE  $mt    OPER  KONV RO 'UN' MU DT INCO 'UN'
   ;
  RV= eqex RV  CLIM
  PRES TIMP bcp 0.
  UN UIMP CD   1. UN VIMP CD 0.  UN UIMP DA   0. UN VIMP DA 0.
  UN UIMP AB   0. UN VIMP AB 0.  UN UIMP BC   0. UN VIMP BC 0. ;

rv.inco= table inco ;
rv.inco.un =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.pres =  kcht $mt  scal KPRESS 1.e-5  ;

 exec rv ;

 AA = chan AA quaf ;
 BB = chan BB quaf ;
 elim (mt et aa et bb ) 1.e-3 ;
 $AA=mode AA 'NAVIER_STOKES' MACRO ;
 $BB=mode BB 'NAVIER_STOKES' MACRO ;
srti=doma $AA 'MAILLAGE'        ;
srth=doma $BB 'MAILLAGE'        ;
evolV = EVOL 'CHPO' (rv.'INCO'.'UN') UX (srti  ) ;
evolH = EVOL 'CHPO' (rv.'INCO'.'UN') UY (srth  ) ;
evx=extr evolV 'ORDO' ;
 list evx ;
evy=extr evolV 'ABSC' ;
evolV= evol 'MANU' 'Vitesse' evx 'Hauteur' evy ;
rv.'EVOLV'=evolV ;
rv.'EVOLH'=evolH ;

si ('EGA' graph 'O' );
TAB1=TABLE;
TAB1.'TITRE'=TABLE ;
TAB1 . 1 ='MARQ      REGU '     ;
TAB1.'TITRE' . 1 = mot 'Composante_UX '     ;
DESS evolV 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;

TAB1.'TITRE' . 1 = mot 'Composante_UY '     ;
DESS evolH 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;
 c1=vect (rv.inco.'UN') 0.3 ux uy jaune ;
 trace c1 mt ;
 pn= ELNO $mt (rv.inco.'PRES') KPRESS ;
 trace pn mt ;
 mess ' MAX P ' (maxi pn) 'MIN P ' (mini pn) ;
finsi ;
FINPROC RV ;

 RV= test 'CENTREP1' 'TRI6' 'MACRO' GRAPH ;
 evv= (rv.'EVOLV');
 evh= (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  2.43000E-08 -2.78210E-02 -4.77528E-02 -7.84300E-02 -9.91939E-02
  -.12522      -.14085     -9.57261E-02 -5.99803E-02 -1.76880E-02
  1.64124E-03  2.31438E-02  6.54147E-02   .15983       .40500
   .67424       .99757;
 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 TRI6 MACRO ' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP1' 'QUA8' 'MACRO' GRAPH ;
 evv= evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  2.43000E-08 -2.76432E-02 -5.22210E-02 -9.03404E-02  -.13069
  -.19205      -.24920      -.23564      -.10473     -1.63207E-02
  9.13911E-02   .15798       .22391       .30032       .50086
   .72370       .99757;
 lr=extr (rv.'EVOLV') 'ABSC' ;
 list lr ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 QUA8 MACRO ' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP1' 'TRI6' 'QUAF' GRAPH ;
 evv=evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  2.43000E-08 -2.68117E-02 -5.26022E-02 -8.51626E-02  -.12411
  -.17500      -.22984      -.20395      -.11338     -1.39594E-02
  8.24660E-02   .13427       .21547       .29016       .49610
   .71023       .99757;
 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 QUA8 MACRO ' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP1' 'QUA8' 'QUAF' GRAPH ;
 evv= evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  2.43000E-08 -2.15770E-02 -4.08992E-02 -7.15706E-02  -.10570
  -.15997      -.21007      -.19966     -9.31649E-02 -2.06067E-02
  6.22750E-02   .11378       .18794       .27287       .47597
   .70673       .99757;
 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 QUA8 QUADR ' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTRE' 'TRI6' 'MACRO' GRAPH ;
 evv= evv et (rv.'EVOLV');
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
  2.43000E-08 -3.51427E-02 -6.59416E-02  -.10420      -.14077
  -.17755      -.20315      -.16433     -8.50078E-02 -5.95431E-03
  5.69258E-02  9.57364E-02   .15170       .24177       .47369
   .70672       .99757;
 lr=extr (rv.'EVOLV') 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTRE TRI6 MACRO ' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

si ('EGA' graph 'O' );
titre 'Coupe oy à x=1/2 ' ;
TAB1=TABLE;
TAB1.'TITRE'= table ;
TAB1 . 1 ='TIRR MARQ CROI REGU ';
TAB1.'TITRE' . 1 ='MACRO TRI6 ';
TAB1 . 2 ='TIRM MARQ PLUS REGU ';
TAB1.'TITRE' . 2 ='MACRO QUA8';
TAB1 . 3 ='TIRR MARQ ETOI REGU ';
TAB1.'TITRE' . 3 =' P1 - P2+Bulle ';
TAB1 . 4 ='TIRM MARQ CARR REGU ';
TAB1.'TITRE' . 4 =' P1 - Q2';
TAB1 . 5 ='TIRR MARQ LOSA REGU ';
TAB1.'TITRE' . 5 =' P0 - P1 Stab ';
DESS EVV 'TITX' 'Oy'    LEGE  TAB1 ;
titre 'Coupe ox à y=1/2 ' ;
DESS EVH 'TITX' 'Ox'    LEGE  TAB1 ;
FINSI ;


FIN ;
















