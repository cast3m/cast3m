* fichier :  pressuhx1.dgibi
************************************************************************
* Section : Fluides Condensation
************************************************************************
*---------------------------------------------------------
*     Pressurisation d'une enceinte
*
* Ce test s'appuie sur le jeu de donnée pressu.dgibi
* Il vise à tester la procédure PROCHEXT qui permet
* d'imposer un échange thermique entre la paroi extérieure de la coque
* et le milieu extérieur (eau de mer pour la partie inférieure et air
* pour la partie émergée.
* Le maillage correspond à une enceinte cylindrique
* d'environ 10 m3 avec un mur en contact avec la
* paroi verticale (10 cm d'acier)
* Tout le volume est initialement a 1bar et 40oC
* et la température du mur est mise à 60oC
* au début du calcul. On calcule la pressurisation
* de cette enceinte sur 50 secondes en injectant un débit
* de 50g/s de vapeur à 150oC. Ce test (un peu long)
*--------------------------------------------------------
COMPLET = FAUX ;
*COMPLET = VRAI ;
 GRAPH =  VRAI                                          ;
 GRAPH =  FAUX                                          ;
 GRAFRXT= FAUX                                          ;
 'OPTI' 'TRAC' 'X'                                ;
*'OPTI' 'TRAC' 'PSC'                              ;

 'SI' COMPLET ;

 nbit=100;
 DT0 = 1. ;
 n1   =  1          ; n2   =  4          ; n3   =  4          ;
 n4   =  8          ; nn   =  2          ;

 'SINON' ;

 nbit= 100;
 DT0 = 10. ;
 n1   =  1          ; n2   =  2          ; n3   =  4          ;
 n4   =  4          ; nn   =  1          ;

 'FINSI' ;

*--------------------------------------------------------
* Definition du maillage de l'enceinte cylindrique
*
 'OPTI' 'DIME' 3 'ELEM' 'CU20'                                ;

 ri   = 1.052      ; sp   =  0.10     ; hc   = 4.163      ;



 epsi =  1.000e-2   ;                                         ;

 p0   =     0.000             0.000                0.000      ;
 px   = -1000.000             0.000                0.000      ;
 py   =     0.000         -1000.000                0.000      ;
 pz   =     0.000             0.000             1000.000      ;
 cd   =     0.000             0.000              -20.000      ;
 ph0  =     0.000             0.000                hc         ;
 phx  =     ri                0.000                hc         ;
 phy  =     0.000             ri                   hc         ;

 fg1 = 0.25  ;
 fg2 = fg1 * (2.0 ** 0.5) / 2. ;

 p1   =  (ri*fg1)             0.000                0.000      ;
 p2   =  (ri*fg2)             (ri*fg2)             0.000      ;
 p3   =  0.000                (ri*fg1)             0.000      ;
 p4   =   ri                  0.000                0.000      ;
 p5   =  0.000                 ri                  0.000      ;
 p6   =  (ri+sp)              0.000                0.000      ;
 p7   =  0.000                (ri+sp)              0.000      ;

* Hauteur de l'enceinte
  h1 = 4.163                                                    ;
* Vecteur de translation
  v1 = 0. 0. h1                                               ;

 l1   =  'DROI' p0 p1 n1                                        ;
 l2   =  'DROI' p1 p2 n1                                        ;
 l3   =  'DROI' p2 p3 n1                                        ;
 l4   =  'DROI' p3 p0 n1                                        ;
 l5   =  'CERC' p4 p0 p5 (2*n1)                                 ;
 l6   =  'CERC' p6 p0 p7 (2*n1)                                 ;

 basf0=  'DALL' l1 l2 l3 l4 'PLAN'                              ;
 basf1=('REGL' (l2 'ET' l3) l5 n2) ;
 l44= cote 2 basf1;
 ax4= (inve l4) et l44 ;
 l11= cote 4 basf1;
 ax1= l11 et (inve l1) ;

 basf =  basf0 'ET' ('REGL' (l2 'ET' l3) l5 n2) ;
 'ELIM' basf epsi ;
 basf =  basf 'ET' ('SYME'  basf 'DROI' p0 p3) ;
 ax11 =  ('SYME'  ax1  'DROI' p0 p3) 'ET' (inve ax1) ;
 'ELIM' basf epsi ;
 basf =  basf 'ET' ('SYME'  basf 'DROI' p0 p1) ;
 ax44 =  (inve ax4)  'ET' ('SYME'  ax4  'DROI' p0 p4) ;
 'ELIM' basf epsi ;

 basm =  'REGL' l5 l6 n3                                        ;
 basm =  basm 'ET' ('SYME'  basm 'DROI' p0 p3) ;
 'ELIM' basm epsi ;
 basm =  basm 'ET' ('SYME'  basm 'DROI' p0 p1) ;
 'ELIM' basm epsi ;

* Creation du volume

 dx = ri / 2. ;
 nz1 = ('ENTIER' ( h1 / dx ))*nn ;

 bas = basf 'VOLU' nz1 'TRAN' v1 ;
 mbas = basm 'VOLU' nz1 'TRAN' v1 ;
 mbas = mbas 'COUL' 'ROUG' ;
 plan1 = ax11 'TRAN' nz1 v1 ;
 plan4 = ax44 'TRAN' nz1 v1 ;
 l5   =  l5   'ET' ('SYME'  l5   'DROI' p0 p1) ;
 l6   =  l6   'ET' ('SYME'  l6   'DROI' p0 p1) ;
 l5   =  l5   'ET' ('SYME'  l5   'DROI' p0 p3) ;
 l6   =  l6   'ET' ('SYME'  l6   'DROI' p0 p3) ;
 pari =  L5   'TRAN' nz1 v1 ;
 pare =  L6   'TRAN' nz1 v1 ;

 pti  =  point 1 l5;
 pte  =  point 1 l6;
 lzi  = pti d nz1 (pti plus v1) ;
 lze  = pte d nz1 (pte plus v1) ;

 'ELIM' (bas et plan1 et plan4 et pari et pare et mbas
         et lzi et lze) epsi ;

 mt = bas ;
 wall = mbas ;
 elim (mt et wall) epsi ;

* Localisation d'une brèche éventuelle au bas de l'enceinte

 pjg  =  'POIN' basf 'PROC' (0.000    0.000    0.000)             ;
 jg   = ('ELEM' basf 'APPUIE' 'LARGEMENT' pjg) 'COUL' 'VERT'      ;

*--------------------------------------------------------------------
* Fin de la définition du maillage
*--------------------------------------------------------------------

*--------------------------------------------------------------------
* Début de l'initialisation de la procédure ENCEINTE : table RXT
*--------------------------------------------------------------------

 rxt          =  'TABLE'                                      ;

*-- Nom du volume fluide
 rxt.'vtf'    =   mt                                        ;
 rxt.'epsi'   =   epsi                                      ;

*-- Definition des murs de l'enceinte : ici un seul mur
*-- en ACIER dont on traitera la thermique dans l'épaisseur
*-- et que l'on initialise a 40oC

*-- On definit d'abort la matériau ACIER avec sa conductivite
*-- thermique LAMBDA (W/m/K) et le produit ro*Cp (J/m3/K)
 rxt.'THERMP' = VRAI                                        ;
 rxt.'vtp'    =   wall                                      ;
 rxt.'LAMBDA' = 15.                                         ;
 rxt.'ROCP'   = 3.9E6                                       ;
 rxt.'Tp0'    = 60.                                         ;
 rxt.'ECHAN'  = 10.                                         ;

*-- Conditions initiales dans l'enceinte de test
 rxt.'TF0'    =       40.0                         ;
 rxt.'PT0'    =        1.00000e5                   ;
 rxt.'Yvap0'  =        0.0023                      ;

*-- On positionne une brèche
 rxt.'breche' =   jg                                         ;
 rxt.'diru1'  =   0 0 1                                      ;

*-- On definit un point interne au maillage pour imposer la valeur de
*-- la pression
 rxt.'pi'     =  (0.0 0.0 0.5)                               ;

*-- On indique que le calcul comporte de la vapeur d'eau
 rxt.'VAPEUR' =  VRAI                                        ;

*-- On active le recalcul automatique du préconditionnement
*-- toutes les 5 itérations
 rxt.'FRPREC' = 5                                          ;
 rxt.'DETMAT' = VRAI                                       ;

*-- Definition du scenario thermohydraulique
 rxt.'scenario'          = table                              ;

*-- Conditions a la breche (Obligatoire pour l'instant)
 rxt.'scenario'.'t'      = prog   0.0 1000.0                  ;
 rxt.'scenario'.'qeau'   = prog   0.050  0.050                ;
 rxt.'scenario'.'qair'   = prog   0.000  0.000                ;
 rxt.'scenario'.'tinj'   = prog   150.0  150.0                ;

*-- On impose le pas de temps (s)
 rxt.'DT0'    =  DT0                                          ;

*-- On impose la viscosite turbulente (m2/s)
 rxt.'MODTURB'='NUTURB'                             ;
 rxt.'NUT'    =  1.e-2                              ;

*-- On lance le calcul sur 20 itérations d'une seconde
 rxt.'GRAPH'=GRAFRXT                    ;


*===================================================================
*===================================================================
* Définition d'une procédure personnelle

 'DEBPROC' MAPROCX TPS*'FLOTTANT' rxt*'TABLE';
*
* Cette procédure personnelle vise à imposer un coefficient d'échange
* variable en temps et/ou en espace en appelant la procédure PROCHEXT.

hz  =evol manu (prog 0. 1.e3) (prog 2. 2.)          ;
hair=evol manu (prog 0. 1.e3) (prog 5. 5.)          ;
tair=evol manu (prog 0. 1.e3) (prog 45. 45.)        ;
hmer=evol manu (prog 0. 1.e3) (prog 100. 100.)      ;
tmer=evol manu (prog 0. 1.e3) (prog 25. 25.)        ;
Lair=3.                                             ;
Lmer=2.                                             ;

 PROCHEXT TPS RXT 'KAS1' HZ HAIR TAIR HMER TMER      ;

 'FINPROC'                                          ;

 rxt.'ECHEXT' = VRAI                                        ;
 rxt.'parext' = pare                                        ;
 rxt.'HEXT'   = 0.                                          ;
 rxt.'TPEXT'  = 0.                                          ;

 rxt.'PERSO'   = Vrai                                       ;
 rxt.'PRCPERSO'= 'MAPROCX'                                  ;
 rxt.'TABPERSO'= TABLE                                      ;

*===================================================================
*===================================================================

 EXECRXT 2    rxt                              ;

 EXECRXT (nbit - 2) rxt                        ;



*************** Echange avec l'extérieur ***************
 hextc= rxt.'TIC'.'HEXTc'                           ;
 $parext=rxt.'GEO'.'$parext'                        ;
 hext = elno hextc $parext                          ;
 tpext = rxt.'TIC'.'TPEXT'                          ;
 TP=rxt.'TIC'.'TP'                                  ;

Si GRAPH                                            ;
 trace hext pare titr 'H externe (KAS1)'            ;
 trace tpext pare titr 'Temperature externe (KAS1)' ;
trace cache TP (pari et pare) TITR ' TEMPERATURE PAROI EXTERNE (KAS1)';
Finsi                                               ;

tpe= redu rxt.'TIC'.'TP' lze;
tpi= redu rxt.'TIC'.'TP' lzi;
tfi= redu rxt.'TIC'.'TF' lzi;

TAB1 = TABLE ;
TAB1.1 = 'MARQ LOSA REGU' ;
TAB1.2 = 'MARQ TRIU REGU' ;
TAB1.3 = 'MARQ PLUS TIRM' ;
TAB1.4 = 'MARQ CARR TIRM' ;
TAB1.'TITRE' = TABLE ;
TAB1.'TITRE' . 1 = 'MOT' 'T paroi int' ;
TAB1.'TITRE' . 2 = 'MOT' 'T paroi ext' ;
TAB1.'TITRE' . 3 = 'MOT' 'T externe' ;
TAB1.'TITRE' . 4 = 'MOT' 'T fluide paroi' ;

evtpxt = (evol chpo (redu tpext pare) lze) coul bleue;
evtpe= (evol chpo tpe lze) coul bleue   ;
evtpi= (evol chpo tpi lzi) coul rouge   ;
evtfi= (evol chpo tfi lzi) coul rouge ;
evhxt = evol chpo (redu hext pare) lze;

Si GRAPH;
dess (evtpi et evtpe et evtpxt et evtfi) lege tab1 'GRIL' titx 'm'
 tity '°C' titr 'Temperatures KAS1 (coef echange impose)' ;

TAB1.'TITRE' . 1 = 'MOT' 'H externe ' ;
dess evhxt lege tab1 'GRIL' titx 'm' tity 'W/m^2/K'
titr 'Coefficient d echange impose (KAS1)' ;
Finsi;
*********** Fin Echange avec l'extérieur ***************

*************** Pressions ******************************
Psatip   = PSATT (tpi + 273.15)  ;
PT       = extr (dime rxt.'TIC'.'PT') (rxt.'TIC'.'PT')    ;
evpsatip = evol chpo Psatip lzi   ;
llzi = prog (mini (coor 3 lzi)) pas 1 (maxi (coor 3 lzi));
evpt    = evol manu llzi (prog (dime llzi)*PT);
Pair= (extr rxt.'TIC'.'Rhomn' 1)*287.1*tpi;
evpair = evol chpo pair lzi;
pvi = PT - Pair;
evpvi = evol chpo pvi lzi;

TAB1 = TABLE ;
TAB1.1 = 'MARQ LOSA REGU' ;
TAB1.2 = 'MARQ TRIU REGU' ;
TAB1.3 = 'MARQ PLUS TIRM' ;
TAB1.4 = 'MARQ CARR TIRM' ;
TAB1.'TITRE' = TABLE ;
TAB1.'TITRE' . 1 = 'MOT' 'PT' ;
TAB1.'TITRE' . 2 = 'MOT' 'Pvsat (Tpi)' ;
TAB1.'TITRE' . 3 = 'MOT' 'Pair paroi int' ;
TAB1.'TITRE' . 4 = 'MOT' 'Pv paroi int' ;
Si GRAPH;
dess (evPT et evpsatip et evpair et evpvi) TITR 'Pressions'
lege tab1 'GRIL' titx 'm' tity 'Pa';
Finsi;
*********** Fin Pressions ******************************

*************** Densités *******************************
Rho     = rxt.'TIC'.'RHO';
Rvp     = rxt.'TIC'.'RVP';
rhpi    = redu Rho lzi   ;
rvpi    = redu Rvp lzi   ;
rinci   = rhpi - rvpi    ;
Rvsati   = Psatip/461.73/(tpi + 273.15) ;
evrhpi  = evol chpo rhpi lzi;
evrvpi  = evol chpo rvpi lzi;
evrinci = evol chpo rinci lzi;
evrvsati = evol chpo Rvsati lzi ;

TAB1 = TABLE ;
TAB1.1 = 'MARQ LOSA REGU' ;
TAB1.2 = 'MARQ TRIU REGU' ;
TAB1.3 = 'MARQ PLUS TIRM' ;
TAB1.4 = 'MARQ CARR TIRM' ;
TAB1.'TITRE' = TABLE ;
TAB1.'TITRE' . 1 = 'MOT' 'Rho paroi int' ;
TAB1.'TITRE' . 2 = 'MOT' 'Rvp paroi int' ;
TAB1.'TITRE' . 3 = 'MOT' 'Rinc paroi int' ;
TAB1.'TITRE' . 4 = 'MOT' 'Rvsat paroi int' ;

Si GRAPH;
dess (evrhpi et evrvpi et evrinci et evrvsati) titx 'm' tity 'kg/m^3'
 TITR ' Densites en paroi' 'GRIL' LEGE tab1;
Finsi;
*********** Fin Densités *******************************

*************** Fractions massiques ********************
yvi=rvpi / rhpi;
evyvi = evol chpo yvi lzi;
yvsati= rvsati / rhpi ;
evyvsati= evol chpo yvsati lzi;
evdyv=evyvi - evyvsati;

TAB1.'TITRE' . 1 = 'MOT' 'Yv paroi int' ;
TAB1.'TITRE' . 2 = 'MOT' 'Yvsat paroi int' ;
TAB1.'TITRE' . 3 = 'MOT' 'Yv - Yvsat' ;
Si GRAPH;
 dess (evyvi et evyvsati et evdyv) 'GRIL' lege TAB1 titx 'm'
  tity 'kg/m^3' titr ' Fractions massiques vapeur en paroi'
  ybor 0. 0.4;
Finsi;
*********** Fin Fractions massiques ********************

list rxt.TIC.'Qc'  ;

Lqc1 =Prog
   0.0000       0.0000       0.0000       4.41111E-04  3.03442E-03
   5.60246E-03  1.09258E-02  1.41658E-02  1.92632E-02  2.14018E-02
   2.53586E-02  2.66048E-02  2.86462E-02  3.13518E-02  3.30313E-02
   3.59090E-02  3.71120E-02  3.89070E-02  4.01531E-02  4.10586E-02
   4.18593E-02  4.25288E-02  4.30960E-02  4.35875E-02  4.40239E-02
   4.44142E-02  4.47644E-02  4.50807E-02  4.53682E-02  4.56301E-02
   4.58690E-02  4.60875E-02  4.62876E-02  4.64711E-02  4.66395E-02;

Lqc2 =Prog
   4.67943E-02  4.69366E-02  4.70677E-02  4.71885E-02  4.73000E-02
   4.74030E-02  4.74983E-02  4.75865E-02  4.76683E-02  4.77442E-02
   4.78148E-02  4.78804E-02  4.79415E-02  4.79985E-02  4.80517E-02
   4.81014E-02  4.81480E-02  4.81915E-02  4.82324E-02  4.82708E-02
   4.83068E-02  4.83407E-02  4.83725E-02  4.84026E-02  4.84309E-02
   4.84576E-02  4.84828E-02  4.85067E-02  4.85292E-02  4.85505E-02
   4.85707E-02  4.85899E-02  4.86080E-02  4.86252E-02  4.86415E-02;

Lqc3 =Prog
   4.86570E-02  4.86717E-02  4.86856E-02  4.86989E-02  4.87115E-02
   4.87235E-02  4.87348E-02  4.87457E-02  4.87560E-02  4.87658E-02
   4.87751E-02  4.87839E-02  4.87924E-02  4.88004E-02  4.88080E-02
   4.88153E-02  4.88222E-02  4.88287E-02  4.88349E-02  4.88408E-02
   4.88465E-02  4.88518E-02  4.88569E-02  4.88617E-02  4.88662E-02
   4.88705E-02  4.88746E-02  4.88785E-02  4.88821E-02  4.88856E-02
   4.88888E-02;

Lqc = lqc1 et Lqc2 et Lqc3;


tic=rxt.'TIC' ;
ERQc=SOMM( abs (lqc  - tic.'Qc' ) )  ;

Mess 'ERQc=' ERQc  ;

Si (ERQc '>' 1.e-4) ; erreur 5 ; Finsi ;

Si GRAPH ;

$vtf=rxt.'GEO'.'$vtf' ;
mt  =doma $vtf maillage ;
Mpl1=chan 'QUAF' plan1 ;
Mpl4=chan 'QUAF' plan4 ;
ELIM (mt et Mpl1 et Mpl4) epsi ;
$mpl1= mode Mpl1 'NAVIER_STOKES' MACRO ;
$mpl4= mode Mpl4 'NAVIER_STOKES' MACRO ;
plan1= doma $mpl1 maillage ;
plan4= doma $mpl4 maillage ;
plan=plan1 et plan4 ;

paroif = rxt.'GEO'.'paroif';
rho=rxt.'TIC'.'RHO';
rvp=rxt.'TIC'.'RVP';
tf=rxt.'TIC'.'TF';
un=rxt.'TIC'.'UN' ;
un1=redu un plan ;
ung= vect un1 1. ux uy uz jaune ;
trace ung plan ;
opti isov suli ;
trace rho plan 'TITRE'' Rho' ;
trace rvp plan 'TITRE'' Rvp' ;
trace tf  plan 'TITRE'' Tf ' ;

trace rho paroif  'TITRE'' Rho' ;
trace rvp paroif  'TITRE'' Rvp' ;
trace Tf  paroif  'TITRE'' Tf ' ;
Fcond = tic.'Fcondw';
trace Fcond paroif  'TITRE' ' Flux de condensation Kg / m**2 ' ;

 axe  = p0 d nz1 (p0 plus v1) ;
 axe = chan axe 'QUAF' ;
 elim (axe et mt) epsi ;

evr= evol 'CHPO' rho axe ;
dess evr 'TITRE' ' Rho axe ';

evt = evol 'CHPO' Tf  axe ;
dess evt 'TITRE' ' Temperature axe ';

evvp= evol 'CHPO' rvp axe ;
dess evvp 'TITRE' ' rvp axe ';



'FINSI' ;

FIN ;


