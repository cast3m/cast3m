* fichier : riccoq.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*******************************************************
*  Cas test de l'implantation numerique du modele
*  RICCOQ - Formulation COQUE Mince
*
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
*     Benjamin.Richard@lmt.ens-cachan.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
*******************************************************
* Choix du cas de charge
* - 1 : Traction/compression - charge dech. (membrane)
* - 2 : Idem - sens oppose

* - 3 : Traction/compression - charge dech. (flexion )
* - 4 : Idem - sens oppose
* - 5 : Idem avec le deux cycle en plus

* - 6 : Distorsion et cisaillement pur plan

* - 7 : Couplage traction/compression
* - 8 :
* - 9 :
*******************************************************
* Choix du cas de charge
ncas  = 1;

*******************************************************
* Choix du modle
*******************************************************
ric0 = VRAI;
sme0 = FAUX;

*sme0 = VRAI;
*ric0 = FAUX;

*graph0 = VRAI;
graph0 = FAUX;

rndm = 0;
*rndm = 1;

*******************************************************
* Parametres matriaux
*******************************************************

* Paramtres physiquement accessibles------------------

* Proprietes elastiques du beton
EBN  = 36000E6;
NUBN = 0.2;

* Epaisseur de la coque
THN  = 1.0;

* Resistance en traction
ftn  = 3.6E6;

* Energie de fissuration
gft  = 160;

* Resistance en compression
fcn  = 10E6;
*fcn  = 36E6;

* Energie de fissuration
gfc  = 2000;

*******************************************************
* Options de calcul
*******************************************************
OPTION DIME 3 MODE TRID ELEM QUA4;
DENSITE (0.25);
NAME = 'f_delta_fin.txt';

*******************************************************
* Maillage
*******************************************************
P1 = 0. 0. 0.;
P2 = 2. 0. 0.;
P3 = 2. 1. 0.;
P4 = 0. 1. 0.;

L1 = P1 DROIT 13 P2 ;
L2 = P2 DROIT P3 ;
L3 = P3 DROIT 13 P4 ;
L4 = P4 DROIT P1 ;

LTOT = L1 ET L2 ET L3 ET L4 ;

S1   = SURF LTOT PLANE;

SI (graph0);
TRAC s1;
FINSI;

*******************************************************
* Conditions aux limites
*******************************************************

* Conditions selon le chargement-----------------------

SI (ET (> ncas 0) (< ncas 3));
CL1 = BLOQ L4 UX;
CL2 = BLOQ P1 DEPL ROTA;
CL3 = BLOQ L2 UX;
CLTOT = CL1 ET CL2 ET CL3;
D1    = DEPI CL3 1;
FINSI;

SI (ET (> ncas 2) (< ncas 6));
CL1 = BLOQ L4 RY UX;
CL2 = BLOQ P1 DEPL ROTA;
CL3 = BLOQ L2 RY;
CLTOT = CL1 ET CL2 ET CL3;
D1    = DEPI CL3 1;
FINSI;

SI (ET (> ncas 5) (< ncas 7));

CL1 = BLOQ P1 DEPL ROTA;
NN = NBEL L4;
D0 = 3.0E-4;
CHPO1 = MANU CHPO (INVE L4) 1 'UX'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO1 = CHPO1 * D0;
CL21 = BLOQ L4 UY;
CL22 = BLOQ L4 UX;
D2  = DEPI CL22 CHPO1;

NN = NBEL L1;
CHPO2 = MANU CHPO (L1) 1 'UY'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO2 = CHPO2 * D0;
CL31 = BLOQ L1 UX;
CL32 = BLOQ L1 UY;
D3  = DEPI CL32 CHPO2;

NN = NBEL L2;
CHPO3 = MANU CHPO (L2) 1 'UX'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO3 = CHPO3 * D0;
CL41 = BLOQ L2 UX;
CL42 = BLOQ L2 UY;
D41  = DEPI CL41 CHPO3;
D42  = DEPI CL42 D0;

NN = NBEL L3;
CHPO4 = MANU CHPO (INVE L3) 1 'UY'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO4 = CHPO4 * D0;
CL51 = BLOQ L3 UX;
CL52 = BLOQ L3 UY;
D51  = DEPI CL51 D0;
D52  = DEPI CL52 CHPO4;

DF1 = FORCE ((-1 * D0) 0 0) L1;
DF2 = FORCE (0 D0 0) L2;
DF3 = FORCE (D0 0 0) L3;
DF4 = FORCE (0 (-1 * D0) 0) L4;
DFT = DF1 ET DF2 ET DF3 ET DF4;

SI (graph0);
vect1 = vect chpo1 50 'DEPL' ROUG;
vect2 = vect chpo2 50 'DEPL' VERT;
vect3 = vect chpo3 50 'DEPL' BLEU;
vect4 = vect chpo4 50 'DEPL' JAUN;
TRAC S1 (VECT1 ET VECT2 ET VECT3 ET VECT4);
FINSI;

CLTOT = CL1 ET CL21 ET CL22 ET CL31 ET CL32 ET CL41 ET
CL42 ET CL51 ET CL52;

DTOT = D2 ET D3 ET D41 ET D42 ET D51 ET D52;
FINSI;

SI (ET (> ncas 6) (< ncas 10));
CL1 = BLOQ P1 DEPL ROTA;
CL2 = BLOQ L4 UX RY;
CL3 = BLOQ L2 RY;
CL4 = BLOQ L2 UX;
CLTOT = CL1 ET CL2 ET CL3 ET CL4;
D1    = DEPI CL3 6.0E-3;
D2    = DEPI CL4 2.0E-4;
DTOT  = D1 ET D2;
FINSI;

*******************************************************
* Choix des modles et des matriaux associs
*******************************************************

* Cas du modle BA-------------------------------------
SI (ric0);

* Modle-----------------------------------------------
MODTOT  = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
          ENDOMMAGEMENT RICCOQ DKT COQ4 CONS COUCHE1;

* Materiaux--------------------------------------------

SI (EGA rndm 1);
ftn      = ALEA BANDES_TOURNANTES MODTOT EXPO SIGMA
          (0.001*ftn) MOYENNE ftn LAMBDA 0.1;

fcn      = ALEA BANDES_TOURNANTES MODTOT EXPO SIGMA
          (0.001*fcn) MOYENNE fcn LAMBDA 0.1;

*TRAC ftn MODTOT;
*TRAC fcn MODTOT;

FINSI;

CHAM1  = MANU CHML MODTOT BTRA 1. TYPE CARACTERISTIQUES RIGIDITE;
CHAM2  = INTG CHAM1 MODTOT ELEM BTRA;
TAIL1  = (2 ** (1.0 / 2.0)) * CHAM2 ** (1.0 / 2.0);

GFT = GFT / THN;
EPSUT = (TAIL1) / GFT;

GFC = GFC / THN;
EPSUC = (TAIL1) / GFC;

MATTOT  = MATE MODTOT YOUN EBN  NU   NUBN
                      FT   ftn  EPUT EPSUT
                      FC   fcn  EPUC EPSUC;

CAR1 = CARA MODTOT EPAI THN EXCE 0.0;
MATTOT = MATTOT ET CAR1;
FINSI;

* Cas du modle multicouche----------------------------
SI (sme0);
MOD1   = MODE S1 MECANIQUE ELASTIQUE
ISOTROPE DKT COQ4 CONS BETO;

MODTOT = MOD1;

mat1 = MATE mod1 YOUN EBN  NU NUBN;
car1 = CARA MOD1 EPAI THN ;
mat1 = mat1 et car1;

mattot = mat1;
FINSI;

*******************************************************
* Choix des cas de charge
*******************************************************
SI (EGA NCAS 1);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. 1E-3 0. -5E-3 1.5E-3;
LIS1 = PROG 0. pas 0.01 1;
FINSI;

SI (EGA NCAS 2);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. -5E-4 0. 2E-4 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 3);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. 6E-3 0. -6E-3 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 4);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. -6E-3 0. 6E-3 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 5);
LI1  = PROG 0. PAS 1 8;
LI2  = PROG 0. -6E-3 0. 6E-3 0. -6E-3 0. 6E-3 0.;
LIS1 = PROG 0. pas 0.05 8;
FINSI;

SI (EGA NCAS 6);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. -1. 0. 1. 0.;
LIS1 = PROG 0. pas 0.01 4;
FINSI;

SI (EGA NCAS 7);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. 1. 0. -1. 0.;
LIS1 = PROG 0. pas 0.05 4;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (EGA NCAS 8);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. -1. 0. 1. 0.;
LIS1 = PROG 0. pas 0.05 4;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (EGA NCAS 9);
LI1  = PROG 0. PAS 1 8;
LI2  = PROG 0. 1 0. -1 0. 1 0. -1 0.;
LIS1 = PROG 0. pas 0.05 8;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (< ncas 6);
EV   = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;
CHATOT = CHA1;
FINSI;

SI (ET (> ncas 5) (< ncas 7));
EV   = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' DTOT EV ;
CHA2 = CHAR 'MECA' DFT  EV ;
CHATOT = CHA1 ET CHA2;
FINSI;

*******************************************************
* Preparation au calcul
*******************************************************
TAB1                      = TABLE                     ;
TAB1.BLOCAGES_MECANIQUES  = CLTOT                     ;
TAB1.MODELE               = MODTOT                    ;
TAB1.MOVA                 = RIEN                      ;
TAB1.CHARGEMENT           = CHATOT                    ;
TAB1.CARACTERISTIQUES     = MATTOT                    ;
TAB1.TEMPS_CALCULES       = LIS1                      ;
*******************************************************
* Calcul
*******************************************************
PASAPAS TAB1 ;

*******************************************************
* Post traitement
*******************************************************
nn=dime tab1.deplacements;
nn=nn-1;

SI (graph0);
SI (ric0);
trac modtot (exco tab1.variables_internes.nn 'DT  ');
trac modtot (exco tab1.variables_internes.nn 'DC  ');
trac modtot (exco tab1.variables_internes.nn 'XYDC');
FINSI;

SI (sme0);
*trac modtot (exco tab1.variables_internes.nn 'D   ');
FINSI;
FINSI;

SI (OU (EGA ncas 1) (EGA ncas 2));
ev2=@global tab1 CL3 EV FX;
SI (graph0);
EV2 = EV2 / THN;
*@EXCEL1 EV2 name;
DESS EV2;
FINSI;
FINSI;

SI ( ET (> ncas 2) (< ncas 6));
evm = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.contraintes.I;
var1 = chan chpo modtot var0;
tot  = @total var1 L4 'M22 ';
evm = evm et (prog tot);
I = I + 1;
FIN BOU1;
evg=@global tab1 CL3 EV FX;
lis1 = extr evg 'ABSC';
ev2 = evol manu lis1 evm;
SI (graph0);
dess ev2;
FINSI;
FINSI;

SI (ega ncas 6);
evm = prog 0;
dep0 = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.contraintes.I;
dep1 = tab1.deplacements.I;
tot  = EXTR VAR0 'N12 ' 1 1 1;
evm = evm et (prog tot);
dep0 = dep0 et (prog (EXTR dep1 'UX  ' P3));
I = I + 1;
FIN BOU1;
ev2 = evol manu dep0 evm;
SI (graph0);
ev2 = ev2 / thn;
dess ev2;
FINSI;
FINSI;

SI (ET (> ncas 6) (< ncas 10));
EVG1 = @GLOBAL TAB1 EV CL4 FX;
LISG1 = EXTR EVG1 ABSC;
LISG1 = (LISG1 * 2.0E-4);

EVG2 = @GLOBAL TAB1 EV CL3 FX;
LISG2 = EXTR EVG2 ABSC;
LISG2 = (LISG2 * 6E-3);

evm1 = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.reactions.I;
tot  = @total var0 L4 'FX  ';
evm1 = evm1 et (prog tot);
I = I + 1;
FIN BOU1;

evm2 = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.reactions.I;
tot  = @total var0 L4 'MY  ';
evm2 = evm2 et (prog tot);
I = I + 1;
FIN BOU1;

EV21 = EVOL MANU LISG1 evm1;
EV22 = EVOL MANU LISG2 evm2;

SI (graph0);
DESS EV21;
DESS EV22;
FINSI;

FINSI;

SI (graph0);
def0 = defo s1 tab1.deplacements.nn 1 bleu;
def1 = defo s1 tab1.deplacements.nn 2000  rouge;
trac modtot def1 (exco tab1.variables_internes.nn 'DT  ');
trac modtot def1 (exco tab1.variables_internes.nn 'DC  ');
FINSI;

var0 = tab1.variables_internes.nn;
ee   = extr var0 'DT  ' 1 1 1;
list ee;

message 'erreur relative : ';
err = (ee - 0.97549) / 0.97549;
list err;
si (> err 3.0E-2);
erreur(5);
finsi;

*******************************************************
* FIN DE L ANALYSE
*******************************************************

FIN;








