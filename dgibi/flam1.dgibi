* fichier :  flam1.dgibi
************************************************************************
* Section : Mecanique Flambage
************************************************************************
*           Test Flam1.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*  TEST FLAM1
*
*           FLAMBAGE EULERIEN D'UNE POUTRE
*             ENCASTREE A UNE EXTREMITE
*
* Dans cet exemple on se propose d'étudier le flambage
* d'une poutre encastrée à une de ses extrémités et
* soumise à une force parallele à la poutre et
* appliquée à l'extrémité libre de la poutre.
*
* Le flambage est visualisé en fin de calcul et le
* multiplicateur de chargement calculé est comparé à
* celui obtenu par la théorie (41.123).
*******************************************************

GRAPH = 'N';

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

TITRE ' FLAMBAGE D UNE POUTRE ENCASTREE ' ;
OPTI DIME 2 ELEM SEG2  MODE PLAN CONT ;
OPTI EPSI LINEAIRE;


TEMPS ;
*
*------------- DEFINITION DU MAILLAGE -----------------
*
DENSITE  1. ;
P1 = 50. 0. ;
P2 = 50. 10. ;
L1 = P1 D P2 ;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' L1 ;
FINSI;
*
*--------- CREATION DU MODELE ET DE MMODEL ------------
*
MOD1 = MODE L1 MECANIQUE ELASTIQUE COQ2;

*
* -------- DEFINITION DES CARACTERISTIQUES ------------
* ----------- MATERIELLES ET GEOMETRIQUES  ------------
*
MAT1   = MATE MOD1 YOUN   20000.    NU 0.3     ;
CAR1   = CARA MOD1 EPAI 1. ;
MAT1=MAT1 ET CAR1;
*
*--------- CALCUL DES RIGIDITES ELEMENTAIRES ----------
*----------- ET DEFINITIONS DES BLOCAGES --------------
*
RIG1   = RIGI  MOD1 MAT1 ;
BL1    = BLOQUE DEPL ROTA P1 ;
RIG11  = RIG1 ET BL1;
*
*------------- DEFINITION DU CHARGEMENT ---------------
*
FE1 = FORC ( 0 -1. ) P2 ;
*
*------- RESOLUTION ET CALCUL DES CONTRAINTES ---------
*
DEP1 = RESO  RIG11    FE1 ;
SIG1 = SIGM 'LINE' MOD1 MAT1  DEP1 ;
*
*--- CALCUL DE LA MATRICE DES CONTRAINTES INITIALES ---
*
KSI1   = KSIGMA  MOD1 SIG1 CAR1 'FLAM' ;
*
*----------- INITIALISATION DE LA TABLE ---------------
*--------- EN ENTREE DE LA PROCEDURE FLAMBAGE ---------
*
ETAB        = TABLE    ;
ETAB.'CLIM' = BL1 ;
ETAB.'SIG1' = SIG1;
ETAB.'MATE' = MAT1;
ETAB.'LAM1' = 0.001 ;
ETAB.'LAM2' = 100. ;
ETAB.'NMOD' = 1   ;
ETAB.'OBJM' = MOD1;
*
*--------- APPEL A LA PROCEDURE DE FLAMBAGE -----------
*
SAUT PAGE ;
STAB = FLAMBAGE ETAB ;
*
*------------ RECUPERATION DES RESULTATS --------------
*
LA1  =  STAB . 1 . LAMB ;
SAUT PAGE ;
*
*------------ DESSIN DU MODE DE FLAMBAGE --------------
*
MM1 = STAB . 1 . DEPL ;
DE  = DEFO MM1 L1 1. VERT ;
DA  = DEFO MM1 L1 0. NOIR  ;

SI (NEG GRAPH 'N');
  TRAC ( DE ET DA ) ;
FINSI;

LREF = 41.123;
ERR=100*(ABS(LREF - LA1)/LREF);
*
MESS ' ' ;
MESS ' ' ;
MESS ' MULTIPLICATEUR DU CHARGEMENT ' ;
MESS '                              ' ;
MESS '   SOLUTION ANALYTIQUE  : ' LREF;
MESS '   SOLUTION CALCULEE    : ' LA1 ;
MESS '     SOIT UN ECART DE                : ' ERR '%' ;

TEMPS ;

*------------ CODE DE BON FONCTIONNEMENT --------------
SI (ERR < 1e-3);
ERRE 0;
SINON;
ERRE 5;
FINSI;

FIN;






