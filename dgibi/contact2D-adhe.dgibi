* fichier : contact2D-adhe.dgibi
*
* TEST  du MODELE CONTACT AVEC COMPOSANTE MATERIAU ADHE
*
*
OPTI DIME 2 ELEM QUA4 ;
OPTI DEBU 1 ;
*
* Maillage
NEL1 = 10 ;
*NEL1 = 14 ;
*NEL1 = 2 ;
P1 = -1. 1.E-3 ; P2 = 1. 1.E-3 ;
L1 = DROI NEL1 P1 P2 ;
S1 = L1 TRAN NEL1 (0. 1.) ;
S1D = S1 'COTE' 2 ;
S1H = S1 'COTE' 3 ;
S1G = S1 'COTE' 4 ;
*
NEL2 = 14 ;
*NEL2 = 5 ;
P3 = -1.5 0. ; P4 = 1.5 0. ;
L2 = DROI NEL2 P3 P4 ;
S2 = L2 TRAN NEL2 (0. -1.) ;
S2D = S2 'COTE' 2 ;
S2B = S2 'COTE' 3 ;
S2G = S2 'COTE' 4 ;
*
LC = 'IMPO' 'MAIL' 'SYME' ('INVE' L2) L1 ;
*
* Conditions aux limites
CL1 = 'BLOQ' 'UX' (S2D 'ET' S2G) ;
CL2 = 'BLOQ' 'UY' S2B ;
CL3 = 'BLOQ' 'UY' S1H ;
CL4 = 'BLOQ' 'UX' (S1G POIN INITIAL) ;
CL4 = 'BLOQ' 'UX' (S1D 'ET' S1G) ;
CL4 = 'BLOQ' 'UX' S1G ;
CLT = CL1 'ET' CL2 'ET' CL3 'ET' CL4 ;
*
* Chargements
COOX = 'COOR'1 S1H ;
*DEIM = 'DEPI' CL3 (-1.E-3 * ('ABS' (('ABS' COOX) - 2.))) ;
DEIM = 'DEPI' CL3 (-1.E-3 * (COOX + ('ABS' ('MINI' COOX)))) ;
LTPS = 'PROG' 0. 1.  2.  3.  4. ;
LCOE = 'PROG' 0. 2. -1. -2. -3. ;
EVZ  = 'EVOL' 'MANU' 'TEMPS' LTPS 'COEF' (30. * LCOE) ;
CHAT = 'CHAR' 'DIMP' DEIM EVZ ;
*
* Modeles
MOD1 = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' ;
MAT1 = 'MATE' MOD1 'YOUN' 200.E9 'NU' 0.3 ;
MOD2 = 'MODE' S2 'MECANIQUE' 'ELASTIQUE' ;
MAT2 = 'MATE' MOD2 'YOUN' 100.E9 'NU' 0.3 ;
*
MODC = 'MODE' LC 'CONTACT' ;
MATC = 'MATE' MODC 'ADHE' 7.E8 ;
MODT = MOD1 'ET' MOD2 'ET' MODC ;
MATT = MAT1 'ET' MAT2 'ET' MATC ;
*
* Resolution
LTIME = 'PROG' 0. 0.5 1. PAS 0.2 4.0 ;

TAB1 = 'TABL' ;
TAB1.'MAITOT' = S1 ET S2 ;
TAB1.'MAICON'= LC ;
TAB1.'MODELE' = MODT ;
TAB1.'CARACTERISTIQUES' = MATT ;
TAB1.'BLOCAGES_MECANIQUES' = CLT ;
TAB1.'CHARGEMENT' = CHAT ;
TAB1.'TEMPS_CALCULES' = LTIME ;
PASAPAS TAB1 ;
*
* Post-traitement
OPTI TRAC PSC ;
DEP = TAB1.'DEPLACEMENTS' ;
DEPP = TAB1.'DEPLACEMENTS'. 0 ;
CONF0 = 'FORM' ;
'REPE' BCL ('DIME' DEP) ;
  TPSZ = TAB1.'TEMPS'. (&BCL - 1) ;
  TITZ = 'CHAI' 'Deformee a l instant 'TPSZ ;
  DEPZ = DEP. (&BCL-1) ;
  DEF0 = 'DEFO' (S1 'ET' S2) DEPZ 0. ;
  DEF1 = 'DEFO' (S1 'ET' S2) DEPZ 1. ;
  TRAC (DEF0 'ET' (DEF1 COUL ROUG)) 'TITR' TITZ ;
'FIN' BCL ;

FIN ;


