* fichier :  nouailhas_a1.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
**********************************************************************
*                                                                    *
* fichier : nouailhas_a1.dgibi                                       *
*                                                                    *
**********************************************************************
*                                                                    *
* fait par PLG le 03-09-97                                           *
*                                                                    *
**********************************************************************
*                                                                    *
* But : ktest pour la loi viscoplastique NOUAILHAS_A                 *
*                                                                    *
**********************************************************************
i_trace = vrai ;
*
debproc perso1 plg*TABLE ;
*
li_t = plg.depo.li_tp ;
li_d = plg.depo.li_def ;
*
ind_1 = index (plg.temps) ;
nb_1 = dime ind_1 ;
*
v_t = plg.temps.(ind_1.nb_1) ;
v_d = extr (plg.deformations_inelastiques.(ind_1.nb_1)) EIYY 1 1 1 ;
*
li_t = li_t et (prog v_t) ;
li_d = li_d et (prog v_d) ;
ev_d = evol manu ' ' li_t ' ' li_d ;
*
plg.depo.li_tp  = li_t ;
plg.depo.li_def = li_d ;
*
si faux ;
dess ((plg.depo.ev_ref) et ev_d) NCLK ;
finsi ;
finproc ;
**********************************************************************
option dime 2 ;
option elem qua8 ;
option mode plan cont ;
*
a00 = 0.0 0.0 ;
a01 = 1.0 0.0 ;
a02 = 1.0 1.0 ;
a03 = 0.0 1.0 ;
*
la00a01 = droi 1 a00 a01 ;
la01a02 = droi 1 a01 a02 ;
la02a03 = droi 1 a02 a03 ;
la03a00 = droi 1 a03 a00 ;
*
tot = daller la00a01 la01a02 la02a03 la03a00 ;
**********************************************************************
m_model = model tot MECANIQUE ELASTIQUE VISCOPLASTIQUE NOUAILHAS_A
 CONS UN ;
*
cst_youn = 260000.0 ;
cst_nu   = 0.3 ;
cst_alph = 1.0e-6 ;
cst_rho  = 1.0e-8 ;
cst_n    = 1.0 ;
cst_kk   = 200.0 ;
cst_k0   = 10000.0 ;
cst_alf  = 0.0 ;
cst_alfr = 0.0 ;
cst_alfk = 0.0 ;
*
cst_cl1  = 7000.0 ;
cst_dnl1 = 0.0 ;
cst_ptm1 = 1.0 ;
cst_gdm1 = 20000.0 ;
*
cst_cl2  = 0.0 ;
cst_dnl2 = 0.0 ;
cst_ptm2 = 0.0 ;
cst_gdm2 = 0.0 ;
*
cst_phi  = 0.0 ;
cst_b    = 0.0 ;
cst_qsta = 0.0 ;
cst_qmax = 1.0 ;
cst_mu   = 0.0 ;
cst_m    = 0.0 ;
cst_gama = 0.0 ;
cst_eta  = 1.0 ;
*
m_mater = mater m_model      YOUN cst_youn NU   cst_nu   ALPH cst_alph
 RHO  cst_rho  N    cst_n    KK   cst_kk   K0   cst_k0   ALF  cst_alf
 CL1  cst_cl1  DNL1 cst_dnl1 PTM1 cst_ptm1 GDM1 cst_gdm1 CL2  cst_cl2
 DNL2 cst_dnl2 PTM2 cst_ptm2 GDM2 cst_gdm2 PHI  cst_phi  B    cst_b
 QSTA cst_qsta QMAX cst_qmax MU   cst_mu   GAMA cst_gama ALFK cst_alfk
 ETA  cst_eta  ALFR cst_alfr M    cst_m ;
**********************************************************************
m01bloca = bloque UX la03a00 ;
m02bloca = bloque UY la00a01 ;
m_bloca = m01bloca et m02bloca ;
*
t00temps = 0.0 ;
t01temps = t00temps +  0.001 ;
t02temps = t01temps + 10.000 ;
t03temps = t02temps +  0.001 ;
t04temps = t03temps +  5.000 ;
t05temps = t04temps +  0.001 ;
t06temps = t05temps +  5.000 ;
*
li_temps = prog t00temps t01temps t02temps t03temps t04temps t05temps
 t06temps ;
*
t00press =   0.0 ;
t01press = -500.0 ;
t02press = -500.0 ;
t03press =  -0.1 ;
t04press =  -0.1 ;
t05press = -500.0 ;
t06press = -500.0 ;
*
li_press = prog t00press t01press t02press t03press t04press t05press
 t06press ;
*
ev_press = evol manu 'TEMPS' li_temps 'P' li_press ;
*
m01press = press MASSE m_model 1.0 la02a03 ;
m_charg = charg MECA m01press ev_press ;
**********************************************************************
p01nbpas =  2.0 ;
p02nbpas = 40.0 ;
p03nbpas =  2.0 ;
p04nbpas =  5.0 ;
p05nbpas =  2.0 ;
p06nbpas = 20.0 ;
*
p01pas   = (t01temps - t00temps) / p01nbpas ;
p02pas   = (t02temps - t01temps) / p02nbpas ;
p03pas   = (t03temps - t02temps) / p03nbpas ;
p04pas   = (t04temps - t03temps) / p04nbpas ;
p05pas   = (t05temps - t04temps) / p05nbpas ;
p06pas   = (t06temps - t05temps) / p06nbpas ;
*
p01lipas = prog (t00temps + p01pas) pas p01pas t01temps ;
p02lipas = prog (t01temps + p02pas) pas p02pas t02temps ;
p03lipas = prog (t02temps + p03pas) pas p03pas t03temps ;
p04lipas = prog (t03temps + p04pas) pas p04pas t04temps ;
p05lipas = prog (t04temps + p05pas) pas p05pas t05temps ;
p06lipas = prog (t05temps + p06pas) pas p06pas t06temps ;
*
p_lipas = p01lipas et p02lipas et p03lipas et p04lipas et p05lipas et
 p06lipas ;
**********************************************************************
li_defvg = prog  0.0     0.02312 0.03720 0.04847 0.05887 0.06899
 0.07903 0.08904 0.09904 0.1090  0.1190  0.1190  0.1306  0.1394
 0.1467  0.1530  0.1587  0.1642  0.1694  0.1746  0.1797  0.1847 ;
*
li_tps   = prog    0.0     1.001   2.001   3.001   4.001   5.001
   6.0     7.0     8.0     9.0    10.0    15.0    15.5    16.0
  16.5    17.0    17.5    18.0    18.5    19.0    19.5    20.0 ;
ev_ref  = evol manu 't' li_tps 'def' li_defvg ;
*dess ev_ref NCLK ;
**********************************************************************
p_table = table ;
p_table.modele                    = m_model ;
p_table.caracteristiques          = m_mater ;
p_table.blocages_mecaniques       = m_bloca ;
p_table.chargement                = m_charg ;
p_table.temps_calcules            = p_lipas ;
*
si i_trace ;
p_table.procedure_perso1          = VRAI ;
p_table.depo = table ;
p_table.depo.ev_ref = ev_ref ;
p_table.depo.li_tp  = prog ;
p_table.depo.li_def = prog ;
finsi ;
**********************************************************************
TMASAU=table;
p_table . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
pasapas p_table ;
**********************************************************************
ind_1 = index (p_table.deformations_inelastiques) ;
nb_1 = dime ind_1 ;
*
v_d = extr (p_table.deformations_inelastiques.(ind_1.nb_1))
 EIYY 1 1 1 ;
*
v_d = abs(v_d) ;
v_diff = abs(v_d - 0.1847) / 0.1847 ;
*
si (v_diff > 0.01) ;
 erre 5 ;
sinon ;
 erre 0 ;
finsi ;
**********************************************************************
fin ;







