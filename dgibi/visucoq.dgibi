* fichier : visucoq.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
************************************************************************
************************************************************************
*                                                                      *
*             Exemple d'utilisation de coq2mas                         *
*         Visualisation 3D de résultats de calcul coque multicouche    *
*                                                                      *
*                      D. Combescure - Juillet 2007                    *
*                      Laboratoire DYN - CEA Saclay                    *
************************************************************************
************************************************************************
*
*FLAGVISU = VRAI;
*FLAGLONG = VRAI;
*
FLAGVISU = FAUX;
*FLAGLONG = FAUX;
*
opti dime 3 elem tri3;
R = 10.;
H = -1.;
ep1 = 1.0;
ep2 = 0.20;
p1 = R 0. 0.;
p2 = 0. R 0.;
p3 = ((-1.)*R) 0. 0.;
p4 = 0. ((-1.)*R) 0.;
p0 = 0. 0. H;
p0b = 0. 0. (-2.*H);
*
n1 = 20;
lig0 = (CER3 n1 p1 p2 p3)
    ET (CER3 n1 p3 p4 p1);
sur0 = SURF lig0 SPHE p0;
sur0b = SURF lig0 SPHE p0b;
*
mod1 = MODE sur0 mecanique elastique dkt cons 'Couche 1';
mat1  = MATE mod1 YOUN 30000.D6 NU 0.25 RHO 2400.
                  EPAI ep1 EXCE (1.*ep1);
mod2 = MODE sur0 mecanique elastique dkt cons 'Couche 2';
mat2  = MATE mod2 YOUN 30000.D6 NU 0.25 RHO 2400.
                  EPAI ep1 EXCE ((-1.)*ep1);
mod3 = MODE sur0 mecanique elastique dkt cons 'Couche 3';
mat3  = MATE mod3 YOUN 30000.D6 NU 0.25 RHO 2400.
                  EPAI ep1 ;
mod4 = MODE sur0b mecanique elastique dkt cons 'Couche 4';
mat4  = MATE mod4 YOUN 30000.D6 NU 0.25 RHO 2400.
                  EPAI ep2 ;
modtot = mod1 et mod2 et mod3 et mod4;
mattot = mat1 et mat2 et mat3 et mat4;
rigtot = rigi modtot mattot;
mastot = mass modtot mattot;
*
tabtot = table;
mesh3D = coq2mas modtot mattot tabtot;
*
SI FLAGVISU;
 trac (sur0 et sur0b) cach;
 trac mesh3D cach;
FINSI;
*
load = mastot * (manu chpo (sur0 et sur0b) 1 UZ -9.81);
bl0 = bloq depl lig0;
*
*
DEPJ = RESO (RIGTOT et BL0) load;
CONJ = SIGMA DEPJ MODTOT MATTOT;
*
TABTOT. 'DEPLACEMENTS' = TABLE;
TABTOT. 'DEPLACEMENTS' . 1 = DEPJ;
TABTOT. 'CONTRAINTES' = TABLE;
TABTOT. 'CONTRAINTES' . 1 = CONJ;
*
MESH3D = COQ2MAS MODTOT MATTOT TABTOT;
*
SI FLAGVISU;
 dep3D = (TABTOT.'DEPLACEMENTS_VOLUMIQUE'. 1 . TOTAL);
 trac (defo mesh3D dep3D) cach;
 mesh3Df = TABTOT.'MAILLAGE_FIBRE_MOYENNE' .  TOTAL;
 con3Df =  EXCO (TABTOT.'CONTRAINTES_FIBRE_MOYENNE'. 1 . TOTAL)
          SMXX;
 trac mesh3Df con3Df cach;
 mesh3D = TABTOT.'MAILLAGE_VOLUMIQUE' .  TOTAL;
 con3D =  EXCO (TABTOT.'CONTRAINTES_VOLUMIQUE'. 1 . TOTAL)
          SMXX;
 trac mesh3D con3D cach;
* Pour relecture avec PARAVIEW
 opti sort 'visucoq.inp';
 sort avs mesh3D (TABTOT.'CONTRAINTES_VOLUMIQUE'. 1 . TOTAL);
FINSI;
*
FIN;
*






