* fichier :  joi44.dgibi
************************************************************************
* Section : Thermique Mecanique
* Section : Mecanique Elastique
************************************************************************
*           Test Joi44.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
OPTI ECHO 0 ;
SAUT PAGE ;
TEMPS ;
*=====================================================*
*                                                     *
*                          TEST JOI44                 *
*                                                     *
*                    CALCUL DES CONTRAINTES           *
*                  THERMIQUES SUR UN JOINT 3D         *
*                                                     *
*      Un joint 3D JOI4 a sa surface inferieure       *
*      encastree. Sa surface superieure est libre.    *
*      Un champ de temperatures est donne, de type    *
*      CHAMELEM, de sous type temperatures avec le    *
*      MCHAML s'appuyant d'une part sur un objet de   *
*      type MAILLAGE, d'autre part sur un objet de    *
*      type MMODEL.                                   *
*      On cherche a calculer le champ des contraintes *
*      thermiques induit par ce champ de temperatures.*
*                                                     *
*      Solution analytique :                          *
*                                                     *
*             SIGMA = K * ALPN * DELTA(T)             *
*      ou                                             *
*      K = raideur dans la direction perpendiculaire  *
*          au plan du joint                           *
*      ALPN = coefficient de dilatation thermique     *
*             suivant la direction n, perpendiculaire *
*             au plan du joint                        *
*             ( ALPN est homogene a une longueur      *
*              divisee par des degres, la longueur    *
*              representant l'epaisseur du joint)     *
*      DELTA(T) = CHAMALEM de temperatures            *
*      SIGMA = 4.2E20 * 2.5 * 1000.0                  *
*            = 1.05E24                                *
*                                                     *
*=====================================================*
OPTION DIME 3 ;
OPTION ELEM QUA4 MODE TRID  ;
*
SOLNUL = 0.0     ;
SOLANA = 1.05E24 ;
*
* ------- DEFINITION DE LA SURFACE TOP DU JOINT -------
*
A1 = 0.00 0.00 0.00 ;
B1 = 2.00 0.00 0.00 ;
C1 = 2.00 2.00 0.00 ;
D1 = 0.00 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
O1 = 0.00 0.00 10.00 ;
H1 = A1 DROIT 1 B1 ;
H2 = B1 DROIT 1 C1 ;
H3 = C1 DROIT 1 D1 ;
H4 = D1 DROIT 1 A1 ;
*
L1 = H1 ET H2 ET H3 ET H4 ;
S1 = SURF L1 PLAN ;
*
* ------- DEFINITION DE LA SURFACE BOT DU JOINT -------
*
IA1 = 0.00 0.00 0.00 ;
IB1 = 2.00 0.00 0.00 ;
IC1 = 2.00 2.00 0.00 ;
ID1 = 0.00 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
IH1 = IA1 DROIT 1 IB1 ;
IH2 = IB1 DROIT 1 IC1 ;
IH3 = IC1 DROIT 1 ID1 ;
IH4 = ID1 DROIT 1 IA1 ;
*
IL1 = IH1 ET IH2 ET IH3 ET IH4 ;
IS1 = SURF IL1 PLAN ;
*
* ----------   CREATION DU JOINT JOI4   ----------
*
OPTION ELEM LIA4 ;
VOL = LIAISON 0.00001 S1 IS1 ;
*
* -------- DEFINITION DES CONDITIONS LIMITES ----------
*
CL11 = BLOQ IA1 UX ;
CL12 = BLOQ IA1 UY ;
CL13 = BLOQ IA1 UZ ;
CL1 = CL11 ET CL12 ET CL13 ;
*
CL21 = BLOQ IB1 UX ;
CL22 = BLOQ IB1 UY ;
CL23 = BLOQ IB1 UZ ;
CL2 = CL21 ET CL22 ET CL23 ;
*
CL31 = BLOQ IC1 UX ;
CL32 = BLOQ IC1 UY ;
CL33 = BLOQ IC1 UZ ;
CL3 = CL31 ET CL32 ET CL33 ;
*
CL41 = BLOQ ID1 UX ;
CL42 = BLOQ ID1 UY ;
CL43 = BLOQ ID1 UZ ;
CL4 = CL41 ET CL42 ET CL43 ;
*
CL = CL1 ET CL2 ET CL3 ET CL4 ;
*
* ---------   DEFINITION DU MODELE DU JOINT  ----------
*
P1 = 0.707 0.707 0.707 ;
MOD1 = MODE VOL 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE'
       JOI4 ;
MA1  = MATE MOD1 'DIRECTION' P1 'PERPENDICULAIRE'
       KS1 4.2E10 KS2 3.0E5 KN 4.2E20 ALPN 2.5;
*
* ----------      MATRICE DE RIGIDITE        ----------
*
RI1 = RIGI MOD1 MA1 ;
RI2 = RI1 ET CL ;
*
* ----------     CONTRAINTES THERMIQUES     ----------
*
*           CHAMELEM S'APPUYANT SUR UN OBJET MODELE
*
C1T =MANU 'CHML' MOD1 'T' 1000.0 'TYPE' 'TEMPERATURES';
SI1 = THET MOD1 MA1 C1T ;
*
MESS '   CHAMELEM S APPUYANT SUR UN OBJET MODELE    ' ;
MESS '                                              ' ;
MESS ' ____________________________________________ ' ;
MESS '!                                            !' ;
MESS '!            JOINT 3D  JOI4                  !' ;
MESS '!                                            !' ;
MESS '!  - Surface inferieure du joint encastree.  !' ;
MESS '!  - Surface superieure du joint libre     . !' ;
MESS '!  - Chamelem de temperatures donne DELTA(T) !' ;
MESS '!    s appuyant sur un objet de type modele  !' ;
MESS '!                                            !' ;
MESS '! ==>                                        !' ;
MESS '! Champ des contraintes thermiques = Sigma   !' ;
MESS '!                                            !' ;
MESS '!  Sigma = raideur * coeff. dilat. thermi.   !' ;
MESS '!          * DELTA(T)                        !' ;
MESS '!____________________________________________!' ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS ' Solution Analytique :'                         ;
MESS '                                              ' ;
MESS '    SMS1 =' SOLNUL  ;
MESS '    SMS2 =' SOLNUL  ;
MESS '    SMN  =' SOLANA  ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS ' Solution Calculee :'                           ;
LIST SI1 ;
*
* ----------     CODE DE FONCTIONNEMENT      ----------
*
MAXCO1 = MAXI SI1 'ABS' ;
RESI1 = ABS( (MAXCO1-SOLANA)/SOLANA ) ;
*
*           CHAMELEM S'APPUYANT SUR UN OBJET MAILLAGE
*
C2T = MANU 'CHML' VOL 'T' 1000.0 'TYPE' 'TEMPERATURES';
SI2 = THET MOD1 MA1 C2T ;
MESS '                                              ' ;
MESS '  CHAMELEM S APPUYANT SUR UN OBJET MAILLAGE   ' ;
MESS '                                              ' ;
MESS ' ____________________________________________ ' ;
MESS '!                                            !' ;
MESS '!            JOINT 3D  JOI4                  !' ;
MESS '!                                            !' ;
MESS '! - Surface inferieure du joint encastree.   !' ;
MESS '! - Surface superieure du joint libre     .  !' ;
MESS '! - Chamelem de temperatures donne DELTA(T)  !' ;
MESS '!   s appuyant sur un objet de type maillage !' ;
MESS '!                                            !' ;
MESS '! ==>                                        !' ;
MESS '! Champ des contraintes thermiques = Sigma   !' ;
MESS '!                                            !' ;
MESS '! Sigma = raideur * coeff. dilat. thermi.    !' ;
MESS '!         * DELTA(T)                         !' ;
MESS '!____________________________________________!' ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS ' Solution Analytique :'                         ;
MESS '                                              ' ;
MESS '    SMS1 =' SOLNUL  ;
MESS '    SMS2 =' SOLNUL  ;
MESS '    SMN  =' SOLANA  ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS '                                              ' ;
MESS ' Solution Calculee :'                           ;
MESS '                                              ' ;
LIST SI2 ;
*
MAXCO2 = MAXI SI2 'ABS' ;
RESI2 = ABS( (MAXCO2-SOLANA)/SOLANA ) ;
*
SI ( (RESI1 <EG 1E-4) ET (RESI2 <EG 1E-4) );
   ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;
*
FIN ;









