* fichier :  fluaendo.dgibi
************************************************************************
* Section : Mecanique Viscoendommagement
* Section : Mecanique Viscoplastique
************************************************************************
complet = faux ;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
OPTI ECHO 1 DIME 2 ELEM QUA8 MODE AXIS;
*
*-----------------------------------------------------------------
*  MAILLAGE AXISYMETRIQUE EPROUVETTE CYLINDRIQUE
*  MATERIAU VISCO-PLASTIQUE ENDOMMAGEABLE DEPENDANT DE LA
*                           =============
*                 TEMPERATURE POUR N M KK A R EVOL
*-----------------------------------------------------------------
   P1 = 0 0;   P2 = 3E-3 0;   P3 = 3E-3 30E-3;   P4 = 0 30E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 1 ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 1 ;
*
*  mesh
*
   EPROU = L1 L2 L3 L4 DALLER PLAN ;
*
* boundary conditions
*
   CL1 = BLOQ L1 UZ ;
   CL3 = BLOQ L4 UR ;
   CL = CL1 ET CL3 ;
*
* MODE defines the behavior of the material and the finite element
*                  formulation
*
   MO = MODE EPROU MECANIQUE ELASTIQUE VISCOPLASTIQUE VISCODOMMAGE;
*
* where is the load applied ?
*
   pres=pression mass mo -137. l3 ;
*
* Definition des coefficients variables avec la temperature
*
   PROGKSI1 = PROG 0. 2.E5 ;
   PROGK1 = PROG 15. 15. ;
   CTRAC1 = EVOL MANU KSI PROGKSI1 K PROGK1 ;
*
   PROGKSI2 = PROG 0. 2.E5 ;
   PROGK2 = PROG 75. 75. ;
   CTRAC2 = EVOL MANU KSI PROGKSI2 K PROGK2 ;
*
CTRAC = NUAGE 'T'*'FLOTTANT' 'EVOL'*'EVOLUTION'
                   1000. CTRAC1 1050. CTRAC2  ;
*
* lors de l'ecoulement :
* interpolation des coefficients avec la temperature
* car les listes de temperatures ne sont pas identiques pour N
* et les autre coef.
   EVN = EVOL MANU 'T' (PROG 1000 1025 1050 ) 'N   '
                                        (PROG 9 7 5);
   EVM = EVOL MANU 'T' (PROG 1000 1050) 'M   ' (PROG 8 4    );
   EVKK = EVOL MANU 'T' (PROG 1000 1050) 'KK  ' (PROG 2200 800   );
   EVR = EVOL MANU 'T' (PROG 1000 1050) 'R   ' (PROG 7 3     );
   EVA = EVOL MANU 'T' (PROG 1000 1050) 'A   ' (PROG 639 1261 );
*
* Definition du materiau variable
*
   MATVAR = MATE MO YOUN 150000. NU 0.3 rho 7800. alph 0.
                 N EVN M EVM KK EVKK ALP1 0. BLP1 0.
                 R EVR A EVA EVOL CTRAC SMAX 0. ;
*
* Definition des cartes de temperature
*
   TEMP0 = MANU CHPO EPROU 1 'T' 1000.   ;
   TEMP1 = MANU CHPO EPROU 1 'T' 1050.   ;
*
* Definition du chargement
*
   LI1 = PROG 0. 3.E7 ; LI2 = PROG 1. 1. ;
   EV = EVOL MANU T LI1 LOAD LI2 ;
   CHA1 = CHAR 'MECA' PRES EV ;
   TEMPSs = TABLE; TEMPE = TABLE;
   TEMPSs.0 = 0. ;TEMPSs.1 = 6000.;
   TEMPE.0 = TEMP0; TEMPE.1 = TEMP1;
   CHA2 = CHAR 'T' TEMPSs TEMPE;
   CHA = CHA1 ET CHA2;

   TAB = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL;
   TAB.'CARACTERISTIQUES' = MATVAR;
   TAB.'MODELE' = MO;
   TAB.'CHARGEMENT' = CHA;
*
si complet;
   LIS = PROG 0. pas 1.E-1 1. 1.5 2 pas 1 10 15 20 pas 10 100
              150 200 pas 100 800. 850. pas 50 1100 pas 20 1400 ;
sinon;
LIS = PROG 0. pas 1.E-1 1. 1.5 2 ;
finsi;
*
   TAB.'TEMPS_CALCULES' = LIS;
   PASAPAS TAB ;
*
*----------------------------------------------------------------
*  CONTROLE DES RESULTATS AVEC DE LA SOLUTION DE REFERENCE
*               OBTENUE PAR ALGORITHME ou par code
*----------------------------------------------------------------
*
si complet;
 REF_D = 5.87E-2 ;
 REF_P = 1.36E-3 ;
sinon;
 REF_D = 3.98156E-05;
 REF_P = 1.51511E-05;
finsi;
*
*tind=index (tab.resucont); ntind=dime tind ;
*T=tind.ntind;
*
D = EXTR ( PECHE TAB VARIABLES_INTERNES) 'VHWD' 1 1 1;
P = EXTR ( PECHE TAB VARIABLES_INTERNES) 'EPSE' 1 1 1;
*
list D; list P;
errd = ABS ((REF_D - D) / REF_D) ;
errp = ABS ((REF_P - P) / REF_P) ;
*
err = MAXI (prog errd errp) ;
*
temps;
SI ( ERR <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
FIN;












