* fichier :  endoaxi2.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
*         Test Endoaxi2.dgibi: Jeux de données        *
*         ------------------------------------        *
*                                                     *

*******************************************************
*                                                     *
* TEST;  MATERIAU ELASTO-PLASTIQUE ENDOMMAGEABLE      *
* MATERIAU DEPENDANT DE LA TEMPERATURE :              *
*                          COURBE DE TRACTION         *
*                          COEFFICIENT DE DILATATION  *
*                          THERMIQUE                  *
*                                                     *
* EPROUVETTE EN TRACTION AVEC DEPLACEMENTS IMPOSES    *
*                           + CHARGEMENT THERMIQUE    *
* MAILLAGE AXISYMETRIQUE.                             *
*                                                     *
* LES RESULTATS OBTENUS SONT COMPARES  A LA SOLUTION  *
* ANALYTIQUE OBTENUE PAR ALGORITHME.                  *
* ON TESTE - LA DEFORMATION PLASTIQUE CUMULEE         *
*            (composante EPSE dans VARIABLES_INTERNES)*
*          - D, LA VARIABLE D'ENDOMMAGEMENT           *
*            DANS VARIABLES_INTERNES .                *
*          - SIGMAzz ( DANS CONTRAINTES ).            *
*******************************************************                                                       *


OPTI ECHO 0 DIME 2 ELEM QUA8 MODE AXIS;
*------------------------------------------------------
   P1 = 0 0;          P2 = 3E-3 0;
   P3 = 3E-3 30E-3;   P4 = 0 30E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 1  ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 1  ;
*
   EPROU = L1 L2 L3 L4 DALLER PLAN ;

   CL1 = BLOQ L1 UZ ;
   CL2 = BLOQ L3 UZ ;
   CL3 = BLOQ L4 UR ;
   CL = CL1 ET CL2 ET CL3 ;
   DEP = DEPI CL2 1E-3 ;
*
   MO = MODE EPROU 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE'
'ENDOMMAGEABLE';
*
* module tangent valant 783MPa
*-----------------------------
  PROGEPS1 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG1 = PROG 0. 350.E06 581.1976E06 816.0976E06 ;
   CTRAC1 = EVOL MANU EPS PROGEPS1 SIG PROGSIG1 ;
*
* module tangent valant 491MPa
*-----------------------------
  PROGEPS2 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG2 = PROG 0. 350.E06 494.9783E06 642.2783E06 ;
   CTRAC2 = EVOL MANU EPS PROGEPS2 SIG PROGSIG2 ;
*
* module tangent valant 199MPa
*-----------------------------
  PROGEPS3 = PROG 0. (350./74020.) 0.3 0.6 ;
  PROGSIG3 = PROG 0. 350.E06 408.7590E06 468.4590E06 ;
   CTRAC3 = EVOL MANU EPS PROGEPS3 SIG PROGSIG3 ;
*
* module tangent variable
*------------------------
CTRAC = NUAGE 'T'*'FLOTTANT' 'TRAC'*'EVOLUTION'
                  0. CTRAC1 100. CTRAC2 200. CTRAC3  ;
*
* coefficient de dilatation thermique vaiable
*--------------------------------------------
*
  PROGTEMP = PROG 0. 100. 200. ;
  PROGALFA = PROG 0. 1E-3 2E-3 ;
  EVALPH = EVOL MANU 'T' PROGTEMP 'ALPH' PROGALFA ;
*
*   MAT = MATE MO YOUN 74020.E06 NU 0.3 RHO 7800.
*   ALPH 1.E-3 TRAC CTRAC1 EPSD 0. DC 0.23 EPSR 0.25 ;
*
   MATvar = MATE MO YOUN 74020.E06 NU 0.3 RHO 7800.
   ALPH EVALPH TRAC CTRAC EPSD 0. DC 0.23 EPSR 0.25 ;
*
************** DEFINITION DU CHARGEMENT ***************
*
   LI1 = PROG 0. 30. ; LI2 = PROG 0. 30. ;
   EV = EVOL MANU T LI1 DEP(T) LI2 ;
   CHA1 = CHAR 'DIMP' DEP EV ;
   TEMP0 = MANU CHPO EPROU 1 'T' 0.     ;
   TEMP200 = MANU CHPO EPROU 1 'T' 200. ;
   t0 = 0. ; t20 = 20.      ;
   TEMP = TABLE; TEMPERA = TABLE;
   TEMP.0 = t0 ; TEMP.1 = t20 ;
   TEMPERA.0 = TEMP0; TEMPERA.1 = TEMP200;
   CHA2 = CHAR 'T' TEMP TEMPERA;
   CHA = CHA1 ET CHA2;
*
* LISTE DES PAS DE CALCUL :
*                         0.14 MM INCREMENT ELASTIQUE
*                         0.15 MM INCREMENT PLASTIQUE
  LIS = PROG 0.14 0.15 0.2 0.4 0.7 1 pas .3
        3.1 3.3 pas .2 4.1 ;
*
TAB = TABLE ;
TAB.'PRECISION' = 1E-3;
TAB.'BLOCAGES_MECANIQUES' = CL;
TAB.'CARACTERISTIQUES' = MATvar;
TAB.'CHARGEMENT' = CHA;
TAB.'TEMPS_CALCULES' = LIS;
TAB.'MODELE' = MO;
PASAPAS TAB ;
*
*
*------------------------------------------------------
*  CONTROLE DES RESULTATS AVEC DE LA SOLUTION
*      DE REFERENCE OBTENUE PAR ALGORITHME
*------------------------------------------------------
*
 REF_D = PROG 2.197E-2 3.417E-2 6.337E-2 8.131E-2 ;
 REF_P = PROG 3.646E-2 5.443E-2 9.296E-2 1.143E-1 ;
 REF_S = (PROG   368.9   376.0   386.4   389.2)*1E6 ;
*
D1 = EXTR (TAB.VARIABLES_INTERNES.7) 'VHWD' 1 1 1;
D2 = EXTR (TAB.VARIABLES_INTERNES.9) 'VHWD' 1 1 1;
D3 = EXTR (TAB.VARIABLES_INTERNES.14) 'VHWD' 1 1 1;
D4 = EXTR (TAB.VARIABLES_INTERNES.18) 'VHWD' 1 1 1;
*
P1 = EXTR (TAB.VARIABLES_INTERNES.7) 'EPSE' 1 1 1;
P2 = EXTR (TAB.VARIABLES_INTERNES.9) 'EPSE' 1 1 1;
P3 = EXTR (TAB.VARIABLES_INTERNES.14) 'EPSE' 1 1 1;
P4 = EXTR (TAB.VARIABLES_INTERNES.18) 'EPSE' 1 1 1;
*
S1 = EXTR (TAB.CONTRAINTES.7) 'SMZZ' 1 1 1;
S2 = EXTR (TAB.CONTRAINTES.9) 'SMZZ' 1 1 1;
S3 = EXTR (TAB.CONTRAINTES.14) 'SMZZ' 1 1 1;
S4 = EXTR (TAB.CONTRAINTES.18) 'SMZZ' 1 1 1;
*
EF_D = PROG D1 D2 D3 D4 ;
EF_P = PROG P1 P2 P3 P4 ;
EF_S = PROG S1 S2 S3 S4 ;
*
PROG_ERD = MAXI (ABS ((EF_D  - REF_D) / REF_D)) ;
PROG_ERP = MAXI (ABS ((EF_P  - REF_P) / REF_P)) ;
PROG_ERS = MAXI (ABS ((EF_S  - REF_S) / REF_S)) ;
*
ERR = MAXI (PROG PROG_ERD PROG_ERP PROG_ERS)    ;
*
temps;
SI ( ERR <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
FIN;












