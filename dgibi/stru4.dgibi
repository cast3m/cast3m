* fichier :  stru4.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*           Test Stru4.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
*******************************************************
*                                                     *
* TEST DE STATIQUE POUR L'ELEMENT TUYAU               *
*                                                     *
* Un tuyau encastre‚ et courbe‚ est soumis a…des      *
* efforts de flexion dans son plan.                   *
*                                                     *
* Ref: Guide VPCS, Test SSLL 06/90                    *
*      Solution analytique; JS Prezemieniecki;        *
*      Theory of matrix structural analysis, New York,*
*      McGraw-Hill, 1968                              *
*                                                     *
* Test effectue‚ le 25/08/92                          *
*                                                     *
*******************************************************

GRAPH= 'N';
opti echo 0;
opti dime 3;
opti elem seg2;

************* GEOMETRIE ********************
P0 = 0. 0. 0.;
PA =  0. 3. 0.;
PB =  3. 0. 0.;
CAB = CERC 20 PA P0 PB;
OEIL = 0. 0. 1000.;

SI ( NEG GRAPH 'N');
TRAC OEIL CAB;
FINSI;

************* MODELE ***********************

MODL1 = MODE cab 'MECANIQUE' 'ELASTIQUE' 'TUYAU';
MAT1  = MATE MODL1 YOUN 2E11 NU 0.3 EPAI 0.002 RAYO
0.01 VECT OEIL;

************* CONDITIONS AUX LIMITES *******

BL1 = BLOQU DEPL ROTA PA;

************* RIGIDITE   *******************

RG1 = RIGI MODL1 MAT1;
RGTOT = RG1 ET BL1;

************* CHARGEMENT *******************

FOR1 = FORC FX 10. FY 5. PB;
MOM1 = MOME MZ 8 PB;
FORT = FOR1 ET MOM1;

************* CALCUL DEPLACEMENT *************

DEP = RESOU RGTOT FORT;
UBX = EXTR DEP PB UX;
UBY = EXTR DEP PB UY;
RBZ = EXTR DEP PB RZ;


************* CALCUL ANALYTIQUE **************

F1 = 10.; F2 = 5.; MZ = 8.; E= 2E11;
I = 4.637E-9; R = 3.;

UBX1 = (R**2)/4/E/I*((F1*PI*R)+(2*F2*R)+(4*MZ));
UBY1 = (R**2)/4/E/I*((2*F1*R)+(((3*PI)-(8.))*F2*R)+
(2*(PI-(2.))*MZ));
RBZ1 = R/4/E/I*((4*R*F1)+(2*(PI-(2.))*F2*R)+(2*PI*MZ));

************* COMPARAISON DES RESULTATS *****

MESS ' RESULTATS ';
MESS ' --------- ';

SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE AU POINT B SUIVANT X;'
UBX1 'M';
MESS ' DEPLACEMENT CALCULE   AU POINT B SUIVANT X;'
UBX  'M';
R1= 100*(ABS((UBX-UBX1)/UBX1));
MESS '     SOIT UN ECART DE       : '  R1  '%' ;
SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE AU POINT B SUIVANT Y;'
UBY1 'M';
MESS ' DEPLACEMENT CALCULE   AU POINT B SUIVANT Y;'
UBY  'M';
R2= 100*(ABS((UBY-UBY1)/UBY1));
MESS '     SOIT UN ECART DE       : '  R2  '%' ;
SAUT 2 LIGN ;
MESS ' ROTATION THEORIQUE AU POINT B;' RBZ1 'rad';
MESS ' ROTATION CALCULEE  AU POINT B;' RBZ 'rad';
R3= 100*(ABS((RBZ-RBZ1)/RBZ1));
MESS '     SOIT UN ECART DE       : '  R3  '%' ;


************* CODE DE FONCTIONNEMENT ********
LR = PROG R1 R2 R3;

ERMAX = MAXI LR;

SI ( ERMAX < 5);
   ERRE 0;
SINON
   ERRE 5;
FINSI;
FIN;







