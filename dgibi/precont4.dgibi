* fichier :  precont4.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
************************************************************************
*  Calcul de la perte de précontrainte d'un cable circulaire tendu a   *
*                    une seule de ses extremités.                      *
************************************************************************
*
opti dime 3 elem seg2 echo 1 ;
*
graph='n';
oeilz = 0. 0. 1.E8 ;
rayc = 25. ;
*
dtailf = 0.1 ;
dens dtailf ;
p0 = 0. 0. 0. ;
pcent = 0. rayc 0. ;
angpi = 179.99 ;
*
p1 = p0 tour angpi pcent (pcent plus (0. 0. 1.)) ;
p2 = p1 tour angpi pcent (pcent plus (0. 0. 1.)) ;
*
lc1 = cerc p0 pcent p1 ;
lc2 = cerc p1 pcent p2 ;
*
lcerc = lc1 et lc2 ;
*
titre 'Cable circulaire' ;
si(neg graph 'n');
trac oeilz lcerc qual ;
finsi;
*
mo = mode lcerc mecanique elastique  barr ;
ma = mate mo YOUN 1.9E11 SECT 5.55e-3  ;
*
vsect = extr ma sect 1 1 1 ;
*
sp0 = 1.4E9 ;
ftens = sp0 * vsect ;
*
TA = TABLE;
TA.'FF  '=0.18;
TA.'GANC' = 8.E-3 ;
TA.'PHIF' = 0.002 ;
TA.'RMU0' = 0.43  ;
TA.'FPRG' = 1.7E9 ;
TA.'RH10' = 2.5E-2;

pre01 = prec mo ma ftens TA p0 ;
pre01p = (chan chpo pre01 mo) / vsect ;
*
titre 'Evolution de la précontrainte donnée par PREC' ;
ev01 = evol roug chpo pre01p effx lcerc ;
si(neg graph 'n');
dess ev01 mima titx 'Absc. curviligne (m)'
               tity 'Précontrainte (Pa)' ;
finsi;
*
************************************************************************
*            Solution théorique pour un cable circulaire               *
*               Prise en compte des pertes par courbure                *
*                et par frottement linéaire uniquement                 *
************************************************************************
*
lrabsc = extr ev01 absc ;
ndlis  = dime lrabsc ;
*
vyoun = extr ma youn 1 1 1 ;
vff   = ta.'FF  ';
vphif = ta.'PHIF';
vganc = ta.'GANC';
*
lginflu =
 (-1.)*(log (1. - ((vganc*vyoun*((vff/rayc)+vphif)/sp0)**0.5)))/
 ((vff/rayc)+vphif) ;
*
lrprec = prog ;
i = 0 ;
repeter bcl1 ndlis ;
  i = i + 1 ;
  xcour = extr i lrabsc ;
  si (xcour < lginflu) ;
    preccour = sp0*(exp (((vff/rayc)+vphif)*(xcour - (2.*lginflu)))) ;
  sinon ;
    preccour = sp0*(exp ((-1.)*((vff/rayc)+vphif)*xcour)) ;
  finsi ;
  lrprec = lrprec et (prog preccour) ;
fin bcl1 ;
*
chtit = chai 'Précontrainte théorique ; influence d ancrage sur'
             lginflu 'm' ;
titre chtit ;
evtheo = evol bleu manu lrabsc lrprec ;
si(neg graph 'n');
dess evtheo mima titx 'Absc. curviligne (m)'
                 tity 'Précontrainte (Pa)';
finsi ;
*
titre
 'Comparaison : Opérateur PREC (rouge) / résultat théorique (bleu)' ;
*
si(neg graph 'n');
dess (ev01 et evtheo) mima titx 'Absc. curviligne (m)'
                           tity 'Précontrainte (Pa)' ;
finsi ;

orca= extr ev01 ORDO;

err1 = ( maxi abs ( orca - lrprec) ) / ( maxi abs lrprec ) ;
mess ' erreur relative maximale sur les ordonnees ' err1;
si ( err1 <  5.E-3  ) ;
  erre 0 ;
sinon ;
  erre 5 ;
finsi ;

*
fin ;







