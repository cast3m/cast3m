* fichier :  tubesrc2.dgibi
**  Exemple TUBESRC2
**  ________________
**
**  --- 2 JUIN 1998 ---
**
**   Tube cylindrique Rayon R0=0.25  Longueur L0=16*R0
**   test cas isotherme NS,FIMP KBBT en Implicite
**
**   On teste aussi un puits de masse uniformément réparti.
**

GRAPH = 'N' ;


DEBPROC TUBESRC ;
ARGU TYPELT*MOT NH*ENTIER NV*ENTIER  GRAPH*MOT KPRESS*MOT
MACRO*MOT SRC*FLOTTANT ;

OPTION DIME 2 ELEM TYPELT ;
 option mode axis ;

r0=0.25 ; L0=16*R0 ;
ae=0. ;
P1=R0 0 ; p2=ae 0 ; p3 = ae (3.*L0/4.)  ;
P4 = p3 plus (0 (L0/4));
r1=R0/2. ;
P7= P4 plus (R0 0) ;
*P8=R0 (3.*L0/4.) ;
entree= p1 d nh p2 ;
axe=p2 d nv p4 ;
sortie=p4 d nh p7 ;
paroi=p7 d nv p1 ;

mt= entree axe sortie paroi daller ;
*trace mt ;
ent = chan entree poi1 ;
ent=elem ent (lect 2 pas 1 (nbel ent)) ;


mtq=chan mt quaf ;
entree= chan entree quaf ;
sortie= chan sortie quaf ;
elim (mt et entree et sortie )1.e-5 ;

$mt= mode mtq 'NAVIER_STOKES' MACRO ; doma $mt 'IMPR' ;

NU=1.5E-2;
uE=1. ;
KPRESS='CENTREP1' ;

RU=eqex 'OMEGA' 0.9 'NITER' 5
  'OPTI' 'EF' 'IMPL' 'SUPG' KPRESS 'ALE'
  ZONE  $mt       OPER    KBBT 1.          INCO 'UN' 'PRES'
  ZONE  $mt       OPER    NS 1. 'UN' NU  'WN'   INCO 'UN'
  'OPTI'  INCOD KPRESS
  ZONE  $mt       OPER    FIMP src       INCO 'PRES'
  CLIM
 UN UIMP entree 0.  UN VIMP ent    UE
 UN UIMP axe 0. UN UIMP paroi 0.
 UN VIMP paroi 0.  ;

ru.inco=table 'INCO' ;
ru.'INCO'.'UN'=kcht $mt vect sommet (0. 1. ) ;
ru.'INCO'.'WN'=kcht $mt vect sommet (0. 0. ) ;
ru.'INCO'.'PRES' =  kcht $mt  scal KPRESS 0.     ;

 exec ru ;

 $entree=mode entree 'NAVIER_STOKES' MACRO  ;
 $sortie=mode sortie 'NAVIER_STOKES' MACRO  ;

 un=ru.'INCO'.'UN';
 qe=dbit un $entree ;
 qs=dbit un $sortie ;

 si ('EGA' graph 'O' );

 mt=doma $mt 'MAILLAGE' ;
 ung1=vect un 5.e-2 ux uy jaune ;
 trace ung1 mt ;

 pn=elno $mt (ru.'INCO'.'PRES') KPRESS ;
 trace pn mt ;

 srti=doma $sortie 'MAILLAGE' ;
 evolV = EVOL 'CHPO' (ru.'INCO'.'UN') UY (srti  ) ;

 TAB1=TABLE;
 TAB1.'TITRE'=TABLE ;
 TAB1 . 1 ='MARQ      REGU '     ;
 TAB1.'TITRE' . 1 = mot 'Composante_UX '     ;
 DESS evolV 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;

 FINSI ;

 V=doma $mt 'VOLUME' ;
 VT=somt V ;

FINPROC RV qe qs vt ;


**************   MACRO  CENTREP1 SRCE=-0.1 ******************
 nv=15 ; nh= 3  ;
 MACRO= 'MACRO';
 kpress='CENTREP1';
 typelt=qua8 ;
 src= 0.1 ;

RV qe qs vt= TUBESRC typelt nh nv graph kpress macro src ;

 dq= (abs qe ) - qs  ;
 mess ' qe ' qe ' qs ' qs ' dq ' dq ;
 mess 'VT= ' vt ' src= ' src  ' VT*src= ' (vt*src) ;

 erq= abs (dq-(vt*src));
 err1=8.e-7 ;
 mess ' Erreur sur le debit : dq-vt*src ' (dq-(vt*src)) ;
* Attention sur les bilans effet du parametre de relaxation 0.9
* si ( erq > err1 ) ; erreur 5 ; finsi ;

 err2 =1.e-7 ;
 dq1=dq - 7.85390E-02 ;
 dq1=abs dq1 ;
 mess 'DQ1=' DQ1 ;
 si ( dq1  > err2  ) ; erreur 5 ; finsi ;


 FIN ;










