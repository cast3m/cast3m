* fichier :  tran10.dgibi
*
* Copie du cas-test "tran8.dgibi" pour tester
* la donnee a PASAPAS d'une :
* - CAPACITE_CONSTANTE
* - CONDUCTIVITE_CONSTRANTE
* et resolution par la procedure DUPONT

* Calcul avec des EF TRI6 (au lieu de QUA4)

*******************************************
* Section : Thermique Conduction
* Section : Thermique Transitoire
*******************************************

*******************************************
*    Test tran10.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

*******************************************
*                  tran10                 *
*                                         *
*   THERMIQUE TRANSITOIRE LINEAIRE EN 2D  *
*                                         *
*   Test NAFEMS numero T3                 *
*                                         *
*                                         *
*  description                            *
*  -----------                            *
*                                         *
*  A                         C       B    *
*                             <_0.02_>    *
*  |                         |       |    *
*  |=================================|    *
*  |                                 |    *
*  <______________0.1 m______________>    *
*            ---> axe X                   *
*                                         *
*                                         *
*    conditions aux limites               *
*    ----------------------               *
*                                         *
*  - temperature imposee au point A :     *
*        TA = 0                           *
*                                         *
*  - temperature imposee au point B :     *
*       TB = 100 * sin (PI * temps / 40)  *
*                                         *
*******************************************
OPTI DIME 2;
OPTI ELEM TRI6 ;

*---------- geometrie : maillage ---------*

PA1 = 0. 0.;   PB1 = 0.1 0.  ;
PA2 = 0. 0.01; PB2 = 0.1 0.01;

N = 10;
D1 = PA1 DROI N PB1;
D2 = PB1 DROI 1 PB2;
D3 = PB2 DROI N PA2;
D4 = PA2 DROI 1 PA1;

SUR1 = DALL D1 D2 D3 D4 PLAN;

PC = POIN SUR1 PROC (0.08 0.);


SI (NEG GRAPH 'N');
  TITR 'TRAN10 : MAILLAGE';
  TRAC 'QUAL' SUR1;
FINSI;

*--------- modeles - materiaux -----------*
MODL1 = MODE SUR1 THERMIQUE ISOTROPE ;
MATR1 = MATE MODL1 'RHO' 7200 'K' 20.0  'C'   240.5 ;

* test de l'option 'CAPACITE_CONSTANTE'
* de PASAPAS :
MATR2 = MATE MODL1 'RHO' 7200 'K' 15.0  'C'   200.0 ;
CAP2  = CAPA MODL1 MATR2 ;
CND2  = COND MODL1 MATR2 ;

* temperatures imposees : fonction du temps
*
*    - Cote PA : temperature constante
*                       de 0 degres celcius
*    - Cote PB : temperature variable
*                      en fonction du temps

BLOCD4 = BLOQ D4 'T';
BLOCD2 = BLOQ D2 'T';

TEMPD4 = DEPI BLOCD4 1.;
TEMPD2 = DEPI BLOCD2 1.;

* Un pas toutes les secondes,
* temps maximum 40. s.
LTEMPS = PROG 0. PAS 1. 40.;

LD4 = PROG 41 * 0.;
LD2 = PROG SINU (1. / 80.) AMPL 100 LTEMPS;

EVOLD4 = EVOL MANU TEMPS LTEMPS THETA LD4;
EVOLD2 = EVOL MANU TEMPS LTEMPS THETA LD2;

CHAD4 = CHAR 'TIMP' TEMPD4 EVOLD4;
CHAD2 = CHAR 'TIMP' TEMPD2 EVOLD2;

* Creation d'un flux nul (type chargement)
FLU0 = MANU CHPO (D1 ET D3) 1 Q 0.;

EVOL3= EVOL MANU TEMP LTEMPS FLUX  (PROG 41 * 1.);
FLU1 = CHAR 'Q' FLU0 EVOL3;

*--- objets pour la procedure PASAPAS ----*
BLOCT = (BLOCD2 ET BLOCD4);
CHART = (FLU1 ET CHAD4 ET CHAD2);

TAB1                          = TABL;
TAB1.'TEMPERATURES'           = TABL;
TAB1.'TEMPERATURES' . 0       =  MANU CHPO SUR1 1 T 0.;
TAB1.'BLOCAGES_THERMIQUES'    = BLOCT;
TAB1.'CHARGEMENT'             = CHART;
TAB1.'MODELE'                 = MODL1;
TAB1.'CARACTERISTIQUES'       = MATR1;
TAB1.'TEMPS_SAUVES'           =  PROG 0. PAS 1. 32.;
TAB1.'TEMPS_CALCULES'         =  PROG 0. PAS 1. 32.;
TAB1.'PROCEDURE_THERMIQUE'    = DUPONT ;
TAB1.'CAPACITE_CONSTANTE'     = CAP2 ;
TAB1.'CONDUCTIVITE_CONSTANTE' = CND2 ;

PASAPAS TAB1;

*------- extraction des resultats --------*
* Temperature du point C a t = 32 s.

* Construction de l'evolution de T au cours
* du temps

LTEMPE = VIDE 'LISTREEL';
LTEMPS = VIDE 'LISTREEL';
NBPAS = DIME (TAB1. 'TEMPS')  ;
REPE SURPAS NBPAS;
  INDICE = &SURPAS;
  LTEMPS =  LTEMPS ET (PROG  (TAB1.'TEMPS'. (INDICE - 1)));
  LTEMPE = LTEMPE ET (PROG  (EXTR TAB1.'TEMPERATURES'. (INDICE - 1) T (SUR1 POIN PROC (0.08 0.))));
FIN SURPAS;

EVTEMPE = EVOL BLEU MANU LTEMPS LTEMPE;


THET1 = EXTR LTEMPE NBPAS;
THET2 = 36.6;

ERG =100 * (ABS ((THET2 - THET1) / THET2));

* Trace facultatif de la repartition
* de temperature a t = 32 s
SI (NEG GRAPH 'N');
  TITR 'TRAN10 : Temperature a t = 32 s';
  CHPO3 = PECHE TAB1 'TEMPERATURES'  TAB1. 'TEMPS' . (NBPAS - 1);
  TRAC SUR1 CHPO3;
   TITR 'Evolution de la temperature en  fonction du temps : ';
  DESS EVTEMPE ;
FINSI;

*-------- Test Reactions dans solution -------*
IREAC1 = 'EXIS' TAB1 'REACTIONS_THERMIQUES' ;

*-------- affichage des resultats --------*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

MESS 'Temperature theorique :' THET2' C';
MESS 'Temperature calculee  :' THET1' C';
MESS '    Soit un ecart de : ' ERG '%';
SAUT 1 LIGN;

*------- code fonctionnement -------------*
SI ((ERG <EG 5) et IREAC1);
   ERRE 0;
SINON;
   'SI' ('NON' IREAC1) ;
     SAUT 1 LIGN;
     'MESS' '*****  ERREUR : il manque les reactions thermiques ! ' ;
     SAUT 1 LIGN;
   'FINS' ERRE 5;
FINSI;

TEMPS;

FIN;






