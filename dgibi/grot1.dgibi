* fichier :  grot1.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
opti dime 2 elem qua4 mode plan defo;
opti echo 0;

********************************************
*   grandes rotations sur un element 2D-DP
*    calcul elastique lineaire
********************************************


l = 1000.;
p1 = 0 l;
p3 = 0 0;

d1 = droit 1 p3 p1;
plaq = d1 tran 1 (l 0);
p2 = (enve plaq) poin proc (l l) ;
p4 = (enve plaq) poin proc (l 0);

*-------------------------------------------------
*   creation du modele
*-------------------------------------------------

objaf = MODE plaq mecanique elastique plastique parfait;

*-------------------------------------------------
*   caracteristiques du materiau
*-------------------------------------------------

mat = MATE objaf YOUNG 200000. NU 0.3 SIGY 10E6 ;

*--------------------------------------------------
*   calcul des rigidites elementaires
*             et
*   definition des blocages
*--------------------------------------------------


*    deplacement impose
cdl1 = bloq UX p1;
cdep1 = depi cdl1 (-1000.);
cdl11 = bloq UY p1;
cdep11 = depi cdl11 (-1000.);

cdl2 = bloq UX p2;
cdep2 = depi cdl2 (-2000.);
cdl21 = bloq UY p2;
cdep21 = depi cdl21 0;

cdl3 = bloq UX p3;
cdep3 = depi cdl3 0;
cdl31 = bloq UY p3;
cdep31 = depi cdl31 0;

cdl4 = bloq UX p4;
cdl41 = bloq UY p4;
cdep4 = depi cdl4 (-1000.);
cdep41 = depi cdl41 (1000.);

cdlt = cdl1 et cdl11 et cdl2 et cdl21 et cdl3 et cdl31
       et cdl4 et cdl41;

fo1 = cdep1 et cdep11 et cdep2 et cdep21 et cdep3 et cdep31
      et cdep4 et cdep41 ;

cdep41 = depi cdl41 1100.;
cdep21 = depi cdl21 100.;

fo2 = cdep1 et cdep11 et cdep21 et cdep3 et cdep31
      et cdep4 et cdep41 et cdep2;


*   creation du chargement
li1 = prog 0 19 20;


li3 = prog 0 1 0;
li4 = prog 0 0 1;
li5 = prog 0 pas 19 19 pas 1 20;
evt2 = evol manu t li1 f li3;
evt3 = evol manu t li1 f li4;

cha1 = char 'DIMP' fo1 evt2;
cha2 = char 'DIMP' fo2 evt3;

*----------------------------------------------------
*             resolution avec pasapas
*----------------------------------------------------

tab1 = table;
tab1.'GRANDS_DEPLACEMENTS' = vrai;
tab1.'BLOCAGES_MECANIQUES' = cdlt;
tab1.'MODELE' = objaf;
tab1.'CARACTERISTIQUES' = mat;
tab1.'CHARGEMENT' = cha1 et cha2;
tab1.'TEMPS_CALCULES' = li5;
pasapas tab1;

pp = react cdl21 (tab1.'DEPLACEMENTS'.(2));
ppa = 1.5548077e7;
ppy = maxi pp;
err1 = abs ((ppy - ppa)/ppa);
SI (ERR1 <  1.E-2);
    ERRE 0;
SINO;
    ERRE 5;
FINSI;

fin;







