* fichier :  normalisation-2.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************

*** Options ...

    opti dime 2 mode plan defo echo 1 ;

    critprec = 1.e-14 ;

*** Points ...

    dens 1. ;

    p1 = 0 0 ;
    p2 = 1 0 ;
    p3 = 0.5 0.7 ;
    li1 = manu seg2 p1 p2 ;
    ma1 = manu tri3 p1 p2 p3 ;

*** Raideurs ...

    mo1 = mode ma1 mecanique elastique ;
    ma1 = mate mo1 YOUN 1. NU 0.3 ;
    ri1 = (rigi mo1 ma1) et (bloq depl li1) ;

*** Force ...

    fo1 = forc FX 1 FY 1 p3 ;

*** Première résolution ...

    de1 = enle (reso ri1 fo1) 'LX' ;
list de1 ;

*** Deuxième, avec norm auto ...

    opti norm AUTO ;

    ri2 = 1 * ri1 ;
    de2 = enle (reso ri2 fo1) 'LX' ;
    dd = abs (de2 - de1) ;
    si((maxi dd) > critprec) ;
       erre 5 ;
    finsi ;

*** Troisième, avec normalisation des primales ...

    opti norm (mots UX UY) (prog 2 3) ;
    ri3 = 1 * ri1 ;
    de3 = enle (reso ri3 fo1) 'LX' ;
    dd = abs (de3 - de1) ;
    si((maxi dd) > critprec) ;
       erre 5 ;
    finsi ;

*** Quatrième, avec normalisation des primales et des duales ...

    opti norm (mots UX UY) (prog 2 3)
              (mots FY FX) (prog 5 4) ;

    ri4 = 1 * ri1 ;
    de4 = enle (reso ri4 fo1) 'LX' ;
    dd = abs (de4 - de1) ;
    si((maxi dd) > critprec) ;
       erre 5 ;
    finsi ;

*** Bye ...

    fin ;





