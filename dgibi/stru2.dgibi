* fichier :  stru2.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*           Test Stru2.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
* Pour visualiser les traces, mettre GRAPH a 'O' :
GRAPH = 'MOT' 'N' ;
OPTION  ECHO 0;
OPTI TRAC X;
*
MESS ' **********************************************';
MESS ' *                                            *';
MESS ' * COMMISSION V.P.C.S.   TEST STRU2.DGIBI     *';
MESS ' *                       TEST SSLS 33/91      *';
MESS ' * GROUPE : STRUCTURE ASSEMBLEE               *';
MESS ' *          STATIQUE LINEAIRE                 *';
MESS ' *                                            *';
MESS ' * TEST : PLAQUE CARREE ORTHOTROPE            *';
MESS ' *        ENCASTREE                           *';
MESS ' *                                            *';
MESS ' * REMARQUE :  CAS TEST FAIT AVEC ELEMENTS    *';
MESS ' *             QUA4 EN CONTRAINTES PLANES     *';
MESS ' *                                            *';
MESS ' * LES VALEURS DE REFERENCES SONT LES         *';
MESS ' * MOYENNES DE RESULTATS OBTENUS PAR SIX      *';
MESS ' * CODES DIFFERENTS                           *';
MESS ' *                                            *';
MESS ' * TEST MODIFIE LE 3/07/92                    *';
MESS ' **********************************************';
*
OPTION DIME 2 ELEM QUA4;
OPTION MODE PLAN CONT;
*
*-----------------------------------------------------*
*   GEOMETRIE                                         *
*-----------------------------------------------------*
*
PA =  0.5  0.5;
PB = -0.5  0.5;
PC = -0.5 -0.5;
PD =  0.5 -0.5;
PE =  0.   0.;
*
D1 = PA DROIT 10 PB;
D2 = PB DROIT 10 PC;
D3 = PC DROIT 10 PD;
D4 = PD DROIT 10 PA;
*
SUR1 = DALLAGE D1 D2 D3 D4 PLAN;
SUR = SUR1 ET PE;
*
ELIM SUR 0.001;
*
SI (NEG GRAPH 'N');
  TITRE 'PLAQUE CARREE ORTHOTROPE';
  TRACE SUR ;
FINSI ;
*
NBNOSUR = NBNO SUR1;
NBELSUR = NBEL SUR1;
*
*-----------------------------------------------------*
*         MODELE                                      *
*-----------------------------------------------------*
*
MOD = MODE SUR1 MECANIQUE ELASTIQUE ORTHOTROPE QUA4;
*
*-----------------------------------------------------*
*       CARACTERISTIQUES - MATERIAUX                  *
*-----------------------------------------------------*
*
DIR1 = (COS 30.) (SIN 30.);
*
MAT = MATE MOD DIRE DIR1 YG1 2.5737E10 YG2 7.377E9
YG3 7.377E9 NU12 0.31 NU23 0.31 NU13 0.31 G12 2.319E9;
*
*-----------------------------------------------------*
*           CONDITIONS AUX LIMITES  -  RIGIDITES      *
*-----------------------------------------------------*
*
CL = BLOQUE DEPL ROTA D2;
RI = RIGI MOD MAT ;
RIG = RI ET CL;
*
*-----------------------------------------------------*
*                      CHARGEMENTS                    *
*-----------------------------------------------------*
* IL S'AGIT D'UN CHARGEMENT LINEIQUE DE 1e4 NEWTONS/M
* SUR AD. L'EPAISSEUR EST DE 0.01M. ON A DONC UNE
* FORCE PAR UNITE D'EPAISSEUR DE 1E6 N/M

FTOT = 1.E6;
NBNOD4 = NBNO D4;
F5 = FTOT / ((2 * NBNOD4) - 2);
F50 = F5 0.;
I = 1;
NBI = NBNOD4 - 1;
CHA = FORC (0. 0.) (D4 POIN 1);
REPETER BLOC1 NBI;
  CHA1 = FORC F50 (D4 POIN I);
  CHA2 = FORC F50 (D4 POIN (I+1));
  CHA = CHA ET CHA1 ET CHA2;
  I = I + 1;
FIN BLOC1;

*-----------------------------------------------------*
*RESULTATS : DEPLACEMENTS,  CONTRAINTES               *
*-----------------------------------------------------*
*
DE = RESO RIG CHA;
*
SI (NEG GRAPH 'N');
  TITR ' PLAQUE CARREE ORTHOTROPE : DEFORMEE .';
  DEF0 = DEFO SUR  DE 0.;
  DEF1 = DEFO SUR  DE 10. ROUGE;
  TRAC (DEF0 ET DEF1);
FINSI;
*
UA = EXTR DE UX PA; UA1 = 1.098e-4;
ERGUA = 100 * (ABS ((UA1 - UA) / UA1));
*
UD = EXTR DE UX PD; UD1 = 1.003e-4;
ERGUD = 100 * (ABS ((UD1 - UD) / UD1));
*
UE = EXTR DE UX PE; UE1 = 4.768E-5;
ERGUE = 100 * (ABS ((UE1 - UE) / UE1));
*
VA = EXTR DE UY PA; VA1 = -1.309E-4;
ERGVA = 100 * (ABS ((VA1 - VA) / VA1));
*
VD = EXTR DE UY PD; VD1 = -7.134E-5;
ERGVD = 100 * (ABS ((VD1 - VD) / VD1));
*
VE = EXTR DE UY PE; VE1 = -4.965E-5;
ERGVE = 100 * (ABS ((VE1 - VE) / VE1));
*
SAUT 2 LIGN;
MESS '    Resultats : test stru2.dgibi';
MESS '    ----------------------------';
SAUT 1 LIGN;
MESS ' Deplacements';
MESS ' ------------';
SAUT 1 LIGN;
*
MESS '   Point A :';
MESS '   ---------';
MESS '  Deplacement theorique horizontal :' UA1 'm';
MESS '  Deplacement calcule horizontal   :' UA 'm';
MESS '      Soit un ecart de :' ERGUA '%';
SAUT 1 LIGN;
MESS '  Deplacement theorique vertical   :' VA1 'm';
MESS '  Deplacement calcule vertical     :' VA 'm';
MESS '      Soit un ecart de :' ERGVA '%';
SAUT 1 LIGN;
*
MESS '   Point D :';
MESS '   ---------';
MESS '  Deplacement theorique horizontal :' UD1 'm';
MESS '  Deplacement calcule horizontal   :' UD 'm';
MESS '      Soit un ecart de :' ERGUD '%';
SAUT 1 LIGN;
MESS '  Deplacement theorique vertical   :' VD1 'm';
MESS '  Deplacement calcule vertical     :' VD 'm';
MESS '      Soit un ecart de :' ERGVD '%';
SAUT 1 LIGN;
*
MESS '   Point E :';
MESS '   ---------';
MESS '  Deplacement theorique horizontal :' UE1 'm';
MESS '  Deplacement calcule horizontal   :' UE 'm';
MESS '      Soit un ecart de :' ERGUE '%';
SAUT 1 LIGN;
MESS '  Deplacement theorique vertical   :' VE1 'm';
MESS '  Deplacement calcule vertical     :' VE 'm';
MESS '      Soit un ecart de :' ERGVE '%';
SAUT 1 LIGN;
*
*SAUT 1 LIGN;
*MESS ' Contraintes au point E';
*MESS ' ----------------------';
*SAUT 1 LIGN;
*
CHAM1 = SIGM MOD MAT DE;
CHAM2 = CHAN NOEUD MOD CHAM1;
CHPO1 = CHAN CHPO MOD CHAM2;
*
SMXXE = EXTR CHPO1 SMXX PE;
SMXXE = SMXXE / 1.E6;
SMXXE1 = 1.004;
ERGXX = 100 * (ABS ((SMXXE1 - SMXXE) / SMXXE1));
*
SMYYE = EXTR CHPO1 SMYY PE;
SMYYE = SMYYE / 1.E6;
SMXYE = EXTR CHPO1 SMXY PE;
SMXYE = SMXYE / 1.E6;
*
*MESS ' Contrainte theorique horizontale :' SMXXE1
*'MPa';
*MESS ' Contrainte calculee horizontale  :' SMXXE
*'MPa';
*MESS '      Soit un ecart de :' ERGXX '%';
SAUT 1 LIGN;
*MESS ' Contrainte calculee verticale    :' SMYYE
*'MPa';
SAUT 1 LIGN;
*MESS ' Contrainte calculee de cisaillement :' SMXYE
*'MPa';
SAUT 1 LIGN;
*
MESS ' Nombre de noeuds  :' NBNOSUR;
MESS ' Nombre d elements :' NBELSUR;
*
* UTILISATION DE LA PROCEDURE AFFICHE
*
SI (NEG GRAPH 'N');
  AFFICHE RIG CHA SUR 1.E-6 1.E-6;
FINSI;
*
SI (NEG GRAPH 'N');
  CHAM10 = EXCO SMXX CHAM1;
  CHAM10 = 'CHAN' 'TYPE' CHAM10 'SCALAIRE' ;
  TITRE 'CONTRAINTE SMXX';
  TRACE MOD CHAM10 SUR;
*
  CHAM11 = EXCO SMYY CHAM1;
  CHAM11 = 'CHAN' 'TYPE' CHAM11 'SCALAIRE' ;
  TITRE 'CONTRAINTE SMYY';
  TRACE MOD CHAM11 SUR;
*
  CHAM12 = EXCO SMXY CHAM1;
  CHAM12 = 'CHAN' 'TYPE' CHAM12 'SCALAIRE' ;
  TITRE 'CONTRAINTE SMXY';
  TRACE MOD CHAM12 SUR;
*
  CHAM13 = VMIS MOD CHAM1;
  CHAM13 = 'CHAN' 'TYPE' CHAM13 'SCALAIRE' ;
  TITRE 'CONTRAINTE DE VON-MISES';
  TRACE MOD CHAM13 SUR;
FINSI ;
*
*=====================================================*
*                code fonctionnement                  *
*=====================================================*
*
ERGMAX=MAXI (PROG ERGUA ERGUD ERGUE ERGVA ERGVD ERGVD);
*
* Il y a erreur si un des pourcentages ERG*
* est superieur a 5%
*
SAUT 2 LIGN;
SI (ERGMAX <EG 5.);
    ERRE  0 ;
SINO;
    ERRE  5 ;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;






