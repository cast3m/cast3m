* fichier :  castest_lse2_litu.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
**********************************************
***            OPTIONS GENERALES           ***
**********************************************
temps;
*
** Graph = VRAI ;
Graph = FAUX ;
*
option dime 3 elem seg2;
OPTI EPSI LINEAIRE;
*
'SI' Graph ;
  option trace 'PSC' ;
  titre 'MODE PROPRE D UN TUYAU REMPLI DE FLUIDE';
'FINSI' ;
**********************************************
***    DONNEES GENERALES DU MAILLAGE       ***
**********************************************
ndiv = 15;
rac2 = 1.4142136;
x1 = 7*rac2/2.;
x2 = 5*rac2;
p1 = 0 0 0;
p2 = x1 x1 0;
p3 = x2 x2 0;
oeil = 0 0 1000;
*
tuy1 = p1 D ndiv p2;
tuy2 = p2 D ndiv p3;
tuy = tuy1 et tuy2;
*
'SI' Graph ; trace oeil tuy; 'FINSI' ;
**********************************************
***        DEFINITION DES MODELES          ***
**********************************************
obcou1 = mode tuy1 liquide mecanique litu;
obflu1 = mode tuy1 liquide lse2;
obtuy1 = mode tuy1 mecanique tuya;
obcou2 = mode tuy2 liquide mecanique litu;
obflu2 = mode tuy2 liquide lse2;
obtuy2 = mode tuy2 mecanique tuya;
**********************************************
***     CARACTERISTIQUES DU TUYAU          ***
**********************************************
* vitesse du son modifie dans le cas E=2E+11
*vit1 = 1355.2619;
*vit2 = 1380.8619;
*etuy= 2.E11;
*--------------------------------------------*
*    vitesse du son modifie pour E=2E+08     *
*--------------------------------------------*
vit1 = 99.778516;
vit2 = 111.49412;
etuy= 2.E8;
*--------------------------------------------*
*            premier troncon                 *
*--------------------------------------------*
macou1 = mate obcou1 rho 1000. rorf 1000. lcar 1.
rayo .05;
maflu1 = mate obflu1 rho 1000. rorf 1000. lcar 1.
cson vit1 cref 1500. rayo .05;
matuy1 = mate obtuy1 rho 7800. youn etuy nu .3
rayo .055 epai .005;
*--------------------------------------------*
*           deuxieme troncon                 *
*--------------------------------------------*
macou2 = mate obcou2 rho 1000. rorf 1000. lcar 1.
rayo .08;
maflu2 = mate obflu2 rho 1000. rorf 1000. lcar 1.
cson vit2 cref 1500. rayo .08;
matuy2 = mate obtuy2 rho 7800. youn etuy nu .3
rayo .09 epai .01;
**********************************************
***  RIGIDITES ET MASSES ET COND. LIMITES  ***
**********************************************
mas1 = mass obcou1 macou1;
mas2 = mass obflu1 maflu1;
mas3 = mass obtuy1 matuy1;
mas4 = mass obcou2 macou2;
mas5 = mass obflu2 maflu2;
mas6 = mass obtuy2 matuy2;
*
mastot = mas1 et mas2 et mas3 et
mas4 et mas5 et mas6;
*
rig2 = rigi obflu1 maflu1;
rig3 = rigi obtuy1 matuy1;
rig4 = rigi obflu2 maflu2;
rig5 = rigi obtuy2 matuy2;
*--------------------------------------------*
*          conditions limites                *
*--------------------------------------------*
rig6 = bloq ux uy p1;
rig7 = bloq uz rx ry rz tuy;
rig8 = bloq 'P   ' 'PI  ' p1;
*--------------------------------------------*
rigtot = rig2 et rig3 et rig4 et rig5 et rig6 et
rig7 et rig8;
**********************************************
***        RESOLUTION ET AFFICHAGE         ***
**********************************************
res = vibr inte 1. 2. rigtot mastot impr;
*
fre = res . modes . 1 . frequence;
mess 'MODE & DE TRACTION COMPRESSION THEORIQUE 1.57 CALCULE 'fre;
chsol = res . modes . 1 . deformee_modale;
'SI' Graph ;
  ev1 = evol chpo chsol 'P   ' tuy;
  dess ev1;
'FINSI' ;
**********************************************
***        CODE DE BON FONCTIONNEMENT      ***
**********************************************
sol = 1.57;
err = 100*((abs(sol-fre)/sol));
si (err < 5);
erre 0;
sinon;
erre 5;
finsi;
*
saut 2 lign;
temps;
fin;











