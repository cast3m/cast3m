* fichier :  ther1.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

*******************************************
*    Test ther1.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*-----------------------------------------*
*                 THER1                   *
*                                         *
*    TEST DES CL DE TEMPERATURE POUR      *
*       LES ELEMENTS QUA4 ET TRI3         *
*            BIDIMENSIONNELS              *
*-----------------------------------------*
*                                         *
*    TEMPERATURE IMPOSEE + CONVECTION     *
*                        + FLUX           *
*                        + SOURCE         *
*                                         *
* Ce test permet de vérifier le bon
* fonctionnement des divers               *
* opérateurs thermiques de CAST3M         *
*                                         *
* Une plaque rectangulaire constituée     *
* d'éléments QUA4 et TRI3 est soumise à   *
* une température imposée à une de ses    *
* extrémités et sucessivement à une       *
* condition de convection forcée, à une   *
* condition de flux imposé et à une       *
* condition de source volumique imposée.  *
*                                         *
* Les résultats sont présentés sous forme *
* d'isotempératures (elles doivent etre   *
* parallèles aux deux faces supérieure et *
* inférieure de la plaque) et par la      *
* valeur de la température obtenue à la   *
* face inférieure de la plaque (la        *
* température est imposée sur la surface  *
* supérieure).                            *
*                                         *
*-----------------------------------------*

*----- OPTIONS GENERALES DE CALCUL -------*
TITR (CHAI 'C.L. DE TEMPERATURE - ELEMENTS'
           ' TRI3 ET QUA4') ;
OPTION DIME 2 ELEM QUA4 ;

TEMPS ;

*- CREATION DE LA GEOMETRIE:       *
*   POINTS SUPPORTS DES ELEMENTS - *

A1 = 0. 0. ;A2 = 1.5 0. ;A3 = 3. 0. ;
B1 = 0. 2. ;B2 = 1.5 2. ;B3 = 3. 2. ;
C1 = 0. 4. ;C2 = 1.5 4. ;C3 = 3. 4. ;
D1 = 0. 6. ;D2 = 1.5 6. ;D3 = 3. 6. ;

*- CREATION DES QUADRANGLES ET TRIANGLES -*

QQ01 = MANU QUA4 A1 A2 B2 B1 ;
QQ02 = MANU QUA4 A2 A3 B3 B2 ;
QQ03 = MANU TRI3 B1 B2 C2 ;
QQ04 = MANU TRI3 B2 B3 C2 ;
QQ05 = MANU TRI3 B1 C2 C1 ;
QQ06 = MANU TRI3 B3 C3 C2 ;
QQ07 = MANU QUA4 C1 C2 D2 D1 ;
QQ08 = MANU QUA4 C2 C3 D3 D2 ;

QTOT = QQ01 ET QQ02 ET QQ03 ET QQ04 ET QQ05
            ET QQ06 ET QQ07 ET QQ08 ;

SI(NEG GRAPH 'N');
    TRACE 'QUAL' QTOT ;
FINSI;

QSOUR = QQ07 ET QQ08 ;

DD1 = D 1 D3 D2 ;
DD2 = D 1 D2 D1 ;
L_SUP = DD1 ET DD2 ;

DD3 = D 1 A3 A2 ;
DD4 = D 1 A2 A1 ;
L_INF = DD3 ET DD4 ;

*--- DONNEES DU PROBLEME DE THERMIQUE ----*

*--------------  MODELISATION ------------*

MOD1 = MODE QTOT  THERMIQUE ISOTROPE ;
MOD2 = MODE L_SUP THERMIQUE CONVECTION ;

*DONNEES DES CARACTERISTIQUES DU MATERIAU

KCOND = 100. ;
MAT1  = MATE MOD1 'K' KCOND ;

HCONV = 15. ;
MAT2  = MATE MOD2 'H' HCONV ;

*- CREATION DES MATRICES DE CONDUCTIVITE -*

CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;

*- TEMPERATURES IMPOSEES: BLOQUE + DEPI --*

BB1 = BLOQUE L_INF 'T' ;
T0  = 10. ;
EE1 = DEPI BB1 T0. ;

*- FLUX EQUIVALENTS A LA CONDITION DE
*                             CONVECTION -*

CNV1 = CONVECTION MOD2 MAT2 'T' 200. ;

*--------------- FLUX IMPOSE -------------*
FLU1 = FLUX MOD1 20. L_SUP ;

*----------- SOURCE DE CHALEUR -----------*
VALQ = 10 ;
S1 = SOURCE MOD1 VALQ QSOUR ;

*- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -*
CCC1 = CND1 ET CND2 ET BB1 ;
CCC2 = CND1 ET BB1 ;

*-ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS
FFF1 = EE1 ET CNV1 ;
FFF2 = EE1 ET FLU1 ;
FFF3 = EE1 ET S1 ;

*----- 1ERE RESOLUTION : CONVECTION ------*
CCC1bis = 'KOPS' 'RIMA' CCC1 ;
CCC1bis = 'KOPS' 'CHANINCO' CCC1bis
    ('MOTS' 'T' 'LX') ('MOTS' 'T' 'LX')
    ('MOTS' 'Q' 'FLX') ('MOTS' 'T' 'LX') ;
CCC1bis = kops TRANSPOS CCC1bis;
FFF1bis = 'NOMC' FFF1 ('MOTS' 'Q' 'FLX')
                      ('MOTS' 'T' 'LX')
                    'NATURE' 'DISCRET' ;

rv='EQEX' ;
rv . 'METHINV' . 'TYPINV' = 5 ;
rv . 'METHINV' . 'IMPINV' = 2 ;
rv . 'METHINV' . 'MATASS' = CCC1bis ;
rv . 'METHINV' . 'MAPREC' = CCC1bis ;
chter1b = 'KRES' CCC1bis 'TYPI'
          (rv . 'METHINV') 'SMBR' FFF1bis ;

CHTER1 = RESO CCC1 FFF1 ;
MESS 'Ecart entre les deux solveurs :'
MESS ('MAXI' ('-' chter1b chter1) 'ABS') ;

*-------- 2EME RESOLUTION : FLUX ---------*
CHTER2 = RESO CCC2 FFF2 ;

*------- 3EME RESOLUTION: SOURCE ---------*
CHTER3 = RESO CCC2 FFF3 ;

*--- POST-TRAITEMENT: TRACE DES CHAMPS ---*
*               RESULTATS                 *
*- ET CALCUL DES VALEURS CARACTERISTIQUES *
TITR 'ELEMENTS TRI3 ET QUA4 : CONVECTION';
SI(NEG GRAPH 'N');
     TRAC QTOT CHTER1 ;
FINSI;
TITR 'ELEMENTS TRI3 ET QUA4 : FLUX IMPOSE';
SI(NEG GRAPH 'N');
     TRAC QTOT CHTER2 ;
FINSI;
TITR 'ELEMENTS TRI3 ET QUA4 : SOURCE' ;
SI(NEG GRAPH 'N');
     TRAC QTOT CHTER3 ;
FINSI;

T1 = EXTR CHTER1 T D2 ;
T2 = EXTR CHTER2 T D2 ;
T3 = EXTR CHTER3 T D2 ;

TEMPS ;

*   CODE DE FONCTIONNEMENT
TREF1 = 100.;
TREF2 = 11.2;

* Calcul de la température en D2
TREF3 = T0 - ( (VALQ / (2. * KCOND)) *
              (
               ((COOR 2 D2)**2) -
               (2*(COOR 2 D2)*(COOR 2 D2))+
               ((COOR 2 C2)**2)
              )
             );

RESI1=100. * (ABS((T1-TREF1)/TREF1));
RESI2=100. * (ABS((T2-TREF2)/TREF2));
RESI3=100. * (ABS((T3-TREF3)/TREF3));

* TEST CONVECTION
MESS 'Temperature theorique :' TREF1 '°C';
MESS 'Temperature calculee  :' T1    '°C';
MESS '    Soit un ecart de : ' RESI1 '%';
SAUTER 1 LIGNES ;

* TEST FLUX
MESS 'Temperature theorique :' TREF2 '°C';
MESS 'Temperature calculee  :' T2    '°C';
MESS '    Soit un ecart de : ' RESI2 '%';
SAUTER 1 LIGNES ;

* TEST SOURCE
MESS 'Temperature theorique :' TREF3 '°C';
MESS 'Temperature calculee  :' T3    '°C';
MESS '    Soit un ecart de : ' RESI3 '%';
SAUTER 1 LIGNES ;


RESITOT = PROG RESI1 RESI2 RESI3;
SI((MAXI RESITOT) <EG 5.);
  ERRE  0;
SINO;
  ERRE  5;
FINSI;
FIN;




