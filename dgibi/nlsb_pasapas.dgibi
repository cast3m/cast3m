************************************************************************
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Endommagement
************************************************************************
*______________________________________________________________________*
*                                                                      *
*      Cas test de l'implantation Nonlocal Stress Based (NLSB)         *
*______________________________________________________________________*
*----------------------------------------------------------------------*
* Description du cas test : Chargement uniaxial sur un cube            *
* -----------------------                                              *
*                                                                      *
* L'objectif de l'essai est de verifier le bon fonctionnement de la    *
* regularisation nonlocale base sur l'etat de contrainte dans un calcul*
* nonlineaire (PASAPAS). Le modeles d'endommagement considere   pour   *
* le test de validation est le modele MAZARS.                          *
*                                                                      *
* On remarquera, que la regularisation n'est pas dependante du modele  *
* et qu'elle peut-etre applique a n'importe quel modele a partir du    *
* moment ou la variable a regulariser est fournie.                     *
*                                                                      *
* C. Giry, F. Dufour, J. Mazars                                        *
* -----------------------------                                        *
* (fichier base sur ricrag_3D.dgibi)                                   *
************************************************************************
graph = 'N';
COMPLET = FAUX ;

* Choix du modele d'endommagement teste
* -------------------------------------
* nmod = 1; modele MAZARS

nmod  = 1;
*
*-------------- Options de calcul ---------------------
*
OPTION DIME 3 ELEM CUB8;

nbel  = 5;
tele  = 1./nbel;
tdef  = 0.02;
nbelh = 2;
*
*-------------- Definition de la geometrie ------------
*
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;

L1 = P1 DROIT nbel P2 ;
L2 = P2 DROIT nbel P3 ;
L3 = P3 DROIT nbel P4 ;
L4 = P4 DROIT nbel P1 ;

SURF1 = DALL L1 L2 L3 L4;

P11 = tdef tdef (0.5-(0.5*tele));
P21 = (1-tdef) tdef (0.5-(0.5*tele));
P31 = (1-tdef) (1-tdef) (0.5-(0.5*tele));
P41 = tdef (1-tdef) (0.5-(0.5*tele));

L11 = P11 DROIT nbel P21 ;
L21 = P21 DROIT nbel P31 ;
L31 = P31 DROIT nbel P41 ;
L41 = P41 DROIT nbel P11 ;

SURF11 = DALL L11 L21 L31 L41;

P12 = tdef tdef (0.5+(0.5*tele));
P22 = (1-tdef) tdef (0.5+(0.5*tele));
P32 = (1-tdef) (1-tdef) (0.5+(0.5*tele));
P42 = tdef (1-tdef) (0.5+(0.5*tele));

L12 = P12 DROIT nbel P22 ;
L22 = P22 DROIT nbel P32 ;
L32 = P32 DROIT nbel P42 ;
L42 = P42 DROIT nbel P12 ;

SURF12 = DALL L12 L22 L32 L42;

P13 = 0. 0. 1.;
P23 = 1. 0. 1.;
P33 = 1. 1. 1.;
P43 = 0. 1. 1.;

L13 = P13 DROIT nbel P23 ;
L23 = P23 DROIT nbel P33 ;
L33 = P33 DROIT nbel P43 ;
L43 = P43 DROIT nbel P13 ;

SURF2 = DALL L13 L23 L33 L43;


VOLTO = SURF1 VOLU nbelh SURF11 ;
VOLT1 = SURF11 VOLU 1 SURF12 ;
VOLT2 = SURF12 VOLU nbelh SURF2 ;

VOLTOT = VOLTO ET VOLT1 ET VOLT2;
*
*------- Definition des conditions aux limites --------
*----------- et des deplacements imposes --------------
*
CL  = BLOQ SURF1 UZ;
CLL = BLOQ P1 'DEPL';
CL1 = BLOQ SURF2 UZ;
CL2 = bloq P2 UY;
CL  = CL et CL2;

D1 = DEPI CL1 1;
*
*----- Definition du modele ---------------------------
*
MOD1 = MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT MAZARS
            'NON_LOCAL' 'SB'  'V_MOYENNE' ('MOTS' 'EPTI') ;
* Parametres
* ----------
EBETON  = 34100E6;
NUBETON = 0.16;

* Longueur interne
lc      = 0.3;

* Module d'Young
youngn = EBETON;

* Coefficient de Poisson
nun    = NUBETON;

* RÃ©sistance en traction
ftn    = 3.6e6;

***************** Modele MAZARS **************

* Seuil de deformation 1ere fissuration
 y0    = ftn/ebeton ;

* Parametres At et Bt (comportement post-pic en traction)
 at = 0.95  ;
 bt = 10000.;

* Parametres At et Bt (comportement non lineaire en compression)
 ac = 1.25 ;
 bc = 1000.;

* Parametre beta (correction pour le cisaillement)
 bet   = 1.06   ;

*
* Material
*

mat1 = MATE mod1 YOUN youngn NU   nun KTR0 y0
                 ATRA at     BTRA bt
                 ACOM ac     BCOM bc  BETA bet
                'LCAR' LC   'SBFT' FTN ;
*
*----- Definition du chargement -------------------
*
LI1  = PROG 0. 1. ;
LI2  = PROG 0. 2.e-3;
'SI' ('NON' COMPLET) ;
  LIS1 = PROG 0. PAS 0.005 0.15 ;
'SINON' ;
  LIS1 = PROG 0. PAS 0.005 1.00 ;
'FINSI' ;
EV = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;
*
*----------- Calcul par l'operateur PASAPAS ------------
*
CO1    = CONNEC mod1 MAT1 'NORMAL';
CHAMFT = MANU 'CHML' mod1 'FT' ftn 'STRESSES';

TAB1                       = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CL ET CLL ET CL1;
TAB1.'MODELE'              = MOD1;
TAB1.'MOVA'                = 'MOT' 'D   ';
TAB1.'CHARGEMENT'          = CHA1;
TAB1.'CARACTERISTIQUES'    = MAT1;
TAB1.'TEMPS_CALCULES'      = LIS1;
*TAB1.'CONN'                = CO1;
**TAB1.'NON_LOCAL'           = 'MOT' 'SB' ;
**TAB1.'LVIAM'               = 'MOTS' 'EPTI' ;
**TAB1.'LCAR'                = LC;
**TAB1.'NLOC_SB_MODL'        = MOD1;
**TAB1.'NLOC_SB_FT'          = CHAMFT;
**TAB1.'NLOC_SB_FT'          = ftn ;

PASAPAS TAB1 ;

dd = dime tab1.deplacements;

*
*----------- Courbe effort-deplacement -----------------
*
si (ega graph 'O');
 ev2=@global tab1 CL1 EV fz;
 DESS EV2;
 endf = EXCO (tab1.variables_internes.(dd-1)) 'D';
 trac endf mod1;
finsi;

endf        = EXCO (tab1.variables_internes.(dd-1)) 'D';
'SI' ('NON' COMPLET) ;
  dlim        = 0.99 ;
'SINO' ;
  dlim        = 0.999 ;
'FINSI' ;
mailend     = endf ELEM 'SUPERIEUR' dlim;
XX1 YY1 ZZ1 = COOR MAILEND;
Zmin        = MINI ZZ1;
Zmax        = MAXI ZZ1;

message 'Taille zone completement endommagee';
err = Zmax-Zmin;
list err;

si (err > (1.1*tele));
erreur(5);
finsi;

fin;







