* fichier : diffu4.dgibi

************************************************************************
* Section : Diffusion Fick
************************************************************************

************************************************************************
* Ce Cas-Test Vérifie le bon fonctionnement du modele de DIFFUSION dans
* les éléments finis suivants : COQ3,COQ4,COQ6,COQ8,MASSIFS(3D)
*    - MODE
*    - MATE
*    - COND
*    - BLOQ
*    - CAPA
*
* Le test consiste a comparer le CHPOINT solution obtenue entre la
* THERMIQUE et la DIFFUSION
************************************************************************

OPTI 'DIME' 3;
OPTI 'ELEM' 'CUB8';
OPTI 'TRAC' 'X';

PRECIS = VALE 'PREC';

BLIST  = FAUX ; 'COMM' 'Mettre VRAI pour avoir les LIST des OBJETS  ';
BTRAC  = FAUX ; 'COMM' 'Mettre VRAI pour avoir les TRAC des CHPOINTS';

************************************************************************
*                               MAILLAGE                               *
************************************************************************

* Nom des composantes PRIMALES et DUALES
MP = 'B';
MD = 'Z';

* POINTS *
P1 = 0. 0. 0. ;
P2 = 1. 0. 0. ;

* LIGNES *
NBE1 = 10 ;
L1   = DROI NBE1 P1 P2;
L2   = L1 'PLUS' (0. 1. 0.);

* SURFACES *
S1   = 'REGL' NBE1 L1 L2   ;
S2   = S1 'PLUS' (0. 0. 1.);
S3   = CHAN S2 'TRI3';
P3   = POIN S3 'PROC' (0. 0. 1.);
P4   = POIN S3 'PROC' (1. 1. 1.);
P5   = POIN S1 'PROC' (1. 1. 0.);

S11  = CHAN S1 'QUA8';
S31  = CHAN S3 'TRI6';

* VOLUMES *
V1   = S1 VOLU NBE1 S2;

* COULEURS *
S1   = S1  'COUL' 'BLEU' ;
S11  = S11 'COUL' 'BLEU' ;
S2   = S2  'COUL' 'ROUG' ;
S3   = S3  'COUL' 'JAUN' ;
S31  = S31 'COUL' 'JAUN' ;

* TRACE *
SI BTRAC;
  TRAC 'CACH' 'QUAL'  V1         ;
  TRAC 'CACH' 'QUAL' (S1  ET S3 );
  TRAC 'CACH' 'QUAL' (S11 ET S31);
FINS;

************************************************************************
*                                MODELE                                *
************************************************************************
MODTHE0 ='MODE'  V1          'THERMIQUE' 'CONDUCTION'              ;
MODTHE1 ='MODE' (S1 ET S3)   'THERMIQUE' 'CONDUCTION' 'COQ3' 'COQ4';
MODTHE2 ='MODE' (S11 ET S31) 'THERMIQUE' 'CONDUCTION' 'COQ6' 'COQ8';

MODDIF0 ='MODE'  V1          
                   'DIFFUSION' 'FICK'               'INCO' MP MD;
MODDIF1 ='MODE' (S1 ET S3)
                   'DIFFUSION' 'FICK' 'COQ3' 'COQ4' 'INCO' MP MD;
MODDIF2 ='MODE' (S11 ET S31)
                   'DIFFUSION' 'FICK' 'COQ6' 'COQ8' 'INCO' MP MD;
SI BLIST;
  LIST MODTHE0;
  LIST MODDIF0;
  LIST MODTHE1;
  LIST MODDIF1;
  LIST MODTHE2;
  LIST MODDIF2;
FINS;

************************************************************************
*                               MATERIAU                               *
************************************************************************
MATTHE0 = MATE MODTHE0 'K'  2.D0 'RHO'  1.D0 'C' 1.D0             ;
MATTHE1 = MATE MODTHE1 'K'  2.D0 'RHO'  1.D0 'C' 1.D0 'EPAI' 1.D-5;
MATTHE2 = MATE MODTHE2 'K'  2.D0 'RHO'  1.D0 'C' 1.D0 'EPAI' 1.D-5;
MATDIF0 = MATE MODDIF0 'KD' 2.D0 'CDIF' 1.D0                      ;
MATDIF1 = MATE MODDIF1 'KD' 2.D0 'CDIF' 1.D0          'EPAI' 1.D-5;
MATDIF2 = MATE MODDIF2 'KD' 2.D0 'CDIF' 1.D0          'EPAI' 1.D-5;

SI BLIST;
  LIST MATTHE0;
  LIST MATDIF0;
  LIST MATTHE1;
  LIST MATDIF1;
  LIST MATTHE2;
  LIST MATDIF2;
FINS;

************************************************************************
*                             CONDUCTIVITE                             *
************************************************************************
CONDTH0 = COND MODTHE0 MATTHE0;
CONDDI0 = COND MODDIF0 MATDIF0;
CONDTH1 = COND MODTHE1 MATTHE1;
CONDDI1 = COND MODDIF1 MATDIF1;
CONDTH2 = COND MODTHE2 MATTHE2;
CONDDI2 = COND MODDIF2 MATDIF2;

SI BLIST;
  LIST CONDTH0;
  LIST CONDDI0;
  LIST CONDTH1;
  LIST CONDDI1;
  LIST CONDTH2;
  LIST CONDDI2;
FINS;


************************************************************************
*                             BLOCAGES                                 *
************************************************************************
BLTH1 = BLOQ 'T' P1;
DDTH1 = DEPI BLTH1 0.D0;
BLTH2 = BLOQ 'T' P5;
DDTH2 = DEPI BLTH2 1.D0;
BLTH3 = BLOQ 'T' P3;
DDTH3 = DEPI BLTH3 0.D0;
BLTH4 = BLOQ 'T' P4;
DDTH4 = DEPI BLTH4 1.D0;

BLDI1 = BLOQ MP P1;
DDDI1 = DEPI BLDI1 0.D0;
BLDI2 = BLOQ MP P5;
DDDI2 = DEPI BLDI2 1.D0;
BLDI3 = BLOQ MP P3;
DDDI3 = DEPI BLDI3 0.D0;
BLDI4 = BLOQ MP P4;
DDDI4 = DEPI BLDI4 1.D0;

************************************************************************
*                             RESOLUTION                               *
************************************************************************
RIGTH0 = CONDTH0 ET BLTH1 ET BLTH2 ET BLTH3 ET BLTH4;
SECTH0 = DDTH1 ET DDTH2 ET DDTH3 ET DDTH4;
T0 = RESO RIGTH0 SECTH0;

RIGTH1 = CONDTH1 ET BLTH1 ET BLTH2 ET BLTH3 ET BLTH4;
SECTH1 = DDTH1 ET DDTH2 ET DDTH3 ET DDTH4;
T1 = RESO RIGTH1 SECTH1;

RIGTH2 = CONDTH2 ET BLTH1 ET BLTH2 ET BLTH3 ET BLTH4;
SECTH2 = DDTH1 ET DDTH2 ET DDTH3 ET DDTH4;
T2 = RESO RIGTH2 SECTH2;

RIGDI0 = CONDDI0 ET BLDI1 ET BLDI2 ET BLDI3 ET BLDI4;
SECDI0 = DDDI1 ET DDDI2 ET DDDI3 ET DDDI4;
D0 = RESO RIGDI0 SECDI0;

RIGDI1 = CONDDI1 ET BLDI1 ET BLDI2 ET BLDI3 ET BLDI4;
SECDI1 = DDDI1 ET DDDI2 ET DDDI3 ET DDDI4;
D1 = RESO RIGDI1 SECDI1;

RIGDI2 = CONDDI2 ET BLDI1 ET BLDI2 ET BLDI3 ET BLDI4;
SECDI2 = DDDI1 ET DDDI2 ET DDDI3 ET DDDI4;
D2 = RESO RIGDI2 SECDI2;

SI BTRAC;
  TRAC (T0 ET D0)  V1         ;
  TRAC (T1 ET D1) (S1  ET S3 );
  TRAC (T2 ET D2) (S11 ET S31);
FINS;


************************************************************************
*                               CAPACITE                               *
************************************************************************
CAPATH0 = CAPA MODTHE0 MATTHE0;
CAPATH1 = CAPA MODTHE1 MATTHE1;
CAPATH2 = CAPA MODTHE2 MATTHE2;
CAPADI0 = CAPA MODDIF0 MATDIF0;
CAPADI1 = CAPA MODDIF1 MATDIF1;
CAPADI2 = CAPA MODDIF2 MATDIF2;

SI BLIST;
  LIST CAPATH0;
  LIST CAPADI0;
  LIST CAPATH1;
  LIST CAPADI1;
  LIST CAPATH2;
  LIST CAPADI2;
FINS;


************************************************************************
*                            COMPARAISONS                              *
************************************************************************
LMOT0a = MOTS 'LX' 'B';
LMOT0b = MOTS 'LX' 'T';
D0b    = CHAN 'COMP' LMOT0a LMOT0b D0;
T0c    = EXCO 'T' T0 ;
D0c    = EXCO 'T' D0b;
RESU0  = RESU (ABS (T0c - D0c));
MAX0   = MAXI RESU0;

LMOT1a = MOTS 'LX' 'BIN ' 'B   ' 'BSU ';
LMOT1b = MOTS 'LX' 'TINF' 'T   ' 'TSUP';
D1b    = CHAN 'COMP' LMOT1a LMOT1b D1;
T1c    = EXCO (MOTS 'TINF' 'T   ' 'TSUP') T1 ;
D1c    = EXCO (MOTS 'TINF' 'T   ' 'TSUP') D1b;
RESU1  = RESU (ABS (T1c - D1c));
MAX1   = MAXI RESU1;

LMOT2a = MOTS 'LX' 'BIN ' 'B   ' 'BSU ';
LMOT2b = MOTS 'LX' 'TINF' 'T   ' 'TSUP';
D2b    = CHAN 'COMP' LMOT2a LMOT2b D2;
T2c    = EXCO (MOTS 'TINF' 'T   ' 'TSUP') T2 ;
D2c    = EXCO (MOTS 'TINF' 'T   ' 'TSUP') D2b;
RESU2  = RESU (ABS (T2c - D2c));
MAX2   = MAXI RESU2;


************************************************************************
*                            VERIFICATION                              *
************************************************************************
MESS 'MAXI DE LA RESULTANTE MAX0:' MAX0;
PRECIS0 = PRECIS * (MAXI T0);
SI (MAX0 >EG PRECIS0);
  ERRE 5;
FINS;

MESS 'MAXI DE LA RESULTANTE MAX1:' MAX1;
PRECIS1 = PRECIS * (MAXI T1);
SI (MAX1 >EG PRECIS1);
  ERRE 5;
FINS;

MESS 'MAXI DE LA RESULTANTE MAX2:' MAX2;
PRECIS2 = PRECIS * (MAXI T2);
SI (MAX2 >EG PRECIS2);
  ERRE 5;
FINS;


FIN;



