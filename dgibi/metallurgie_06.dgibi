* fichier :  metallurgie_06.dgibi
************************************************************************
* Section : Thermique Metallurgie
************************************************************************

*__________________________________________________________________*
*   TEST METALLURGIE_06                                            *
*                                                                  *
*   CALCUL DES PROPORTIONS DE PHASE METALLURGIQUE                  *
*                                                                  *
*   Un MODELE thermo-metallurgique est cree et l'essai JOMINY      *
*   est simule en 2D Axisymétrique                                 *
*                                                                  *
*   Le modèle métallurgique est codé en GIBIANE                    *
*      - Procédure PARATHER : Mise a jour du matériaux THERMIQUE   *
*                             avec la proportion de phase          *
*      - Procédure CHARTHER : Mise a jour des proportions de phase *
*                             selon un schéma EULER 1 Implicite    *
*                                                                  *
*   Le modèle métallurgique contient 5 phases ayant des cinétiques *
*   de transformation différentes                                  *
*      - Metal de Base (MB)                                        *
*      - Austénite                                                 *
*      - Martensite                                                *
*      - Bainite                                                   *
*      - Ferrite                                                   *
*__________________________________________________________________*
*   MAILLAGE :                                                     *
*    - Eprouvette cylindrique de hauteur 100mm et de rayon 12,5mm  *
*      P4 _____ P3                                                 *
*        |  J  |    ^                                              *
*        |  O  |    |                                              *
*        |  M  |    |                                              *
*        |  I  |    | h0      Y                                    *
*        |  N  |    |         ^                                    *
*        |  Y  |    |         |                                    *
*      P1 _____ P2  v         o --> X                              *
*                                                                  *
*        <-----> r0                                                *
*      Origine du repere : P1                                      *
*                                                                  *
*   CHARGEMENT :                                                   *
*    - Montee lineaire en temperature depuis 25°C à 900°C en 1200s *
*    - Maintient à 900°C jusqu'a 3000s                             *
*    - Refroidissement par convection par la face cylindrique      *
*      inférieure jusqu'à 3600s                                    *
*                                                                  *
*   CL :                                                           *
*    - Blocage de la temperature (T imposee jusqu'a 3000s)         *
*    - Convection de 3000s à 3600s avec h=0,1W.mm-2.K-1            *
*__________________________________________________________________*
OPTI DIME 2 MODE AXIS ELEM QUA4 ;

'OPTI' 'TRAC' PSC 'EPTR' 10 ;

TMAX = 900.  ;
TMIN = 25.   ;
HCON = 0.1   ;

************************************************************************
*  Pour faire des beaux DESSINS
************************************************************************
* LISTMOTS contenant les couleurs
  LESCOUL = 'MOTS' 'VIOL' 'VERT' 'CYAN' 'BRIQ' 'BOUT' 'JAUN' 'OLIV'
                   'ORAN' 'ROUG' 'BLEU' 'BRUN' 'CARA' 'GRIS' 'NOIR' ;
  NBCOUL  = 'DIME' LESCOUL;

* LISTMOTS contenant les marques dans DESS
  LESMARQ = 'MOTS' 'CROI' 'PLUS' 'MOIN' 'BARR' 'ETOI' 'CARR'
                   'LOSA' 'ROND' 'TRID' 'TRIU' 'TRIL' 'TRIR';
  NBMARQ  = 'DIME' LESMARQ;
************************************************************************

************************************************************************
* Debut de la procedure CHARTHER
************************************************************************
'DEBP' CHARTHER PRECED*'TABLE'  tt*'FLOTTANT' ;
************************************************************************
* PROCEDURE CHARTHER
* - met a jour la fraction de PHASE dans le materiaux
*     Intégration numérique selon un schéma d'Euler d'ordre 1 avec la
*     TETA méthode.
************************************************************************
  ETAB   = PRECED.'WTABLE'         ;
  XTOLR  = ETAB.'PRECISION'        ;
  XZPREC =('VALE' 'PREC') * 100.D0 ;


  SI ('EXIS'   PRECED 'PTOL') ;
    XTOLA  = PRECED.'PTOL';
  SINO;
    XTOLA  = XTOLR ;
  FINS;
  SI ('EXIS'   PRECED 'NITERPHASE_MAX') ;
    NITMAX  = PRECED.'NITERPHASE_MAX';
  SINO;
    NITMAX  = 200 ;
  FINS;
  SI ('EXIS'   PRECED 'IGNORE_NON_CONV') ;
    BIGNORE  = PRECED.'IGNORE_NON_CONV';
  SINO;
    BIGNORE  = FAUX ;
  FINS;

  MODCOND  = ETAB.'MOD_COND';
  MODRCOND = MODCOND;
  PARA0 ='VALE' 'PARA'            ;
  SI ('EGA' ('TYPE' MODRCOND) 'TABLE');
   'OPTI' 'PARA' VRAI;
  FINS;

* Récuperation du numero de PAS du calcul et du DT
  NPA ='DIME' PRECED.'TEMPS';
  DT  = ETAB.'DT' ;

* Récuperation des proportions de phase en debut de pas et courant
  Phas1   = 'REDU' (PRECED.'PROPORTIONS_PHASES' .(NPA - 1)) MODRCOND;
  LISCOM  = 'EXTR' Phas1 'COMP'   ;
  Phas1   = 'REDU' Phas1  MODRCOND;
  SI (EXIS  PRECED.'PROPORTIONS_PHASES'   NPA )   ;
    Phas2 = 'REDU' (PRECED.'PROPORTIONS_PHASES' . NPA) MODRCOND     ;
  SINO ;
    Phas2 = Phas1;
  FINS ;

  TPS_1        = PRECED.'TEMPS'.(NPA - 1);
  TPS_2        = tt                      ;

  T_CHPO1  ='EXCO' PRECED.'CONTINUATION'.'TEMPERATURES' 'T'  'SCAL' ;
  T_CHPO2  ='EXCO' ETAB  .'THER_COURANT'                'T'  'SCAL' ;
  T_CHAM1  ='CHAN' 'CHAM' T_CHPO1 MODRCOND  ;
  T_CHAM2  ='CHAN' 'CHAM' T_CHPO2 MODRCOND  ;

  TPOINT   = (T_CHAM2 - T_CHAM1) / (TPS_2 - TPS_1);
  MTPOIN   = -1.D0 * TPOINT;
  TPOINT1  ='CHAN' 'COMP' TPOINT 'TPOI';

  LREAC    = TPHASE . 'REACTIONS' ;
  TREAC    = TPHASE . 'TYPE'      ;
  NBREAC   ='DIME' TREAC          ;
  NBPHAS   ='MAXI' LREAC          ;

  TPHASP   ='TABL';
  REPE SURREAC NBREAC;
    IREAC = &SURREAC;
    TPHASP.IREAC ='REDU' TPHASE.IREAC MODRCOND ;
  FIN SURREAC;

* THETA  :instant relatif sur le Sous-Pas pour sont tirés les paramètres
  THETA    = 0.5D0 ;

  TPS_I    =((1.D0 - THETA)*TPS_1  ) + (THETA*TPS_2  );
  T_CHAM_I =((1.D0 - THETA)*T_CHAM1) + (THETA*T_CHAM2);
  ETATi    =('CHAN'  'COMP' T_CHAM_I 'T') 'ET' TPOINT1;

*   Convergence sur les proportions de phase
  REPE CONVERG NITMAX;
    ICONV    = &CONVERG;
    Phas_I   =((1.D0 - THETA)*Phas1  ) + (THETA*Phas2  );
*   'MESS' '  ICONV=' ICONV;

    Ai        = 'REDU' ('VIDE' 'MCHAML') MODRCOND;
    REPE SURPAH NBPHAS;
*     Boucle sur toutes les phases
      IPHAS = &SURPAH;

      REPE SURREAC NBREAC;
*       Boucle sur toutes les reactions possibles
        IREAC =&SURREAC;
        II    ='EXTR' LREAC (2 * (IREAC - 1) + 1);
        JJ    ='EXTR' LREAC (2 *  IREAC         );

        SI (('EGA' IPHAS JJ) 'OU' ('NEG' IPHAS II));
          'ITER' SURREAC;
        FINS;

        CTYP  ='EXTR' TREAC IREAC ;
*      'MESS' ('CHAI' 'Reaction 'II '==>' JJ ' : ' CTYP);

        NOMi  ='EXTR' LISCOM II           ;
        Pi1   ='EXCO' Phas1  NOMi 'SCAL';
        Pi    ='EXCO' Phas_I NOMi 'SCAL';

        NOMj  ='EXTR' LISCOM JJ ;
        Pj1   ='EXCO' Phas1  NOMj 'SCAL';
        Pj    ='EXCO' Phas_I NOMj 'SCAL';

*       Instanciation des champs pour T et dT/dt donné
        CHPROPi ='VARI' 'NUAG'  MODRCOND TPHASP.IREAC ETATi 'NOEUD';

        SI ('EGA' CTYP 'LEBL');
*         Cas Leblond
          Pjeq ='EXCO' CHPROPi 'PEQ' 'SCAL';
          Tauij='EXCO' CHPROPi 'TAU' 'SCAL';
          Fij  ='EXCO' CHPROPi 'F'   'SCAL';
          Kij  =  1. * Pjeq / Tauij        ;
          Kij2  =  (1. - Pjeq) / Tauij        ;

          Aij  = -1 * ((Kij * Pi) - (Kij2 * Pj)) * Fij ;
          Aij = Aij * (Aij 'MASQ' 'INFERIEUR' 0.D0) ;
        FINS;

        SI ('EGA' CTYP 'KOMA');
*         Cas Koistinen-Marburger
          KM    ='EXCO' CHPROPi 'KM' 'SCAL'    ;
          MS    ='EXCO' CHPROPi 'MS' 'SCAL'    ;

          MASQ1 ='MASQ' T_CHAM_I 'EGINFE' MS   ;
          MASQ2 ='MASQ' TPOINT   'EGINFE' 0.D0 ;
          MASQ1 ='REDU' MASQ1 MODRCOND         ;
          MASQ2 ='REDU' MASQ2 MODRCOND         ;
          Kij   = MASQ1 * KM                   ;
          Fij   = MASQ2 * TPOINT               ;

          Aij  = Kij * Fij * Pi ;
        FINS;

*       Controle pour que Pi <= 1 >= 0
        SI ('EXIS' Ai NOMi);
          Aiprec = 'CHAN' 'COMP' ('EXCO' Ai NOMi) 'SCAL' ;
        SINO;
          Aiprec = 0.D0;
        FINS;

        Aitest = Aiprec '+' Aij;
        Pitest = Pi1 '+' (DT * Aitest);

        MASQiinf ='MASQ'  Pitest               'INFERIEUR' 0.D0 ;
        MASQisup ='MASQ'  Pitest               'SUPERIEUR' 1.D0 ;
        MASQiok  ='MASQ' (MASQiinf + MASQisup) 'INFERIEUR' 0.5D0;
        MASQiinf ='REDU' MASQiinf MODRCOND ;
        MASQisup ='REDU' MASQisup MODRCOND ;
        MASQiok  ='REDU' MASQiok  MODRCOND ;

        SI('MAXI' MASQiinf '>EG' 0.5);
*         Correction de Aij
          Aic = MASQiinf * (((XZPREC - Pi1)/DT)
                - Aitest);
          Aij = Aij '+' Aic;
        FINS;

        SI('MAXI' MASQisup '>EG' 0.5);
*         Correction de Aij
          Aic = MASQisup * (((1.D0 - XZPREC - Pi1)/DT)
                - Aitest);
          Aij = Aij '+' Aic;
        FINS;

        Ai = ( 'CHAN' 'COMP' Aij NOMi         ) '+'
             (('CHAN' 'COMP' Aij NOMj) * -1.D0) '+'
                Ai;
      FIN SURREAC;
    FIN SURPAH;

*   Intégration explicite sur le pas de temps
    Phas2p= Phas2;       'COMM' 'Iteration precedente pour convergence';
    Phas2 = Phas1 '+' (DT * Ai);

*   Test sur la convergence absolue uniquement
    SI (ICONV > 1);
      DIFFABS = 'ABS' (Phas2  -        Phas2p)              ;
      DIFFI   =('ABS'  Phas2) - ('ABS' Phas2p)              ;
      MASQI1  = 'MASQ' DIFFI 'SUPERIEUR' 0.D0               ;
      MASQI2  = 1.D0 - MASQI1                               ;
      MASQI1  = 'REDU' MASQI1 MODRCOND                      ;
      MASQI2  = 'REDU' MASQI2 MODRCOND                      ;
      MAXD1   = 'ABS' ((Phas2 * MASQI1) + (Phas2p * MASQI2));

      CRITNUM = DIFFABS - (MAXD1          * XZPREC);
      CRITREL = DIFFABS - (('ABS' Phas2p) * XTOLR );
      CRITABS = DIFFABS -                   XTOLA  ;
      TOTO = 'MAXI' CRITNUM;
      TITI = 'MAXI' CRITREL;
      TATA = 'MAXI' CRITABS;
      MASQNUM ='MASQ' CRITNUM 'SUPERIEUR' 0.D0     ;
      MASQREL ='MASQ' CRITREL 'SUPERIEUR' 0.D0     ;
      MASQABS ='MASQ' CRITABS 'SUPERIEUR' 0.D0     ;

*     'MESS' 'COMV:' ICONV (MAXI CRITNUM) (MAXI CRITREL) (MAXI CRITABS);
      SI (('MAXI' (MASQNUM * MASQREL * MASQABS)) '<' 0.5D0);
       'QUIT' CONVERG;
      FINS;

*     Compteur MAX atteint
      SI(ICONV 'EGA' NITMAX);
        ERRI = 'MAXI' ('PROG'
                    ('MAXI' CRITNUM) ('MAXI' CRITREL) ('MAXI' CRITABS));
       'MESS' '*******************************************************';
       'MESS' '      Comportement CHARTHER non converge';
       'MESS' '      Comportement : ICONV=' ICONV 'ERRI =' ERRI;
       'MESS'   ('MAXI' CRITNUM) ('MAXI' CRITREL) ('MAXI' CRITABS) ;
       'MESS' '*******************************************************';
        SI ('NON' BIGNORE);
         'ERRE' 21;
        FINS;
      FINS;
    FINS;
  FIN CONVERG;

  PRECED.'PROPORTIONS_PHASES'. NPA = 'REDU' Phas2 MODCOND;

************************************************************************
* Remplissage des indices de sortie : ADDI_MATRICE ET ADDI_SECOND
************************************************************************
TAA ='TABL';
* Actuellement aucun second membre ni aucune rigidite additionnelle

*  Remise en place de la parallélisation comme au depart
  'OPTI' 'PARA' PARA0 ;
'FINP' TAA;
************************************************************************
* Fin de la procedure CHARTHER
************************************************************************

************************************************************************
* Debut de la procedure PARATHER
************************************************************************
'DEBP' PARATHER  PRECED*'TABLE' TPS1*'FLOTTANT';
************************************************************************
* PROCEDURE PARATHER :
*   DESCRIPTION :
*   -------------
*   Cette procedure permet de modifier le materiaux avant l'appel a
*   VARI 'NUAG' dans TRANSNON.
*     - Les composantes K, C et RHO sont une moyenne arithméthique
*       pondérée par la fraction de phase des Ki, Ci et RHOi
*   L'indice ETAB.'MAT_TOT' est remplacé
*
*   ENTREES :
*   ---------
*   - PRECED : TABLE complete de PASAPAS
*   - TPS1   : Temps courant
*
*   SORTIES :
*   ---------
*   - Aucune
*
************************************************************************
   ETAB    = PRECED.'WTABLE';
   MOD1    = ETAB.'MOD_TOT' ;
   T1      = ETAB.'THER_COURANT' ;
   T1      ='CHAN' 'CHAM' T1 MOD1 'RIGIDITE' 'CARACTERISTIQUES';

   MAT_ORIG= ETAB.'MAT_TOT';
   COMPORIG='EXTR' MAT_ORIG 'COMP';
   NBCOMP  ='DIME' COMPORIG;

   MAT0 ='VIDE' 'MCHAML';
   REPE SURCOM NBCOMP;
     II     = &SURCOM;
     LACOMP ='EXTR' COMPORIG II;
     SI(('NEG' LACOMP 'K'  ) 'ET'
        ('NEG' LACOMP 'RHO') 'ET'
        ('NEG' LACOMP 'C'  ));
        MAT0 = MAT0 'ET' ('EXCO' LACOMP MAT_ORIG);
     FINS;
   FIN SURCOM;

*  Instanciation du matériaux de chaque phase
   MATPHAS ='REDU'  MOD1 MATPHAS ;
   MAT2    ='VARI' 'NUAG' MOD1 MATPHAS T1 'RIGIDITE';

*  On fait ensuite la moyenne qui nous interesse (Ponderation par la
*  proportion de phase)
   NPA ='DIME' PRECED.'TEMPS';
   SI ('EXIS' PRECED.'PROPORTIONS_PHASES'   NPA )   ;
     MCHPHA = PRECED.'PROPORTIONS_PHASES' . NPA     ;
   SINO ;
     MCHPHA = PRECED.'PROPORTIONS_PHASES' .(NPA - 1);
     PRECED.'PROPORTIONS_PHASES' . NPA = MCHPHA     ;
   FINS ;

   MCHPHA ='CHAN' 'RIGIDITE' MCHPHA MOD1           ;
   MCHPHA ='CHAN' 'TYPE' MCHPHA 'CARACTERISTIQUES' ;
   MOPROP1='EXTR'  MCHPHA 'COMP'                   ;
   MOMAT2 ='EXTR'  MAT2   'COMP'                   ;

   MCHK ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;
   MCHR ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;
   MCHC ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;

* Boucle sur les phases
   NBPHA  = 'DIME' MOPROP1;
   REPE SURP NBPHA;
     IP = &SURP ;
     PROPIP ='EXCO' MCHPHA ('EXTR' MOPROP1 IP) 'SCAL' ;
*    Moyenne sur les K
     IB = IP;
     MCHK = (PROPIP * ('EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL')) + MCHK ;

*    Moyenne sur les RHO
     IB = IB + NBPHA;
     MCHR = (PROPIP * ('EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL')) + MCHR ;

*    Moyenne sur les CP
     IB = IB + NBPHA;
     MCHC = (PROPIP * ('EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL')) + MCHC ;
   FIN SURP;

   MCHK ='CHAN' 'COMP' 'K'   MCHK ;
   MCHR ='CHAN' 'COMP' 'RHO' MCHR ;
   MCHC ='CHAN' 'COMP' 'C'   MCHC ;

*  REMPLACEMENT DU MATERIAUX
   ETAB.'MAT_TOT' = MCHK 'ET' MCHR 'ET' MCHC 'ET' MAT0 ;

'FINP';
************************************************************************
* Fin de la procedure PARATHER
************************************************************************

*MAILLAGE
h0 = 100.  ; COMM 'hauteur du cylindre' ;
r0 = 12.5  ; COMM 'rayon du cylindre'   ;
dens0 = 1. ; COMM 'densité maillage'    ;

axes = @repere (0. 0.) ('PROG' 25. 25.) 'ROUG' ;
P1 = POIN 0. 0. ;
P2 = POIN r0 0. ;
P3 = POIN r0 h0 ;
P4 = POIN 0. h0 ;

L1 = DROI P1 P2 'DINI' dens0 'DFIN' dens0 ;
L2 = DROI P2 P3 'DINI' dens0 'DFIN' dens0 ;
L3 = DROI P3 P4 'DINI' dens0 'DFIN' dens0 ;
L4 = DROI P4 P1 'DINI' dens0 'DFIN' dens0 ;

MAILT = (DALL L1 L2 L3 L4) COUL 'BLEU' ;
ELIM MAILT 1.e-3 ;
CONTT = CONT MAILT ;
PTBAS = CONTT POIN 'DROIT' P1 P2 1.e-3 ;
SURFBAS = CONTT ELEM 'APPUYE' 'STRICTEMENT' PTBAS ;

*MODELE
MODTHE = MODE MAILT   'THERMIQUE' 'CONDUCTION' 'CONS' 'MAT1';
MODCON = MODE SURFBAS 'THERMIQUE' 'CONVECTION';
MODTOT = MODTHE 'ET' MODCON;

************************************************************************
* Definition de la metallurgie : DEBUT
************************************************************************
*  1- Métal de base 100% initialement
*  2- Martensite
*  3- Bainite
*  4- Ferrite
*  5- Austénite

* Evolution des proprietes thermiques pour les phases pures
MCHK   = VIDE 'MCHAML';
MCHRHO = VIDE 'MCHAML';
MCHC   = VIDE 'MCHAML';
CTYP   = MOT  'CARACTERISTIQUES';
********************
* 1- Métal de base *
********************
IPAHS  = 1;
CPHAS  = CHAI 'K' IPAHS;
LTKP1  = PROG 0 55 113 162 191 243 293 392
              457 507 605 655 756 795 840 856
              900 950 1000 1101 1202 1303 1400 1500
              1502 1600   ;
LKP1   = PROG 0.0376 0.0389 0.0401 0.0407 0.0407 0.0404 0.0398 0.038
              0.0364 0.0352 0.0329 0.0317 0.0291 0.0273 0.0252 0.0248
              0.0253 0.0261 0.027 0.0283 0.0299 0.0314 0.0328 0.0343
              0.05 0.05;
EVKP1  = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK   = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS  = CHAI 'R' IPAHS;
LTRHOP1= PROG 0 20 100 200 300 400
              500 600 700 800 900 1440
              2500 ;
LRHOP1 = PROG 7852.D-9 7852.D-9 7815.D-9 7778.D-9 7741.D-9 7705.D-9
              7668.D-9 7633.D-9 7595.D-9 7562.D-9 7527.D-9 7345.D-9
              7011.D-9 ;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS  = CHAI 'C' IPAHS;
LTCP1  = PROG 21 50 150 200 250 300 350 450 550
              650 700 750 850 1000 1200 1300 1400 1440
              1465 1490 1540 1590 1615 1640 2500 ;
LCP1   = PROG 460.5 485.6 519.1 531.6 556.7 573.5 598.6 661.4 749.3
              845.6 1431.6 950.2 900 845.6 800 815 840 850
              1210 1840 2470 1840 1210 840 707 ;
EVCP1  = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC         = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

*****************
* 2- Martensite *
*****************
IPAHS   = 2;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

**************
* 3- Bainite *
**************
IPAHS  = 3;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

**************
* 4- Ferrite *
**************
IPAHS  = 4;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

****************
* 5- Austénite *
****************
IPAHS  = 5;
CPHAS  = CHAI 'K' IPAHS;
LTKP5  = PROG 0    20 100 200 400 600 800 856
              900 1000 1200 1500 1502 1600   ;
LKP5   = PROG 0.0121 0.0124 0.0136 0.015 0.018 0.021 0.0239 0.0248
              0.0254 0.0269 0.0298 0.0343 0.05 0.05;
EVKP5  = EVOL 'MANU' 'T' LTKP5 CPHAS LKP5;
MCHK         = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP5 'TYPE' CTYP) ;

CPHAS  = CHAI 'R' IPAHS;
LTRHOP5= PROG 0 20 100 200 300 400
              500 600 700 800 900 1440
              2500 ;
LRHOP5 = PROG 7852.D-9 7841.D-9 7794.D-9 7736.D-9 7680.D-9 7624.D-9
              7569.D-9 7514.D-9 7458.D-9 7408.D-9 7356.D-9 7087.D-9
              6613.D-9 ;
EVRHOP5 = EVOL 'MANU' 'T' LTRHOP5 CPHAS LRHOP5;
MCHRHO       = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP5 'TYPE' CTYP);


CPHAS  = CHAI 'C' IPAHS;
LTCP5  = PROG 21   50   150  200  250  300  350  475  575  675  800
              950  1000 1100 1200 1300 1400 1440 1465 1490 1540 1590
              1615 1640 2500 ;
LCP5   = PROG 460.5 485.6 519.1 531.6 556.7 573.5 598.6 652 671 685 715
              742 750 774 795 815 840 850 1210 1840 2470 1840
              1210 840 707 ;
EVCP5  = EVOL 'MANU' 'T' LTCP5 CPHAS LCP5;
MCHC         = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP5 'TYPE' CTYP) ;




* Parametres pour le changement de phase
CTYP   = MOT  'SCALAIRE';
TPHASE = TABL;

* Liste des transformations existantes dans un LISTENTIE
TPHASE . 'REACTIONS' = LECT 1 5 2 5 3 5 4 5 5 2 5 3 5 4;
* Lire : 1 vers 5, 2 vers 5, 3 vers 5, 4 vers 5
*        5 vers 2, 5 vers 3, 5 vers 4

TPHASE . 'TYPE' = MOTS 'LEBL' 'LEBL' 'LEBL' 'LEBL' 'KOMA' 'LEBL' 'LEBL';

*-----------------------------------------------------------------------
IDREAC = 1; COMM '1 vers 5';
* Leblond
EVOPEQ = EVOL 'MANU' 'T'   (PROG 716. 802. )
                     'PEQ' (PROG 0.   1.   );
EVOTAU = EVOL 'MANU' 'T'   (PROG 716. 802. )
                     'TAU' (PROG 12.  0.5  );
EVOF   = EVOL 'MANU' 'TPOI'(PROG -1.D-6 0. )
                     'F'   (PROG  0.    1. );

TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 2; COMM '2 vers 5';
* Leblond
TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 3; COMM '3 vers 5';
* Leblond
TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 4; COMM '4 vers 5';
* Leblond
TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 5; COMM '5 vers 2';
* Koistinen-Marburger
TPHASE . IDREAC = MANU 'CHML' MODTHE 'MS' 380. 'KM' 0.0247
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 6; COMM '5 vers 3';
* Leblond
EVOPEQ = EVOL 'MANU' 'T'   (PROG 375 380 405 600 )
                     'PEQ' (PROG 0.  1.  1.  0.  );
EVOTAU = EVOL 'MANU' 'T'   (PROG 375  380)
                     'TAU' (PROG 1.D6 20 );
EVOF   = EVOL 'MANU' 'TPOI'(PROG -100. -80.  -60.  -50.  -40.  -30.
                                 -25.  -20.  -18.  -15.  -12.  -10.
                                 -9.   -5.   -1.    -0.05    0.D0
                                  1.D-6)
                     'F'   (PROG  0.005 1.573 2.857 3.417 3.982 4.583
                                  4.833 5.26  5.472 6.033 7.675 11.4
                                  18.45 17.1 0.328  0.00238 0.00238
                                  0.D0 );
TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------
IDREAC = 7; COMM '5 vers 4';
* Leblond
EVOPEQ = EVOL 'MANU' 'T'   (PROG 625 630 730 735 )
                     'PEQ' (PROG 0   1   1   0   );
EVOTAU = EVOL 'MANU' 'T'   (PROG 625  630 )
                     'TAU' (PROG 1.D6 5   );
EVOF   = EVOL 'MANU' 'TPOI'(PROG -8.5  -6.2 -4.7  0.  1.D-6)
                     'F'   (PROG  0.001 0.13 1.5  1.5 0.   );
TPHASE . IDREAC = MANU 'CHML' MODTHE 'PEQ' EVOPEQ 'TAU' EVOTAU 'F' EVOF
                       'TYPE' CTYP 'NOEUD';
*-----------------------------------------------------------------------

* Verification des donnees rentrees
NBREAC =  (DIME TPHASE . 'REACTIONS') / 2;
NBTYPE =  (DIME TPHASE . 'TYPE'     )    ;
NBINDI =  (DIME TPHASE)                  ;
SI ('NEG' NBREAC NBTYPE);
  OPTI ECHO 0;
  CHAI1 = CHAI 'Le Nombre de reaction (' NBREAC ') ';
  CHAI2 = CHAI 'ne correspond pas avec ';
  CHAI3 = CHAI 'la taille de la liste des noms (' NBTYPE ').';
  MESS CHAI1;
  MESS CHAI2;
  MESS CHAI3;
  OPTI ECHO 1;
  ERRE 21;
FINS;

SI ('NEG'  NBINDI (NBREAC + 2));
  OPTI ECHO 0;
  CHAI1 = CHAI 'Le Nombre de reaction (' NBREAC ') ';
  CHAI2 = CHAI 'ne correspond pas avec ';
  CHAI3 = CHAI 'le nombre de reactions donnees (' (NBINDI - 2) ').';
  MESS CHAI1;
  MESS CHAI2;
  MESS CHAI3;
  OPTI ECHO 1;
  ERRE 21;
FINS;
************************************************************************
* Definition de la metallurgie : FIN
************************************************************************


************************************************************************
* Phase 1 et 2 :
*   Rampe de temperature jusqu'à 900°C d'une durée de 20min
*   Palier isotherme à           900°C d'une durée de 30min
************************************************************************


*TEMPERATURE INITIALE
CHPTINI = MANU 'CHPO' MAILT 1 'T' TMIN 'NATU' 'DIFFUS' ;

* Creation du MCHAML de proportion de phase INITIALE
CTYP    = MOT  'SCALAIRE';
MCHPHA  = MANU 'CHML' MODTHE 'MB  ' 0.1 'MART' 0.2 'BAIN' 0.3
                             'FERR' 0.4 'AUST' 0.0
               'TYPE' CTYP 'NOEUD' ;

*MATERIAUX
MATPHAS = MCHK 'ET' MCHRHO 'ET' MCHC  ;
MATCONV ='MATE' MODCON 'H' 0.D0 ;
MATCOND ='MATE' MODTHE 'K' 0.D0 'RHO' 0.D0 'C' 0.D0;
MATTOT  = MATCOND 'ET' MATCONV;

************************************************************************
* Fabrication du diagramme TRC (Pour vérification de la mise en donnee)
************************************************************************
* Calcul automatique de la liste des vitesses de refroidissement
  VIT1  = 250.      ;
  VIT2  = 1.        ;
  NBVI  = 50        ;
  VLOG1 = 'LOG' VIT1;
  VLOG2 = 'LOG' VIT2;

  PASLOG = (VLOG2 - VLOG1) / (NBVI - 1);
  LRELOG ='PROG' VLOG1 'PAS' PASLOG VLOG2;
  LISTVIT='EXP' LRELOG;

* Liste manuelle des vitesses de refroidissement
  LISTVIT = PROG 250. 50. 10. 7. 4. ;
************************************************************************

*CLs
*TEMPERATURE IMPOSE EN BAS
CLTOT   = BLOQ 'T' MAILT ;
TIMPO   = DEPI 1.  CLTOT ;

* CHARGEMENT
LTPSCHA = PROG 0.   1200. 3000. ;
LTCHA   = PROG TMIN TMAX  TMAX  ;
EVT     = EVOL 'MANU' LTPSCHA LTCHA;
* DESS EVT;
CHARTOT = CHAR 'TIMP' TIMPO EVT ;

*TEMPS_CALCULES
LTCAL1   = PROG 0. 'PAS' 100. 945. 'PAS' 2. 1100.
                   'PAS' 5. 1200.  'PAS' 50. 3000.  ;

*PASAPAS
TPASAP                          ='TABL'         ;
TPASAP.'MODELE'                 = MODTOT        ;
TPASAP.'CARACTERISTIQUES'       = MATTOT        ;
TPASAP.'BLOCAGES_THERMIQUES'    = CLTOT         ;
TPASAP.'CHARGEMENT'             = CHARTOT       ;
TPASAP.'TEMPS_CALCULES'         = LTCAL1        ;

TPASAP.'TEMPS'                  ='TABL'         ;
TPASAP.'TEMPERATURES'           ='TABL'         ;
TPASAP.'PROPORTIONS_PHASES'     ='TABL'         ;
TPASAP.'TEMPS'              . 0 ='EXTR' LTCAL1 1;
TPASAP.'TEMPERATURES'       . 0 = CHPTINI       ;
TPASAP.'PROPORTIONS_PHASES' . 0 = MCHPHA        ;
TPASAP.'PROCESSEURS'            ='MOT' 'MONO_PROCESSEUR';

* PROCEDURES Utilisateurs
TPASAP.'PROCEDURE_PARATHER'    = VRAI          ;
TPASAP.'PROCEDURE_CHARTHER'    = VRAI          ;

* Option pour l'integration du COMPORTEMENT des PHASES
TPASAP.'PRECISION'              = 1.D-8     ; COMM 'Critere Relatif';
TPASAP.'PTOL'                   = 1.D-4     ; COMM 'Critere Absolu ';
TPASAP.'NITERPHASE_MAX'         = 10        ; COMM 'Voir CHARTHER'  ;
TPASAP.'IGNORE_NON_CONV'        = VRAI      ; COMM 'Voir CHARTHER'  ;

PASAPAS TPASAP ;

************************************************************************
* Phase 3 :
*   Refroidissement par convection sur un des bords
************************************************************************
* MATERIAU
MATCONV  ='MATE' MODCON  'H' HCON ;
MATTOT   = MATCOND 'ET' MATCONV    ;

* CHARGEMENT
LTPSCHA = PROG 0.D0 1.D6 ;
LTCHA   = PROG 1.D0 1.D0 ;
EVTECO  = EVOL 'MANU' LTPSCHA LTCHA;
* DESS EVTECO;
CHPTECO ='MANU' 'CHPO' SURFBAS 1 'T' 20. ;
CHARTOT = CHAR 'TECO' CHPTECO EVTECO ;

*TEMPS_CALCULES
MTPS1    ='MAXI' LTCAL1 ;
MTPS2    = MTPS1 + 600. ;
LTCAL2   ='PROG' 3000. 'PAS' 1.  3040. 'PAS' 2 3160.   'PAS' 4.  3370.
                       'PAS' 20. MTPS2 ;

* Remplacement des indices dans la TABLE TPASAP
TPASAP.'BLOCAGES_THERMIQUES'='VIDE' 'RIGIDITE';
TTPS  = TPASAP.'TEMPS';
ID_IND=('DIME' TTPS) - 1;
MCHPHA=TPASAP.'PROPORTIONS_PHASES' . ID_IND ;
ID2 = ID_IND + 1 ;

TPASAP.'CARACTERISTIQUES'         = MATTOT ;
TPASAP.'CHARGEMENT'               = CHARTOT;
TPASAP.'TEMPS_CALCULES'           = LTCAL2 ;
TPASAP.'PROPORTIONS_PHASES' . ID2 = MCHPHA ;

* PROCEDURES Utilisateurs
TPASAP.'PROCEDURE_PARATHER'    = VRAI     ;
TPASAP.'PROCEDURE_CHARTHER'    = VRAI     ;

* Option pour l'integration du COMPORTEMENT des PHASES
TPASAP.'PRECISION'              = 1.D-8    ; COMM 'Critere Relatif';
TPASAP.'PTOL'                   = 1.D-4    ; COMM 'Critere Absolu ';
TPASAP.'NITERPHASE_MAX'         = 10       ; COMM 'Voir CHARTHER'  ;
TPASAP.'IGNORE_NON_CONV'        = VRAI     ; COMM 'Voir CHARTHER'  ;

PASAPAS TPASAP ;


************************************************************************
* POST-TRAITEMENT des résultats
************************************************************************
fic_sauv = 'CHAI' 'JOMINY.sauv' ;
OPTI SAUV fic_sauv ;
SAUV ;

OPTI TRAC 'PSC' ;
*OPTI ISOV 'SULI' ;


* Initialisations pour le POST-TRAITEMENT
LISCOM ='EXTR' MCHPHA 'COMP' ;
NBCOMP ='DIME' LISCOM        ;
TABPHA ='TABL';
REPE SURPHA NBCOMP;
  IPH = &SURPHA;
  TABPHA . IPH ='VIDE' 'LISTREEL' ;
FIN SURPHA;
LTPS ='VIDE' 'LISTREEL';

REPE BLOC0 ('DIME' TPASAP.'TEMPS')         ;
  I      = &BLOC0  - 1                     ;
  CHT    = TPASAP.'TEMPERATURES'      . I  ;
  CHPHA  = TPASAP.'PROPORTIONS_PHASES'. I  ;
  LTPS   = LTPS 'ET' (TPASAP.'TEMPS'  . I) ;

* Extraction pour chaque phase de la valeur courante
  REPE SURPHA NBCOMP;
    IPH = &SURPHA;
    NOMP='EXTR' LISCOM IPH;
*    Pi  ='EXTR' CHPHA NOMP 1 1288 1 ;
*    Pi  ='EXTR' CHPHA NOMP 1 378  1 ;
    Pi  ='EXTR' CHPHA NOMP 1 1   1 ;
    TABPHA . IPH = TABPHA . IPH 'ET' Pi;
  FIN SURPHA;
FIN BLOC0 ;

*Reconstitution des EVOL
EVTOTa           ='VIDE' 'EVOLUTIO';
TDESS1           ='TABL' ;
TDESS1 . 'TITRE' ='TABL' ;
EVOPHAS          ='VIDE' 'EVOLUTIO';
CPPHAS = 'CHAN' 'CHPO' CHPHA MODTOT;
REPE SURPHA NBCOMP;
  IPH          = &SURPHA;
  NOMP         ='EXTR' LISCOM IPH;
  ICOUL        = @MOD  IPH NBCOUL;
  COUL1        ='EXTR' LESCOUL ICOUL;
  Lpi          = TABPHA. IPH ;
  TABPHA . IPH ='EVOL' COUL1 'MANU' 'TEMP' LTPS NOMP Lpi;
  EVTOTa       = EVTOTa 'ET' TABPHA . IPH ;
  IMARQ        = @MOD   IPH NBMARQ;
  LAMAR        ='EXTR'  IMARQ LESMARQ;
  TDESS1 . 'TITRE' . IPH = NOMP;
*  TDESS1 . IPH ='CHAI' 'MARQ ' LAMAR ' REGU';
  EVOPHAS      = EVOPHAS 'ET' ('EVOL' COUL1 'CHPO' CPPHAS NOMP L2);
FIN SURPHA;

EVTOTa = 'CHAN' 'NOMORD' EVTOTa 'PROP';
'DESS' EVTOTa  'LEGE' TDESS1 'TITR' ('CHAI' 'Evolution des Props en fonction du temps' ) ;

*Temperature au cours du temps... pour tests !
TURE = 'VIDE' 'LISTREEL' ;
REPE BLOC0 ('DIME' TPASAP.'TEMPS') ;
  I      = &BLOC0  - 1 ;
  CHTURE  = TPASAP.'TEMPERATURES'. I  ;

*  Pi     ='EXCO' CHTURE T 'T';
*  PiEvol = 'EXTR' Pi T 1 1 1;
  PiEvol = 'EXTR' CHTURE 'T' P1;
  TURE = TURE 'ET' PiEvol ;
FIN BLOC0 ;
EVOTURE ='EVOL' 'ROUG' 'MANU' 'TEMP' LTPS 'TURE' TURE;
'DESS' EVOTURE 'TITR' ('CHAI' 'Evolution de la T°C en fonction du temps' ) ;
*Temperature au cours du temps... pour tests ! FIN

EVOPHAS = 'CHAN' 'NOMORD' EVOPHAS 'PROP';
'DESS' EVOPHAS 'LEGE' TDESS1 'TITR' ('CHAI' 'Props a Temps final' ) ;

 fic_CSV = 'CHAI' 'JOMINY.csv' ;
'OPTI' 'SORT'  fic_CSV    ;
'SORT' 'EXCE'  EVOPHAS    ;


*On trace les champs de temperatures... pour tests

TURE  = TPASAP.'TEMPERATURES'. ( ('DIME' TPASAP.'TEMPERATURES') - 1 )  ;
TURE2 = 'CHAN' 'CHPO' TURE MODTOT;
EVOTURE      = 'EVOL' 'ROUGE' 'CHPO' TURE2 'T' L4;
'DESS' EVOTURE 'TITR' ('CHAI' 'T°C a Temps final' ) ;


*REPE B1 ('DIME' TPASAP.'TEMPS') ;
*  TPS1 = TPASAP . 'TEMPS' . (&B1 - 1) ;
*  TE1 = TPASAP . 'TEMPERATURES' . (&B1 - 1) ;
*  TRAC TE1 MAILT (PROG TMIN PAS ((TMAX - TMIN) / 20.) TMAX) 'TITR' (CHAI 'Temperature au temps' TPS1) ;
*FIN B1 ;


REPE BLOC0 ('DIME' TPASAP.'TEMPS')         ;

  I      = &BLOC0  - 1 ;

* Temperature a chaque instant sur L4 :
  TURE  = TPASAP.'TEMPERATURES'. I  ;
  TURE2 = 'CHAN' 'CHPO' TURE MODTOT;
  EVOTURE      = 'EVOL' 'ROUGE' 'CHPO' TURE2 'T' L4;
  'DESS' EVOTURE 'TITR' ('CHAI' 'Temps:' (TPASAP.'TEMPS'  . I) ) ;

* Props de Phases sur L2 a chaques instant :
  CHT    = TPASAP.'TEMPERATURES'      . I  ;
  CHPHA  = TPASAP.'PROPORTIONS_PHASES'. I  ;
  LTPS   = LTPS 'ET' (TPASAP.'TEMPS'  . I) ;
  EVTOTa           ='VIDE' 'EVOLUTIO';
  TDESS1           ='TABL' ;
  TDESS1 . 'TITRE' ='TABL' ;
  EVOPHAS          ='VIDE' 'EVOLUTIO';
  CPPHAS = 'CHAN' 'CHPO' CHPHA MODTOT;
  REPE SURPHA NBCOMP;
    IPH          = &SURPHA;
    NOMP         ='EXTR' LISCOM IPH;
    ICOUL        = @MOD  IPH NBCOUL;
    COUL1        ='EXTR' LESCOUL ICOUL;
    TDESS1 . 'TITRE' . IPH = NOMP;
    EVOPHAS      = EVOPHAS 'ET' ('EVOL' COUL1 'CHPO' CPPHAS NOMP L2);
  FIN SURPHA;
  EVOPHAS = 'CHAN' 'NOMORD' EVOPHAS 'PROP';
  'DESS' EVOPHAS 'LEGE' TDESS1 'TITR' ('CHAI' 'Temps:' (TPASAP.'TEMPS'  . I) );

FIN BLOC0 ;

FIN ;


