* fichier :  prim_errord.dgibi
************************************************************************
* Section : Fluides Darcy
************************************************************************
************************************************************************
**** APPROCHE VF "Cell-Centred Formulation" pour la  solution des   ****
**** Equations d'Euler pour un gaz parfait                          ****
**** OPERATEUR  PRIM                                                ****
**** Test erreur ordre composantes vitesse-fractions massiques      ****
****                                                                ****
**** A. BECCANTINI DRN/DMT/SEMT/TTMF  FEVRIER 2000                  ****
************************************************************************
*
'OPTION'  'DIME' 2 ;
'OPTION'  'ELEM' QUA4 ;
'OPTION'  'ECHO'  0 ;
'OPTION'  'TRAC' 'X';
*
*
**** GRAPH
*
*
GRAPH = FAUX ;
* GRAPH = VRAI ;
*


*) Degre de polynome = 4

***********************
**** LA TABLE PGAZ ****
***********************

PGAZ = 'TABLE' ;

*
**** Ordre des polynoms
*

PGAZ . 'NORD' = 4 ;

*
**** Especes qui sont dans les equations d'Euler
*

PGAZ . 'ESPEULE' = 'MOTS' 'H2  ' 'N2  ' ;

*
**** Espece qui n'y est pas
*


PGAZ . 'ESPNEULE' =  'O2  ' ;

*

PGAZ .  'H2  ' = 'TABLE'  ;
PGAZ .  'O2  ' = 'TABLE'  ;
PGAZ .  'N2  ' = 'TABLE'  ;

*
**** R (J/Kg/K)
*

PGAZ .  'H2  ' . 'R' = 4130.0 ;
PGAZ .  'O2  ' . 'R' = 259.8 ;
PGAZ .  'N2  ' . 'R' = 296.8 ;


*
**** Regressions polynomials
*

PGAZ .  'H2  ' . 'A' = 'PROG'  9834.91866 0.54273926 0.000862203836
                               -2.37281455E-07 1.84701105E-11 ;
PGAZ .  'O2  ' . 'A' = 'PROG' 575.012333  0.350522002 -0.000128294865
                              2.33636971E-08 -1.53304905E-12;
PGAZ .  'N2  ' . 'A' = 'PROG' 652.940766 0.288239099 -7.80442298E-05
                              8.78233606E-09 -3.05514485E-13 ;

*
**** "Enthalpies" (ou energies) de formations a OK (J/Kg)
*

PGAZ .  'H2  ' . 'H0K' = -4.195D6 ;
PGAZ .  'O2  ' . 'H0K' = -2.634D5 ;
PGAZ .  'N2  ' . 'H0K' = -2.953D5 ;

*
*** Fin PGAZ
*


***************************
***** DOMAINE SPATIAL  ****
***************************


A1 = 0.0D0 0.0D0;
A2 = 2.0D0 0.0D0;
A3 = 3.0D0 0.0D0;
A4 = 4.0D0 1.0D0;

L12 = A1 'DROIT' 21 A2;
L23 = A2 'DROIT' 32 A3;
L34 = A3 'DROIT' 43 A4;
L41 = A4 'DROIT' 51 A1;

LDOM1 = L12 'ET' L23 'ET' L34 'ET' L41 ;
DOM1  = 'SURFACE' LDOM1 'PLANE';

'SI' GRAPH ;
   'TRACER' DOM1 'TITRE' 'Domaine' ;
'FINSI' ;

$DOM1 = 'DOMA' DOM1 ;

MAIL =   $DOM1 . 'CENTRE' ;

*
**** Energie, densite, temperature, vitesse, fractions massiques
*

RN   = 'BRUI' 'BLAN' 'UNIF' 1.0  0.1  MAIL ;
TN   = 'BRUI' 'BLAN' 'UNIF' 5000  1000  MAIL ;

YN2  = 'BRUI' 'BLAN' 'UNIF' 0.3  0.1  MAIL ;
YO2  = 'BRUI' 'BLAN' 'UNIF' 0.2  0.1  MAIL ;
RYN2  = 'NOMC' (RN * YN2) 'N2  ' 'NATU' 'DISCRET' ;
RYH2  = 'NOMC' (RN * YN2) 'H2  ' 'NATU' 'DISCRET' ;
RYN =  RYH2 'ET' RYN2 ;

VNX = 'BRUI' 'BLAN' 'UNIF' 1.0  0.1  MAIL ;
VNY = 'BRUI' 'BLAN' 'UNIF' 1.0  0.1  MAIL ;

VN = ('NOMC' VNX 'UX' 'NATU' 'DISCRET' ) 'ET'
     ('NOMC' VNY 'UY' 'NATU' 'DISCRET' ) ;

GN = ('NOMC' (VNX * RN) 'UX' 'NATU' 'DISCRET' ) 'ET'
     ('NOMC' (VNY * RN) 'UY' 'NATU' 'DISCRET' ) ;

* Energie de l'ordre de (0.5 * 1^2) '+' (*10^6)

RET = 'BRUI' 'BLAN' 'UNIF' 1.0D6  1.0D5  MAIL  ;

*
**** PRIM
*

LCOMRYN = 'EXTRAIRE' RYN 'COMP' ;
LCOMRVN = 'EXTRAIRE' GN 'COMP' ;

VIT PRES TEMP YN GAMN
       = 'PRIM' 'PERFTEMP' PGAZ RN GN RET RYN ;

*
**** On inverse l'ordre des composantes des fractions massiques
*

RYN =  RYN2 'ET' RYH2 ;

VIT1 PRES1 TEMP1 YN1 GAMN1
       = 'PRIM' 'PERFTEMP' PGAZ RN GN RET RYN ;


ERRV = ('MAXIMUM' (VIT1 '-' VIT) 'ABS')
        '/' ('MAXIMUM' VN 'ABS') ;

ERRP  = 'MAXIMUM' ((PRES1 '-' PRES)  '/'  PRES) 'ABS';

ERRT  = 'MAXIMUM' ((TEMP1 '-' TEMP) '/' TEMP) 'ABS';

ERRG = 'MAXIMUM' ((GAMN1 '-' GAMN) '/' GAMN) 'ABS';

ERRY = ('MAXIMUM' (YN1 '-' YN) 'ABS')
        '/' ('MAXIMUM' YN 'ABS') ;

ERRO = 'MAXIMUM' ('PROG' ERRV ERRP ERRT ERRG  ERRY  );

'SI' (ERRO > 1.0D-16) ;
        'MESSAGE' ('CHAINE' 'Erreur maximum  ' ERRO) ;
     'ERREUR'  5;
'FINSI' ;


* On inverse l'ordre des composante de vitesse

GN = ('NOMC' (VNY * RN) 'UY' 'NATU' 'DISCRET' ) 'ET'
     ('NOMC' (VNX * RN) 'UX' 'NATU' 'DISCRET' ) ;

VIT1 PRES1 TEMP1 YN1 GAMN1
       = 'PRIM' 'PERFTEMP' PGAZ RN GN RET RYN ;



ERRV = ('MAXIMUM' (VIT1 '-' VIT) 'ABS')
        '/' ('MAXIMUM' VN 'ABS') ;

ERRP  = 'MAXIMUM' ((PRES1 '-' PRES)  '/'  PRES) 'ABS';

ERRT  = 'MAXIMUM' ((TEMP1 '-' TEMP) '/' TEMP) 'ABS';

ERRG = 'MAXIMUM' ((GAMN1 '-' GAMN) '/' GAMN) 'ABS';

ERRY = ('MAXIMUM' (YN1 '-' YN) 'ABS')
        '/' ('MAXIMUM' YN 'ABS') ;

ERRO = 'MAXIMUM' ('PROG' ERRV ERRP ERRT ERRG  ERRY  );


'SI' (ERRO > 1.0D-16) ;
        'MESSAGE' ('CHAINE' 'Erreur maximum  ' ERRO) ;
     'ERREUR'  5;
'FINSI' ;

'FIN' ;




