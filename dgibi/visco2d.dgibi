* fichier :  visco2d.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*    Test visco2d.dgibi
*     ------------
*
*-----------------------------------
*   Exemple de simulation d'essai de fluage sur une eprouvette
*
*   axisymetrique entaillee en 16MND5.
*
*   Utilisation de la loi de comportement VISCODD: loi viscoplastique
*
*   a ecrouissage isotrope avec deux variables d endommagement: Dd et Dc
*
*   Dd: endommagement ductile isotrope
*
*   Dc: endommagement de fluage anisotrpe
*-----------------------------------
OPTI DIME 2 ELEM QUA8 MODE AXIS;
*
complet = faux ;
opti trac psc;
*-------------- Parametres geometriques pour AE10------------------------
DIAM0 = 6. ;
RAY0 = DIAM0 / 2. ;
DIAMm = 10. ;
RAYm = DIAMm / 2. ;
RAYe = 6. ;
LE1 = 10. ;
*-------------- Definition de la geometrie AE10--------------------------
PT0 =  0.  0.  ;
PT1 =  RAY0   0.  ;
X2 = RAY0 + RAYe ;
PT2 =  X2  0. ;
*
Y3 = 0.5;
TMP1 = RAYe ** (2.) ;
TMP2 = Y3 ** (2.) ;
TMP3 = TMP1 - TMP2 ;
TMP3 = TMP3**(0.5) ;
TMP4 = Ray0 + RAYe - TMP3 ;
PT3 = TMP4  Y3 ;
PT8 = 0. Y3 ;
*
X4 = (RAY0 + RAYm)/2. ;
TMP1 = RAYe ** (2.) ;
TMP2 = (RAYm - RAY0)/2.0 ;
TMP3 = RAYe - TMP2 ;
TMP4 = TMP3**(2.) ;
TMP5 = TMP1 - TMP4 ;
Y4 = (TMP5)**(0.5) ;
PT4 = X4  Y4 ;
*
TMP1 = RAYe ** (2.) ;
TMP2 = TMP1 - ((RAYe + RAY0 - RAYm)**(2.)) ;
Y5 = (TMP2)**(0.5) ;
PT5 =  RAYm  Y5  ;
*
PT6 = RAYm LE1 ;
PT7 = 0.0 LE1 ;
*
L1 = DROIT 20 PT0 PT1 ;
N1 = NBEL L1 ;
C2 = CERC 3 PT3 PT2 PT1 ;
L10 = DROIT N1 PT3 PT8 ;
L8 = DROIT 3 PT8 PT0 ;
C3 = CERC PT4 PT2 PT3 'DINI' 0.4 'DFIN' 0.15;
N3 = NBEL C3 ;
C4 = CERC 8 PT5 PT2 PT4 ;
L5 = DROIT N1 PT5 PT6 ;
L6 = DROIT 8 PT6 PT7 ;
L7 = DROIT  ((-1)*N3) PT7 PT8 'DINI' 2. 'DFIN' 0.25;
L9 = DROIT N1 PT4 PT7 ;
*
SURF1 = DALL L1 C2 L10 L8 'PLANE' ;
SURF2 = DALL L10 C3 L9 L7 'PLANE' ;
SURF3 = DALL L9 C4 L5 L6 'PLANE' ;
SURFT= SURF1 ET SURF2 ET SURF3 ;
*
NELT = NBEL SURFT;
NNOT = NBNO SURFT;
titre 'Maillage eprouvette AE10 avec ' NNOT 'noeuds'
et NELT 'elements' ;
TRAC SURFT;
*--------------------Definition du modele----------------------------------
*
MOD1 = MODE SURFT  MECANIQUE ELASTIQUE VISCOPLASTIQUE VISCODD Q8RI ;
* Remarque: Q8RI sont des elements sous integres. Pour ce genre de calculs en viscoplasticite
* il est courant d'utiliser des elements sous integres, sous peine de voir apparaitre des modes de
* deformations parasites.
*
*-------------------Caracteristiques du 16MND5 a 900°C-------------------------
Y0 = 21280. ;
NU0 = 0.3 ;
N0 = 3.5 ;
SIGY0 = 1.;
* mat A
KS0 = 587.  ;
* mat B
*KS0 = 645.7 ;
B0 = 3. ;
Q0 = 8.0 ;
* Parametres loi d'endommagement ductile mat A
*A0 en (Mpa,s)
sd1 = 0.2 ;
rd1 = 0.5 ;
pd1 = 0.18 ;
* Parametres loi d'endommagement de fluage mat B
*A20 en (Mpa,s)
sc1 = 34.05 ;
rc1 = 1.55  ;
pc1 = 0. ;
*
*
MAT0 = MATE MOD1 'YOUN' Y0 'NU' NU0
                  'N'  N0  'K'  KS0
                  'B'  B0  'RI'  Q0
                  'SIGY' SIGY0
                  'SD' sd1 'RD' rd1 'PD' pd1
                  'SC' sc1 'RC' rc1 'PC' pc1 ;
*
*-----------------------Conditions limites------------------------------------
CL1 = BLOQ 'UZ' L1 ;

*--------------------Cargement de fluage (effort constant)--------------------------
TEMPS1 = prog 0.  50. 100000.;
CON1 = PROG  0.  1.0  1.0;
SIG1 = EVOL MANU 'TEMPS' TEMPS1 'CONTR' CON1;
CHAR1 = PRES MASS MOD1 -14. L6 ;
CHARS1  = CHAR 'MECA'  CHAR1 SIG1 ;
DESS (SIG1 coul vert) xbor 0. 300. ;
*
*-------------Table de donnees pour le calcul PASAPAS----------------------------
*
TAB1 =  TABLE ;
TAB1.'PRECISION'  = 1.E-3 ;
TAB1.'MODELE' = MOD1;
TAB1.'CARACTERISTIQUES' = MAT0;
TAB1.'GRANDS_DEPLACEMENTS'  = VRAI ;
TAB1.'BLOCAGES_MECANIQUES'  = CL1 ;
TAB1.'CHARGEMENT'  = CHARS1 ;
TAB1.'PAS_AJUSTE' = FAUX ;
si complet ;
TAB1.'TEMPS_CALCULES' =  prog 0. PAS 10. 50. PAS 50. 3500.  ;
sinon ;
TAB1.'TEMPS_CALCULES' =  prog 0. PAS 10. 50.  ;
finsi ;
TAB1.'LAGRANGIEN'='MOT' 'REACTUALISE';
PASAPAS TAB1 ;
* -------------------------Sauvegarde des resultats-------------------------------
*opti sauv 'AE10dc.resu';
*sauv TAB1;
*sauv SURFT;
*opti sauv 'toto.dgibi';
*
si (non complet) ;
 NDIM = DIME TAB1.TEMPS ;
 SIGM1 = TAB1.CONTRAINTES.(NDIM-1) ;
 smaxrr = 41.052 ; sminrr = .76007 ;
 sma1 = maxi (exco sigm1 smzz) ; smi1 = mini (exco sigm1 smzz) ;
 err1 = abs ((sma1 - smaxrr)/smaxrr) ;
 err2 = abs ((smi1 - sminrr)/sminrr) ;
 si ((err1 < 1.e-3) et (err2 < 1.e-3)) ;
    erre 0 ;
 sinon ;
    erre 5 ;
 finsi ;
 fin ;
finsi ;

opti donn 5 ;
*--------------------------Sortie graphique-------------------------------------
*
NDIM = DIME TAB1.TEMPS ;
*
DEP1 = TAB1.DEPLACEMENTS.(NDIM-1) ;
DEF0 = DEFO SURFT DEP1 0. BLEU ;
DEF1 = DEFO SURFT DEP1 ROUG ;
TITR 'DEFROMEE APRES CHARGEMENT';
TRAC (DEF0 ET DEF1);
*
SIGM1 = TAB1.CONTRAINTES.(NDIM-1) ;
SIGEQ1 = VMIS MOD1 SIGM1 ;
TEMPO =  tab1.temps.(NDIM-1) ;
TITRE 'CONRAINTE DE VON MISES AU TEMPS' TEMPO;
TRAC MOD1 SIGEQ1 ;
*
DC1 = EXCO (TAB1.VARIABLES_INTERNES.(NDIM-1)) DC ;
TEMPO =  tab1.temps.(NDIM-1) ;
TITRE 'ENDOMMAGEMENT DE FLUAGE AU TEMPS' TEMPO;
TRAC MOD1 DC1 ;
*
DD1 = EXCO (TAB1.VARIABLES_INTERNES.(NDIM-1)) DD ;
TEMPO =  tab1.temps.(NDIM-1) ;
TITRE 'ENDOMMAGEMENT DUCTILE AU TEMPS' TEMPO;
TRAC MOD1 DD1 ;
*
EPSE1 = EXCO (TAB1.VARIABLES_INTERNES.(NDIM-1)) EPSE ;
TEMPO =  tab1.temps.(NDIM-1) ;
TITRE 'DEFORMATIONS CUMULEES AU TEMPS' TEMPO;
TRAC MOD1 EPSE1 ;
*
fin;











