* fichier : test_acos.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************


*===================================================================
*
* Teste le bon fonctionnement des opérateurs ACOS, ASIN, ATG et TAN
*
*===================================================================
*
* Auteur : P. Maugis, 26/09/2007
*
*===================================================================
* Teste ACOS, ASIN, ATG
* ---------------------

EPS0=1d-13 ;

* flottants
x  = 'ACOS' 0. ;
'SI' ('NEG' x 90. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ASIN' 1. ;
'SI' ('NEG' x 90. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ATG'  1.D30 ;
'SI' ('NEG' x 90. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ATG'  1. 0. ;
'SI' ('NEG' x 90. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ACOS' (2**-.5) ;
'SI' ('NEG' x 45. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ASIN' (2**-.5) ;
'SI' ('NEG' x 45. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ATG'  1. ;
'SI' ('NEG' x 45. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
x  = 'ATG'  2. 2.;
'SI' ('NEG' x 45. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;

* listes
l  = 'PROG' 1. 1. .5 0. -.5 -1. -1 ;
l2 = 'PROG' 0. 0. 60. 90. 120. 180. 180. ;
*'SI' (('MAXIMUM' (('ACOS' l) - l2) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
l3 = 'PROG' 1d-30 (3.**.5) 1d30 (-1. * (3**.5)) -1d30 ;
l31= 'PROG' 45. 45. 45. -135. -135. ;
l4 = 'PROG' 0. 60. 90. -60. -90. ;
'SI' (('MAXIMUM' (('ATG' l3) - l4) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
'SI' (('MAXIMUM' (('ATG' l3 l3)-l31) 'ABS') > EPS0) ;
  'ERREUR' 5;
'FINSI';

* champ-points
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;
m  = 'DROIT' (0 0) 4 (0 1) ;
m2  = 'DROIT' (0 0) 6 (0 1) ;
_m = 'CHAN' 'POI1' m ;
_m2 = 'CHAN' 'POI1' m2 ;
c  = 'MANU' 'CHPO' 1 _m2 'HOP' l ;
c2 = 'MANU' 'CHPO' 1 _m2 'HOP' l2 ;
*'SI' (('MAXIMUM' (('ACOS' c) - c2) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
c3 = 'MANU' 'CHPO' 1 _m 'HOP' l3 ;
c31= 'MANU' 'CHPO' 1 _m 'HOP' l31;
c4 = 'MANU' 'CHPO' 1 _m 'HOP' l4 ;
'SI' (('MAXIMUM' (('ATG' c3) - c4) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
  LIST ('ATG' c3 c3);
  LIST ('MAXIMUM' (('ATG' c3 c3) - c31) 'ABS');
'SI' (('MAXIMUM' (('ATG' c3 c3) - c31) 'ABS') > EPS0) ;
  'ERREUR' 5;
'FINSI';

* chamelem (ici SEG2 aux noeuds)
d  = 'CHANGER' 'CHAM' c  m2 'NOEUD' ;
d2 = 'CHANGER' 'CHAM' c2 m2 'NOEUD' ;
'SI' (('MAXIMUM' (('ACOS' d) - d2) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
d3 = 'CHANGER' 'CHAM' c3  m 'NOEUD' ;
d31= 'CHANGER' 'CHAM' c31 m 'NOEUD' ;
d4 = 'CHANGER' 'CHAM' c4  m 'NOEUD' ;

'SI' (('MAXIMUM' (('ATG' d3 d3) - d31) 'ABS') > EPS0) ;
  'ERREUR' 5;
'FINSI';
'SI' (('MAXIMUM' (('ATG' d3) - d4) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';

* Teste TAN
* ---------
* flottants
x  = 'TAN' 45. ;
'SI' ('NEG' x 1. EPS0) ; 'ERREUR' 5 ; 'FINSI' ;
*
x = 'TAN'  90.   ; mess x ;
x = 'TAN'  270.  ; mess x ;
x = 'TAN' -90.   ; mess x ;
x = 'TAN' -270.  ; mess x ;

* listes
l5 = 'PROG' 0. 45. 60. 120. 135. 180. ;
l6 = 'PROG' 0. 1. (3.**.5) (-1. * (3**.5)) -1. 0. ;
'SI' (('MAXIMUM' (('TAN' l5) - l6) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';

* champ-points
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;
m  = 'DROIT' (0 0) 5 (0 1) ;
_m = 'CHAN' 'POI1' m ;
c5 = 'MANU' 'CHPO' 1 _m 'HOP' l5;
c6 = 'MANU' 'CHPO' 1 _m 'HOP' l6 ;
'SI' (('MAXIMUM' (('TAN' c5) - c6) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';


* chamelem (ici SEG2 aux noeuds)
d5 = 'CHANGER' 'CHAM' c5 m 'NOEUD' ;
d6 = 'CHANGER' 'CHAM' c6 m 'NOEUD' ;
'SI' (('MAXIMUM' (('TAN' d5) - d6) 'ABS') > EPS0) ; 'ERREUR' 5; 'FINSI';
'FIN' ;







