* fichier :  four1.dgibi
************************************************************************
* Section : Mecanique Fourier
************************************************************************
*           Test Four1.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test four1.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*******************************************************
*  TEST FOUR1
*
*  CYLINDRE INFINI SOUS PRESSION EXTERNE(NU=0)
*
* Soit un cylindre infini soumis à une pression externe
*
* Une analyse de flambage permet de déterminer la charge critique
* associée au deuxième mode de Fourier de la structure :
* (u = u°*cos(2*Teta) )
*
* Le calcul est fait deux fois
*  1- Avec les précontraintes dues à la pression externe
*  2- Avec en plus la prise en compte des forces suiveuses (Evolution
*      des efforts de pression avec le déplacement de la structure)
*
*******************************************************
TITRE 'CYLINDRE INFINI SOUS PRESSION EXTERNE';
OPTI DIME 2 ELEM SEG2 MODE FOUR 0;
OPTI EPSI LINEAIRE;

TEMPS ;

*
*----------- CONSTRUCTION DE LA GEOMETRIE -------------
*
PA=1000. 0.;PB=1000. 100.;PO1=0. 0.;PO2=0. 100.;
CYL = PA D 4 PB;
*
SI (NEG GRAPH 'N');
 TRAC 'QUAL' ECLA ELEM CYL;
FINSI;
*
MOD1 = MODE CYL MECANIQUE ELASTIQUE COQ2;

*
*--- DECLARATION DE FOURIER NOHARM POUR LES OBJETS ----
*-------  QUI SERONT UTILISES POUR PLUSIEURS ----------
*--------------- NUMEROS D'HARMONIQUE -----------------
*
OPTI MODE FOUR NOHARM;
*
*
*-------- CONDITIONS AUX LIMITES SYMETRIQUES ----------
*
SYMB = SYMT CYL DEPL ROTA PA PO1  0.5;
SYMH = SYMT CYL DEPL ROTA PB PO2  0.5;
CDL  = SYMB ET SYMH;
*
*----------- MATERIAU ET CARACTERISTIQUES -------------
*
MAT = MATE MOD1 YOUN 20000. NU 0.;
CAR = CARA MOD1 EPAI 1.;
MAT = MAT  ET   CAR ;
*
*----------- DECLARATION DE FOURIER MODE 0 ------------
*----------- POUR LE CALCUL DES CONTRAINTES -----------
*
OPTI MODE FOUR 0;
MOP = 'MODE' CYL 'CHARGEMENT' 'PRESSION' 'COQ2' ;
MAP = 'PRES' MOP 'PRES' 1. ;
MOD1 = MOD1 ET MOP ; MAT = MAT ;
FP = 'BSIG' MOP MAP ;
*
RIG = RIGI MOD1 MAT;

RIGADD = RIGI UT 1.E-4 PA ;

AAA = RIG ET CDL ET RIGADD ;
U   = RESO AAA  FP ;
SIG=SIGMA U MOD1 MAT;
*
*----------- DECLARATION DE FOURIER MODE 2 ------------
*------------ POUR L'ANALYSE DE FLAMBAGE --------------
*
OPTI MODE FOUR 2;

PP  = MANU CHPO CYL 1 P 1. ;
*
MKSI= KSIGMA MOD1 MAT (SIG * -1.)  FLAM;
RIG = RIGI MOD1 MAT ;
*
*------- RECHERCHE DE LA 1ERE FREQUENCE PROPRE --------
*
MODF   = VIBR PROC (PROG 0.) (RIG ET CDL) MKSI;
W1 = MODF . MODES . 1 . FREQUENCE;
LAMBDA1=(W1 * PI * 2) ** 2 * (SIGN W1);
ERR1   = 100*(ABS((2E-5/3) - LAMBDA1)/(2E-5/3));

MKP    = KP PP MOP FLAM ;
MODF   = VIBR PROC (PROG 0.) (RIG ET CDL) (MKSI ET MKP);
W2 = MODF . MODES . 1 . FREQUENCE;
LAMBDA2=(W2 * PI * 2) ** 2 * (SIGN W2);
ERR2   = 100*(ABS(5E-6    - LAMBDA2)/5E-6   );

SAUT PAGE ;
SAUT 5 LIGN ;
MESS 'K(SIG) SEUL  : ON DOIT TROUVER LAMBDA=  6.66E-6'  ;
MESS '               LE CALCUL DONNE LAMBDA=' LAMBDA1   ;
MESS '     SOIT UN ECART DE                : ' ERR1 '%' ;
SAUT 2 LIGN ;

MESS 'K(SIG)+K(P)  : ON DOIT TROUVER LAMBDA=  5E-6'     ;
MESS '               LE CALCUL DONNE LAMBDA=' LAMBDA2   ;
MESS '     SOIT UN ECART DE                : ' ERR2 '%' ;
SAUT 2 LIGN ;

TEMPS ;
*-------------  CODE DE FONCTIONNEMENT ----------------

ERRMAX=MAXI (PROG ERR1 ERR2);

SI (ERRMAX < 2e-5 );
  ERRE 0;
SINON ;
  ERRE 5;
FINSI;

FIN;










