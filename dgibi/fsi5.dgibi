* fichier :  fsi5.dgibi
************************************************************************
* Section : Fluides Vibration
* Section : Mecanique Interaction Fluide Structure
************************************************************************
* CAS TEST DU 91/10/04     PROVENANCE : PETI

*******************************************
*    Test fsi5.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI TRAC PSC ;
FINSI ;

SAUT PAGE;
*******************************************
*              TEST FSI5                  *
*                                         *
*  ANNULAR FLUID LAYER WITH FREE SURFACE  *
*                                         *
*  Calculation of the frequencies of an   *
*  annular water layer with free surface, *
*  for m= 1                               *
*  The mean radius is 0.5m and the        *
*  thickness of annulus is 5mm            *
*                                         *
*                P5_________              *
*                 |         |             *
*                 |         |             *
*                 |         |             *
*                 |  fluid  |             *
*                 |         |             *
*                 |         |             *
*                 |         |             *
*             P6  -----------             *
*                                         *
*  The boundary conditions are            *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dz  | h = 0.                       *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dr  | r = Ri , Ro                  *
*******************************************
TEMP ZERO;

OPTI DIME 2;
OPTI MODE FOUR 1  ;
OPTI ELEM QUA4;
OPTI EPSI LINEAIRE;
*=========================================*
*               geometry                  *
*=========================================*

* Dimensions en metres

* Points

THICK =  5.0      ;
N1 = 20           ;
R  = 500.         ;
P5 = (R - (THICK * 0.5 )) 1000.0 ;
P6 = (R - (THICK * 0.5 )) 0.0    ;

S5 =P5 D N1 P6    ;

WATER = S5 TRAN 1  (THICK  0. )    ;
FRSUR  = COTE WATER  4 ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI5 : MAILLAGE';
  TRAC QUAL (WATER ET (0 0));
FINSI;

*=========================================*
*    MODE - materiau - rigidite - masse   *
*=========================================*

MODLIQ =MODE WATER LIQUIDE LQU4 ;
MODSUR =MODE FRSUR  LIQUIDE LSU2;


MATLIQ = MATE (MODLIQ ET MODSUR)
               RHO 1.E-9  RORF 1.E-9
               CSON 1435.E3 CREF 1435.E3
               LCAR 1000. G 9810.;

RIG1 = RIGI  (MODLIQ ET MODSUR) MATLIQ  ;
MAS1 = MASS  (MODLIQ ET MODSUR) MATLIQ  ;

*=========================================*
*                boundary conditions      *
*=========================================*
* No explicit boundary condition
* the boundary conditions are natural

* theoretical value  of frequency
FRE1   = 0.6922;
LIST1 = PROG FRE1                        ;

*=========================================*
*     calculation of the frequencies      *
*                   and                   *
*       extraction of some results        *
*=========================================*

* Use of the operator VIBR (option PROC)
RESUL2 = VIBR 'PROC' LIST1     RIG1  MAS1     ;

* Use of the operator VIBR (option IRAM)
*  opti impi 5;
RESUL3 = VIBR 'IRAM' 0.001 1      RIG1  MAS1     ;
*  opti impi 0;

*=========================================*
*                   results               *
*=========================================*

OPTI ECHO 0;
MESS ' RESULTATS ';
MESS ' ----------------------------------------------------------';
MESS '      |    Frequence propre en Hz (erreur relative en %) ';
MESS ' Mode |  Theorie |    VIBR PROCH     |    VIBR IRAM      ';
MESS ' ----------------------------------------------------------';

  FRE2 = RESUL2  . MODES . 1 . FREQUENCE;
  ERG2  = 100 * (ABS ((FRE1   - FRE2  ) / FRE1  ));
  FRE3 = RESUL3  . MODES . 1 . FREQUENCE;
  ERG3  = 100 * (ABS ((FRE1   - FRE3  ) / FRE1  ));
   chacha = chai 1 *5 ' |' FORMAT '(F9.6)' FRE1
          ' |' FORMAT '(F9.6)' FRE2 ' (' FORMAT '(F5.3)' ERG2 '%)'
          ' |' FORMAT '(F9.6)' FRE3 ' (' FORMAT '(F5.3)' ERG3 '%)';
   MESS chacha;

* mode shape extraction and optional trace

  SI (NEG GRAPH 'N');
     MOD2  = RESUL2  . MODES . I . DEFORMEE_MODALE;
     DEF0 = DEFO  FRSUR  MOD2 0.  ;
     DEF2 = DEFO  FRSUR  MOD2 'BLEU';
     TRAC (DEF2 ET DEF0);
     DEF3 = DEFO  FRSUR  MOD3 'ROSE';
     TRAC (DEF3 ET DEF0);
  FINSI;

MESS ' ----------------------------------------------------------';
OPTI ECHO 1;

*=========================================*
*          code validation                *
*=========================================*

ERG2MAX = ERG2   ;
ERG3MAX = ERG3   ;

SI (ERG2MAX > 4.5);
  MESS 'ERREUR VIBR OPTION PROCHE';
  ERRE 5;
FINSI;
SI (ERG3MAX > 4.5);
  MESS 'ERREUR VIBR OPTION IRAM';
  ERRE 5;
FINSI;
  ERRE 0;

TEMP;

FIN;








