'OPTI' 'ECHO' 0 ;
************************************************************************
* Section : Mecanique Plastique
************************************************************************
itrac = FAUX ;

************************************************************************
* Cas test sur la mise a jour des vecteurs orientant les elements JOI1 *
* avec FORM                                                            *
*                                                                      *
* On modelise le contact entre deux poutres a section annulaires       *
* lorsque l'une d'elles est flechie et vient toucher l'autre.          *
*                                                                      *
* Les poutres sont sont modelisees avec des elements TUYA.             *
* Le contact est modelise par des elements JOI1                        *
* La mise a jour du repere local des joints, au cours du calcul,       *
* permet de gerer le contact lorsque sa direction change               *
*                                                                      *
*                                                                      *
*    _________________________                                         *
*                |                                                     *
*                |                                                     *
*                |             Deplacement UX impose au sommet         *
*                |             de la poutre A                          *
*                |                                        ____         *
*                |                                    B .'    `.       *
*                |                                     /        \      *
*            A | | B                                   |        |      *
*              |                               ____    \        /      *
*              |  Z                        A .'    `.   `.____.'       *
*              |  |                         /        \                 *
*              |  ---X                      |  --->  |     Y           *
*              |                            \        /     |           *
*              |                             `.____.'      ---X        *
*    __________|______________                                         *
*                                                                      *
*                                                                      *
* - element joint, liaison, JOI1                                       *
* - grands deplacements                                                *
* - PASAPAS                                                            *
*                                                                      *
************************************************************************




** Parametres geometriques des poutres (section annulaire)
*  hauteur, rayon, epaisseur, entraxe, angle
h     = 0.5 ;
re    = 1.2E-2 ;
ep    = 1.E-3 ;
dt    = 3.E-2 ;
alpha = 30. ;


** Hauteur du maillage de contact (par des joints)
hc = 0.01 ;


** Valeur du deplacement impose au sommet de la poutre 1
uimp = 2. * dt ;


** Verification qu'il y aura bien contact
de = 2. * re ;
a = (uimp / 1.)**2 ;
b = -2. * dt * (COS alpha) * uimp / 1. ;
c = (dt**2) - (de**2) ;
delta = (b**2) - (4. * a * c) ;
SI (delta < 0.) ;
  MESS ;
  MESS '***** Aucun contact possible avec ce jeu de parametres' ;
  MESS '***** ECHEC du cas test !' ;
  MESS ;
  ERREUR 5 ;
FINSI ;


** Options generales
OPTI 'DIME' 3 'ELEM' 'SEG2' ;


** Maillage des poutres
p1b = 0. 0. 0. ;
p1h = 0. 0. h ;
l1 = DROI 40 p1b p1h ;
p1hb = l1 POIN 'PROC' (0. 0. (h - hc)) ;
z1b = COOR 3 p1hb ;
l2 = l1 PLUS ((dt * (COS alpha)) (dt * (SIN alpha)) z1b) ;
p2b = l2 POIN 'INIT' ;
p2h = l2 POIN 'FINA' ;
p2bb = l2 POIN 'PROC' p1h ;
ltot = l1 ET l2 ;


** Maillage des joints sur la zone ou les poutres peuvent se toucher
l1c = l1 ELEM 'COMP' p1hb p1h ;
mp1 = CHAN 'POI1' l1c ;
j = VIDE 'MAILLAGE'/'SEG2' ;
REPE b1 (NBEL mp1) ;
  p11 = mp1 POIN &b1 ;
  p22 = l2 POIN 'PROC' p11 ;
  j = j ET (DROI 1 p11 p22) ;
FIN b1 ;


** Modele et materiau
mo = MODE ltot 'MECANIQUE' 'ELASTIQUE' 'TUYA' 'CONS' 'AIGUILLES' ;
ma = MATE mo 'YOUN' 200.E9 'NU' 0.3 'EPAI' ep 'RAYO' re ;
moj = MODE j 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE'
                         'PLASTIQUE' 'COULOMB'
                         'JOI1' 'CONS' 'JOINTS' 'LIE' ;
maj = MATE moj 'KN'  1.E6  'KS1' 1. 'KS2'  1.
               'QN'  1.    'QS1' 1. 'QS2'  1.
               'FNE' 1.E30 'QT'  0. 'COHE' 0. 'FRIC' 0. 'TYPE' 1
               'DIRECTION' ((COS alpha) (SIN alpha) 0.) (0. 0. 1.) ;


** Conditions aux limites (encastrement des extremites et
*  deplacement impose au sommet de la poutre 1)
bl1 = BLOQ 'DEPL' 'ROTA' (p1b ET p2h) ;
bl2 = BLOQ 'UY' 'UZ' p1h ;
bl3 = BLOQ 'UX' p1h ;
f1 = DEPI bl3 uimp ;
ev1 = EVOL 'MANU' (PROG 0. 1.E-9 1.) (PROG 0. 0. 1.) ;
cha1 = CHAR 'DIMP' f1 ev1 ;


** Chargement de deformation impose dans les joints pour
*  represener le jeu initial
jeu0 = dt - (2. * re) ;
exx = MANU 'CHML' moj 'EXX' (-1. * jeu0) 'TYPE' 'DEFORMATIONS'
                                         'STRESSES' ;
e1 = (ZERO (mo ET moj) 'DEFORMATIONS') + exx ;
ev2 = EVOL 'MANU' (PROG 0. 1.E-9 1.) (PROG 0. 1. 1.) ;
cha2 = CHAR 'DEFI' e1 ev2 ;


** Trace du maillage
SI itrac ;
  TRAC (ltot ET (j COUL 'VERT')) 'TITR' 'Maillage' ;
FINSI ;


** Calcul avec PASAPAS
* on fait 1 premier pas de temps tres petit pour la mise en place
* du jeu initial sous forme de deformation imposee dans les joints
xpas = 0.01 ;
tab = TABL ;
tab . 'MODELE'              = mo ET moj ;
tab . 'CARACTERISTIQUES'    = ma ET maj ;
tab . 'BLOCAGES_MECANIQUES' = bl1 ET bl2 ET bl3 ;
tab . 'CHARGEMENT'          = cha1 ET cha2 ;
tab . 'TEMPS_CALCULES'      = PROG 1.E-9 xpas PAS xpas 1. ;
tab . 'GRANDS_DEPLACEMENTS' = VRAI ;
PASAPAS tab ;




** Verification du bon comportement
* - la distance entre les deux tuyaux doit rester superieure
*   au diametre exterieur
* - le premier instant du contact doit etre egal a la valeur theorique
* - la poutre 2 doit toujours flechir dans la direction du contact


** Cercles pour visualiser les sections sur le maillage deforme
SI itrac ;
  pcer = re 0. 0. ;
  cer0 = CERC 10 pcer (0. 0. 0.) (0. re 0.) ;
  cer0 = CERC 10 cer0 (0. 0. 0.) ((-1. * re) 0. 0.) ;
  cer0 = CERC 10 cer0 (0. 0. 0.) (0. (-1. * re) 0.) ;
  cer0 = CERC 10 cer0 (0. 0. 0.) pcer ;
FINSI ;


** Calcul des indicateurs de post-traitement
MESS ;
tt    = tab . 'TEMPS' ;
tu    = tab . 'DEPLACEMENTS' ;
ts    = tab . 'CONTRAINTES' ;
n1    = 'DIME' tt ;
tdc   = 0. ;
tfc   = 1. ;
logdc = FAUX ;
logfc = FAUX ;
ltps  = PROG ;
leffx = PROG ;
lvu2  = PROG ;
ldir1 = PROG ;
ldir2 = PROG ;
conf0 = FORM ;
ld12  = PROG ;
REPE b1 n1 ;
  i1 = &b1 - 1 ;
  tps1 = tt . i1 ;
  SI (EGA tps1 1.E-9) ;
    ITER b1 ;
  FINSI ;
  ltps = ltps ET tps1 ;
  u1 = tu . i1 ;
  FORM u1 ;
* distance entre les points de contact au sommet
  p12 = p2bb MOIN p1h ;
  ld12 = ld12 ET (NORM p12) ;
* reaction au contact
  s1 = REDU (ts . i1) moj ;
  effx1 = 0. ;
  REPE b2 (NBEL j) ;
    effx1 = effx1 + (ABS (EXTR s1 'EFFX' 1 &b2 1)) ;
  FIN b2 ;
  leffx = leffx ET effx1 ;
* detection de l'instant ou debute le contact
  SI ((NON logdc) ET (effx1 > 1.E-1)) ;
    logdc = VRAI ;
    tdc  = 0.5 * (tps1 + (tt . (i1 - 1))) ;
  FINSI ;
* detection de l'instant ou fini le contact
  SI (logdc ET (NON logfc) ET (effx1 <EG 1.E-1)) ;
    logfc = VRAI ;
    tfc  = 0.5 * (tps1 + (tt . (i1 - 1))) ;
  FINSI ;
* direction du contact (angle d'orientation du bipoint)
  dir1 = ATG (COOR 2 p12) (COOR 1 p12) ;
  ldir1 = ldir1 ET dir1 ;
* amplitude du deplacement du point "esclave"
  up12 = REDU u1 (p1h ET p2bb) ;
  ux2 = EXTR up12 'UX' p2bb ;
  uy2 = EXTR up12 'UY' p2bb ;
  uz2 = EXTR up12 'UZ' p2bb ;
  vu2 = ((ux2**2) + (uy2**2) + (uz2**2))**0.5 ;
  lvu2 = lvu2 ET vu2 ;
* direction du deplacement du point "esclave"
  vu22 = ((ux2**2) + (uy2**2))**0.5 ;
  SI ((vu22 <EG 1.E-6) OU (NON logdc) OU logfc) ;
    dir2 = 0. ;
  SINON ;
    dir2 = ATG uy2 ux2 ;
  FINSI ;
  ldir2 = ldir2 ET dir2 ;
* trace de la deformee
  SI itrac ;
    vec1 = VECT up12 'DEPL' 1. 'VERT' ;
    tit1 = CHAI 'Deformee a l instant ' tps1 ;
    cer1 = ((cer0 PLUS p1h) ET (cer0 PLUS p1hb)) COUL 'ROUG' ;
    cer2 = ((cer0 PLUS p2b) ET (cer0 PLUS p2bb)) COUL 'JAUN' ;
    TRAC vec1 (ltot ET cer1 ET cer2) 'TITR' tit1 ;
  FINSI ;
  FORM conf0 ;
FIN b1 ;

MESS ;
MESS '***** LE CONTACT A LIEU ENTRE LES INSTANTS :' ;
MESS tdc ;
MESS tfc ;

SI itrac ;
  evd12 = EVOL 'ROUG' 'MANU' 'Temps' ltps 'Distance' ld12 ;
  DESS evd12 'TITR' 'Distance points A--B vs Temps' ;
FINSI ;


** Test1 : premier instant de contact
MESS ;
MESS '***** TEST 1 : INSTANT DU PREMIER CONTACT' ;
tdcthe = ((-1. * b) - (delta**0.5)) / (2. * a) ;
err1 = ABS ((tdc - tdcthe) / xpas) ;
MESS '- Instant de contact theorique    =' tdcthe ;
MESS '- Instant de contact calcule      =' tdc ;
MESS '- Ecart en nombre de pas de temps =' err1 ;
SI itrac ;
  eveffx = EVOL 'ROUG' 'MANU' 'Temps' ltps 'Effort' leffx ;
  DESS eveffx 'TITR' 'Resultante de l effort de contact vs Temps' ;
FINSI ;
SI (err1 > 1.5) ;
  MESS '***** ECHEC du cas test !' ;
  MESS ;
  ERREUR 5 ;
SINON ;
  MESS '***** OK !' ;
FINSI ;


** Test 2 : la poutre 2 doit se deplacer dans la direction du contact
MESS ;
MESS '***** TEST 2 : DEPLACEMENT DE LA POUTRE 2' ;
err2m = 0. ;
tps2m = 0. ;
err3m = 0. ;
tps3m = 0. ;
REPE b1 (DIME ltps) ;
  tps1 = EXTR ltps &b1 ;
* hors du contact, on regarde l'amplitude du deplacement
  SI ((tps1 < tdc) OU (tps1 > tfc)) ;
    v2 = EXTR lvu2 &b1 ;
    SI (v2 > err3m) ;
      err3m = v2 ;
      tps3m = tps1 ;
    FINSI ;
* pendant le contact, on regarde la direction du deplacement
  SINON ;
    dir1 = EXTR ldir1 &b1 ;
    dir2 = EXTR ldir2 &b1 ;
    err2 = ABS (dir2 - dir1) ;
    SI (err2 > err2m) ;
      err2m = err2 ;
      tps2m = tps1 ;
    FINSI ;
  FINSI ;
FIN b1 ;
MESS '- [Pendant le contact] ecart d angle max =' err2m ;
MESS '- [ Hors  du  contact] amplitude max     =' err3m ;
SI ((err2m > 3.) OU (err3m > (0.1 * uimp))) ;
  MESS '***** ECHEC du cas test !' ;
  MESS '***** instant de l ecart max d angle = ' tps2m ;
  MESS '***** instant de l amplitude max     = ' tps3m ;
  MESS ;
  ERREUR 5 ;
SINON ;
  MESS '***** OK !' ;
FINSI ;
SI itrac ;
  evd1 = EVOL 'ROUG' 'MANU' 'Temps' ltps 'Direction' ldir1 ;
  evd2 = EVOL 'VERT' 'MANU' 'Temps' ltps 'Direction' ldir2 ;
  DESS (evd1 ET evd2) 'TITR'
   'Direction du deplacement (v) et du contact (r) vs Temps' ;
  evu2 = EVOL 'ROUG' 'MANU' 'Temps' ltps 'Apmplitude' lvu2 ;
  DESS evu2 'TITR' 'Amplitude du deplacement vs Temps' ;
FINSI ;


FIN ;



