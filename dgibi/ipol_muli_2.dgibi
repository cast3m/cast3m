OPTI 'DIME' 3 'ELEM' 'CU20' 'ECHO' 0 ;
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************

************************************************************************
*                 Test de l'operateur IPOL option GRILL                *
*     interpolation multi-lineaire d'une fonction de n parametres      *
*                   definie sur une grile de points                    *
*                                                                      *
* Application a l'interpolation d'un champ de temperature sur un       *
* cylindre                                                             *
* - test avec temperature fonction de 2 variables (r, theta)           *
* - test avec temperature fonction de 3 variables (r, theta, z)        *
* - test avec des CHPOINTs et des MCHAMLs                              *
************************************************************************


** Indicateur pour le trace des champs interpoles
itrac = FAUX ;

** Tolerance pour les tests sur les valeurs interpolees
tol1 = 1.E-14 ;

** Maillage du cylindre
p1 = 1.5 0. -5. ;
p2 = 3.5 0. -5. ;
l1 = DROI 20 p1 p2 ;
mail1 = l1 ROTA 72 360. (0. 0. 0.) (0. 0. 1.) ;
ELIM 1.E-9 mail1 ;
mail2 = mail1 VOLU 'TRAN' 50 (0. 0. 20.) ;
MESS ; MESS ; MESS ;



** TEST en dimension 2
** -------------------
** Grille de valeurs de la fonction
lr = PROG 2. 3. ;
lt = PROG -180. 0. 180. ;
lf = PROG 450. 410.   430. 400.   450. 410. ;

** Champs de coordonnees polaires du maillage de la couronne inferieure
x y z = COOR mail1 ;
r = ((x**2) + (y**2))**0.5 ;
t = ATG y x ;
chp1 = (NOMC 'R' r 'NATU' 'DIFFUS') ET (NOMC 'T' t 'NATU' 'DIFFUS') ;
mo = MODE mail1 'MECANIQUE' ;
che1 = CHAN 'CHAM' mo chp1 'STRESSES' ;

** Interpolation du champ sur la couronne
MESS 'Interpolation bi-lineaire' ;
MESS ;
nu1 = NUAG 'COMP' 'R'    lr
           'COMP' 'T'    lt
           'COMP' 'FONC' lf ;
chp2 = IPOL chp1 nu1 'GRILL' ;
che2 = IPOL che1 nu1 'GRILL' ;

** Test des valeurs interpolees en certains points cibles
tp = TABL ;
tf = TABL ;
tp . 1 = (1.5 * (COS 0.)) (1.5 * (SIN 0.)) -1. ;
tf . 1 = 430. ;
tp . 2 = (1.5 * (COS 90.)) (1.5 * (SIN 90.)) -1. ;
tf . 2 = 440. ;
tp . 3 = (2.5 * (COS 90.)) (2.5 * (SIN 90.)) -1. ;
tf . 3 = 422.5 ;
tp . 4 = (2.5 * (COS 220.)) (2.5 * (SIN 220.)) -1. ;
tf . 4 = 426.666666666666 ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp2 'FONC' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp2 mail1 'TITR' 'Interpolation bi-lineaire CHPOINT' ;
  TRAC che2 mo    'TITR' 'Interpolation bi-lineaire MCHAML' ;
FINSI ;
MESS ; MESS ; MESS ;



** TEST en dimension 3
** -------------------
** Grille de valeurs de la fonction
lz = PROG 0. 10. ;
lf = lf ET (PROG 750. 700.   660. 600.   750. 700.) ;

** Champ de coordonnees polaires du maillage du cylindre
x y z = COOR mail2 ;
r = ((x**2) + (y**2))**0.5 ;
t = ATG y x ;
chp1 = (NOMC 'R' r 'NATU' 'DIFFUS') ET (NOMC 'T' t 'NATU' 'DIFFUS') ET
       (NOMC 'Z' z 'NATU' 'DIFFUS') ;
mo = MODE mail2 'MECANIQUE' ;
che1 = CHAN 'CHAM' mo chp1 'STRESSES' ;

** Interpolation tri-lineaire
MESS 'Interpolation tri-lineaire' ;
MESS ;
nu1 = NUAG 'COMP' 'R'    lr
           'COMP' 'T'    lt
           'COMP' 'Z'    lz
           'COMP' 'FONC' lf ;
chp2 = IPOL chp1 nu1 'GRILL' ;
che2 = IPOL che1 nu1 'GRILL' ;

** Test des valeurs interpolees en certains points cibles
tp = TABL ;
tf = TABL ;
tp . 1 = (1.5 * (COS 0.)) (1.5 * (SIN 0.)) -1. ;
tf . 1 = 430. ;
tp . 2 = (1.5 * (COS 90.)) (1.5 * (SIN 90.)) -1. ;
tf . 2 = 440. ;
tp . 3 = (2.5 * (COS 90.)) (2.5 * (SIN 90.)) 0. ;
tf . 3 = 422.5 ;
tp . 4 = (2.5 * (COS 220.)) (2.5 * (SIN 220.)) 0. ;
tf . 4 = 426.666666666666 ;
tp . 5 = (3. * (COS 270.)) (3. * (SIN 270.)) 11. ;
tf . 5 = 650. ;
tp . 6 = (2. * (COS 180.)) (2. * (SIN 180.)) 10. ;
tf . 6 = 750. ;
tp . 7 = (2.5 * (COS 140.)) (2.5 * (SIN 140.)) 10. ;
tf . 7 = 703.888888888888 ;
tp . 8 = (2.5 * (COS 140.)) (2.5 * (SIN 140.)) 5. ;
tf . 8 = 565.277777777777 ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail2 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp2 'FONC' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp2 mail2 'TITR' 'Interpolation tri-lineaire CHPOINT' ;
  TRAC che2 mo    'TITR' 'Interpolation tri-lineaire MCHAML' ;
FINSI ;
MESS ; MESS ; MESS ;



MESS 'SUCCES DU CAS TEST !' ;


FIN ;





