* fichier :  plas4.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Plas4.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test plas4.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************************
*     CUBE EN TRACTION UNIAXIALE
*
*     TEST ELEMENTAIRE DU GROUPE DE TRAVAIL
*     'STATIQUE NON LINEAIRE' COMMISSION VPCS
*
*     LE CALCUL MARCHE ET DONNE EXACTEMENT LES
*     RESULTATS THEORIQUES LE 05/05/90
*
*     Un cube constitué de 2 éléments CUB8 est soumis à
*     une traction uniaxiale (blocages des déplacements
*     suivant Y et Z et déplacement imposé suivant X
*     aux deux extrémités (surfaces SU1 et SU2))
*
*     Les contraintes obtenues sont comparées aux
*     résultats théoriques et donnent les résultats
*     précisés plus bas.
*
*******************************************************
*

SAUT PAGE;
TITRE 'CUBE EN TRACTION UNIAXIALE';
OPTION DIME 3 ECHO 0;
TEMPS ;
DENS 1;
*------------------------------------------------------
***** DEFINITION DE LA GEOMETRIE
*------------------------------------------------------
A = 1.;
A1 = -1.;
A2 = .5;
B = 1.;
C = 1.;
P1 = A 0 0; Q1 = A2 0 0; R1 = 0 0 0;
P2 = A B 0; Q2 = A2 B 0; R2 = 0 B 0;
P3 = A B C; Q3 = A2 B C; R3 = 0 B C;
P4 = A 0 C; Q4 = A2 0 C; R4 = 0 0 C;
TR = A1 0 0;
OPTION ELEM SEG2;
LI1 = P1 DROIT 1 P2;
LI2 = P2 DROIT 1 P3;
LI3 = P3 DROIT 1 P4;
LI4 = P4 DROIT 1 P1;
CO1 = LI1 ET LI2 ET LI3 ET LI4;
LJ1 = Q1 DROIT 1 Q2;
LJ2 = Q2 DROIT 1 Q3;
LJ3 = Q3 DROIT 1 Q4;
LJ4 = Q4 DROIT 1 Q1;
CO2 = LJ1 ET LJ2 ET LJ3 ET LJ4;
LK1 = R1 DROIT 1 R2;
LK2 = R2 DROIT 1 R3;
LK3 = R3 DROIT 1 R4;
LK4 = R4 DROIT 1 R1;
CO3 = LK1 ET LK2 ET LK3 ET LK4;
OPTION ELEM QUA4;
SU1 = SURFACE CO1 PLANE;
SU2 = SURFACE CO2 PLANE;
SU3 = SURFACE CO3 PLANE;
OPTION ELEM CUB8;
VOL1 = SU1 VOLU 1 SU2;
VOL2 = SU2 VOLU 1 SU3;
VOL = VOL1 ET VOL2;
ELIM .001 VOL;
*------------------------------------------------------
***** TRACE
*------------------------------------------------------
OEIL = (30 20 20);
SI(NEG GRAPH 'N');
   TRAC OEIL 'QUAL' VOL;
FINSI;
*------------------------------------------------------
***** CONDITIONS AUX LIMITES
*------------------------------------------------------
CL1 = BLOQUE SU3 UX;
CL2 = BLOQUE VOL UY UZ;
CL3 = BLOQUE SU2 UX;
CL4 = BLOQ SU1 UX;
CL = CL1 ET CL2 ET CL3 ET CL4;
*------------------------------------------------------
* VALEURS IMPOSEES DES DEPLACEMENTS
*------------------------------------------------------
EPS0 = .25E-4;
COEF1 = A * EPS0 * .5;
COEF2 = A * EPS0;
D1 = DEPI CL3 COEF1;
D2 = DEPI CL4 COEF2;
DEP = D1 ET D2;
*------------------------------------------------------
***** CALCUL MODELE CARACTERISTIQUES
*------------------------------------------------------
MO=MODE VOL MECANIQUE ELASTIQUE PLASTIQUE PARFAIT CUB8;
MAT1 = MATE MO YOUN 2.5E11 SIGY 5.0E6 NU .25;
*------------------------------------------------------
***** CHAMPS RELATIFS A L'OPERATEUR NONLIN
*------------------------------------------------------
LP1 = PROG 0. 1. 2. 3. 4.;
LP2 = PROG 0. 1. 2. 1. 0.;
EV = EVOL MANU T LP1 F(T) LP2;
CHA1 = CHAR DIMP DEP EV;
LIS1 = PROG 1. 2. 3. 4.;
*------------------------------------------------------
*****  CALCUL NON LINEAIRE
*------------------------------------------------------
TAB1 = TABLE;
TAB1.'CARACTERISTIQUES'    = MAT1;
TAB1.'CHARGEMENT'          = CHA1;
TAB1.'MODELE'              = MO;
TAB1.'TEMPS_CALCULES'      = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;
PASAPAS TAB1;
*------------------------------------------------------
***** EXTRACTION DES RESULTATS
*------------------------------------------------------
SI1 = TAB1.CONTRAINTES.1;
SI2 = TAB1.CONTRAINTES.2;
SI3 = TAB1.CONTRAINTES.3;
SI4 = TAB1.CONTRAINTES.4;
SXX1 = EXTR SI1 SMXX 1 1 1;
SYY1 = EXTR SI1 SMYY 1 1 1;
SZZ1 = EXTR SI1 SMZZ 1 1 1;
SXX2 = EXTR SI2 SMXX 1 1 1;
SYY2 = EXTR SI2 SMYY 1 1 1;
SZZ2 = EXTR SI2 SMZZ 1 1 1;
SXX3 = EXTR SI3 SMXX 1 1 1;
SYY3 = EXTR SI3 SMYY 1 1 1;
SZZ3 = EXTR SI3 SMZZ 1 1 1;
SXX4 = EXTR SI4 SMXX 1 1 1;
SYY4 = EXTR SI4 SMYY 1 1 1;
SZZ4 = EXTR SI4 SMZZ 1 1 1;


SI(NEG GRAPH 'N');
   TITR 'Contrainte SMYY increment 4 en MPa';
   TRAC OEIL ((EXCO SMYY SI4)/1e6) MO;
FINSI;
*------------------------------------------------------
***** AFFICHAGE DES RESULTATS
*------------------------------------------------------
SAUT PAGE;
*////////// PARAMETRE D'EVOLUTION  T : 1 //////////';
*------------------------------------------------------
MESS '                 T = 1 ';
MESS ' RESULTATS THEORIQUES : SIGMAXX =  7.5 E6' ;
MESS '                        SIGMAYY =  2.5 E6' ;
MESS '                        SIGMAZZ =  2.5 E6' ;
MESS '                                         ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX1 ;
MESS '                        SIGMAYY = 'SYY1 ;
MESS '                        SIGMAZZ = 'SZZ1 ;
SAUT 1 LIGNE ;

RESX1 = 100 * (ABS((SXX1-7.5e6)/7.5e6));
RESY1 = 100 * (ABS((SYY1-2.5e6)/2.5e6));
RESZ1 = 100 * (ABS((SZZ1-2.5e6)/2.5e6));
*
*////////// PARAMETRE D'EVOLUTION  T : 2 //////////';
*------------------------------------------------------
MESS '                 T = 2 ';
MESS ' RESULTATS THEORIQUES : SIGMAXX = 11.66 E6' ;
MESS '                        SIGMAYY =  6.66 E6' ;
MESS '                        SIGMAZZ =  6.66 E6' ;
MESS '                                          ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX2 ;
MESS '                        SIGMAYY = 'SYY2 ;
MESS '                        SIGMAZZ = 'SZZ2 ;
SAUT 1 LIGNE ;
RESX2 = 100 * (ABS((SXX2-11.66e6)/11.66e6));
RESY2 = 100 * (ABS((SYY2-6.66e6) /6.66e6 ));
RESZ2 = 100 * (ABS((SZZ2-6.66e6) /6.66e6 ));
*
*////////// PARAMETRE D'EVOLUTION  T : 3 //////////';
*------------------------------------------------------
MESS '                 T = 3 ' ;
MESS ' RESULTATS THEORIQUES : SIGMAXX =  4.16 E6' ;
MESS '                        SIGMAYY =  4.16 E6' ;
MESS '                        SIGMAZZ =  4.16 E6' ;
MESS '                                          ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX3 ;
MESS '                        SIGMAYY = 'SYY3 ;
MESS '                        SIGMAZZ = 'SZZ3 ;
SAUT 1 LIGNE ;
RESX3 = 100 * (ABS((SXX3-4.16e6) /4.16e6));
RESY3 = 100 * (ABS((SYY3-4.16e6) /4.16e6 ));
RESZ3 = 100 * (ABS((SZZ3-4.16e6) /4.16e6 ));

*
*////////// PARAMETRE D'EVOLUTION  T : 4 //////////';
*------------------------------------------------------
MESS '                 T = 4 ' ;
MESS ' RESULTATS THEORIQUES : SIGMAXX = -3.33 E6' ;
MESS '                        SIGMAYY =  1.66 E6' ;
MESS '                        SIGMAZZ =  1.66 E6' ;
MESS '                                          ' ;
MESS ' RESULTATS CALCULES   : SIGMAXX = 'SXX4 ;
MESS '                        SIGMAYY = 'SYY4 ;
MESS '                        SIGMAZZ = 'SZZ4 ;
SAUT 1 LIGNE ;
TEMPS ;
RESX4 = 100 * (ABS((SXX4+3.33e6) /-3.33e6 ));
RESY4 = 100 * (ABS((SYY4-1.66e6) / 1.66e6 ));
RESZ4 = 100 * (ABS((SZZ4-1.66e6) / 1.66e6 ));

*------------------------------------------------------
*                CODE DE FONCTIONNEMENT
*------------------------------------------------------
RESID=MAXI (PROG RESX1 RESX2 RESX3 RESY1 RESY2 RESY3 RESZ1 RESZ2 RESZ3);

SI(RESID <EG 0.5);
  ERRE  0;
SINO;
  ERRE  5;
FINSI;

FIN;




