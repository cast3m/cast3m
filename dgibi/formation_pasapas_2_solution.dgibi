************************************************************************
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Rupture d'une plaque trouee en traction                              *
* Comportement elastique lineaire                                      *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement                                        *
* - la resolution avec PASAPAS                                         *
* - la suppression d'elements du maillage selon un critere de rupture  *
*   base sur la 1ere contrainte principale (avec la procedure PERSO1)  *
* - le post traitement (contraintes, courbe force/deplacement)         *
*                                                                      *
************************************************************************


** Options generales
OPTI 'DIME' 2 'ELEM' 'QUA8' 'MODE' 'PLAN' 'DEFO' 'ECHO' 0 ;
ITRAC = FAUX ;


** Parametres geometriques
L1 = 1. ;
R1 = 0.15 ;


** Maillage
DENS1 = R1 / 15. ;
DENS2 = L1 / 15. ;
P0 = 0. 0. ;
P1 = R1 0. ;
P2 = 0. R1 ;
P3 = L1 0. ;
P4 = L1 L1 ;
P5 = 0. L1 ;
CER = CERC 20 P1 P0 P2 ;
LID = DROI 10 P3 P4 ;
LIH = DROI 10 P4 P5 ;
S1 = REGL CER (LID ET LIH) 'DINI' DENS1 'DFIN' DENS2 ;
LIG = S1 COTE 2 ;
LIB = S1 COTE 4 ;
CS1 = CONT S1 ;


** Modele et materiau
MO = MODE S1 'MECANIQUE' 'ELASTIQUE' ;
MA = MATE MO 'YOUN' 200.E9 'NU' 0.3 ;


** Blocages mecaniques
BL1 = BLOQ 'UY' LIB ;
BL2 = BLOQ 'UX' LIG ;
BL3 = BLOQ 'UY' LIH ;
BL  = BL1 ET BL2 ET BL3 ;


** Chargement en deplacement impose
UMAX = L1 / 20. ;
F1 = DEPI BL3 UMAX ;
EV1 = EVOL 'MANU' (PROG 0. 1.) (PROG 0. 1.) ;
CH = CHAR 'DIMP' F1 EV1 ;


** Procedure PERSO1 pour supprimer les elements selon un critere sur
*  les contraintes principales
DEBP PERSO1 T1*'TABLE' ;
* modele et maillage courant
  MO1 = T1 . 'WTABLE' . 'MO_TOT' ;
  MAIL1 = EXTR MO1 'MAIL' ;
* 1ere contrainte principale
  SG1 = T1 . 'ESTIMATION' . 'CONTRAINTES' ;
  SGP1 = PRIN SG1 MO1 ;
* maillage des elements a conserver (et ceux a retirer)
  SG11 = CHAN 'GRAVITE' (EXCO 'SI11' SGP1) MO1 ;
  MAIL2 = SG11 ELEM 'INFERIEUR' 2.2E10 ;
  MAIL3 = DIFF MAIL1 MAIL2 ;
  NE3 = NBEL MAIL3 ;
* suppression des elements du modele
  SI (NE3 > 0) ;
    MESS ' [PERSO1 :] Suppression de' NE3 'elements' ;
    MO2 = REDU MO1 MAIL2 ;
    T1 . 'WTABLE' . 'MO_TOT' = MO2 ;
    T1 . 'WTABLE' . 'MO_TOTAL' = MO2 ;
    T1 . 'WTABLE' . 'MOD_MEC' = MO2 ;
  FINSI ;
FINP ;


** Resolution
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MO ;
TAB1 . 'CARACTERISTIQUES'    = MA ;
TAB1 . 'BLOCAGES_MECANIQUES' = BL ;
TAB1 . 'CHARGEMENT'          = CH ;
TAB1 . 'TEMPS_CALCULES'      = PROG 0.1 'PAS' 0.1 0.6 'PAS' 0.01 1. ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'PROCEDURE_PERSO1'    = VRAI ;
PASAPAS TAB1 ;


** Post-traitement (deformees, contraintes, reaction aux appuis, courbe force/deplacement)
TT = TAB1 . 'TEMPS' ;
TU = TAB1 . 'DEPLACEMENTS' ;
TS = TAB1 . 'CONTRAINTES' ;
TR = TAB1 . 'REACTIONS' ;
LISO = PROG 0. 'PAS' 4.E9 4.E10 ;
DEF0 = DEFO CS1 (TU . 0) 0. 'ROUG' ;
LTPS = PROG 0. ;
LDEP = PROG 0. ;
LFOR = PROG 0. ;
NT1 = (DIME TT) - 1 ;
REPE B1 NT1 ;
* instant
  TPS1 = TT . &B1 ;
  LTPS = LTPS ET TPS1 ;
* contraintes
  SG1 = TS . &B1 ;
  SP1 = EXCO 'SI11' (PRIN SG1 MO) ;
* deplacements
  U1 = TU . &B1 ;
  UY1 = EXTR U1 'UY' P5 ;
  LDEP = LDEP ET UY1 ;
* reactions
  R1 = TR . &B1 ;
  VEC1 = VECT R1 5.E-10 'FORC' 'ROSE' ;
  RES1 = MAXI 'ABS' (RESU (REDU R1 LIB)) ;
  LFOR = LFOR ET RES1 ;
* deformees
  DEF1 = DEFO S1 U1 1. VEC1 SP1 MO ;
* traces
  TIT1 = CHAI 'Contraintes au temps ' TPS1 ;
  SI ITRAC ;
    TRAC (DEF1 ET DEF0) 'TITR' TIT1 LISO ;
  FINSI ;
FIN B1 ;
* courbe force vs deplacement
EVFC = EVOL 'MANU' 'Deplacement' LDEP 'Force' LFOR ;
SI ITRAC ;
  DESS EVFC 'TITR' 'Force vs Deplacement' ;
FINSI ;


FIN ;


