* fichier :  elas9.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas9.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

*  POUR CALCUL COMPLET METTRE COMPLET = VRAI;
complet = VRAI;

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*___________________________________________________________________
*   TEST ELAS9                                                     *
*                                                                  *
*   MEMBRANE EPAISSE ELLIPTIQUE CHARGEE EN PRESSION                *
*                                                                  *
*   Test NAFEMS numero le10                                        *
*   Nom : THICK PLATE PRESSURE                                     *
*                                                                  *
*       La plaque epaisse est maillee a l'aide d'elements CU20.    *
*   Elle est chargee normalement par une pression de 1 MPa.        *
*   On s'interesse a la contrainte smyy au point D                 *
*                                                                  *
*       La valeur calculee de la contrainte est ensuite comparee   *
*   avec une valeur theorique obtenue avec un maillage plus fin.   *
*                                                                  *
*___________________________________________________________________
*
OPTI DIME 3;
OPTI ELEM CU20;
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
* Dimensions en metres.
*
*     Pour obtenir des arcs d'ellipse on projete des arcs de cercle
*  sur le plan xOy.
*
* Corfficient de finesse du maillage.
*
si complet;
K = 2;
sinon;
 k= 1;
finsi;
*
PB = 0. 2.75 0.;
PB1 = 0. 2.75 (((3.25 ** 2) - (2.75 ** 2)) ** 0.5);
PC = 3.25 0. 0.;
*
C1 = PC CERC (6 * K) (0. 0. 0.) PB1;
C2 = C1 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
PA = 0. 1. 0.;
PA1 = 0. 1. (((2. ** 2) - (1. ** 2)) ** 0.5);
PD = 2. 0. 0.;
*
C3 = Pd CERC (6 * K) (0. 0. 0.) PA1;
C4 = C3 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
D1 = PA DROI (2 * K) (0. 1.583 0.) DROI (2 * K) PB;
D3 = PC DROI (2 * K) (2.417 0. 0.) DROI (2 * K) PD;
*
ELIM (D1 ET C2 ET D3 ET C4) 0.0001;
SUR1 = DALL D1 C2 D3 C4 PLAN;
*
VOL1 = SUR1 VOLU (2 * K) TRAN (0. 0. -0.6);
*
* Surfaces et arc d'ellipse pour conditions aux limites
*
FACE1 = D1 TRAN (2 * K) (0. 0. -0.6);
FACE2 = C2 TRAN (2 * K) (0. 0. -0.6);
FACE3 = D3 TRAN (2 * K) (0. 0. -0.6);
C2CL  = C2 PLUS (0. 0. -0.3);
*
ELIM (VOL1 ET FACE1 ET FACE2 ET FACE3 ET C2CL) 0.0001;
*
SI (NEG GRAPH 'N');
  TITR 'ELAS9 : MAILLAGE';
  TRAC (-1000 -1000 1000) FACE QUAL (COUL VOL1 BLAN);
FINSI;
*
*==================================================================*
*          modele - materiau - caracteristique                     *
*           rigidite - conditions aux limites                      *
*==================================================================*
*
MODL1 = MODE VOL1 MECANIQUE ELASTIQUE ISOTROPE CU20;
MATR1 = MATE MODL1 YOUN 2.1E11 NU 0.3;
*
MOP  = 'MODE' SUR1 'CHARGEMENT' 'PRESSION' ;
MAP  = 'PRES' MOP 'PRES' 1.E6 ;
MODLT = MODL1 ;
*
RIG1 = RIGI MODLT MATR1 ;
*
*==================================================================*
*                  conditions aux limites                          *
*==================================================================*
*
*   - face dans le plan xOz : ux = 0.,
*   - face dans le plan yOz : uy = 0.;
*   - face elliptique exterieure : ux = uy = 0. et
*      uz = 0. le long de la surface moyenne.
*
CL1 = BLOQ UX FACE1;
CL2 = BLOQ UY FACE3;
CL3 = BLOQ UX FACE2;
CL4 = BLOQ UY FACE2;
CL5 = BLOQ UZ C2CL;
*
RIG1 = RIG1 ET CL1 ET CL2 ET CL3 ET CL4 ET CL5;
*
*==================================================================*
*                     chargement mecanique                         *
*==================================================================*
*
*    La plaque est soumis a une pression uniforme normal de 1 MPa
* sur la face superieure.
*
CHA1 = 'BSIG' MOP MAP ;
*
*==================================================================*
*                 calcul du champ de deplacement                   *
*==================================================================*
*
DEP1 = RESO RIG1 CHA1;
*
* Trace facultatif de la deformee.
*
SI (NEG GRAPH 'N');
   DEF0 = DEFO 0. DEP1 VOL1;
   DEF1 = DEFO 5.E2 DEP1 VOL1 VERT;
   TITR 'ELAS9 : DEFORMEE';
   TRAC (-1000 -1000 1000) CACH VOL1 (DEF0 ET DEF1);
FINSI;
*
*==================================================================*
*                  extraction d'une contrainte                     *
*            et comparaison avec solution analytique               *
*==================================================================*
*
*   On cherche la contrainte smyy au point D (2. 0. 0.).
*
*  Calcul du champs de contraintes.
*
CHAM1 = SIGM MODL1 MATR1 DEP1;
*
* Interpolation aux noeuds du maillage.
*
CHAM2 = CHAN NOEUD MODL1 CHAM1;
CHPO2 = CHAN CHPO  MODL1 CHAM2;

*
* Trace facultatif des contraintes aux noeuds.
*
SI (NEG GRAPH 'N');
   TITR 'ELAS9 : Contraintes Nodales';
   TRAC (-1000 -1000 1000) CACH VOL1 ((EXCO CHPO2 SMYY)*1e-6);
FINSI;
*
* Extraction de la contriante smyy au point D.
*
SMYYD = EXTR CHPO2 SMYY  PD;
SMYYD = SMYYD / 1.E6;
*
SMYYD1 = -5.38;
*
* Calcul de l'ecart avec la solution de reference.
*
ERG = 100 * (ABS ((SMYYD1 - SMYYD) / SMYYD1));
*
*==================================================================*
*                   affichage des resultats                        *
*==================================================================*
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
*
MESS '   Contrainte theorique SMYYD :' SMYYD1 'MPa';
SAUT 1 LIGN;
MESS '   Contrainte calculee SMYYD  :' SMYYD 'MPa';
SAUT 1 LIGN;
MESS '             Soit un ecart de :' ERG '%';
SAUT 1 LIGN;
*
*==================================================================*
*                      code fonctionnement                         *
*==================================================================*
*
si complet; ertes=3.; sinon; ertes=7. ; finsi;
SI (ERG <EG ertes);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
SAUT 1 LIGN;
  TEMPS;
SAUT 1 LIGN;
*
FIN;










