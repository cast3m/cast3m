*******************************************************
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Elastique
************************************************************************
*  Cas test de l'implantation numérique du modele
*  RICRAG 3D LOCAL/NON LOCAL
*******************************************************
* Développé par :
*     Maxime VASSAUX
*     Benjamin RICHARD
*
*******************************************************
* Les cas de charges sont entrés :
* - 1 : Traction monotone
* - 2 : Compression monotone
* - 3 : Traction cyclique
* - 4 : Compression cyclique
* - 5 : Traction/compression
*******************************************************
* Choix du cas de charge
ncas  = 1;
graph=mot 'N';
* Test du fichier compatible avec le non local
* nloc0 = 0;   Cas local
* nloc0 = 1;   Cas non local

nloc0 = 0;
*******************************************************

*
*-------------- Options de calcul ---------------------
*
OPTION DIME 3 ELEM CUB8;

*
*-------------- Definition de la geometrie ------------
*
dim1 = 1.0;
dim2 = 1.0;
dim3 = 1.0;
P1 = 0.   0.   0.;
P2 = dim1 0.   0.;
P3 = dim1 dim2 0.;
P4 = 0.   dim2 0.;
P5 = 0.   0.   dim3;
P6 = dim1 0.   dim3;
P7 = dim1 dim2 dim3;
P8 = 0.   dim2 dim3;

nelem1 = 1;
nelem2 = 1;
nelem3 = 1;

L1 = P1 DROIT nelem1 P2 ;
L2 = P2 DROIT nelem2 P3 ;
L3 = P3 DROIT nelem1 P4 ;
L4 = P4 DROIT nelem2 P1 ;

L5 = P5 DROIT nelem1 P6 ;
L6 = P6 DROIT nelem2 P7 ;
L7 = P7 DROIT nelem1 P8 ;
L8 = P8 DROIT nelem2 P5 ;

L9  = P1 DROIT nelem3 P5 ;
L10 = P2 DROIT nelem3 P6 ;
L11 = P3 DROIT nelem3 P7 ;
L12 = P4 DROIT nelem3 P8 ;

LTOT1 = L1 ET L2 ET L3 ET L4 ;
SURF1 = SURF LTOT1 PLANE;

LTOT2 = L5 ET L6 ET L7 ET L8 ;
SURF2 = SURF LTOT2 PLANE;

LTOT3 = L1 ET L5 ET L10 ET L9 ;
SURF3 = SURF LTOT3 PLANE;

LTOT4 = L2 ET L11 ET L6 ET L10 ;
SURF4 = SURF LTOT4 PLANE;

LTOT5 = L3 ET L12 ET L7 ET L11 ;
SURF5 = SURF LTOT5 PLANE;

LTOT6 = L4 ET L9 ET L8 ET L12 ;
SURF6 = SURF LTOT6 PLANE coul 'ROUG';

STOT = SURF1 et SURF2 et SURF3 et SURF4 et SURF5 et SURF6;

VOLTO = surf1 volu nelem3 tran (P5);

VOLTOT = VOLTO;

elim stot voltot 0.0001;

*
*------- Définition des conditions aux limites --------
*----------- et des déplacements imposés --------------
*
CL1 = BLOQ 'UZ'  SURF1;
CL3 = BLOQ 'UY'  SURF3;
CL6 = BLOQ 'UX'  SURF6;

BCL = CL1 ET CL3 ET CL6;
CL2 = BLOQ SURF2 UZ;

D1 = DEPI CL2 1;

*
*----- Définition du modèle ---------------------------
*
SI (EGA nloc0 1);
MOD1   = MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE
                               ENDOMMAGEMENT CONCYC
      'NON_LOCAL' 'MOYE' 'V_MOYENNE' (MOTS 'EPTI') ;
SINON ;
MOD1   = MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE
                               ENDOMMAGEMENT CONCYC;
FINSI;
*
*----- Paramètres matériaux ---------------------------
*
* Module de Young
    youngn = 30.0E9;

* Coefficient de Poisson
    nun    = 0.2;

* Choix du modèle d'endommagement pour 'CONCYC' : Ricrag (1) - consolidation modifiee (2) - critere modifie (3)
    nendn  = 2;

* Contrainte limite elastique
    sigtn  = 3.00E6;

* Parametre controlant l'energie de fissuration
    atran  = 0.004;

* RicRag classique *
    SI (EGA nendn 1);
        btran = 0.0;
    FINSI;

* RicRag avec consolidation modifiee *
    SI (EGA nendn 2);
        btran = 4.5;
    FINSI;

* RicRag avec critere modifie *
    SI (EGA nendn 3);
        btran = 0.31;
    FINSI;

* "Vitesse" de refermeture de fissure
    qpn = 6.5;

* Coefficient de frottement des fissures
    cfn = 2.89;

    MAT1   = MATE MOD1 'YOUN' youngn  'NU  ' nun
                       'NEND' nendn   'SIGT' sigtn
                       'ATRA' atran   'BTRA' btran
                       'QP  ' qpn     'CF  ' cfn ;

*
*----- Définition des cas de charge -------------------
*

SI (EGA ncas 1) ;
LI1  = PROG 0. 1.;
LI2  = PROG 0. 4.0e-4;
LIS1 = PROG 0. PAS 0.0025 1.;
FINSI;

SI (EGA ncas 2) ;
LI1  = PROG 0.  1.;
LI2  = PROG 0. -8.0e-3;
LIS1 = PROG 0. PAS 0.0025 1.;
FINSI;

SI (EGA ncas 3) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 1.5e-4 -2.0e-4 2.0E-4 -2.0e-4 2.5E-4;
LIS1 = PROG 0. PAS 0.0025 5.;
FINSI;

SI (EGA ncas 4) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. -3.0e-3 -1.5e-4 -5.0E-3 -3.5E-4 -8.0E-3;
LIS1 = PROG 0. PAS 0.0025 5.;
FINSI;

SI (EGA ncas 5) ;
LI1  = PROG 0. 1. 2.;
LI2  = PROG 0. 2.0e-4 -4.0e-3;
LIS1 = PROG 0. PAS 0.0025 2;
FINSI;

EV = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;

*
*----------- Calcul par l'operateur PASAPAS ------------
*
LC = 1.0e-10;
CO1 = CONNEC mod1 LC NORMAL;

TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = BCL ET CL2;
TAB1.'MODELE' = MOD1;
TAB1.'MOVA' = 'D   ';
TAB1.'CHARGEMENT' = CHA1;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'MAXITERATION' = 10;

SI (EGA nloc0 1);
TAB1.CONN = CO1;
MAT1 = MAT1 ET ( MATE MOD1 'LCAR' LC) ;
FINSI;

TAB1.'CARACTERISTIQUES' = MAT1;
PASAPAS TAB1 ;

*
*----------- Courbe effort-deplacement -----------------
*
ev2=@global tab1 CL2 EV fz;
ee = extr ev2 ordo 1;
  aa = extr ee ( dime ee);
  list aa;
  err =  abs (((aa)  - (5.14929E+05)) / (5.14929E+05))  ;
  message ' erreur relative ' err;
  si (err > 1.e-3);
    erreur (5);
  finsi;

si ( ega graph 'O');
  DESS EV2;
finsi;
  list ev2;
fin;












