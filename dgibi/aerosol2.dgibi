* fichier :  aerosol2.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************

GRAPH = FAUX ;
*************************************************************************
*                          AEROSOL2.DGIBI                               *
*************************************************************************
*                                                                       *
*    NATURE DU PROBLEME   :  TRANSPORT DE PARTICULES AVEC DEPOT         *
*                            CONVECTION FORCEE                          *
*                            TRANSITOIRE SIMULTANE SUR LES PARTICULES   *
*                                                      ET L'ECOULEMENT  *
*                            TURBULENT                                  *
*                                                                       *
*    OPERATEURS           :  NSKE , FPU , PRESSION , VNIMP              *
*                            TSCAL , FPA                                *
*                                                                       *
*    GEOMETRIE            :  TUYAU CYLINDRIQUE VERTICAL 3D              *
*                                                                       *
*P.CORNET-SEMT/TTMF-DECEMBRE 1997                                       *
*************************************************************************
*
*      Nombre de Reynolds Re=9894 et diametre du tube D=1.27 cm
*      Longueur du tube = 5 cm
*      Vitesse d'entree = 11.7 m/s
*      Viscosite de l'air = 1.5E-5  m2/s
*      Diametre des particules = 7.2E-6 m
*      epaisseur initiale de la couche limite = 1.E-3 m
*
*---------------------- CREATION DU MAILLAGE DE BASE 2D ------------------------

OPTION DIME 2 ELEM QUA4 ;

* Maillage du carre (P1 P2 P3 P4) de depart : (Rayon du tube=0.38E-2)
P0  =  0. 0. ;
PA1 =  0.38E-2  0.38E-2 ;
PA2 = -0.38E-2  0.38E-2 ;
PA3 = -0.38E-2 -0.38E-2 ;
PA4 =  0.38E-2 -0.38E-2 ;


D1=DROIT PA1 10 PA2;
D2=DROIT PA2 10 PA3;
D3=DROIT PA3 10 PA4;
D4=DROIT PA4 10 PA1;

CARREBAS = DALLER D1 D2 D3 D4;

GEO = MANU TRI3 (NOEUD 3) (NOEUD 15) (NOEUD 50);
GEO = GEO ET (MANU TRI3 (NOEUD 3) (NOEUD 50) (NOEUD 14));
GEO = GEO ET (MANU TRI3 (NOEUD 23) (NOEUD 4) (NOEUD 122));
GEO = GEO ET (MANU TRI3 (NOEUD 4) (NOEUD 24) (NOEUD 122));
GEO = GEO ET (MANU TRI3 (NOEUD 114) (NOEUD 32) (NOEUD 5));
GEO = GEO ET (MANU TRI3 (NOEUD 114) (NOEUD 5) (NOEUD 33));
GEO = GEO ET (MANU TRI3 (NOEUD 6) (NOEUD 42) (NOEUD 2));
GEO = GEO ET (MANU TRI3 (NOEUD 42) (NOEUD 41) (NOEUD 2));

GEO1 = CARREBAS ELEM APPUYE LARGEMENT PA1;
GEO1 = (GEO1 ET (CARREBAS ELEM APPUYE LARGEMENT PA2));
GEO1 = (GEO1 ET (CARREBAS ELEM APPUYE LARGEMENT PA3));
GEO1 = (GEO1 ET (CARREBAS ELEM APPUYE LARGEMENT PA4));
GEO1 = CARREBAS DIFF GEO1;

GEO = GEO ET GEO1 ;

* Definition du cercle circonscrit au carre :
ARC1 = CERCLE 10 PA1 P0 PA2;
ARC2 = CERCLE 10 PA2 P0 PA3;
ARC3 = CERCLE 10 PA3 P0 PA4;
ARC4 = CERCLE 10 PA4 P0 PA1;

* Deformation maillage carre -> maillage cercle :
BAS=GEO DEDU (GEO CONT) (ARC1 ET ARC2 ET ARC3 ET ARC4);

* Definition du contour de la base du tuyau :
PERIB = ARC1 ET ARC2 ET ARC3 ET ARC4;

*----------------------  MAILLAGE 3D  ---------------------------------------

OPTI DIME 3 ELEM PRI6 ;
*CUB8;
V=0. 0. 5.E-2;
NH=5 ;

PERIH = PERIB PLUS V;
BORDOM = PERIB REGL NH PERIH;
NBD=NBEL(BORDOM) ;
NBD1=NBD / NH + 1 ;
BORDOK=ELEM BORDOM (LECT NBD1 PAS 1 NBD) ;
HAUT = BAS PLUS V;

DOMTOT=VOLU TRAN BAS V NH;

MDOMTOT = CHAN DOMTOT QUAF ;
MBORDOM = CHAN BORDOM QUAF ;
MBORDOK = CHAN BORDOK QUAF ;
MHAUT   = CHAN HAUT   QUAF ;
MBAS    = CHAN BAS    QUAF ;

ELIM (MBORDOM ET MHAUT ET MBAS ET MDOMTOT ET MBORDOK) 1.E-5;

*---------------  CREATION DES TABLES DOMAINE -----------------------------------

$DOMTOT = MODE MDOMTOT 'NAVIER_STOKES' LINE ;
DOMA $DOMTOT 'IMPR' ;
$BORDOM = MODE MBORDOM 'NAVIER_STOKES' LINE ;
$BORDOK = MODE MBORDOK 'NAVIER_STOKES' LINE ;
$HAUT   = MODE MHAUT 'NAVIER_STOKES' LINE ;
$BAS    = MODE MBAS 'NAVIER_STOKES' LINE ;
MT = DOMA $DOMTOT maillage ;

*-------------- PARAMETRES DU PROBLEME : AIR ET PARTICULES --------------------

NU  = 1.5E-5            ;
U0  = 11.7              ;
K0  = (U0/1000.)        ;
E0  = K0**(1.5)/0.6e-2  ;
UET0 = 0.75             ;
YP  = 1.E-3             ;

RAP = 3.6E-6                                          ;
ROP = 920.                                            ;
ROG = 0. 0. (-9.81*ROP)                               ;
C0  = 1.E0                                            ;
VS0  = 12277.*RAP*RAP*9.81*ROP                        ;
DIF0 = 1./(5.62E21*NU*RAP)                            ;
VAR = EXP(1.667E7*RAP)                                   ;
VS0   = VS0*(1. + (8.296E-8/RAP) + (2.64E-8/(RAP*VAR)))  ;
DIF0 = DIF0*(1. + (8.296E-8/RAP) + (2.64E-8/(RAP*VAR)))  ;                                             ;

*--------------------- INITIALISATIONS --------------------------------------

UET= kcht $BORDOK 'SCAL' 'CENTRE' UET0 ;
AK = KCHT $BORDOK 'SCAL' 'CENTRE' 1.E-3 ;

CHPC = DOMA $DOMTOT 'NORMALE' ;
NORM = KCHT $BORDOK VECT CENTRE CHPC  ;

*--------------------- VITESSE DE SEDIMENTATION -------------------------

VSF = kcht $DOMTOT 'VECT' 'SOMMET' ( 0. 0. VS0 ) ;
VSF = CHAN 'ATTRIBUT' VSF 'NATURE' 'DISCRET' ;
VS0 = 0. - VS0 ;
VSP = kcht $BORDOM 'VECT' 'SOMMET' ( 0. 0. VS0 ) ;
VSP = CHAN 'ATTRIBUT' VSP 'NATURE' 'DISCRET' ;
VS = kcht $DOMTOT  'VECT' 'SOMMET' (VSF et VSP) ;

*-------------- DEFINITION DES TABLES DE RESOLUTION ----------------------

CONC=TABLE 'ARG';

rv = eqex $DOMTOT 'ITMA'  20 'ALFA' 0.8
 'ZONE' $BORDOK 'OPER' 'FPU'  NU UET YP 'INCO' 'UN' 'KN' 'EN'
 'ZONE' $DOMTOT 'OPER' 'NSKE' NU NUT    'INCO' 'UN' 'KN' 'EN' ;
rv = eqex rv
  'ZONE' $BORDOK 'OPER' 'FPA' NU YP UET NORM  AK  ROG RAP 'INCO' 'CN'
  'ZONE' $DOMTOT 'OPER' 'TSCAL' NUT  'UN' 0.              'INCO' 'CN'
;

rv = eqex rv OPTI EFM1 'CENTREE'
  'ZONE' $DOMTOT  'OPER' 'DFDT' 1. 'UN' 'DELTAT' 'INCO' 'UN'
  'ZONE' $DOMTOT  'OPER' 'DFDT' 1. 'KN' 'DELTAT' 'INCO' 'KN'
  'ZONE' $DOMTOT  'OPER' 'DFDT' 1. 'EN' 'DELTAT' 'INCO' 'EN'
  'ZONE' $DOMTOT  'OPER' 'DFDT' 1. 'CN' 'DELTAT' 'INCO' 'CN'
;

rv = eqex rv
 'CLIM' 'KN' 'TIMP' BAS K0 'EN' 'TIMP' BAS E0
        'CN' 'TIMP' BAS C0 ;

rvp= eqpr $DOMTOT
 'ZONE' $DOMTOT   'OPER' 'PRESSION' 0.
 'ZONE' $BORDOM   'OPER' 'VNIMP' 0.
 'ZONE' $BAS      'OPER' 'VNIMP' U0 ;
**** LA CONDITION VTIMP=0 EST CONTENUE DANS FPU


rv.'PRESSION'=rvp ;
RV.'SEDIM'=VS;
RV.'COEF'=DIF0;

rv.inco=table 'INCO' ;
rv.inco.'UN'  = kcht $DOMTOT 'VECT' 'SOMMET' (1.E-5 1.E-5 1.E+1) ;
rv.inco.'KN'  = kcht $DOMTOT 'SCAL' 'SOMMET' K0 ;
rv.inco.'EN'  = kcht $DOMTOT 'SCAL' 'SOMMET' E0 ;
rv.inco.'NUT' = kcht $DOMTOT 'SCAL' 'CENTRE' 1.E-10 ;
rv.inco.'CN'  = kcht $DOMTOT 'SCAL' 'SOMMET' 0. ;


rv.co=CONC;
rv.co.vitesse=kops RV.'INCO'.'UN' '-' VS;
  KN = RV.inco.'KN' ;
  EN = RV.inco.'EN' ;
  KN = kops (kops KN '*' KN) '/' EN ;
  DIF = kops (kops KN '*' 0.09) '+' rv.'COEF' ;
rv.co.diffu=(NOEL $domtot DIF) ;
rv.co.tempera=RV.inco.'CN';


* Historiques :

PH1 = MT POIN 'PROC' (0. 0.      0.01) ;
PH2 = MT POIN 'PROC' (0. 0.38E-2 0.01) ;
PH3 = MT POIN 'PROC' (0. 0.19    0.01) ;
PH4 = MT POIN 'PROC' (0. 0.      0.05) ;
PH5 = MT POIN 'PROC' (0. 0.38E-2 0.05) ;
PH6 = MT POIN 'PROC' (0. 0.19    0.05) ;

LH = PH1 et PH2 et PH3 et PH4 et PH5 et PH6 ;

  his=khis 'UN' 1 lh  'UN' 2 lh  'UN' 3 lh
                 'KN'   lh  'EN'   lh  'CN'   lh  ;
  rv.'HIST'=his ;

*----------------------- EXECUTION : ECOULEMENT + CONCENTRATION ------------

EXEC rv ;

*------------------------ POSTRAITEMENT ------------------------------------
SI GRAPH ;

  titre 'BASE DU MAILLAGE' ;
  trac BAS ;
  titre 'MAILLAGE' ;
  trac mt ;

  UNCH = VECT RV.INCO.UN 0.0005 UX UY UZ VERT;
  TITRE 'VITESSES' ;
  TRAC UNCH BORDOM;
    TITRE 'ENERGIE CINETIQUE TURBULENTE' ;
    TRAC RV.INCO.'KN' BORDOM;
  TITRE 'DISSIPATION DE K' ;
  TRAC RV.INCO.'EN' BORDOM;
    TITRE 'CONCENTRATION' ;
    TRAC RV.INCO.'CN' BORDOM;

*   HISTORIQUES

  DESS (HIS.'TABD') (HIS.'1UN') 'LEGE' ;
  DESS (HIS.'TABD') (HIS.'2UN') 'LEGE' ;
  DESS (HIS.'TABD') (HIS.'3UN') 'LEGE' ;
  DESS (HIS.'TABD') (HIS.'KN')  'LEGE' ;
  DESS (HIS.'TABD') (HIS.'EN')  'LEGE' ;
  DESS (HIS.'TABD') (HIS.'CN')  'LEGE' ;

FINSI ;

FIN ;















