* fichier :  simtrc.dgibi
************************************************************************
* Section : Thermique Metallurgie
************************************************************************
SAUT PAGE ;
complet = faux ;
GRAPH = 'N' ;
*********************************************************************
*                   simtrc.dgibi                                    *
*          Simulation du trc avec l'operateur comp                  *
*          Prise en compte de la concentration en carbone (cte)     *
*          version initiale : Martinez le 30/07/98                  *
*********************************************************************
* mettre complet = vrai pour generer le trc du 16MND5
**** Parametres du calcul :
**** Vitesse de refroidissement
*
lis_ref1 = prog -0.05 -0.1 -0.15 -0.2 -0.3 -0.5 -1. -2. -3. -4. -5.
                -6. -7. -8. -9. -10. -12. -15. -18. -20. -25. -30.
                -40. -50. -60. -80. -100. -400. ;
lis_ref1 = prog -0.05 -0.1 -0.3 -0.4 -0.5 -1. -2. -3. -4. -5.  -7. -10.
                -15. -25. -35. -40. -60. -80. -150. ;
lis_ref2 = prog -0.07 -0.11 -0.18 -0.23 -0.4 -0.57 -1.3 -2.8 -3.1
                 -4.2 -5.5  -6.4 -7.3 -8.7 -9.9 -10.01 -12.3 -15.5
              -19. -23. -26. -34.  -45. -57. -61. -88. -300. -390. ;
lis_ref = lis_ref1 ;
*
**** Delta Temperature
d_tempe = 2. ;
*
******** INITIALISATION DU DEPOUILLEMENT ***************************
*
dfer = 100. ;
dbai = 100. ;
dmar = 100. ;
ffer = 100. ;
fbai = 100. ;
fmar = 100. ;
tdf = 1. ;  tff = 1. ;
tdb = 1. ;  tfb = 1. ;
tdm = 1. ;  tfm = 1. ;
lis_dfer = prog ;
lis_dbai = prog ;
lis_dmar = prog ;
lis_tdf = prog ;
lis_tdb = prog ;
lis_tdm = prog ;
lis_ffer = prog ;
lis_fbai = prog ;
lis_tff = prog ;
lis_tfb = prog ;
lis_refr = prog ;
lis_pfer = prog ;
lis_pbai = prog ;
lis_pmar = prog ;
*
**** LECTURE DES DONNEES DU DIAGRAMME TRC **************************
*
DTRC_1 = nuage 'TP'*flottant 'ZFF'*flottant 'ZFB'*flottant
      'TDF'*flottant 'TFF'*flottant 'TDB'*flottant 'TFB'*flottant
 -0.05   0.67   0.33   769.    644.    548.    395.
-0.1  0.45  0.55  752.  644.0  566.0   395.0
-0.3  0.16  0.84  714.0 639.0  588.0   395.0
-0.4  0.06  0.94  700.0 659.0  591.0   395.0
-0.5  0.    1.    700.0 690.0  593.0   395.0
-1.0  0.    0.96  700.0 690.   600.0   395.0
-2.0  0.    0.91  700.0 690.0  588.0   395.0 ;
DTRC_1 = DTRC_1 et (nuage 'TP'*flottant 'ZFF'*flottant 'ZFB'*flottant
   'TDF'*flottant 'TFF'*flottant 'TDB'*flottant 'TFB'*flottant
-3.0  0.    0.87  700.0 390.0  581.0   396.0
-4.0  0.0   0.69  700.0 690.0  573.0   396.0
-5.0  0.0   0.55  700.0 690.0  568.0   398.0
-7.0  0.0   0.46  700.0 690.0  559.0   400.0
-10.0 0.0   0.34  700.0 690.0  543.0   402.0
-15.0 0.0   0.22  700.0 690.0  525.0   405.0
-25.0 0.0   0.05  700.0 690.0  498.0   415.0) ;
DTRC_1 = DTRC_1 et (nuage 'TP'*flottant 'ZFF'*flottant 'ZFB'*flottant
   'TDF'*flottant 'TFF'*flottant 'TDB'*flottant 'TFB'*flottant
-35.0 0.0   0.    710.0 700.0  430.0   425.0
-40.0 0.0   0.    710.0 700.0  430.0   425.0
-60.0 0.0   0.    710.0 700.0  430.0   425.0
-80.0 0.0   0.0   710.0 700.0  430.0   425.0
-150.0 0.0  0.0   710.0 700.0  430.0   425.0 );
*
*  Chauffage (modele de leblond)
*
DTCH_1  = nuage
 'COMP' 'TE' 730.0 750.0 770.0 790.0 810.0 830.0 840.0 860.0
 880.0 900.0
 'COMP' 'CK' 0.0 0.22 0.53 1.05 2.02 4.55 5.6 7.37 10.77 20.0
 'COMP' 'CL' 1.0 0.97 0.94 0.87 0.76 0.45 0.0 0.0  0.0   0.0 ;

**** LECTURE DU MAILLAGE *******************************************
*
opti dime 2 elem qua8 mode axis ;
elmat = table ;
piece = table ;
a00 = 0. 0. ;
a01 = 0.01 0. ;
a02 = 0.01 0.01 ;
a03 = 0. 0.01 ;
d1 =  droi 1 a00 a01 ;
d2 =  droi 1 a01 a02 ;
d3 =  droi 1 a02 a03 ;
d4 =  droi 1 a03 a00 ;
l_ext   = (d1 et d2 et d3) ;
tot = dalle d1 d2 d3 d4 plan ;
*
******** DEFINITION D'UN MODELE *************************************
*
p_mode1 = mode tot 'THERMIQUE' 'ISOTROPE' 'CONS' me1 ;
si faux ;
p_mode2 = mode tot 'TAILGRAI' 'ISOTROPE' 'CONS' me2 ;
p_mode3 = mode tot 'DIFFUSION' 'CONS' carb1 ;
finsi ;

momic_1 = mode tot melange cerem ;
mamic_1 = mate momic_1
* changement de type de certaines donnees
 'AC1' 730. 'AR1' 760. 'MS0' 422.
'BETA' -0.0307 'AC' 9146. 'AA' 37.5 'ZS' 0. 'TPLM' -0.5 'CARB' 0.0074
'ACAR' 540. 'DG0' 0.00001 'AGRA' 11200.
 'TIHT' 1000. 'TFHT' 200. 'DTHT' 2. 'NHTR' DTRC_1  'NLEB'  DTCH_1
 'AUST' 1. 'FERR' 0. 'BAIN' 0. 'MART' 0. 'MS' 400. ;
*********************************************************************
******** BOUCLE SUR LES HISTOIRES THERMIQUES ************************
*********************************************************************
*
n_bou1 = dime lis_ref ;
repeter bou_1 n_bou1 ;
*
******** CHARGEMENT THERMIQUE **************************************
*
  t0tempe = 1000.0 ;
  t1tempe = 100.0 ;
*
  t_chtem  = manu CHPO tot 1 'T' 1. ;
  t0chtem  = t0tempe * t_chtem ;
*
  app_fer = 0 ;
  app_bai = 0 ;
  app_mar = 0 ;
*
******** INITIALISATION  *********************************
*
si faux ;
  chcarb = manu 'CHML' p_mode3 'ACC' 0.0074 'TYPE' 'ACTIVITE' ;
  chdg = manu 'CHML' p_mode2 'DG' 10.e-6 'TYPE' 'GRAIN' ;
finsi ;
  p_table                = table ;
  p_table.'TEMPERATURES' = table ;
  p_table.'PHASES'       = table ;
  p_table.'TRC'          = table ;
*
  p_table.TEMPERATURES.0      = t0chtem ;
  p_table.PHASES.0            = mamic_1 ;
*
  lis_tps=prog ;
  lis_te =prog ;
  lis_pa =prog ;
  lis_pf =prog ;
  lis_pb =prog ;
  lis_pm =prog ;
*
*
*********************************************************************
******** BOUCLE SUR LE TEMPS ****************************************
*********************************************************************
*
  t_point = extr lis_ref &bou_1 ;
*
  t0temps = 0. ;
  t1temps = (t1tempe - t0tempe) / t_point  ;
  atpnt  = ABS t_point ;
  deltt  = d_tempe / atpnt ;
  n_bou2   = enti((t1temps - t0temps) / deltt) + 1 ;
*
  t_ps = t0temps - deltt ;
*
mess '=============================================================' ;
mess '=============================================================' ;
mess ' Vitesse de refroidissement : ' t_point ;
*
  repeter bou_2 n_bou2 ;
*
   chdtps1 = manu chml momic_1 'DTPS' deltt ;
 chtemp0 = manu chml tot temp t_ps ;
 chtemp1 = manu chml tot temp (t_ps + deltt) ;
     t_ps = t_ps + deltt ;
     p_table.temperatures.&bou_2 = p_table.temperatures.(&bou_2-1) +
             (t_chtem * t_point * deltt) ;
     n1 = (dime p_table.temperatures) - 1 ;
     si (n1 > 0) ;
       cot1 = p_table.temperatures.&bou_2 ;
       cot0 = p_table.temperatures.(&bou_2-1) ;
       dt1  = cot1 - cot0 ;
       dtp1 = dt1 / deltt ;
       max_dt = maxi dt1 ;
       cht1 = chan 'CHAM' cot1 tot ;
       cht0 = chan 'CHAM' cot0 tot ;
       chp0 = p_table.'PHASES'.(&bou_2-1) ;
       chtp1 = chan cham dtp1 tot ;
       htab1 = p_table.'TRC' ;

   cho1 = comp momic_1 (cht0 et chtemp0 et chp0 et chdtps1)
      (cht1 et chtemp1) ;

 lma_1 = extr mamic_1 comp ;
  chp1 = exco cho1 lma_1 ;

       p_table.'PHASES'.&bou_2 = chp1 ;
     finsi ;
*=====================================================================
**** verification des temperatures de debut et de fin de chgt de phase
*=====================================================================
*
    p_fe0 = maxi (exco 'FERR' chp0) ;
    p_ba0 = maxi (exco 'BAIN' chp0) ;
    p_au  = maxi (exco 'AUST' chp1) ;
    p_fe  = maxi (exco 'FERR' chp1) ;
    p_ba  = maxi (exco 'BAIN' chp1) ;
    p_ma  = maxi (exco 'MART' chp1) ;
    t_app = maxi (exco T cot1) ;
    tr_ma = maxi (exco chp1 MS) ;
*
    lis_tps= lis_tps et (prog t_ps) ;
    lis_te = lis_te et (prog t_app) ;
    lis_pa = lis_pa et (prog p_au)  ;
    lis_pf = lis_pf et (prog p_fe)  ;
    lis_pb = lis_pb et (prog p_ba)  ;
    lis_pm = lis_pm et (prog p_ma)  ;
*
**** Temperatures d'apparition :ferrite, bainite et martensite
*
     si (p_fe > 0.001) ;
      si (app_fer EGA 0) ;
       app_fer = 1 ;
       t_app = maxi (exco T cot1) ;
       dfer = t_app ;
       tdf = t_ps ;
      finsi ;
     finsi ;
     si (p_ba > 0.001) ;
      si (app_bai EGA 0) ;
       app_bai = 1 ;
       t_app = maxi (exco T cot1) ;
       dbai = t_app ;
       tdb = t_ps ;
      finsi ;
     finsi ;
     si (p_ma > 0.001) ;
      si (app_mar EGA 0) ;
       app_mar = 1 ;
       t_app = maxi (exco T cot1) ;
       dmar = t_app ;
       tdm = t_ps ;
      finsi ;
     finsi ;
*
**** Temperatures de fin de transformation de ferrite et bainite
*
     si (app_fer EGA 1) ;
        d_fe = p_fe - p_fe0 ;
        si (d_fe < 0.000001) ;
         app_fer = 2 ;
         t_app = maxi (exco T cot1) ;
         ffer = t_app ;
         tff  = t_ps ;
        finsi ;
       finsi ;
       si (app_bai EGA 1) ;
        d_ba = p_ba - p_ba0 ;
        si (d_ba < 0.000001) ;
         app_bai = 2 ;
         t_app = maxi (exco T cot1) ;
         fbai = t_app ;
         tfb  = t_ps ;
        finsi ;
      finsi ;
*
si ((non complet) et (&bou_2 > 150)) ;
 quitter bou_2 ;
finsi ;
  fin bou_2 ;
*
  lis_dfer = lis_dfer et (prog dfer) ;
  lis_dbai = lis_dbai et (prog dbai) ;
  lis_dmar = lis_dmar et (prog dmar) ;
  lis_tdf = lis_tdf et (prog tdf) ;
  lis_tdb = lis_tdb et (prog tdb) ;
  lis_tdm = lis_tdm et (prog tdm) ;
*
  lis_ffer = lis_ffer et (prog ffer) ;
  lis_fbai = lis_fbai et (prog fbai) ;
  lis_tff = lis_tff et (prog tff) ;
  lis_tfb = lis_tfb et (prog tfb) ;
*
**** presentation des resultats ***************************************
*
mess 'ferrite' dfer ffer ;
mess 'bainite' dbai fbai ;
 mess '==========================================================' ;
*
*
  n_f = (dime p_table.PHASES) - 1 ;
  ch4 = p_table.PHASES.n_f ;
**** austenite
  p_au = maxi (exco 'AUST' ch4) ;
**** ferrite
  p_fe = maxi (exco 'FERR' ch4) ;
**** bainite
  p_ba = maxi (exco 'BAIN' ch4) ;
**** martensite
  p_ma = maxi (exco 'MART' ch4) ;
**** Verification des proportions *****************************
  p_tot = p_au + p_fe + p_ba + p_ma ;
*
mess ' INTG des fractions volumiques : ' p_tot ;
mess '===============================================================';
mess 'Tpoint     %austenite     %ferrite     %bainite     %martensite';
mess t_point p_au p_fe p_ba p_ma ;
*
si (non complet) ;
 quitter bou_1 ;
finsi ;
fin bou_1 ;
*
************************************************************************
******** TRACE DU TRC **************************************************
************************************************************************
*
ev_dfer = evol bleu manu TPS lis_tdf T lis_dfer ;
ev_dbai = evol vert manu TPS lis_tdb T lis_dbai ;
ev_dmar = evol  manu TPS lis_tdm T lis_dmar ;
ev_ffer = evol bleu manu TPS lis_tff T lis_ffer ;
ev_fbai = evol vert manu TPS lis_tfb T lis_fbai ;
*
tabdess = table;
tabdess . 1 = 'MARQ TRIA NOLI' ;
tabdess . 2 = 'MARQ LOSA NOLI' ;
tabdess . 3 = 'MARQ CARR NOLI' ;
tabdess . 4 = 'MARQ TRIA NOLI' ;
tabdess . 5 = 'MARQ LOSA NOLI' ;
tabdess . 'TITRE' = table ;
tabdess . 'TITRE' . 1 = MOT 'ferrite' ;
tabdess . 'TITRE' . 2 = MOT 'bainite' ;
tabdess . 'TITRE' . 3 = MOT 'martensite' ;
tabdess . 'TITRE' . 4 = MOT 'ferrite' ;
tabdess . 'TITRE' . 5 = MOT 'bainite' ;
*
'SI' ('EGA' GRAPH 'O') ;
opti trac psc ;
dess (ev_dfer  et ev_dbai et  ev_dmar et ev_ffer et  ev_fbai) 'LOGX'
     LEGE TITX 'temps' TITY 'temperature' tabdess ;
'FINSI' ;
*
*
si (non complet) ;
p_au0 = .65196 ;

RESI=ABS((p_au-p_au0)/p_au0);
SI(RESI <EG 1E-2);
    ERRE 0;
SINO;
    ERRE 5;
FINSI;
finsi ;

si complet ;
opti sauv 'simtrc.k2000' ;
sauv ;
finsi ;
fin;


