* fichier :  thpl5.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
complet = faux ;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
* CAS TEST DU 92/03/20     PROVENANCE : TC1
* CAS TEST DU 92/03/19     PROVENANCE : PHIL
OPTI ECHO 0;
*
MESS '*********************************************************';
MESS '* COMMISSION V.P.C.S.  : THERMO-PLASTICITE              *';
MESS '*                                                       *';
MESS '*CYLINDRE SOUMIS A UNE REPARTITION LOGARITHMIQUE        *';
MESS '* DE TEMPERATURE.                                       *';
MESS '*CALCUL EN DEFORMATIONS PLANES                          *';
MESS '*                                                       *';
MESS '*FONCTIONS TESTEES : ELASTO-PLASTICITE AVEC ECROUISSAGE *';
MESS '*  SOUS CHARGEMENT THERMIQUE.                           *';
MESS '*                                                       *';
MESS '*********************************************************';
*
OPTI DIME 2;
OPTI MODE PLAN DEFO;
OPTI ELEM QUA8;
OPTI TRAC BENS;
GRAPH='N';

*++++++++++++++++++++++++++++++++*
*    GEOMETRIE DU CYLINDRE       *
*++++++++++++++++++++++++++++++++*
*
DENS 1. ; XA = 1. ; YA = 0. ; PA = XA YA;
DENS 1.2; XB = 2. ; YB = 0. ; PB = XB YB;
DENS 1. ; XC = 0. ; YC = 1. ; PC = XC YC;
DENS 1.2; XD = 0. ; YD = 2. ; PD = XD YD;
DENS 1. ; XO = 0. ; YO = 0. ; PO = XO YO ;
DENS 1.1; pz = 0. 1.497; pm = 1.497 0. ;


DAm = PA droi -7 pm;
DmB = Pm DROI -3 PB;
DDz = PD DROI -3 Pz;
DzC = Pz DROI -7 pc;
DAB = DAm et DmB;
DDC = DDz et DzC;
*
si complet;
N2 = 20;
sinon;
n2 = 10;
finsi;
SUR1 = DAB ROTA N2 90 PO;

*
NOSUR1 = NBNO SUR1;
ELSUR1 = NBEL SUR1;
*
ELIM (SUR1 ET DAB ET DDC) 0.0001;
*
SI(NEG GRAPH 'N');
  TITR ' SSNP05 : MAILLAGE PAR QUA8 (NOEUDS)';
  TRACE NOEUD SUR1;
  TITR ' SSNP05 : MAILLAGE PAR QUA8 (ELEMENTS)';
  TRACE ELEM SUR1;
FINSI;
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  CONDITIONS AUX LIMITES                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = SYMT DEPL ROTA PA PB SUR1 0.0001;
CL2 = SYMT DEPL ROTA PC PD SUR1 0.0001;
CL = CL1 ET CL2 ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*         MODELES - MATERIAUX - CARACTERISTIQUES
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MO = MODE sur1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'PARFAIT' QUA8;

MAT = MATE MO YOUN 200000 NU 0.3 ALPHA 12E-6 SIGY 300;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*         CHAMPS RELATIFS A LA PROCEDURE                       *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*  CHAMPS DE TEMPERATURE REPARTIE SUR SUR1 AUX DIFFERENTS
*     INSTANTS DE CHARGEMENT
*        10 PAS DE CHARGEMENT ENTRE TE = 0C ET TE = 200C
*         (TE = TEMPERATURE EXTERIEURE).
*
*==================================================================*
*  TABLE DE TRAVAIL : CHPOINT DE TEMPERATURE A T0 = 0 ET T1 = 1.   *
*==================================================================*
*
T0 = 0.; T1 = 1. ; T2 = 2.; T3 = 3.; T4 = 4.; T5 = 5.;
T6 = 6.; T7 = 7. ; T8 = 8.; T9 = 9.; T10 = 10.;

*
* EXTRACTIONS DES CHPOINTS DES COORDONNEES X ET Y DES NOEUDS DE SUR1
*
CHPSURX = COOR 1 SUR1;
CHPSURY = COOR 2 SUR1;
CHPSUR = ((CHPSURX ** 2) + (CHPSURY ** 2)) ** 0.5;
*
* CREATION DU CHPOINT DE TEMPERATURE POUR TE = 1C
*
CONST1 = 1. / (LOG 2.);
CHP2 = (LOG CHPSUR) * CONST1;
*
* TRANSFORMATION DU SOUS-TYPE "SCAL" EN SOUS-TYPE "T" POUR CHP2
*    UTILISATION DE L'OPERATEUR VARI
*
LIS10 = PROG 0. 200.;
LIS20 = PROG 0. 200.;
EVOL2 = EVOL MANU SCAL LIS10 T LIS20;
*
CHP1 = VARI CHP2 EVOL2 T;
*
* CHARGEMENT DE LA TABLE DES CHPOINTS DE TEMPERATURE
*
TEMP = TABLE;WCH1 = TABLE;
TEMP . 0 = T0;TEMP . 1 = T1;
TEMP . 2 = T2;TEMP . 3 = T3;
TEMP . 4 = T4;TEMP . 5 = T5;
TEMP . 6 = T6;TEMP . 7 = T8;
TEMP . 8 = T8;TEMP . 9 = T9;
TEMP . 10 = T10;

WCH1.0 = MANU CHPO SUR1 1 T 0.  ;
WCH1.1  = CHP1 * 120.;
WCH1.2  = CHP1 * 130.;
WCH1.3  = CHP1 * 140.;
WCH1.4  = CHP1 * 150.;
WCH1.5  = CHP1 * 160.;
WCH1.6  = CHP1 * 170.;
WCH1.7  = CHP1 * 180.;
WCH1.8  = CHP1 * 190.;
WCH1.9  = CHP1 * 195.;
WCH1.10 = CHP1 * 200.;

CHA2 = CHAR 'T' TEMP WCH1;
*
*
*
* LISTE DE VALEURS DE TEMPS OU L'ON VEUT DES RESULTATS : LIS1
*
si complet;
LIS1 = PROG T0 T1 T2 T3 T4 T5 T6 T7 T8 T9 T10;
sinon;
LIS1 =    PROG T0 T1 T2;
finsi;

*
*   CREATION D'UN CHAMPS DE FORCE NUL : THERMO-ELASTICITE
*
si complet;
LFORCE = PROG 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. ;
sinon;
LFORCE = PROG 0. 0. 0.;
finsi;

FOR1 = FORCE FX 1. PA;
CHPO1 = RESU FOR1;
EVOL1 = EVOL MANU TEMPS LIS1 FORCE LFORCE;
CHA1 = CHAR 'MECA' CHPO1 EVOL1;
*
*
TAB1 = TABLE;
TAB1.'MODELE' = MO ;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'CHARGEMENT' = (CHA1 ET CHA2);
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'TEMPS_SAUVES' = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;
TAB1.'PROCESSEURS' = 'AUTOMATIQUE';
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               PROCEDURE PASAPAS                              *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
PASAPAS TAB1;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*         EXTRACTION et AFFICHAGE des RESULTATS                *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MESS ' RESULTATS DU TEST SSNP05';
MESS ' ------------------------';
MESS ' Les resultats de reference sont la moyenne des calculs';
MESS ' effectues par Framasoft et le Cetim';
SAUT 2 LIGN;

*
MESS ' Contraintes interpolees (MPa)';
MESS ' -----------------------------';
SAUT 1 LIGN;
*
* Interpolations des contraintes aux noeuds
*
si complet;
NN10 = 10;
sinon;
nn10 = 2;
finsi;
CHEL1 = TAB1.CONTRAINTES.NN10;
CHPO1 = CHAN CHPO MO CHEL1;

* Extraction des contraintes
*
si complet ;
SMRR10 = 0.1731;     SMRR30 = 0.3806;      SMRR20 = 47.04;
SMTT10 = 340.6;   SMTT30 = -191.75 ;       SMTT20 = -50.63;
SMZZ10 = 114.1;  SMZZ30 = -345.95;   SMZZ20 = -284.  ;
sinon;
SMRR10 = .36175;    SMRR30 = -2.9683 ; SMRR20 = 35.943;
SMTT10 =272.68;    SMTT30 = -169.49; SMTT20 =  -21.166;
SMZZ10 = 81.913;  SMZZ30 = -348.10;   SMZZ20 = -177.17;
finsi;
SMRR1 = EXTR CHPO1 SMXX PA;
SMRR3 = EXTR CHPO1 SMXX PB;
SMRR2 = EXTR CHPO1 SMXX pm;
*
SMTT1 = EXTR CHPO1 SMYY PA;
SMTT3 = EXTR CHPO1 SMYY PB;
SMTT2 = EXTR CHPO1 SMYY pm;
* ;
SMZZ1 = EXTR CHPO1 SMZZ PA;
SMZZ3 = EXTR CHPO1 SMZZ PB;
SMZZ2 = EXTR CHPO1 SMZZ pm;
*
* Calcul des ecarts avec resultats de reference
*
ERGRR1 = 100 * (ABS ((SMRR1 - SMRR10) / SMRR10));
ERGRR2 = 100 * (ABS ((SMRR2 - SMRR20) / SMRR20));
ERGRR3 = 100 * (ABS ((SMRR3 - SMRR30) / SMRR30));
*
ERGTT1 = 100 * (ABS ((SMTT1 - SMTT10) / SMTT10));
ERGTT2 = 100 * (ABS ((SMTT2 - SMTT20) / SMTT20));
ERGTT3 = 100 * (ABS ((SMTT3 - SMTT30) / SMTT30));
*
ERGZZ1 = 100 * (ABS ((SMZZ1 - SMZZ10) / SMZZ10));
ERGZZ2 = 100 * (ABS ((SMZZ2 - SMZZ20) / SMZZ20));
ERGZZ3 = 100 * (ABS ((SMZZ3 - SMZZ30) / SMZZ30));
*
MESS ' Resultats pour r = 1. mm ';
MESS ' ------------------------ ';
SAUT 1 LIGN;
*
MESS ' Contrainte radiale de reference (smrr)  :' SMRR10 'MPa';
MESS ' Contrainte radiale calculee  (smrr)     :' SMRR1 'MPa';
MESS '            Soit un ecart de :' ERGRR1 '%';
MESS '            ECART NON-SIGNIFICATIF';
SAUT 1 LIGN;
*
MESS ' Contrainte tangentielle de reference     :' SMTT10 'MPa';
MESS ' Contrainte tangentielle calculee  (smtt) :' SMTT1 'MPa';
MESS '            Soit un ecart de :' ERGTT1 '%';
SAUT 1 LIGN;
*
MESS ' Contrainte axiale de reference           :' SMZZ10 'MPa';
MESS ' Contrainte axiale calculee  (smzz)       :' SMZZ1 'MPa';
MESS '            Soit un ecart de :' ERGZZ1 '%';
SAUT 1 LIGN;
*
MESS ' Resultats pour r = 1.497 mm ';
MESS ' ---------------------------- ';
SAUT 1 LIGN;
*
MESS ' Contrainte radiale de reference          :' SMRR20 'MPa';
MESS ' Contrainte radiale calculee  (smrr)      :' SMRR2 'MPa';
MESS '            Soit un ecart de :' ERGRR2 '%';
SAUT 1 LIGN;
*
MESS ' Contrainte tangentielle de reference     :' SMTT20 'MPa';
MESS ' Contrainte tangentielle calculee  (smtt) :' SMTT2 'MPa';
MESS '            Soit un ecart de :' ERGTT2 '%';
SAUT 1 LIGN;
*
MESS ' Contrainte axiale de reference           :' SMZZ20 'MPa';
MESS ' Contrainte axiale calculee  (smzz)       :' SMZZ2 'MPa';
MESS '            Soit un ecart de :' ERGZZ2 '%';
SAUT 1 LIGN;

*
MESS ' Resultats pour r = 2. mm ';
MESS ' ------------------------ ';
SAUT 1 LIGN;
*
MESS ' Contrainte radiale de reference          :' SMRR30 'MPa';
MESS ' Contrainte radiale calculee  (smrr)      :' SMRR3 'MPa';
MESS '            Soit un ecart de :' ERGRR3 '%';
MESS '            ECART NON-SIGNIFICATIF';
SAUT 1 LIGN;
*
MESS ' Contrainte tangentielle de reference     :' SMTT30 'MPa';
MESS ' Contrainte tangentielle calculee  (smtt) :' SMTT3 'MPa';
MESS '            Soit un ecart de :' ERGTT3 '%';
SAUT 1 LIGN;
*
MESS ' Contrainte axiale de reference           :' SMZZ30 'MPa';
MESS ' Contrainte axiale calculee  (smzz)       :' SMZZ3 'MPa';
MESS '            Soit un ecart de :' ERGZZ3 '%';
SAUT 1 LIGN;
*
SAUT 1 LIGN;
MESS ' Deplacements radiaux';
MESS ' --------------------';
SAUT 1 LIGN;
*
* Extractions des deplacements radiaux
*
DEP1 = TAB1.DEPLACEMENTS.NN10;
*
UR1 = EXTR DEP1 UX PA;
UR2 = EXTR DEP1 UX PB;
UR3 = EXTR DEP1 UX pm;
mess ur1 ur2 ur3;
*
si complet;
UR10 = 0.1804E-2;
UR20 = 0.386E-2;
sinon;
ur10 =  1.24001E-03  ;ur20 = 2.48285E-03;
finsi;
*UR30 = NON CALCULE
*
* Calcul des ecarts avec solution V.P.C.S.
*
ERGUR1 = 100 * ( ABS (( UR1 - UR10) / UR10 ));
ERGUR2 = 100 * ( ABS (( UR2 - UR20) / UR20 ));
*ERGUR3 = 100 * ( ABS (( UR3 - UR30) / UR30 ));
*
MESS ' Deplacement de reference radial pour r = 1. mm :' UR10 'mm';
MESS ' Deplacement calcul  radial  pour r = 1. mm    :' UR1 'mm';
MESS '    Soit un ecart de :' ERGUR1 '%';
SAUT 1 LIGN;;
*
*MESS ' Deplacement de reference radial pour r = 1.497 mm :' UR30 'mm';
MESS ' Deplacement calcul      radial pour r = 1.497 mm :' UR3 'mm';
*MESS '    Soit un ecart de :' ERGUR3 '%';
SAUT 1 LIGN;
*
MESS ' Deplacement de reference radial pour r = 2. mm :' UR20 'mm';
MESS ' Deplacement calcul      radial pour r = 2. mm :' UR2 'mm';
MESS '    Soit un ecart de :' ERGUR2 '%';
SAUT 1 LIGN;
*
MESS ' Nombre de noeuds  : ' NOSUR1;
MESS ' Nombre d elements : ' ELSUR1;
SAUT 1 LIGN;
*
* Tracer de la deformee.
*
SI (NEG GRAPH 'N');
  DEF0 = DEFO SUR1 DEP1 0. ;
  DEF1 = DEFO SUR1 DEP1 10. ROUGE ;
  TITR ' SSNP05 : DEFORMEE (DEFO. PLANE)';
  TRAC (DEF0 ET DEF1);
FINSI;
*
* tracer de la contrainte equivalente de Von-Mises
*
SI (NEG GRAPH 'N');
   CHEL2 = VMIS mo CHEL1;
   TITR ' TRACE DES CONTRAINTES DE VON-MISES';
   TRAC CHEL2 mo SUR1;
   LLIS2 = PROG 0. 290. 295. 300. 305. 310. 600.;
   TITR 'LIEU OU IL Y A PLASTIFICATION';
   TRAC LLIS2 CHEL2 mo SUR1;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
*===================================================================*
*                  Code fonctionnement                              *
*===================================================================*
*
* Maximum des erreurs relatives
*
ERG1 = MAXI (PROG ergtt1 ergzz1 ergrr2 ergtt2 ergzz2 ergtt3 ergzz3);
*
* remarque : on ne tient pas compte des ecarts sur la contrainte
*           radiale pour r = 1.mm et r = 2.mm
*
*
ERG2 = MAXI (PROG ERGur1 ergur2);
ERGMAX = MAXI (PROG ERG1 ERG2);
*
* Il y a erreur si un des pourcentage erg*** est suprieur a 5%.
*
SI (ERGMAX <EG 5.);
     ERRE  0 ;
SINON;
     ERRE  5 ;
FINSI;
*
FIN;
*













