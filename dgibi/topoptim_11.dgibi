* fichier topoptim_11.dgibi
************************************************************************
************************************************************************

************************************************************************
** Topology optimization of a simple 2D compliant mechanism: inverter
** (maximization of the output displacements in the opposite direction
** to the input displacements)
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V1.0 2017/11/11  Original version compatible with TOPOPTIM V2.1
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' QUA4 ;

* Mesh
nelx0 = 40 ;
nely0 = 20 ;
p0 = 0.0 0.0 ;
p1 = (FLOT nelx0) 0.0 ;
p2 = 0.0 (FLOT nely0) ;
lgn0 = DROI nelx0 p0 p1 ;
msh0 = TRAN lgn0 nely0 p2 ;
p2 = msh0 POIN 'PROC' p2 ;

* Model and material
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' ;
mat0 = MATE mod0 'YOUN' 1.0 'NU' 0.3 ;

* Boundary conditions and loading
bc0 = (BLOQ 'UY' lgn0) ET (BLOQ 'DEPL' p2) ;
load0 = FORC (-1.0 0.0) p0 ;

* Output displacement elementary field to be maximized
load1 = FORC (1.0 0.0) p1 ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ;
mdl0.'BLOCAGES_MECANIQUES' = bc0 ;
mdl0.'CHARGEMENT'          = load0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_LINEAIRE' = mdl0 ;
tab0.'FRACTION_VOLUME'     = 0.3 ;
tab0.'FACTEUR_D'           = 0.3 ;

tab0.'MECANISME'           = load1 ;
tab0.'TOPOLOGIE_MAX_INC'   = 0.1 ;
tab0.'TAUX_FILTRAGE'       = 2 ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Plot to screen
topo0    = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
SI graph0 ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;



