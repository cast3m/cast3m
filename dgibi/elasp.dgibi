* fichier :  elasp.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
SAUT PAGE;
*
********************************************************************
*                                                                  *
*  TEST ELASP                                                      *
*                                                                  *
*  MEMBRANE ELLIPTIQUE (Contraintes planes)                        *
*                                                                  *
*  cas-test NAFEMS : test numero LE1                               *
*                                                                  *
*                                                                  *
*    Une membrane elliptique obtenue par projection d'arcs de      *
* cercles sur un plan, est soumis a une force lineique sur l'un    *
* de ces cotes. Le but de ce test est de controler l'élément       *
* polygone. Il est issu du test elas8.                             *
*                                                                  *
*    Vue les symetries le probleme est traite en contraintes       *
* planes.                                                          *
*                                                                  *
*    On se propose de calculer la contrainte smyy au point D.      *
*                                                                  *
********************************************************************
*
OPTI ECHO 0;
*
OPTI DIME 3;
OPTI ELEM POLY;
*
GRAPH = 'N';
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
* Dimension en metres
*
* Points
*
PB = 0. 2.75 0.;
PB1 = 0. 2.75 (((3.25 ** 2) - (2.75 ** 2)) ** 0.5);
PC = 3.25 0. 0.;
*
C1 = PC CERC 30 (0. 0. 0.) PB1;
C2 = C1 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
PA = 0. 1. 0.;
PA1 = 0. 1. (((2. ** 2) - (1. ** 2)) ** 0.5);
PD = 2. 0. 0.;
*
C3 = PD CERC 24 (0. 0. 0.) PA1;
C4 = C3 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
D1 = PA DROI 26 PB;
D3 = PC DROI 26 PD;
*
ELIM (D1 ET C2 ET D3 ET C4) 0.0001;
SUR1 = SURF (D1 ET C2 ET D3 ET C4) PLAN;
*
SI (NEG GRAPH 'N');
  TITR 'ELASP : MAILLAGE';
  TRAC (0 0 1000) QUAL SUR1;
FINSI;
*
OPTI DIME 2;
OPTI MODE PLAN CONT;
*
*==================================================================*
*      modele - affecte - materiau - caracteristique               *
*           rigidite - conditions aux limites                      *
*==================================================================*
*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE POLY;
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3;
RI1 = RIGI MODL1 MATR1;
*
* Conditions aux limites
*    - droite d1 : symetrie par rapport a l'axe des ordonnees,
*    - droite d2 : symetrie par rapport a l'axe des abscisses.
*
CL1 = SYMT DEPL ROTA (0 0) (0 1) SUR1 0.0001;
CL2 = SYMT DEPL ROTA (0 0) (1 0) SUR1 0.0001;
*
RIG1 = RI1 ET CL1 ET CL2;
*
*==================================================================*
*                      Chargement                                  *
*==================================================================*
*
*   Pression externe uniforme de 10e6Pa.
*
CHA1 = PRES MASS MODL1 -10.E6 C2;
*
*==================================================================*
*            resolution : champs de deplacements                   *
*==================================================================*
*
DEP1 = RESO RIG1 CHA1;
*
* Trace facultatif de la deformee et des reactions
*
SI (NEG GRAPH 'N');
  AMPVEC = 1.E-7;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT CHA1 AMPVEC FX FY ROUGE;
  VECT2 = VECT REA1 AMPVEC FX FY BLEU;
  DEF0 = DEFO 0. DEP1 SUR1;
  DEF1 = DEFO 150. DEP1 SUR1 (VECT1 ET VECT2) VERT;
  TITR 'ELAS8 : DEFORMEE, CHARGEMENT, REACTIONS';
  TRAC SUR1 (DEF0 ET DEF1);
FINSI;
*
*==================================================================*
*                 champs de contraintes                            *
*==================================================================*
*
CHAM1 = SIGM MODL1 MATR1 DEP1;
*
* Interpolation aux noeuds du maillage
*
CHAM11 = CHAN NOEUD CHAM1 MODL1;
CHP11 = CHAN CHPO CHAM11 MODL1;
*
*==================================================================*
*         extraction et affichage d'une contrainte                 *
*==================================================================*
*
*   On cherche la contrainte smyy a point D.
*
SMYYD = EXTR CHP11 SMYY PD;SMYYD = SMYYD / 1.E6;
*
SMYYD1 = 92.7;
ERG = 100 * (ABS ((SMYYD - SMYYD1) / SMYYD1));
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
MESS ' CONTRAINTE SMYY THEORIQUE AU POINT D :' SMYYD1 'MPA';
MESS ' CONTRAINTE SMYY CALCULEE AU POINT D  :' SMYYD 'MPA';
MESS '    SOIT UN ECART DE :' ERG '%';
SAUT 1 LIGN;
*
NBNOSUR1 = NBNO SUR1;
NBELSUR1 = NBEL SUR1;
MESS ' NOMBRE D ELEMENTS :' NBELSUR1 'POLY';
MESS ' NOMBRE DE NOEUDS  ;' NBNOSUR1 ;
SAUT 1 LIGN;
*
*==================================================================*
*                   code fonctionnement                            *
*==================================================================*
*
SI (ERG <EG 5.);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
SAUT 1 LIGN;
  TEMPS;
SAUT 1 LIGN;
*
FIN;







