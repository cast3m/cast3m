* fichier :  elas7.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas7.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*______________________________________________________
*   TEST ELAS7                                        *
*                                                     *
*   COQUE AVEC BRANCHEMENT                            *
*                                                     *
*   TEST ELEMENTAIRE NAFEMS : ANALYSE LINEAIRE        *
*                                     ELASTIQUE       *
*           numero LE9                AXISYMETRIQUE   *
*                                                     *
*       L'enceinte est constituee par un cylindre et  *
*   par une sphere (demie), reposant sur un cylindre. *
*   L'enceinte est soumise a une pression interne     *
*   uniforme de 1e6Pa.                                *
*      On cherche la contrainte axiale sur la surface *
*   exterieure du cylindre superieure au point C. La  *
*   contrainte obtenue est comparee au resultat       *
*   theorique.                                        *
*                                                     *
*______________________________________________________
*
OPTI DIME 2;
OPTI MODE AXIS;
OPTI ELEM SEG2;
*
*=====================================================*
*             geometrie : maillage                    *
*=====================================================*
*
* Dimension en metres
*
* Points
*
COS75 = COS 7.5; COS375 = COS 37.5; COS45 = COS 45;
SIN75 = SIN 7.5; SIN375 = SIN 37.5; SIN45 = SIN 45;
R = 2 ** -.5;
*
RA = R; ZA = 0.; PA = RA ZA;
RB = 0; ZB = COS45; PB = RB ZB;
RC = R; ZC = 1.; PC = RC ZC;
RD = R; ZD = 2.; PD = RD ZD;
RO = 0; ZO = 1 + COS45; PO = RO ZO;
PA1 = PC PLUS (0 -0.125);
PD1 = PC PLUS (0 0.125);
PB1 = PB PLUS (SIN375 (1 - COS375));
*
* Droites, cercles et lignes
*
* coefficient de finesse du maillage
*
K = 1;
*
SI (K EGA 1);
  TITR ' LE9 : MAILLAGE GROSSIER';
SINON;
  TITR ' LE9 : MAILLAGE FIN';
FINSI;
*
N1 = 4 * K;
AA1 = PA  DROI N1 PA1;
A1C = PA1 DROI N1 PC;
CD1 = PC  DROI N1 PD1;
D1D = PD1 DROI N1 PD;
B1C = PB1 CERC N1 PO PC;
*
N2 = 5 * K;
BB1 = PB CERC N2 PO PB1;
*
* Maillage
*
SUR1 = BB1 ET B1C;
SUR2 = CD1 ET D1D;
SUR3 = AA1 ET A1C;
SURF1 = SUR1 ET SUR2 ET SUR3;
*
SI (NEG GRAPH 'N');
   TITR 'ELAS7 : MAILLAGE';
   TRAC QUAL SURF1;
FINSI;
*
ELSURF1 = NBEL SURF1;
NOSURF1 = NBNO SURF1;
*
*=====================================================*
*   modele - affecte - materiau - caracteristique     *
*       rigidite - conditions aux limites             *
*=====================================================*
*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE COQ2;
MODL2 = MODE SUR2 MECANIQUE ELASTIQUE ISOTROPE COQ2;
MODL3 = MODE SUR3 MECANIQUE ELASTIQUE ISOTROPE COQ2;
*
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3 EPAI 0.01;
MATR2 = MATE MODL2 YOUN 210E9 NU 0.3 EPAI 0.01;
MATR3 = MATE MODL3 YOUN 210E9 NU 0.3 EPAI 0.01;
*
RI1 = RIGI MODL1 MATR1;
RI2 = RIGI MODL2 MATR2;
RI3 = RIGI MODL3 MATR3;
*
* Conditions aux limites
*    - point pa : encastrement complet.
*
CL1 = BLOQ ROTA DEPL PA;
*
RIG1 = RI1 ET RI2 ET RI3 ET CL1;
*
*=====================================================*
*               Chargement                            *
*=====================================================*
*
*   Pression interne uniforme de 1e6Pa.
*
MOP1  = 'MODE' SUR1 'CHARGEMENT' 'PRESSION' 'COQ2' ;
MAP1  = 'PRES' MOP1 'PRES' -1.E6 ;
CH1   = 'BSIG' MOP1 MAP1 ;
MOP2  = 'MODE' SUR2 'CHARGEMENT' 'PRESSION' 'COQ2' ;
MAP2  = 'PRES' MOP2 'PRES' -1.E6 ;
CH2   = 'BSIG' MOP2 MAP2 ;
CHA1  = CH1 ET CH2;
*
*=====================================================*
*        resolution : champs de deplacements          *
*=====================================================*
*
DEP1 = RESO RIG1 CHA1;
*
* Trace facultatif de la deformee et des reactions
*
SI (NEG GRAPH 'N');
  AMPVEC = .4E-6;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT CHA1 (AMPVEC * 1) FR FZ ROUGE;
  VECT2 = VECT REA1 (AMPVEC * 1) FR FZ BLEU ;
  DEF0 = DEFO 0. DEP1 SURF1;
  DEF1 = DEFO 40. DEP1 SURF1 (VECT1 ET VECT2) VERT;
  TITR 'ELAS7 : DEFORMEE, CHARGEMENT, REACTIONS';
  TRAC SURF1 (DEF0 ET DEF1);
FINSI;
*
*=====================================================*
*              extraction d'une contrainte            *
*       et comparaison avec solution analytique       *
*=====================================================*
*
*      On cherche la contrainte axiale sur la surface
* exterieure du cylindre superieure au point C.
*
*    Calcul du champs de contraintes generalisees
* uniquement sur le cylindre superieur (sur2).
*
* Reduction du chpoint des deplacements au cylindre
* superieur.
*
DEP2 = REDU DEP1 SUR2;
CHAM1 = SIGM MODL2 MATR2 DEP2;
*
* Calcul du champs de contraintes au sens des milieux
* continus. Pour cela on utilise l'operateur CALP .
*
CHAM2 = CALP CHAM1 MATR2 MODL2 INFE;
*
* Calcul des contraintes aux noeuds du maillage
*
CHAM3 = CHAN NOEUD MODL2 CHAM2;
CHPO3 = CHAN CHPO MODL2 CHAM3;
*
* Extraction de la contrainte recherchee
*
SMZZC = EXTR CHPO3 SMXX PC;
SMZZC = SMZZC / 1.E6;
*
SMZZC1 = -319.9;
*
* Calcul de l'ecart avec la solution de reference
*
ERGZZC = 100 * (ABS ((SMZZC - SMZZC1) / SMZZC1));
*
*=====================================================*
*           affichage des resultats                   *
*=====================================================*
*
MESS ' RESULTATS : TEST ELAS7 ';
MESS ' ---------------------- ';
SAUT 2 LIGN;
*
MESS '   On cherche la contrainte axiale sur la surface exterieure';
MESS ' du cylindre superieure au point C.';
MESS ' Elle est comparee a une valeur theorique obtenue analytiquement.';
SAUT 1 LIGN;
MESS ' Contrainte axiale theorique en C:' SMZZC1 'MPa';
SAUT 1 LIGN;
MESS ' Contrainte axiale calculee en C :' SMZZC 'MPa';
SAUT 1 LIGN;
MESS '          Soit un ecart de :' ERGZZC '%';
SAUT 2 LIGN;
*
SI (K EGA 1);
  MESS ' MAILLAGE GROSSIER';
SINON;
  MESS ' MAILLAGE FIN';
FINSI;
*
MESS '    Nombre d elements :' ELSURF1;
MESS '    Nombre de noeuds  :' NOSURF1;
MESS '       soit ' (NOSURF1 * 3) 'd.d.l.';
*
*=====================================================*
*             code fonctionnement                     *
*=====================================================*
*
*  L'ecart maximum entre valeur theorique et calculee
* doit etre inferieure a 2.5%.
*
SAUT 2 LIGN;
SI (ERGZZC <EG 2.5);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*=====================================================*
*           Temps de calcul et fin                    *
*=====================================================*
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;












