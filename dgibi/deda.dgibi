'OPTI' 'ECHO' 0 ;
************************************************************************
* Section : Langage Objets
* Section : Maillage Autres
************************************************************************



************************************************************************
* Cas test pour l'operateur DEDAns                                     *
* On test le resultat de l'operateur DEDA sur des points sur des       *
* points situes a l'exterieur et a l'interieur d'un contour (2D) et    *
* d'une enveloppe (3D)                                                 *
* Le maillage utilise est non convexe et non connexe (2 parties)       *
* On teste avec des points :                                           *
* - franchement loin du bord                                           *
* - tres pres du bord                                                  *
* - colineaire/coplanaire a un element du bord                         *
* - situes sur le bord (face, arete, sommet)                           *
* - appartenant au bord (noeud du bord)                                *
************************************************************************


** Indicateur pour le trace
itrac = FAUX ;

** Distance pour placer des points "tres pres" du bord
*  si l'on reduit cette distance, il convient d'adapter le critere de
*  l'operateur DEDA en le choisissant plus eleve que la valeur par
*  defaut
epsi  = 1.E-5 ;



************************************************************************
* EN DIMENSION 2                                                       *
************************************************************************

'OPTI' 'DIME' 2 'ELEM' 'SEG2' ;

'MESS' ;
'MESS' '*************************' ;
'MESS' '       DIMENSION 2' ;
'MESS' '*************************' ;
'MESS' ;

** Maillage du contour oriente convenablement : les trous internes
*  tournent dans le sens oppose du cadre exterieur
p0 = 0. 0. ;
p1 = 8. 0. ;
p2 = 8. 8. ;
p3 = 4. 4. ;
p4 = 0. 8. ;
ne1 = 10 ;
cadre = p0 'DROI' ne1 p1 'DROI' ne1 p2 'DROI' ne1 p3 'DROI' ne1 p4
           'DROI' ne1 p0 ;
*
p5 = 2. 2. ;
p6 = 3. 2. ;
cerc1 = 'LIGN' 20 p5 p6 -360. 'ROTA' ;
'ELIM' 1.E-9 cerc1 ;
*
cont1 = cadre 'ET' cerc1 ;


** Table de points a l'exterieur et a l'interieur
tpe = 'TABL' 'ESCLAVE' ;
tpe . 1  = -1. -1. ;
tpe . 2  =  4.  6. ;
tpe . 3  = 2.5  2.7 ;
tpe . 4  =  1.5 1.5 ;
tpe . 5  =  p3 'PLUS' (0. epsi) ;
tpe . 6  =  (-1. * epsi) 4. ;
tpe . 7  =  15. 15. ;
tpe . 8  =  0. 10. ;
tpe . 9  =  10. 0. ;
mpe = ('ET' tpe) 'COUL' 'ROUG' ;
tpi = 'TABL' 'ESCLAVE' ;
tpi . 1  =  4.  3. ;
tpi . 2  =  6.  2. ;
tpi . 3  =  2.  epsi ;
tpi . 4  =  (8. - epsi) 4. ;
tpi . 5  =  5. 0. ;
tpi . 6  =  1. 7. ;
tpi . 7  =  4. 4. ;
tpi . 8  =  8. 0. ;
tpi . 9  =  p0 ;
tpi . 10 =  p2 ;
mpi = ('ET' tpi) 'COUL' 'VERT' ;
*
'SI' itrac ;
  'TRAC' (cont1 'ET' mpe 'ET' mpi)
    'TITR' 'Point ext. (rouge) et points int. (vert)' ;
'FINSI' ;


** Test si les points sont bien a l'exterieur du contour
'MESS' '***** Resultat de DEDA pour les points a l exterieur' ;
'REPE' b1 (('DIME' tpe) - 2) ;
  pe1 = tpe . &b1 ;
  log1 = 'DEDA' pe1 cont1 ;
  'LIST' log1 ;
  'SI' log1 ;
    'MESS' '***** ECHEC  du cas test !' ;
    'MESS' 'Le ' &b1 'ieme point exterieur est detecte a l interieur' ;
    'LIST' pe1 ;
    'ERREUR' 5 ;
  'FINSI' ;
'FIN' b1 ;


** Test si les points sont bien a l'interieur du contour
'MESS' ;
'MESS' '***** Resultat de DEDA pour les points a l interieur' ;
'REPE' b1 (('DIME' tpi) - 2) ;
  pi1 = tpi . &b1 ;
  log1 = 'DEDA' pi1 cont1 ;
  'LIST' log1 ;
  'SI' ('NON' log1) ;
     'MESS' '***** ECHEC  du cas test !' ;
    'MESS' 'Le ' &b1 'ieme point interieur est detecte a l exterieur' ;
    'LIST' pi1 ;
    'ERREUR' 5 ;
  'FINSI' ;
'FIN' b1 ;


'MESS' ;
'MESS' ;



************************************************************************
* EN DIMENSION 3                                                       *
************************************************************************

'OPTI' 'DIME' 3 'ELEM' 'TRI3' ;

'MESS' ;
'MESS' '*************************' ;
'MESS' '       DIMENSION 3' ;
'MESS' '*************************' ;
'MESS' ;

** Maillage de l'enveloppe par revolution du contour precedent
p7 = 0. 10. 0. ;
p8 = 1. 10. 0. ;
ne2 = 10 ;
sur1 = 'SURF' cont1 'PLAN' ;
sur1 = 'ORIE' sur1 'POIN' (0. 0. -1.) ;
env1 = cont1 'ROTA' ne2 90. p7 p8 ;
cont2 = env1 'COTE' 3 ;
sur2 = 'SURF' cont2 'PLAN' ;
sur2 = 'ORIE' sur2 'POIN' (0. 1. 0.) ;
env1 = env1 'ET' sur1 'ET' sur2 ;


** Table de points a l'exterieur et a l'interieur
tpe = 'TABL' 'ESCLAVE' ;
tpe . 1  = 1. 1. 1. ;
tpe . 2  = -1. 10. -2. ;
tpe . 3  = (2. 2. 0.) 'TOUR' 40. p7 p8 ;
tpe . 4  = 4. 2. epsi ;
tpe . 5  = 3. (10. + epsi) -6. ;
tpe . 6  = (env1 'POIN' 'PROC' (p3 'TOUR' 65. p7 p8)) 'PLUS'
           (0. 0. epsi) ;
tpe . 7  = 2. 8. 0. ;
tpe . 8  = 0. 10. 0. ;
tpe . 9  = 2. 2. 0. ;
mpe = ('ET' tpe) 'COUL' 'ROUG' ;
tpi = 'TABL' 'ESCLAVE' ;
tpi . 1  =  4.  3. -1. ;
tpi . 2  =  7.  8. -5. ;
tpi . 3  =  2. (10. - epsi) -5. ;
tpi . 4  = (env1 'POIN' 'PROC' (p3 'TOUR' 80. p7 p8)) 'MOIN'
           (0. 0. epsi) ;
tpi . 5  = 3. 4. 0. ;
tpi . 6  = 8. 10. -8. ;
tpi . 7  = 0. 0. 0. ;
tpi . 8  = (env1 'POIN' 'PROC' (p6 'TOUR' 30. p7 p8)) 'PLUS'
           (0. 0. 0.) ;
tpi . 9  = env1 'POIN' 'PROC' (p3 'TOUR' 45. p7 p8) ;
tpi . 10 = p6 ;
mpi = ('ET' tpi) 'COUL' 'VERT' ;

'SI' itrac ;
  are1 = 'ARET' env1 ;
  'TRAC' (are1 'ET' mpe 'ET' mpi)
    'TITR' 'Point ext. (rouge) et points int. (vert)' ;
'FINSI' ;


** Test si les points sont bien a l'exterieur du contour
'MESS' '***** Resultat de DEDA pour les points a l exterieur' ;
'REPE' b1 (('DIME' tpe) - 2) ;
  pe1 = tpe . &b1 ;
  log1 = 'DEDA' pe1 env1 ;
  'LIST' log1 ;
  'SI' log1 ;
    'MESS' '***** ECHEC  du cas test !' ;
    'MESS' 'Le ' &b1 'ieme point exterieur est detecte a l interieur' ;
    'LIST' pe1 ;
    'ERREUR' 5 ;
  'FINSI' ;
'FIN' b1 ;


** Test si les points sont bien a l'interieur du contour
'MESS' ;
'MESS' '***** Resultat de DEDA pour les points a l interieur' ;
'REPE' b1 (('DIME' tpi) - 2) ;
  pi1 = tpi . &b1 ;
  log1 = 'DEDA' pi1 env1 ;
  'LIST' log1 ;
  'SI' ('NON' log1) ;
     'MESS' '***** ECHEC  du cas test !' ;
    'MESS' 'Le ' &b1 'ieme point interieur est detecte a l exterieur' ;
    'LIST' pi1 ;
    'ERREUR' 5 ;
  'FINSI' ;
'FIN' b1 ;



** Fin normale du cas test
'MESS' ;
'MESS' ;
'MESS' ;
'MESS' '***** SUCCES du cas test !' ;
'FIN' ;







