* fichier :  lapn.dgibi
************************************************************************
* Section : Fluides Euler
************************************************************************
******************************************************************
******************************************************************
* CALCUL DU LAPLACIAN EN VF                                      *
* LAPL(T)=0                                                      *
*                                                                *
* On controlle que l'algorithme est lineaire exact               *
* A. BECCANTINI LTMF    JUILLET    2001                          *
******************************************************************
******************************************************************

 'OPTION'  'DIME' 2
           'ISOV' 'SULI' 'TRAC' 'X'
           'ELEM' 'QUA4' 'ECHO'  0 ;

 GRAPH = FAUX ;
* GRAPH = VRAI ;

*
**** Domaine
*

 A1 = 0.0 0.0 ;
 A2 = 1.0 0.0 ;
 A3 = 1.0 1.0 ;
 A4 = 0.0 1.0 ;

 A1A2 = A1 'DROIT' 4 A2 ;
 A2A3 = A2 'DROIT' 5 A3 ;
 A3A4 = A3 'DROIT' 6 A4 ;
 A4A1 = A4 'DROIT' 5 A1 ;

 DOMCON = A1A2 'ET' A2A3 'ET' A3A4 'ET'
          A4A1 ;
 DOMTOT = 'SURF' DOMCON 'PLAN' ;

*
*** L'objet modele Euler
*


 $DOMTOT = 'MODELISER' DOMTOT 'EULER';

 $DOMCON = 'MODELISER'  DOMCON 'EULER';
 $A1A2 = 'MODELISER'  A1A2 'EULER';
 $A2A3 = 'MODELISER'  A2A3 'EULER';
 $A3A4 = 'MODELISER'  A3A4 'EULER';
 $A4A1 = 'MODELISER'  A4A1 'EULER';

 TDOMTOT = 'DOMA'  $DOMTOT 'VF';

 TDOMCON = 'DOMA'  $DOMCON 'VF';
 TA1A2 = 'DOMA'  $A1A2 'VF';
 TA2A3 = 'DOMA'  $A2A3 'VF';
 TA3A4 = 'DOMA'  $A3A4 'VF';
 TA4A1 = 'DOMA'  $A4A1 'VF';

 MDOMCON = TDOMCON . 'QUAF' ;
 MA1A2 = TA1A2 . 'QUAF' ;
 MA2A3 = TA2A3 . 'QUAF' ;
 MA3A4 = TA3A4 . 'QUAF' ;
 MA4A1 = TA4A1 . 'QUAF' ;

**** old stuff  $DOMTOT = 'DOMA' DOMTOT ;

 MDOMTOT = TDOMTOT . 'QUAF' ;

 'ELIMINATION'  (MDOMTOT ET MDOMCON) 0.0001 ;
 'ELIMINATION'  (MDOMTOT ET MA1A2) 0.0001 ;
 'ELIMINATION'  (MDOMTOT ET MA2A3) 0.0001 ;
 'ELIMINATION'  (MDOMTOT ET MA3A4) 0.0001 ;
 'ELIMINATION'  (MDOMTOT ET MA4A1) 0.0001 ;


************************************
************************************
*** Conditions initiales et aux ****
*** limites                     ****
************************************
************************************
*
*
*  A4     -      A3
*
*
*  |
*
*  A1     -      A2
*


*
* TEST1
* Champ lineaire
* Conditions aux limites de type Dirichlet
*

 XXLIM YYLIM = 'COORDONNEE' DOMCON ;
 ACOEF = 1.0 ;
 BCOEF = 7.0 ;
 TLIM = (ACOEF '*' XXLIM) '+' (BCOEF '*' YYLIM) ;

 XXCEN YYCEN = 'COORDONNEE' ('DOMA' $DOMTOT 'CENTRE') ;
 TN = (ACOEF '*' XXCEN) '+' (BCOEF '*' YYCEN) ;

*
*** Graphique des c.i.
*

 MOD1     =  'MODELISER'  ('DOMA' $DOMTOT 'MAILLAGE') 'THERMIQUE' ;

 'SI' GRAPH ;
    CHM_TN  =  'KCHA' $DOMTOT 'CHAM' TN ;
    'TRAC' CHM_TN  MOD1 'TITR' ('CHAINE' 'TN at t= ' 0.0);
 'FINSI' ;


*
**** Solutions
*

 GRADTN MCHAM = 'PENT'  $DOMTOT  'FACE' 'DIAMANT'
          TN 'CLIM' TLIM ;

* Le reste ne serve pas, mais il faut l'initialiser


 MU = 0.0 ;
 CV = 1.0 ;
 KAPPA = 1.0 ;

 RN = 'MANUEL' 'CHPO'  ('DOMA' $DOMTOT 'CENTRE') 1 'SCAL' 1.0 ;

 VN = 'MANUEL' 'CHPO'  ('DOMA' $DOMTOT 'CENTRE') 2 'UX' 0.0 'UY' 0.0 ;
 GRADVN = 'MANUEL' 'CHPO'  ('DOMA' $DOMTOT 'FACE') 4
      'P1DX' 0.0 'P1DY' 0.0
      'P2DX' 0.0 'P2DY' 0.0 ;
 LINC = 'MOTS' 'RN' 'UX' 'UY' 'TN' ;

*
**** Le calcul
*

 IJACO IRESI1 DT = 'LAPN' 'VF' 'PROPCOST' 'RESI' 'EXPL'
    $DOMTOT  MU KAPPA CV RN VN TN GRADVN  GRADTN
      LINC  ;

 'SI' (('MAXIMUM' IRESI1 'ABS') '>' 1.0D-5) ;
    'MESSAGE' 'Probleme 1' ;
    'ERREUR' 5 ;
 'FINSI' ;

*
**** TEST 2
*    Champ lineaire
*    Condition limite de type Neumann
*

 QLIM = (-1.0 '*' KAPPA) '*'
  ('MANUEL' 'CHPO' ('DOMA' $DOMCON 'CENTRE') 2 'UX' ACOEF 'UY' BCOEF);

 GRADTN MCHAM = 'PENT'  $DOMTOT  'FACE' 'DIAMANT'
          TN  ;

 IJACO IRESI1 DT = 'LAPN' 'VF' 'PROPCOST' 'RESI' 'EXPL'
    $DOMTOT  MU KAPPA CV RN VN TN GRADVN  GRADTN
      LINC  'QIMP' QLIM ;

 'SI' (('MAXIMUM' IRESI1 'ABS') '>' 1.0D-5) ;
    'MESSAGE' 'Probleme 1' ;
    'ERREUR' 5 ;
 'FINSI' ;

 'FIN' ;















