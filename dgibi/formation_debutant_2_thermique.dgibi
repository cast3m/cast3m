* fichier : formation_debutant_2_thermique.dgibi
*
************************************************************************
*      FORMATION DEBUTER AVEC CAST3M - CALCULS THERMO-MECANIQUES       *
*                                                                      *
* Modelisation du comportement thermo-mecanique d'une plaque perforee  *
*                                                                      *
* Ce fichier est la partie 2 sur 3 de la formation debutant a Cast3M   *
*                                                                      *
* Partie 2 : calculs thermiques                                        *
*            restitution des objets                                    *
*            conduction, convection, rayonnement                       *
*            regimes stationnaires et transitoires                     *
*            creation d'une procedure Gibiane                          *
*            instructions conditionelles, gestion des erreurs          *
*            sauvegarde des objets                                     *
*                                                                      *
************************************************************************




* RESTITUTION DES OBJETS CREES
* ----------------------------
OPTI 'REST' 'formation_debutant_1_maillage.sauv' ;
REST ;


* OBJET LOGIQUE POUR INDIQUER SI L'ON FAIT LES TRACES OU NON
* ----------------------------------------------------------
ITRAC    = FAUX ;






************************************************************************
* CHAPITRE 2 : THERMIQUE LINEAIRE STATIONNAIRE                         *
************************************************************************


* MODELE THERMIQUE (CONDUCTION) A MATERIAU UNIFORME ET CONSTANT
* -------------------------------------------------------------
MOT      = MODE SU 'THERMIQUE' 'ISOTROPE' 'CONDUCTION' ;
MAT      = MATE MOT 'K' CONDUMAT 'C' CAPAMAT 'RHO' RHOMAT ;


* CALCUL DE LA MATRICE DE CONDUCTIVITE
* ------------------------------------
CON      = COND MOT MAT ;


* MATRICE DE BLOCAGE : TEMPERATURE IMPOSEE SUR LE TROU
* ----------------------------------------------------
BLT      = BLOQ CE   'T' ;


* FLUX NODAUX ASSOCIES AU BLOCAGE
* -------------------------------
TMAX     = 250. ;
FLT1     = DEPI BLT TMAX ;


* FLUX IMPOSE SUR LA LIGNE GAUCHE
* -------------------------------
FLT2     = FLUX MOT LIGA -3.5E4 ;


* CALCUL DU CHAMP DE TEMPERATURES PAR APPEL AU SOLVEUR
* ----------------------------------------------------
TCON1    = RESO (CON ET BLT) (FLT1 ET FLT2) ;


* AFFICHAGE DU CHAMP DE TEMPERATURE
* ---------------------------------
LISO1    = PROG 0. 'PAS' (TMAX / 15.) TMAX ;
SI ITRAC ;
  TRAC TCON1 SU ;
  TRAC TCON1 SU CSU ;
  TRAC TCON1 SU CSU LISO1 ;
  TRAC TCON1 SU CSU LISO1 'TITR' '[2] Temperature' ;
FINSI ;










************************************************************************
* CHAPITRE 3 : THERMIQUE LINEAIRE TRANSITOIRE                          *
*              UTILISATION DE PASAPAS                                  *
************************************************************************


* INSTANT FINAL DU CALCUL THERMIQUE
* ---------------------------------
TPSFIN   = 5.E4 ;


* CHAMP DE TEMPERATURES INITIALES (UNIFORME)
* ------------------------------------------
T0       = 25. ;
T_INI    = MANU 'CHPO' 1 SU 'T' T0 ;


* CHARGEMENTS THERMIQUES POUR LES TEMPERATURES ET FLUX IMPOSES
* MONTEE RAPIDE PUIS MAINTENUE AU COURS DU TEMPS
* ------------------------------------------------------------
LIST1    = PROG 0. (0.1 * TPSFIN) TPSFIN ;
LIST2    = PROG (T0 / TMAX) 1. 1. ;
EVT1     = EVOL 'MANU' LIST1 LIST2 ;
CHT1     = CHAR 'TIMP' FLT1 EVT1 ;

LIST3    = PROG 0. (0.2 * TPSFIN) (0.5 * TPSFIN) TPSFIN ;
LIST4    = PROG 0. 0. 1. 1. ;
EVT2     = EVOL 'MANU' LIST3 LIST4 ;
CHT2     = CHAR 'Q'    FLT2 EVT2 ;
CHT      = CHT1 ET CHT2 ;


* DEFINITION DE LA TABLE D'ARGUMENTS A FOURNIR EN DONNEE D'ENTREE A LA
* PROCEDURE PASAPAS
* --------------------------------------------------------------------
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MOT ;
TAB1 . 'CARACTERISTIQUES'    = MAT ;
TAB1 . 'BLOCAGES_THERMIQUES' = BLT ;
TAB1 . 'CHARGEMENT'          = CHT ;
TAB1 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' (0.02 * TPSFIN) TPSFIN ;
TAB1 . 'TEMPERATURES'        = TABL ;
TAB1 . 'TEMPERATURES' . 0    = T_INI ;


* APPEL A PASAPAS
* ---------------
PASAPAS TAB1 ;


* POST TRAITEMENT
* ---------------
* EVOLUTION TEMPORELLE DE LE TEMPERATURE EN DEUX POINTS POUR VERIFIER
* QUE L'ON A ATTEINT L'ETAT STATIONNAIRE
PMIL     = SU POIN 'PROC' ((0.5 * LONG) (0.5 * HAUT)) ;
EV1      = EVOL 'ROUG' 'TEMP' TAB1 'TEMPERATURES' 'T' PMIL ;
EV2      = EVOL 'BLEU' 'TEMP' TAB1 'TEMPERATURES' 'T' PG ;
SI ITRAC ;
  DESS (EV1 ET EV2) 'TITR' '[3] Temperature vs Temps' ;
FINSI ;

* BOUCLE SUR TOUS LES PAS DE TEMPS ET TRACE DU CHAMP DE TEMPERATURES
N1       = DIME (TAB1 . 'TEMPERATURES') ;
SI ITRAC ;
  REPE B1 N1 ;
    T_I      = TAB1 . 'TEMPERATURES' . (&B1 - 1) ;
    TPS_I    = TAB1 . 'TEMPS' . (&B1 - 1) ;
    MOT_I    = CHAI '[3] Temperature au temps ' TPS_I ;
    TRAC T_I SU CSU 'TITR' MOT_I LISO1 ;
  FIN B1 ;
FINSI ;

* VECTEUR FLUX DE CHALEUR ==> CREATION D'UNE PROCEDURE
DEBP VECFLU  CHP1*'CHPOINT'  MOD1*'MMODEL'  MAT1*'MCHAML'  MOT1/'MOT' ;
* CALCUL DU GRADIENT DE T ET CHANGEMENT DU TYPE
  G1       = GRAD CHP1 MOD1 ;
  G2       = CHAN 'TYPE' G1 'CARACTERISTIQUES' ;
* MULTIPLICATION DES CHAMPS ENTRE EUX
  Q        = MAT1 * G2 (MOTS 'K' 'K') (MOTS 'T,X' 'T,Y') (MOTS 'QX' 'QY') ;
  Q        = -1. * Q ;
* CREATION D'UN OBJET VECTEUR
  SI (EGA (TYPE MOT1) 'MOT') ;
    SI (EGA MOT1 'COUL') ;
      QP       = CHAN 'CHPO' Q MOD1 ;
      VEC1     = @VECOUL QP 2.E-6 ;
    SINON ;
      ERRE '--> ERREUR dans la procedure VECFLU, le mot clef attendu est COUL <--' ;
    FINSI ;
  SINON ;
    VEC1     = VECT Q MOD1 (MOTS 'QX' 'QY') 2.E-6 ;
  FINSI ;
FINP VEC1 ;

* BOUCLE SUR TOUS LES PAS DE TEMPS ET TRACE VECTEUR FLUX DE CHALEUR
SI ITRAC ;
  REPE B1 N1 ;
    T_I      = TAB1 . 'TEMPERATURES' . (&B1 - 1) ;
    VF_I     = VECFLU T_I MOT MAT 'COUL' ;
    TPS_I    = TAB1 . 'TEMPS' . (&B1 - 1) ;
    MOT_I    = CHAI '[3] Vecteur flux de chaleur au temps ' TPS_I ;
    TRAC VF_I CSU 'TITR' MOT_I ;
  FIN B1 ;
FINSI ;

* VECTEUR FLUX ET CHAMP DE TEMPRATURE SOUS FORME DE LIGNES D'ISOVALEURS
OPTI 'ISOV' 'LIGN' ;
SI ITRAC ;
  TRAC VF_I T_I SU CSU 15 'TITR' MOT_I ;
FINSI ;
OPTI 'ISOV' 'SURF' ;









************************************************************************
* CHAPITRE 4 : THERMIQUE NON LINEAIRE TRANSITOIRE                      *
*              AJOUT DE CONVECTION ET DE RAYONNEMENT                   *
************************************************************************


* RECUPERATION DE LA LIGNE A DROITE
* ---------------------------------
X1       = COOR SU 1 ;
PLD      = X1 POIN 'EGAL' LONG ;
LD       = CSU ELEM 'APPU' 'STRI' PLD ;


* MODELE DE CONVECTION SUR LA LIGNE DE DROITE
* ET MATERIAU REPRESENTANT LE COEFFICIENT D'ECHANGE
* -------------------------------------------------
MOC      = MODE LD 'THERMIQUE' 'CONVECTION' ;
MAC      = MATE MOC 'H' 100. ;


* CHARGEMENT DE CONVECTION REPRESENTANT LA TEMPERATURE
* EXTERIEURE (MAINTENUE)
* ----------------------------------------------------
CHTC     = MANU 'CHPO' LD 1 'T' T0 ;
CHACONV  = CHAR 'TECO' CHTC EVT2 ;


* MODELE DE RAYONNEMENT SUR LA LIGNE DU HAUT
* ET MATERIAU REPRESENTANT L'EMISSIVITE
* ------------------------------------------
MOR      = MODE LHAUT 'THERMIQUE' 'RAYONNEMENT' 'INFINI' ;
MAR      = MATE MOR 'EMIS' 0.8 ;


* CHARGEMENT DE RAYONNEMENT REPRESENTANT LA TEMPERATURE
* EXTERIEURE (MAINTENUE)
* -----------------------------------------------------
CHTR     = MANU 'CHPO' LHAUT 1 'T' T0 ;
CHARAYE  = CHAR 'TERA' CHTR EVT2 ;


* REDEFINITION DE LA TABLE TAB1 POUR PASAPAS
* ------------------------------------------
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MOT ET MOC ET MOR ;
TAB1 . 'CARACTERISTIQUES'    = MAT ET MAC ET MAR ;
TAB1 . 'BLOCAGES_THERMIQUES' = BLT ;
TAB1 . 'CHARGEMENT'          = CHT ET CHACONV ET CHARAYE ;
TAB1 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' (0.02 * TPSFIN) TPSFIN ;
TAB1 . 'TEMPERATURES'        = TABL ;
TAB1 . 'TEMPERATURES' . 0    = T_INI ;
TAB1 . 'CELSIUS'             = VRAI ;


* APPEL A PASAPAS
* ---------------
PASAPAS TAB1 ;


* POST TRAITEMENT
* ---------------
* EVOLUTION TEMPORELLE DE LA TEMPERATURE
EV11     = EVOL 'ROUG' 'TEMP' TAB1 'TEMPERATURES' 'T' PMIL ;
EV22     = EVOL 'BLEU' 'TEMP' TAB1 'TEMPERATURES' 'T' PG ;
TL       = TABL ;
TL . 1   = 'TIRR' ;
TL . 2   = 'TIRR' ;
TL . 'TITRE' = TABL ;
TL . 'TITRE' . 1 = 'PMIL cond seule' ;
TL . 'TITRE' . 2 = 'PG   cond seule' ;
TL . 'TITRE' . 3 = 'PMIL cond+conv+ray' ;
TL . 'TITRE' . 4 = 'PG   cond+conv+ray' ;
SI ITRAC ;
  DESS (EV1 ET EV2 ET EV11 ET EV22) 'LEGE' TL 'TITR' '[4] Temperature vs Temps' ;
FINSI ;

* BOUCLE SUR TOUS LES PAS DE TEMPS ET TRACE DU CHAMP DE TEMPERATURES
N1       = DIME (TAB1 . 'TEMPERATURES') ;
SI ITRAC ;
  REPE B1 N1 ;
    T_I      = TAB1 . 'TEMPERATURES' . (&B1 - 1) ;
    TPS_I    = TAB1 . 'TEMPS' . (&B1 - 1) ;
    MOT_I    = CHAI '[4] Temperature au temps ' TPS_I ' (conv. + ray.)' ;
    TRAC T_I SU CSU 'TITR' MOT_I LISO1 ;
  FIN B1 ;
FINSI ;

* BOUCLE SUR TOUS LES PAS DE TEMPS ET TRACE VECTEUR FLUX DE CHALEUR
SI ITRAC ;
  REPE B1 N1 ;
    T_I      = TAB1 . 'TEMPERATURES' . (&B1 - 1) ;
    VF_I     = VECFLU T_I MOT MAT 'COUL' ;
    TPS_I    = TAB1 . 'TEMPS' . (&B1 - 1) ;
    MOT_I    = CHAI '[4] Vecteur flux de chaleur au temps ' TPS_I ;
    TRAC VF_I CSU 'TITR' MOT_I ;
  FIN B1 ;
FINSI ;


* SAUVEGARDE DES OBJETS CREES
* ---------------------------
OPTI 'SAUV' 'formation_debutant_2_thermique.sauv' ;
SAUV ;


FIN ;






