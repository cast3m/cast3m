* fichier :  ther8.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST
SAUT PAGE;
*
***************************************************************
*                                                             *
*   TEST THER8                                                *
*                                                             *
*   TRANSFERT DE CHALEUR AVEC CONVECTION EN 2D                *
*                                                             *
*   Test NAFEMS numero T4                                     *
*                                                             *
*                                                             *
*  description                                                *
*  -----------                                                *
*                                                             *
*            <- 0.6 m ->                                      *
*           D           C  _____                              *
*            .----------.       |                             *
*           /|          |       |                             *
*           /|          |       |                             *
*           /|          |       |                             *
*           /|          |       |                             *
*           /|          |     1.0 m                           *
*           /|          |       |                             *
*           /|          | __    |                             *
*           /|          |  |    |                             *
*           /|          | 0.2 m |                             *
*            .----------. _|____|                             *
*           A           B                                     *
*                                                             *
*                                                             *
*    conditions aux limites                                   *
*    ----------------------                                   *
*                                                             *
*       - temperature imposee sur la droite AB :              *
*                     T = 100 degres Celcius                  *
*                                                             *
*       - convection sur droites BC et CD :                   *
*           Temperature exterieure de 0. degres Celcius       *
*                                                             *
*       - droite DA isolee : pas de convection                *
*                                                             *
*    On cherche la temperture au point E. La valeur trouvee   *
*    est ensuite comparee a la valeur theorique.              *
*                                                             *
***************************************************************
*
OPTI ECHO 0;
*
OPTI DIME 2;
OPTI ELEM QUA8;
*
GRAPH = 'N';
*
*-------------------- geometrie : maillage --------------------*
*
PA = 0.  0.;
PB = 0.6 0.;
PC = 0.6 1.0;
PD = 0.  1.0;
*
N1 = 5;
N2 = 3;
D1 = PA DROI N2 PB;
D2 = PB DROI N1 PC;
D3 = PC DROI N2 PD;
D4 = PD DROI N1 PA;
*
SUR1 = DALL D1 D2 D3 D4 PLAN;
*
SI (NEG GRAPH 'N');
  TITR 'THER8 : MAILLAGE';
  TRAC QUAL SUR1;
FINSI;
*
*------------------ modeles - materiaux -----------------------*
*
MODL1 = MODE SUR1 THERMIQUE ISOTROPE QUA8;
MATR1 = MATE MODL1 K 52.0;
*
MODL2 = MODE (D2 ET D3) thermique CONVECTION;
MATR2 = MATE MODL2 H 750.0;
*
*---------------- matrices de conductivite --------------------*
*
COND1 = CONDUCTIVITE MODL1 MATR1;
COND2 = CONDUCTIVITE MODL2 MATR2;
*
*------------------- temperatures imposees --------------------*
*
*    - Cote AB (droite D1) : temperature constante
*                             de 100 degres celcius
*
BLOCD1 = BLOQ D1 'T';
*
TEMPD1 = DEPI BLOCD1 100.;
*
*-------- flux equivalent a la condition de convection --------*
*
* Temperature ambiante de 0. degres Celcius
*
CNVBC = CONVECTION MODL2 MATR2 T 0.;
*
*------------------ assemblage des flux -----------------------*
*
FLU1 = TEMPD1 ET CNVBC;
*
*---------------- assemblage des conductivites ----------------*
*
COND3 = COND1 ET COND2 ET BLOCD1;
*
*-------------------- resolution ------------------------------*
*
CHPO1 = RESO COND3 FLU1;
*
*---------------- extraction des resultats --------------------*
*
* Trace facultatif de la repartition de temperature
*
SI (NEG GRAPH 'N');
   TITR 'THER8 : TEMPERATURE';
   TRAC SUR1 CHPO1;
FINSI;
*
PE = SUR1 POIN PROC (0.6 0.2);
THETPE2 = EXTR CHPO1 T PE;
THETPE1 = 18.3;
*
ERG = 100 *(ABS ((THETPE2 - THETPE1) / THETPE1));
*
*---------------- affichage des resultats ----------------------*
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
*
MESS '    Temperature theorique en E : ' THETPE1;
MESS '    Temperature calculee en E  : ' THETPE2;
MESS '       Soit un ecart de : 'ERG '%';
SAUT 1 LIGN;
*
*---------------- code fonctionnement --------------------------*
*
SI (ERG <EG 5);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;
SAUT 1 LIGN;
*
TEMPS;
*
FIN;
*







