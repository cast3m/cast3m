* fichier :  elas12.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas12.dgibi: Jeux de données        *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*_________________________________________________________
*   TEST ELAS12                                          *
*                                                        *
*   ARC MINCE BI-ARTICULE EN FLEXION PLANE               *
*                                                        *
*   Test V.P.C.S. numero SSLL08/90                       *
*                                                        *
*   Reference : P. Dellus, Resistance des materiaux,     *
*               Paris, Technique et Vulgarisation, 1958  *
*                                                        *
*     La structure est composee d'un arc de cercle d'un  *
*   metre de rayon moyen, et de section circulaire creuse*
*   Le diametre interieur est de 0.016 m, le diametre    *
*   exterieur est de 0.02 m.                             *
*                                                        *
*     Une extremite (point A) est articule (UA = VA =0.) *
*   l'autre extremite (point B) est bloque suivant y     *
*   (VB = 0.). La structure est chargee par une force    *
*   ponctuelle verticale au point C (centre de l'arc).   *
*                                                        *
*     On se propose de calculer les rotations en A et B, *
*   ainsi que les deplacements horizontal du point B et  *
*   vertical du point C.                                 *
*                                                        *
*     Les valeurs trouvees sont ensuites comparees aux   *
*   valeurs de reference obtenues par solution           *
*   analytique.                                          *
*                                                        *
*_________________________________________________________
OPTI DIME 3;
OPTI ELEM SEG2;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  maillage - geometrie                  *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
PO =  0. 0. 0.;
PA = -1. 0. 0.;
PC =  0. 1. 0.;
PB =  1. 0. 0.;
*
C1 = PA CERC 6 PO PC;
C2 = PC CERC 6 PO PB;
*
SUR1 = C1 ET C2;
*
SI (NEG GRAPH 'N');
   TITR 'ELAS12 : MAILLAGE';
   TRAC (0. 0. 1000.) QUAL SUR1;
FINSI;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*             calcul de la rigidite                      *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE POUT;
MATR1 = MATE MODL1 YOUN 2.E11 NU 0.3 TORS 1000.
             SECT 1.131E-4 INRY 4.637E-9 INRZ 4.637E-9;
RIGI1 = RIGI MODL1 MATR1;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*              conditions aux limites                    *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = BLOQ UX PA;
CL2 = BLOQ UY PA;
CL3 = BLOQ UY PB;
*
CL4 = BLOQ UZ SUR1;
CL5 = BLOQ RX SUR1;
*
RIGI1 = RIGI1 ET CL1 ET CL2 ET CL3 ET CL4 ET CL5;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                     chargement                         *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CHAR1 = FORC (0. -100 0.) PC;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*        resolution : champs de deplacements             *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
DEPL1 = RESO RIGI1 CHAR1;
*
SI (NEG GRAPH 'N');
  TITR' ELAS12 : DEFORMEE? CHARGEMENT, REACTIONS';
  VECT1 = VECT CHAR1 FX FY FZ 4.E-3 ROUGE;
  REAC1 = REAC RIGI1 DEPL1;
  VECT2 = VECT REAC1 FX FY FZ 4.E-3 BLEU;
  DEF0 = DEFO SUR1 DEPL1 0.;
  DEF1 = DEFO SUR1 DEPL1 2. (VECT1 ET VECT2) VERT;
  TRAC (0. 0. 1000.) SUR1 (DEF0 ET DEF1);
FINSI;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               extraction des resultats                 *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
* Rotation au noeud A :
RZA1 = -3.0774E-2;
RZA2 = EXTR DEPL1 RZ PA;
ERG1 = 100 * (ABS ((RZA1 - RZA2) / RZA1));
*
* Rotation au noeud B :
RZB1 = 3.0774E-2;
RZB2 = EXTR DEPL1 RZ PB;
ERG2 = 100 * (ABS ((RZB1 - RZB2) / RZB1));
*
* Deplacement vertical du noeud C :
UYC1 = -1.9206E-2;
UYC2 = EXTR DEPL1 UY PC;
ERG3 = 100 * (ABS ((UYC1 - UYC2) / UYC1));
*
* Deplacement horizontal au noeud B :
UXB1 = 5.3912E-2;
UXB2 = EXTR DEPL1 UX PB;
ERG4 = 100 * (ABS ((UXB1 - UXB2) / UXB1));
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*             affichage des resultats                    *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MESS 'RESULTATS : TEST ELAS12';
MESS '-----------------------';
SAUT LIGN;
*
MESS '  Rotation au noeud A';
MESS '  -------------------';
MESS '     Valeur theorique :' RZA1 'rad';
MESS '     Valeur calculee  :' RZA2 'rad';
MESS '               Ecart  :' ERG1 '%';
SAUT LIGN;
*
MESS '  Rotation au noeud B';
MESS '  -------------------';
MESS '     Valeur theorique :' RZB1 'rad';
MESS '     Valeur calculee  :' RZB2 'rad';
MESS '               Ecart  :' ERG2 '%';
SAUT LIGN;
*
MESS '  Deplacement vertical du noeud C';
MESS '  -------------------------------';
MESS '     Valeur theorique :' UYC1 'm';
MESS '     Valeur calculee  :' UYC2 'm';
MESS '               Ecart  :' ERG3 '%';
SAUT LIGN;
*
MESS '  Deplacement horizontal au noeud B';
MESS '  ---------------------------------';
MESS '     Valeur theorique :' UXB1 'm';
MESS '     Valeur calculee  :' UXB2 'm';
MESS '               Ecart  :' ERG4 '%';
SAUT LIGN;
*
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                    code fonctionnement                 *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
ERGMAX = MAXI (PROG ERG1 ERG2 ERG3 ERG4);
SI (ERGMAX <EG 5.);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;
SAUT LIGN;

TEMPS;

FIN;




