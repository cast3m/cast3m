* fichier : temps.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************

* Presentation : Ce  cas-test  de  Verification  permet  de  tester  les
*                differentes  syntaxes de la  directive / operateur TEMP
*                qui a pour charge de faire des mesures de temps.
*
* Creation : 09/07/2015
* Createur : C. BERTHINIER
*******************************************

OPTI DIME 2;
OPTI ELEM QUA4;
x = prog 0. PAS 1. 2.E6;
y = x + x;
LIST (VALE ELEM);


************** TEMPS PASSE : IMPRESSION ECRAN ****************

* Simple message du temps passe
TEMP;

* Impression a l'ecran des valeurs des temps passes dans les operateurs
TEMP 'IMPR';

* Impression resumee des valeurs des temps passes dans les operateurs
* ordonne en sens decroissant
TEMP 'IMPR' 'SOMM' 'CPU';


************** MEMOIRE ****************

* Impression a l'ecran de l'occupation memoire ESOPE
TEMP 'PLAC';

* Recuperation du nombre de segments actifs et taille correspondante
A B = TEMP 'SGAC';

* Impression a l'ecran du nombre de segments laisses actifs apres les
* passages dans les operateurs
TEMP 'SGAC' 'IMPR';


************** TEMPS PASSE : RECUPERATION ****************

* Recuperation du temps passe dans un ENTier
tcpu = TEMP 'ENTI' 'CPU';
tclk = TEMP 'ENTI' 'HORLOGE';
mess 'MESSAGE PERSO: temps HORLOGE =' tclk  ' / temps CPU =' tcpu ;

* Recuperation dans une table de ces valeurs
TAB1 = TEMP 'NOEC' ;

* Listage de tous les indices de la table devant exister
LIST TAB1;
LIST TAB1.'TEMPS_HORLOGE';
LIST TAB1.'TEMPS_CPU'    ;
LIST TAB1.'APPELS'       ;
LIST TAB1.'EFFICACITE'   ;

* Listage du nombre d'appels a OPTI (2 sur le MAITRE ==> ASSISTANT 0)
LIST TAB1.'APPELS'.'OPTI';

* Verification pour ERREUR dans Cast3M
OPTI ECHO 0;
ENTI1 = EXTR TAB1.'APPELS'.'OPTI' 1;
SI (NEG ENTI1 2);
  MESS 'Disfonctionnement dans l"operateur TEMP';
  MESS 'Valeur attendue :' 2 ' Valeur extraite :' ENTI1;
  ERRE 5;
FINS;
OPTI ECHO 1;


************** TEMPS PASSE : RAZ + RECUPERATION ****************

* Remise a zero du temps
TEMP 'ZERO';
TAB1  = TEMP 'NOEC' ;

* Listage du nombre d'appels a TEMP (1 sur le MAITRE ==> ASSISTANT 0)
LIST TAB1.'APPELS'.'TEMP';

* Verification pour ERREUR dans Cast3M
OPTI ECHO 0;
ENTI2 = EXTR TAB1.'APPELS'.'TEMP' 1;
SI (NEG ENTI2 1);
  MESS 'Disfonctionnement dans l"operateur TEMP ZERO';
  MESS 'Valeur attendue :' 1 ' Valeur extraite :' ENTI2;
  ERRE 5;
FINS;
OPTI ECHO 1;

* Fin du cas-test
FIN;







