* fichier :  lyre3.dgibi
************************************************************************
* Section : Thermique Mecanique
************************************************************************
* CAS TEST DU 91/06/13    PROVENANCE : TEST

*******************************************
*    Test lyre3.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

*******************************************
*                                         *
*            LYRE-VICTUS 3                *
*                                         *
*   ESSAI ELEMENT TUYAU (DROIT ET COUDE)  *
*                                         *
*   description :                         *
*   -----------                           *
*   Test en statique lineaire, chargement *
*   thermique.                            *
*   La structure est constituee d'elements*
*   TUYAU :                               *
*     - 7 portions de tuyau droit,        *
*     - 6 portions de tuyau coude.        *
*                                         *
*   Proprietes du materaiux :             *
* - Module d'Young :         YOUN = 2.e11 *
* - Coefficient de Poisson : NU   = 0.3   *
* - Dilatabilite thermique : ALPH = 18.e-6*
*                                         *
*   Conditions aux limites :              *
*     Encastrement aux extremites         *
*    (P1 et P14)                          *
*                                         *
*   Chargement :                          *
*     Chargement thermique : Elevation de *
*     temperature de 400 degres Celcius.  *
*     Le poids propre est neglige.        *
*     Test corrig‚ le 10/09/92            *
*******************************************

OPTI DIME 3;
OPTI ELEM SEG2;
*+++++++++++++++++++++++++++++++++++++++++*
*               GEOMETRIE : MAILLAGE      *
*+++++++++++++++++++++++++++++++++++++++++*

OEIL = 100 -100 100;

P1  = 0.    0.     0.;
P2  = 0.79  0.     0.;
P3  = 1.095 -.305  0.;
P4  = 1.095 -1.155 0.;
P5  = 1.400 -1.460 0.;
P6  = 2.480 -1.460 0.;
P7  = 2.785 -1.460 .305;
P8  = 2.785 -1.460 1.155;
P9  = 2.785 -1.155 1.460;
P10 = 2.785  -.305 1.460;
P11 = 2.785  0.    1.155;
P12 = 2.785  0.    0.305;
P13 = 3.090  0.    0.;
P14 = 3.880  0.    0.;

T = TABL;
T.1 = P1  DROI 10 P2;
T.2 = P3  DROI 11 P4;
T.3 = P5  DROI 14 P6;
T.4 = P7  DROI 11 P8;
T.5 = P9  DROI 11 P10;
T.6 = P11 DROI 11 P12;
T.7 = P13 DROI 12 P14;

C = TABL;
C1 = 0.79  -0.305 0.;
C2 = 1.4   -1.155 0.;
C3 = 2.48  -1.460 0.305;
C4 = 2.885 -1.155 1.155;
C5 = 2.885 -0.305 1.155;
C6 = 3.190  0.0   0.305;

C.1 = P2  CERC 12 C1 P3;
C.2 = P4  CERC 12 C2 P5;
C.3 = P6  CERC 12 C3 P7;
C.4 = P8  CERC 12 C4 P9;
C.5 = P10 CERC 12 C5 P11;
C.6 = P12 CERC 12 C6 P13;

SUR1 = T.7;
I = 0;
REPETER BLOC1 6;
  I = I + 1;
  SUR1 = SUR1 ET T.I ET C.I;
FIN BLOC1;

SI (NEG GRAPH 'N');
   TITR 'LYRE3 : MAILLAGE';
   TRAC OEIL 'QUAL' SUR1;
FINSI;

*+++++++++++++++++++++++++++++++++++++++++*
*     CALCUL DE LA RIGIDITE               *
*+++++++++++++++++++++++++++++++++++++++++*
VECT1 = TABL;

VECT1.1 = ( 0.  0. -1.);
VECT1.2 = ( 0.  0.  1.);
VECT1.3 = ( 0. -1.  0.);
VECT1.4 = (-1.  0.  0.);
VECT1.5 = (-1.  0.  0.);
VECT1.6 = ( 0. -1.  0.);

MODL1 = MODE T.7 MECANIQUE ELASTIQUE
                 ISOTROPE TUYA;
MATR1 = MATE MODL1 YOUN 2.E11 NU 0.3
                   ALPH 18.E-6
                   EPAI 8.18E-3
                   RAYON 109.59E-3;
RIGI1 = RIGI MODL1 MATR1;

I = 0;
REPETER BLOC2 6;
  I = I + 1;

  MODL2 = MODE T.I MECANIQUE ELASTIQUE
                   ISOTROPE TUYA;
  MATR2 = MATE MODL2 YOUN 2.E11 NU 0.3
                     ALPH 18.E-6
                     EPAI 8.18E-3
                     RAYON 109.59E-3;
  RIGI2 = RIGI MODL2 MATR2;

  MODL3 = MODE C.I MECANIQUE ELASTIQUE
                   ISOTROPE TUYA;
  MATR3 = MATE MODL3 YOUN 2.E11 NU 0.3
                     ALPH 18.E-6
                     EPAI 8.18E-3
                     RAYON 109.59E-3
                 RACO 305.E-3 VECT VECT1.I;
  RIGI3 = RIGI MODL3 MATR3;

  MODL1 = MODL1 ET MODL2 ET MODL3;
  MATR1 = MATR1 ET MATR2 ET MATR3;
  RIGI1 = RIGI1 ET RIGI2 ET RIGI3;

FIN BLOC2;

*+++++++++++++++++++++++++++++++++++++++++*
*         RESOLUTION : CALCUL ELASTIQUE   *
*+++++++++++++++++++++++++++++++++++++++++*

* Conditions aux limites
CL1   = BLOQ DEPL ROTA P1;
CL2   = BLOQ DEPL ROTA P14;
RIGI1 = RIGI1 ET CL1 ET CL2;

* Chargement thermique
CHPOTEMP = MANU CHPO SUR1 1 'T' 400.;
SIGM1    = THET MODL1 MATR1 CHPOTEMP;
CHAR1    = BSIG MODL1 SIGM1 MATR1;

* Resolution
DEPL1    = RESO RIGI1 CHAR1;

* Trace
SI (NEG GRAPH 'N');

  TITR 'LYRE3 : DEFORMEE';
  DEF0  = DEFO SUR1 DEPL1 0. NOIR;
  DEF1  = DEFO SUR1 DEPL1 1. BLEU;
  TRAC OEIL (DEF0 ET DEF1);
FINSI;

*+++++++++++++++++++++++++++++++++++++++++*
*          extraction des resultats       *
*+++++++++++++++++++++++++++++++++++++++++*
V1P6 = -1.374E-2;
V2P6 = EXTR DEPL1 UY P6;
ERG1 = 100. * (ABS ((V1P6 - V2P6) / V1P6));

U1P7 = 1.070E-2;
U2P7 = EXTR DEPL1 UX P7;
ERG2 = 100. * (ABS ((U1P7 - U2P7) / U1P7));

W1P9 = 1.301E-2;
W2P9 = EXTR DEPL1 UZ P9;
ERG3 = 100. * (ABS ((W1P9 - W2P9) / W1P9));

*+++++++++++++++++++++++++++++++++++++++++*
*          affichage des resultats        *
*+++++++++++++++++++++++++++++++++++++++++*
MESS 'RESULTATS : TEST LYRE VICTUS 3 ';
MESS '------------------------------ ';
SAUT LIGN;

MESS '   Deplacement v (m) du point P6 ';
MESS '   ----------------------------- ';
MESS '      Valeur theorique : ' V1P6 'm';
MESS '      Valeur calculee  : ' V2P6 'm';
MESS '                Ecart  : ' ERG1 '%';
SAUT LIGN;

MESS '   Deplacement u (m) du point P7 ';
MESS '   ----------------------------- ';
MESS '      Valeur theorique : ' U1P7 'm';
MESS '      Valeur calculee  : ' U2P7 'm';
MESS '                Ecart  : ' ERG2 '%';
SAUT LIGN;

MESS '   Deplacement w (m) du point P9 ';
MESS '   ----------------------------- ';
MESS '      Valeur theorique : ' W1P9 'm';
MESS '      Valeur calculee  : ' W2P9 'm';
MESS '                Ecart  : ' ERG3 '%';
SAUT LIGN;

*+++++++++++++++++++++++++++++++++++++++++*
*         Code de fonctionnement          *
*+++++++++++++++++++++++++++++++++++++++++*
ERGMAX = MAXI (PROG ERG1 ERG2 ERG3);

SI (ERGMAX <EG 3.);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;
SAUT LIGN

TEMPS;

FIN;




