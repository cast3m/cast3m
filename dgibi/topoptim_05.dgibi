* fichier topoptim_05.dgibi
************************************************************************
************************************************************************

************************************************************************
** Topology optimization of a simple 2D structure subjected to a
** mechanical loading, with penalty factor, GSF, a hole and an frozen
** area.
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V2.0 2017/04/18 Updated to make it compatible with TOPOPTIM V2.0
** V1.0 2014/04/28 Original version compatible with TOPOPTIM V1.0
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' QUA4 ;

* Mesh
p0 = 0.0 0.0 ;
p1 = 0.0 60.0 ;
p2 = 30.0 0.0 ;
p3 = 0.0 30.0 ;
p4 = 30.0 25.0 ;
p5 = 15.0 15.0 ;
p6 = 19.0 15.0 ;
lgn0 = DROI 60 p0 p1 ;
lgn1 = DROI 30 p3 p1 ;
ELIM lgn0 lgn1 0.01 ;
msh0 = TRAN lgn0 30 p2 ;
tmp0 = msh0 POIN 'SPHE' p5 p6 4.0 ;
tmp0 = msh0 ELEM 'APPU' 'STRI' tmp0 ;
msh0 = DIFF msh0 tmp0 ;

p2 = msh0 POIN 'PROC' p2 ;
msh1 = msh0 POIN 'DROI' p3 p4 2.5 ;
msh1 = msh0 ELEM 'APPU' 'STRI' msh1 ;

* Model and material
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' ;
mat0 = MATE mod0 'YOUN' 210.0e9 'NU' 0.3 ;

* Boundary conditions and loading
bc0 = BLOQ 'UX' 'UY' lgn1 ;
load0 = FORC (1.0 0.0) p2 ;

* Factor evolutions
pfct0 = EVOL 'MANU' 'CYCLES' (PROG  5 15) 'P' (PROG 1.0 3.0) ;
qfct0 = EVOL 'MANU' 'CYCLES' (PROG 10 90) 'Q' (PROG 1.0 5.0) ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ;
mdl0.'BLOCAGES_MECANIQUES' = bc0 ;
mdl0.'CHARGEMENT'          = load0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_LINEAIRE' = mdl0 ;
tab0.'ZONE_FIGEE'          = msh1 ;
tab0.'TAUX_FILTRAGE'       = 10 ;
tab0.'FACTEUR_P'           = pfct0 ;
tab0.'FACTEUR_Q'           = qfct0 ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Plot to screen
topo0 = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
SI graph0 ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;



