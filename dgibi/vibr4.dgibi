* fichier :  vibr4.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************

*******************************************************
*           Test vibr4.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI TRAC PSC ;
FINSI ;

SAUT PAGE;
****************************************************************
*                               VIBR4
****************************************************************
*                                                              *
*  Mots-clés : Vibrations, calcul modal, 3D                    *
*                                                              *
*  TEST VIBR4                                                  *
*                                                              *
*  CAS TEST DU 91/06/13     PROVENANCE : TEST                  *
*                                                              *
*  PLAQUE EPAISSE EN FORME DE LOSANGE                          *
*                                                              *
*  Test NAFEMS numero fv22                                     *
*                                                              *
*      On cherche les frequences et modes propres d'une plaque *
*   en forme de losange d'un metre d'epaisseur.                *
*                                                              *
*      Les conditions aux limites imposees (voir dessin)sont   *
*   les suivantes :                                            *
*            - ux  = uy  = rz = 0 pour tout les noeuds,        *
*            - uz' = rx' = ry = 0 pour les 4 cotes.            *
*                                                              *
*  dessin                                                      *
*  ------                                                      *
*                                                              *
*                        axe y'                                *
*                          |                                   *
*                          |                                   *
*                          |   axe x'                          *
*                   P4      ----->                             *
*                    ----------------- P3                      *
*          axe y    /               /                          *
*            |     /               /                           *
*            |    /               /                            *
*            |   /               /                             *
*            |  /               /                              *
*            | /               /                               *
*             -----------------     ---> axe x                 *
*            P1               P2                               *
*                                                              *
*             |_____10.m______|                                *
*                                                              *
*                                                              *
*                                                              *
****************************************************************
OPTI DIME 3;
OPTI ELEM QUA8;
OPTI EPSI LINEAIRE;
*
*--------------------------------------------------------------*
*                  geometrie - maillage                        *
*--------------------------------------------------------------*
*
ALPHA = 45.; COS = COS ALPHA; SIN = SIN ALPHA;
*
P1 =   0. 0. 0.;
P2 =  10. 0. 0.;
P3 = (10. * (1 + COS)) (10. * SIN) 0.;
P4 = (10. * COS) (10. * SIN) 0.;
*
OEIL = 7. -100. 70.;
*
D12 = P1 DROI 6 P2;
D23 = P2 DROI 6 P3;
D34 = P3 DROI 6 P4;
D41 = P4 DROI 6 P1;
*
SUR1 = DALL D12 D23 D34 D41 PLAN;
*
SI (NEG GRAPH 'N');
  TRAC OEIL 'QUAL' SUR1;
FINSI;
*
*--------------------------------------------------------------*
*       MODE - materiau - caracteristique                      *
*                   rigidite - masse                           *
*--------------------------------------------------------------*
*
MOD1 = MODE SUR1 MECANIQUE COQ8;
MAT1 = MATE  MOD1 YOUN 2.E11 NU 0.3 RHO 8.E3;
CAR1 = CARA MOD1 EPAI 1.;
MAT1 = MAT1 ET CAR1;
RIG1 = RIGI MOD1 MAT1 ;
MAS1 = MASS MOD1 MAT1;
*
*--------------------------------------------------------------*
*               conditions aux limites                         *
*--------------------------------------------------------------*
*   - ux  = uy  = rz = 0 pour tout les noeuds,
*   - uz' = rx' = ry' = 0 pour les 4 cotes.

si (faux);

CL1 = BLOQ UX SUR1;
CL2 = BLOQ UY SUR1;
CL3 = BLOQ RZ SUR1;

CL4 = BLOQ UZ (D12 ET D34);
CL5 = BLOQ RX (D12 ET D34);
CL6 = BLOQ RY (D12 ET D34);

DIR1 = COS SIN 0.; DIR2 = (-1 * SIN) COS 0.;
CL7 = BLOQ UZ (D23 ET D41);
CL8 = BLOQ ROTA DIRECTION DIR1 (D23 ET D41);
CL9 = BLOQ ROTA DIRECTION DIR2 (D23 ET D41);

RIG1 = RIG1 ET CL1 ET CL2 ET CL3 ET CL4 ET CL5
            ET CL6 ET CL7 ET CL8 ET CL9;

sinon;
* CL1 = BLOQ 'UX' 'UY' 'RZ' SUR1;
CL1 = BLOQ 'UX' 'UY' SUR1;
CL2 = BLOQ 'UZ' 'RX' 'RY' (D12 ET D23 ET D34 ET D41);

RIG1 = RIG1 ET CL1 ET CL2 ;

finsi;
*
*--------------------------------------------------------------*
*           calcul des frequences propres                      *
*--------------------------------------------------------------*
*
* table des frequence propres theoriques
FRE1 = TABLE;
FRE1.1 = 133.95;
FRE1.2 = 201.41;
FRE1.3 = 265.81;
FRE1.4 = 282.74;
FRE1.5 = 334.45;
FRE1.6 = 386.62;

* Utilisation de l'operateur VIBR avec l'option PROCH
* + listreel de 6 valeurs :
* 6 repetitions de la methode de la puissance inverse avec 6 decalage
* spectral
LIST1  = PROG FRE1.1 FRE1.2 FRE1.3 FRE1.4 FRE1.5 FRE1.6;
RESUL2 = VIBR PROC LIST1 RIG1 MAS1;
* RESUL2 = VIBR PROC (prog 100.) (lect 6) RIG1 MAS1;

* Utilisation de l'operateur VIBR avec l'option IRAM :
*  opti impi 5;
RESUL3 = VIBR IRAM 100. 6 RIG1 MAS1;
*  opti impi 0;

*
*--------------------------------------------------------------*
*               extraction des resultats ...                   *
*--------------------------------------------------------------*
*
*... via la procedure explorer :
*
  toto = TABL;
  toto . 'MAILLAGE' = SUR1;
* * +en interactif
*   OPTI TRAC X ;
* EXPLORER RESUL3 (mots 'TABL' 'DEF0' 'DEFO') toto;
* +en ps
  OPTI TRAC PSC ;
  EXPLORER RESUL3 (mots 'TABL' 'DEF0' 'DEFO') toto;
*
*... a la main :
*
FRE3 = TABLE; ERG3 = TABLE;
FRE2 = TABLE; ERG2 = TABLE;
DEP  = TABLE; DEF = TABLE;

OPTI ECHO 0;
MESS ' RESULTATS ';
MESS ' ----------------------------------------------------------';
MESS '      |    Frequence propre en Hz (erreur relative en %) ';
MESS ' Mode |  Theorie |    VIBR PROCH     |    VIBR IRAM      ';
MESS ' ----------------------------------------------------------';
I = 0;
REPETER BLOC1 6;
   I = I + 1;
   FRE2.I = RESUL2 . MODES . I . FREQUENCE;
   ERG2.I = 100 * (ABS ((FRE2.I - FRE1.I) / FRE1.I));
   FRE3.I = RESUL3 . MODES . I . FREQUENCE;
   ERG3.I = 100 * (ABS ((FRE3.I - FRE1.I) / FRE1.I));
   chacha = chai I *5 ' |' FORMAT '(F9.3)' FRE1.I
          ' |' FORMAT '(F9.3)' FRE2.I ' (' FORMAT '(F5.3)' ERG2.I '%)'
          ' |' FORMAT '(F9.3)' FRE3.I ' (' FORMAT '(F5.3)' ERG3.I '%)';
   MESS chacha;
  SI (NEG GRAPH 'N');
    DEP.I  = RESUL2 . MODES . I . DEFORMEE_MODALE;
    DEPZ   = EXCO DEP.I 'UZ';
    DEF0   = DEFO SUR1 DEP.I 0. 'NOIR';
    DEF.I  = DEFO SUR1 DEP.I 1. 'BLEU';
    TRAC OEIL (DEF0 ET DEF.I);
    TRAC OEIL DEPZ SUR1 (DEFO SUR1 DEP.I 0.5 NOIR);
  FINSI;
FIN BLOC1;
MESS ' ----------------------------------------------------------';
OPTI ECHO 1;
*
*
*--------------------------------------------------------------*
*                code fonctionnement                           *
*--------------------------------------------------------------*
*
ERG2MAX = MAXI (PROG ERG2.1 ERG2.2 ERG2.3 ERG2.4 ERG2.5 ERG2.6);
ERG3MAX = MAXI (PROG ERG3.1 ERG3.2 ERG3.3 ERG3.4 ERG3.5 ERG3.6);

SI (ERG2MAX > 4.5);
  MESS 'ERREUR VIBR OPTION PROCHE';
  ERRE 5;
FINSI;
SI (ERG3MAX > 4.5);
  MESS 'ERREUR VIBR OPTION IRAM';
  ERRE 5;
FINSI;
  ERRE 0;

TEMP;

FIN;










