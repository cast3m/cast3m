* fichier : crit_pplan.dgibi
************************************************************************
* Section : Langage Objets
* Section : Maillage Autres
************************************************************************
*---------------------------------------------------------------------*
* Test du critere d'ecart a la planeite de l'operateur 'SURF', option *
* 'PLAN' : (S1 = SURF GEO1 PLAN CRIT ;)                               *
*---------------------------------------------------------------------*
'OPTI' 'DIME' 3 'ELEM' 'TET4' ;
*                                                                     *
* Mettre IG1 a VRAI pour trace :                                      *
IG1      = FAUX ;
*                                                                     *
* Contour "test" :                                                    *
P0       = 0. 0. 0. ;
P1       = 1. 0. 0. ;
P2       = 1. 1. 0. ;
P3       = 0. 1. 0. ;
N1       = 10 ;
CNT1     = (P0 'DROI' N1 P1) 'ET' (P1 'DROI' N1 P2)
  'ET' (P2 'DROI' N1 P3) 'ET' (P3 'DROI' N1 P0) ;
'DEPL' P2 'PLUS' (0. 0. 2.5) ;
*                                                                     *
* Calcul de la normale au contour selon meme methode que 'SURF' :     *
NB1      = 'NBNO' CNT1 ;
PN1      = P0 ;
PI1      = CNT1 'POIN' NB1 ;
'REPE' BOU1 NB1 ;
  PI2      = CNT1 'POIN' &BOU1 ;
  PN1      = PN1 'PLUS' (PI1 'PVEC' PI2) ;
  PI1      = PI2 ;
'FIN' BOU1 ;
PN1      = PN1 / ('NORM' PN1) ;
*                                                                     *
* Calcul du critere de planeite :                                     *
PG1      = 'BARY' CNT1 ;
XTEST1   = ('PSCA' (P2 'MOIN' PG1) PN1) / ('NORM' (P2 'MOIN' PG1)) ;
*                                                                     *
S1       = CNT1 'SURF' 'PLAN' (1.0001 * XTEST1) ;
*                                                                     *
'SI' IG1 ;
  'TRAC' S1 'TITR' 'Critere d"ecart a la planeite de "SURF" OK !' ;
'FINS' ;
*                                                                     *
'FIN' ;




