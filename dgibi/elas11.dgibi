* fichier :  elas11.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas11.dgibi: Jeux de données        *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*______________________________________________________
*   TEST ELAS11                                       *
*                                                     *
*         SOLIDE (CYLINDRE - CONE - SPHERE)           *
*            SOUMIS A UN GRADIENT                     *
*    DE TEMPERATURE (Calcul axisymetrique 2D).        *
*                                                     *
*   Test NAFEMS : numero LE11                         *
*   Nom : solid cylinder / taper / sphere temperature *
*                                                     *
*    La structure est une enceinte epaisse.  Elle est *
*   constituee d'une partie spherique surmontee d'une *
*   partie conique, elle -meme surmontee d'une partie *
*   cylindrique.                                      *
*                                                     *
*      Elle est soumis a un gradient lineaire de      *
*   temperature s'exercant radialement et axialement. *
*   Le champs de temperature s'exprime comme suit :   *
*                                                     *
*           T(x,y,z) = (x**2 + y**2)**.5 + z          *
*        ou                                           *
*           T(r,z)   = r + z                          *
*                                                     *
*    L'ensemble est maille avec des elements massifs  *
*   CU20. Le maillage est obtenue par rotation de 90  *
*   degres d'une coupe verticale.                     *
*                                                     *
*    On se propose de calculer la contrainte smyy au  *
*   point A. Cette valeur est ensuite comparee avec   *
*   la valeur de reference obtenue a l'aide d'un      *
*   calcul axisymetrique sur un maillage fin.         *
*                                                     *
*______________________________________________________
*
OPTI DIME 2;
OPTI MODE AXIS;
OPTI ELEM QUA8;
*
*=====================================================*
*             geometrie : maillage                    *
*=====================================================*
*
* Dimension en metres
*
* Points
*
PO = 0. 0.;
P1 = 0. 1.;
PA = 1. 0.;
PB = 1.4 0.;
PE = (1. * (COS 30)) (1. * (SIN 30));
PD = (1.4 * (COS 30)) 0.7;
PC = (COS 30) (SIN 30);
PE = (COS 45) (SIN 45);
PH = 0.7071 1.79;
PI = 1. 1.79;
PG = 1. 1.39;
*
* Droites, cercles et lignes
*
* coefficient de finesse du maillage
*
K = 2;
*
N1 = 1 * K;
AB = PA DROI N1 PB;
GI = PG DROI N1 PI;
EC = PE CERC N1 PO PC;
IH = PI DROI N1 PH;
*
N2 = 2 * K;
BD = PB CERC N2 PO PD;
DG = PD DROI N2 PG;
CA = PC CERC N2 PO PA;
HE = PH DROI N2 PE;
*
* maillage par elements massifs du volume
*
SUR1 = DALL AB (BD ET DG ET GI) IH (HE ET EC ET CA) PLAN;
*
SI (NEG GRAPH 'N');
  SI (K EGA 1);
    TITR 'ELAS11 : MAILLAGE GROSSIER';
  SINON;
    TITR 'ELAS11 : MAILLAGE FIN';
  FINSI;
  TRAC QUAL SUR1;
FINSI;
*
ELSUR1 = NBEL SUR1;
NOSUR1 = NBNO SUR1;
*
*=====================================================*
*                   modele - materiau                 *
*               caracteristique - rigidite            *
*=====================================================*
*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE QUA8;
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3 ALPH 2.3E-4;
RI1 = RIGI MODL1 MATR1;
*
*=====================================================*
*               Conditions aux limites                *
*=====================================================*
*
*    - deplacement vertical nul pour les faces superieure
*      et inferieure.
*
CL1 = BLOQ UZ AB;
CL2 = BLOQ UZ IH;
*
CLI1 = CL1 ET CL2;
RIG1 = RI1 ET CLI1;
*
*=====================================================*
*                  Chargement thermique               *
*=====================================================*
*
*   Le solide est soumis a un gradient de temperature
* lineaire, s'appliquant radialement et axialement.
*   La temperature est la suivante :
*
*        T(r,z) = r + z
*
* Creation du champs de temperature
*
CHPR = COOR 1 SUR1;
CHPZ = COOR 2 SUR1;
*
CHP4 = CHPR + CHPZ;
*
* Transformation du sous-type "scal" en sous-type "t"
*  pour chp4. Utilisation de l'operateur "vari".
*
LIS10 = PROG 0. 200.;
LIS20 = PROG 0. 200.;
EVOL1 = EVOL MANU SCAL LIS10 T LIS20;
CHP5 = VARI CHP4 EVOL1 T;
*
* Creation du champ de contraintes equivalentes
*
CHAM10 = THET MODL1 MATR1 CHP5;
*
* Creation du champ de forces equivalentes
*
CHAR1 = BSIG MODL1 CHAM10;
*
*=====================================================*
*       Resolution : champs de deplacements           *
*=====================================================*
*
DEP1 = RESO RIG1 CHAR1;
*
* Trace facultatif de la deformee et des reactions
*
SI (NEG GRAPH 'N');
  AMPVEC = 1.E-8;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT REA1 AMPVEC FR FZ BLEU;
  DEF0 = DEFO 0. DEP1 SUR1;
  DEF1 = DEFO 100. DEP1 SUR1 VECT1 VERT;
  TITR 'ELAS11 ; DEFORMEE, REACTIONS';
  TRAC SUR1 (DEF0 ET DEF1);
FINSI;
*
*=====================================================*
*              extraction d'une contrainte            *
*        et comparaison avec solution analytique      *
*=====================================================*
*
*    On cherche la contrainte axiale sigzz au noeud A
*
* Calcul du champs de contraintes totales.
*
CHAM1 = SIGM MODL1 MATR1 DEP1;
*
* On retranche les contraintes d'origine thermique
*
CHAM1 = CHAM1 - CHAM10;
*
* Interpolation aux noeuds du maillage.
*
CHAM2 = CHAN NOEUD MODL1 CHAM1;
CHPO10 = CHAN CHPO MODL1 CHAM2;
*
* Trace facultatif des contraintes aux noeuds.
*
SI (NEG GRAPH 'N');
   TITR 'ELAS9 : Contraintes Nodales';
   TRAC CACH SUR1 ((EXCO CHPO10 SMZZ)*1e-6);
FINSI;
*
* Extraction de la contrainte recherchee.
*
SMZZA = EXTR CHPO10 SMZZ PA;
SMZZA = SMZZA / 1.E6;
*
SMZZA1 = -105;
*
* Calcul de l'ecart avec la solution de reference.
*
ERGZZA = 100 * (ABS ((SMZZA - SMZZA1) / SMZZA1));
*
*=====================================================*
*            affichage des resultats                  *
*=====================================================*
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
*
MESS '    La valeur cherchee est la contrainte axiale au point A,';
MESS ' elle est comparee a une valeur theorique obtenue analytiquement.';
SAUT 1 LIGN;
MESS ' Contrainte verticale theorique en A :' SMZZA1 'MPa';
SAUT 1 LIGN;
MESS ' Contrainte verticale calculee en A  :' SMZZA 'MPa';
SAUT 1 LIGN;
MESS '          Soit un ecart de :' ERGZZA '%';
SAUT 2 LIGN;
*
SI (K EGA 1);
  MESS ' Maillage grossier';
SINON;
  MESS ' Maillage fin';
FINSI;
*
MESS '    Nombre d elements :' ELSUR1;
MESS '    Nombre de noeuds  :' NOSUR1;
MESS '                 Soit :' (NOSUR1 * 3) 'd.d.l.';
*
*=====================================================*
*                  code fonctionnement                *
*=====================================================*
*     L'ecart maximum entre valeur theorique et
* calculee doit etre inferieure a 2%.
*
SAUT 1 LIGNE;
SI (ERGZZA <EG 2);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*=====================================================*
*                 Temps de calcul et fin              *
*=====================================================*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
FIN;






