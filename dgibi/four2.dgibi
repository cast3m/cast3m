* fichier :  four2.dgibi
************************************************************************
* Section : Mecanique Fourier
************************************************************************
*           Test Four2.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test four2.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*************************************************************************
*  TEST FOUR2
*
*  CYLINDRE INFINI SOUS PRESSION EXTERNE(NU=0)
*
* Soit un cylindre infini soumis a une pression externe.
*
* Une analyse de flambage permet de determiner la charge critique
* associée aux 10 premiers modes de Fourier de la structure :
* (u = u°*cos(2*Teta) )
* (u = u°*cos(3*Teta) )
* (u = u°*cos(4*Teta) )
* (u = u°*cos(5*Teta) )
* ...
* Les éléments utilisés sont des éléments massifs.
*
* Comparaison a une solution analytique
*************************************************************************
*
TITRE 'CYLINDRE INFINI SOUS PRESSION EXTERNE';
OPTI DIME 2 ELEM qua8 MODE FOUR 0;
OPTI EPSI LINEAIRE;
*
*------------------ CONSTRUCTION DE LA GEOMETRIE ------------------------
*
PA1=999.5 0.;PB1=999.5 10.;PO1=0. 0.;PO2=0. 10.;
PA2=1000.5 0.;PB2=1000.5 10.;
L1 =  PA1 DROI 5 PA2;
L2 =  PA2 DROI 19 PB2;
L3 =  PB2 DROI 5 PB1;
L4 =  PB1 DROI 19 PA1;

CYL = DALLER L1 L2 L3 L4 PLAN;
SI (NEG GRAPH 'N');
  TRAC 'QUAL' CYL;
FINSI;

MOD1=MODE CYL MECANIQUE ELASTIQUE qua8;

*
*--- DECLARATION DE FOURIER NOHARM POUR LES OBJETS QUI SERONT UTILISES --
*------------- POUR PLUSIEURS NUMEROS D'HARMONIQUE ----------------------
*
OPTI MODE FOUR NOHARM;
*
*
*-------------- CONDITIONS AUX LIMITES SYMETRIQUES ----------------------
*
SYMB=SYMT CYL DEPL PA1 PO1  0.05;
SYMH=SYMT CYL DEPL PB1 PO2  0.05;
CDL=SYMB ET SYMH;
*
*-------------- MATERIAU ET CARACTERISTIQUES ----------------------------
*
MAT =MATE MOD1 YOUN 20000. NU 0.;
*
*------ DECLARATION DE FOURIER MODE 0 POUR LE CALCUL DES CONTRAINTES ----
*
OPTI MODE FOUR 0;

FP=PRES MASS MOD1 1. L2 ;
RIG=RIGI MOD1 MAT;
*
*
AAA = RIG ET CDL et (bloq PA1 UT);
U   = RESO AAA  FP ;
SIG = SIGMA U MOD1 MAT;
I   = 1;
ERRMAX = 0.;
REPE BOUC1 9;
   I = I + 1;
*
*----- DECLARATION DE FOURIER MODE I POUR L'ANALYSE DE FLAMBAGE ---------
*
   OPTI MODE FOUR I;
   MKSI = KSIGMA MOD1 MAT (SIG * -1.)  FLAM;
   RIG  = RIGI MOD1 MAT ;
*
*--------- RECHERCHE DE LA 1ERE FREQUENCE PROPRE ------------------------
*
   MODF=VIBR PROC (PROG 0.01) (RIG ET CDL) MKSI;
   W1 = MODF . MODES . 1 . FREQUENCE;
   LAMBDA1=(W1 * 2. * PI) ** 2 * (SIGN W1);
   LDUM = (20000./(1. - 0.))*((1./1000.)**3)/12.;
   LTH1 = LDUM*I*I;
   ERR1   = (LTH1 - LAMBDA1)/LTH1*100;

   SAUT 1 LIGN ;
   MESS 'Mode : ' I;
   SAUT 1 LIGN ;
   MESS '  K(SIG) SEUL  :   ON DOIT TROUVER LAMBDA=' LTH1;
   MESS '                   LE CALCUL DONNE LAMBDA=' LAMBDA1;
   MESS '     SOIT UN ECART DE                : ' ERR1 '%' ;
   SAUT 1 LIGN ;

   ERRMAX = MAXI ABS (PROG ERRMAX ERR1);
FIN BOUC1;

mess ' errmax vaut ' errmax '%';
SI (ERRMAX < 1.4D0 );
   ERRE 0;
SINON ;
   ERRE 5;
FINSI;

FIN;














