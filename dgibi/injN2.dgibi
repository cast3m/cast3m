* fichier :  injairAC.dgibi
*
*  Enceinte 2D Axisymetrique
*  Injection d'air en non adiabatique
*  Comparaison Castem 2D axi / Cathare / Solution analytique
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
'OPTI' 'MODE' 'AXIS' ;
opti trace 'X';
opti EPTR 10 ;

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

Si COMPLET;
  DTC=0.;
  n1=5 ;
  n2=10;
  n3=20;
  nf1=4;
  nf2=20;
  nf3=20;
Sinon;
  DTC=2.;
  n1=3;
  n2=3;
  n3=3;
  nf1=2;
  nf2=5;
  nf3=5;
Finsi;

*=======================================================================
* Feuille inox de protection
e4=(2.36-2.3588)/2.;
lb4=15.5;
*lb4=17.;
ROCP4=35.325e5;
h4i=e4/lb4;
* Isolant
e1=(2.46-2.36)/2.;
lb1=0.025;
ROCP1=2.392e5;
h1i=e1/lb1;
* paroi inox Riva
e3=0.02;
lb3=18.;
*lb3=19.;
ROCP3=35.325e5;
h3i=e3/lb3;
* Echange interne
Hint=15.;
* Echange externe
hext=10.;
Text=tetai;
* H equivalent
Heq=(h4i + h1i + h3i + (1./hext)+(1./Hint))**(-1.);
mess 'Heq=' Heq;

*=======================================================================

*=======================================================================
* Solution analytique
DEBPROC SOREF Tmax*'FLOTTANT' DT0*'FLOTTANT' QM*'FLOTTANT' Vr*'FLOTTANT'
 Sr*'FLOTTANT' Pnm*'FLOTTANT' tetai*'FLOTTANT' INCOND*'MOT'
 HEXT*'FLOTTANT' TEXT*'FLOTTANT' COL*'MOT';
* Les températures son en °C

Tps=0.;
nbit = 50000;

Si('EGA' INCOND 'AIR');
a1 a2 a3 a4 a5 a6 Cp=calcp tetai ;
Rinc=287.1;
SINON;
Si('EGA' INCOND 'N2');
a1 a2 a3 Cp a5 a6 a7=calcp tetai ;
Rinc=296.9;
SINON;
mess ' Incondensable inconnu';
QUITTER SOREF;
FINSI;
FINSI;
*  Rinc=292.;  Valeur pour faire mieux fiter la solution analytique avec
*  cathare en air.

mess ' Incondensable R=' Rinc;
Rhonm=Pnm/Rinc/(tetai + 273.15);
a=Cp/Rinc-1.;

Lrho= prog Rhonm;
Ltps= prog Tps  ;
Lp  = prog Pnm  ;
LT  = prog tetai;

repeter BLOC nbit;
tps=tps + dt0;
Si (tps > Tmax); quitter Bloc ; Finsi ;
Rhon=Rhonm + (DT0*Qm/Vr);
Fd=(Qm**3.)/(Rhon**2.)/(Vr**(10./3.));

A    = Cp/Rinc - 1.     ;
Alfa = Sr*hext/Rhon/Rinc;
Beta = Sr*hext*(Text + 273.15)     ;
AA = DT0/(Alfa*DT0+A)  ;

Pn = (A*DT0/(A/DT0 + Alfa)*Pnm) + (AA/Vr*Qm*Cp*(tetai + 273.15))
 + (AA*Beta);

**Pn  =Pnm + (DT0*Qm/Vr*(Rinc*Cp)/(Cp-Rinc)*(tetai + 273.15));

Tn  =(Pn/Rhon/Rinc) - 273.15;

Si('EGA' INCOND 'AIR');
a1 a2 a3 a4 a5 a6 Cp=calcp tn;
Finsi;
Si('EGA' INCOND 'N2');
a1 a2 a3 Cp a5 a6 a7=calcp tn;
FINSI;

Lrho= Lrho et (prog Rhon);
Ltps= Ltps et (prog Tps)  ;
Lp  = Lp   et (prog Pn)   ;
LT  = LT   et (prog Tn)   ;
Rhonm = Rhon;
Pnm = pn;

Fin BLOC;

evrho1 = (evol manu ltps lrho) coul col  ;
evp1   = (evol manu ltps lp) coul col    ;
evt1   = (evol manu ltps lt) coul col    ;
evmair1= evrho1* Vr                      ;
FINPROC evp1 evt1 evrho1 evmair1 ;

Tmax=500.;
DT0=1. ;
Qm=0.2  ;
Vr=17.4;
Sr=35.914;
Pnm=1.e5;
tetai=300. - 273.15;

hexteq=0.; Text = 0.;
evpa evta evrhoa evmaira = SOREF Tmax DT0 QM Vr Sr Pnm tetai 'N2'
 HEXTeq TEXT 'ROUGE';

hexteq=Heq; Text = tetai;
evp1 evt1 evrho1 evmair1 = SOREF Tmax DT0 QM Vr Sr Pnm tetai 'N2'
 HEXTeq TEXT 'VERT';


*=======================================================================
Si GRAPH;
TAB1 =TABLE ;
TAB1.'TITRE'=TABLE ;
tab1 . 1 = 'MOT' 'MARQ CROI REGU ';
tab1 . 'TITRE' . 1 = 'Sol Ref adia';
tab1 . 'TITRE' . 2 = 'Sol Ref non adia' ;

tit= chai 'Pression enceinte' ;
dess (evpa et evp1) titr tit
'GRIL' titx 's' tity 'Pa' lege tab1;

tit= chai 'Température enceinte' ;
dess (evta et evt1) titr tit
'GRIL' titx 's' tity '°C' lege tab1;

tit = chai 'Densité moyenne enceinte';
dess (evrhoa et evrho1) titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;

tit = chai 'Masse d air';
dess (evmaira et evmair1) titr tit
'GRIL' titx 's' tity 'kg' lege tab1;
Finsi;


epsi=1.e-8;

episo=0.044;
db=0.1 ;
R1=1.2 ;
R1=1.2 - episo;
h1=1.605;
h2=2.342 - episo;
hb=0.835;
PA0=R1 0.;
PAb=PA0 moins (0. hb);
PAbm=PAb moins ((R1/2.) 0.);
PA1=R1 h1;
PB1=R1 ((-1.)*h1);
PA1b=PA1 moins (0. hb);
CA1=0. 0.275     ;
CA2=(0.33*R1) H1   ;
PAX2=0. h2;
PA20= PAX2 tour -5. CA1;
PA2= PA20 tour -15. CA1;
p0=0 0 ;
paxb= p0 moins (0. hb);
p2=db 0. ;
p2b=p2 moins (0. hb);
breche = paxb  'DROI' n1 p2b ;
bas= p2b 'DROI' dini (db/n1) dfin (R1/10.) PABM
         'DROI' dini (R1/10.) dfin (db/n1) PAB;
n2= nbel bas;
plaf0 = PAX2 C n1 CA1 PA20;
plaf1 = PA20 C (n2/2) CA1 PA2;
plaf2 = PA1  C (n2/2) CA2 PA2;

plaf  =plaf0 et plaf1 et (inve plaf2);
axeh = PAX2 'DROI' n3 paxb ;
PAXM=(PAX2 plus PAXB)*0.5;

axeh = PAX2 DROI dini (h2/n3/4.) dfin (h1/n3) PAXM
            DROI dini (h1/n3) dfin (h2/n3/4.) PAXB;
n3n= nbel axeh ;
paroih=PAb d n3n PA1;

pla=plaf et (inve paroih) ;
pla = pla et (syme pla droit p0 pax2);
Si GRAPH;
trace (pla et ca1 et ca2 et PA1);
trace (plaf et paroih et axeh et breche et bas
et ca1);
Finsi;
mth= 'DALLER' (breche 'ET' bas) paroih plaf axeh;

entb = breche plus (0. 0.);
DEPL entb tour 15. P2b;
pbx2= pax2 syme 'DROITE' P0 PA0;
p0b= poin 1 entb ;
axeb = PBX2 'DROI' n3 p0b;
paroib =pab d n3 pb1;
fond =  plaf SYME 'DROITE' P0 PA0;
elim  (entb 'ET' bas et paroib et fond et axeb) epsi;
mtb= 'DALLER' (entb 'ET' bas) paroib fond axeb;
axe=axeb et axeh;
mt = mth et mtb ;
elim (mtb et bas et entb et bas) epsi;
Parois = paroib et paroih et plaf et fond et entb ;
parext = plaf et (inve paroih) et paroib et (inve fond);

parexp = parext plus (0. 0.);
debproc crec parexp*'MAILLAGE' e1*'FLOTTANT' n1*'ENTIER' col*'MOT';
cq= vide maillage;
tt=table;
repeter BLOC (nbel parexp);
ei= elem &bloc parexp;
p1=point 1 ei ;
p2=point 2 ei;
p1p2=p2 moins p1 ;
a = norm p1p2;
p1p2=p1p2*(e1/a);
pp2=p1 plus p1p2;
p3= pp2 tour 90. p1;
tt.&bloc=p3;
Si(Ega &bloc 1);
p0=p3;
sinon;
cq = cq et (p0 d 1 p3);
p0 = p3;
finsi;
*trace ( parexp et cq) 'NCLK';
fin bloc;
pp2= p2 moins p1p2;
p2= pp2 tour (-90.) p2;
cq = cq et (p3 d 1 p2);
cq = elem cq 'SEG2';
mcq= parexp regle n1 cq;
mcq= mcq coul col ;
finproc cq mcq ;


cq1 mcq1 = crec parexp e4 nf1 rouge;
cq2 mcq2 = crec cq1    e1 nf2 vert;
cq3 mcq3 = crec cq2    e3 nf3 TURQ;

mtp= (mcq1 et mcq2 et mcq3);

Si Graph;
trace mtp;
trace (elim ((mt et mtp) et (syme (mt et mtp) 'DROIT' P0 pax2)) epsi);
Finsi;

************************************************************************
* Petite disgression pour calculer la surface de l'enceinte
Si FAUX;
opti dime 3 ;

la= prog;
ln=prog;
nn=25;
repeter BLOC 40;
a= rota parext nn 90. pax2 pbx2;
sa=((mesu a 'SURF' ) * 4.);
la = la et (prog sa);
ln = ln et (prog nn);
nn= nn + 25;
fin bloc;
evmes= evol manu ln la ;
dess evmes GRIL TITR ' Surface interne de l enceinte RIVA';
opti donn 5;
Finsi ;
************************************************************************

rxt = 'TABLE' ;
rxt.'vtf'=mt ;
rxt.'axe'   = axe ;
rxt.'breche'=breche ;
rxt.'diru1'=0. 1. ;
rxt.'epsi'= epsi  ;
rxt.'pi'  = 0.5 0.5 ;
rxt.'DT0'  = (maxi (prog DT0 DTC));
rxt.'DETMAT'= VRAI ;
rxt.'RENU'  = 'RIEN' ;
rxt.'DISCR'= 'LINE';
rxt.'KPRE' = 'MSOMMET';
rxt.'N2'   = VRAI     ;
rxt.'MODTURB'  = 'LMEL'  ;
rxt.'LMEL'  = 0.01 ;
rxt.'GRAPH'  = GRAPH ;

*rxt.'THERCO'=VRAI ;
rxt.'PAROIS'=table;
rxt.'PAROIS'.'Feuille'=table ;
rxt.'PAROIS'.'Feuille'.'vtp'=mcq1;
rxt.'PAROIS'.'Feuille'.'LAMBDA'=LB4;
rxt.'PAROIS'.'Feuille'.'ROCP'=ROCP4;
rxt.'PAROIS'.'Feuille'.'Tp0'=tetai;

rxt.'PAROIS'.'Isolant'=table ;
rxt.'PAROIS'.'Isolant'.'vtp'=mcq2;
rxt.'PAROIS'.'Isolant'.'LAMBDA'=LB1;
rxt.'PAROIS'.'Isolant'.'ROCP'=ROCP1;
rxt.'PAROIS'.'Isolant'.'Tp0'=tetai;

rxt.'PAROIS'.'Inox'=table ;
rxt.'PAROIS'.'Inox'.'vtp'=mcq3;
rxt.'PAROIS'.'Inox'.'LAMBDA'=LB3;
rxt.'PAROIS'.'Inox'.'ROCP'=ROCP3;
rxt.'PAROIS'.'Inox'.'Tp0'=tetai;

rxt.'ECHEXT' = VRAI;
rxt.'parext' = cq3 ;
rxt.'HEXT'   = HEXT ;
rxt.'TPEXT'  = TEXT ;

rxt.'ECHAN'  = Hint ;

 rxt.'Yn20'   =  0.                                    ;
 rxt.'TF0'    =  tetai                                 ;
 rxt.'PT0'    =  1.0e5                                 ;

 Qinc = Qm  ;
 rxt.'scenario'          = table                              ;
 rxt.'scenario'.'t'      = prog   0.0 1000.                   ;
 rxt.'scenario'.'qair'   = prog   0.   0.                     ;
 rxt.'scenario'.'qn2'    = prog   Qinc Qinc                   ;
 rxt.'scenario'.'tinj'   = prog   tetai tetai                 ;

rxt.'GRAPH' = FAUX ;
execrxt  0 rxt ;

Si COMPLET;
rxt.'TBT'.'RTF'.'1TSCA'.'KOPT'.'CMD'=0.;
Finsi;

Diag=rxt.'GEO'.'Diag';
mess ' Volume de RIVA ' (somt diag);
rxt.'GRAPH' = GRAPH ;

nbit = ENTI (Tmax / rxt.'DT0') ;
execrxt  nbit rxt ;

TAB1 =TABLE ;
TAB1.'TITRE'=TABLE ;
tab1 . 1 = 'MOT' 'MARQ CROI REGU ';
tab1 . 2 = 'MOT' 'MARQ CARR REGU TIRC';
tab1 . 3 = 'MOT' 'MARQ LOSA REGU TIRC';
tab1 . 4 = 'MOT' 'MARQ TRIU REGU TIRC';
tab1 . 5 = 'MOT' 'MARQ TRID REGU TIRC';
tab1 . 'TITRE' . 1 = 'Sol Ref non adia';
tab1 . 'TITRE' . 2 = 'Nautilus CFD';
*.................................................................
evrho2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'Rhomn');
evp2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'PT');
evt2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'Tfm');
evmair2=evrho2 * Vr;

Si GRAPH;
tit= chai 'Pression enceinte' ;
dess (evp1 et evp2) titr tit
'GRIL' titx 's' tity 'Pa' lege tab1;

tit= chai 'Température moyenne enceinte';
dess (evt1 et evt2) titr tit
'GRIL' titx 's' tity '°C' lege tab1;

tit= chai 'Densité moyenne enceinte ' ;
dess (evrho1 et evrho2) titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;

tit= chai 'Masse d air '  ;
dess (evmair1 et evmair2) titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;

tp= rxt.tic.'TP';
tf= rxt.tic.'TF';
$vtp = rxt.geo.'$vtp';
$vtf = rxt.geo.'$vtf';
vtp = doma $vtp maillage;
vtf = doma $vtf maillage;
a1  = elem vtp 'APPUYE' 'LARGEMENT' parexp ;
vtp1= diff vtp a1;
tp1 = redu tp vtp1;
trace (tp1 et tf) (vtp1 et vtf) titr 'Isothermes';

Finsi;

devp=evp1 - evp2 abs;
erp= maxi (extr devp 'ORDO');
mess ' ERP =' ERP;
Si (ERP '>' 30000.  ) ; erreur 5 ; Finsi ;


FIN ;


opti donn 5 ;

un=rxt.'TIC'.'UN';
tf =rxt.'TIC'.'TF' ;

$vtf=rxt.'GEO'.'$vtf' ;
vtf=doma $vtf maillage;
rho=rxt.'TIC'.'RHO' ;
un =rxt.'TIC'.'UN';

ixe=inve axe ;
evauz = evol chpo (exco un 'UY') ixe ;
evarh = evol chpo rho ixe ;
evatf = evol chpo tf  ixe ;
evair = evol chpo rair ixe ;


Si GRAPH ;

 dess evauz 'TITRE' 'Vitesse sur l axe';
 dess evarh 'TITRE' 'Densite melange sur l axe';
 dess evatf 'TITRE' 'Temperature sur l axe';

ung= vect un 0.1 ux uy jaune;
trace ung vtf;
opti isov suli ;
trace rho vtf 'TITRE' ' Rho' ;
*trace rh2 vtf 'TITRE' ' Rh2' ;
*trace rco vtf 'TITRE' ' Rco' ;
 trace tf  vtf 'TITRE' ' Tf ' ;
Finsi ;

* FIN ;






