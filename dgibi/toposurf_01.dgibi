* fichier toposurf_01.dgibi
************************************************************************
************************************************************************

************************************************************************
** Extraction of the 2D smoothed surface from a simple 2D topology
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** 3.0 2018/01/29 Updated to make it compatible with TOPOSURF V3.0
** 2.0 2018/01/26 Updated to make it compatible with TOPOSURF V2.0
** 1.0 2016/12/13 Original version compatible with TOPOSURF V1.0
************************************************************************

* General options
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' QUA4 ;
graph0 = FAUX ;

* Mesh
p0 = 0.0 0.0 ;
p1 = 0.0 100.0 ;
p2 = 100.0 0.0 ;
lgn0 = DROI 100 p1 p0 ;
msh0 = TRAN lgn0 100 p2 ;

* Model
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;

* Topology (usualy obtained from procedure TOPOPTIM)
pnt0 = (msh0 POIN 'DROI' (100.0 0.0) (60.0 100.0) 10.0)
             POIN 'DROI' (0.0 60.0) (100.0 60.0) 30.0 ;
pnt0 = pnt0 ET (msh0 POIN 'SPHE' (40.0 30.0) (40.0 50.0) 5.0) ;
pnt0 = pnt0 ET (msh0 POIN 'SPHE' (-50.0 150.0) (50.0 150.0) 15.0) ;
elm0 = msh0 ELEM 'APPU' pnt0 ;
topo0 = MANU 'CHML' mod0 'SCAL' 1.0 'TYPE' 'SCALAIRE' 'GRAVITE' ;
topo0 = ((topo0 * 0.0) REDU (DIFF msh0 elm0)) + (topo0 REDU elm0) ;

* Plot the topology to screen
SI graph0 ;
    TRAC topo0 mod0 ;
FINS ;

* Topology table
tab0 = TABL ;
tab0.'TOPOLOGIE' = topo0 ;
tab0.'MODELE'    = mod0 ;
tab0.'EPAISSEUR' = 0.0 ;

* Generate the smoothed boundary of the topology
srf0 = TOPOSURF tab0 ;

* Plot the smoothed boundary to screen
SI graph0 ;
    TRAC srf0 ;
FINS ;

FIN ;



