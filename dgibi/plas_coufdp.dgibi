* fichier :  plas_coufdp.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*
* ESSAIS COUDE EN FLEXION DANS LE PLAN
* COUDE MINCE ( e = 2.15 mm dext = 179.0 mm )
*
OPTI ECHO 0 ;
OPTI DIME 3 ELEM SEG2 ;
OEIL = 100. 0. 0. ;
NELCOU = 10 ;
LH1 = 1.450 ;
LA1 = .179 ;
RCO1 = .275 ;
LARC1 = LA1 + RCO1 ;
LB1 = LH1 - LARC1 ;
*
P1 = 0. 0. LH1 ;
P2 = 0. 0. LARC1 ;
P3 = 0. 0. RCO1 ;
P4 = 0. RCO1 0. ;
P5 = 0. LARC1 0. ;
P6 = 0. ( LARC1 + 50.E-3 ) 0. ;
*
LREN1 = P1 D 5 P2 ;
LDRO1 = P2 D 3 P3 ;
COUD1 = P3 C NELCOU ( 0. RCO1 RCO1 ) P4 ;
LDRO2 = P4 D 3 P5 ;
LREN2 = P5 D 1 P6 ;
*
LIGN1 = LREN1 ET LDRO1 ET COUD1 ET LDRO2 ;
LIGN1 = LIGN1 ET LREN2 ;
ELIM LIGN1 1.E-4 ;
TASS LIGN1 ;
*
* MODELE PLASTIQUE ECROUISSAGE ISOTROPE
*
OBLR1 = MODE ( LREN1 ET LREN2 ) 'MECANIQUE' 'ELASTIQUE'
        'PLASTIQUE' 'TUYA' ;
OBLD1 = MODE LDRO1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'TUYA' ;
OBCO1 = MODE COUD1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'TUYA' ;
OBLD2 = MODE LDRO2 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'TUYA' ;
OBDRO1 = OBLD1 ET OBLD2 ;
modlign = OBLR1 ET OBLD1 ET OBCO1 ET OBLD2 ;
*
REXT1 = 179.0E-3 / 2. ;
ECOU1 = 2.15E-3 ;
EDRO1 = 2.15E-3 ;
ERIG1 = 10.0E-3 ;
CAREN1  = 'CARA' OBLR1 'EPAI' ERIG1 'RAYO' REXT1 'VECT' ( 1. 0. 0. ) ;
CARDRO1 = 'CARA' OBDRO1 'EPAI' EDRO1 'RAYO' REXT1 'VECT' ( 1. 0. 0. ) ;
CARCOU1 = 'CARA' OBCO1 'EPAI' ECOU1 'RAYO' REXT1
          'RACO' RCO1 'VECT' ( 1. 0. 0. ) ;
CARLIGN = CAREN1 ET CARDRO1 ET CARCOU1 ;
*
* COURBE DE TRACTION
*
LSIG1 = 'PROG' 0. 220. 260.0 300.0 330. 360.0 390.0
        420.0 450.0 480. 520.0 620.0 ;
LSIG1 = LSIG1 * 1.E6 ;
LEPS1 = 'PROG' 0. .118 .152 .203 .273 .398 1.044
        4.331 8.55 12.78 20.5 67. ;
LEPS1 = LEPS1 * 1.E-2 ;
*
SSTBG = 'EVOL' 'MANU' 'DEFO' LEPS1 'CONT' LSIG1 ;
*
SIGM1 = 'EXTR' SSTBG 'ORDO' ;
EPSI1 = 'EXTR' SSTBG 'ABSC' ;
YOUN1 = ( 'EXTR' SIGM1 2 ) / ( 'EXTR' EPSI1 2 ) ;
'MESS' 'MODULE D YOUNG = ' YOUN1 ;
*
MATLIGN = 'MATE' modlign 'YOUN' YOUN1 'NU' .3 'TRAC' SSTBG ;
*
*BLO1 = 'BLOQ' 'DEPL' 'ROTA' P6 ;
*BLO2 = 'BLOQ' 'UX' 'RY' P1 ;
BLO1 = 'BLOQ' 'UY' 'UZ' 'RX' 'RZ' P6 ;
BLO2 = 'BLOQ' 'UX' 'RY' lign1 ;
rigblo1 = BLO1 ET BLO2 ;
*
* PAS DE CHARGE
*
DFE1 = 1.E3 ;
FELF = 2.E3 ;
*
LISCEF = 'PROG' 0. PAS DFE1 FELF ;
LISCF = LISCEF ;
*
DFE1 = 50. ;
FELI = FELF ;
FELF = 2.75E3 ;
*
LISCEF = 'PROG' ( FELI + DFE1 ) PAS DFE1 FELF ;
LISCF = LISCF ET LISCEF ;
*
* CHARGEMENT
*
FP = 'FORC' 'FY' 1. P1 ;
EVOLU1 = 'EVOL' 'MANU' 'PAS' LISCF 'FORC' LISCF ;
CHATOT = 'CHAR' MECA FP EVOLU1 ;
*
CARTOT = MATLIGN ET CARLIGN ;
RIG1 = 'RIGI' modlign CARTOT ;
RIGTOT = RIG1 ET rigblo1 ;
*
ZTAB = 'TABLE' ;
ZTAB.blocages_mecaniques = rigblo1 ;
ZTAB.caracteristiques = cartot;
ztab.modele = modlign ;
ZTAB.chargement = CHATOT ;
ZTAB.temps_calcules = LISCF ;
ZTAB.'CONVERGENCE_FORCEE'  = FAUX ;
ZTAB.'DELTAITER'           = 1000 ;
PASAPAS ZTAB ;
*
* POSTRAITEMENTS
*
NELCOU   = 'NBEL' COUD1 ;
EPA1 = ECOU1 ;
REX1 = REXT1 ;
RAC1 = RCO1 ;
RIN1 = REX1 - EPA1 ;
XIN1 = PI * ( ( REX1 ** 4. ) - ( RIN1 ** 4. ) ) * 0.25 ;
XJN1 = 2. * XIN1 ;
SEC1 = PI * ( ( REX1 ** 2. ) - ( RIN1 ** 2. ) ) ;
*
XLAM1 = EPA1 * RAC1 * 4. / ( ( REX1 + RIN1 ) ** 2. ) ;
PI4   = PI / 4. ;
R33   = ( 3. ** 0.5 ) ;
RMOY1 = REX1 - ( EPA1 * 0.5 ) ;
R89   = 8. / 9. ;
PUI23 = -2. / 3. ;
GAM1  = R89 * ( XLAM1 ** PUI23 ) ;
KK1   = 1.65 / XLAM1 ;
ALF2    = PI4 * GAM1 ;
FACT1   = ( RMOY1 * ALF2 ) / KK1 ;
LCOUD   = RAC1 * 90. ;
LELEM   = LCOUD / NELCOU ;
*
depl1 = ztab . deplacements ;
effo1 = ztab . contraintes  ;
*********coupl1 = ztab . deformations_inelastiques ;
*
ntpr1 = ( 'DIME' depl1 ) - 1 ;
*
* verif dernier pas elastique
*
ipas = 3 ;
*
* moments
*
fimp1 = extr liscf ( ipas + 1 ) ;
mtheo1 = fimp1 * lh1 ;
effor1 = redu ( effo1 . ipas ) coud1 ;
mcalc1 = extr effor1 'MOMY' 1 nelcou 2 ;
*
* deformations
*
sigi1 = 'EXTR' sstbg 'ORDO' ;
epsi1 = 'EXTR' sstbg 'ABSC' ;
deptot = depl1 . ipas ;
coutr1 = redu ( 'EPSI' modlign deptot cartot ) coud1 ;
cycal1 = extr coutr1 'CY' 1 nelcou 2 ;
defyt1 = ( alf2 * rmoy1 * cycal1 ) / kk1 ;
vmitr1 = redu ( 'VMIS' modlign ( effo1 . ipas ) cartot ) coud1 ;
vmise1 = extr vmitr1 'SCAL' 1 nelcou 2 ;
epsit1 = ipol vmise1 sigi1 epsi1 ;
mess ' ' ;
mess 'moment theorique = ' mtheo1 'moment calcule = ' mcalc1 ;
res1 = abs ( ( mtheo1 - mcalc1 ) / mcalc1 ) ;
slim1 = ( extr sigi1 2 ) * 1.e-6 ;
vmim1 = vmise1 * 1.e-6 ;
mess 'Limite elastique = ' slim1 ' Vmis Max = ' vmim1 ;
mess 'deftot theorique = ' epsit1 'deftot calcule = ' defyt1 ;
res2 = abs ( ( epsit1 - defyt1 ) / epsit1 ) ;
progelas = 'PROG' res1 res2 ;
maxerr1 = 'MAXI' progelas ;
'SI' (maxerr1 <eg 1.e-4 ) ;
   erre 0 ;
'SINON' ;
   erre 5 ;
'FINSI' ;
*
* verif dernier pas plastique
*
ipas = ntpr1 ;
*
* moments
*
fimp1 = extr liscf ( dime liscf ) ;
mtheo1 = fimp1 * lh1 ;
effor1 = redu ( effo1 . ipas ) coud1 ;
mcalc1 = extr effor1 'MOMY' 1 nelcou 2 ;
*
* deformations
*
deptot = depl1 . ipas ;
coutr1 = redu ( 'EPSI' modlign deptot cartot ) coud1 ;
cycal1 = extr coutr1 'CY' 1 nelcou 2 ;
defyt1 = ( alf2 * rmoy1 * cycal1 ) / kk1 ;
vmitr1 = redu ( 'VMIS' modlign ( effo1 . ipas ) cartot ) coud1 ;
vmise1 = extr vmitr1 'SCAL' 1 nelcou 2 ;
epsit1 = ipol vmise1 sigi1 epsi1 ;
mess ' ' ;
mess 'moment theorique = ' mtheo1 'moment calcule = ' mcalc1 ;
res1 = abs ( ( mtheo1 - mcalc1 ) / mcalc1 ) ;
slim1 = ( extr sigi1 2 ) * 1.e-6 ;
vmim1 = vmise1 * 1.e-6 ;
mess 'Limite elastique = ' slim1 ' Vmis Max = ' vmim1 ;
mess 'deftot theorique = ' epsit1 'deftot calcule = ' defyt1 ;
res2 = abs ( ( epsit1 - defyt1 ) / epsit1 ) ;
progplas = 'PROG' res1 res2 ;
maxerr2 = 'MAXI' progplas ;
'SI' (maxerr1 <eg 1.e-4 ) ;
   erre 0 ;
'SINON' ;
   erre 5 ;
'FINSI' ;
fin ;











