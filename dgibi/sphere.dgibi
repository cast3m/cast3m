* fichier :  sphere.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*==================================================================*
*                                                                  *                                                                  *
*              Sphere sous pression interne                        *
*                                                                  *
*                                                                  *
*   R0 rayon interne aleatoire       LogNormale(50  , 2.5)         *
*   R1 rayon externe aleatoire       LogNormale(100 , 5  )         *
*   P0 pression interne aleatoire    LogNormale(130 , 8  )         *
*   Fa limite d'ecoulement           LogNormale(300 , 20 )         *
*                                                                  *
*                                                                  *
*                                                                  *
*  BUT = Calcul de la probabilite de defaillance Pf                *
*                                                                  *
*         Pf = Probabilite(VonMises > Limite ecoulement)           *
*                                                                  *
*  Les resultats sont compares avec FORM, SORM                     *
*                et surface de reponses                            *
*                                                                  *
*                                                                  *
*==================================================================*
OPTI ECHO 0;
*
GRAPH = 'N';
*
OPTI DIME 2;
OPTI ELEM QUA8;
OPTI MODE AXIS;
* Finesse angulaire du maillage
*
NA = 20;
* Finesse radiale du maillage
*
NR = 10;
*
*==================================================================*
*             Calcul des points d integration                      *
*==================================================================*
*
NpR0 = 3;
NpR1 = 3;
NpP0 = 3;
TabR0 = QUADRATU 'LOGN' 50. 2.5 NpR0;
TabR1 = QUADRATU 'LOGN' 100. 5. NpR1;
TabP0 = QUADRATU 'LOGN' 130. 8. NpP0;
*
*
*==================================================================*
*             Definition de la table globale                       *
*==================================================================*
*
TabGLO = TABLE;
ITEM = 0 ;
REPE BcR0 NpR0;
 REPE BcR1 NpR1;
  REPE BcP0 NpP0;
   ITEM = ITEM + 1 ;
   TabGLO .ITEM = TABLE;
   TabGLO .ITEM. 'POIDS' =
       TabR0 .&BcR0. 'POIDS' *
       TabR1 .&BcR1. 'POIDS' *
       TabP0 .&BcP0. 'POIDS';
   TabGLO .ITEM. 'R0' = TabR0 .&BcR0. 'POINT';
   TabGLO .ITEM. 'R1' = TabR1 .&BcR1. 'POINT';
   TabGLO .ITEM. 'P0' = TabP0 .&BcP0. 'POINT';
  FIN BcP0;
 FIN BcR1;
FIN BcR0;
*
*
*==================================================================*
*             Procedure de calcul du Von Mises                     *
*==================================================================*
DEBPROC CalVM R0*FLOTTANT R1*FLOTTANT P0*FLOTTANT;
P1 = R0 0.;
P2 = R1 0.;
Li = P1 DROI NR P2;
Su = Li ROTA NA 90. (0. 0.);
Lip = COTE 4 Su;
MO = MODEL SU MECANIQUE ELASTIQUE;
MA = MATE MO YOUNG 100000. NU 0.3;
RI = RIGI MO MA;
BL = BLOQ UZ Li;
FPr = PRESS MASSIF MO Lip P0;
Dep = RESO (RI ET BL) FPr;
SS = SIGMA Dep MO MA;
SE = CHAN NOEUD MO SS;
VM = VMIS SE MO;
SMax = MAXI VM;
FINPROC Smax;
*
*
*
*
*==================================================================*
*   CALCUL AUX POINT D INTEGRATION + MOMENTS STATISTIQUES          *
*==================================================================*
N = DIME TabGLO;
TabRES = TABLE;
ITEM = 0;
REPE BOUC N;
  TabRES .&BOUC = TABLE;
  TabRES .&BOUC. POIDS = TabGLO .&BOUC. POIDS;
  TabRES .&BOUC. POINT =
    CalVM TabGLO .&BOUC. R0 TabGLO .&BOUC. R1 TabGLO .&BOUC. P0;
FIN BOUC;
*
MU SIGMA RB1 B2 = PARASTAT TabRES;
*
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
MESS '%%% CALCUL DES PARAMETRES STATISTIQUES ';
MESS '%%% MOYENNE       ' MU;
MESS '%%% ECART_TYPE    ' SIGMA;
MESS '%%% SYMETRIE      ' RB1;
MESS '%%% APLATISSEMENT ' B2 ;
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
opti echo 0;
*
*
*
SI (NEG GRAPH 'N');
  PDF CDF = PROBDENS MU SIGMA RB1 B2;
  DESS PDF;
  DESS CDF;
FINSI;
*
*
* Calcul de la probabilite de defaillance
*     (L'utilisateur peut fournir une table qui contient les
*      points et points d integration pour la limite
*      d'écoulement)
*
*  Alternative : TabFa = QUADRATU 'LOGN' 300. 20. 10;
*
*
TabFa = TABLE;
REPE BOUC 11;
  TabFa. &BOUC = TABLE;
FIN BOUC;
*
TabFa. 1 .POINT = 221.666490;
TabFa. 2 .POINT = 241.022813;
TabFa. 3 .POINT = 258.919954;
TabFa. 4 .POINT = 276.625787;
TabFa. 5 .POINT = 294.715015;
TabFa. 6 .POINT = 313.603185;
TabFa. 7 .POINT = 333.701890;
TabFa. 8 .POINT = 355.523462;
TabFa. 9 .POINT = 379.835374;
TabFa. 10 .POINT = 408.040037;
TabFa. 11 .POINT = 443.670839;
*
TabFa. 1 .POIDS = 2.175677e-05;
TabFa. 2 .POIDS = .002284;
TabFa. 3 .POIDS = .038159;
TabFa. 4 .POIDS = .191306;
TabFa. 5 .POIDS = .364416;
TabFa. 6 .POIDS = .290527;
TabFa. 7 .POIDS = .098867;
TabFa. 8 .POIDS = .013724;
TabFa. 9 .POIDS = .000682;
TabFa. 10 .POIDS = 9.080232e-06;
TabFa. 11 .POIDS = 1.490409e-08;
*
*
Pf = 0.;
REPE BOUC (DIME TabFA);
  p = PROB MU SIGMA RB1 B2 (TabFa. &BOUC .POINT);
  Pf = Pf + ((1-p)*(TabFa. &BOUC .POIDS));
FIN BOUC;
*
* Calcul de l'indice Beta
*
*
BetaFORM = 3.055;
BetaSORM = 3.003;
BetaSRQ  = 3.053;
BetaQuad = INDIBETA 0. 7. Pf;
ErFORM =100 * (ABS ((BetaQuad - BetaFORM) / BetaFORM));
ErSORM =100 * (ABS ((BetaQuad - BetaSORM) / BetaSORM));
ErSRQ  =100 * (ABS ((BetaQuad - BetaSRQ) / BetaSRQ));
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
MESS '                       RESULTATS';
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
MESS 'Propabilite de defaillance par quadrature' Pf;
MESS 'Indice de fiabilite par quadrature       ' BetaQuad;
MESS '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%';
MESS 'Soit :';
MESS '        ERREUR par rapport a FORM' ErFORM '%';
MESS '        ERREUR par rapport a SORM' ErSORM '%';
MESS '        ERREUR par rapport a SRQ ' ErSRQ '%';
*==================================================================*
*                  code fonctionnement                             *
*==================================================================*
*
*     L'ecart maximum entre valeur theorique et calculee doit etre
* inferieure a 2 %.
*
SI (ErFORM <EG 2);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
SI (ErSORM <EG 2);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
SI (ErSRQ <EG 2);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*==================================================================*
*                 Temps de calcul et fin                           *
*==================================================================*
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
OPTI ECHO 1;
*
FIN;




