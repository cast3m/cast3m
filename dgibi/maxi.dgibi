
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
************************************************************************
*
* Mots-clés : MAXI, MANU 'CHPO'
*
* Test de verification de les operateurs MAXI et MANU 'CHPO'
*
* creation : bp, 2015-06-09
* modifs   : ... a completer ...
*
************************************************************************

OPTI DIME 2 ELEM QUA4;
NBERR = 0; lerr = lect;
XPREC = 1.E-10;

************************************************************************
* 3eme syntaxe de MAXI :
* OBJET3   =  MAXI  OBJET1  OBJET2 (objeti ..)
************************************************************************

*** LISTREEL ***

pipi = prog  1 2 3 4;
pipi2 = 2.* pipi;
pipi3 = 3.* pipi;

popo = maxi pipi pipi2;
popo3 = maxi pipi pipi2 pipi3;

* list popo;
xpopo = SOMM popo;
SI ((ABS (xpopo - 20.)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 1;
FINSI;
* list popo3;
xpopo3 = SOMM popo3;
SI ((ABS (xpopo3 - 30.)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 2;
FINSI;


*** CHPOINT ***

*************** créé via MANU CHPO + maillage de POI1 :

p0 = 0. 0.;
mesh1 = MANU 'POI1' p0 ;
kr = MANU 'CHPO' mesh1 1 scal 0.19;
va = MANU 'CHPO' mesh1 1 scal 0.10;
* list kr ;
* list va ;
* on peut aussi verifier ce qu il y a comme maillage
* kr1 = extr  kr 'MAIL'; list kr1;
* va1 = extr  va 'MAIL'; list va1;
obj = MAXI va kr ;
* list obj;
xobj = EXTR obj 'SCAL' p0;
SI ((ABS (xobj - 0.19)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 3;
FINSI;

*************** créé via COOR + maillage d elements

p1 = 0 0;
p2 = 2 1;
l12 = p1 droi 10 p2;
x = coor l12 1;
list x;
* meshx = extr x MAIL; list meshx;
x2 = x**2;
x3 = x**3;
toto =  maxi x x2;
list toto;

toto3 =  maxi x x2 x3;
list toto3;

* test
xtoto = MAXI  toto  'ABS';
xtoto3 = MAXI toto3 'ABS';
SI ((ABS (xtoto  - 4.)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 4;
FINSI;
SI ((ABS (xtoto3 - 8.)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 5;
FINSI;
xtoto = MAXI (RESU  toto)  ;
xtoto3 = MAXI (RESU toto3) ;
SI ((ABS (xtoto  - 16.2)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 6;
FINSI;
SI ((ABS (xtoto3 - 25.4)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 7;
FINSI;

* apres un REDU (le meleme change)
pmil = l12 poin 6;
d1mil = l12 elem compris p1 pmil;
titi = maxi (redu x d1mil) (redu x2 d1mil);
xtiti = MAXI (RESU  titi)  ;
SI ((ABS (xtiti  - 3.0)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 8;
FINSI;



*************** créé via MANU CHPO + maillage d elements :

kr2 = MANU 'CHPO' l12 2 'UX' 0.19 'UY'  0.29;
va2 = MANU 'CHPO' l12 2 'UX' 0.10 'UY' -0.45;
obj2 = MAXI va2 kr2 ;
xobj2 = EXTR obj2 'UY' p2;
SI ((ABS (xobj2 - 0.29)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 9;
FINSI;


*************** créé via MANU CHPO + maillage de POI1 avec des doublons:

mesh11 = (MANU 'POI1' p0) et p1 'TELQUEL';
mesh11 = mesh11 et p2 'TELQUEL';
mesh11 = mesh11 et p0 'TELQUEL';
kr1 = MANU 'CHPO' mesh11 1 scal 0.19 'NATURE' 'DIFFUS';
va1 = MANU 'CHPO' mesh11 1 scal 0.10 'NATURE' 'DIFFUS';
max11 = MAXI kr1 va1;
xmax11 = MAXI max11;
SI ((ABS (xmax11 - 0.19)) > XPREC);
  NBERR=NBERR + 1; lerr= lerr et 10;
FINSI;


opti echo 0;
saut lign ;
mess '>>>>> ' NBERR ' ERREURS DETECTEES  <<<<<' ;
mess 'pour les cas tests :'; list lerr;
saut lign ;
SI (NBERR EGA 0); ERRE 0;
SINON;            ERRE 5;
FINSI;

FIN ;








