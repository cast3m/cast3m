OPTI DIME 3 MODE TRID ELEM QUA4;
*
**********************************************************
*  Test sur la procedure de MRCSHELL pour calculer
*  les marges de securites pour les elements en beton arme
*  de type coque.
*  On cosidere deux cas:
*  Cas1 CAS_1 = 1;
*     On considere deux elements sur lesquels on a definit 
*     les efforts statiques et l'enveloppe des efforts sismiques
*     Les proprietes sont les suivantes:
*       epaisseur    = 0.12
*       enrobage_ext = 0.005
*       enrobage_int = 0.005
*       ferrailage_ext = 0.001
*       ferrailage_int = 0.01
*       ferrailage_cor = 0.01
*       fck = 30 MPa
*       fsk = 500 MPa
*       gamma_c = 1.5
*       gamma_s = 1.15
*       nu_c = 1.0
*       alpa = 1.0
*       cot(theta) = 1.0
*     Pour l'element 1 on connais les marges analitiquement.
*       lambda*N12 = fc/2     (externe ou interne)
*       lambda*T1 = fs*rho_t*cot(theta)  (intermediaire)
*       avec fc = nu*alpa*fck/gamma_c
*            fs = fsk/gamma_s
*     Pour l'element 2 l'etat statique des effort est en dehors
*     des surfaces ultimes. En consequence:
*       lambda = 0
*  Cas2 CAS_1 = 2;
*    Calcul complet des marges d'une voile qui compose une batiment
*    a plusieurs etages. La chene du calcul complet est effectué.
*    Les procedures EFFMARTI et SISSIB (avec calcul des covariances)
*    sont appelées
*
*  Develloppé par Alberto FRAU /DEN/DANS/DM2S/SEMT/EMSI
*              et Nicolas ILE  /DEN/DANS/DM2S/SEMT/EMSI
*
**********************************************************
*
CAS_1 = 1;
GRAP1 = 'N';
* ---------------------- Cas1 -------------------------------
SI (CAS_1 EGA 1);
*
* Definition des points
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;
* Definition des lignes
L1 = D 1 P1 P2;
L2 = D 1 P2 P3;
L3 = D 1 P3 P4;
L4 = D 1 P4 P1;
* Definition du maillage
ELE1 = DALL L1 L2 L3 L4;
ELE2 = ELE1 PLUS (1. 0. 0.);
ELE_TOT = ELE1 ET ELE2;
ELIM 0.001 ELE_TOT;
* Modele
MOD1 = MODE ELE1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MOD2 = MODE ELE2 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MOD_TOT = MOD1 ET MOD2;
* Champ par element covariances
* [  N11e²   N11eN22e N11eN12e    [1.e8 0.e8 0.e8   
*  N11eN22e    N22e²  N11eN12e  =  0.e8 1.e8 0.e8
*  N11eN22e  N11eN12e   N12e² ]    0.e8 0.e8 25.e8]
* [  N11i²   N11iN22i N11iN12i    [1.e8 0.e8 0.e8   
*  N11iN22i    N22i²  N11iN12i  =  0.e8 1.e8 0.e8
*  N11iN22i  N11eN12i   N12i² ]    0.e8 0.e8 4.e8]
* [ V1²  V1V2   [4.e10 0.e8
*  V1V2   V2²]=  0.e8  1.0e8]
*
CH1 = (MANU CHML MOD1  
'C11E' ((1.0E4)**(2.0)) 'C22E' ((1.0E4)**(2.0)) 'C33E' ((5.0E4)**(2.0))
'C12E' ((0.0E6)**(2.0)) 'C13E' ((0.0E6)**(2.0)) 'C23E' ((0.0E6)**(2.0))
'C11I' ((1.0E4)**(2.0)) 'C22I' ((1.0E4)**(2.0)) 'C33I' ((2.0E4)**(2.0))
'C12I' ((0.0E6)**(2.0)) 'C13I' ((0.0E6)**(2.0)) 'C23I' ((0.0E6)**(2.0))
'CV11' ((2.0E5)**(2.0)) 'CV22' ((1.0E4)**(2.0)) 
'CV12' ((0.0E6)**(2.0))); 
CH1 = CH1 ET 
(MANU CHML MOD2  
'C11E' ((1.0E4)**(2.0)) 'C22E' ((1.0E4)**(2.0)) 'C33E' ((5.0E4)**(2.0))
'C12E' ((0.0E6)**(2.0)) 'C13E' ((0.0E6)**(2.0)) 'C23E' ((0.0E6)**(2.0))
'C11I' ((1.0E4)**(2.0)) 'C22I' ((1.0E4)**(2.0)) 'C33I' ((4.0E4)**(2.0))
'C12I' ((0.0E6)**(2.0)) 'C13I' ((0.0E6)**(2.0)) 'C23I' ((0.0E6)**(2.0))
'CV11' ((2.0E5)**(2.0)) 'CV22' ((1.0E4)**(2.0)) 
'CV12' ((0.0E6)**(2.0)));
*
* Etat Statique
* N11e = -10.e4
* N22e = -10.e4
* N12e = -0.e4
* N11i = -10.e4
* N22i = -10.e4
* N12i = -0.e4
* V1 = 0.0
* V2 = 0.0
CH2 = (MANU CHML MOD1 
                  'N11E' ((-10.0E4)) 'N22E' ((-10.0E4))
                  'N12E' ((0.0E4))  'N11I' ((-10.0E4))
                  'N22I' ((-10.0E4))  'N12I' ((0.0E4))
                  'M11T' ((0.0E6))  'M22T' ((0.0E6))
                  'M12T' ((0.0E6))  'V1T'  ((0.0E6))
                  'V2T'  ((0.0E6))  'VR'   ((0.1E6)));
CH2 = CH2 ET (MANU CHML MOD2 
                  'N11E' ((-100.0E4)) 'N22E' ((-100.0E4))
                  'N12E' ((0.0E4))  'N11I' ((-100.0E4))
                  'N22I' ((-100.0E4))  'N12I' ((0.0E4))
                  'M11T' ((0.0E6))  'M22T' ((0.0E6))
                  'M12T' ((0.0E6))  'V1T'  ((100.0E6))
                  'V2T'  ((100.0E6))  'VR'   ((0.1E6)));
*
* Definition des caracteristiques rho1e = 0.001
*                                 rho2e = 0.01
*                                 enro_e = 0.005
*                                 rho1i = 0.001
*                                 rho2i = 0.001
*                                 enro_i = 0.005
CAR_EXT1 = MANU CHML MOD_TOT 'RHO1' 0.001 'RHO2' 0.01 'ENRO' 0.005;
CAR_INT1 = MANU CHML MOD_TOT 'RHO1' 0.001 'RHO2' 0.01 'ENRO' 0.005;
CAR_COR1 = MANU CHML MOD_TOT 'H' 0.12 'RHOT' 0.01  'COTH' 1.0;
*
*  DEfinition de la table d'entree
TAB_MAR = TABLE;
TAB_MAR.'MAILLAGE' = ELE_TOT;
TAB_MAR.'EFFORT_SEISME' = CH1;
TAB_MAR.'EFFORT_STATIQUE' = CH2;
TAB_MAR.'CARACTERISTIQUES_EXTERNE' = CAR_EXT1; 
TAB_MAR.'CARACTERISTIQUES_INTERNE' = CAR_INT1;
TAB_MAR.'CARACTERISTIQUES_CORE' =    CAR_COR1;
TAB_MAR.'FC_BET' =  30.E6;
TAB_MAR.'GAM_C' = 1.5;
TAB_MAR.'ALP_C' = 1.0;
TAB_MAR.'NU_C' = 1.0;
TAB_MAR.'FS_ACI' = 500.0E6;
TAB_MAR.'GAM_S' = 1.15;
*
* Calcul des marges
VAL_TOL1 = 0.000001;
ELE_LI1 = LECT 1;
OPTI EPSILON LINEAIRE;
T_LAM_EL = MRCSHELL 'ELLIPSOIDE' TAB_MAR VAL_TOL1 ELE_LI1;
T_LAM_RE = MRCSHELL 'RECTANGLE' TAB_MAR VAL_TOL1 ELE_LI1;
*
* extrapolation des resultats
LAM1_E = EXTR (T_LAM_EL.CH_LAMBDA_E) 'LAME' 1 1 1;
LAM1_I = EXTR (T_LAM_EL.CH_LAMBDA_I) 'LAMI' 1 1 1;
LAM1_C = EXTR (T_LAM_EL.CH_LAMBDA_C) 'LAMC' 1 1 1;
LAM2_E = EXTR (T_LAM_RE.CH_LAMBDA_E) 'LAME' 1 1 1;
LAM2_I = EXTR (T_LAM_RE.CH_LAMBDA_I) 'LAMI' 1 1 1;
LAM2_C = EXTR (T_LAM_RE.CH_LAMBDA_C) 'LAMC' 1 1 1;
*
LIM_E = T_LAM_EL.GRAPHIQUES. 1 .LIMITE_E;
LIM_I = T_LAM_EL.GRAPHIQUES. 1 .LIMITE_I;
LIM_C = T_LAM_EL.GRAPHIQUES. 1 .LIMITE_C;
LIM_E = LIM_E COUL ROUG;
LIM_I = LIM_I COUL ROUG;
LIM_C = LIM_C COUL ROUG;
*
ELP_E = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_E;
ELP_I = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_I;
ELP_C = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_C;
ELP_E = ELP_E COUL BLEU;
ELP_I = ELP_I COUL BLEU;
ELP_C = ELP_C COUL BLEU;
*
REC_E = T_LAM_EL.GRAPHIQUES. 1 .RECTANGLE_E;
REC_I = T_LAM_EL.GRAPHIQUES. 1 .RECTANGLE_I;
REC_C = T_LAM_EL.GRAPHIQUES. 1 .RECTANGLE_C;
REC_E = REC_E COUL BLEU;
REC_I = REC_I COUL BLEU;
REC_C = REC_C COUL BLEU;
*
ELP_E_A = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_E_AUG;
ELP_I_A = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_I_AUG;
ELP_C_A = T_LAM_EL.GRAPHIQUES. 1 .ELLIPSOIDE_C_AUG;
ELP_E_A = ELP_E_A COUL VERT;
ELP_I_A = ELP_I_A COUL VERT;
ELP_C_A = ELP_C_A COUL VERT;
*
REC_E_A = T_LAM_RE.GRAPHIQUES. 1 .RECTANGLE_E_AUG;
REC_I_A = T_LAM_RE.GRAPHIQUES. 1 .RECTANGLE_I_AUG;
REC_C_A = T_LAM_RE.GRAPHIQUES. 1 .RECTANGLE_C_AUG;
REC_E_A = REC_E_A COUL VERT;
REC_I_A = REC_I_A COUL VERT;
REC_C_A = REC_C_A COUL VERT;
*
* Valeurs analytiques
FC = 30.E6/1.5;
LAM_E_T = (FC/2)/(((5.E4)/(0.01)));
LAM_I_T = (FC/2)/(((2.E4)/(0.01)));
FS = (500.E6/1.15)*(0.01);
LAM_C_T = (FS)/(((2.E5)/(0.1)));
*
* Control
SI ((ABS(LAM1_E - LAM_E_T)) > (1.E-8));
 ERRE 5;
FINSI;
SI ((ABS(LAM1_I - LAM_I_T)) > (1.E-8));
 ERRE 5;
FINSI;
SI ((ABS(LAM1_C - LAM_C_T)) > (1.E-8));
 ERRE 5;
FINSI;
*
SI ((LAM1_E) < (LAM2_E));
 ERRE 5;
FINSI;
SI ((LAM1_I) < (LAM2_I));
 ERRE 5;
FINSI;
SI ((LAM1_C) < (LAM2_C));
 ERRE 5;
FINSI;
*
* Trace surface
SI ('EGA' GRAP1 'O');
  TRAC (1.E6 0. 0.) (LIM_E ET ELP_E ET REC_E ET ELP_E_A ET REC_E_A);
  TRAC (0. 1.E6 0.) (LIM_E ET ELP_E ET REC_E ET ELP_E_A ET REC_E_A);
  TRAC (1.E6 0. 0.) (LIM_I ET ELP_I ET REC_I ET ELP_I_A ET REC_I_A);
  TRAC (0. 1.E6 0.) (LIM_I ET ELP_I ET REC_I ET ELP_I_A ET REC_I_A);
  TRAC (0. 0. 1.E6) (LIM_C ET ELP_C ET REC_C ET ELP_C_A ET REC_C_A);

FINSI;
*
FIN;
FINSI;
SI (CAS_1 EGA 2);

*Les longueurs sont en cm
*Les Forces sont en daN
*Les masses sont en kg * 10-3
*Les masses volumiques kg/m3 * 10-9
*Les accelerations cm/sec2
******************************************************
*
OPTI DIME 3 ELEM QUA4;
OPTI EPSI LINEAIRE ;
*OPTI ECHO 0;
*
*
*POINTS DE VUE
OEIL = 3000.0 -20000.0 6000.0 ;
******************************************************
* CONTRUCTION MAILLAGE
******************************************************
*points e la base 
0P0 = 0.0 0.0 0.0 ;
0P1 = 0P0 PLUS (700.0 0.0 0.0) ;
0P2 = 0P1 PLUS (700.0 0.0 0.0) ;
0P3 = 0P2 PLUS (0.0 700.0 0.0) ; 
0P4 = 0P1 PLUS (0.0 700.0 0.0) ; 
0P5 = 0P0 PLUS (0.0 700.0 0.0) ; 
*points haut du RDC
*
1P0 = 0P0 PLUS (0.0 0.0 400.0) ;
1P1 = 0P1 PLUS (0.0 0.0 400.0) ;
1P2 = 0P2 PLUS (0.0 0.0 400.0) ;
1P3 = 0P3 PLUS (0.0 0.0 400.0) ;
1P4 = 0P4 PLUS (0.0 0.0 400.0) ;
1P5 = 0P5 PLUS (0.0 0.0 400.0) ;
*
*points haut du 1er etage
2P0 = 1P0 PLUS (0.0 0.0 400.0) ;
2P1 = 1P1 PLUS (0.0 0.0 400.0) ;
2P2 = 1P2 PLUS (0.0 0.0 400.0) ;
2P3 = 1P3 PLUS (0.0 0.0 400.0) ;
2P4 = 1P4 PLUS (0.0 0.0 400.0) ;
2P5 = 1P5 PLUS (0.0 0.0 400.0) ;
*
*points haut du 1er etage
3P0 = 2P0 PLUS (0.0 0.0 400.0) ;
3P1 = 2P1 PLUS (0.0 0.0 400.0) ;
3P2 = 2P2 PLUS (0.0 0.0 400.0) ;
3P3 = 2P3 PLUS (0.0 0.0 400.0) ;
3P4 = 2P4 PLUS (0.0 0.0 400.0) ;
3P5 = 2P5 PLUS (0.0 0.0 400.0) ;
*
*CONSTRUCTION DES SUPPORTS 
*GEOMETRIQUES POUR LES ELEMENTS
*
*poteaux RDC
1POT1 = (DROI 7 0P0 1P0) ;
1POT2 = (DROI 7 0P1 1P1) ;
1POT3 = (DROI 7 0P5 1P5) ;
*
*poteaux 1er etage
2POT1 = (DROI 7 1P0 2P0) ;
2POT2 = (DROI 7 1P1 2P1) ;
2POT3 = (DROI 7 1P5 2P5) ;
*
*poteaux 2eme etage
3POT1 = (DROI 7 2P0 3P0) ;
3POT2 = (DROI 7 2P1 3P1) ;
3POT3 = (DROI 7 2P5 3P5) ;
*
* Maillage Total tous les poteaux
*
SPOT = 1POT1 ET 1POT2 ET 1POT3 
ET 2POT1 ET 2POT2 ET 2POT3
ET 3POT1 ET 3POT2 ET 3POT3 ;
ELIM 0.001 SPOT ;
*
*TRAC OEIL CACHE SPOT ; FIN ;
*poutres haut du RDC longitudinales (suivant X)
1POUL1 = (DROI 10 1P0 1P1) ;
1POUL2 = (DROI 10 1P1 1P2) ;
1POUL3 = (DROI 10 1P5 1P4) ;
*
*poutres haut du 1er etage longitudinales (suivant X)
2POUL1 = (DROI 10 2P0 2P1) ;
2POUL2 = (DROI 10 2P1 2P2) ;
2POUL3 = (DROI 10 2P5 2P4) ;
*
*poutres haut du 2eme etage longitudinales (suivant X)
3POUL1 = (DROI 10 3P0 3P1) ;
3POUL2 = (DROI 10 3P1 3P2) ;
3POUL3 = (DROI 10 3P5 3P4) ;
*
*Maillage pour tous les poutres longitudinales
*
SPOUTL = 1POUL1 ET 1POUL2 ET 1POUL3 ET 
2POUL1 ET 2POUL2 ET 2POUL3 ET 3POUL1 ET 3POUL2 
ET 3POUL3 ;
SPOUTL = SPOUTL COUL VERTE ;
ELIM 0.001 SPOUTL ;
*
*poutres haut du RDC transversales (suivant Y)
1POUT1 = (DROI 10 1P0 1P5) ;
1POUT2 = (DROI 10 1P1 1P4) ;
*
*poutres haut du 1er etage transversales (suivant Y)
2POUT1 = (DROI 10 2P0 2P5) ;
2POUT2 = (DROI 10 2P1 2P4) ;
*
*poutres haut du 2eme etage transversales (suivant Y)
3POUT1 = (DROI 10 3P0 3P5) ;
3POUT2 = (DROI 10 3P1 3P4) ;
*
*
* Maillage total toutes les poutres longitudinales
*
SPOUTT = 1POUT1 ET 1POUT2 ET 2POUT1 ET 2POUT2 ET 
3POUT1 ET 3POUT2 ;
SPOUTT = SPOUTT COUL JAUNE ;
ELIM 0.001 SPOUTT ;
*
* Maillage voile suivant x
*
LV1 = DROI 10 0P2 0P3 ;
LV2 = DROI 21 0P3 3P3 ;
LV3 = DROI 10 3P3 3P2 ;
LV4 = DROI 21 3P2 0P2 ;
SVOILEX = DALLE LV1 LV2 LV3 LV4 'PLANE' ;
ELIM 0.001 SVOILEX ;
*
* Maillage voile suivant y
*
AV1 = DROI 10 0P3 0P4 ;
AV2 = DROI 21 0P4 3P4 ;
AV3 = DROI 10 3P4 3P3 ;
AV4 = DROI 21 3P3 0P3 ;
SVOILEY = DALLE AV1 AV2 AV3 AV4 'PLANE' ;
ELIM 0.001 SVOILEY ;
*TRAC OEIL CACHE SVOILEY ;
*
SVOILEY = ORIENTER SVOILEY 'POINT' (1.0 0.0 0.0) ;
SVOILEY = VERSENS SVOILEY ;
*
SVOILEX = ORIENTER SVOILEX 'POINT' (0.0 1.0 0.0) ;
SVOILEX = VERSENS SVOILEX ;
*
*TRAC OEIL CACHE SVOILEY ; FIN ;
*
* Maillage Dalles haut du rdc et haut du 1er etage
*
LDA1 = DROI 20 1P0 1P2 ;
LDA2 = DROI 10 1P2 1P3 ;
LDA3 = DROI 20 1P3 1P5 ;
LDA4 = DROI 10 1P5 1P0 ;
SDAL1 = DALLE LDA1 LDA2 LDA3 LDA4 'PLANE' ;
ELIM 0.001 SDAL1 ;
*
HDA1 = DROI 20 2P0 2P2 ;
HDA2 = DROI 10 2P2 2P3 ;
HDA3 = DROI 20 2P3 2P5 ;
HDA4 = DROI 10 2P5 2P0 ;
SDAL2 = DALLE HDA1 HDA2 HDA3 HDA4 'PLANE' ;
ELIM 0.001 SDAL2 ;
*
SDAL3 = SDAL2 PLUS (0.0 0.0 400.0) ;
*
*MAILLAGE TOTAL DALLES
*
SDAL = SDAL1 ET SDAL2 ET SDAL3 ;
*
*MAILLAGE TOTAL DE LA STRUCTURE
*
STOT = SPOT ET SPOUTL ET SPOUTT ET SVOILEX ET 
SVOILEY ET SDAL ;
ELIM 0.001 STOT ;
SI ('EGA' GRAP1 'O');
  TRAC OEIL CACHE STOT ;
  TRAC OEIL CACHE SVOILEY ;
FINSI;
*
******************************************************
* CONTRUCTION MODELES
******************************************************
*
*vecteur orientant l'axe local y des poteaux
PORIEC = 1.0 0.0 0.0 ;
*vecteur orientant l'axe local y des poutres longitudinales
PORIEL = 0.0 1.0 0.0 ;
*vecteur orientant l'axe local y des poutres transversales
PORIET = 1.0 0.0 0.0 ;
*
*poteaux
MODPOT = MODEL SPOT MECANIQUE ELASTIQUE POUT ; 
*poutres longitudinales
MODPOUTL = MODEL SPOUTL MECANIQUE ELASTIQUE POUT ;
*poutres transversales
MODPOUTT = MODEL SPOUTT MECANIQUE ELASTIQUE POUT ;
*voile X
MODVOIX = MODEL SVOILEX MECANIQUE ELASTIQUE COQ4 ;
*voile Y
MODVOIY = MODEL SVOILEY MECANIQUE ELASTIQUE COQ4 ;
*les deux dalles 
MODAL1 = MODEL SDAL1 MECANIQUE ELASTIQUE COQ4 ;
MODAL2 = MODEL SDAL2 MECANIQUE ELASTIQUE COQ4 ;
MODAL3 = MODEL SDAL3 MECANIQUE ELASTIQUE COQ4 ;
MODAL = MODAL1 ET MODAL2 ET MODAL3 ;
*
MODLTOT = MODPOT ET MODPOUTL ET MODPOUTT ET  MODVOIX 
ET MODVOIY ET MODAL;
*
******************************************************
* DEFINITION MATERIAUX
******************************************************
*
*module du beton
EBET = 330000.0 ;
*coeficient de poisson
cpoi = 0.20 ;
* densite beton pour les poutres, les poteaux et les voiles
RHOBET = 2500.0E-09 ;
*
*densite beton dalle + charges d'exploitation dalles 
*(inclues dans la densite des dalles)
RHODAL = 2500.0E-09*1.67 ;
*
*caracteristiques des coques et des poutres
*epaisseur voiles
epv = 25.0 ;
*epaisseur dalles
epa = 30.0 ;
*excentrement des voiles et des dalles par rapport 
*au plan moyen
ecpm = 0.0 ;
*
*sections poteaux
bp = 50.0 ;
hp = 50.0 ;
*sections poutres
bpout = 40.0 ;
hpout = 80.0 ;
*caracteristiques inertielles poteaux
IIY = (bp*hp*hp*hp)/(12.0) ;
IIZ = (hp*bp*bp*bp)/(12.0) ;
IIX = (0.141)*(bp*hp*hp*hp) ;
*caracteristiques inertielles poutres 
IIYP = (bpout*hpout*hpout*hpout)/(12.0) ;
IIZP = (bpout*hpout*hpout*hpout)/(12.0) ;
IIXP = (0.196)*(hpout*bpout*bpout*bpout) ;
*section poteaux
APOT = bp*hp ;
*sections poutres
ABEAM = bpout*hpout ;
*
*poteaux
MATPOT = MATER MODPOT 'YOUN' EBET 'NU' cpoi 'SECT' APOT 'VECT' 
PORIEC 'INRY' IIY 'INRZ' IIZ 'TORS' IIX 'RHO' RHOBET ; 
*poutres longitudinales
MATPOUTL = MATER MODPOUTL 'YOUN' EBET 'NU' cpoi 'SECT' ABEAM 
'VECT' PORIEL 'INRY' IIYP 'INRZ' IIZP 'TORS' IIXP 
'RHO' RHOBET ; 
*poutres transversales
MATPOUTT = MATER MODPOUTT 'YOUN' EBET 'NU' cpoi 'SECT' ABEAM 
'VECT' PORIET 'INRY' IIYP 'INRZ' IIZP 'TORS' IIXP
'RHO' RHOBET ; 
*voile X
MATVOIX  = MATER MODVOIX 'YOUN' EBET 'NU' cpoi 'EPAI' epv 
'RHO' RHOBET 'EXCE' ecpm ;
*voile Y
MATVOIY  = MATER MODVOIY 'YOUN' EBET 'NU' cpoi 'EPAI' epv 
'RHO' RHOBET 'EXCE' ecpm ;
*dalles
MATDAL1 = MATER MODAL1 'YOUN' EBET 'NU' cpoi 'EPAI' epa 
'RHO' RHODAL 'EXCE' ecpm ;
*
MATDAL2 = MATER MODAL2 'YOUN' EBET 'NU' cpoi 'EPAI' epa 
'RHO' RHODAL 'EXCE' ecpm ;
*
MATDAL3 = MATER MODAL3 'YOUN' EBET 'NU' cpoi 'EPAI' epa 
'RHO' RHODAL 'EXCE' ecpm ;
*
MATDAL = MATDAL1 ET MATDAL2 ET MATDAL3 ;
*
MATOT = MATPOT ET MATPOUTL ET MATPOUTT ET MATVOIX  ET
MATVOIY ET MATDAL ;
*
******************************************************
* BLOCAGES
******************************************************
*
CL1 = BLOQ 'DEPLA' 'ROTA' 0P0 ;
CL2 = BLOQ 'DEPLA' 'ROTA' 0P1 ;
CL3 = BLOQ 'DEPLA' 'ROTA' 0P5 ;
CL4 = BLOQ 'DEPLA' 'ROTA' LV1 ;
CL5 = BLOQ 'DEPLA' 'ROTA' AV1 ;
*
CLT  = CL1 ET CL2 ET CL3 ET CL4 ET CL5 ;
*
******************************************************
* DEFINITION MATRICES RIGIDITE ET MASSE
******************************************************
*
RIGSTR = RIGI MODLTOT MATOT ;
*rigidite totale
RIGT = RIGSTR ET CLT ;
MASTOT = MASS MODLTOT MATOT ;
*
******************************************************
* DEFINITION FORCE STATIQUE
******************************************************
*
CPSTAT = MANU CHPO STOT 1 UZ -981.0 ;
FSTAT = MASTOT*CPSTAT ;
*
*
******************************************************
* DEFINITION FORCE STATIQUE
******************************************************
*
DEPL = RESO RIGT FSTAT ;
*
* Contraintes statiques
CS = SIGMA MODLTOT MATOT DEPL ;
*
*TRAC OEIL CACHE SVOILEX ; FIN ;
*contraintes voile VY
CSVY = REDU CS SVOILEX ;
CSVY = 1.35*CSVY ;
*liste CSVY ; 
CSVY = CHAN 'GRAVITE' MODVOIX CSVY ;
*liste CSVY ; FIN ;
*
*********************************************
*DEFINITION DU BRAS DE LEVIER
*******************************
couv = 2.5 ;
haut = 25.0 ;
d = haut-(2*couv) ;
*liste d ; fin ;
ttv = 0.70 ;
*
******************************************************
* EFFORT STATIQUE SELON LE MODELE DE MARTI
******************************************************
*
VECT_11 = 0. 0. 1.;
VECT_22 = 0. 1. 0.;
*
EFF0 = EFFMARTI CSVY MODVOIX MATVOIX
           VECT_11 VECT_22 25. 2.5 2.5 0.7;
*
******************************************************
* ANALYSE MODALE
******************************************************
*
*
* masse modales dans les 3 directions pour selection des modes utiles
MMT_1 = 0. ;
MMT_2 = 0. ;
MMT_3 = 0. ;
*
* masse totale de l'ouvrage complet
CHP3 = MANU CHPO STOT UX 1. UY 1. UZ 1. ;
CHP3 = MASTOT * CHP3 ;
*
MT_1 = @TOTAL STOT CHP3 FX ;  
MT_2 = @TOTAL STOT CHP3 FY ;
MT_3 = @TOTAL STOT CHP3 FZ ;
*
*
*plage de frequences d'analyse
*si l'on veut on peut aller bien sur plus loin
*on peut par exemple mettre FREQSUP = 50, etc.
FREQINF = 0.05 ;
FREQSUP = 33.0 ;
*
* Analyse modale
TB1 = VIBRATION 'INTERVALLE' FREQINF FREQSUP
RIGT MASTOT 'MULT'  ;
*
* nombre de modes dans la table TB1 issue de VIBR
NB_MODE = (DIME (TB1 . 'MODES')) - 2 ;
*
* Calcul des contraintes modales                                                                            
TB1 = SIGSOL MODLTOT MATOT TB1 ;
*
*Calcul des reactions modales                                                                       
TB1 = REAC CLT TB1 ;
*
******************************************************
* CHARGEMENT SISMIQUE
******************************************************
*
*** liste des frequences [Hz]
LIS_FREQ = PROG 0.1 0.25 2.5 8.0 30.0 40.0 50.0 400.0 ;
*** liste des accelerations pour seisme suivant x et y [m.s-2]
LIS_ACCX = PROG 0.0175 0.0175 0.184 0.208 0.1 0.1 0.1 0.1 ;
*** liste des accelerations pour seisme suivant z [m.s-2]
LIS_ACCZ = (LIS_ACCX)*0.6666667 ;
***************************************************************
LIS_ACCX = (LIS_ACCX * 981.0)*3.0 ; ;
LIS_ACCZ = (LIS_ACCZ * 981.0)*3.0 ; ;
*
SEISME_X = EVOL MANU '[Hz]' LIS_FREQ '[cm.s-2]' LIS_ACCX ;
SEISME_Y = EVOL MANU '[Hz]' LIS_FREQ '[cm.s-2]' LIS_ACCX ;
SEISME_Z = EVOL ROUG MANU '[Hz]' LIS_FREQ '[cm.s-2]' LIS_ACCZ ;
SI ('EGA' GRAP1 'O');
  DESS (SEISME_X ET SEISME_Z) ; 
  DESS (SEISME_Y ET SEISME_Z) ;
FINSI;
*
******************************************************
* REPONSE BASE MODALE
******************************************************
*
*ATENTION: ICI ON SPECIFIE L'AMORTISSEMENT
*DES SPECTRES ET DES MODES
*
* liste des amortissements
LIS_AMOR = PROG 7.e-2  ;
BAS_AMOR = PROG NB_MODE * 7.e-2  ;
*
TAB1 = 'TABLE' ;
TAB3 = 'TABLE' 'EXCITATION' ;
TAB1 . 'STRUCTURE' =  TB1 ;
*
TAB1 . 'AMORTISSEMENT' = BAS_AMOR ;
TAB1 . 'EXCITATION' = TAB3 ; 
*TAB1 . 'TRONCATURE' = VRAI;
* Excitation suivant x (axe longitudinal de l'ouvrage)
TAB3 . 1 = 'TABLE' ;
TAB3 . 1 . 'DIRECTION' = 'X' ;
TAB3 . 1 . 'SPECTRE' = SEISME_X ;
TAB3 . 1 . 'AMORTISSEMENT' = LIS_AMOR ;
*
* Excitation suivant y (axe transversal de l'ouvrage)
TAB3 . 2 = 'TABLE' ;
TAB3 . 2 . 'DIRECTION' = 'Y' ;
TAB3 . 2 . 'SPECTRE' = SEISME_Y ;
TAB3 . 2 . 'AMORTISSEMENT' = LIS_AMOR ;
*
* Excitation suivant z (axe vertical de l'ouvrage)
TAB3 . 3 = 'TABLE' ;
TAB3 . 3 . 'DIRECTION' = 'Z' ;
TAB3 . 3 . 'SPECTRE' = SEISME_Z ;
TAB3 . 3 . 'AMORTISSEMENT' = LIS_AMOR ;
*
* recombinaisson quadratique complete
TAB1 . 'RECOMBINAISON_MODES' = 'CQC';
* Recombinaison des directions de seisme
TAB1 . 'RECOMBINAISON_DIRECTIONS' =  'QUADRATIQUE';
TAB1 . 'IMPRESSION' = FAUX ;
*
TAB4 = 'TABLE' 'SORTIES' ;
TAB1 . 'SORTIES' = TAB4 ;
*
* domaine sur lequel porte les resultats 
TAB4 . 'DOMAINE' = MODLTOT;
TAB4 . 'DEPLACEMENTS'   = VRAI ;
TAB4 . 'REACTIONS'      = VRAI ;
TAB4 . 'CONTRAINTES'    = VRAI ;
TAB4 . 'ACCELERATIONS'  = VRAI;
*
TAB5 = TABLE;
TAB5. 1                = TABLE;
TAB5. 1 .'MAIL'        = SVOILEX;
TAB5. 1 .'MODE'        = MODVOIX;
TAB5. 1 .'MATE'        = MATVOIX;
TAB5. 1 .'EPAISSEUR'   = 25.;
TAB5. 1 .'ENROBAGE_E'  = 2.5;
TAB5. 1 .'ENROBAGE_I'  = 2.5;
TAB5. 1 .'COT_THETA'   = 0.7;
TAB5. 1 .'DIR1'        = VECT_11;
TAB5. 1 .'DIR2'        = VECT_22;
*
* Calcul SISSIB
TAB2 = SISSIB TAB1 TAB5;
**************************************************************
*
*
SI ('EGA' GRAP1 'O');
TRAC 'CACH' (EXCO EFF0 'N11E') MODVOIX 'TITR' 'N11E - Statique';
TRAC 'CACH' (EXCO EFF0 'N22E') MODVOIX 'TITR' 'N22E - Statique';
TRAC 'CACH' (EXCO EFF0 'N12E') MODVOIX 'TITR' 'N12E - Statique';
TRAC 'CACH' (EXCO EFF0 'N11I') MODVOIX 'TITR' 'N11I - Statique';
TRAC 'CACH' (EXCO EFF0 'N22I') MODVOIX 'TITR' 'N22I - Statique';
TRAC 'CACH' (EXCO EFF0 'N12I') MODVOIX 'TITR' 'N12I - Statique';
*
CHEL1 = TAB2.'REPONSE_TOTALE'.'COVARIANCE'.'VOI1';
TRAC 'CACH' ((EXCO CHEL1 'C11E')**(0.5)) MODVOIX 'TITR' 'N11E - Seisme';
TRAC 'CACH' ((EXCO CHEL1 'C22E')**(0.5)) MODVOIX 'TITR' 'N22E - Seisme';
TRAC 'CACH' ((EXCO CHEL1 'C33E')**(0.5)) MODVOIX 'TITR' 'N12E - Seisme';
TRAC 'CACH' ((EXCO CHEL1 'C11I')**(0.5)) MODVOIX 'TITR' 'N11I - Seisme';
TRAC 'CACH' ((EXCO CHEL1 'C22I')**(0.5)) MODVOIX 'TITR' 'N22I - Seisme';
TRAC 'CACH' ((EXCO CHEL1 'C33I')**(0.5)) MODVOIX 'TITR' 'N12I - Seisme';
OUBL CHEL1;
FINSI;
*
******************************************************
* CALCUL DES MARGES
******************************************************
*
* On selection plusieurs zones de la voile SVOILEX
CH1 CH2 CH3 = 'COOR' (SVOILEX);
ELE1 = CH1 'POIN' 'COMPRIS' -2000. 2000.;
CH2  = 'REDU' CH2 ELE1 ;
ELE1 = CH2 'POIN' 'COMPRIS' -1000.001 1000.01;
CH3  = 'REDU' CH3 ELE1 ;
ELE1 = CH3 'POIN' 'COMPRIS' -0.001 400.01;
CH3  = 'REDU' CH3 ELE1 ;
SVOIX1 = (SVOILEX) 'ELEM' 'APPUYE' 
                   'STRICTEMENT' ELE1;
*
CH1 CH2 CH3 = 'COOR' (SVOILEX);
ELE1 = CH1 'POIN' 'COMPRIS' -2000. 2000.;
CH2  = 'REDU' CH2 ELE1 ;
ELE1 = CH2 'POIN' 'COMPRIS' -0.001 700.01;
CH3  = 'REDU' CH3 ELE1 ;
ELE1 = CH3 'POIN' 'COMPRIS' 399.98 800.01;
CH3  = 'REDU' CH3 ELE1 ;
SVOIX2 = (SVOILEX) 'ELEM' 'APPUYE' 
                   'STRICTEMENT' ELE1;
*
CH1 CH2 CH3 = 'COOR' (SVOILEX);
ELE1 = CH1 'POIN' 'COMPRIS' -2000. 2000.;
CH2  = 'REDU' CH2 ELE1 ;
ELE1 = CH2 'POIN' 'COMPRIS' -0.001 700.01;
CH3  = 'REDU' CH3 ELE1 ;
ELE1 = CH3 'POIN' 'COMPRIS' 799.98 1200.01;
CH3  = 'REDU' CH3 ELE1 ;
SVOIX3 = (SVOILEX) 'ELEM' 'APPUYE' 
                   'STRICTEMENT' ELE1;
*
MODVOIX1 = MODE SVOIX1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODVOIX2 = MODE SVOIX2 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODVOIX3 = MODE SVOIX3 MECANIQUE ELASTIQUE ISOTROPE COQ4;
*
* definition des caracteristiques
CAR_EXT1 = (MANU CHML MODVOIX1 'RHO1' 0.045 'RHO2' 0.02 'ENRO' 2.5)
        ET (MANU CHML MODVOIX2 'RHO1' 0.035 'RHO2' 0.02 'ENRO' 2.5)
        ET (MANU CHML MODVOIX3 'RHO1' 0.02 'RHO2' 0.015 'ENRO' 2.5);
CAR_INT1 = (MANU CHML MODVOIX1 'RHO1' 0.045 'RHO2' 0.02 'ENRO' 2.5)
        ET (MANU CHML MODVOIX2 'RHO1' 0.035 'RHO2' 0.02 'ENRO' 2.5)
        ET (MANU CHML MODVOIX3 'RHO1' 0.02 'RHO2' 0.015 'ENRO' 2.5);
CAR_COR1 = (MANU CHML MODVOIX1 'H' 25. 'RHOT' 0.001  'COTH' 2.5)
        ET (MANU CHML MODVOIX2 'H' 25. 'RHOT' 0.001  'COTH' 2.5)
        ET (MANU CHML MODVOIX3 'H' 25. 'RHOT' 0.001  'COTH' 2.5);
*
TAB_MAR = TABLE;
*
FBET1 = 300.;
FACI1 = 4000.;
GAM_C1 = 1.0;
ALP_C1 = 1.0;
NU_C1 = 1.0;
GAM_A1 = 1.0;
* definition table d'entres
TAB_MAR.'MAILLAGE' = SVOILEX;
TAB_MAR.'EFFORT_SEISME' = (TAB2.REPONSE_TOTALE.COVARIANCE.VOI1);
TAB_MAR.'EFFORT_STATIQUE' = EFF0;
TAB_MAR.'CARACTERISTIQUES_EXTERNE' = CAR_EXT1; 
TAB_MAR.'CARACTERISTIQUES_INTERNE' = CAR_INT1;
TAB_MAR.'CARACTERISTIQUES_CORE' =    CAR_COR1;
TAB_MAR.'FC_BET' =  FBET1;
TAB_MAR.'GAM_C' = GAM_C1;
TAB_MAR.'ALP_C' = ALP_C1;
TAB_MAR.'NU_C' = NU_C1;
TAB_MAR.'FS_ACI' = FACI1;
TAB_MAR.'GAM_S' = GAM_A1;
*
* liste des elements
ELE_LI1 = LECT 1 10 20 30 40 50 60 70 80 90 100;
*ELE_LI1 = LECT 169;
VAL_TOL1 = 0.04;
* verification elipsoide
T_LAM_EL = MRCSHELL 'ELLIPSOIDE' TAB_MAR VAL_TOL1 ELE_LI1;
* verification rectangle
T_LAM_RE = MRCSHELL 'RECTANGLE' TAB_MAR VAL_TOL1 ELE_LI1;
*
******************************************************
* Dessin
******************************************************
* extraction des sorties du calcul
LAM_RE_E = T_LAM_RE.'CH_LAMBDA_E';
LAM_RE_I = T_LAM_RE.'CH_LAMBDA_I';
LAM_RE_C = T_LAM_RE.'CH_LAMBDA_C';
CAR_RE_E = T_LAM_RE.'CARTE_E';
CAR_RE_I = T_LAM_RE.'CARTE_I';
CAR_RE_C = T_LAM_RE.'CARTE_C';
*
LAM_EL_E = T_LAM_EL.'CH_LAMBDA_E';
LAM_EL_I = T_LAM_EL.'CH_LAMBDA_I';
LAM_EL_C = T_LAM_EL.'CH_LAMBDA_C';
CAR_EL_E = T_LAM_EL.'CARTE_E';
CAR_EL_I = T_LAM_EL.'CARTE_I';
CAR_EL_C = T_LAM_EL.'CARTE_C';
*
* trace
SI ('EGA' GRAP1 'O');
V_MAX1 = MAXI LAM_RE_E;
V_MIN1 = MINI LAM_RE_E;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE 'outer layer: rectangle margin' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_RE_E) MODVOIX L_ISO1;
V_MAX1 = MAXI LAM_RE_I;
V_MIN1 = MINI LAM_RE_I;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE ' inner layer: rectangle margin' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_RE_I) MODVOIX L_ISO1;

V_MAX1 = MAXI LAM_RE_C;
V_MIN1 = MINI LAM_RE_C;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE 'core layer: ellipsoid margin lamda' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_RE_C) MODVOIX L_ISO1;

V_MAX1 = MAXI LAM_EL_E;
V_MIN1 = MINI LAM_EL_E;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE 'outer layer: ellipsoid margin lamda' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_EL_E) MODVOIX L_ISO1;
V_MAX1 = MAXI LAM_EL_I;
V_MIN1 = MINI LAM_EL_I;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE 'inner layer: ellipsoid margin lamda' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_EL_I) MODVOIX L_ISO1;

V_MAX1 = MAXI LAM_EL_C;
V_MIN1 = MINI LAM_EL_C;
L_ISO1 = PROG V_MIN1 PAS ((V_MAX1 - V_MIN1)/(20.)) V_MAX1;
TITRE 'core layer: ellipsoid margin lamda' ;
TRAC 'CACH' (1.E9 0. 0.) (LAM_EL_C) MODVOIX L_ISO1;

*
TRAC 'CACH' 'FACE' (1.E9 0. 0.) 
  ((CAR_RE_E) ET (CAR_EL_E PLUS (0. 800. 0.)))
'TITR' 'outer layer: left RECTANGLE right ELLIPSOID 
RED margin < 1, GREEN margin > 1';
TRAC 'CACH' 'FACE' (1.E9 0. 0.) 
  ((CAR_RE_I) ET (CAR_EL_I PLUS (0. 800. 0.)))
'TITR' 'inner layer: left RECTANGLE right ELLIPSOID 
RED margin < 1, GREEN margin > 1';
TRAC 'CACH' 'FACE' (1.E9 0. 0.) 
  ((CAR_RE_C) ET (CAR_EL_C PLUS (0. 800. 0.)))
'TITR' 'core layer: left RECTANGLE right ELLIPSOID 
RED margin < 1, GREEN margin > 1';
FINSI;
FIN;
FINSI;


 



