* fichier : flurevi.dgibi
************************************************************************
* Section : Mecanique Fluage
* Section : Mecanique Viscoplastique
************************************************************************
OPTION ECHO 0;
GRAPH='N';
SAUT PAGE ;

*
*******************************************************
*
*  TEST DU MODELE DE FLUAGE DE N.REVIRON
*
*******************************************************
*
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
a1 = 0.48;
b1 = 25.e6;
slmax = 1.;

* parametre de fluage propre
* fluage propre
k1 = 776.7e9;
k2 = 472.e9;
k3 = 98.1e9;
eta1 = 2.44e12*1.e9;

opti 'DIME' 2 'MODE' axis 'ELEM' qua8;

*
* GENERATION DU MAILLAGE
*

h = 5.e-3;
ri = 0.;
re = ri + 8.e-2;

p1= ri 0.;
p2= re 0.;
p3= re h;
p4= ri h;

dini1 = 1.003;
dfin1 = 0.997;

nelem1 = 1;
nelem2 = -30;

l1 = droi nelem2 p1 p2 'DINI' dini1 'DFIN' dfin1;
l2 = droi nelem1 p2 P3 ;
l3 = droi nelem2 p3 p4 'DINI' dfin1 'DFIN' dini1;
l4 = droi nelem1 p4 p1 ;

surf1 = DALLER l1 l2 l3 l4;

SI(NEG GRAPH 'N');
 trac surf1 ;
FINSI ;

*
* DISCRETISATION TEOMPORELLE
*
* 1h 1j 30j 700j
* nombre de pas de temps entre les temps indiqués
*

nb1 =  2 ;
nb2 =  20 ;
nb3 =  20 ;
nb4 =  30 ;

nbt1 = nb1 + nb2 + nb3 + nb4;

temps1 = 0.1*3600.;
temps2 = 2.99*24.*3600.;
temps3 = 3.*24.*3600.;
temps4 = 4.*24.*3600.;


ltemps1 = PROG 0.
 pas (temps1/nb1) temps1
 pas ((temps2-temps1)/nb2) temps2
 pas ((temps3-temps2)/nb3) temps3
 pas ((temps4-temps3)/nb4) temps4;

* ---------------------------
* CALCUL MECANIQUE
* ---------------------------

* Modele
mod1 = MODE surf1 'MECANIQUE' 'ELASTIQUE' 'VISCOPLASTIQUE' 'FLUTRA';


* Module d Young
eyou1 = 1.e20;
*33.7e9;
* Coefficient de Poisson
nu1 = 0.248;
* Seuil de fissuration en traction
kt01 = 3.5e6/eyou1 ;

atram1 = -0.2;
btram1 = 5.e3;
acomm1 = 14.4;
bcomm1 = 920.;

jour1 = 24. * 3600.;

mat1 = MATE mod1 'YOUN' eyou1 'NU  ' nu1 'KTR0' kt01
 'ALPHA' 1.22e-5
 'ATRA' atram1 'BTRA' btram1 'ACOM' acomm1 'BCOM' bcomm1
 'EKV1' k1 'TKV1' (0.1*jour1)  'EKV2' k2 'TKV2' (1.*jour1)
 'EKV3' k3 'TKV3' (10.*jour1)  'EKV4' 1.e30  'TKV4' (100.*jour1)
 'EKV5' 1.e30  'TKV5' (1000.*jour1)  'EKV6' 1.e30 'TKV6' (10.*jour1)
 'ETA1' eta1 'NUPO' nu1 'BETA' 0. 'XFDE' 3.e-10
 'VGEA' a1 'VGEB' b1 'SLMA' slmax;

* conditions aux limites mecaniques

rig1 = BLOQ 'UR' l4;
rig2 = BLOQ 'UZ' l1;

* Evolution du chargement de traction

vcont1 = -1.e6;
cont1 = PRES 'MASS' mod1 l3 vcont1;
pre1 = PROG (nb1+1)*1. (nb2+nb3)*1. nb4*1.e-6;
ev1 = EVOL 'MANU' 'Temps' ltemps1 'Pression' pre1;

SI(NEG GRAPH 'N');
 DESS EV1 ;
FINSI ;

cham1 = CHAR 'MECA' cont1 ev1;

* ------------------------
* calcul pasapas
* ------------------------

tab1=table;

tab1.caracteristiques=  mat1;
tab1.chargement=cham1;
tab1.modele= mod1;
tab1.temps_calcules=ltemps1;

tab1.blocages_mecaniques=rig1 ET rig2;
tab1.mova='D';
pasapas tab1;


ind1='INDEX' (TAB1.'CONTRAINTES') ;
nfin1 = ('DIME' ind1) - 1;

p5= p3;

n3 = 0;
dep1 = PROG;
tmps = PROG;

REPETER BOUCLE (nfin1 + 1);

tmps1 = tab1.temps.n3;
tmps1 = tmps1 / (24.*3600).;
tmps = tmps ET (PROG tmps1);

val1 = EXTR tab1.deplacements.n3 'UZ' p5;
val1 = -1. * val1 * 1.e12 / (vcont1 * (h));
dep1 = dep1 ET (PROG val1);


n3 = n3 + 1;

FIN BOUCLE;

evdep1 = EVOL 'ROUG' 'MANU' 'temps jours' tmps 'Deplact 1e6' dep1;

SI(NEG GRAPH 'N');
    DESS evdep1;
FINSI ;
*
TEMPS ;
*             CODE FONCTIONNEMENT

DEPREF = PROG   0. 1.65860E-02  4.93848E-02   1.4608       2.0792
       2.4920       2.8345       3.1404       3.4200       3.6778
       3.9168       4.1396       4.3480       4.5439       4.7287
       4.9038       5.0702       5.2290       5.3809       5.5267
       5.6670       5.8024       5.9333       5.9337       5.9341 ;
DEPREF = DEPREF ET ( PROG
       5.9346       5.9350       5.9354       5.9359       5.9363
       5.9367       5.9372       5.9376       5.9380       5.9384
       5.9389       5.9393       5.9397       5.9402       5.9406 );
DEPREF = DEPREF ET ( PROG
       5.9410       5.9415       5.9419       5.7260       5.3419
       5.0478       4.8190       4.6373       4.4899       4.3676
       4.2638       4.1736       4.0937       4.0216       3.9554 );
DEPREF = DEPREF ET ( PROG
       3.8940       3.8363       3.7816       3.7296       3.6797
       3.6317       3.5855       3.5407       3.4974       3.4554
       3.4147       3.3751       3.3366       3.2991       3.2627
       3.2273       3.1928       3.1592 ) ;

VMOY = 0. ;
NVAL = DIME DEPREF ;
REPE LAB NVAL  ;
  VMOY = VMOY + ( EXTR DEPREF &LAB) ;
FIN LAB;
VMOY = VMOY / NVAL ;
PRECIS = 2.E-5 ;

RESI1=(MAXI ABS (DEP1 - DEPREF))/ VMOY ;
MESS '    ';
MESS '    ';
MESS '    ';
mess ' RESIDU ' RESI1  '  precision ' PRECIS ;
MESS '    ';

SI(RESI1 >  PRECIS);
EVAL = EVOL 'ROUG' 'MANU' 'REFERENCE' DEPREF 'CALCUL' DEP1;
LIST EVAL ;
    ERRE  5;
FINSI;
FIN;









