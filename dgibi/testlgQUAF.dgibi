* fichier :  testlgQUAF.dgibi
************************************************************************
* Section : Maillage Autres
************************************************************************
*
* On teste le bon fonctionnement de l'option ARETE de DOMA.
* Ceci necessite la bonne description des lignes et des faces
* des elements QUAF
*
* EN 2D  QUA9 et TRI7
opti dime 2 elem QUA8;

Graph = FAUX ;

DISCR= QUAF ;
p0 = 0. 0. ;
p1 = 1. 0. ;
AB = p0 d 4  p1 ;
mt = ab trans 4  (0. 2.) ;
cd = cote 3 mt ;
opti elem tri6 ;
mtt = cd trans 2 (1. 1.);
mt = mt et mtt  ;
Mmt = chan mt QUAF ;
$mt = mode Mmt 'NAVIER_STOKES' DISCR;

arm = doma $mt 'ARETE';
lt = elem arm type;
Si (NON (EGA (dime lt) 1));
ERREUR 5;
FINSI;
A= extr lt 1 ;
Si (NON (EGA a 'SEG3'));
ERREUR 5;
FINSI;

Si Graph;
xv= coor 1 Mmt ;
trace xv Mmt ;
Finsi ;

* EN 3D  CU27 et PR21
opti dime 3 ELEM cu20 ;

vt = mt volu 8 trans (0. 0. 2.);
Mvt = chan QUAF vt ;
$vt = mode Mvt 'NAVIER_STOKES' DISCR;
arm = doma $vt 'ARETE';

lt = elem arm type;
Si (NON (EGA (dime lt) 1));
ERREUR 5;
FINSI;
A= extr lt 1 ;
Si (NON (EGA a 'SEG3'));
ERREUR 5;
FINSI;

Si Graph;
xv= (coor 1 Mvt) ;
trace xv Mvt ;
Finsi ;

* EN 3D  les 4 elements CU27 PR21 TE15 et PY19
 ae = enve vt ;
 u  = volu ae ;

 Mu = chan u QUAF ;
le = elem Mu type ;

Te=( (EXIST le 'CU27') et (EXIST le 'PR21') et
     (EXIST le 'TE15') et (EXIST le 'PY19'));
Si (NON TE);
ERREUR 5;
FINSI;

 ul = chan ligne Mu ;

lt = elem ul  type;
Si (NON (EGA (dime lt) 1));
ERREUR 5;
FINSI;
A= extr lt 1 ;
Si (NON (EGA a 'SEG3'));
ERREUR 5;
FINSI;


Si Graph;
xv =  (coor 1 Mu);
trace xv Mu;
Finsi ;
Fin ;








