
*********************************************************************
*                                                                   *
*      VALIDATION DE LA LIAISON COUPLAGE_DEPLACEMENT base A         *
*      OPTION FONCTION_CONVOLUTION DE L'OPERATEUR DYNE              *
*      modele GRANGER PAIDOUSSIS                                    *
*      (UTILISEE POUR LE CALCUL DES FORCES FLUIDELASTIQUES)         *
*      EXEMPLE INSPIRE DU TEST38 de GERBOISE                        *
*                                                                   *
*      CREATION : BP, 2017-01-05                                    *
*                                                                   *
*      MANIP TGV-ICE (CEA,DEN,DM2S,SEMT,DYN)                        *
*                                                                   *
*        Y                                                          *
*       /|\                                                         *
*        |                                                          *
*                                                                   *
*        P1   P2 P3                      P4 P5 P6                   *
*                __________________________                         *
*        |------||                         |||                      *
*        |      ||                         |||    ------>X          *
*        |------||_________________________|||                      *
*          LAME          TUBE            RONDELLE 2                 *
*              RONDELLE 1                                           *
*                                                                   *
*      HYPOTHESES SIMPLIFICATRICES :                                *
*      -1 seul mode                                                 *
*      -pas de forces turbulentes                                   *
*                                                                   *
*********************************************************************


*********************************************************************
*     Parametres globaux
*********************************************************************
GRAPH = FAUX; COMPLET = FAUX;
* GRAPH = VRAI; COMPLET = VRAI;

OPTION DIME 3 ELEM SEG2 ;
OPTION 'TRAC' 'PSC' 'EPTR' 5 'POTR' 'HELVETICA_16';
OPTION 'EPSI' 'LINEAIRE';

* point de vue
  oeil = 0. -10. 0.;
* nombre de modes a calculer
  NMODE = 1;
*   NMODE = 3;
*   NMODE = 10;

*********************************************************************
*     DEFINITION DU MODELE
*********************************************************************

*--------------------- Valeurs Numériques ----------------

 xpas = 45e-3 ;
* position de l'accelero
 xA = 160e-3;

* Lame Flexible
 ll = 100E-3 ;
 a = 25E-3 ;
 b = 4E-3 ;
 SL = a*b ;
 ILy = a*(b**3)/12 ;
 ILz = b*(a**3)/12 ;
 ILx = 0.292*(a**3)*(b**3)/((a**2)+(b**2)) ;

* cordon de soudure
 bc   = b*1. ;
 SLc  = a*bc ;
 ILyc = a*(bc**3)/12;
 ILzc = bc*(a**3)/12 ;
 ILxc = 0.292*(a**3)*(bc**3)/((a**2)+(bc**2)) ;

* Tube
 R = 15E-3 ;
 D = 2*R   ;
 e = 0.5E-3 ;
 L = 298E-3 ;
 ST  = pi*((R**2)-((R-e)**2)) ;

* Module Young
  YE=2.1E11 ;

* Rondelle cote lame
 e1 = 6E-3 ;
 SR1 = pi*(D**2)/4 ;

* Rondelle cote libre - definition en deux parties
 e2 = 15.8E-3 ;
 e3 = 0.8E-3 ;
 SR2 = pi*((D**2)-((D-(2*(e+e3)))**2))/4 ;
 SR3 = SR1 ;


* Coefficient de confinement des tubes
 DDe = (0.56*xpas/D+1.07)*xpas/D ;
 beta = (DDe**2+1)/(DDe**2-1) ;

* Masse ajoutee d'eau sous forme de masse volumique
 Rhos  = 7800.0 ;
 roflu = 1000.0;
 maslin = beta*roflu*pi*(D**2)/4 ;
 maslin1 = roflu*pi*(a**2) ;
 mvo = maslin1/SL + Rhos ;
 mv  = maslin/ST  + Rhos ;
 mv1 = maslin/SR1 + Rhos ;
 mv2 = maslin/SR2 + Rhos ;
 mv3 = maslin/SR3 + Rhos ;

*------------------------- Géométrie ---------------------

 P1 = 0.       0. 0. ;
 Pj = 0.004    0. 0. ;
 P2 = ll       0. 0. ;
 P3 = ll+e1    0. 0. ;
 P4 = ll+L-e2  0. 0. ;
 P5 = ll+L-e3  0. 0. ;
 P6 = ll+L     0. 0. ;

 L1 =  DROI 10  P1 Pj ;
 L2 =  DROI 100 Pj P2 ;
 L3 =  DROI 5   P2 P3 ;
 L4 =  DROI 100 P3 P4 ;
 L5 =  DROI 10  P4 P5 ;
 L6 =  DROI 5   P5 P6 ;
 L0 = L1 ET L2 ET L3 ET L4 ET L5 ET L6;
 L0 = L1 ET L2 ET L3 ET L4 ET L5 ET L6;

*-------------------------- Modèle -----------------------

 MDL1 = MODEL L1  MECANIQUE ELASTIQUE POUT ;
 MDL2 = MODEL L2  MECANIQUE ELASTIQUE POUT ;
 MDL3 = MODEL L3  MECANIQUE TUYAU ;
 MDL4 = MODEL L4  MECANIQUE TUYAU ;
 MDL5 = MODEL L5  MECANIQUE TUYAU ;
 MDL6 = MODEL L6  MECANIQUE TUYAU ;
 MDL  = MDL1 ET MDL2 ET MDL3 ET MDL4 ET MDL5 ET MDL6;

*-------------- Matériaux et Caractéristiques ------------

 MAT1 = MATE MDL1  YOUN YE NU 0.3 RHO mvo ;
 MAT2 = MATE MDL2  YOUN YE NU 0.3 RHO mvo ;
 MAT3 = MATE MDL3  YOUN YE NU 0.3 RHO mv1 ;
 MAT4 = MATE MDL4  YOUN YE NU 0.3 RHO mv ;
 MAT5 = MATE MDL5  YOUN YE NU 0.3 RHO mv2 ;
 MAT6 = MATE MDL6  YOUN YE NU 0.3 RHO mv3 ;
 MAT  = MAT1 ET MAT2 ET MAT3 ET MAT4 ET MAT5 ET MAT6;

 CAR1 = CARA MDL1  SECT SL INRY ILy INRZ ILz TORS ILx ;
 CAR2 = CARA MDL2  SECT SL INRY ILy INRZ ILz TORS ILx ;
 CAR3 = CARA MDL3  RAYO R EPAI R  ;
 CAR4 = CARA MDL4  RAYO R EPAI e  ;
 CAR5 = CARA MDL5  RAYO R EPAI (e+e3) ;
 CAR6 = CARA MDL6  RAYO R EPAI R  ;
 CAR =  CAR1 ET CAR2 ET CAR3 ET CAR4 ET CAR5 ET CAR6;

*------------- Matrices de Rigidité et de Masse ----------

 Ks0 = RIGI MDL (MAT ET CAR) ;
 Ms = MASS MDL (MAT ET CAR) ;


*------------ Condition Limite en Déplacement ------------

 BLQs =    BLOQ UZ RY P1 ;
 BLQplan = BLOQ UX UY RX RZ (L0) ;

 Ks = Ks0 ET BLQs ET BLQplan  ;

*------------------Calcul des Modes Propres --------------

 BMOD = VIBR 'IRAM' 0. NMODE Ks Ms ;
 BMOD.'MODES' = NNOR BMOD.'MODES' ( mots UX UY UZ ) ;
 TBM = BMOD . 'MODES';

*  POSTVIBR BMOD;

* amortissement modal xi_i
  LIS_XI = PROG 0.009173 0.001553 0.000914 0.000596 0.000532
                0.000361 0.000363 0.000258 0.000269 0.000204;
  LIS_XI = EXTR LIS_XI (LECT 1 PAS 1 NMODE) ;


*********************************************************************
*     Définition du profil de VITESSE et de Vmoy
*********************************************************************
*
*   0m/s            3.4 m/s
*           ^    ^    ^    ^    ^
*           |    |    |    |    |
* +---------+-------------------+-------> X
* 0        ll                 ll+L
*
* d'ou la vitesse moyenne sur le profil complet
Vmoy1 = 3.4 ;
Vmoy  = Vmoy1 * (L / (ll+L));
mess 'Vmoy1='Vmoy1 'm/s sur ['ll (ll+L)']  -->  Vmoy=' Vmoy 'm/s';


*********************************************************************
*  COEFFICIENTS temporels du MODELE DE GRANGER PAIDOUSSIS
*********************************************************************

  D_EXT = 30.E-3 ;
  RHO_2 = 1000.0 ;
  DCLDX = -8.11 ;
  CD    = 0.256 ;
  ALPHA = PROG  1.258145697069813  -2.258327764366904;
  DELTA = PROG  0.220857290865074  32.121982253844081;


*********************************************************************
*  DEFINITION DES TABLES NECESSAIRES AU CALCUL NON LINEAIRE
*********************************************************************

*----------------------------------------------*
*  CONDITIONS INITIALES
*----------------------------------------------*

  TAB_INIT = TABLE 'INITIAL' ;


*----------------------------------------------*
*  CHARGEMENT = PETIT CHOC INITIAL POUR LANCER L'INSTABILITE
*----------------------------------------------*
*
  PY1 = TBM . 1 . 'POINT_REPERE' ;
  GFC1 = MANU CHPO PY1 1 'FALF' 1. ;
  LFC1 = PROG 0. 1. 0. 0. ;
  LTEMPS = PROG 0. 1E-2 2E-2 100.  ;
  EVTC   = EVOL 'BRIQ' MANU LTEMPS LFC1 ;
* dess EVTC 'XBOR' 0. 1.;
  CHARG1 = CHAR GFC1 EVTC ;
  TAB_CHAR = TABLE 'CHARGEMENT';
  TAB_CHAR . 'BASE_A' = CHARG1;

*----------------------------------------------*
*     Les FORCES DE CHOC
*----------------------------------------------*

  jeu1 = 1.5E-3;
  PCHOC  = L0 POIN PROCH (0.248 0. 0.);
  list PCHOC;
  TL1 = TABLE 'LIAISON_ELEMENTAIRE' ;
  TL1.'TYPE_LIAISON' = MOT 'POINT_CERCLE_FROTTEMENT' ;
  TL1.'SUPPORT'                  =    PCHOC      ;
  TL1.'RAIDEUR'                  =    1.0E6   ;
  TL1.'RAYON'                    =    jeu1  ;
  TL1.'NORMALE'                  = ( 1.0  0.0  0.0 ) ;
  TL1.'EXCENTRATION'             = ( 0.0  0.0  0.0 ) ;
  TL1.'RAIDEUR_TANGENTIELLE'     =    0.0     ;
  TL1.'AMORTISSEMENT_TANGENTIEL' =    0.0     ;
  TL1.'COEFFICIENT_GLISSEMENT'   =    0.0     ;
  TL1.'COEFFICIENT_ADHERENCE'    =    0.0     ;

*----------------------------------------------*
*     Les FORCES FLUIDELASTIQUES
*----------------------------------------------*

  TFL1 = TABL 'LIAISON_ELEMENTAIRE';
  TFL1 . 'TYPE_LIAISON'             = MOT  'COUPLAGE_DEPLACEMENT';
  TFL1 . 'SUPPORT'                  = PY1;
  TFL1 . 'ORIGINE'                  = PY1;
  TFL1 . 'FONCTION_CONVOLUTION'     = MOT  'GRANGER_PAIDOUSSIS'  ;
  TFL1 . 'ALPHA'   = PROG  1.258145697069813  -2.258327764366904;
  TFL1 . 'DELTA'   = PROG  0.220857290865074  32.121982253844081;
  TFL1 . 'VITESSE'                  = Vmoy1;
  TFL1 . 'DIAMETRE'                 = D_EXT;
  TFL1 . 'COEFFICIENT'              =  -0.61649988E+06  ;

  TFL2 = TABL 'LIAISON_ELEMENTAIRE';
  TFL2 . 'TYPE_LIAISON'    = MOT   'COUPLAGE_VITESSE';
  TFL2 . 'SUPPORT'         = PY1;
  TFL2 . 'ORIGINE'         = PY1;
  TFL2 . 'COEFFICIENT'     =  -0.15152040E+01;

* rem: les COEFFICIENTs sont issus de calculs GERBOISE


*----------------------------------------------*
*     Regroupement des LIAISONS
*----------------------------------------------*
  TAB_LIAI   = TABLE 'LIAISON' ;
  TAB_LIAI . 'LIAISON_A' = TABLE 'LIAISON_A';
  TAB_LIAI . 'LIAISON_A' . 1 = TFL1;
  TAB_LIAI . 'LIAISON_A' . 2 = TFL2;
  TAB_LIAI . 'LIAISON_B' = TABLE 'LIAISON_B';
  TAB_LIAI . 'LIAISON_B' . 1 = TL1;

*----------------------------------------------*
*     L'AMORTISSEMENT
*----------------------------------------------*
  TAB_AMOR = TABLE 'AMORTISSEMENT';
  TAB_AMOR . 'AMORTISSEMENT' = AMOR BMOD (100.*LIS_XI);

*----------------------------------------------*
*     Les SORTIES
*----------------------------------------------*
*
  TAB_SORT = TABLE 'SORTIE' ;
  TAB_SORT . 'VARIABLE' = TABLE 'SORTIE';
  TAB_SORT . 'VARIABLE' . 'DEPLACEMENT'  = VRAI ;
  TAB_SORT . 'VARIABLE' . 'VITESSE'      = VRAI ;
  TAB_SORT . 'VARIABLE' . 'TYPE_SORTIE'  = MOT 'LISTREEL';


*********************************************************************
*     CALCUL NON LINEAIRE
*********************************************************************

* PARAMETRES :
* sortie tous les 'PAS_SAUVES' pas de temps (1 par defaut)
  nexpS = 2;
  nSORT = 2 ** nexpS;
* pour avoir un bilan d'energie propre
* nSORT = 1;
  PAS_TEMP  = 2.5E-5  ;
  si COMPLET;  nbloc     = 100; nexp      = 15;
  sinon;       nbloc     = 4;   nexp      = 16;
  finsi;
  NB_PAS    = 2 ** nexp ;
  nfft = nexp - nexpS;

* BOUCLE SUR LES BLOCS
  TR1 = tabl;
  REPE BBLOC nbloc;

    SI ( EGA &BBLOC 1 );
      TDY  =  DYNE  'DE_VOGELAERE' BMOD TAB_CHAR TAB_SORT TAB_LIAI
                                   TAB_AMOR NB_PAS PAS_TEMP nSORT ;
    SINON;
      TAB_INIT . 'REPRISE' = TDY . 'REPRISE' ;
      TDY = 'DYNE' 'DE_VOGELAERE' BMOD TAB_CHAR TAB_SORT TAB_LIAI
                          TAB_AMOR TAB_INIT NB_PAS PAS_TEMP nSORT ;
    FINSI;
    TR1 . &BBLOC = TDY;

  FIN BBLOC;


*********************************************************************
*  POST TRAITEMENT
*********************************************************************

*-----------------------------------------------------------------------
*  TRACES
*-----------------------------------------------------------------------

  moncoco  = MOTS   'JAUN' 'LIME' 'OLIV' 'BOUT'
                    'TURQ' 'CYAN' 'AZUR' 'BLEU' 'VIOL' 'INDI' ;
  ewQVQ = VIDE 'EVOLUTION' ;
  ewDSP = VIDE 'EVOLUTION' ;
  TDESS1 = TABL;
  TDESS1 . 'TITRE' = TABL;
  PRRICE = PROG;
  NB_PAS2 = NB_PAS / nSORT;
  NB_PAS3 = NB_PAS2 - 1;
  pr1 = prog 0. pas 1. NB_PAS3;
  hann1 = 0.5 * (1. - (cos (360./NB_PAS3 * pr1)));


* BOUCLE SUR LES BLOCS
  REPE BBLOC nbloc;

*   recup des donnees du bloc
    si COMPLET; coco = MOT 'VIOL';
    sinon;      coco = EXTR moncoco &BBLOC;
    finsi;
    TDYN1 = TR1 . &BBLOC;

*   grandeurs sorties  (temps et deplacement, vitesse modales)
    prtim1  = TDYN1 . 'TEMPS_DE_SORTIE';
    mess '>>> bloc ' &BBLOC ' : ' (dime prtim1) ' pas sauves';
    prQ1    = TDYN1 . 'DEPLACEMENT' . PY1;
    prVQ1   = TDYN1 . 'VITESSE'     . PY1;

    evQ1  = EVOL coco 'MANU' 't (s)' prtim1 'Q1' prQ1;
*     evVQ1 = EVOL coco 'MANU' 't (s)' prtim1 'VQ1' prVQ1;
*   diagramme de phase
    evQVQ = EVOL coco 'MANU'  'Q1' prQ1 'VQ1' prVQ1;
    ewQVQ = ewQVQ ET evQVQ;

*   calcul du spectre
    si (ega &BBLOC 1);
      prtim2= enle prtim1 (NB_PAS2 + 1);
      prQ2  = enle prQ1   (NB_PAS2 + 1);
    sinon;
      prtim2=prtim1; prQ2=prQ1;
    finsi;
    evQ2  = EVOL coco 'MANU' 't (s)' prtim2 'Q1' (hann1*prQ2);
    evDSP1 = DSPR nfft evQ1 'FMAX' 200. coco;
    evDSP2 = DSPR nfft evQ2 'FMAX' 200. 'PEAU';
    ewDSP = ewDSP ET evDSP1;

*   titres des courbes
    tdeb = extr prtim1 1;
    tfin = extr prtim1 (dime prtim1);
    TDESS1 . 'TITRE' . &BBLOC
    = CHAI 't\Î[' FORMAT '(F4.2)' tdeb '-' FORMAT '(F4.2)' tfin ']';

*   FREQUENCE DE RICE (OU FREQUENCE CENTRALE)
    n1 = DIME prVQ1;
    MOY_V  = (SOMM prVQ1) / n1;
    MOY_V2 = (SOMM (prVQ1**2)) / n1;
    SIG_V  = (MOY_V2 - (MOY_V**2))**0.5;
    MOY_U  = (SOMM prQ1) / n1;
    MOY_U2 = (SOMM (prQ1**2)) / n1;
    SIG_U  = (MOY_U2 - (MOY_U**2))**0.5;
    XRICE = (0.5 / PI) * SIG_V / SIG_U;
    PRRICE = PRRICE ET XRICE;

*   petit trace mignon
    SI GRAPH;
*     dess evDSP1 'LOGY' 'YBOR' 1.E-14 1.E-5
      dess (evDSP1 et evDSP2) 'LOGY' 'YBOR' 1.E-14 1.E-6
       'TITRE' (chai 'bloc ' &BBLOC ' t=' tfin 's');
    FINSI;

  FIN BBLOC;

  evRICE = evol 'BOUT' MANU 'bloc'  (prog 1 pas 1 nbloc)
                            'f_{Rice} (Hz)' PRRICE;
SI GRAPH;
  dess ewQVQ TDESS1 'LEGE';
  dess ewDSP TDESS1 'LOGY';
  dess evRICE;
FINSI;

*-----------------------------------------------------------------------
*  TEST DE NON REGRESSION
*-----------------------------------------------------------------------

* TEST
  XREF = 28.55;
  XTOL = 0.005 ;
  XERR = ABS (XRICE - XREF) / XREF;
  MESS XRICE XERR XTOL;

SI (XERR > XTOL) ;
  MESS 'IL Y A UNE ERREUR !' ;
  ERRE 5 ;
SINON ;
  MESS 'AUCUN PROBLEME DETECTE !' ;
FINSI ;

*-----------------------------------------------------------------------
* performances
*-----------------------------------------------------------------------
  TEMP IMPR MAXI CPU;

FIN ;




