* fichier :  test_jointsoft.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
********************************
*
* Cas test joint_soft
*
*  Ancrage d'une barre d'acier
*   Essai push-pull
*
*  D. COMBESCURE - EMSI 1999
*
********************************
opti dime 2 elem seg2;
*
ncot = 1;
*
p0 = 0.  0.;
p1 = 0.  1.;
bar1 = p0 d ncot p1;
*
opti elem qua4;
p01 = 0. 0.;
p11 = 0. 1.;
p02 = 1. 0.;
p12 = 1. 1.;
*
beto = (tran (d ncot p01 p11) ncot p02);
ligsup = d ncot p11 p12;
ligext = d ncot p01 p11;
elim 0.001 (beto et p01 et p11 et p02 et p12 et ligsup et ligext);
*
jointot = racc 1.d-4 ligext bar1 ;
*
* Acier
*
younbar = 200000.;
*
nubar = 0.;
*
Sbar = 3.14159*((10./2000)**2);
*
modbar1 = mode bar1 mecanique elastique barr;
matbar1 = mate modbar1 YOUN younbar NU nubar SECT Sbar
          RHO 0.;
*
*  Joint
*
* Courbe cisaillement
*
KN = (30000./0.05);
FT = 2.1;
*
UXX1 = prog 0.  (6./KN) 1.d-3 3.d-3 10.5d-3 50.d-3;
FXX1 = PROG 0.  6.0    13.0  13.0  6.0     6.0;
*
tras = evol manu UXX1 FXX1;
*
UXX2 = prog 0. (FT/KN) (5.*(FT/KN)) (10.*(FT/KN)) (20.*(FT/KN));
FXX2 = PROG 0.   FT      FT          (0.5*FT)        (0.5*FT) ;
*
trat = evol manu UXX2 FXX2;
*
PNOR = 0.;
*
*
modj=mode jointot 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
                                   JOI2;
matj='MATE' modj 'KS  ' KN 'KN  ' KN ;
*modj=mode jointot 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
*               'PLASTIQUE' 'JOINT_SOFT' JOI2;
*matj='MATE' modj 'KS  ' KN 'KN  ' KN
*                  'PNOR' 0.  'TRAS' TRAS 'TRAT' TRAT;
*
* BETON
*
YOUNBET = 30000.;
nubet = 0.25;
*
modbet = mode beto 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' QUA4;
matbet = mate modbet 'YOUN' younbet 'NU  ' nubet 'RHO ' 0.;
*
modtot = modbar1 et modj et modbet;
mattot = matbar1 et matj et matbet;
*
BL0 = BLOQ DEPL ligsup;
BL1 = BLOQ UY p0;
BL2 = BLOQ UY p1;
DEPP1 = DEPI (BL1 et BL2) (1.);
Protemp = prog 0. 10. 20. 101.;
ProHori = PROG 0. 0.01 -0.01 0.;
Procalc = PROG 0. 0.001 pas 0.1 1. pas 0.25  10.;
CharHori = CHAR DIMP DEPP1 (evol manu Protemp ProHori);
*
TAB1 = TABLE;
***TAB1 . 'MOVA'          = 'RIEN';
TAB1 . 'TEMPS_CALCULES' = ProCalc ;
*TAB1 . 'CONVERGENCE_FORCEE ' = 'FAUX';
TAB1 .'MAXITERATION'  = 50;
*TAB1.'PRECISION' = 1.D-5;
TAB1 .'CHARGEMENT' = Charhori;
TAB1 .'DYNAMIQUE' = faux;
TAB1 .'MODELE' = modtot;
TAB1 .'CARACTERISTIQUES'= mattot;
TAB1 .'BLOCAGES_MECANIQUES' = BL0 ET BL1 et BL2;
*
PASAPAS TAB1;
*
*
DEPT = TAB1.DEPLACEMENTS;
CONT = TAB1.CONTRAINTES;
TIME = INDEX DEPT;
DIMT = DIME TIME;
*
TIMP = PROG 0.;
DEPX = PROG 0.;
GXYT = prog 0.;
EFFX = prog 0.;
*
EFFXXX = PROG;
REPETER BOU1 (DIMT - 1);
  DEP1 = DEPT.(TIME.(&BOU1 + 1));
  CON1 = CONT.(TIME.(&BOU1 + 1));
  FOR1 = RESU (REAC (BL1 ET BL2) DEP1);
  DEPX = DEPX ET (PROG  (extr DEP1 UY p1));
  EFFX = EFFX ET (PROG (extr FOR1 FY p1));
  TIMP = TIMP ET (PROG (TIME.(&BOU1 + 1)));
FIN BOU1;
*
DXFX = EVOL MANU (DEPX) (EFFX);
TIFX =  EVOL MANU  TIMP (EFFX);
*
*dreal = extr (ipol (prog 0.001) DEPX EFFX) 1;
*dtheo =  extr (ipol (prog 0.001) UXX1 (Sbar*FXX1)) 1;
*prec = abs (dreal - dtheo);
*
*xlim0 = 1.D-7;
*
def1 = defo (extr modtot mail) DEP1;
*
FIN;
*












