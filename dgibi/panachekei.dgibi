* fichier : panachekei.dgibi
*********************************************************************
*    JET/PANACHE  2D - SEMI-INFINI                                  *
*    Comparaison K - Epsilon / RNG K - Epsilon                      *
*     il suffit pour cela d'enlever ou remettre                     *
*     l'option RNG                                                  *
*     Mode axisymetrique                                            *
* Le Richardson est parametrable Ri = -5.e-2                        *
*     Gregory Turbelin 29/12/1998                                   *
*                                                                   *
*     jpm, mars 2006, adaptation pour K-epsilon implicite (kei)     *
*     En implicite il est bon de contrôler les réentrées de fluide  *
*     procédure outflow                                             *
*                                                                   *
*********************************************************************

 OPTI DIME 2 ELEM QUA8;
*OPTI MODE AXIS; OPTI ISOV SULI;
 OPTI MODE AXIS;
*OPTI TRAC 'PSC' ;

COMPLET = VRAI;
COMPLET = FAUX;
graph = faux;
*graph = vrai;
*opti trace 'PSC' ;

*--------------------------- Numérique ---------------------------------
Si COMPLET;
NBIT=400;
DT = 1. ;
DISCR = MACRO;
KPRES= CENTREP1;
sinon;
NBIT=40;
DT = 5.   ;
DISCR = LINE;
DISCR = MACRO;
KPRES= CENTRE;
*KPRES= CENTREP1;
finsi;



*ALGOKEPS= mots 'Nut' 'Filtres' ;
 ALGOKEPS= mots 'Nut'  ;
*--------------------------- procédure ---------------------------------
*$$$$ OUTFLOW
'DEBPROC' OUTFLOW  ;
ARGU RX*TABLE ;
************************************************************************
* ZONE $Mt OPER 'OUTFLOW' $out Ro Un Muf INCO 'UN'
*
*         UN
*         MUF       viscosité dynamique effective
*                             (tourbillonnaire+moléculaire)
************************************************************************
rv=rx.'EQEX' ;
iarg=rx.'IARG' ;
*mess 'iarg='iarg;
$mt=rx.'DOMZ' ;

* Lecture du 1er Argument objet mmodel
Si(ega ('TYPE' rx.'ARG1') 'MMODEL  ');
$mfront=rx.'ARG1';
Dg=doma $mfront 'XXDIAGSI' ;
Sinon ;
erreur 2;
quitter outflow;
Finsi ;

* Lecture du 2ème Argument la densité
Si(ega ('TYPE' rx.'ARG2') 'MOT     ');
Ro = rv.inco.(rx.'ARG2');
Sinon ;
Ro = rx.'ARG2';
Finsi ;

* Lecture du 3ème Argument la vitesse
Si(ega ('TYPE' rx.'ARG3') 'MOT     ');
un = rv.inco.(rx.'ARG3');
Sinon ;
un = rx.'ARG3';
Finsi ;

* Lecture du 4ème Argument la viscosité
Si(ega ('TYPE' rx.'ARG4') 'MOT     ');
Muf = rv.inco.(rx.'ARG4');
Sinon ;
Muf = rx.'ARG4';
Finsi ;

**************** INITIALISATIONs ***********************
Si (Exist rx 'rxtm');
rxtm=rx.'rxtm';
lmx=rxtm.'lmx';
lm0=rxtm.'lm0';
lm=rxtm.'lm';
nc=rxtm.'nc';
MSV = chai rxtm.'msv' ;
MI1 = rxtm.'MI1' ;
KPRES=rxtm.'KPRES';
Sinon;

rxtm=table 'KIZX';
rx.'rxtm'=rxtm;
Si (NON (EGA (dime rx.'LISTINCO') 1));
  mess ' Erreur dans la procédure Outflow !';
  mess ' Il doit y avoir une inconnue !';
  quitter outflow ;
Finsi;
MI1=EXTR rx.'LISTINCO' 1 ;
lmx=(extr (rv.inco.MI1) 'COMP');
lm0=(extr un 'COMP');
nc=dime lm0;

Si(nc > 1);
*mess 'Outflow Cas vectoriel ' MI1;
MSV=chai 'VECT';
lm=mots (chai 1 MI1) (chai 2 MI1);
Si (EGA (vale dime) 3);
lm=lm et (mots (chai 3 MI1));
Finsi;
kpr=rx.'KOPT'.'KPOIN';
Si(EGA KPR 2);KPRES='CENTRE';Finsi;
Si(EGA KPR 4);KPRES='CENTREP1';Finsi;
Si(EGA KPR 5);KPRES='MSOMMET';Finsi;
Sinon;
*mess 'Outflow Cas scalaire  ' MI1;
MSV=chai 'SCAL';
lm=mots MI1;
Finsi;
rxtm.'lmx'=lmx;
rxtm.'lm0'=lm0;
rxtm.'lm'=lm;
rxtm.'nc'=nc;
rxtm.'msv'=chai MSV;
rxtm.'MI1'=MI1;
rxtm.'KPRES'=KPRES;

rxtm.'LISTINCO'=MOTS MI1 ;
rxtm.'DOMZ'=$mfront;
rxtm.'KOPT'=rx.'KOPT';
rxtm.'IARG'=1;
rxtm.'EQEX'=rx.'EQEX';
rxtm.'NOMZONE'=' ';
rxtm.'TDOMZ'=0;
rxtm.'NOMOPER'=mot 'MDIA_T';
Finsi;
**************** Fin Initialisations *******************

 mfront=doma $mfront maillage ;
 nj=doma $mt 'NORMALEV';
*unj= vect nj 0.1 ux uy jaune ;
*trace unj mt;
 njf = redu nj mfront ;
*unj= vect njf 0.1 ux uy rouge ;
*trace unj mt TITR ' FRONTIERE';
 xn = rv.inco.MI1;
 ujf = redu un mfront ;
 us=psca ujf lm0 njf lm0;
 ius=masq us 'INFERIEUR' 0.;
 rv.inco.'us'=us;
 rv.inco.'ius'=ius;
 Si(Ega nc 1);
 us=2.*(redu xn mfront);
 Finsi;
 rxtm.'ARG1'=kcht $mfront scal sommet ((-1.)*Ro*us*ius);

 st1 mat1 = MDIA rxtm ;
*st1 mat1 = KOPS 'MATRIK' ;

 Dgi=Dg*ius;
 Si(nc > 1);
 puj=0.;
 Si(EXIST (rv.inco) 'PRESSION');
 pn=redu (elno $mt rv.inco.'PRESSION' KPRES) mfront;
 puj=Ro*pn*us;
 Finsi;
 gru = redu (Muf*(kops 'GRADS' (exco un 'UX') $mt)) mfront;
 grv = redu (Muf*(kops 'GRADS' (exco un 'UY') $mt)) mfront;
 mgunj = (nomc (Dgi*((psca gru lm0 njf lm0)-puj)) (extr lm 1)) + (nomc (Dgi*((psca grv lm0 njf lm0)-puj)) (extr lm 2)) ;
 Si(Ega (vale dime) 3);
 mgunj = mgunj + (nomc (Dgi*((psca grv lm0 njf lm0)-puj)) (extr lm 3));
 Finsi;
 Sinon;
 grt = redu (Muf*(kops 'GRADS' xn $mt)) mfront ;
 mgunj = (nomc (Dgi*(psca grt lm0 njf lm0)) (extr lm 1)) ;
 Finsi;
 St1 = kcht $mfront MSV sommet comp lm ((-1.)*mgunj);

RESPRO St1 mat1 ;
FINPROC ;

** PROCEDURE DE FILTRE DE LA CONCENTRATION

 DEBP 4FILTRE ; ARGU RX*TABLE ;
 rv=rx.'EQEX' ;
 cn=rv.inco.'cn';
 cn=KOPS cn '|<' 0.;
 cn=KOPS cn '>|' 1.;
 rv.inco.'cn'=cn ;
as2 ama1 = 'KOPS' 'MATRIK';
'RESPRO' as2 ama1;
 FINPROC ;
*----------------------- fin procédures --------------------------------

****************************
* CONSTRUCTION DU MAILLAGE *
****************************
 DJ=1. ;RJ=DJ/2. ;

** POINTS
 P0=0.  0.; P1=RJ  0.;
 P2=8.  0.; P3=8. 15.;
 P4=RJ 15.; P5=0. 15.;

 P1b=4. 0. ; P2b=8. 8.;
 P3b=4. 15.; P5b=0. 8.;
 P6=8.   5.; P7=8.  10.;
 P9=0.  10.; P10=0.  5.;

 Pe1=20. 0. ; Pe2=20. 8.;
 Pe3=20. 15.; Pe4=20. 50.;
 Pe5=8. 50. ; Pe6=4. 50.;
 Pe7=RJ 50. ; Pe8=0. 50. ;

***** SEGMENTS *****
Si complet ;
N1=5 ; N2=-18 ; N3=-10 ; N4=-12 ; N5=-30 ; N6=-38 ;
N1=3 ; N2=-12; N3=-6 ; N4=-9 ; N5=-21 ; N6=-27;
sinon ;
N1=1 ; N2=-4 ; N3=-2 ; N4=-3 ; N5=-7  ; N6=-9 ;
finsi ;

 ENT=P0 DROI N1   P1;
 BA1=P1 DROI N2  P1b DINI (0.008*DJ)  DFIN (0.1*DJ);
 BA2=P1b DROI N3 P2 DINI (1.1*DJ)  DFIN (1.4*DJ);
 BA3=P2 DROI N4 Pe1 DINI (1.6*DJ) DFIN (3.*DJ);
 BAS=BA1 ET BA2 ET BA3;
 BAT=ENT ET BAS;

 CO1=Pe1 DROI N5  Pe2 DINI (0.008*DJ)  DFIN (0.1*DJ);
 CO2=Pe2 DROI N2  Pe3 DINI (1.35*DJ)  DFIN (1.5*DJ);
 CO3=Pe3 DROI N6  Pe4 DINI (1.8*DJ) DFIN (3.*DJ);
 COT=CO1 ET CO2 ET CO3;

 HT1=Pe4 DROI N4 Pe5 DINI (3.*DJ) DFIN (1.6*DJ);
 HT2=Pe5 DROI N3 Pe6 DINI (1.4*DJ) DFIN (1.1*DJ);
 HT3=Pe6 DROI N2 Pe7 DINI (0.1*DJ) DFIN (0.008*DJ);
 HT4=Pe7 DROI N1 Pe8;
 HTT=HT1 ET HT2 ET HT3 ET HT4;

 AX1=Pe8 DROI N6  P5 DINI (3.*DJ) DFIN (1.8*DJ);
 AX2=P5 DROI N2  P5b DINI (1.5*DJ) DFIN (1.35*DJ);
 AX3=P5b DROI N5  P0 DINI (0.1*DJ) DFIN (0.008*DJ);
 AXE=AX1 ET AX2 ET AX3;

 ELIM (BAT ET COT ET HTT ET AXE) 1.e-5;
 MT=DALL BAT COT HTT AXE PLAN;
 ORIENTER MT;

 Mmt= changer QUAF mt ;
 Mcot = chan QUAF cot ;
 Maxe = chan QUAF axe ;
 Mbat = chan QUAF bat ;
 Mhtt = chan QUAF htt ;
 Ment = chan QUAF ent ;
 Msor = chan QUAF (cot et htt);

 Elim (Mmt et Mcot et Maxe et Mbat et Mhtt et Msor) 1.e-5 ;

***** Modèles Navier_Stokes *********
 $MT =mode MMT  'NAVIER_STOKES' DISCR ;
 $COT=mode MCOT 'NAVIER_STOKES' DISCR ;
 $AXE=mode MAXE 'NAVIER_STOKES' DISCR ;
 $BAT=mode MBAT 'NAVIER_STOKES' DISCR ;
 $HTT=mode MHTT 'NAVIER_STOKES' DISCR ;
 $ENT=mode MENT 'NAVIER_STOKES' DISCR ;
 $SOR=mode MSOR 'NAVIER_STOKES' DISCR ;

 MT=doma $MT maillage;
 cot = doma $cot maillage ;
 axe = doma $axe maillage ;
 bat = doma $bat maillage ;
 htt = doma $htt maillage ;
 ent = doma $ent maillage ;
 sorp= doma $sor 'MSOMMET';

si graph;  TRAC MT; finsi;

**********************
* DONNEES PHYSIQUES  *
**********************
 rhoa=2*1.19; rhof=2*0.58;
 mua=1.8e-5 ; nua=mua/rhoa;
 cref=0.    ; Ri=0. -5.e-2;
 cref=0.    ; Ri=0. -1.   ;
 Re=3000.   ; iRe=1./Re;
 db=1.e-5   ; iSc=db/nua;
 RS=iRe*iSc ;  Sct=0.7;
 KA=2.e-5   ; EA=1.E-6;

***************
* EQUATIONS   *
***************
  rv=EQEX $MT ITMA  NBIT   ALFA 1.  OPTI EF 'SUPG' 'IMPL' 'ZONE' $MT OPER OUTFLOW $sor 1. 'un' 'MUF'  'INCO' 'un' ;
  rv = eqex rv OPTI EF 'SUPG' 'IMPL' 'ZONE' $mt 'OPER' 'KEPSILON' 1. 'un' iRe DT Ri 'cn' 'INCO' 'kn' 'en' ZONE $MT OPER NS 1. 'un' 'MUF' Ri 'cn' cref INCO 'un' ZONE $MT OPER 4FILTRE 'cn' ZONE $MT OPER TSCA 1. 'un' 'MUF' 0.  INCO 'cn' ;

  rv = eqex rv OPTI EFM1 'CENTREE' ZONE $MT OPER DFDT 1. 'un' DT 'un' 'MUF' INCO 'un' ZONE $MT OPER DFDT 1. 'cn' DT 'un' 'MUF' INCO 'cn' ;

 rv.nomvi='un';
  rv=eqex rv
    CLIM 'un' UIMP ENT 0.
         'un' VIMP ENT 1.
         'un' VIMP BAS 0.
         'un' UIMP AXE 0.
         'un' VIMP COT 0.
         'kn' TIMP ENT KA
         'en' TIMP ENT EA ;
  rv=eqex rv
    CLIM 'cn' TIMP ENT 1.
         'cn' TIMP COT 0.
         'cn' TIMP BAS 0.
         'kn' TIMP COT 1.e-5
         'en' TIMP COT 1.e-6;

    RVP= EQEX 'OPTI' 'EF' KPRES 'ZONE' $mt 'OPER' 'KBBT'  -1.  'INCO' 'un' 'PRES'
*  'ZONE' $paroi 'OPER' 'VNIMP' $vc 0.   'INCO' 'UN' 'PRES'
   ;
   Si (EGA KPRES 'MSOMMET');
   RVP= EQEX RVP 'CLIM' 'PRES' TIMP sorp 0.;
   Finsi;

    RV.'PROJ'= RVP ;

    RV.'ALGO_KEPSILON'=ALGOKEPS;

** INITIALISATIONS
 rv.inco=TABLE INCO;
 rv.inco.'un'=KCHT $MT VECT SOMMET (1.e-7 1.e-7);
 rv.inco.'kn'=KCHT $MT SCAL SOMMET 1.e-3;
 rv.inco.'en'=KCHT $MT SCAL SOMMET 1.e-4;
 rv.inco.'MUF'=KCHT $MT SCAL SOMMET nua;
 rv.inco.'ALFT'=KCHT $MT SCAL CENTRE nua;
 rv.inco.'cn'=KCHT $MT SCAL SOMMET 0.;
 rv.inco.'PRES'=kcht $mt scal KPRES 0. ;

 lh=   (POIN MT PROC (0.75 1.))  et (POIN MT PROC (0.75 5.)) et (POIN MT PROC (0.75 10.)) et (POIN MT PROC (0.75 15.))
    et (POIN MT PROC (0.75 20.)) et (POIN MT PROC (0.75 25.)) et (POIN MT PROC (0.75 33.)) et (POIN MT PROC (0.75 40.))
    et (POIN MT PROC (0.75 45.)) et (POIN MT PROC (0.75 50.));
  his=khis 'un' 1 lh 'un' 2 lh 'kn'   lh 'cn'   lh 'en'   lh; his.'KFIH'=1;
  rv.'HIST'=his;

 EXEC rv;


** POST-TRAITEMENT
**----------------
** HISTORIQUES
**------------
TAB1=TABLE;
TAB1.'TITRE'= table ;
TAB1.1 ='TIRR MARQ CROI';
TAB1.'TITRE' . 1 ='P1';
TAB1.2 ='TIRR MARQ TRIA';
TAB1.'TITRE' . 2 ='P2';
TAB1.3 ='TIRR MARQ CARR';
TAB1.'TITRE' . 3 ='P3';
TAB1.4 ='TIRR MARQ ETOI';
TAB1.'TITRE' . 4 ='P4';
TAB1.5 ='TIRR MARQ PLUS';
TAB1.'TITRE' . 5 ='P5';
TAB1.6 ='TIRR MARQ LOSA';
TAB1.'TITRE' . 6 ='P6';
TAB1.7 ='TIRR MARQ TRIB';
TAB1.'TITRE' . 7 ='P7';

 si graph;
 DESS (his.'1un') LEGE  TAB1 ;
 DESS (his.'2un') LEGE  TAB1 ;
 DESS (his.'kn') LEGE TAB1;
 DESS (his.'cn') LEGE TAB1;
 DESS (his.'en') LEGE TAB1;
*dessin his.'TABD' his.'kn' ;
*dessin his.'TABD' his.'en' ;
*dessin his.'TABD' his.'1un' ;
*dessin his.'TABD' his.'2un' ;

 finsi ;

** PROFILS SUR LE DOMAINE TOTAL
**------------------------------
vv = vect rv.inco.'un' 5. ux uy jaune ;
kk =  rv.inco.'kn' ;
ee =  rv.inco.'en';
cc =  rv.inco.'cn' ;
nn = rv.inco.'MUF' * (1./iRe);

 si graph;
titre 'Vecteur Vitesse';
trac vv MT (CONT MT); ;
titre 'PRODT';
trac (rv.inco.'PRODT') MT (CONT MT);
titre 'Ksi';
trac (rv.inco.'Ksi') MT (CONT MT);
titre 'Energie Cinetique Turbulente';
trac kk MT (CONT MT);
titre 'MUF /mu';
trac nn MT (CONT MT);
titre 'dissipation';
trac ee MT (CONT MT);
 finsi ;

** PROFILS A DIFFERENTES ALTITUDES
**---------------------------------
 LMT=CHAN LIGNE MT;
** ALTITUDES
 alt1 = 1. ; alt2 = 5.;
 alt3 = 10.; alt4 = 25.;

** LIGNES HORIZONTALES
 lign1=POIN MT DROI (POIN MT PROC (0. alt1)) (POIN MT PROC (20. alt1)) 1.e-3;
 lign2=POIN MT DROI (POIN MT PROC (0. alt2)) (POIN MT PROC (20. alt2)) 1.e-3;
 lign3=POIN MT DROI (POIN MT PROC (0. alt3)) (POIN MT PROC (20. alt3)) 1.e-3;
 lign4=POIN MT DROI (POIN MT PROC (0. alt4)) (POIN MT PROC (20. alt4)) 1.e-3;

 L1=ELEM LMT APPUYE lign1;
 L2=ELEM LMT APPUYE lign2;
 L3=ELEM LMT APPUYE lign3;
 L4=ELEM LMT APPUYE lign4;

 nut=rv.inco.'MUF' ;

 prvy1=EVOL CHPO (rv.inco.'un') uy L1;
 prvx1=EVOL CHPO (rv.inco.'un') ux L1;
 prc1=EVOL CHPO (rv.inco.'cn') scal L1;
 prk1=EVOL CHPO (rv.inco.'kn') scal L1;
 prnut1=EVOL CHPO nut scal L1;

 prvy2=EVOL CHPO (rv.inco.'un') uy L2;
 prvx2=EVOL CHPO (rv.inco.'un') ux L2;
 prc2=EVOL CHPO (rv.inco.'cn') scal L2;
 prk2=EVOL CHPO (rv.inco.'kn') scal L2;
 prnut2=EVOL CHPO nut scal L2;

 prvy3=EVOL CHPO (rv.inco.'un') uy L3;
 prvx3=EVOL CHPO (rv.inco.'un') ux L3;
 prc3=EVOL CHPO (rv.inco.'cn') scal L3;
 prk3=EVOL CHPO (rv.inco.'kn') scal L3;
 prnut3=EVOL CHPO nut scal L3;

 prvy4=EVOL CHPO (rv.inco.'un') uy L4;
 prvx4=EVOL CHPO (rv.inco.'un') ux L4;
 prc4=EVOL CHPO (rv.inco.'cn') scal L4;
 prk4=EVOL CHPO (rv.inco.'kn') scal L4;
 prnut4=EVOL CHPO nut scal L4;

 TAB2=TABLE;
 TAB2.'TITRE'= TABLE ;
 TAB2.1='TIRR ';
 TAB2.'TITRE' . 1  = MOT 'Z=1';
 TAB2.2='TIRC';
 TAB2.'TITRE' . 2  = MOT 'Z=5';
 TAB2.3='TIRL';
 TAB2.'TITRE' . 3  = MOT 'Z=10';
 TAB2.4='TIRM';
 TAB2.'TITRE' . 4  = MOT 'Z=25';

** ZOOM SUR L'AXE X
 xbinf = 0.;
 xbsup = 6.;
 xbsup2 =1.2;

 si graph;
 dess (prvy1 et prvy2 et prvy3 et prvy4) titr 'PROFIL VITESSE (Uz)' Xbor xbinf xbsup LEGE TAB2 titx 'R/DJ' tity 'Uz';
 dess (prc1 et prc2 et prc3 et prc4) titr 'PROFIL CONCENTRATION' Xbor xbinf xbsup LEGE TAB2 titx 'R/DJ' tity 'Cf';
 dess (prk1 et prk2 et prk3 et prk4) titr 'PROFIL EN CINET TURB' Xbor xbinf xbsup LEGE TAB2 titx 'R/DJ' tity 'K';
 dess (prnut1 et prnut2 et prnut3 et prnut4) titr 'PROFIL VISCO TURB' Xbor xbinf xbsup LEGE TAB2 titx 'R' tity 'Muf';
 finsi ;

** DIAMETRES CARACTERISTIQUES
**----------------------------
DEBP ADIM VISU*EVOLUTIO ;
LV = EXTR VISU 'ORDO' ;
LX = EXTR VISU 'ABSC' ;

** Recherche des grandeurs d'adimensionnalisation
Vaxe = EXTR LV 1 ;
V12 = Vaxe / 2. ;
i = 1 ;
REPETER BOUCLE (dime LV) ;
 Vcour = EXTR LV i ;
 SI (Vcour < V12) ;  quitter boucle   ;
 SINON ; i = i + 1 ;
 FINSI ;
FIN BOUCLE ;

** Determination de X12
Xi = EXTR LX i ;
Xii = EXTR LX (i - 1) ;
Vi = EXTR LV i ;
Vii = EXTR LV (i - 1) ;
X12 = (((Vii - V12) * Xi) + ((V12 -Vi) * Xii)) / (Vii - Vi) ;

** Adimensionnalisation
LVA = LV / (Vaxe + 1e-20) ;
LXA = LX / (X12 + 1e -20) ;
VISUA = EVOL 'MANU' 'X / X1/2' LXA 'V / Vmax' LVA ;
FINP X12 ;

 yaxe=COOR 2 axe;
 lyaxe=PROG;
 i=0;
 repe bloc (nbno AXE);
 i=i+1;
 vyaxe=EXTR yaxe SCAL (AXE POIN i);
 lyaxe=lyaxe et (prog vyaxe);
 fin bloc;
 lyaxe = ORDO lyaxe;
 LMT=CHAN LIGNE MT;
 diam2=prog;
 i=0;
 repeter boucl3 (dime lyaxe);
 i=i+1;
*mess 'i vaut' i;
 yi=extr lyaxe i;
 lign6=POIN MT DROI (POIN MT PROC (0. yi)) (POIN MT PROC (10. yi)) 1.e-3;
 L6=elem lmt appuye lign6;
 visu1=evol chpo rv.inco.'un' uy L6;
 XD=adim visu1;
 diam2=diam2 et (prog XD);
 menage;
 fin boucl3;
 dcar1=evol manu diam2 lyaxe;

 si graph;
 dess (dcar1)  titx 'R' tity  'Z';
 finsi ;

** GRANDEURS SUR L'AXE ADIMENSIONNEES
**------------------------------------

** Vitesse
 evv4=EVOL CHPO (rv.inco.'un') uy (INVE AXE);
 ev4=EXTR evv4 ORDO ;ev4=ENLEV ev4 1;
 evn4=LOG ev4;

 z4=EXTR evv4 ABSC ; z4=ENLEV z4 1;zn4=LOG z4;

 nevv4=EVOL MANU  zn4  evn4;
 Si graph;
 dess evv4 titr 'UY SUR L AXE (ADIM)';
 dess nevv4 titr 'UY SUR L AXE (ADIM) ECHELLE LOG';
 Finsi;
** Concentration
** Attention, on ne peut calculer la concentration
** qu'à partir d'un grand nombre de pas de temps
*
  Xa=287.; Xhe=2079.;
  cn1=KOPS (KOPS Xa '*' (rv.inco.'cn')) '/' (KOPS  (KOPS (KOPS 1 '-' (rv.inco.'cn')) '*' Xhe) '+' (KOPS (rv.inco.'cn') '*' Xa));


  evc1=EVOL CHPO cn1 SCAL (INVE AXE);
  ec1=extr evc1 ORDO; ec1=ENLE ec1 1; ecn1=LOG ec1;
  zc1=extr evc1 ABSC; zc1=ENLE zc1 1; zcn1=LOG zc1;
  nevc1=EVOL MANU  zcn1  ecn1;

 Si graph;
 dess evc1 titr 'CONCENTRATION SUR L AXE (ADIM)';
 dess nevc1 titr 'CONCENTRATION SUR L AXE (ADIM) ECHELLE LOG';

  TAB3=TABLE;
  TAB3.'TITRE'= TABLE ;
  TAB3.'TITRE' . 1  = MOT 'VITESSE AXIALE';
  TAB3.2='TIRC';
  TAB3.'TITRE' . 2  = MOT 'CONCENTRATION';

  dess (evv4 et evc1) LEGE TAB3;
  dess (nevv4 et nevc1) LEGE  TAB3;
 Finsi;

** Energie
 evk1=EVOL CHPO (rv.inco.'kn') SCAL (INVE AXE);
 ek1=EXTR evk1 ORDO;ek1=ENLE ek1 1;ek1=ABS ek1; ekn1=LOG ek1;
 zk1=EXTR evk1 ABSC;zk1=ENLE zk1 1;zk1=ABS zk1; zkn1=LOG zk1;
 nevk1=EVOL MANU 'Z' zkn1 'K' ekn1;

 si graph;
 dess evk1 titr 'EN. CINET. TURB. SUR L AXE (ADIM)';
 dess nevk1 titr 'EN. CINET. TURB. SUR L AXE (ADIM) ECHELLE LOG';
 finsi ;

**************************
*RESULTATS COMPLEMENTAIRES*
***************************
** LIGNES DE COURANT
**-----------------
 corx=COOR 1 MT;
 un=rv.inco.'un';
 un1=EXCO un ux; un1=-1*corx*un1;
 un2=EXCO un uy; un3=corx*un2;
 un1=KCHT $MT SCAL SOMMET un1;
 un2=KCHT $MT SCAL SOMMET un2;
 un3=KCHT $MT SCAL SOMMET un3;
 uncz=NOEL $MT un2;
 uncz=KCHT $MT SCAL CENTRE uncz;

 rt2d=KOPS un 'ROT' $MT;
 corx=KCHT $MT SCAL SOMMET corx;
 corm=NOEL $MT corx;

 unn1=KCHT $BAT SCAL SOMMET un1;
 unn2=KCHT $COT SCAL SOMMET un3;
 unn3=KCHT $HTT SCAL SOMMET (-1*un1);
 unn4=KCHT $AXE SCAL SOMMET (-1*un3);

 unn1=NOEL $BAT unn1;
 unn1=KCHT $BAT SCAL CENTRE unn1;
 unn2=NOEL $COT unn2;
 unn2=KCHT $COT SCAL CENTRE unn2;
 unn3=NOEL $HTT unn3;
 unn3=KCHT $HTT SCAL CENTRE unn3;
 unn4=NOEL $AXE unn4;
 unn4=KCHT $AXE SCAL CENTRE unn4;

 sw=2*uncz - (corm*rt2d);

 rk=EQEX $MT 'OPTI' 'EF' 'IMPL'
   ZONE $MT OPER LAPN 1. inco 'psi'
   ZONE $MT OPER FIMP sw inco 'psi'
   ZONE $BAT OPER FIMP unn1 inco 'psi'
   ZONE $COT OPER FIMP unn2 inco 'psi'
   ZONE $HTT OPER FIMP unn3 inco 'psi'
   ZONE $AXE OPER FIMP unn4 inco 'psi'
   CLIM 'psi' TIMP BAS 0.;
 rk.inco.'psi'=KCHT $MT SCAL SOMMET 0.;
 exec rk;

 si graph;
 psi=rk.inco.'psi';
 OPTI ISOV LIGNE;
 TRAC psi MT  (CONT (MT)) titr 'LIGNES DE COURANT';

 psi1=EVOL CHPO psi SCAL COT;
 db1=EXTR psi1 ORDO;db2=EXTR psi1 ABSC;
 npsi1=EVOL MANU 'Z' db2 'DEBIT' db1 ;
 DESS npsi1;
 titr 'DEBIT D ENTRAINEMENT';

 FINSI ;


 Si (NON COMPLET);
 uaxref=prog
   1.0000      0.95603       1.5700       1.9336       2.3534 2.5648       2.8291       2.9057       3.0356       3.0249
   3.0781       3.0320       3.0420       2.9820       2.9616 2.8752       2.8122       2.7166       2.6450       2.5539
   2.4868       2.4086       2.3435       2.2210       2.1198;
 uaxref=uaxref et (prog
   2.0306       1.9548       1.8835       1.8199       1.7601 1.7059       1.6548       1.6088       1.5662       1.5270
   1.4891       1.4534       1.4197       1.3889       1.3608 1.3397 );
zaxe = extr evv4 'ABSC';
list (extr evv4 'ORDO');
evuaxer=evol manu zaxe uaxref ;
m=(INTG evuaxer 'ABSO') SOMM ;
delt2= SOMM (INTG (evuaxer - evv4) 'ABSO')/m;
mess ' deltaL2 : m=' m ' delt2=' delt2;
  Si (delt2 > 1.e-2);ERREUR 5;Finsi;
 FINSI;

 FIN ;








