* fichier :  nouailhas_b1.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
**********************************************************************
*                                                                    *
* fichier : nouailhas_b1.dgibi                                       *
*                                                                    *
**********************************************************************
*                                                                    *
* fait par PLG le 03-09-97                                           *
*                                                                    *
**********************************************************************
*                                                                    *
* But : ktest pour la loi viscoplastique NOUAILHAS_B                 *
*                                                                    *
**********************************************************************
i_trace = faux ;
*
debproc perso1 plg*TABLE ;
*
li_t = plg.depo.li_tp ;
li_d = plg.depo.li_def ;
*
ind_1 = index (plg.temps) ;
nb_1 = dime ind_1 ;
*
v_t = plg.temps.(ind_1.nb_1) ;
v_d = extr (plg.deformations_inelastiques.(ind_1.nb_1)) EIZZ 1 1 1 ;
*
li_t = li_t et (prog v_t) ;
li_d = li_d et (prog (0.0 - v_d)) ;
ev_d = evol manu ' ' li_t ' ' li_d ;
*
plg.depo.li_tp  = li_t ;
plg.depo.li_def = li_d ;
*
dess ((plg.depo.ev_ref) et ev_d) NCLK ;
finproc ;
**********************************************************************
option dime 2 ;
option elem qua8 ;
option mode axis ;
*
a00 = 0.0 0.0 ;
a01 = 1.0 0.0 ;
a02 = 1.0 1.0 ;
a03 = 0.0 1.0 ;
*
la00a01 = droi 1 a00 a01 ;
la01a02 = droi 1 a01 a02 ;
la02a03 = droi 1 a02 a03 ;
la03a00 = droi 1 a03 a00 ;
*
tot = daller la00a01 la01a02 la02a03 la03a00 ;
**********************************************************************
m_model = model tot MECANIQUE ELASTIQUE VISCOPLASTIQUE NOUAILHAS_B
 CONS UN ;
*
cst_youn = 260000.0 ;
cst_nu   = 0.3 ;
cst_alph = 1.0e-6 ;
cst_rho  = 1.0e-8 ;
cst_n    = 7.0 ;
cst_kk   = 130.0 ;
cst_k0   = 400.0 ;
cst_alf  = 0.0 ;
cst_cl1  = 15000.0 ;
cst_dnl1 = 300.0 ;
cst_ptm1 = 0.0 ;
cst_gdm1 = 0.0 ;
cst_cl2  = 6000.0 ;
cst_dnl2 = 100.0 ;
cst_phi  = 0.0 ;
cst_b    = 0.0 ;
cst_rmax = 150.0 ;
cst_br   = 500.0 ;
*
m_mater = mater m_model      YOUN cst_youn NU   cst_nu   ALPH cst_alph
 RHO  cst_rho  N    cst_n    KK   cst_kk   K0   cst_k0   ALF  cst_alf
 CL1  cst_cl1  DNL1 cst_dnl1 PTM1 cst_ptm1 GDM1 cst_gdm1 CL2  cst_cl2
 DNL2 cst_dnl2 PHI  cst_phi  B    cst_b    RMAX cst_rmax BR   cst_br   ;
**********************************************************************
m_bloca = bloque UZ la00a01 ;
*
t00temps = 0.0 ;
t01temps = t00temps +    0.01 ;
t02temps = t01temps + 1000.00 ;
*
li_temps = prog t00temps t01temps t02temps ;
*
t00press =   0.0 ;
t01press = 200.0 ;
t02press = 200.0 ;
*
li_press = prog t00press t01press t02press ;
*
ev_press = evol manu 'TEMPS' li_temps 'P' li_press ;
*
m01press = press MASSE m_model 1.0 la02a03 ;
m_charg = charg MECA m01press ev_press ;
**********************************************************************
p01nbpas =  2.0 ;
p02nbpas = 50.0 ;
*
p01pas   = (t01temps - t00temps) / p01nbpas ;
p02pas   = (t02temps - t01temps) / p02nbpas ;
*
p01lipas = prog (t00temps + p01pas) pas p01pas t01temps ;
p02lipas = prog (t01temps + p02pas) pas p02pas t02temps ;
*
p_lipas = p01lipas et p02lipas ;
**********************************************************************
li_defvg = prog      0.0       0.1066e-3 0.1861e-3 0.3487e-3 0.4539e-3
 0.5944e-3 0.6889e-3 0.7997e-3 0.8792e-3 ;
li_tps   = prog           0.0      25.0      50.0     125.0     200.0
    350.0     500.0     750.0    1000.0 ;
ev_ref  = evol manu 't' li_tps 'def' li_defvg ;
*dess ev_ref NCLK ;
**********************************************************************
p_table = table ;
p_table.modele                    = m_model ;
p_table.caracteristiques          = m_mater ;
p_table.blocages_mecaniques       = m_bloca ;
p_table.chargement                = m_charg ;
p_table.temps_calcules            = p_lipas ;
*
si i_trace ;
p_table.procedure_perso1          = VRAI ;
p_table.depo = table ;
p_table.depo.ev_ref = ev_ref ;
p_table.depo.li_tp  = prog ;
p_table.depo.li_def = prog ;
finsi ;
**********************************************************************
TMASAU=table;
p_table . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
pasapas p_table ;
**********************************************************************
ind_1 = index (p_table.deformations_inelastiques) ;
nb_1 = dime ind_1 ;
v_d = extr (p_table.deformations_inelastiques.(ind_1.nb_1))
 EIZZ 1 1 1 ;
v_d = abs(v_d) ;
v_dif = abs(v_d - 0.8777e-3)/0.8777e-3 ;
si (v_dif > 0.01) ;
 erre 5 ;
sinon ;
 erre 0 ;
finsi ;
**********************************************************************
fin ;







