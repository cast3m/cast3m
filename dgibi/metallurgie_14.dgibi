* fichier :  metallurgie_06.dgibi
************************************************************************
* Section : Thermique Metallurgie
************************************************************************

*__________________________________________________________________*
*   TEST METALLURGIE_14                                            *
*                                                                  *
*   CALCUL DES PROPORTIONS DE PHASE METALLURGIQUE                  *
*                                                                  *
*   Un MODELE thermo-metallurgique est cree et l'essai JOMINY      *
*   est simule en 2D Axisymétrique                                 *
*                                                                  *
*   Le modèle métallurgique est codé en ESOPE (Cast3m)             *
*   Utilisation de PASAPAS et d'une procedure utilisateur :        *
*      - Procédure PARATHER : Mise a jour du matériaux THERMIQUE   *
*                             avec la proportion de phase.         *
*                             Mise a jour du chargement 'TPOI'     *
*                                                                  *
*   Le modèle métallurgique contient 5 phases ayant des cinétiques *
*   de transformation différentes                                  *
*      - Metal de Base (MB)                                        *
*      - Austénite                                                 *
*      - Martensite                                                *
*      - Bainite                                                   *
*      - Ferrite                                                   *
*__________________________________________________________________*
*   MAILLAGE :                                                     *
*    - Eprouvette cylindrique de hauteur 100mm et de rayon 12,5mm  *
*      P4 _____ P3                                                 *
*        |  J  |    ^                                              *
*        |  O  |    |                                              *
*        |  M  |    |                                              *
*        |  I  |    | h0      Y                                    *
*        |  N  |    |         ^                                    *
*        |  Y  |    |         |                                    *
*      P1 _____ P2  v         o --> X                              *
*                                                                  *
*        <-----> r0                                                *
*      Origine du repere : P1                                      *
*                                                                  *
*   CHARGEMENT :                                                   *
*    - Montee lineaire en temperature depuis 25°C à 900°C en 1200s *
*    - Maintient à 900°C jusqu'a 3000s                             *
*    - Refroidissement par convection par la face cylindrique      *
*      inférieure jusqu'à 3600s                                    *
*                                                                  *
*   CL :                                                           *
*    - Blocage de la temperature (T imposee jusqu'a 3000s)         *
*    - Convection de 3000s à 3600s avec h=0,1W.mm-2.K-1            *
*__________________________________________________________________*
OPTI DIME 2 MODE AXIS ELEM QUA4 ;

'OPTI' 'TRAC' PSC 'EPTR' 10 ;

TMAX = 900.  ;
TMIN = 25.   ;
HCON = 0.1   ;

************************************************************************
*  Pour faire des beaux DESSINS
************************************************************************
* LISTMOTS contenant les couleurs
  LESCOUL = 'MOTS' 'VIOL' 'VERT' 'CYAN' 'BRIQ' 'BOUT' 'JAUN' 'OLIV'
                   'ORAN' 'ROUG' 'BLEU' 'BRUN' 'CARA' 'GRIS' 'NOIR' ;
  NBCOUL  = 'DIME' LESCOUL;

* LISTMOTS contenant les marques dans DESS
  LESMARQ = 'MOTS' 'CROI' 'PLUS' 'MOIN' 'BARR' 'ETOI' 'CARR'
                   'LOSA' 'ROND' 'TRID' 'TRIU' 'TRIL' 'TRIR';
  NBMARQ  = 'DIME' LESMARQ;
************************************************************************

************************************************************************
* Debut de la procedure PARATHER
************************************************************************
'DEBP' PARATHER  PRECED*'TABLE' TPS1*'FLOTTANT';
************************************************************************
* PROCEDURE PARATHER :
*   DESCRIPTION :
*   -------------
*   Cette procedure permet de modifier le materiaux avant l'appel a
*   VARI 'NUAG' dans TRANSNON.
*     - Les composantes K, C et RHO sont une moyenne arithméthique
*       pondérée par la fraction de phase des Ki, Ci et RHOi
*   L'indice ETAB.'MAT_TOT' est remplacé
*   Cette procedure permet de modifier le chargement 'TPOI' avant
*   l'appel a VARI 'NUAG' dans TRANSNON.
*   L'indice ETAB . 'CHARGEMENT' est remplacé
*
*   ENTREES :
*   ---------
*   - PRECED : TABLE complete de PASAPAS
*   - TPS1   : Temps courant
*
*   SORTIES :
*   ---------
*   - Aucune
*
************************************************************************

*I - Mise a jour du materiau Thermique avec les proportions de phases
   ETAB    = PRECED.'WTABLE';
   MOD1    = ETAB.'MOD_TOT' ;
   T1      = ETAB.'THER_COURANT' ;
   T1      ='CHAN' 'CHAM' T1 MOD1 'RIGIDITE' 'CARACTERISTIQUES';

   MAT_ORIG= ETAB.'MAT_TOT';
   COMPORIG='EXTR' MAT_ORIG 'COMP';
   NBCOMP  ='DIME' COMPORIG;

   MAT0 ='VIDE' 'MCHAML';
   REPE SURCOM NBCOMP;
     II     = &SURCOM;
     LACOMP ='EXTR' COMPORIG II;
     SI(('NEG' LACOMP 'K'  ) 'ET'
        ('NEG' LACOMP 'RHO') 'ET'
        ('NEG' LACOMP 'C'  ));
        MAT0 = MAT0 'ET' ('EXCO' LACOMP MAT_ORIG);
     FINS;
   FIN SURCOM;

*  Instanciation du matériaux de chaque phase
   MATPHAS ='REDU'  MOD1 MATPHAS ;
   MAT2    ='VARI' 'NUAG' MOD1 MATPHAS T1 'RIGIDITE';

*  On fait ensuite la moyenne qui nous interesse (Ponderation par la
*  proportion de phase)
   NPA ='DIME' PRECED.'TEMPS';
   SI ('EXIS' PRECED.'PROPORTIONS_PHASES'   NPA )   ;
     MCHPHA = PRECED.'PROPORTIONS_PHASES' . NPA     ;
   SINO ;
     MCHPHA = PRECED.'PROPORTIONS_PHASES' .(NPA - 1);
     PRECED.'PROPORTIONS_PHASES' . NPA = MCHPHA     ;
   FINS ;

   MCHPHA = 'CHAN' 'CONS' MCHPHA 'MAT1'  ;
   MCHPHA = 'REDU' MCHPHA ETAB.'MOD_COND' ;
   MCHPHA ='CHAN' 'RIGIDITE' MCHPHA MOD1           ;
   MCHPHA ='CHAN' 'TYPE' MCHPHA 'CARACTERISTIQUES' ;

   MOPROP1='EXTR'  MCHPHA 'COMP'                   ;
   MOMAT2 ='EXTR'  MAT2   'COMP'                   ;

   MCHK ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;
   MCHR ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;
   MCHC ='CHAN' 'TYPE' ('VIDE' 'MCHAML') 'CARACTERISTIQUES' ;

* Boucle sur les phases
   NBPHA  = 'DIME' MOPROP1;
   REPE SURP NBPHA;
     IP = &SURP ;
     PROPIP ='EXCO' MCHPHA ('EXTR' MOPROP1 IP) 'SCAL' ; 'COMM' 'Prop de la phase numero IP' ;

*    Moyenne sur les K
     IB = IP;
     RATIO = 'EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL';
     MCHK = (PROPIP * RATIO) + MCHK ;

*    Moyenne sur les RHO
     IB = IB + NBPHA;
     RATIO2 = 'EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL';
     MCHR = (PROPIP * RATIO2) + MCHR ;

*    Moyenne sur les CP
     IB = IB + NBPHA;
     RATIO3 = 'EXCO' MAT2 ('EXTR' MOMAT2 IB) 'SCAL';
     MCHC = (PROPIP * RATIO3) + MCHC ;
   FIN SURP;

   MCHK ='CHAN' 'COMP' 'K'   MCHK ;
   MCHR ='CHAN' 'COMP' 'RHO' MCHR ;
   MCHC ='CHAN' 'COMP' 'C'   MCHC ;

*  REMPLACEMENT DU MATERIAUX
   ETAB.'MAT_TOT' = MCHK 'ET' MCHR 'ET' MCHC 'ET' MAT0 ;


*II - Mise a jour du Chargement TPOI avec les champs de temperatures initiaux et finaux

* On recupere le modele complet : Conduction + Convection + Metallurgie :
  MODTOT  = ETAB.'MOD_TOT';

  ETAB = PRECED . 'WTABLE';
  CHARG = ETAB . 'CHARGEMENT';

* On modifie uniquement le chargement TPOI, on conserve les autres chargements.
  CHAR_T = 'ENLE' CHARG 'TPOI';

* On recupere tempINI et tempFIN
  NPA ='DIME' PRECED.'TEMPS';
  temps0        = PRECED.'TEMPS'.(NPA - 1);
  temps1        = TPS1                    ;

* On recupere T_INI et T_FIN
  T_CHPO1  ='EXCO' PRECED.'CONTINUATION'.'TEMPERATURES' 'T'  'SCAL' ;
  T_CHPO2  ='EXCO' ETAB  .'THER_COURANT' 'T'  'SCAL' ;
  T_CHAM1  ='CHAN' 'CHAM' T_CHPO1 MODTOT 'RIGIDITE';
  T_CHAM2  ='CHAN' 'CHAM' T_CHPO2 MODTOT 'RIGIDITE';

* Dependance temporelle du chargement TPOI :
  LTPSCHA ='PROG'  temps0  temps1 ;
  LTPOI   ='PROG'  1.D0    1.D0 ;
  EVT     ='EVOL' 'MANU' LTPSCHA LTPOI ;

* Dependance spatiale du chargement TPOI :
  TPOIN   = 'EXCO' ( ( T_CHPO2 - T_CHPO1 ) / (temps1 - temps0) ) 'SCAL' 'TPOI' ;

* Le chargement TPOI :
  CHAR_TPO   ='CHAR' 'TPOI' TPOIN EVT ;

* Le chargement complet (TPOI + le reste) :
  CHAR_TOT = CHAR_T 'ET' CHAR_TPO ;

* Mise a jour du chargement :
  ETAB . 'CHARGEMENT' = CHAR_TOT ;

'FINP';
************************************************************************
* Fin de la procedure PARATHER
************************************************************************

*MAILLAGE
h0 = 100.  ; COMM 'hauteur du cylindre' ;
r0 = 12.5  ; COMM 'rayon du cylindre'   ;
dens0 = 1. ; COMM 'densité maillage'    ;

axes = @repere (0. 0.) ('PROG' 25. 25.) 'ROUG' ;
P1 = POIN 0. 0. ;
P2 = POIN r0 0. ;
P3 = POIN r0 h0 ;
P4 = POIN 0. h0 ;

L1 = DROI P1 P2 'DINI' dens0 'DFIN' dens0 ;
L2 = DROI P2 P3 'DINI' dens0 'DFIN' dens0 ;
L3 = DROI P3 P4 'DINI' dens0 'DFIN' dens0 ;
L4 = DROI P4 P1 'DINI' dens0 'DFIN' dens0 ;

MAILT = (DALL L1 L2 L3 L4) COUL 'BLEU' ;
ELIM MAILT 1.e-3 ;
CONTT = CONT MAILT ;
PTBAS = CONTT POIN 'DROIT' P1 P2 1.e-3 ;
SURFBAS = CONTT ELEM 'APPUYE' 'STRICTEMENT' PTBAS ;

*MODELE
MODTHE = MODE MAILT   'THERMIQUE' 'CONDUCTION' 'QUA4' 'CONS' 'MAT1' ;
MODCON = MODE SURFBAS 'THERMIQUE' 'CONVECTION' 'SEG2';
MODTHERM = MODTHE 'ET' MODCON;

************************************************************************
* Definition de la metallurgie : DEBUT
************************************************************************
*  1- Métal de base 100% initialement
*  2- Martensite
*  3- Bainite
*  4- Ferrite
*  5- Austénite

* Evolution des proprietes thermiques pour les phases pures
MCHK   = VIDE 'MCHAML';
MCHRHO = VIDE 'MCHAML';
MCHC   = VIDE 'MCHAML';
CTYP   = MOT  'CARACTERISTIQUES';
********************
* 1- Métal de base *
********************
IPAHS  = 1;
CPHAS  = CHAI 'K' IPAHS;
LTKP1  = PROG 0 55 113 162 191 243 293 392
              457 507 605 655 756 795 840 856
              900 950 1000 1101 1202 1303 1400 1500
              1502 1600   ;
LKP1   = PROG 0.0376 0.0389 0.0401 0.0407 0.0407 0.0404 0.0398 0.038
              0.0364 0.0352 0.0329 0.0317 0.0291 0.0273 0.0252 0.0248
              0.0253 0.0261 0.027 0.0283 0.0299 0.0314 0.0328 0.0343
              0.05 0.05;
EVKP1  = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK   = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS  = CHAI 'R' IPAHS;
LTRHOP1= PROG 0 20 100 200 300 400
              500 600 700 800 900 1440
              2500 ;
LRHOP1 = PROG 7852.D-9 7852.D-9 7815.D-9 7778.D-9 7741.D-9 7705.D-9
              7668.D-9 7633.D-9 7595.D-9 7562.D-9 7527.D-9 7345.D-9
              7011.D-9 ;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS  = CHAI 'C' IPAHS;
LTCP1  = PROG 21 50 150 200 250 300 350 450 550
              650 700 750 850 1000 1200 1300 1400 1440
              1465 1490 1540 1590 1615 1640 2500 ;
LCP1   = PROG 460.5 485.6 519.1 531.6 556.7 573.5 598.6 661.4 749.3
              845.6 1431.6 950.2 900 845.6 800 815 840 850
              1210 1840 2470 1840 1210 840 707 ;
EVCP1  = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC         = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

*****************
* 2- Martensite *
*****************
IPAHS   = 2;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

**************
* 3- Bainite *
**************
IPAHS  = 3;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

**************
* 4- Ferrite *
**************
IPAHS  = 4;
CPHAS   = CHAI 'K' IPAHS;
EVKP1   = EVOL 'MANU' 'T' LTKP1 CPHAS LKP1;
MCHK    = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'R' IPAHS;
EVRHOP1 = EVOL 'MANU' 'T' LTRHOP1 CPHAS LRHOP1;
MCHRHO  = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP1 'TYPE' CTYP) ;

CPHAS   = CHAI 'C' IPAHS;
EVCP1   = EVOL 'MANU' 'T' LTCP1 CPHAS LCP1;
MCHC    = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP1 'TYPE' CTYP) ;

****************
* 5- Austénite *
****************
IPAHS  = 5;
CPHAS  = CHAI 'K' IPAHS;
LTKP5  = PROG 0    20 100 200 400 600 800 856
              900 1000 1200 1500 1502 1600   ;
LKP5   = PROG 0.0121 0.0124 0.0136 0.015 0.018 0.021 0.0239 0.0248
              0.0254 0.0269 0.0298 0.0343 0.05 0.05;
EVKP5  = EVOL 'MANU' 'T' LTKP5 CPHAS LKP5;
MCHK         = MCHK   ET (MANU 'CHML' MODTHE CPHAS EVKP5 'TYPE' CTYP) ;

CPHAS  = CHAI 'R' IPAHS;
LTRHOP5= PROG 0 20 100 200 300 400
              500 600 700 800 900 1440
              2500 ;
LRHOP5 = PROG 7852.D-9 7841.D-9 7794.D-9 7736.D-9 7680.D-9 7624.D-9
              7569.D-9 7514.D-9 7458.D-9 7408.D-9 7356.D-9 7087.D-9
              6613.D-9 ;
EVRHOP5 = EVOL 'MANU' 'T' LTRHOP5 CPHAS LRHOP5;
MCHRHO       = MCHRHO ET (MANU 'CHML' MODTHE CPHAS EVRHOP5 'TYPE' CTYP);


CPHAS  = CHAI 'C' IPAHS;
LTCP5  = PROG 21   50   150  200  250  300  350  475  575  675  800
              950  1000 1100 1200 1300 1400 1440 1465 1490 1540 1590
              1615 1640 2500 ;
LCP5   = PROG 460.5 485.6 519.1 531.6 556.7 573.5 598.6 652 671 685 715
              742 750 774 795 815 840 850 1210 1840 2470 1840
              1210 840 707 ;
EVCP5  = EVOL 'MANU' 'T' LTCP5 CPHAS LCP5;
MCHC         = MCHC   ET (MANU 'CHML' MODTHE CPHAS EVCP5 'TYPE' CTYP) ;


TPHASE = TABL;
* Liste des transformations existantes dans un LISTENTIE
TPHASE . 'PHASES' = MOTS 'MB  ' 'MART' 'BAIN' 'FERR' 'AUST' ;
TPHASE . 'REACTIFS' = MOTS 'MB  ' 'MART' 'BAIN' 'FERR' 'AUST' 'AUST' 'AUST' ;
TPHASE . 'PRODUITS' = MOTS 'AUST' 'AUST' 'AUST' 'AUST' 'MART' 'BAIN' 'FERR' ;
TPHASE . 'TYPE'     = MOTS 'LEBL' 'LEBL' 'LEBL' 'LEBL' 'KOIS' 'LEBL' 'LEBL' ;

MODCP1 = MODE MAILT 'METALLURGIE' 'PHASES' (TPHASE . 'PHASES')
                                  'REACTIFS' (TPHASE . 'REACTIFS')
                                  'PRODUITS' (TPHASE . 'PRODUITS')
                                  'TYPE' (TPHASE . 'TYPE')
                                  'QUA4' ;

PROG_T = 'PROG' 716. 802. ;
PROG_TPO = 'PROG' -1.D-6 0. ;
PROG_PEQ = 'PROG' 0. 1. ;
PROG_F = 'PROG' 0. 1. ;
PROG_TAU = 'PROG' 12. 0.5 ;

*1ere transformation :
  PEQ1 = EVOL 'MANU' 'T'   PROG_T 'PEQ1' PROG_PEQ ;
  F1   = EVOL 'MANU' 'TPOI'PROG_TPO 'F1' PROG_F ;
  TAU1 = EVOL 'MANU' 'T'   PROG_T 'TAU1' PROG_TAU ;
*2eme transformation : parametres identiques a la premiere
  PEQ2 = EVOL 'MANU' 'T'   PROG_T 'PEQ2' PROG_PEQ ;
  F2   = EVOL 'MANU' 'TPOI'PROG_TPO 'F2' PROG_F ;
  TAU2 = EVOL 'MANU' 'T'   PROG_T 'TAU2' PROG_TAU ;
*3eme transformation : parametres identiques a la premiere
  PEQ3 = EVOL 'MANU' 'T'   PROG_T 'PEQ3' PROG_PEQ ;
  F3   = EVOL 'MANU' 'TPOI'PROG_TPO 'F3' PROG_F ;
  TAU3 = EVOL 'MANU' 'T'   PROG_T 'TAU3' PROG_TAU ;
*4eme transformation : parametres identiques a la premiere
  PEQ4 = EVOL 'MANU' 'T'   PROG_T 'PEQ4' PROG_PEQ ;
  F4   = EVOL 'MANU' 'TPOI'PROG_TPO 'F4' PROG_F ;
  TAU4 = EVOL 'MANU' 'T'   PROG_T 'TAU4' PROG_TAU ;
*5eme transformation
  MS5 = 380. ;
  KM5 = 0.0247 ;

*6eme transformation
  PEQ6 = EVOL 'MANU' 'T'   (PROG 375 380 405 600)
                     'PEQ6' (PROG 0.  1.  1.  0.  );

  F6  = 'EVOL' 'MANU' 'TPOI' (PROG -100. -80. -60. -50. -40. -30. -25. -20. -18. -15. -12. -10. -9. -5. -1. -0.05 0.D0 1.D-6)
                      'F6' (PROG 0.005 1.573 2.857 3.417 3.982 4.583 4.833 5.26 5.472 6.033 7.675 11.4 18.45 17.1 0.328 0.00238 0.00238 0.) ;

  TAU6 = EVOL 'MANU' 'T'   (PROG 375  380)
                     'TAU6' (PROG 1.D6 20 );

*7eme transformation
  PEQ7 = 'EVOL' 'MANU' 'T' (PROG 625 630 730 735 )
                       'PEQ7' (PROG 0   1   1   0   );

  F7  = 'EVOL' 'MANU' 'TPOI' (PROG -8.5  -6.2 -4.7  0.  1.D-6)
                      'F7'    (PROG  0.001 0.13 1.5  1.5 0.   );

  TAU7 = EVOL 'MANU' 'T'   (PROG 625  630 )
                     'TAU7' (PROG 1.D6 5   );

  MATCP1 = 'MATE' MODCP1 'PEQ1' PEQ1 'TAU1' TAU1 'F1' F1
                         'PEQ2' PEQ2 'TAU2' TAU2 'F2' F2
                         'PEQ3' PEQ3 'TAU3' TAU3 'F3' F3
                         'PEQ4' PEQ4 'TAU4' TAU4 'F4' F4
                         'MS5' MS5 'KM5' KM5
                         'PEQ6' PEQ6 'TAU6' TAU6 'F6' F6
                         'PEQ7' PEQ7 'TAU7' TAU7 'F7' F7;

************************************************************************
* Definition de la metallurgie : FIN
************************************************************************

MODTOT = MODTHE 'ET' MODCP1 'ET' MODCON ;

************************************************************************
* Phase 1 et 2 :
*   Rampe de temperature jusqu'à 900°C d'une durée de 20min
*   Palier isotherme à           900°C d'une durée de 30min
************************************************************************

*TEMPERATURE INITIALE
CHPTINI = MANU 'CHPO' MAILT 1 'T' TMIN 'NATU' 'DIFFUS' ;

CTYP   ='CHAI'  'VARIABLES_INTERNES';
MCHPHA  = MANU 'CHML' MODCP1 'MB  ' 0.1 'MART' 0.2 'BAIN' 0.3
                             'FERR' 0.4 'AUST' 0.0
               'TYPE' CTYP 'NOEUD' ;

*MATERIAUX
MATPHAS = MCHK 'ET' MCHRHO 'ET' MCHC  ;
MATCONV ='MATE' MODCON 'H' 0.D0 ;
MATCOND ='MATE' MODTHE 'K' 0.D0 'RHO' 0.D0 'C' 0.D0;
MATTOT  = MATCOND 'ET' MATCONV 'ET' MATCP1 ;


************************************************************************
* Fabrication du diagramme TRC (Pour vérification de la mise en donnee)
************************************************************************
* Calcul automatique de la liste des vitesses de refroidissement
  VIT1  = 250.      ;
  VIT2  = 1.        ;
  NBVI  = 50        ;
  VLOG1 = 'LOG' VIT1;
  VLOG2 = 'LOG' VIT2;

  PASLOG = (VLOG2 - VLOG1) / (NBVI - 1);
  LRELOG ='PROG' VLOG1 'PAS' PASLOG VLOG2;
  LISTVIT='EXP' LRELOG;

* Liste manuelle des vitesses de refroidissement
  LISTVIT = PROG 250. 50. 10. 7. 4. ;
************************************************************************

*CLs
*TEMPERATURE IMPOSE EN BAS
CLTOT   = BLOQ 'T' MAILT ;
TIMPO   = DEPI 1.  CLTOT ;

* CHARGEMENT Thermque 'T'
LTPSCHA = PROG 0.   1200. 3000. ;
LTCHA   = PROG TMIN TMAX  TMAX  ;
EVT     = EVOL 'MANU' LTPSCHA LTCHA;
CHAR1 = CHAR 'TIMP' TIMPO EVT ;

* CHARGEMENT 'TPOI'. Ce chargement est mis a jour dans PARATHER
TPS0   = 0.D0 ;
TPS1 = 3000.D0 ;
VREFROI= 10 ;
VREFROI= ( TMAX - TMIN ) / (1200. - 0.) ;

VITESSE = -1 * VREFROI ;
LTPSCHA ='PROG'  0.       1200.     1201.  3000. ;
LTPOI   ='PROG'  VITESSE  VITESSE   0.     0.    ;
EVT     ='EVOL' 'MANU' LTPSCHA LTPOI ;
TPOIN   = 'MANU' 'CHPO' MAILT 1 'TPOI' 1.D0 ; 'COMM' 'Le chargement vaut 1 * chargement(temps) partout !';
CHAR2   ='CHAR' 'TPOI' TPOIN EVT ;

*CHARGEMENT Total
CHARTOT = CHAR1 'ET' CHAR2 ;

*TEMPS_CALCULES
LTCAL1   = PROG 0. 'PAS' 100. 945. 'PAS' 2. 1100.
                   'PAS' 5. 1200.  'PAS' 50. 3000.  ;

*PASAPAS
TPASAP                          ='TABL'         ;
TPASAP.'MODELE'                 = MODTOT        ;
TPASAP.'CARACTERISTIQUES'       = MATTOT        ;
TPASAP.'BLOCAGES_THERMIQUES'    = CLTOT         ;
TPASAP.'CHARGEMENT'             = CHARTOT       ;
TPASAP.'TEMPS_CALCULES'         = LTCAL1        ;

TPASAP.'TEMPS'                  ='TABL'         ;
TPASAP.'TEMPERATURES'           ='TABL'         ;
TPASAP.'PROPORTIONS_PHASES'     ='TABL'         ;
TPASAP.'TEMPS'              . 0 ='EXTR' LTCAL1 1;
TPASAP.'TEMPERATURES'       . 0 = CHPTINI       ;
TPASAP.'PROPORTIONS_PHASES' . 0 = MCHPHA        ;
TPASAP.'PROCESSEURS'            ='MOT' 'MONO_PROCESSEUR';

* PROCEDURES Utilisateurs
TPASAP.'PROCEDURE_PARATHER'    = VRAI          ;

PASAPAS TPASAP ;

************************************************************************
* Phase 3 :
*   Refroidissement par convection sur un des bords
************************************************************************
* MATERIAU
MATCONV  ='MATE' MODCON  'H' HCON ;
MATTOT   = MATCOND 'ET' MATCONV    ;

MATTOT  = MATCOND 'ET' MATCONV 'ET' MATCP1 ;

* CHARGEMENT Thermque 'T'
LTPSCHA = PROG 0.D0 1.D6 ;
LTCHA   = PROG 1.D0 1.D0 ;
EVTECO  = EVOL 'MANU' LTPSCHA LTCHA;
CHPTECO ='MANU' 'CHPO' SURFBAS 1 'T' 20. ;
CHAR1 = CHAR 'TECO' CHPTECO EVTECO ;

* CHARGEMENT 'TPOI'. Ce chargement est mis a jour dans PARATHER
VITESSE = -1 * VREFROI ;
LTPSCHA = 'PROG' 0.D0    1.D6    ;
LTPOI   ='PROG'  0.      0.      ;
EVT     ='EVOL' 'MANU' LTPSCHA LTPOI ;
TPOIN   = 'MANU' 'CHPO' MAILT 1 'TPOI' 1.D0 ;
CHAR2   ='CHAR' 'TPOI' TPOIN EVT ;

*CHARGEMENT Total
CHARTOT = CHAR1 'ET' CHAR2 ;

*TEMPS_CALCULES
MTPS1    ='MAXI' LTCAL1 ;
MTPS2    = MTPS1 + 600. ;
LTCAL2   ='PROG' 3000. 'PAS' 1.  3040. 'PAS' 2 3160.   'PAS' 4.  3370.
                       'PAS' 20. MTPS2 ;

* Remplacement des indices dans la TABLE TPASAP
TPASAP.'BLOCAGES_THERMIQUES'='VIDE' 'RIGIDITE';
TTPS  = TPASAP.'TEMPS';
ID_IND=('DIME' TTPS) - 1;
MCHPHA=TPASAP.'PROPORTIONS_PHASES' . ID_IND ;
ID2 = ID_IND + 1 ;

TPASAP.'CARACTERISTIQUES'         = MATTOT ;
TPASAP.'CHARGEMENT'               = CHARTOT;
TPASAP.'TEMPS_CALCULES'           = LTCAL2 ;
TPASAP.'PROPORTIONS_PHASES' . ID2 = MCHPHA ;

PASAPAS TPASAP ;

************************************************************************
* POST-TRAITEMENT des résultats
************************************************************************
fic_sauv = 'CHAI' 'JOMINY.sauv' ;
OPTI SAUV fic_sauv ;
SAUV ;

* Initialisations pour le POST-TRAITEMENT
LISCOM ='EXTR' MCHPHA 'COMP' ;
NBCOMP ='DIME' LISCOM        ;
TABPHA ='TABL';
REPE SURPHA NBCOMP;
  IPH = &SURPHA;
  TABPHA . IPH ='VIDE' 'LISTREEL' ;
FIN SURPHA;
LTPS ='VIDE' 'LISTREEL';

REPE BLOC0 ('DIME' TPASAP.'TEMPS')         ;
  I      = &BLOC0  - 1                     ;
  CHT    = TPASAP.'TEMPERATURES'      . I  ;
  CHPHA  = TPASAP.'PROPORTIONS_PHASES'. I  ;
  LTPS   = LTPS 'ET' (TPASAP.'TEMPS'  . I) ;

* Extraction pour chaque phase de la valeur courante
  REPE SURPHA NBCOMP;
    IPH = &SURPHA;
    NOMP='EXTR' LISCOM IPH;
*    Pi  ='EXTR' CHPHA NOMP 1 1288 1 ;
*    Pi  ='EXTR' CHPHA NOMP 1 378  1 ;
    Pi  ='EXTR' CHPHA NOMP 1 1   1 ;
    TABPHA . IPH = TABPHA . IPH 'ET' Pi;
  FIN SURPHA;
FIN BLOC0 ;

*Reconstitution des EVOL
EVTOTa           ='VIDE' 'EVOLUTIO';
TDESS1           ='TABL' ;
TDESS1 . 'TITRE' ='TABL' ;
EVOPHAS          ='VIDE' 'EVOLUTIO';
CPPHAS = 'CHAN' 'CHPO' CHPHA MODTOT;

REPE SURPHA NBCOMP;
  IPH          = &SURPHA;
  NOMP         ='EXTR' LISCOM IPH;
  ICOUL        = @MOD  IPH NBCOUL;
  COUL1        ='EXTR' LESCOUL ICOUL;
  Lpi          = TABPHA. IPH ;
  TABPHA . IPH ='EVOL' COUL1 'MANU' 'TEMP' LTPS NOMP Lpi;
  EVTOTa       = EVTOTa 'ET' TABPHA . IPH ;
  IMARQ        = @MOD   IPH NBMARQ;
  LAMAR        ='EXTR'  IMARQ LESMARQ;
  TDESS1 . 'TITRE' . IPH = NOMP;
*  TDESS1 . IPH ='CHAI' 'MARQ ' LAMAR ' REGU';
  EVOPHAS      = EVOPHAS 'ET' ('EVOL' COUL1 'CHPO' CPPHAS NOMP L2);
FIN SURPHA;

EVTOTa = 'CHAN' 'NOMORD' EVTOTa 'PROP';
'DESS' EVTOTa  'LEGE' TDESS1 'TITR' ('CHAI' 'Evolution des Props en fonction du temps' ) 'YBOR' 0. 1. ;

EVOPHAS = 'CHAN' 'NOMORD' EVOPHAS 'PROP';
'DESS' EVOPHAS 'LEGE' TDESS1 'TITR' ('CHAI' 'Props a Temps final' ) 'YBOR' 0. 1. ;

FIN ;






