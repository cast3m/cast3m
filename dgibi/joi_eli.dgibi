* fichier :  joi_eli.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
   GRAPH = 'N' ;
   SAUT PAGE ;
********************************************************************
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  TEST AVEC UN JOINT (2D) POUR LE MODELE D ANCRAGE           ';
MESS '                    D ELIGEHAUSEN                           ';
MESS '                                                             ';
MESS '  LA SURFACE DE HAUT DU JOINT EST SOUMISE A UN DEPL. IMPOSE  ';
MESS '   SELON LA DIRECTION TANGENTIELLE                           ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                   A _______________ H                       ';
MESS '                    |               |                        ';
MESS '                    |_______________|                        ';
MESS '                   G                 D                       ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  JOINT FORME DES NOEUDS DG-HA                               ';
MESS '                                                             ';
MESS '  CONDITIONS AUX LIMITES  :                                  ';
MESS '       - DG  BLOQUEE EN UX ET EN UY                          ';
MESS '                                                             ';
MESS '  CHARGEMENT              :                                  ';
MESS '       - DEPLACEMENT IMPOSE ET FORCE SUR HA                  ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  DANS CET ESSAI ON COMPARE LA CONTRAINTE TANGENTIELLE ET    ';
MESS '   LE DEPLACEMENT NORMAL AUX QUANTITES CORRESPONDANTES       ';
MESS '   CALCULEES ANALYTIQUEMENT.                                 ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
********************************************************************
*
*
   OPTION ECHO 1 DIME 2 ;
   OPTION ELEM SEG2 MODE PLAN CONT ;
   TEMPS ;
*
*------------------------------------------------
* --------- DEFINITION DE LA GEOMETRIE ----------
*------------------------------------------------
*
   A = 0.00  0.00 ;
   H = 1.00  0.00 ;
*
   G = 0.00 0.00 ;
   D = 1.00 0.00 ;
*
   HA =  H DROIT 1 A ;
*
   DG =  D DROIT 1 G ;
*
*-----------------------------------------------------
* -------------- DEFINITION DU MAILLAGE --------------
*-----------------------------------------------------
*
   OPTION ELEM RAC2 ;
*
   JOINT = RACC 0.001 (INVE HA ) ( INVE DG )  ;
*
*------------------------------------------------------------------
* -------------- DEFINITION DES CONDITIONS AUX LIMITES ------------
*------------------------------------------------------------------
*
   CL1 = BLOQ DG UX UY ;
*
   CL5 = BLOQ HA UX ;
*
   CLTOT = CL1 ET CL5 ;
*
*-----------------------------------------------------------------
* -------------- DEFINITION DU MODELE E DU MATERIAU --------------
*-----------------------------------------------------------------
*
   MODJOI = MODE JOINT 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE'
                       'ANCRAGE_ELIGEHAUSEN' JOI2 ;
*
   phi0 = 0.010;
*
   MAJOI  = MATE MODJOI  'KS  ' ((30000./(2.*(1+0.25)*0.05))*pi*phi0)
                         'KN  ' ((30000./0.05)*pi*phi0)
                         'S1T' 0.001 'S2T' 0.003 'S3T' 0.010
                         'T1T' 15. 'T3T' 5.
                         'ALFA' 0.4 'PERI' (pi*phi0);

*--------------------------------------------------------------
* ------------------- DEFINITION DU CHARGEMENT ----------------
*--------------------------------------------------------------
*
   DEP6 = FORCE FY -10.0 HA ;
   DEP5 = DEPI CL5 (-20.0/1000.) ;
*
   L56 = PROG 0.0  1.0  1.0 ;
   L55 = PROG 0.0  0.0  1.0 ;
   L50 = PROG 0.0  1.0  2.0 ;
*
   EV5 = EVOL MANU X L50 Y L55 ;
   EV6 = EVOL MANU X L50 Y L56 ;
*
   CAR5 = CHAR 'DIMP' DEP5 EV5 ;
   CAR6 = CHAR 'MECA' DEP6 EV6 ;
   CART = CAR5 ET CAR6 ;
*
*-------------------------------------------------------
* --------------------- RESOLUTION ---------------------
*-------------------------------------------------------
*
   TAB = TABLE ;
   LIS1 = PROG 0.0 1.0 PAS 0.05 2.0 ;
   TAB.'BLOCAGES_MECANIQUES' = CLTOT;
   TAB.'MODELE' = MODJOI;
   TAB.'CHARGEMENT' = CART;
   TAB.'TEMPS_CALCULES' = LIS1;
   TAB.'CARACTERISTIQUES' = MAJOI;
   TAB.'MOVA'             = RIEN;
*
TMASAU=table;
tab. 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
   PASAPAS TAB ;
*
*--------------------------------------
* ---------- POST TRAITEMENT ----------
*--------------------------------------
*
sig1 = prog ; sig2 = prog ; var1 = prog ; var2 = prog ;
var3 = prog ; var4 = prog ; var5 = prog ; var6 = prog ;
dep1 = prog ; dep2 = prog ; tem0 = prog ; tem1 = prog ;
*
dtab1=index(tab.deplacements) ;
*
ndime=dime dtab1 ;
*
*
i=0 ;
*
REPETER BOU1 ndime ;
*
i=i+1 ;
*
d=dtab1.i ;
*
dep0 = tab.deplacements.d ;
sig0 = tab.contraintes.d ;
var0 = tab.variables_internes.d ;
def0 = tab.deformations_inelastiques.d ;
*
DEPJOI = REDU DEP0 JOINT ;
SIGJOI = REDU SIG0 JOINT ;
VARJOI = REDU VAR0 JOINT ;
DEFJOI = REDU DEF0 JOINT ;
*
* Contrainte de cisaillement
tau1 = extr SIGJOI 'SMSN' 1 1 1 ;
* Contrainte normale
tau2 = extr SIGJOI 'SMN' 1 1 1 ;
*
* Glissement plastique atteint pour un glissement positif
sor1 = extr VARJOI 'EPPP' 1 1 1 ;
* Glissement plastique atteint pour un glissement négatif
sor2 = extr VARJOI 'EPPM' 1 1 1 ;
* Glissement total
sor3 = extr VARJOI 'EPSO' 1 1 1 ;
* Rigidité tangente en glissement
sor4 = extr VARJOI 'KSTA' 1 1 1 ;
* Rigidité tangente en traction/compression
sor5 = extr VARJOI 'KNEL' 1 1 1 ;
*
* Déformations plastiques
pla1 = extr DEFJOI 'FRSN' 1 1 1 ;
pla2 = extr DEFJOI 'FRN' 1 1 1 ;
*
sig1 = inse sig1 i tau1 ;
sig2 = inse sig2 i tau2 ;
*
var1 = inse var1 i sor1 ;
var2 = inse var2 i sor2 ;
var3 = inse var3 i sor3 ;
var4 = inse var4 i sor4 ;
var5 = inse var5 i sor5 ;
*
tem0 = inse tem0 i d  ;
*
dep1 = inse dep1 i pla1 ;
dep2 = inse dep2 i pla2 ;
*
fin bou1 ;
*
   si ( NEG graph 'N' ) ;
*
      gra1 = evol manu U var3 T sig1 ;
      gra2 = evol manu t0 tem0 EPPP var1 ;
      gra3 = evol manu t0 tem0 EPPM var2 ;
      gra4 = evol manu t0 tem0 EPSO var3 ;
      gra5 = evol manu t0 tem0 KSTA var4 ;
      gra6 = evol manu t0 tem0 KNEL var5 ;
*
      dess gra1 ;
      dess gra2 ;
      dess gra3 ;
      dess gra4 ;
      dess gra5 ;
      dess gra6 ;
*
   finsi ;
*
*-------------------------------------------------------------
*------------------ CODE DE FONCTIONNEMENT -------------------
*-------------------------------------------------------------
*
*
* listreel de la contrainte tangentielle de repère en fonction du temps *
*
pro1 = prog
       0.0  0.0  -.47124 -.47124  -.47124  -.42636  -.38148
     -.33660  -.29172  -.24684  -.20196 -.15708  -.15708  -.15708  -.15708
     -.15708  -.15708  -.15708  -.15708  -.15708  -.15708  -.15708;
*
*
***** listreel du glissement total en fonction du temps *****
*
pro2 = prog  0.0  0.0 -0.001  -0.002 -0.003 -0.004 -0.005 -0.006 -0.007 -0.008
     -0.009 -0.01 -0.011 -0.012 -0.013 -0.014 -0.015 -0.016 -0.017 -0.018
     -0.019 -0.020;
*
j1 = 0 ;
j2 = 0 ;
ii0 = 0 ;
num1 = 0. ;
num2 = 0. ;
den1 = 0. ;
den2 = 0. ;
*
repeter bou2 20 ;
*
j1 = j1 + 1 ;
j2 = j2 + 1 ;
ii0 = ii0 + 1 ;
*
ttt1 = extr sig1 j1 ;
vvv1 = extr var3 j2 ;
ttt0 = extr pro1 ii0 ;
vvv0 = extr pro2 ii0 ;
*
num1 = num1 + ((ttt1 - ttt0) * (ttt1 - ttt0)) ;
num2 = num2 + ((vvv1 - vvv0) * (vvv1 - vvv0)) ;
den1 = den1 + (ttt0 * ttt0) ;
den2 = den2 + (vvv0 * vvv0) ;
*
fin bou2 ;
*
err0 = (num1/den1) * 100. ;
err1 = (num2/den2) * 100. ;
*
si ((err0 < 5.) et (err1 < 5.)) ;
*
ERRE 0 ;
*
sinon ;
*
ERRE 5 ;
*
finsi ;
*
fin;














