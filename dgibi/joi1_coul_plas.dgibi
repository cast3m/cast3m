* fichier : joi1_coul_plas.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*
f = mot fin; l = mot list; tr = mot trac;
'OPTI' 'ECHO' 1 ;

'OPTI' 'ELEM' 'SEG2' 'DIME' 3 'MODE' 'TRID' ;
'OPTI' 'EPSILON' 'LINEAIRE'                 ;

GRAPH = FAUX ;

P1 = 0 0 0   ;
P2 = 0 0 0   ;

jo = P1 P2 'DROIT' 1  ;

************************************************************************
*                          Début: MODELE                               *
************************************************************************
mo = MODE jo  'MECANIQUE' 'ELASTIQUE'     'ORTHOTROPE'
                          'PLASTIQUE'     'COULOMB' 'JOI1'        ;

************************************************************************
*                           Fin: MODELE                                *
************************************************************************


************************************************************************
*                          Début: MATERIAU                             *
************************************************************************
kn  = 100. ;
ks1 =  50. ;
ks2 = ks1  ;

qn  = 40.  ;
qs1 = 20.  ;
qs2 = 30.  ;
*
phi  = 45. ;

kt  = kn/10. ;
fne = 15.    ;
*fne = 150.    ;


ma = 'MATE' mo 'DIRECTION' (1 0 0) (0 1 0)
                                    'KN  ' kn   'KS1 ' ks1  'KS2 ' ks2
                                    'QN  ' qn   'QS1 ' qs1  'QS2 ' qs2
                                    'TYPE' 1.   'FRIC' phi
                                    'QT  ' kt   'FNE ' fne  'COHE' 0.  ;

************************************************************************
*                          Fin: MATERIAU                               *
************************************************************************

************************************************************************
*                          Début: CL & CHARGEMENT                      *
************************************************************************
cl1 = 'BLOQ' 'DEPL' 'ROTA'  P1     ;
cl2 = 'BLOQ' 'UX'           P2     ;
cl3 = 'BLOQ' 'UY'           P2     ;
cl  = cl1 'ET' cl2 'ET' cl3        ;

vdpi = 0.1                                                            ;
dpi  = 'DEPI' cl2   vdpi                                              ;
lt   = 'PROG' 0     2    4    6    7                                  ;
lc   = 'PROG' 0    -2   -2   -2   -1                                  ;
ev   = 'EVOL' 'MANU' 't' lt 'coef'  lc                                ;

cha1 = 'CHAR' 'DIMP' dpi ev ;


vdpi2 =  0.1                                                          ;
dpi2  = 'DEPI' cl3   vdpi2                                            ;
lt2   = 'PROG' 0     2     4    6    7                                ;
lc2   = 'PROG' 0     0    3.1   5    5                                ;
ev2   = 'EVOL' 'MANU' 't' lt2 'coef'  lc2                             ;

cha2 = 'CHAR' 'DIMP' dpi2 ev2 ;


cha  = cha1 et cha2 ;
*opti donn 5;
************************************************************************
*                          Fin: CL & CHARGEMENT                        *
************************************************************************
dt1 = 0.10                    ;
dt2 = 0.05                    ;
lt  = 'PROG' 0.1 pas dt1 7  ;


tab                       = 'TABLE' ;
tab.'MODELE'              = mo      ;
tab.'CARACTERISTIQUES'    = ma      ;
tab.'BLOCAGES_MECANIQUES' = cl      ;
tab.'CHARGEMENT'          = cha     ;
tab.'TEMPS_CALCULES'      = lt      ;
tab.'FTOL'                = 1E-3    ;

PASAPAS tab ;


time=tab.'TEMPS';
ntime = 'DIME' time ;
n=ntime - 1 ;

progt    = 'PROG' ;
prN      = 'PROG' ;
prFY     = 'PROG' ;
prepse   = 'PROG' ;
prouv    = 'PROG' ;
prlam1   = 'PROG' ;
prlam3   = 'PROG' ;
prpla1   = 'PROG' ;
prpla3   = 'PROG' ;
prepco   = 'PROG' ;
pretat   = 'PROG' ;
ouv      = 0.     ;
************************************************************************
*                         RESULTATS CALCULES                           *
************************************************************************
REPETER contro ntime                                                   ;
   i      = &contro - 1                                                ;
   t      = time.i                                                     ;
   progt  = progt 'ET' ('PROG' t)                                      ;
*-
   sig    = tab.'CONTRAINTES'.i                                        ;
   prN    = prN  'ET' ('PROG' ('EXTR' sig 'EFFX' 1 1 1))               ;
   prFY   = prFY 'ET' ('PROG' ('EXTR' sig 'EFFY' 1 1 1))               ;
*-
   vi    = tab.'VARIABLES_INTERNES'.i                                  ;
  prepse = prepse 'ET' ('PROG' ('EXTR' vi 'EPSE' 1 1 1))               ;
  ouv    = ouv + ('EXTR' vi 'EPOU' 1 1 1)                              ;
  prouv  = prouv  'ET' ('PROG' ouv)                                    ;
  prlam1 = prlam1 'ET' ('PROG' ('EXTR' vi 'LAM1' 1 1 1))               ;
  prlam3 = prlam3 'ET' ('PROG' ('EXTR' vi 'LAM3' 1 1 1))               ;
  prpla1 = prpla1 'ET' ('PROG' ('EXTR' vi 'PLA1' 1 1 1))               ;
  prpla3 = prpla3 'ET' ('PROG' ('EXTR' vi 'PLA3' 1 1 1))               ;
  prepco = prepco 'ET' ('PROG' ('EXTR' vi 'EPCO' 1 1 1))               ;
  pretat = pretat 'ET' ('PROG' ('EXTR' vi 'STAT' 1 1 1))               ;
FIN contro                                                             ;
*-----------------------------------------------------------------------
evN    = evol manu 't' progt 'N'    prN      ;
evepco = evol manu 't' progt 'EPCO' prepco   ;
evlam3 = evol manu 't' progt 'LAM3' prlam3   ;
evouv  = evol manu 't' progt 'EPOU' prouv    ;
evlam1 = evol manu 't' progt 'LAM1' prlam1   ;
evpla3 = evol manu 't' progt 'PLA3' prpla3   ;
evepse = evol manu 't' progt 'EPSE' prepse   ;
evFY   = evol manu 't' progt 'FY'   prFY     ;
evpla1 = evol manu 't' progt 'PLA1' prpla1   ;
evetat = evol manu 't' progt 'STAT' pretat   ;

************************************************************************
*                       RESULTATS THEORIQUES                           *
************************************************************************
tg=tan phi ;

*- Elasticité en compression (0 à t_0)
* ------------------------------------
t_0     = 1.5       ;
epse_0  = -1*fne/kn ;
eps_0   = epse_0    ;
N_0     = -1*fne    ;
epco_0  = 0.        ;
lam3_0  = 0.        ;
ouv_0   = 0.        ;
lam1_0  = 0.        ;
pla3_0  = 0.        ;
p_0     = 0.        ;
Fy_0    = 0.        ;
pla1_0  = 0.        ;
etat_0  = 2         ;

*- Plasticité en compression (t_0 à t_1)
*  -------------------------------------
t_1     = 2.                                ;
eps_1   = -0.2                              ;
N_1     = (kt*eps_1) + (fne*((kt/kn) - 1.)) ;
epse_1  = N_1/kn                            ;
epco_1  = eps_1 - epse_1                    ;
lam3_1  = abs epco_1                        ;
ouv_1   = 0.                                ;
lam1_1  = 0.                                ;
pla3_1  = epco_1                            ;
p_1     = abs pla3_1                        ;
Fy_1    = 0.                                ;
pla1_1  = 0.                                ;
etat_1  = 3                                 ;

*- Cisaillement élastique à effort de compression constant (t_1 à t_2)
*  -------------------------------------------------------------------
t_2     = 4.       ;
eps_2   = -0.2     ;
N_2     = N_1      ;
epse_2  = N_2/kn   ;
epco_2  = epco_1   ;
lam3_2  = lam3_1   ;
ouv_2   = 0.       ;
lam1_2  = 0.       ;
pla3_2  = pla3_1   ;
p_2     = p_1      ;
gy_2    = 0.31     ;
Fy_2    = ks1*gy_2 ;
pla1_2  = 0.       ;
etat_2  = 3        ;

*- Cisaillement Coulomb à effort de compression constant (t_2 à t_3)
*  -----------------------------------------------------------------
t_3     = 6.                          ;
eps_3   = -0.2                        ;
N_3     = N_2                         ;
epse_3  = N_3/kn                      ;
epco_3  = epco_2                      ;
lam3_3  = lam3_2                      ;
ouv_3   = 0.                          ;
lam1_3  = 0.                          ;
pla3_3  = eps_3 - epse_3              ;
gy_3    = 0.5                         ;
Fy_3    = -1*tg*N_3                   ;
gye_3    = Fy_3/ks1                   ;
pla1_3  = gy_3 - gye_3                ;
p_3     = p_2 + (abs (pla1_3-pla1_2)) ;
etat_3  = 3                           ;

*- Décharge élast. de l'effort normal et cisail. Coulomb (t_3 à t_4)
*  -----------------------------------------------------------------
t_4     = 7.                          ;
eps_4   = -0.1                        ;
N_4     = N_1 + (kn*(eps_4-eps_1))    ;
epse_4  = N_4/kn                      ;
epco_4  = epco_3                      ;
lam3_4  = lam3_3                      ;
ouv_4   = 0.                          ;
lam1_4  = 0.                          ;
pla3_4  = eps_4 - epse_4              ;
gy_4    = gy_3                        ;
Fy_4    = -1*tg*N_4                   ;
gye_4   = Fy_4/ks1                    ;
pla1_4  = gy_4 - gye_4                ;
p_4     = p_3 + (abs (pla1_4-pla1_3)) ;
etat_4  = 2                           ;


pt = prog 0  t_0  t_1  t_2  t_3  t_4 ;

ptN    = prog 0.  N_0    N_1    N_2    N_3    N_4  ;
ptepco = prog
 0. epco_0  epco_1  epco_2  epco_3   epco_4        ;
ptlam3 = prog
 0. lam3_0  lam3_1  lam3_2  lam3_3   lam3_4        ;
ptouv  = prog
 0. ouv_0   ouv_1   ouv_2   ouv_3    ouv_4         ;
ptlam1 = prog
 0. lam1_0  lam1_1  lam1_2  lam1_3   lam1_4        ;
ptpla3 = prog
 0. pla3_0  pla3_1  pla3_2  pla3_3   pla3_4        ;
ptepse = prog
 0. p_0     p_1     p_2     p_3      p_4           ;
ptFy   = prog
 0. Fy_0    Fy_1    Fy_2    Fy_3     Fy_4          ;
ptpla1 = prog
 0. pla1_0  pla1_1  pla1_2  pla1_3   pla1_4        ;

tt = prog     0    dt1     (t_0-dt1)    t_0     (t_0+dt1)   (t_1-dt1) ;
ptetat = prog 0   etat_0    etat_0     etat_1     etat_1     etat_1   ;

tt     = tt     et (prog t_1    (t_1+dt1)) ;
ptetat = ptetat et (prog etat_1    etat_2) ;

tt     = tt     et (prog (t_2-dt2)   t_2       (t_2+dt2)) ;
ptetat = ptetat et (prog   etat_2   etat_2       etat_3)    ;

tt     = tt     et (prog  (t_3-dt1)    t_3    (t_3+dt1)    t_4) ;
ptetat = ptetat et (prog    etat_3    etat_3   etat_4    etat_4);



tN    = evol roug manu 't' pt 'N'    ptN        ;
tepco = evol roug manu 't' pt 'EPCO' ptepco     ;
tlam3 = evol roug manu 't' pt 'LAM3' ptlam3     ;
touv  = evol roug manu 't' pt 'EPOU' ptouv      ;
tlam1 = evol roug manu 't' pt 'LAM1' ptlam1     ;
tpla3 = evol roug manu 't' pt 'PLA3' ptpla3     ;
tepse = evol roug manu 't' pt 'EPSE' ptepse     ;
tetat = evol roug manu 't' tt 'STAT' ptetat     ;
tFy   = evol roug manu 't' pt 'FY'   ptFy       ;
tpla1 = evol roug manu 't' pt 'PLA1' ptpla1     ;



t             = 'TABLE'           ;
t . 1         = 'MOT' 'MARQ CROI' ;
t . 2         = 'MOT' 'MARQ LOSA' ;
t.'TITRE'     = 'TABLE'           ;
t.'TITRE' . 1 = 'Theorie'         ;
t.'TITRE' . 2 = 'Calcul'          ;


'SI' GRAPH ;
'DESS' (tN    et evN)    'TITR' 'Effort normal'                t 'LEGE';
'DESS' (tepco et evepco) 'TITR' 'Def. plas. de compression'    t 'LEGE';
'DESS' (tlam3 et evlam3) 'TITR' 'Mult. plas. / compression'    t 'LEGE';
'DESS' (touv  et evouv)  'TITR' 'Def. plas. en ouverture'      t 'LEGE';
'DESS' (tlam1 et evlam1) 'TITR' 'Mult. plas. / traction'       t 'LEGE';
'DESS' (tpla3 et evpla3) 'TITR' 'Def. plas. normale'           t 'LEGE';
'DESS' (tepse et evepse) 'TITR' 'Def. plas. cumulee'           t 'LEGE';
'DESS' (tFy 'ET' evFY)   'TITR' 'Effort de glissement'         t 'LEGE';
'DESS' (tpla1 et evpla1) 'TITR' 'Def. plas. de glissement'     t 'LEGE';
'DESS' (tetat et evetat) 'TITR' 'Etat du joint'                t 'LEGE';
'FINSI' ;


*
* ----------    CODE DE FONCTIONNEMENT       ----------
*
dif_N    = 'MAXI' ('ABS' ('EXTR'  (tN  - evN)       'ORDO')) ;
dif_Fy   = 'MAXI' ('ABS' ('EXTR'  (tFy - evFY)      'ORDO')) ;
dif_pla1 = 'MAXI' ('ABS' ('EXTR'  (tpla1 - evpla1)  'ORDO')) ;
dif_pla3 = 'MAXI' ('ABS' ('EXTR'  (tpla3 - evpla3)  'ORDO')) ;

test = 'MAXI' ('PROG' dif_N  dif_Fy  dif_pla1  dif_pla3) ;


'SI' (test '<EG' 1E-6 ) ;
  'ERRE'  0 ;
'SINO';
   'ERRE'  5 ;
'FINSI' ;
'FIN' ;









