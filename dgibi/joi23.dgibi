* fichier :  joi23.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 0 ;
SAUT PAGE ;
MESS'!==============================================================!';
MESS'!                                                              !';
MESS'!                      TEST JOI23                              !';
MESS'!                                                              !';
MESS'!         ESSAI DE CISAILLEMENT SUR UN JOINT 2D                !';
MESS'!                                                              !';
MESS'!  Un joint 2D JOI2 a son bord inferieur encastre.             !';
MESS'!  Son bord superieur est libre. Un effort de cisaillemnent    !';
MESS'!  est exercee sur son bord superieur.                         !';
MESS'!                                                              !';
MESS'!                                                              !';
MESS'!  Solution analytique :                                       !';
MESS'!                                                              !';
MESS'!     En ecrivant le principe des travaux virtuels, on montre  !';
MESS'!  que le deplacement delta en chaque noeud est egal, dans le  !';
MESS'!  cas d un essai de traction uniaxial, a :                    !';
MESS'!                       F                                      !';
MESS'!        delta = ----------------                              !';
MESS'!                   k * L * l                                  !';
MESS'!  ou                                                          !';
MESS'!        F = force totale exercee sur la structure             !';
MESS'!        k = raideur                                           !';
MESS'!        L = longueur de l element joint                       !';
MESS'!        l = largeur de l element joint                        !';
MESS'!                                                              !';
MESS'!  Application numerique :                                     !';
MESS'!                                                              !';
MESS'!        F = 100000.0                                          !';
MESS'!        k = 4.2E08                                            !';
MESS'!        L = 2.0                                               !';
MESS'!        l = 1.0                                               !';
MESS'!                                                              !';
MESS'!       Delta   =  100000 / (4.2E08 * 2.0 * 1.0)               !';
MESS'!               =  1.19047619E-4                               !';
MESS'!                                                              !';
MESS'!==============================================================!';
OPTION DIME 2 ;
OPTION ELEM SEG2 MODE PLAN CONT  ;
*
SOLANA = 1.190476E-4 ;
SOLNUL = 0.0 ;
*
* ---------- DEFINITION DE LA SURFACE TOP DU JOINT ----------
*
A1 = 0.00 0.00 ;
B1 = 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
H1 = A1 DROIT 1 B1 ;
*
L1 = H1 ;
*
* ---------- DEFINITION DE LA SURFACE BOT DU JOINT ----------
*
IA1 = 0.00 0.00 ;
IB1 = 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
IH1 = IA1 DROIT 1 IB1 ;
*
IL1 = IH1 ;
*
* ----------   CREATION DU JOINT JOI2   ----------
*
OPTION ELEM RAC2 ;
VOL = RACCORD 0.00001 L1 IL1 ;
*
* ---------- DEFINITION DES CONDITIONS LIMITES ----------
*
CL11 = BLOQ IA1 UX ;
CL12 = BLOQ IA1 UY ;
CL1 = CL11 ET CL12 ;
*
CL21 = BLOQ IB1 UX ;
CL22 = BLOQ IB1 UY ;
CL2 = CL21 ET CL22 ;
*
CL = CL1 ET CL2 ;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MOD1 = MODE VOL 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' JOI2 ;
MA1 = MATE MOD1 KS 4.2E08 KN 4.2E10 ;
*
* ----------              MATRICE DE HOOK               ----------
*
HOO1 = HOOKE MOD1 MA1 ;
******LIST HOO1 ;
*
* ----------       MATRICE DE RIGIDITE        ----------
*
******RI1 = RIGI MOD1 MA1 ;
RI1 = RIGI MOD1 HOO1 ;
RI2 = RI1 ET CL ;
*
* ----------        FORCE DE TRACTION         ----------
*
FO1 = FORCE ( 100000.0 0.0 ) L1 ;
*
* ----------            RESOLUTION            ----------
*
RE = RESO RI2 FO1 ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS ' Solution Analytique :'                                    ;
MESS '                                                         ' ;
MESS '    UX =' SOLANA  ;
MESS '    UY =' SOLNUL  ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS '                                                         ' ;
MESS ' Solution Calculee :'                                      ;
MESS '                                                             ' ;
LIST RE ;
*
* ----------        CODE DE FONCTIONNEMENT          ----------
*
DEPA1 = EXTR RE UX A1 ;
RESI = ABS( (DEPA1-SOLANA)/SOLANA ) ;
*
SI (RESI <EG 1E-4 ) ;
   ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;
*
* ----------        CALCUL DES DEFORMATIONS         ----------
*
EPS1 = EPSI MOD1 RE ;
LIST EPS1 ;
*
* ----------        CALCUL DES CONTRAINTES         ----------
*
SIG1 = SIGMA MOD1 MA1 RE ;
LIST SIG1 ;
*
FIN ;









