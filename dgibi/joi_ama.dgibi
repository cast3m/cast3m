* fichier :  joi_ama.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
   GRAPH = 'N' ;
   SAUT PAGE ;
********************************************************************
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  TEST AVEC UN JOINT (2D) POUR LE MODELE DE AMADEI-SAEB      ';
MESS '                                                             ';
MESS '  LA SURFACE DE HAUT DU JOINT EST SOUMISE A UN DEPL. IMPOSE  ';
MESS '   SELON LA DIRECTION TANGENTIELLE ET A UNE FORCE CONSTANTE  ';
MESS '   SELON LA DIRETION NORMALE                                 ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                   A _______________ H                       ';
MESS '                    |               |                        ';
MESS '                    |_______________|                        ';
MESS '                   G                 D                       ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  JOINT FORME DES NOEUDS DG-HA                               ';
MESS '                                                             ';
MESS '  CONDITIONS AUX LIMITES  :                                  ';
MESS '       - DG  BLOQUEE EN UX ET EN UY                          ';
MESS '                                                             ';
MESS '  CHARGEMENT              :                                  ';
MESS '       - DEPLACEMENT IMPOSE ET FORCE SUR HA                  ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
MESS '  DANS CET ESSAI ON COMPARE LA CONTRAINTE TANGENTIELLE ET    ';
MESS '   LE DEPLACEMENT NORMAL AUX QUANTITES CORRESPONDANTES       ';
MESS '   CALCULEES ANALYTIQUEMENT.                                 ';
MESS '                                                             ';
MESS '                                                             ';
MESS '                                                             ';
********************************************************************
*
*
   OPTION ECHO 1 DIME 2 ;
   OPTION ELEM SEG2 MODE PLAN CONT ;
   TEMPS ;
*
*------------------------------------------------
* --------- DEFINITION DE LA GEOMETRIE ----------
*------------------------------------------------
*
   A = 0.00  0.00 ;
   H = 1.00  0.00 ;
*
   G = 0.00 0.00 ;
   D = 1.00 0.00 ;
*
   HA =  H DROIT 1 A ;
*
   DG =  D DROIT 1 G ;
*
*-----------------------------------------------------
* -------------- DEFINITION DU MAILLAGE --------------
*-----------------------------------------------------
*
   OPTION ELEM RAC2 ;
*
   JOINT = RACC 0.001  (INVE HA) (INVE DG) ;
*
*------------------------------------------------------------------
* -------------- DEFINITION DES CONDITIONS AUX LIMITES ------------
*------------------------------------------------------------------
*
   CL1 = BLOQ DG UX UY ;
*
   CL5 = BLOQ HA UX ;
*
   CLTOT = CL1 ET CL5 ;
*
*-----------------------------------------------------------------
* -------------- DEFINITION DU MODELE E DU MATERIAU --------------
*-----------------------------------------------------------------
*
   MODJOI = MODE JOINT 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE'
                       'AMADEI' JOI2 ;
*
   MAJOI  = MATE MODJOI  KS 5.  KN 20.  RHO 0.  ALPN 0.  FIMU 30.
                         SGMT -20.  I0 10.  S0 6.5  B0 0.7  UR 14.
                         UP 4.  KNI 6.  FI0 23.  VM 1. ;
*
*--------------------------------------------------------------
* ------------------- DEFINITION DU CHARGEMENT ----------------
*--------------------------------------------------------------
*
   DEP6 = FORCE FY -10.0 HA ;
   DEP5 = DEPI CL5  20.0 ;
*
   L56 = PROG 0.0  1.0  1.0 ;
   L55 = PROG 0.0  0.0  1.0 ;
   L50 = PROG 0.0  1.0  2.0 ;
*
   EV5 = EVOL MANU X L50 Y L55 ;
   EV6 = EVOL MANU X L50 Y L56 ;
*
   CAR5 = CHAR 'DIMP' DEP5 EV5 ;
   CAR6 = CHAR 'MECA' DEP6 EV6 ;
   CART = CAR5 ET CAR6 ;

*
*-------------------------------------------------------
* --------------------- RESOLUTION ---------------------
*-------------------------------------------------------
*
   TAB = TABLE ;
   LIS1 = PROG 0.0 PAS 0.05 2.0 ;
   TAB.'BLOCAGES_MECANIQUES' = CLTOT;
   TAB.'MODELE' = MODJOI;
   TAB.'CHARGEMENT' = CART;
   TAB.'TEMPS_CALCULES' = LIS1;
   TAB.'CARACTERISTIQUES' = MAJOI;
*
TMASAU=table;
tab . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
   PASAPAS TAB ;
*
*--------------------------------------
* ---------- POST TRAITEMENT ----------
*--------------------------------------
*
sig1 = prog ; sig2 = prog ; var1 = prog ; var2 = prog ;
var3 = prog ; var4 = prog ; var5 = prog ; var6 = prog ;
dep1 = prog ; dep2 = prog ; tem0 = prog ; tem1 = prog ;
*
dtab1=index(tab.deplacements) ;
*
ndime=dime dtab1 ;
*
*
i=0 ;
*
REPETER BOU1 ndime ;
*
i=i+1 ;
*
d=dtab1.i ;
*
dep0 = tab.deplacements.d ;
sig0 = tab.contraintes.d ;
var0 = tab.variables_internes.d ;
def0 = tab.deformations_inelastiques.d ;
*
DEPJOI = REDU DEP0 JOINT ;
SIGJOI = REDU SIG0 JOINT ;
VARJOI = REDU VAR0 JOINT ;
DEFJOI = REDU DEF0 JOINT ;
*
tau1 = extr SIGJOI 'SMSN' 1 1 1 ;
tau2 = extr SIGJOI 'SMN' 1 1 1 ;
*
sor1 = extr VARJOI 'EPSE' 1 1 1 ;
sor2 = extr VARJOI 'EPOU' 1 1 1 ;
sor3 = extr VARJOI 'STAT' 1 1 1 ;
sor4 = extr VARJOI 'UEQU' 1 1 1 ;
sor5 = extr VARJOI 'EPS1' 1 1 1 ;
sor6 = extr VARJOI 'EPS2' 1 1 1 ;
*
pla1 = extr DEFJOI 'FRSN' 1 1 1 ;
pla2 = extr DEFJOI 'FRN' 1 1 1 ;
*
sig1 = inse sig1 i tau1 ;
sig2 = inse sig2 i tau2 ;
*
var1 = inse var1 i sor1 ;
var2 = inse var2 i sor2 ;
var3 = inse var3 i sor3 ;
var4 = inse var4 i sor4 ;
var5 = inse var5 i sor5 ;
var6 = inse var6 i sor6 ;
*
tem0 = inse tem0 i d  ;
*
dep1 = inse dep1 i pla1 ;
dep2 = inse dep2 i pla2 ;
*
fin bou1 ;
*
   si ( NEG graph 'N' ) ;
*
      gra1 = evol manu U var5 T sig1 ;
      gra2 = evol manu V var6 S sig2 ;
      gra3 = evol manu t0 tem0 EPSE var1 ;
      gra4 = evol manu t0 tem0 UEQU var4 ;
      gra5 = evol manu t0 tem0 STAT var3 ;
      gra6 = evol comp t0 tem0 U_DRS var5 dep1 ;
      gra7 = evol manu t0 tem0 DRN dep2 ;
*
      dess gra1 ;
      dess gra2 ;
      dess gra3 ;
      dess gra4 ;
      dess gra5 ;
      dess gra6 ;
      dess gra7 ;
*
   finsi ;
*
*-------------------------------------------------------------
*------------------ CODE DE FONCTIONNEMENT -------------------
*-------------------------------------------------------------
*
*
* listreel de la contrainte tangentielle de repère en fonction du temps *
*
pro1 = prog
       2.2599 4.5197 6.7796 9.0394 8.9038 8.7682 8.6327 8.4971 8.3615
       8.2259 8.0903 7.9547 7.8191 7.6835 7.6835 7.6835 7.6835 7.6835
       7.6835 7.6835 ;
*
*
***** listreel du déplacement normal de repère en fonction du temps *****
*
pro2 = prog
       -7.6984E-02 -.1430 -.2001 -.2501 -.2943 -.3335 -.3686
       -.4002 -.4288 -.4548 -.4785 -.5003 -.5203 -.5388 -.5558
       -.5717 -.5866 -.6003 -.6132 -.6252 ;
*
j1 = 21 ;
j2 = 1 ;
ii0 = 0 ;
num1 = 0. ;
num2 = 0. ;
den1 = 0. ;
den2 = 0. ;
*
repeter bou2 20 ;
*
j1 = j1 + 1 ;
j2 = j2 + 1 ;
ii0 = ii0 + 1 ;
*
ttt1 = extr sig1 j1 ;
vvv1 = extr var6 j2 ;
ttt0 = extr pro1 ii0 ;
vvv0 = extr pro2 ii0 ;
*
num1 = num1 + ((ttt1 - ttt0) * (ttt1 - ttt0)) ;
num2 = num2 + ((vvv1 - vvv0) * (vvv1 - vvv0)) ;
den1 = den1 + (ttt0 * ttt0) ;
den2 = den2 + (vvv0 * vvv0) ;
*
fin bou2 ;
*
err0 = (num1/den1) * 100. ;
err1 = (num2/den2) * 100. ;
*
si ((err0 < 5.) et (err1 < 5.)) ;
*
ERRE 0 ;
*
sinon ;
*
ERRE 5 ;
*
finsi ;
*
fin;












