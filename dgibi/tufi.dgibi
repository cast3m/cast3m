* fichier :  tufi.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Plastique
************************************************************************
OPTION ECHO 0;
GRAPH = 'N';
SAUT PAGE;
*
*   TUYAU FISSURE SOLLICITE EN FLEXION PURE
*
OPTI DIME 3 ELEM SEG2 MODE TRID;
REXT1=52.73;T1=8.41;
E=13950;SY=14.47;SM=51.51;
P1=0. 0. 0.;P2=0. 0. 0.;
TUFSS=MANU SEG2 P1 P2;
*
*  COURBE DE TRACTION RENTREE = LOI MOMENT-COURBURE DE L ESSAI
*
LISTM0=PROG 0. 0.5 0.68 0.78 0.87 0.95 1.03 1.12 1.18;
LISTM0=LISTM0 * 1.E6;
LISTPHI0=PROG 0. 0.41 0.65 0.84 1.06 1.38 1.94 2.89 4.20;
LISTPHIC=2 * (LISTPHI0 * PI / 180.) ;
* calcul de la complaisance CMM pour calcul de la rotation
* elastique correspondant au 1er point de la courbe
OBJTUFI=MODE TUFSS  MECANIQUE ELASTIQUE TUFI;
MATTUFI=MATE OBJTUFI YOUN E NU 0.3;
CARTUFI= 'CARA' OBJTUFI RAYO REXT1 EPAI T1
VX 1. VY 0. VZ 0. VXF 0. VYF 0. VZF -1. ANGL 120.;
RITUFI=RIGI (MATTUFI et CARTUFI) OBJTUFI;
CDL1=BLOQ depla rota p1;CDL2=BLOQ RY p2;
RR=RITUFI ET CDL1 ET CDL2;
DF=DEPI CDL2 -1.;PHI=-1;
U=RESO RR DF;FEXT=REAC RR U;M=EXTR FEXT MY P2;
CMM=PHI / M;
phie=CMM * 0.5E6;remp LISTPHIC 2 phie;
*
EV0=evol manu LISTPHIC LISTM0;
COUTRA=EVOL BLEU MANU 'PHI' (LISTPHIC ET (PROG 1.))
 'MOMENT' (LISTM0 ET (PROG 1.18E6));
CJDA=evol manu 'da' (prog 0. 20.) 'f' (prog 3.5E6 3.5E6);
***********RESOLUTION******************
***********PLASTIQUE*******************
OBJAF=MODE TUFSS  MECANIQUE ELASTIQUE PLASTIQUE
 TUYAU_FISSURE TUFI;
MAT=MATE OBJAF YOUN E NU 0.3 JDA CJDA TRAC COUTRA;
CAR= 'CARA' OBJAF RAYO REXT1 EPAI T1
VX 1. VY 0. VZ 0. VXF 0. VYF 0. VZF -1. ANGL 120.;
CARTOT=MAT et CAR;
CDL1=BLOQ depla rota p1;
CDL2=BLOQ RY P2;
CL=CDL1 ET CDL2;
DF=DEPI CDL2 -0.005;
*
EVT=EVOL MANU T (PROG 0. 100.) DF(T) (PROG 0. 100.);
PGT=PROG 1. PAS 1 22.;
FDT=CHAR 'DIMP' DF EVT;
TENTR=TABLE;
TENTR.MAXITERATION=50;
TENTR.CARACTERISTIQUES=CARTOT;
TENTR.MODELE=OBJAF;
TENTR.CHARGEMENT=FDT;
TENTR.BLOCAGES_MECANIQUES=CL;
TENTR.TEMPS_CALCULES= PGT;

PASAPAS TENTR;

pm=prog 0.;pphi=prog 0.;
REPETER BLO 22;
J=&BLO;
UF=TENTR.DEPLACEMENTS.J;
PHI=EXTR UF RY P2;
FEXT=TENTR.REACTIONS.J;M=EXTR FEXT MY P2;
pm=pm et (prog M);pphi=pphi et (prog PHI);
pm=abs pm;pphi=abs pphi;
SI (J EGA 22);
PHIR=IPOL (abs M) LISTM0 LISTPHIC;
ERR = abs ((PHIR - (abs PHI))/PHIR);
SI (ERR <  1.E-3);ERRE 0;SINO;ERRE 5;FINSI;
FINSI;
FIN BLO;

SI ( NEG GRAPH 'N' );
evcal=evol manu pphi pm;
t=tabl;t.1='MARQ CARR ';
t.2='MARQ ETOI ';
t.'TITRE' =tabl;
t.'TITRE'. 1 = MOT 'RENTREE';
t.'TITRE'. 2 = MOT 'CALCULEE';
titr 'comparaison des courbes (m,phi) rentree et calculee';
evt=EV0    et evcal;
dess evt lege t logo;
FINSI;
FIN;









