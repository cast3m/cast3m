* fichier :  lire_STL.dgibi
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************

*******************************************************
*           Test lire_STL.dgibi: Jeux de donnÃ©es      *
*           ---------------------------------         *
*******************************************************


* Presentation : Ce cas-test permet de lire des MAILLAGES de 'TRI3' au
*                format 'STL'. Le resultat est un objet de type
*                TABLE. Chaque indice est un MAILLAGE
*
* Creation : 12/12/2016
* Createur : C. BERTHINIER
*******************************************************

OPTI TRAC 'PSC';
OPTI DIME 3;
OPTI ELEM CUB8;

complet = VRAI;

*******************************************************
* Creation d'un MAILLAGE dans Cast3M
*  - MAILLAGE copie du cas-test elas9.dgibi
*******************************************************
si complet;
 K= 5;
sinon;
 K= 1;
finsi;
*
PB = 0. 2.75 0.;
PB1 = 0. 2.75 (((3.25 ** 2) - (2.75 ** 2)) ** 0.5);
PC = 3.25 0. 0.;
*
C1 = PC CERC (6 * K) (0. 0. 0.) PB1;
C2 = C1 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
PA = 0. 1. 0.;
PA1 = 0. 1. (((2. ** 2) - (1. ** 2)) ** 0.5);
PD = 2. 0. 0.;
*
C3 = Pd CERC (6 * K) (0. 0. 0.) PA1;
C4 = C3 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
D1 = PA DROI (2 * K) (0. 1.583 0.) DROI (2 * K) PB;
D3 = PC DROI (2 * K) (2.417 0. 0.) DROI (2 * K) PD;
*
ELIM (D1 ET C2 ET D3 ET C4) 0.0001;
SUR1 = DALL D1 C2 D3 C4 PLAN;
*
VOL1 = SUR1 VOLU (2 * K) TRAN (0. 0. -0.6);

ENV1 = ENVE VOL1;
ENV1 = CHAN 'TRI3' ENV1;

*******************************************************
* Sortie du MAILLAGE en STL ASCII
*******************************************************
OPTI SORT 'Mesh_STL.stl';
SORT 'STL' ENV1;

*******************************************************
* Relecture du MAILLAGE STL ASCII
*******************************************************
MAIL0 = LIRE 'STL' 'Mesh_STL.stl';
MAIL0 = MAIL0 'COUL' 'ROUG';
ELIM MAIL0 1D-8;
TRAC FACE MAIL0;

* On essaye de faire VOLU pour verification
OPTI ELEM 'TET4';
MAIV2 = VOLU MAIL0;
MAIV2 = MAIV2 COUL 'BLEU';
TRAC FACE MAIV2;

*******************************************************
* Relecture du MAILLAGE STL ASCII Multi-Solids
*******************************************************
MAIL01 = LIRE 'STL' '../divers/Multi_Solid.stl';
MAIL01 = MAIL01 'COUL' 'ROUG';
ELIM MAIL01 1D-8;
TRAC FACE MAIL01;

*******************************************************
* Relecture de MAILLAGES STL binaire
*  - Incompatible avec IBM AIX RS6000 ==> COMMENTE
*  - Voir avec XDR plutot
*******************************************************
SI FAUX;
  MAIL1 = LIRE 'STL' '../divers/Bear_Voronoi_fine.stl';
  MAIL1 = MAIL1 'COUL' 'BRUN';
  ELIM MAIL1 1D-8;
  TRAC FACE MAIL1;

  MAIL2 = LIRE 'STL' '../divers/Thenatarius.stl';
  MAIL2 = MAIL2 'COUL' 'BRUN';
  ELIM MAIL2 1D-8;
  TRAC FACE MAIL2;
FINS;

FIN;



