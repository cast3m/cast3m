* fichier :  plas15.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*                                                     *
GRAPH='N';
SAUT PAGE ;
*
*******************************************************
*  TEST PLAS15
*
* Essai de compression simple d'un cube en beton
* comportement élasto-plastique modèle OTTOSEN
*
* Un parallelogramme est soumis à un déplacement imposé
* sur une de ses faces et fixé de manière isostatique.
* On compare la courbe de réponse uniaxiale à la
* solution exacte.
*
*******************************************************

TITRE 'PARALLELOGRAMME EN COMPRESSION UNIAXIALE ' ;
OPTION ECHO 0 ;
OPTION DIME 2 ELEM QUA4 MODE PLAN CONT ;
*------------------------------------------------------
*           DEFINITION DE LA GEOMETRIE
*------------------------------------------------------
A1= 0. 0. ;
B1= 1. 0. ;
C1= 1. 1. ;
D1= 0. 1. ;
*
L1 = A1 DROIT 1 B1  ;
L2 = B1  DROIT 1 C1  ;
L3 = C1  DROIT 1 D1  ;
L4 = D1  DROIT 1 A1  ;
LL = L1 ET L2 ET L3 ET L4 ;
SU= SURF LL PLAN ;
*------------------------------------------------------
*           DEFINITION DES CONDITIONS AUX LIMITES
*                ET DES DEPLACEMENTS IMPOSES
*------------------------------------------------------
CLX1 = BLOQ L4 UX;
CLX2 = BLOQ L2 UX;
CLY1 = BLOQ L1 UY;
CLY2 = BLOQ L3 UY;
CL1= CLX1  ET CLY1  ;
CL2 = CLX2 ;
DEP = DEPI CL2   -3.5E-4 ;
LI2 = PROG 0.  PAS 0.1 10. ;
LI1 = LI2;
EV = EVOL MANU T LI1 F(T) LI2 ;

CL = CL1 ET CL2;
MO= MODE SU MECANIQUE ELASTIQUE PLASTIQUE 'OTTOSEN';
TBET = TAILLE   MO;

*
*------------------------------------------------------
*           DEFINITION DES PROPRIETES MATERIELLES
*------------------------------------------------------
YG =  40000. ;
RC = 10. ;
MAT  = ( MATE MO  YOUN YG   NU 0.21
'GFTR' 1.5D-4 'BTR' 0.25
'GS' ( 1.8/30.)  'LTR' 1.  'LCS' RC  )  ET TBET   ;
*------------------------------------------
*            APPEL A PASAPAS
* AU TEMPS T , LE DEPLACEMENT VAUDRA F(T)*D
*------------------------------------------
CHA1 = CHAR DIMP DEP EV ;
LIS1 = PROG 0. PAS 0.05  4.  ;
TAB1 = TABLE ;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'CHARGEMENT' =  CHA1 ;
TAB1.'MODELE' = MO;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;
TAB1.FTOL=0.000000001;
PASAPAS TAB1;
*------------------------------------------
*           DEPOUILLEMENT ET
*     CALCUL DE LA SOLUTION ANALYTIQUE
*
*------------------------------------------
*
 PGX = PROG 0. ;
 PGY = PROG 0. ;
 PGT = PROG 0. ;
 KE=( 4.*RC)/(3.*YG) ;
 KU=5.*KE ;
 DK = KU - KE ;
 EMAX=(RC/YG) + KE ;
 EULT= KU;

 TT = TAB1 . CONTRAINTES ;
 NN = (DIME TT) - 1  ;
*
 REPE BOU NN;
 SX = EXTR  ( TAB1 . CONTRAINTES . (&BOU) )
      SMXX 1 1 1 ;
 SX = ABS ( SX );
 EPS = EPSI MO ( TAB1 . DEPLACEMENTS . (&BOU) ) ;
 E =  ABS (EXTR EPS EPXX 1 1 1) ;
 E  = ABS E ;
 PGX = PGX ET ( PROG E ) ;
 PGY = PGY ET ( PROG SX ) ;
  SI ( E <EG  (RC /(3. * YG)));
     S = YG * E ;
  SINON;
  SI ( E <EG EMAX) ;
  AA = 2. / (( KE * YG) ** 2 ) ;
  BB = (3./RC)+  ( 4. / ( KE * YG ) ) ;
  BB = BB - (( 4. * E ) / ((KE * YG) * KE ));
  CC =(( 2. * (E* E))/ (KE*KE))  - 1. ;
  CC = CC - (( 4. * E) /KE )  ;
  DD = (BB * BB ) - (( 4. * AA)  * CC) ;
  S = ((-1.* BB)  + (DD ** 0.5)) / ( 2. * AA ) ;
  SINON;
  SI  ( E <EG EULT) ;
  DDE = E - KE ;
  AA = 1. / (( DK * YG) ** 2 ) ;
  BB = (1./RC) - (( 2. * DDE) / (( DK*DK) * YG ) ) ;
  CC =((DDE*DDE)/ (DK*DK))  - 1. ;
  DD = (BB * BB ) - (( 4. * AA)  * CC) ;
  S = ((-1.* BB)  + (DD ** 0.5)) / ( 2. * AA ) ;
  SINON;
   S=0.;
  FINSI;
  FINSI;
  FINSI;
  FINSI;

  PGT = PGT ET ( PROG S   ) ;
  FIN BOU ;
  EV1= EVOL ROUG  MANU 'EPX' PGX 'SIGX' PGY ;
  EV2= EVOL TURQ  MANU 'EPX' PGX 'STHE' PGT ;
  SI(NEG GRAPH 'N');
    DESS ( EV1 ET EV2 ) ;
  FINSI ;
*
 TEMPS ;
*             CODE FONCTIONNEMENT
  ZZ =( MAXI ABS ( PGY - PGT )  ) / RC ;
  SI (ZZ <EG 2.E-2) ;
 MESS '    ';
 MESS '    ';
 MESS '    ';
 MESS ' COMPARAISON RESULTATS THEORIQUES ET CALCULES';
 MESS '    ';
 MESS ' JUSQU AU TEMPS 4. : PRECISION < A 2E-2 PRES';
SINO;
    ERRE  5;
FINSI;
FIN;







