* fichier :  elas4.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas4.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*______________________________________________________
*  TEST ELAS4                                         *
*                                                     *
*  POUTRE A SECTION EN FORME DE "Z" CHARGEE EN TORSION*
*                                                     *
*  Cas test NAFEMS numero LE5 (Z-section cantilever)  *
*                                                     *
*      La poutre est maillee a l'aide d'elements de   *
*  coques COQ4.                                       *
*  Le maillage a ete affine car on ne peut utiliser   *
*  l'operateur CALP avec l'element COQ8.              *
*                                                     *
*      La poutre est soumis a un couple de torsion    *
*  de 1200 m.N, obtenue par deux forces de 600 N      *
*  uniformement reparties sur chaque flanc.           *
*                                                     *
*      On se propose de caculer la contrainte axiale  *
*  de la surface au point A.                          *
*                                                     *
*______________________________________________________
OPTI DIME 3;
OPTI ELEM QUA4;
*======================================================*
*             geometrie : maillage                     *
*======================================================*
*
* Dimension en metres
*
* Points
*
OEIL = 1000 -1000 1000;
*
P1 = 0 0 0; P21 = (P1 PLUS (10 0 0));
P2 = 0 0 1; P22 = (P2 PLUS (10 0 0));
P3 = 0 2 1; P23 = (P3 PLUS (10 0 0));
P4 = 0 2 2; P24 = (P4 PLUS (10 0 0));
*
P1M = 10 0 .5; P2M = 10 2 1.5;
*
* Droites et lignes
*
* Remarque : ne pouvant utiliser d'element coq8,
* et seulement des coq4 le maillage a ete affine.
*
N1 = 2;
LIG1 = P1 D N1 P2 D N1 P3 D N1 P4;
*
* Maillage
*
N2 = 16;
SUR1 = LIG1 TRANS N2 (10 0 0);

ELIM (SUR1 ET P21 ET P22 ET P23 ET P24 ET P1M ET  P2M) 0.001;

SI (NEG GRAPH 'N');
   TITR ' TEST ELAS4 : MAILLAGE ';
   TRAC OEIL QUAL SUR1;
FINSI;

*====================================================*
*           modele - materiau - caracteristique      *
*           rigidite - conditions aux limites        *
*====================================================*
MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3 EPAI 0.1;
RI1   = RIGI MATR1 MODL1;
CL1   = BLOQ DEPL LIG1;
RIG1  = RI1 ET CL1;

*====================================================*
*                      Chargement                    *
*====================================================*
*   Couple de torsion de 1.2e6 m.N applique dans la
* section x = 10 m  par deux forces uniformement
* reparties egales de 600 N sur chaque flanc.

F1  = 0 0 -150E3;
F1M = 0 0 -300E3;
F2  = 0 0  150E3;
F2M = 0 0  300E3;

FORC1  = FORC F1  P21;
FORC2  = FORC F1  P22;
FORC1M = FORC F1M P1M;
FORC3  = FORC F2  P23;
FORC4  = FORC F2  P24;
FORC2M = FORC F2M P2M;

CHA1 = FORC1 ET FORC2 ET FORC3 ET FORC4 ET FORC1M ET
FORC2M;

*=====================================================*
*            resolution : champs de deplacements      *
*                         champs de contraintes       *
*=====================================================*
DEP1 = RESO RIG1 CHA1;

*
* Trace facultatif de la deformee et des reactions
SI (NEG GRAPH 'N');
  AMPVEC = .5E-6;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT CHA1 (AMPVEC * 20) FX FY FZ ROUGE;
  VECT2 = VECT REA1 AMPVEC FX FY FZ BLEU;
  DEF0 = DEFO 0.  DEP1 SUR1;
  DEF1 = DEFO 10. DEP1 SUR1 (VECT1 ET VECT2) VERT;
  TITR ' ELAS4 : Deformee, chargement, reactions';
  TRAC OEIL SUR1 (DEF0 ET DEF1);
FINSI;

*======================================================*
*                  extraction d'une contrainte         *
*            et comparaison avec solution analytique   *
*======================================================*
*     On cherche la contrainte axiale au point A sur la
* surface moyenne.

* Calcul des contraintes generalisees dans le repere
* local des elements
CHAM1 = SIGM MODL1 MATR1 DEP1;

* Calcul du champs de contraintes au sens des milieux
* continus.
*   Pour cela on utilise l'operateur CALP.
CHAM2 = CALP CHAM1 MATR1 MODL1 MOYEN;
CHAM3 = CHAN NOEUD CHAM2 MODL1;
CHPO3 = CHAN CHPO CHAM3 MODL1;
PA = SUR1 POIN PROC (2.5 0 0);
*
* Trace facultatif des contraintes aux noeuds.
*
SI (NEG GRAPH 'N');
   TITR 'ELAS4 : Maillage';
   TRAC CACH SUR1 'QUAL';
   TITR 'ELAS4 : Contraintes Nodales';
   TRAC CACH SUR1 ((EXCO CHPO3 SMYY)*1e-6);
FINSI;

SMXXA2 = EXTR CHPO3 SMYY PA;
SMXXA2 = SMXXA2 / 1.E6;
SMXXA1 = -108;
ERGXXA = 100 * (ABS ((SMXXA2 - SMXXA1) / SMXXA1));

*=======================================================*
*                   affichage des resultats             *
*=======================================================*
MESS ' RESULTATS TEST ELAS4 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS '   On cherche la contrainte axiale sur la surface moyenne';
MESS ' au point A.';
MESS '   Elle est comparee a une valeur theorique obtenue';
MESS ' analytiquement.';
SAUT 2 LIGN;
MESS ' Contrainte axiale theorique en A :' SMXXA1 'MPa';
MESS ' Contrainte axiale calculee en A  :' SMXXA2 'MPa';
MESS '    Soit un ecart de :' ERGXXA '%';
SAUT 1 LIGN;

ELSUR1 = NBEL SUR1;
NOSUR1 = NBNO SUR1;

MESS '    Nombre d elements :' ELSUR1;
MESS '    Nombre de noeuds  :' NOSUR1;

*========================================================*
*                  code fonctionnement                   *
*========================================================*
SAUT 2 LIGNE;
SI (ERGXXA <EG 5);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;

TEMPS;
FIN;




