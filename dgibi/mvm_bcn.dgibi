* fichier :  mvm_bcn.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Dynamique
************************************************************************
********************************************************************************
* TEST : Modified Von Mises nonlocal damage model                              *
* UNITS:                                                                       *
*          Dimensions in mm                                                    *
*          Loads      in N                                                     *
*          Modulus    in N/mm2 (Mpa)                                           *
*                                                                              *
********************************************************************************
 OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO ;
*-------------------------------------------------------------------------------
* Generation of mesh
*-------------------------------------------------------------------------------
 p1 = 0. 0.;
 p2 = 1. 0.;
 l12 = D 1 p1 p2;
 ma1 = l12 TRAN 1 (0. 1);
*
 p1 = POIN ma1 PROC (0. 0.) ;
 p2 = POIN ma1 PROC (1. 0.) ;
 p3 = POIN ma1 PROC (0. 1) ;
 p4 = POIN ma1 PROC (1. 1) ;
*
 l12 = (CONT ma1) ELEM COMPRIS p1 p2 ;
 l13 = (CONT ma1) ELEM COMPRIS p3 p1 ;
 l24 = (CONT ma1) ELEM COMPRIS p2 p4 ;
 l34 = (CONT ma1) ELEM COMPRIS p4 p3 ;
*-------------------------------------------------------------------------------
* Definition of the model and material parameters
*-------------------------------------------------------------------------------
 mobet = MODE ma1 MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT MVM
      'NON_LOCAL' 'MOYE' 'V_MOYENNE' (MOTS 'EPTI') ;
*
 b1    = 15000.     ; b2       = 0.5     ;
 y0    = 1.e-4      ; ratio_ct = 10.     ;
 ll    = 2.         ; nuc   = 0.         ;
 younc = 1.e4       ; loi   = 1          ;
*
 mabet = MATE mobet 'YOUN' younc 'NU' nuc RHO 1.
                    'Y0' y0 'RATI' ratio_ct
                    'B1' b1 'B2' b2 'LOI' loi 'LCAR' LL ;
*-------------------------------------------------------------------------------
* Boundary conditions
*-------------------------------------------------------------------------------
* 1) Fixed boundaries
 bcfix = (BLOQUE L13 DEPL) ;
* 2) Prescrite Displacements
 bcdisp = BLOQUE  (L24) UX ;
*-------------------------------------------------------------------------------
* Adjustment of the reference load
*-------------------------------------------------------------------------------
 chdisp1 = MANU CHPO L24 1 'UX' 10*y0 ;
 ffextp  = DEPI bcdisp chdisp1 ;
 evol1  = EVOL MANU Temps (PROG 0. 1.) Depl (PROG 0. 1.) ;
 cha1   = CHAR DIMP evol1 ffextp ;
*-------------------------------------------------------------------------------
* Connectivities (for nonlocal averaging)
*-------------------------------------------------------------------------------
 conn1 = CONN mobet MABET 'NORMAL' 'INTERIEUR' ;
*-------------------------------------------------------------------------------
* Input for pasapas procedure
*-------------------------------------------------------------------------------
 t0 = TABLE                                   ;
 t0.'MODELE'             = mobet              ;
 t0.'CARACTERISTIQUES'   = mabet              ;
 t0.'CHARGEMENT'         = cha1               ;
* il y a un probleme pour le temps 0.1  PV
 t0.'TEMPS_CALCULES'     = PROG 0. 0.05  0.099 PAS 0.05 1.;
 t0.'BLOCAGES_MECANIQUES' =  bcfix  et bcdisp ;
 t0.'MAXITERATION'       = 100                ;
 t0.'NACCE'              = 100                ;
 t0.'MOVA'               = 'D   '             ;
 t0.'CONN'               = conn1              ;
 t0.'FACTMIN'            = .001               ;
 t0.'MAXSTEP'            = 10000              ;
 t0.'PRECISION'          = 1.E-10             ;
 t0.'PRECFLEX'           = 1.E-10             ;
 t0.'CONVERGENCE_FORCEE' = FAUX               ;
 t0.'ACCELERATION'       = 50                 ;
*
 PASAPAS t0 ;
*
*------------------------------------------------------------------------------
* Evolution of stress.vs.strain and force.vs.displacement
*------------------------------------------------------------------------------
 sig0 = t0.CONTRAINTES        ;
 dep0 = t0.DEPLACEMENTS       ;
 var0 = t0.VARIABLES_INTERNES ;
 for0 = t0.REACTIONS          ;
 ind0 = INDEX sig0            ;
 nind  = DIME for0            ;
*
 for     = PROG ;
 dep     = PROG ;
 sigma   = PROG ;
 epsilon = PROG ;
*
REPETER indj  nind ;
   i = &indj - 1 ;
*
   sii = EXCO 'SMXX' (sig0.i) 'SCAL' ;
   epi = EXCO 'EPXX' (EPSI mobet (dep0.i)) 'SCAL' ;
   sigp2 = EXTR sii 'SCAL' 1 1 1 ;
   sigma = sigma INSE &indj (sigp2) ;
   epsp2 = EXTR epi 'SCAL' 1 1 1 ;
   epsilon = epsilon INSE &indj (epsp2) ;
*
   fi = EXTR (for0.i) FX p2 ;
   di = EXTR (dep0.i) UX p2 ;
   for      = for    INSE &indj (fi) ;
   dep      = dep   INSE &indj (di) ;
FIN indj ;
*
 SI ((ABS (MAXI ((0.5*sigma) - for))) < 0.5e-5) ;
   ERRE 0 ;
 SINON ;
 mess 'test :' (ABS (MAXI ((0.5*sigma) - for)));
  ERRE 5 ;
 FINSI ;

FIN;













