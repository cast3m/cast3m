* fichier :  plas5.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Plas5.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test plas5.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************************
*  TEST PLAS5
*
* Sortie du domaine élastique et phase plastique
* (comportement élasto-plastique modèle DRUCKER-PRAGER)
*
* Une plaque carrée est soumise à un déplacement imposé
* sur deux de ses bords et fixée sur les deux autres.
* On compare les contraintes à la solution exacte.
*
*******************************************************
*
*       ^    ^
*       |    |  d(t)
*       |    |
*
*     D .____. C    ----> d(t)
*       |    |
*       |    |
*     A .____. B    ----> d(t)
*
*
*******************************************************

TITRE 'PLAQUE CARRE EN BI-TRACTION PURE ';
OPTION ECHO 0 ;
OPTION DIME 2 ELEM QUA4 MODE PLAN CONT ;
TEMPS ;
*------------------------------------------------------
*           DEFINITION DE LA GEOMETRIE
*------------------------------------------------------
A = 0. 0. ;
B = 1.5 0. ;
C = 1.5 1.5 ;
D = 0. 1.5 ;
*
L1 = A DROIT 1 B  ;
L2 = B  DROIT 1 C  ;
L3 = C  DROIT 1 D  ;
L4 = D  DROIT 1 A  ;
LL = L1 ET L2 ET L3 ET L4 ;
SU = SURF LL PLAN ;
SI(NEG GRAPH 'N');
   TRACE  QUAL SU ;
FINSI;
*------------------------------------------------------
*         DEFINITION DES CONDITIONS AUX LIMITES
*              ET DES DEPLACEMENTS IMPOSES
*------------------------------------------------------
CL4 = BLOQ L4 UX  ;
CL2 = BLOQ L2 UX  ;
CL3 = BLOQ L3 UY  ;
CL1 = BLOQ L1 UY  ;
CL = CL1 ET CL2 ET CL3 ET CL4 ;
*
D3 = DEPI CL3 1.5E-4 ;
D2 = DEPI CL2 1.5E-4 ;
DEP = D2 ET D3 ;
*
*-------- Utilisation de la procedure PASAPAS ---------
*-- Les objets utilisés par la procédure PASAPAS sont -
*-------- définis dans les lignes qui suivent ---------
*
MO = MODE SU MECANIQUE ELASTIQUE PLASTIQUE DRUCKER_PRAGER QUA4 ;

KLL= 3.56 ;
ETT= 0.441;
MUU= 1.02 ;
MAT1 = MATE MO YOUN 40000  NU 0.21 ALFA 0.441 BETA 1.02 K 4.1 GAMM 0.441 DELT 1.02 ETA ETT  MU MUU KL KLL  H 0. ;
*
*------- Création d'une relation linéaire entre -------
*----------- le déplacement et le temps ---------------
* Remarque : au temps T, le déplacement vaut F(T)*D .
*
LI1 = PROG 0. 1. 2. 3. 4. 5.  ;
LI2 = PROG 0. 1. 2. 3. 4. 5.  ;
EV = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR DIMP  DEP EV ;
*
*------- Création de la liste des pas de calcul -------
*
LIS1 = PROG 0. 1. 2.;
TAB1 = TABLE ;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'MODELE' = MO;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;
*
*---------- Calcul par la procedure PASAPAS -----------
*
PASAPAS TAB1;
*
*    recherche des contraintes resultats
*    par la procedure PECHE
*
SIG1 = PECHE TAB1 CONTRAINTES 1.;
SIG2 = PECHE TAB1 CONTRAINTES 2.;


SI(NEG GRAPH 'N');
   TITR 'Contrainte SMYY increment 2 en MPa';
   TRAC (EXCO SMYY SIG2) MO;
FINSI;

*
*  extraction d'une valeur d'une composante
*  en un point de gauss
*
SXX = EXTR SIG2 'SMXX' 1 1 1 ;
SYY = EXTR SIG2 'SMYY' 1 1 1 ;
*
*   calcul de la valeur th{orique
*
VALTH = KLL / ( ( 2. * ETT ) +  MUU );
RESI1=100 * (ABS((SXX-VALTH)/VALTH));
RESI2=100 * (ABS((SYY-VALTH)/VALTH));
SAUT PAGE ; SAUT 2 LIGN ;
*
MESS '        CONTRAINTE THEORIQUE CONTRAINTES CALCULEES   ';
SAUT 1 LIGN ;
MESS '           SIG = '  VALTH  ' SMXX=' SXX '  SMYY=' SYY ;
MESS ' SOIT UN ECART DE  ' RESI1 '%  SUR SMXX'   ;
MESS ' SOIT UN ECART DE  ' RESI2 '%  SUR SMYY'   ;
*
TEMPS ;
*             CODE FONCTIONNEMENT

SI((RESI1 <EG 1e-10) ET (RESI2 <EG 1e-10));
  ERRE  0;
SINO;
  ERRE  5;
FINSI;

FIN;






