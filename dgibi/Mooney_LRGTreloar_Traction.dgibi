* fichier : Mooney_LRGTreloar_Traction.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Elastique
************************************************************************
*======================================================================*
*         MODELE HYPERELASTIQUE MOONEY-RIVLIN INCOMPRESSIBLE           *
*           EN GRANDES TRANSFORMATIONS - CONTRAINTES PLANES            *
*                                                                      *
*   TEST DE VALIDATION DU MODELE : TRACTION SELON LA DIRECTION Y       *
*   COMPARAISON AVEC LA SOLUTION ANALYTIQUE                            *
*                                                                      *
*   Contribution de Laurent Gornet - Ecole Centrale de Nantes (2006)   *
*======================================================================*
*  Pour plus d'informations, voir la presentation de L. Gornet lors    *
*  du Club Cast3m 2006, disponible sur le site Web de Cast3m.          *
*======================================================================*
*  Exemple d'utilisation d'un modele UMAT en grandes transformations   *
*                                                                      *
*  Note : Actuellement en grandes deformations dans PASAPAS, le modele *
*         ne peut contenir que des modeles de type UMAT. On ne peut    *
*         pas melanger les derivees objectives et les modeles de C3m.  *
*======================================================================*

'OPTION' 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ECHO' 0 ;

* Mettre VRAI si l'on souhaite divers traces.
GRAPH = FAUX ;

title = 'CHAINE' 'MOONEY-RIVLIN - ' 'TRACTION UNIAXIALE Y' ;

*======================================================================*
*             Geometrie - Maillage                                     *
*======================================================================*
* Longueur (direction x) de la plaque/membrane :
Lg_x = 1. ;
* Largeur (direction y) de la plaque/membrane :
Lg_y = 1. ;
* Nombre d'elements selon les directions x et y :
Nel_x = 2 ;
Nel_y = 2 ;
*
'OPTION' 'ELEM' 'TRI6' ;option epsilon 'UTILISATEUR';
*
P1 =  0.    0. ;
P2 = Lg_x   0. ;
P3 = Lg_x Lg_y ;
P4 =  0.  Lg_y ;
*
L1 = 'DROITE' Nel_x P1 P2 ;
L2 = 'DROITE' Nel_y P2 P3 ;
L3 = 'DROITE' Nel_x P3 P4 ;
L4 = 'DROITE' Nel_y P4 P1 ;
*
SU = 'DALLER' L1 L2 L3 L4 ;
'SI' GRAPH ;
  'TRACER' SU 'TITRE' ('CHAINE' title ' - MAILLAGE') ;
'FINSI' ;

*======================================================================*
*             Modele - Materiau - Caracteristiques (en Pa)             *
*======================================================================*
'SI' (('NEG' ('VALEUR' 'DIME') 2) 'ET'
      ('NEG' ('VALEUR' 'MODE') 'PLANCONT')) ;
  'MESS' 'Ce modele ne fonctionne qu en 2D CONTRAINTES PLANES' ;
  'ERREUR' 5 ;
'FINSI' ;
*
* Ne pas oublier de definir les parametres lies a l'elasticite.
* Meme si ce n'est pas utilise dans le modele, cela est utile pour
* l'operateur de convergence mecanique de PASAPAS-INCREME.
*
LCMAT = 'MOTS' 'YOUN' 'NU  '  'C1  ' 'C2  ' ;
MO = MODE SU 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
                         'NON_LINEAIRE' 'UTILISATEUR'
                         'NUME_LOI' 31  'C_MATERIAU' LCMAT ;
*
* Coefficients du modele de Mooney-Rivlin (en Pa) :
C1 = 0.183E+6 ;     C2 = 0.0034E+6  ;
*
* On fixe le coefficient de Poisson XNU a une valeur proche de 0.5
* du fait de l'incompressibilite inherente au modele.
* Le module de Young YOU est alors connu, car, pour ce modele, le
* module de cisaillement MU vaut : MU = YOU/(2*(1+XNU)) = 2.(C1+C2)
* Il s'agit de la valeur initiale et de la borne inferieure dans le cas
* de la traction. En fonction du niveau de deformation atteinte en
* traction, il faut augmenter cette valeur afin de pouvoir faire
* converger les calculs (module tangent en fin de calculs).
* Prendre des valeurs superieures n'entraine pas de modification des
* resultats, cela modifie seulement le nombre d'iterations mecaniques.
*
XNU = 0.497 ;
YOUini = 3.*(2.*(C1+C2)) ;   YOU = 100. * YOUini ;
*
MA = 'MATERIAU' MO 'YOUN' YOU  'NU  ' XNU
                   'C1  ' C1   'C2  ' C2 ;

*======================================================================*
*             Conditions aux limites - Traction suivant UY             *
*======================================================================*
BL1 = 'BLOQUER' 'UY  ' L1 ;
BL2 = 'BLOQUER' 'UY  ' L3 ;
BL4 = 'BLOQUER' 'UX  ' P1 ;
BLTOT = BL1 'ET' BL2 'ET' BL4 ;
*
* Definition des instants du chargement :
t_deb = 0. ;   t_fin = 10. ;
L_tps = 'PROG' t_deb t_fin ;
* Deplacement suivant Y :
L_UY = 'PROG' 0. ( 3. * Lg_y) ;
FF_y = 'DEPIMP' BL2 1. ;
EV_y = 'EVOL' 'MANU' 'TEMPS' L_tps  'LAMY' L_UY ;
CHARTOT = 'CHARGEMENT' 'DIMP' FF_y EV_y ;

*======================================================================*
*             Initialisation de la table pour appel a PASAPAS          *
*======================================================================*
TAB1 = 'TABLE' ;
TAB1.'MODELE' = MO ;
TAB1.'CARACTERISTIQUES' = MA ;
TAB1.'BLOCAGES_MECANIQUES' = BLTOT ;
TAB1.'CHARGEMENT' = CHARTOT ;
*TAB1.'PRECISION' = 1.E-5 ;
*TAB1.'FTOL' = 1.E-5 ;
*TAB1.'MTOL' = 1.E-5 ;
TAB1.'CONVERGENCE_FORCEE' = FAUX ;
TAB1.'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1.'TEMPS_CALCULES' = 'PROG' t_deb 'PAS' 0.1 t_fin ;
TAB1.'TEMPS_SAUVES' = 'PROG' t_deb 'PAS' 0.5 t_fin ;
*
L_abs = TAB1.'TEMPS_SAUVES' ;
n_abs = 'DIMENSION' L_abs ;
*
PASAPAS TAB1 ;
*
* Quelques traces de controle apres calculs
'SI' GRAPH ;
  Defo_0 = 'DEFORMEE' SU (TAB1.'DEPLACEMENTS'.(n_abs-1)) 0. ;
  Defo_1 = 'DEFORMEE' SU (TAB1.'DEPLACEMENTS'.(n_abs-1)) 1. 'VERT' ;
  'TRACER' (Defo_0 'ET' Defo_1)
           'TITRE' ('CHAINE' title ' - DEFORMEES INITIALE ET FINALE') ;
  'TRACER' MO (TAB1.'CONTRAINTES'.(n_abs-1))
           'TITRE' ('CHAINE' title ' - CONTRAINTES EN FIN DE CALCUL') ;
'FINSI' ;
*
*======================================================================*
*             Construction de la solution analytique                   *
*======================================================================*
* Definitions :
* - Allongement selon direction y : Lamy = 1 + (UY/Lg_y)
* - Densite d'energie de deformation hyperelastique : W(I1,I2)
* - I1, I2 : trois invariants du tenseur de Cauchy-Green droit
* Dans le cas du modele de Mooney-Rivlin :
*   W = C1*(I1-3.)+C2*(I2-3.) , soit dW/dI1 = C1 et dW/dI2 = C2
*
* Les contraintes de Cauchy sont calculables analytiquement :
* - SCxx = 0.
* - SCyy = 2.(Lamy**2 - 1./Lamy).(dW/dI1 + 1./Lamy.dW/dI2)
* - SCxy = 0  (pas de cisaillement)
* - SCzz = 0  (hypothese des contraintes planes)
*
L_Un  = 'PROG' n_abs '*' 1. ;
Lamy  = L_Un + (('IPOL' L_abs L_tps L_UY) / Lg_Y) ;
*
SCxx_th = 0. * L_Un ;
L_z1 = Lamy * Lamy ;  L_z2 = L_Un / Lamy ;
SCyy_th = (L_z1 - L_z2) * ((2.*C1*L_Un) + (2.*C2*L_z2)) ;
SCxy_th = 0. * L_Un ;

*======================================================================*
*             Comparaison des resultats avec la solution analytique    *
*======================================================================*
* La comparaison s'effectue entre les valeurs moyennes des contraintes
* calculees et les solutions analytiques correspondantes.
* On ne cherche pas a verifier l'uniformite du champ de contraintes.
* (Faire le calcul en mettant GRAPH a VRAI et voir les isovaleurs !)
*
TabD = TAB1.'DEPLACEMENTS' ;
TabS = TAB1.'CONTRAINTES' ;
Confini = 'FORM' ;
ChmUn = 'MANU' 'CHML' MO 'SCAL' 1. ;
*
SCxx = 'PROG' 0. ;
SCyy = 'PROG' 0. ;
SCxy = 'PROG' 0. ;
'REPETER' Boucle (n_abs - 1) ;
  'FORM' (TabD.&Boucle) ;
  VolSU = 'INTG' MO ChmUn ;
  SCxx = SCxx 'ET' ('PROG' (('INTG' MO (TabS. &Boucle) 'SMXX')/VolSU)) ;
  SCyy = SCyy 'ET' ('PROG' (('INTG' MO (TabS. &Boucle) 'SMYY')/VolSU)) ;
  SCxy = SCxy 'ET' ('PROG' (('INTG' MO (TabS. &Boucle) 'SMXY')/VolSU)) ;
  'FORM' Confini ;
'FIN' Boucle ;
*
'SI' GRAPH ;
  tlege = 'TABLE' ;
  tlege. 1 = 'MARQ CROI' ;
  tlege.'TITRE' = 'TABLE' ;
  tlege.'TITRE'. 1 = 'Numerique' ;
  tlege.'TITRE'. 2 = 'Analytique' ;
  Evxx    = 'EVOL' 'ROUG' 'MANU' 'LAMB' L_abs 'SCXX' SCxx ;
  Evxx_th = 'EVOL' 'BLEU' 'MANU' 'LAMB' L_abs 'SCXX' SCxx_th ;
  'DESSIN' (Evxx 'ET' Evxx_th) 'LEGE' tlege
           'TITRE' ('CHAINE' title ' - CONTRAINTE DE CAUCHY XX (Pa)') ;
  Evyy    = 'EVOL' 'ROUG' 'MANU' 'LAMB' L_abs 'SCYY' SCyy ;
  Evyy_th = 'EVOL' 'BLEU' 'MANU' 'LAMB' L_abs 'SCYY' SCyy_th ;
  'DESSIN' (Evyy 'ET' Evyy_th) 'LEGE' tlege
           'TITRE' ('CHAINE' title ' - CONTRAINTE DE CAUCHY YY (Pa)') ;
  Evxy    = 'EVOL' 'ROUG' 'MANU' 'LAMB' L_abs 'SCXY' SCxy ;
  Evxy_th = 'EVOL' 'BLEU' 'MANU' 'LAMB' L_abs 'SCXY' SCxy_th ;
  'DESSIN' (Evxy 'ET' Evxy_th) 'LEGE' tlege
           'TITRE' ('CHAINE' title ' - CONTRAINTE DE CAUCHY XY (Pa)');
'FINSI' ;
*
* Tests de bon fonctionnement :
r_z = 'MAXIMUM' ('ABS' SCyy_th) ;
r_xx = 'MAXIMUM' ('ABS' (SCxx - SCxx_th)) / r_z ;
r_yy = 'MAXIMUM' ('ABS' (SCyy - SCyy_th)) / r_z ;
r_xy = 'MAXIMUM' ('ABS' (SCxy - SCxy_th)) / r_z ;
*
MESS ' RESULTATS : ' title ;
MESS ' ------------------------------------------------- ';
'SAUTER' 1 'LIGNE' ;
'MESS' '  Tests de bon fonctionnement :' ;
'MESS' ' -------------------------------' ;
'MESS' ' Comparaison effectuee sur les contraintes de Cauchy' ;
'MESS' ' Ecart relatif maximal entre la valeur moyenne '
       'calculee' ;
'MESS' '                          et la '
       'solution analytique associee' ;
'MESS' ' Composante XX : ' r_xx ;
'MESS' ' Composante YY : ' r_yy ;
'MESS' ' Composante XY : ' r_xy ;
'SAUTER' 1 'LIGNE' ;
* Ecart relatif maximal tolere
Sigref = 1.E-3 ;
'SI' ('>EG' ('MAXIMUM' ('PROG' r_xx r_yy r_xy)) Sigref) ;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  'ERREUR' 5 ;
'SINON' ;
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
'FINSI' ;
'SAUTER' 1 'LIGNE' ;

'FIN' ;









