* fichier :  rupt11.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
****************************************************
*                                                  *
*   VALIDATION DES PROCEDURES GTHETA ET T_PITETA   *
*    PAR UNE PLAQUE EN TRACTION PURE. SOLUTION     *
*    DE REFERENCE : ISIDA, On the tension of a     *
*      strip with a central elliptical hole.       *
*   Tran. Jap. Soc. Mec. Engng., vol. 21 (1955).   *
*                                                  *
*  GEOMETRIE :                                     *
*                                                  *
*  longueur totale de la plaque  : 700 MM          *
*  largeur totale de la plaque   : 200 MM          *
*  longueur totale de la fissure : 100 MM          *
*                                                  *
*  MATERIAU :                                      *
*                                                  *
*  Module d'Young         : 19775 Kgf/mm²          *
*  Coefficient de poisson : 0,3                    *
*                                                  *
*  CHARGEMENT :                                    *
*                                                  *
*  Traction pure          : 100 Kgf/mm²            *
*                                                  *
*  SOLUTION ANALYTIQUE :                           *
*                                                  *
*  Le taux de restitution d'énergie (G_THETA) :    *
*       G = 102,23 Kgf/mm                          *
*  La dérivée seconde de l'énergie (T_PITETA) :    *
*       dG/da = 3,650 Kgf/mm²                      *
*                                                  *
****************************************************
DEBP PRMAIL P0*POINT LI11*MAILLAGE LI12*MAILLAGE LI13*MAILLAGE
            LI14*MAILLAGE NVTAI*FLOTTANT NDC*ENTIER;
  P21=P0 MOIN (NVTAI 0.);P22=P21 PLUS (0. NVTAI);
  P23=P22 PLUS (NVTAI 0.);P24=P23 PLUS (NVTAI 0.);
  P25=P24 MOIN (0. NVTAI);
  LI21=P21 D NDC P22;LI22=P22 D NDC P23;
  LI23=P23 D NDC P24;LI24=P24 D NDC P25;
  SU21=COUT LI11 LI21;SU22=COUT LI12 LI22;
  SU23=COUT LI13 LI23;SU24=COUT LI14 LI24;
  SUT=SU21 ET SU22 ET SU23 ET SU24;
FINP LI21 LI22 LI23 LI24 SUT;
*
OPTI DIME 2 ELEM QUA8 MODE PLAN DEFO;
opti epsilon lineaire;
PB=50. 0.; VTAI=0.5; TVTAI=TABLE;
***************************************
* MAILLAGE CARRE EN POINTE DE FISSURE *
***************************************
CVTAI=10. * VTAI; TVTAI=TABL; TNDC=TABL;
TVTAI.1= 12. * VTAI; TNDC.1=8;
TVTAI.2= 15. * VTAI; TNDC.2=6;
TVTAI.3= 20. * VTAI; TNDC.3=5;
TVTAI.4= 28. * VTAI; TNDC.4=4;
TVTAI.5= 40. * VTAI; TNDC.5=3;
TVTAI.6= 58. * VTAI; TNDC.6=3;
TVTAI.7= 78. * VTAI; TNDC.7=3;
TVTAI.8=100. * VTAI; TNDC.8=2;
P11=PB MOIN (CVTAI 0.); P12=P11 PLUS (0. CVTAI);
MCARR1=(P11 D 10 P12) TRAN 10 (CVTAI 0.);
MCARR2=(INVE (COTE 3 MCARR1)) TRAN 10 (CVTAI 0.);
MCARR0=MCARR1 ET MCARR2;
PB=MCARR0 POIN PROCH PB;
MFOND=MCARR0;
*********************************************
* ON ELARGIT ET ON MET A LA PLACE DE SUFISS *
*********************************************
LI11=COTE 1 MCARR1;LI12=COTE 2 MCARR1;
LI13=COTE 2 MCARR2;LI14=COTE 3 MCARR2;
I=0;
REPETER BCOU (DIME TVTAI);
  I=I + 1;
  NVTAI=TVTAI.I;NDC=TNDC.I;
  LI11 LI12 LI13 LI14 SUT2=PRMAIL PB LI11 LI12 LI13 LI14 NVTAI NDC;
  MFOND=MFOND ET SUT2;
FIN BCOU;
ELIM 0.01 MFOND;
SUT = (LI12 ET LI13) TRAN -12 DINI 8 DFIN 30 (0. (350. - 50.));
SUT = SUT ET MFOND;
CSUT = CONT SUT;
ELIM 0.01 SUT;
*******************************************
* PREPARATION POUR CALCUL SUR MAILLES 1MM *
*******************************************
LICOT=CSUT ELEM COMP (SUT POIN PROC (0. 350.)) (SUT POIN PROC (0. 0.));
LIGAM=CSUT ELEM COMP (SUT POIN PROC (50. 0.)) (SUT POIN PROC (100. 0.));
LIFIS=CSUT ELEM COMP (SUT POIN PROC (0. 0.)) (SUT POIN PROC (50. 0.));
LIHAU=CSUT ELEM COMP (SUT POIN PROC (100. 350.))
      (SUT POIN PROC (0. 350.));
OB1= modeli sut mecanique elastique;
MA1=MATE OB1 YOUN 19775. NU 0.3 ALPHA 1.5E-5;
RI1=RIGI MA1 OB1;
CDL1=(BLOQ UX LICOT) ET (BLOQ UY LIGAM);
FO1=PRES MASS OB1 -100. LIHAU;
U=RESO (RI1 ET CDL1) FO1;
SIG=SIGM MA1 U OB1;
titr 'Fissure deformee sous la pression';
vec1=vect fo1 fx fy 0.01 roug;
*trac (defo sut u vec1) face (cont sut);
***** Solution analytique ******
COE1 = (0.91/19775.)*100.*100.*pi;
COE2 = pi*50./200;
COS1 = cos (COE2*180./pi); SIN1 = sin (COE2*180./pi);
G_ANA = COE1*50.*(1./COS1);
DGDA_ANA = COE1*((1./COS1) + (COE2*SIN1/(COS1*COS1)));
********************************
* Appel à la procédure G_THETA *
********************************
TAB = TABLE ;
TAB.'MAILLAGE'= SUT;
TAB.'FISSURE' = LIFIS;
TAB.'FONDFISS' = PB;
AFF = TABLE; AFF.1 = OB1;
TAB2=TABLE;
TAB2.'DEPLACEMENT'=U;
TAB2.'CONTRAINTE'=SIG;

SUPTAB = TABLE ;
SUPTAB.'OBJECTIF' = MOT 'J';
SUPTAB.'LEVRE_SUPERIEURE' = lifis;
SUPTAB.'FRONT_FISSURE' = PB ;
SUPTAB.'MODELE' = ob1;
SUPTAB.'CARACTERISTIQUES' = ma1;
SUPTAB.'SOLUTION_RESO' = u;
SUPTAB.'CHARGEMENTS_MECANIQUES' = FO1;
i = 5;
repeter bbbb 1;
   i = i + 1;
   SUPTAB.'COUCHE' = i;
   G_THETA SUPTAB;
   G_NUM = SUPTAB.'RESULTATS';
   ERR1 = (ABS ((G_NUM - G_ANA)/G_ANA))*100.;
   MESS 'G_NUM =' G_NUM 'Erreur =' (CHAI ERR1 ' %');
fin bbbb;
*********************************
* Appel à la procédure T_PITETA *
*********************************
SUPTAB.'OBJECTIF' = MOT 'DJ/DA';
SUPTAB.'BLOCAGES_MECANIQUES'=CDL1 ;
i = 5;
repeter bbbb 1;
   i = i + 1;
   SUPTAB.'COUCHE' = i;
   G_THETA SUPTAB;
   DGDA_NUM = SUPTAB.'RESULTATS';
   ERR2 = (ABS ((DGDA_NUM - DGDA_ANA)/DGDA_ANA))*100.;
   MESS 'DGDA_NUM =' DGDA_NUM 'Erreur =' (CHAI ERR2 ' %');
fin bbbb;
**************************************
************ Arrêt du calcul *********
**************************************
SI ((ERR1 > 0.65) OU (ERR2 > 0.5));
  ERRE 5;
SINO;
  ERRE 0;
FINSI;
FIN;

















