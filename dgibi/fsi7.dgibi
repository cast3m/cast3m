* fichier : fsi7.dgibi
************************************************************************
* Section : Fluides Vibration
* Section : Mecanique Interaction Fluide Structure
************************************************************************
*************************************************************
*
* Cas test FSI7
*
* Calcul d'une masse ajoutee en mode de Fourier
*  (lame fluide)
*
*
*
* D. Combescure Décembre 2006
*
*************************************************************
*
*FLAGVISU = VRAI;
*FLAGLONG = VRAI;
*
FLAGVISU = FAUX;
FLAGLONG = FAUX;
*
opti dime 2 elem qua4 mode four 1;
*
*
HH1 = 9.D-3;
R1 = 7.85D-3;
h1 = 0.3D-3;
*
HH2 = 20.D-3;
R2 = 10.D-3;
h2 = 0.6D-3;
*
SI FLAGLONG;
 nz12 = 10;
 nz1 = 10;
 nx1 = 5;
 nx2 = 5;
SINON;
 nz12 = 1;
 nz1 = 1;
 nx1 = 1;
 nx2 = 1;
FINSI;
*
p1 = 0.   0.;
p2 = (R1 - h1)   0.;
p3 = (R1 + h1)   0.;
surbas1 = droi nx1 p2 p3;
p4 = (R2 - h2)   0.;
p5 = (R2 + h2)   0.;
surbas2 = droi nx2 p4 p5;
*
p3s = p3 plus (0. 0.);
p4s = p4 plus (0. 0.);
sursol = droi 1 p3s p4s;
*
vz1= 0. HH1;
p2h = p2 plus (0.5*vz1);
p3h = p3 plus (0.5*vz1);
p4h = p4 plus (0.5*vz1);
p5h = p5 plus (0.5*vz1);
p2b = p2 plus (-0.5*vz1);
p3b = p3 plus (-0.5*vz1);
p4b = p4 plus (-0.5*vz1);
p5b = p5 plus (-0.5*vz1);
*
vz12= 0. (HH2 - HH1);
p4h2 = p4h plus (0.5*vz12);
p5h2 = p5h plus (0.5*vz12);
p4b2 = p4b plus (-0.5*vz12);
p5b2 = p5b plus (-0.5*vz12);
*
volint1 = (tran surbas1 nz1 (0.5*vz1)) et
          (tran surbas1 nz1 ((-0.5)*vz1)) ;
volint2 = ((tran surbas2 nz1 (0.5*vz1)) et
          (tran (surbas2 plus (0.5*vz1)) nz12 (0.5*vz12)))
       et ((tran surbas2 nz1 ((-0.5)*vz1)) et
          (tran (surbas2 plus ((-0.5)*vz1)) nz12 ((-0.5)*vz12)));
volsol =  ((tran sursol nz1 (0.5*vz1)) et
          (tran (sursol plus (0.5*vz1)) nz12 (0.5*vz12)) et
          (tran sursol nz1 ((-0.5)*vz1)) et
          (tran (sursol plus ((-0.5)*vz1)) nz12 ((-0.5)*vz12)))
            coul rouge;
*
SI FLAGVISU;
 trac (volsol et volint1 et volint2);
FINSI;
*
surint = (d nz1 p2 p2h)
      et (d nz1 p2 p2b);
surint2 = (d nz1 p3 p3h)
      et (d nz1 p3 p3b);
surext = (d nz1 p4 p4h) et (d nz12 p4h p4h2)
      et (d nz1 p4 p4b)
      et (d nz12 p4b p4b2);
surext2 = (d nz1 p5 p5h) et (d nz12 p5h p5h2)
      et (d nz1 p5 p5b)
      et (d nz12 p5b p5b2);
surver1 = (surbas1 plus (0.5*vz1))
      et (surbas1 plus (-0.5*vz1));
surver2 = (surbas2 plus (0.5*(vz1 plus vz12)))
      et (surbas2 plus (-0.5*(vz1 plus vz12)));
*
elim 0.0001
   (surint et surint2 et surext et surext2
     et volint1 et volint2 et surver1 et surver2);
surextb = surext plus (0. 0.);
surint2b = surint2 plus (0. 0.);
elim 0.0001
   (surint2b et surextb et volsol);
*
SI FLAGVISU;
 trac (surint et surext et surint2 et surext2
    et volint1 et volint2 et volsol);
FINSI;
*
*
*
* Modèle de fluide
*
ro1 = 800.;
cf1 = 343.;
*
mod1 = MODE (volint1) liquide lqu4;
mat1 = MATE mod1 rho ro1 rorf ro1
                 cson cf1 cref cf1 lcar 1.D-3 g 0.;
mod2 = MODE (volint2) liquide lqu4;
mat2 = MATE mod2 rho ro1 rorf ro1
                 cson cf1 cref cf1 lcar 1.D-3 g 0.;
rig1 = rigi (mod1 et mod2) (mat1 et mat2);
mas1 = mass (mod1 et mod2) (mat1 et mat2);
*
blfl = bloq UR UT UZ
  (surint et surext2 );
blflI = bloq IUR IUT IUZ
  (surint et surext2 );
blfl2 = bloq UZ
  (surver1 et surver2);
blfl2I = bloq IUZ
  (surver1 et surver2);
*
*
opti elem rac2;
mrac2 = racc 0.0001 (surext)
                   (surextb);
mrac1 = racc 0.0001 (surint2)
                   (surint2b);
*
modrac1 = MODE mrac1 mecanique liquide rac2;
matrac1 = cara modrac1 liqu volint1;
*
modrac2 = MODE mrac2 mecanique liquide rac2;
matrac2 = cara modrac2 liqu volint2;
*
modliq = MODE (volint1 et volint2) liquide lqu4;
matliq = MATE (modliq et modrac1 et modrac2) rho ro1 rorf ro1
                 cson cf1 cref cf1 lcar 1. g 0.;
*
bl1 = bloq UR UT UZ  (surext2 et surint);
bl2ur = bloq UR  (surextb et surint2b);
bl2ut = bloq UT  (surextb et surint2b);
bl2uz = bloq UZ  (surextb et surint2b);
bl1I = bloq IUR IUT IUZ  (surext2 et surint);
bl2Iur = bloq IUR  (surextb et surint2b);
bl2Iut = bloq IUT  (surextb et surint2b);
bl2Iuz = bloq IUZ  (surextb et surint2b);
dep1ur = depi bl2ur  1.;
dep1ut = depi bl2ut -1.;
Ktot = rigi (modrac1 et modrac2 et modliq)
             (matrac1 et matrac2 et matliq) ;
Mtot = mass (modrac1 et modrac2  et modliq)
             (matrac1 et matrac2 et matliq);
*
Mimpe = 'IMPE' Mtot 1. 'MASSE';
Kimpe = 'IMPE' Ktot 'RAIDEUR';
*
SI FLAGLONG;
 prfreq = prog 10 pas 10. 1000.;
SINON;
 prfreq = prog 100 pas 100. 1000.;
FINSI;
*
prFR = prog;
prFT = prog;
repeter lab1 (dime prfreq);
 fr1 = extr prfreq &lab1;
 ImpTot = Kimpe et (((2.*pi*fr1)**2)*Mimpe)
       et bl1 et bl2ur et bl2ut et bl2uz et
   bl1I et bl2Iur et bl2Iut et bl2Iuz et blfl et blflI
   et blfl2 et blfl2I;
 deptot = reso ImpTot (dep1ur et dep1ut);
 reatot = reac deptot (bl2ur et bl2ut);
 prFR = prfr et (prog (maxi (exco (resu reatot) FR)));
 prFT = prft et (prog (maxi (exco (resu reatot) FT)));
fin lab1;
*
evfr = evol manu prfreq prfr;
evft = evol manu prfreq prft;
*
mm = 57.6d-3;
evfrma = evol rouge manu prfreq ((-1.)*((2.*pi*prfreq)**2)*mm);
*
TEST = ABS ((SOMM ((extr evfrma ordo) - (extr evfr ordo)))/
       (SOMM (extr evfrma ordo)));
*
SI FLAGVISU;
 trac (volint1 et volint2) (exco deptot 'P');
 trac (volint1 et volint2) (vecteur reatot 'FR' 'FZ');
 dess (evfr et evfrma);
FINSI;
*
SI (TEST >EG 1.d-2);
 ERRE 4;
FINSI;
*
tab1 = table;
tab1.'CHAMPS_POINTS_SYMETRIQUES' = TABLE;
tab1.'CHAMPS_POINTS_SYMETRIQUES'. 1 = exco deptot 'P' 'SCAL';
tab1.'EFFORTS' = TABLE;
tab1.'EFFORTS'. 1 = reatot;
mesh3d1 = four2tri (modrac1 et modrac2 et modliq)
        tab1 (prog 0. pas 10. 270.) 1;
SI FLAGVISU;
 trac mesh3D1 (tab1.'CHAMPS_POINTS_SYME_3D'. 1 );
 trac mesh3D1 (vecteur (tab1.'EFFORTS_3D'. 1 ) FX FY FZ);
FINSI;
*
FIN;
*















