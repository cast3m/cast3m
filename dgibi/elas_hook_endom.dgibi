************************************************************************
* Petit test des operateurs ELAS et HOOK dans les cas des modeles      *
* endommageables avec fourniture d'un MCHAML de variables internes.    *
* On verifie que la matrice de Hooke utilisee tiens bien compte de     *
* l'endommagement.                                                     *
************************************************************************


** Options generales
OPTI 'DIME' 3 'ELEM' 'CUB8' 'ECHO' 0 ;


** Valeur d'endommagement
d1 = 0.4815162342 ;


** Un petit maillage
mail1 = ((DROI 1 (0. 0. 0.) (1. 0. 0.)) TRAN 1 (0. 1. 0.))
        VOLU 'TRAN' 1 (0. 0. 1.) ;


** Modeles et parametres materiaux (on boucle sur les modeles avec
*  endommagement geres par ELAS et HOOK, sauf le FLUAGE CERAMIQUE)
ct = EVOL 'MANU' (PROG 0. 1. 2.) (PROG 0. 1. 2.) ;
REPE b1 6 ;
  MESS ;
  MESS '---------------------------------------------------------' ;
  icas = &b1 ;
  SI (EGA icas 1) ;
    MESS 'Modele PLASTIQUE ENDOMMAGEABLE' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'ENDOMMAGEABLE';
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'TRAC' ct 'EPSD' 1. 'DC' 1.
                 'EPSR' 1. ;
    mot1 = MOT 'VHWD' ;
  FINSI ;
  SI (EGA icas 2) ;
    MESS 'Modele VISCOPLASTIQUE VISCODOMMAGE' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'VISCOPLASTIQUE'
                                            'VISCODOMMAGE' ;
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'N' 1. 'M' 1. 'KK' 1. 'ALP1' 1.
                 'BLP1' 1. 'R' 1. 'A' 1. 'EVOL' 1. ;
    mot1 = MOT 'VHWD' ;
  FINSI ;
  SI (EGA icas 3) ;
    MESS 'Modele ENDOMMAGEMENT MAZARS' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'ENDOMMAGEMENT' 'MAZARS' ;
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'BTRA' 1. 'KTR0' 1. 'ATRA' 1.
                 'ACOM' 1. 'BCOM' 1. 'BETA' 1. ;
    mot1 = MOT 'D' ;
  FINSI ;
  SI (EGA icas 4) ;
    MESS 'Modele ENDOMMAGEMENT MVM' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'ENDOMMAGEMENT' 'MVM' ;
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'B1' 1. 'B2' 1. 'RATI' 1. 'LOI' 1. ;
    mot1 = MOT 'D' ;
  FINSI ;
  SI (EGA icas 5) ;
    MESS 'Modele PLASTIQUE_ENDOM ROUSSELIER' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE_ENDOM'
                                            'ROUSSELIER' ;
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'TRAC' ct 'F' 0.1 'D' 1. 'SIG1' 1.
                 'FC' 0.9 ;
    mot1 = MOT 'VHOT' ;
  FINSI ;
  SI (EGA icas 6) ;
    MESS 'Modele PLASTIQUE_ENDOM GURSON2' ;
    mo = MODE mail1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE_ENDOM'
                                            'GURSON2' ;
    ma = MATE mo 'YOUN' 1. 'NU' 0.3 'TRAC' ct 'Q' 1. 'FU' 1. 'FF' 1.
                 'FC' 1. 'FNS0' 1. 'FNE0' 1. 'SNS' 1. 'SNE' 1. 'SIGN' 1.
                 'EPSN' 1. 'F0' 0. ;
    mot1 = MOT 'VHWF' ;
  FINSI ;


** Champ de variables internes avec la composante D non nulle
  vi0 = ZERO mo 'VARINTER' ;
  vi1 = vi0 + (MANU 'CHML' mo mot1 d1 'STRESSES') ;
  SI (EGA icas 5) ;
    vi1 = vi0 + (MANU 'CHML' mo mot1 (1. - d1) 'STRESSES') ;
  FINSI ;


** Test du passage deformation --> contrainte avec ELAS et HOOK
  ep0 = ZERO mo 'DEFORMAT' ;
  ep1 = ep0 + (MANU 'CHML' mo 'EPXX' 1. 'STRESSES') ;
  val0 = 1. * (1.-d1) * 1. * (1.-0.3)/((1.+0.3)*(1.-(2.*0.3))) ;
  sg1 = ELAS mo ep1 ma vi1 ;
  val1 = EXTR sg1 'SMXX' 1 1 1 ;
  ec1 = ABS ((val1 - val0) / val0) ;
  ho1 = HOOK mo ma vi1 ;
  sg2 = ho1 * ep1 mo ;
  val2 = EXTR sg2 'SMXX' 1 1 1 ;
  ec2 = ABS ((val2 - val0) / val0) ;


** Test du passage contrainte --> deformation avec ELAS seulement
  sg0 = ZERO mo 'CONTRAIN' ;
  sg1 = sg0 + (MANU 'CHML' mo 'SMXX' 1. 'STRESSES') ;
  val00 = 1. / (1. * (1.-d1)) ;
  ep1 = ELAS mo sg1 ma vi1 ;
  val3 = EXTR ep1 'EPXX' 1 1 1 ;
  ec3 = ABS ((val3 - val00) / val00) ;


** Test des valeurs calculees
  MESS 'Contrainte [theorique]          : ' val0 ;
  MESS 'Contrainte [ELAS] et ecart (%)  : ' val1 ec1 ;
  MESS 'Contrainte [HOOK] et ecart (%)  : ' val2 ec2 ;
  MESS 'Deformation [theorique]         : ' val00 ;
  MESS 'Deformation [ELAS] et ecart (%) : ' val3 ec3 ;
  MESS ;

** Erreur si ecart trop grand
  SI (ec1 > 1.E-15) ;
    MESS '*****  Erreur dans l operateur ELAS lors du calcul de '
         'la matrice de Hooke';
    ERRE 5 ;
  FINSI ;
  SI (ec2 > 1.E-15) ;
    MESS '*****  Erreur dans l operateur HOOK lors du calcul de '
         'la matrice de Hooke';
    ERRE 5 ;
  FINSI ;
  SI (ec3 > 1.E-15) ;
    MESS '*****  Erreur dans l operateur ELAS lors du calcul de '
         'la matrice de Hooke';
    ERRE 5 ;
  FINSI ;
FIN b1 ;

FIN ;



