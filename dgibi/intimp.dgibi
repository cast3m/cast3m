* fichier : intimp.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*******************************************************
* Cas test de l'implantation numérique du modele
* INTIMP permettant de prendre en compte le caractére
* imparfait de l'interface acier/béton dans un calcul
* multifibre (fondée sur l'approche proposée par
* Combescure et Wang)
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
* Benjamin.Richard@lmt.ens-cachan.fr
*******************************************************
* Les cas de charges sont entrés :
* - Traction pour l'acier
* - Cisaillement pour l'interface
*******************************************************

* Mode du test-----------------------------------------
* ncala = 0 : Calcul avec le modele couplé
* ncala = 1 : Calcul seulement avec l'interface
* ncala = 2 : Calcul seulement avec l'acier
graph= mot 'N';
ncala = 0;


* Options de calcul------------------------------------
OPTION DIME 2 ELEM QUA4;

* Nombre d element dans le sens longitudinale----------
nelem = 1;

*-------------- Materiaux------------------------------


* Diametre acier---------------------------------------
phiN  = 0.006;
secN  = 3.14159*((phiN**2)/4.);

* Parametre acier--------------------------------------

* Young acier
ESSS = 200000E6;

* Coefficient Poisson acier
nun  = 0.33;

* Section acier
C1  = secN;

* Endommagement critique
C2  = 0.2;

* Limite d'elasticite
C3  = 400E+6;

* Degre de corrosion
C4  = 0.;

* Exposant d écrouissage
C5  = 2.786;

* Coefficient d'écrouissage
C6  = 500e6;

* Parametre interface----------------------------------

* Module de Coulomb
C7  = 15000e6;

* Fragilité
C8  = 7.5e-5;

* Coefficient a pour l'écrouissage cinématique
C9  = 5.0e-7;

* Coefficient gamma pour l'écrouissage cinématique
C10 = 7.0e9;

* Longueur l'ancrage de l'interface
SI (EGA ncala 0);
   C11 = 0.0475;
SINON;
   C11 = 1.;
FINSI;

* Déformation limite seuil de l'endommagement
C12 = 1.0e-4;

* Degré de corrosion
C13 = C4;

* Maillage acier---------------------------------------
SACIER1 = 0. 0.;
SACIER2 = 1. 0.;
SACIER3 = 1. 1.;
SACIER4 = 0. 1.;

LL1 = D 1 SACIER1 SACIER2;
LL2 = D 1 SACIER2 SACIER3;
LL3 = D 1 SACIER3 SACIER4;
LL4 = D 1 SACIER4 SACIER1;

SACIER  = DALL LL1 LL2 LL3 LL4;

* Modele sur maillage section--------------------------
OPTI DIME 3 ELEM SEG2;

mo1 = mode sacier mecanique elastique plastique intimp
 quas;

ma1 = MATE mo1 YOUN ESSS NU nun
'SOCT' C1
'DCS ' C2
'SOGS' C3
'TCS ' C4
'MS  ' C5
'KS  ' C6
'GCEO' C7
'AD  ' C8
'ACOE' C9
'GAMC' C10
'LCCO' C11
'EPSC' C12
'TCI ' C13
'CALA' ncala;

car1 = CARA mo1 ALPY .66 ALPZ .66;

modstot = mo1;
matstot = ma1 et car1;

* Geometrie de la poutre en 3D-------------------------
OPTI ELEM SEG2;

P1 = 0. 0. 0.;
P2 = 0. 0. 1.;

Lpoutre = d nelem P1 P2;

modpout = MODE Lpoutre MECANIQUE ELASTIQUE SECTION
               PLASTIQUE SECTION TIMO;
matpout = MATE modpout MODS modsTOT MATS matsTOT
               VECT (0 1 0);

* Conditions aux limites-------------------------------
con1 = BLOQ DEPL ROTA         P1;
con3 = BLOQ UZ                P2;
con2 = BLOQ UX UY RX RY RZ P2 ;

* Chargement-------------------------------------------
fo1  = depi con3 1.0;

lt1  = prog 0. 1.;
ld1  = prog 0. 1.;
ev1  = evol manu lt1 ld1;

cha1 = char dimp fo1 ev1;

* Temps calculés---------------------------------------
SI (OU (EGA ncala 0) (EGA ncala 2));
   tcalc = prog 0. pas 0.001 0.20;
SINON;
   tcalc = prog 0. pas 0.00005 0.005;
FINSI;

* Resolution-------------------------------------------
TAB1 = TABLE;
TAB1 . BLOCAGES_MECANIQUES = con1 et con3 et con2;
TAB1 . MODELE              = modpout;
TAB1 . CARACTERISTIQUES    = matpout;
TAB1 . CHARGEMENT          = cha1;
TAB1 . TEMPS_CALCULES      = tcalc;
TAB1 . MAXDEFOR            = 1.E-5;
TAB1 . MOVA                = RIEN;
TAB1 . MAXITERATION        = 100;

PASAPAS TAB1;

* Post traitement--------------------------------------
dep0 = TAB1.'DEPLACEMENTS';
tim0 = tab1. TEMPS;
sig0 = TAB1.'CONTRAINTES';
var0 = TAB1.'VARIABLES_INTERNES';
time = PROG (tim0 . 0);
tabtrac = table;
tabtrac. 'DEPLACEMENTS' = table;
tabtrac. 'VAIS'         = table;
tabtrac. 'VONS'         = table;

uzp0 = PROG 0.0;
fzp0 = PROG 0.0;
lamb = PROG 0.5;
ints = PROG 0.0;
ept0 = PROG 0.0;
ept1 = PROG 0.0;

j = 1;

REPETER LAB1 ((DIME TAB1.DEPLACEMENTS) - 1);

  indj  = j;
  depj  = dep0.indj;
  sigj  = sig0.indj;
  varj  = var0.indj;

  tabtrac. 'DEPLACEMENTS' . 1 =depj;
  tabtrac. 'VAIS' . 1 = exco varj 'VAIS';
  tabtrac. 'VONS' . 1 = exco varj 'VONS';

  mesh3D = pout2mas modpout matpout gauss tabtrac;
  lamj  = tabtrac.vais_3D.1;

  epsj  = epsi depj modpout matpout;
  vecj  = bsigma sigj modpout matpout;
  time  = time et (PROG tim0. &LAB1);
  uzp0  = uzp0 ET (PROG (EXTR depj 'UZ' P2));
  fzp0  = fzp0 ET (PROG (EXTR vecj 'FZ' P2));
  lamb  = lamb et (PROG (EXTR (EXCO lamj XLAM) SCAL
 (NOEUD (35+((j-1)*19)))));
  ints  = ints et (PROG (EXTR (EXCO lamj NA2 ) SCAL
 (NOEUD (35+((j-1)*19)))));
  ept0  = ept0 et (PROG (EXTR (EXCO lamj NA1 ) SCAL
 (NOEUD (35+((j-1)*19)))));
  ept1  = ept1 et (PROG (EXTR (EXCO lamj NA3 ) SCAL
 (NOEUD (35+((j-1)*19)))));

  j = j + 1;

FIN LAB1;

SI (EGA ncala 0);
   evuzfz = evol manu ept1 fzp0;
   evtila = evol manu time lamb;
   evepta = evol manu ept0 ints;
   si ( ega graph 'O');
     dess evuzfz;
     dess evtila;
     dess evepta;
  finsi;
FINSI;

SI (EGA ncala 1);
   evepta = evol manu ept0 ints;
   si ( ega graph 'O');
     dess evepta;
   finsi;
FINSI;

SI (EGA ncala 2);
   evuzfz = evol manu ept1 fzp0;
   si ( ega graph 'O');
     dess evuzfz;
   finsi;
     FINSI;

FIN;








































