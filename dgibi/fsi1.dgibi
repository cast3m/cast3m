* fichier :  fsi1.dgibi
************************************************************************
* Section : Fluides Vibration
************************************************************************
* CAS TEST DU 91/10/16     PROVENANCE : PETI
*           Test fsi1.dgibi: Jeux de données          *
*           ---------------------------------         *
*                                                     *

*******************************************
*    Test fsi1.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************
*                                         *
*             TEST FSI1                   *
*                                         *
*   CYLINDRICAL FLUID CAVITY WITH FREE    *
*                SURFACE                  *
*                                         *
* Calculation of the frequencies of free  *
* surface mode,for m = 1, of cylindrical  *
* water volume of radius 1.43m and height *
* 1.039m                                  *
*                                         *
*                                         *
*      P8___________ P5                   *
*        |          |                     *
*        |          |                     *
*        |          |                     *
*        |          |                     *
*        |__________|                     *
*      P7            P6                   *
*                                         *
*    The boundary conditions are          *
*                                         *
*      dp  |                              *
*     ---- |         =  0                 *
*      dz  | h = 0.                       *
*                                         *
*      dp  |                              *
*     ---- |         =  0                 *
*      dr  | r = R                        *
*******************************************

TEMPS;

OPTI DIME 2 ;
OPTI MODE FOUR 1;
OPTI ELEM QUA4;
OPTI EPSI LINEAIRE;

*=========================================*
*               geometry                  *
*=========================================*

* Dimensions en metres

* Points

P5 = 1.43 1.039  ;
P6 = 1.43 0.0    ;
P7 = 0. 0.        ;
P8 = 0. 1.039    ;

N1 = 10       ;
S4 = P6 D N1 P5 ; S5 =P7 D N1 P6 ;
S6 = P7 D N1 P8 ; S7 =P8 D N1 P5 ;

WATER = DALL S4 S5 S6 S7  PLAN   ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI1 : MAILLAGE';
  TRAC QUAL (WATER ET (0 0));
FINSI;

*=========================================*
* MODE  - materiau - rigidite - masse     *
*=========================================*

MODLIQ1=MODE WATER LIQUIDE LQU4 ;
MODLIQ2=MODE S7    LIQUIDE LSU2 ;

MATLIQ = MATE (MODLIQ1 ET MODLIQ2)
         RHO 1.E3 RORF 1.E3 CSON 1435.
         CREF 1435. LCAR 1   G 10.0 ;

RIG1 = RIGI  (MODLIQ1 ET MODLIQ2) MATLIQ ;
MAS1 = MASS  (MODLIQ1 ET MODLIQ2) MATLIQ ;

*=========================================*
*                boundary conditions      *
*=========================================*

* No explicit boundary condition
* the boundary conditions are natural
*=========================================*
* Theoritical values of frequencies

FRE1 = TABLE;
FRE1.1 = 0.5278;
FRE1.2 = 0.9621;
FRE1.3 = 1.2170;
LIST1 = PROG FRE1.1 FRE1.2 FRE1.3  ;

* Theoritical values of mode shapes

S7PRE   = TABLE ;
S7PRE.1 = prog 0.0 .15755 .31110 .45677
                .59091 .71018 .81166 .89292
                .95212 .98801 1.0000 ;
S7PRE.2 = prog 0.0 .45137 .80878 1.0000
                .99098 .79310 .45945
                7.13120E-02  -.28176
                -.52329 -.60741 ;
S7PRE.3 = prog 0.0 .67298 1.0000 .83272
                .29951 -.27942 -.58584
                -.48679 -9.75875E-02
                .30721 .47269  ;

*=========================================*
*      calculation of the frequencies     *
*                    and                  *
*        extraction of some results       *
*=========================================*

* Use of the operator VIBR. (option PROC)

RESUL = VIBR PROC LIST1 RIG1  MAS1 ;

*=========================================*
*                   results               *
*=========================================*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

FRE2 = TABL;
MOD  = TABL;
DEF  = TABL;
ERG  = TABL;

S70 = CHAN 'POI1' S7 ;

I = 0;
REPETER BLOC1 3;
  I = I + 1;
  FRE2.I = RESUL . MODES . I . FREQUENCE;
  ERG.I = 100 *
        (ABS ((FRE1.I - FRE2.I) / FRE1.I));

  MOD.I = RESUL . MODES . I .  DEFORMEE_MODALE;
  PRE1  = REDU MOD.I S7       ;
  PRE2  = EXCO 'P' PRE1 'P'   ;
  CHP1 = MANU CHPO S70 1 P S7PRE.I ;
  CHP2  = (PRE2 / (MAXI PRE2 ABS));
  CHP3  =  (CHP1 - CHP2 )         ;
  CHP4  =  (CHP1 + CHP2 )         ;

  MESS ' MODE '  I     ;
  MESS ' ----------';
  MESS 'Frequence theorique :' FRE1.I 'Hz';
  MESS 'Frequence calculee  :' FRE2.I 'Hz';
  MESS '    Soit un ecart de : ' ERG.I '%';
  SAUT 1 LIGN;

* mode shape extraction and optional trace

  SI (NEG GRAPH 'N');
     DEF0  = DEFO S7   MOD.I 0.;
     DEF.I = DEFO S7   MOD.I BLEU;
     TRAC (DEF.I ET DEF0);
  FINSI;
FIN BLOC1;

*=========================================*
*                   code validation       *
*=========================================*

ERGMAX = MAXI (PROG ERG.1 ERG.2 ERG.3 );
*
SI (ERGMAX <EG 5.);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;








