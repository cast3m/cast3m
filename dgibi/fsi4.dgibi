
************************************************************************
* Section : Fluides Vibration
* Section : Mecanique Interaction Fluide Structure
************************************************************************
* fichier :  fsi4.dgibi
*           Test Fsi4.dgibi: Jeux de données          *
*           --------------------------------          *
* CAS TEST DU 91/10/04     PROVENANCE : PETI

*******************************************
*    Test fsi4.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************
*                                         *
*                TEST FSI4                *
*                                         *
*   CONCENTRIC SHELLS COUPLED BY FLUID    *
*                                         *
*  Calculation of the frequencies of two  *
* concentric shells with springs coulped  *
* by water in between. The inner radius   *
* is 0.5m and the outer radius is 1.0m    *
*                                         *
*                                         *
*                P5          P7           *
*                 |         |             *
*                 |         |             *
*                 |         |             *
*                 |  fluid  |             *
*                 |         |             *
*                 |         |             *
*                 |         |             *
*                 |         |             *
*          ^^^^^^^ ^^^^^^^^^ ^^^^^^^      *
*            K   P6    K   P8   K         *
*                                         *
*  The boundary conditions are            *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dz  | h = 0. , 1.0                 *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dr  | r = Ri , Ro                  *
*******************************************

TEMPS;

OPTI DIME 2;
OPTI MODE FOUR 1  ;
OPTI ELEM QUA4;
OPTI EPSI LINEAIRE;

*=========================================*
*               geometry                  *
*=========================================*

* Dimensions en mm

* Points
P0 = 0. 0. ;
N1 = 1        ; N2 = 1           ;
P5 = 500.0 1000.0 ; P6 = 500.0 0.0 ;
P7 = 1000. 1000.0 ; P8 = 1000. 0.0 ;

S5 =P5 D N1 P6 ; S6 = P8 D N1 P7 ;

STEEL = S5 ET S6                 ;
WATER = (S5 PLUS (2.5 0.))
          TRAN N2 (495.0 0. )    ;
RAC1  = RACC 3.0   WATER STEEL   ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI4 : MAILLAGE';
  TRAC QUAL (WATER ET P0);
FINSI;

*=========================================*
*    modele - affecte - materiau
*    rigidite - masse                     *
*=========================================*

MODCOQ = MODE STEEL MECANIQUE COQ2 ;
MODLIQ = MODE WATER LIQUIDE   LQU4;
MODRAC = MODE RAC1 MECANIQUE  LIQUIDE RACO;

MATCOQ = MATE MODCOQ RHO 8.E-9  YOUN 2.E5
                     NU 0.3 ;
MATLIQ = MATE (MODLIQ ET MODRAC)
               RHO 1.E-9  RORF 1.E-9
               CSON 1435.0E3 CREF 1435.0E3
               LCAR 1000.    G 0.;
CARCOQ = CARA MODCOQ EPAI 5.0  ;
CARRAC = CARA MODRAC LIQU WATER;
MATCOQ = MATCOQ ET CARCOQ;
MATLIQ = MATLIQ ET CARRAC;

K     = 3.7458E4/2.0;
PPNEW = PROG K ( -1*K) ( -1*K) K;
RIG1  = RIGI  (MODCOQ ET MODLIQ ET MODRAC)
              (MATCOQ ET MATLIQ)   ;
MAS4 = MASS   (MODRAC ET MODLIQ  )(MATLIQ);

*=========================================*
*                boundary conditions      *
*=========================================*

RES1 = BLOQ UZ RT  (S5 ET S6)        ;
RIG2 = (APPU UR UT K P8 ) ET
       (APPU UR UT K P6 ) ;
RIG3 = ( MANU 'RIGI' type 'RIGIDITE'
        (P6 D 1 P8) (MOTS UR)  PPNEW )
    ET ( MANU 'RIGI' type 'RIGIDITE'
        (P6 D 1 P8) (MOTS UT)  PPNEW );
RIG4 = RIG1 ET RIG2 ET RIG3;

* theoretical value  of frequency

FRE1  = TABLE ;
FRE1.1 = 18.88 ;
FRE1.2 = 55.278;
LIST1 = PROG FRE1.1 FRE1.2 ;             ;

*=========================================*
*    calculation of the frequencies       *
*                  and                    *
*      extraction of some results         *
*=========================================*

* Use of the operator VIBR. (option PROC)

RESUL = VIBR PROC LIST1 (RES1 ET RIG4 )  MAS4  ;

*=========================================*
*                   results               *
*=========================================*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

FRE2 = TABL;
MOD  = TABL;
DEF  = TABL;
ERG  = TABL;

I = 0;
REPETER BLOC1 2;
  I = I + 1;
  FRE2.I = RESUL . MODES . I . FREQUENCE;
  ERG.I = 100 *
        (ABS ((FRE1.I - FRE2.I) / FRE1.I));
  MESS ' MODE '  I     ;
  MESS ' ----------';
  MESS 'Frequence theorique :' FRE1.I 'Hz';
  MESS 'Frequence calculee  :' FRE2.I 'Hz';
  MESS '    Soit un ecart de : ' ERG.I '%';
  SAUT 1 LIGN;
FIN BLOC1;

*=========================================*
*                   code validation       *
*=========================================*

ERGMAX = MAXI (PROG ERG.1 ERG.2 );

SI (ERGMAX <EG 5.);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






