* fichier :  comp2.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test Comp2.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;
*______________________________________________________
*                 PLAQUE BI-COUCHE A +/- 45
*                APPUYEE , PRESSION REPARTIE
*                       ELEMENT DKT
*
*
* Une plaque composite carrée est appuyée sur toute sa
* périphérie (blocage en déplacement suivant Z et en
* rotation suivant Z).
*
* Le noeud situé au centre de la plaque ne peut se
* déplacer que suivant l'axe vertical Z (blocage en
* déplacement et en rotation suivant Z).
*
* Une pression répartie est appliquée sur toute
* la plaque.
*
* Le déplacement calculé au centre de la plaque est
* comparé au résultat théorique : 23.25
*______________________________________________________
SAUT PAGE ;
TITRE 'PLAQUE COMPOSITE APPUYEE - PRESSION REPARTIE';
OPTI DIME 3 ELEM TRI3 ECHO 1 ;
*______________________________________________________
*  GEOMETRIE
*______________________________________________________

TEMPS ;

A = 127. ; B = A ; AM = -127. ; BM = AM ;
H = 5.08 ; ES = H/4 ; EI = H/-4 ; S = H/2 ;
P = 0.6894 ;
N = 12 ;
P0 = 0. 0. 0. ;
P1 = AM BM 0. ; P2 = A BM 0. ;
P3 = A B 0.   ; P4 = AM B 0. ;
V1 = 0. (2*B) 0. ; V2 = 0. 0. 1. ;
L1 = DROI N P1 P2 ;

SI1=L1 TRAN V1 N;
S1=ORIE SI1 V2;
CONF (S1 POIN PROC P1) P1 ;
CONF (S1 POIN PROC P2) P2 ;
CONF (S1 POIN PROC P3) P3 ;
CONF (S1 POIN PROC P4) P4 ;
CONF (S1 POIN PROC P0) P0 ;
*
SI (NEG GRAPH 'N') ;
  TRACE S1 V2 'QUAL';
FINSI ;
*______________________________________________________
*  MATERIAUX
*______________________________________________________
MOD1 = MODE S1 MECANIQUE ELASTIQUE ORTHOTROPE DKT;

MOP = 'MODE' S1 'CHARGEMENT' 'PRESSION' DKT ;
MAP = 'PRES' MOP 'PRES' P ;

CARS = CARA MOD1 EPAI S EXCE ES ;
CARI = CARA MOD1 EPAI S EXCE EI ;
E11 = 276E3 ; E22 = 6.9E3 ; N12 = 0.25 ; GX = 3.4E3 ;
MATS = MATE MOD1 DIRE V1 INCLINE  45. V2 YG1 E11 YG2 E22
           NU12 N12 G12 GX ;
MATI = MATE MOD1 DIRE V1 INCLINE -45. V2 YG1 E11 YG2 E22
          NU12 N12 G12 GX ;
MATS=MATS ET CARS;
MATI=MATI ET CARI;
*______________________________________________________
*  CONDITIONS AUX LIMITES ET RIGIDITE
*______________________________________________________
RIS = RIGI MOD1 MATS  ;
RII = RIGI MOD1 MATI  ;
CL=(BLOQ UZ (CONT S1)) ET (BLOQ RZ (CONT S1)) ET
   (BLOQ UX UY RX RY P0);
RIT = RIS ET RII ET CL ;
*______________________________________________________
*  CALCUL ET SORTIE
*______________________________________________________
FP  = BSIG MOP MAP ;
DEP = RESO RIT  FP        ;
FP1 = EXTR DEP  UZ P0     ;
SAUT PAGE          ;
SU1=DEFO S1 DEP    ;
DZ=EXCO DEP UZ     ;
OEIL= 0 1000 0     ;
SI (NEG GRAPH 'N') ;
  TRAC OEIL CACH SU1 DZ;
FINSI ;
*
MESS 'FLECHE THEORIQUE      23.25' ;
SAUT 1 LIGN ;
MESS 'FLECHE CALCULEE           ' FP1 ;
SAUT 2 LIGN ;
TEMPS ;
*______________________________________________________
*            CODE FONCTIONNEMENT
*______________________________________________________
FLEREF = 23.25;
RESI = ABS ((FP1-FLEREF)/FLEREF);
SI (RESI <EG 1E-2);
    ERRE 0 ;
SINO;
    ERRE 5 ;
FINSI;
FIN ;








