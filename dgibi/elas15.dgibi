* fichier :  elas15.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
graph='N';
saut page;
********************************************************************
* Etude d'une poutre a un element, encastree d'un cote
*
*
*  encastrement                           sollicitation
*
*     |-------------------------------------*
*
*     p1                                    p2
*
********************************************************************
*
*  verification de solutions elementaires en flexion et en
*  cisaillement, pour deux orientations des axes locaux Ox et
*  Oz.
*
********************************************************************
*
opti dime 3 elem seg2;
*
* 1) maillage avec 2 orientations de Ox opposees
*
p1 = 0 0 0; p2 = 1 0 0;
d1= p1 d 1 p2;
d2= p2 d 1 p1;
*
* 2) modeles, materiaux et caracteristiques
*
modti1=MODE d1 mecanique elastique timo;
modti2=MODE d2 mecanique elastique timo;
*
mater1=MATE modti1 'YOUN' 1. 'NU' .3;
section=2.6; inery=1.; inerz=1.; torsion=1.;
carac1= 'CARA' modti1 'SECT' section 'INRY' inery 'INRZ' inerz
                      'SECY' section 'SECZ' section 'TORS' torsion;
mc1=carac1 et mater1;
*
mater2=MATE modti2 'YOUN' 1. 'NU' .3;
carac2= 'CARA' modti2 'SECT' section 'INRY' inery 'INRZ' inerz
                      'SECY' section 'SECZ' section 'TORS' torsion;
mc2=carac2 et mater2;
*
* 3) rigidite
*
rigti1=rigi modti1 mc1;
rigti2=rigi modti2 mc2;
*
* 4) etat de flexion 'positif' EN ANGLE en Z
*
* 4.1) solution theorique
*
uty=.5; rtz=1.; motz=1.;
*
* 4.2) definition des blocages et des rigidites totales
*
bloqtot=bloq d1 'UZ' 'RX' 'RY';
bloqp1 =bloq p1 'UX' 'UY' 'RZ';
rigtti1=rigti1 et bloqtot et bloqp1;
rigtti2=rigti2 et bloqtot et bloqp1;
fext=moment 'MZ' 1. p2;
*
* 4.3) resolutions
*
depti1=reso rigtti1 fext;
depti2=reso rigtti2 fext;
*
* 4.4) contraintes
*
sigti1=sigma modti1 mc1 depti1;
sigti2=sigma modti2 mc2 depti2;
*
* 4.5) messages
*
uuy1=extr depti1 'UY' p2; rrz1=extr depti1 'RZ' p2;
uuy2=extr depti2 'UY' p2; rrz2=extr depti2 'RZ' p2;
* WARNING: l'axe local Oz change de signe
mmz1=extr sigti1 'MOMZ' 1 1 1; mmz2=(-1)*(extr sigti2 'MOMZ' 1 1 1);
message 'flexion en Z';
message 'UY  (orient 1) UY (orient 2) Uth' uuy1 uuy2 uty;
message 'RZ  (orient 1) RZ (orient 2) Rth' rrz1 rrz2 rtz;
message 'mz  (orient 1) mz (orient 2) mth' mmz1 mmz2 motz;
*
* 4.6) erreur
*
errr=uuy1 + uuy2 - (2*uty) + rrz1 + rrz2 - (2*rtz) +
     mmz1 + mmz2 - (2*motz);
si ((abs errr) > 1.d-6); erre 5;
sinon;                   erre 0;  finsi;
*
* 5) etat de flexion positif EN ANGLE en Y
*
* 5.1) solution theorique
*
utz=-.5; rty=1.; moty=1.;
*
* 5.2) definition des blocages et des rigidites totales
*
bloqtot=bloq d1 'UY' 'RX' 'RZ';
bloqp1 =bloq p1 'UX' 'UZ' 'RY';
rigtti1=rigti1 et bloqtot et bloqp1;
rigtti2=rigti2 et bloqtot et bloqp1;
fext=moment 'MY' 1. p2;
*
* 5.3) resolutions
*
depti1=reso rigtti1 fext;
depti2=reso rigtti2 fext;
*
* 5.4) contraintes
*
sigti1=sigma modti1 mc1 depti1;
sigti2=sigma modti2 mc2 depti2;
*
* 5.5) messages
*
message 'flexion en Y';
uuz1=extr depti1 'UZ' p2; rry1=extr depti1 'RY' p2;
uuz2=extr depti2 'UZ' p2; rry2=extr depti2 'RY' p2;
mmy1=extr sigti1 'MOMY' 1 1 1; mmy2=extr sigti2 'MOMY' 1 1 1;
message 'UZ  (orient 1) UZ (orient 2) Uth' uuz1 uuz2 utz;
message 'RY  (orient 1) RY (orient 2) Rth' rry1 rry2 rty;
message 'my  (orient 1) my (orient 2) mth' mmy1 mmy2 moty;
*
* 5.6) erreur
*
errr=uuz1 + uuz2 - (2*utz) + rry1 + rry2 - (2*rty) +
     mmy1 + mmy2 - (2*moty);
si ((abs errr) > 1.d-6); erre 5;
sinon;                   erre 0;  finsi;
*
* 6) etat de cisaillement positif en Y
*
* 6.1) solution theorique
*
uty=1.; rtz=0.; fty=1.;
*
* 6.2) definition des blocages et des rigidites totales
*
bloqtot=bloq d1 'UZ' 'RX' 'RY';
bloqp1 =bloq p1 'UX' 'UY' 'RZ';
rigtti1=rigti1 et bloqtot et bloqp1;
rigtti2=rigti2 et bloqtot et bloqp1;
fext=(force 'FY' 1. p2) et (moment 'MZ' -.5 p2);
*
* 6.3) resolutions
*
depti1=reso rigtti1 fext;
depti2=reso rigtti2 fext;
*
* 6.4) contraintes
*
sigti1=sigma modti1 mc1 depti1;
sigti2=sigma modti2 mc2 depti2;
*
* 6.5) messages
*
message 'cisaillement en Y';
uuy1=extr depti1 'UY' p2; rrz1=extr depti1 'RZ' p2;
uuy2=extr depti2 'UY' p2; rrz2=extr depti2 'RZ' p2;
ffy1=extr sigti1 'EFFY' 1 1 1; ffy2=extr sigti2 'EFFY' 1 1 1;
message 'UY  (orient 1) UY (orient 2) Uth' uuy1 uuy2 uty;
message 'RZ  (orient 1) RZ (orient 2) Rth' rrz1 rrz2 rtz;
message 'fy  (orient 1) fy (orient 2) mth' ffy1 ffy2 fty;
*
* 6.6) erreur
*
errr=uuy1 + uuy2 - (2*uty) + rrz1 + rrz2 - (2*rtz) +
     ffy1 + ffy2 - (2*fty);
si ((abs errr) > 1.d-6); erre 5;
sinon;                   erre 0;  finsi;
*
* 7) etat de cisaillement positif en Z
*
* 7.1) solution theorique
*
utz=1.; rty=0.; fty=1.;
*
* 7.2) definition des blocages et des rigidites totales
*
bloqtot=bloq d1 'UY' 'RX' 'RZ';
bloqp1 =bloq p1 'UX' 'UZ' 'RY';
rigtti1=rigti1 et bloqtot et bloqp1;
rigtti2=rigti2 et bloqtot et bloqp1;
fext=(force 'FZ' 1. p2) et (moment 'MY' .5 p2);
*
* 7.3) resolutions
*
depti1=reso rigtti1 fext;
depti2=reso rigtti2 fext;
*
* 7.4) contraintes
*
sigti1=sigma modti1 mc1 depti1;
sigti2=sigma modti2 mc2 depti2;
*
* 7.5) messages
*
message 'cisaillement en Z';
uuz1=extr depti1 'UZ' p2; rry1=extr depti1 'RY' p2;
uuz2=extr depti2 'UZ' p2; rry2=extr depti2 'RY' p2;
* WARNING: l'axe local Oz change de signe
ffz1=extr sigti1 'EFFZ' 1 1 1; ffz2=(-1)*(extr sigti2 'EFFZ' 1 1 1);
message 'UZ  (orient 1) UZ (orient 2) Uth' uuz1 uuz2 utz;
message 'RY  (orient 1) RY (orient 2) Rth' rry1 rry2 rty;
message 'fz  (orient 1) fz (orient 2) mth' ffz1 ffz2 fty;
*
* 7.6) erreur
*
errr=uuz1 + uuz2 - (2*utz) + rry1 + rry2 - (2*rty) +
     ffy1 + ffy2 - (2*fty);
si ((abs errr) > 1.d-6); erre 5;
sinon;                   erre 0;  finsi;
*
fin;







