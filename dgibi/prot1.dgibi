* fichier :  prot1.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
********************************************************************
*                                                                  *
*    Test de la procedure PROT : Projection de Temperature d'un    *
*    massif sur une coque                                          *
*                                                                  *
********************************************************************
*
OPTI ECHO 0;
GRAPH = 'N';
*
OPTI DIME 3 ;
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
* Dimension en metres
*
* Points
*
P1 = -3. -1. 4.; P1E = -3.01 -1.01 4.;
P3 =  3. -1. 4.; P3E =  3.01 -1.01 4.;
P4 = -3.  1. 4.; P4E = -3.01  1.01 4.;
P6 =  3.  1. 4.; P6E =  3.01  1.01 4.;
P01 = 0.  0. 5.215 ;
P02 = 0.  0. 4.995  ;
C0 =  0.  0. 0.;

OPTI ELEM SEG3;
L1 = P1 CERC 10 C0 P3 CERC 4 C0 P6 CERC 10 C0 P4 CERC 4 C0 ;

OPTI ELEM SEG2;

L1L = P1E CERC 12 C0 P3E CERC 5 C0 P6E CERC 12 C0 P4E CERC 5 C0 ;
L2 = P3E CERC 5 C0 P6E  ;
L3 = P1E CERC 5 C0 P4E  ;

L1B = L1L PROJ 'CONI' C0 'SPHE' C0 P01;
L1C = L1L PROJ 'CONI' C0 'SPHE' C0 P02;
L2B = L2 PROJ 'CONI' C0 'SPHE' C0 P01;
L3C = L3 PROJ 'CONI' C0 'SPHE' C0 P02;

*  Creation des objets

OPTI ELEM TRI6 ;
PLAN1 = SURF  L1 SPHERIQUE C0 ;

OPTI ELEM TRI3 ;

PLAN1C = SURF L1C SPHERIQUE C0 ;
PLAN1B = PLAN1C PROJ 'CONI' C0 'SPHE' C0 P01;

OPTI ELEM PRI6 ;
GEOV = PLAN1C VOLU PLAN1B  ;
ELIM 1.E-3 (L1B ET L2B ET GEOV ET L3C);

SI (NEG GRAPH 'N') ;
  trace (PLAN1 ET GEOV);
FINSI ;

* Definition du modele coque
*----------------------------

MO1 = MODELI PLAN1 THERMIQUE COQ6 ;
MO2 = MODELI PLAN1 THERMIQUE COQ6 ;

MO = MO1 ET MO2 ;

* Definition du materiau et de l'epaisseur

MA1 = MATE MO1 EPAI 0.1 EXCE .5E-1 ;
MA2 = MATE MO2 EPAI 0.1 EXCE -.5E-1 ;
MA  = MA1 ET MA2 ;

* Definition du modele massif
*-----------------------------

MOV = MODELI GEOV THERMIQUE  ;

* Definition du materiau et de l'epaisseur

MAV = MATE MOV K 15. 'C' 460. RHO 8000. ;

* Definition des conditions aux limites
*---------------------------------------

CL1 = BLOQ T L2B ;
CL2 = BLOQ T L3C ;

RIT =  CL1 ET CL2;

* Temperature imposee
*---------------------

TEM1 = DEPI CL1 500.;
TEM2 = DEPI CL2 0.;

* Matrice conductivite
*----------------------

CON1 = CONDUCTIVITE MOV MAV ;
CONT = CON1 ET RIT ;

* Second membre
*---------------

TEMT = TEM1 ET TEM2 ;

* Resolution du systeme
*-----------------------

CHT1 = RESO CONT TEMT ;

CHT2 = CHAN CHAM CHT1 GEOV ;

* Projection sur la coque
*-------------------------

CHT31 = CALP MO1 MA1 CHT2 ;
CHT32 = CALP  MO2 MA2 CHT2 ;
CHP31 = CHANGE 'CHPO'  MO1 CHT31;
CHP32 = CHANGE 'CHPO'  MO2 CHT32;

CHT31T = EXCO CHT31 T ;
CHT31TI = EXCO CHT31 TINF ;
CHT31TS = EXCO CHT31 TSUP ;

CHT32T = EXCO CHT32 T ;
CHT32TI = EXCO CHT32 TINF ;
CHT32TS = EXCO CHT32 TSUP ;

SI (NEG GRAPH 'N') ;
  trace CHT32TI MO2 ;
  trace CHT32T MO2 ;
  trace CHT32TS MO2 ;
  trace CHT31TI MO1 ;
  trace CHT31T MO1 ;
  trace CHT31TS MO1 ;
FINSI ;


T31S3 = EXTR CHP31 TSUP P3;
T32I1 = EXTR CHP32 TINF P1;
*
* Les temperatures TINF sur la coque associee au modele MO1 doit
* etre nulle au point P3 ( Condition  au limite), et la temperature
* TSUP sur la coque associee au modele MO2 doit etre egale a 500 en P1
*

DIF = (ABS(T32I1)) + (ABS(T31S3 - 500.)) ;
SI (DIF <EG 5.) ;
  ERRE 0 ;
SINO ;
  ERRE 5 ;
FINSI ;

FIN ;









