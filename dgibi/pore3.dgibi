* fichier :  pore3.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Pore3.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
complet =faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
*******************************************************
*                                                     *
*  TEST PORE3                                         *
*                                                     *
*  CONSOLIDATION UNIDIMENSIONNELLE                    *
*                                                     *
*  REFERENCE : Probleme de Terzaghi                   *
*                                                     *
*      Le milieu est poroélastique                    *
*      On charge à la surface par une pression        *
*      On compare avec la solution analytique         *
*                                                     *
*******************************************************
*
OPTI ECHO 1 ;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;

TITRE   ' TEST PORE3  ' ;
GRAPH = 'N';
OPTIO DIME 2   ELEM QUA8 MODE PLAN DEFO ;
MOTP = MOTS 'P' ;
LREP = PROG 1.E6  ;
OPTI NORM  MOTP LREP ;
*
*=====================================================*
*             geometrie : maillage                    *
*=====================================================*
*
DENS 1. ;
*
H = 10. ;H2 = H * H ;
P1 = 0. 0. ; P2 = 1. 0. ; P3 = 1. 10. ; P4 = 0. 10. ;
LI1= P1 D  1 P2 ;
LI2= P2 D    P3 DINI 1. DFIN 0.1 ;
LI3= P3 D 1 P4 ;
LI4= P4 D    P1 DINI 0.1  DFIN 1. ;
LI = LI1 ET LI2 ET LI3 ET LI4 ;
CADRE = DALLER LI1 LI2 LI3 LI4 PLAN ;
SI (NEG GRAPH 'N');
   TRAC CADRE ;
FINSI;
*
*=====================================================*
*           modele - materiau                         *
*           rigidite - conditions aux limites         *
*=====================================================*
*
MO=MODE CADRE POREUX;
*
LAM = 1.D10 ;
MU = 2.5D9  ;
PK = 1.D-9  ;
*
 MAT=MATE MO   YOUNG  7000D6    NU 0.4    MOB  1.D18
  COB  1.  PERM PK      VISCO 1.
  ALPH 0. KF 1.  RHOF 0. ALPF 0. ALPM 0. RHO 0. ;
BL1=BLOQ UY  LI1 ;
BL2=BLOQ UX (LI2 ET LI4)  ;
BLP = BLOQ P LI3 ;
*
*    on impose une pression sur le dessus
*
P0 = 4.D3  ;
FPP = PRESSION MASS MO  P0  LI3;
*
*=====================================================*
*             consolidation                           *
*=====================================================*
*
PER = PERM MO MAT;
ev = evol manu t ( prog 0 1.D-5 1000) F  ( PROG 0 1 1);
CHA = CHAR 'MECA' FPP EV ;
si complet;
LETEMP= PROG 0 PAS  1.D-2 1.D-1 PAS 1.D-1 1.D0
        PAS 5.D-1 1.D1;
sinon;
letemp = prog 0 PAS  1.D-2 1.D-1 PAS 1.D-1 5.d-1;
finsi;

NBT = DIME LETEMP ;
TFINAL = EXTR LETEMP NBT;

*
*======================================================
*              remplissage de la table
*======================================================
*
TAB1 = TABLE ;
TAB1.'MODELE' = MO;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'BLOCAGES_MECANIQUES' = BL1 ET BL2 ET BLP;
TAB1.'TEMPS_CALCULES' = LETEMP;
TAB1.'CHARGEMENT' = CHA;
TAB1.'CONSOLIDATION' = VRAI;
TAB1.'TETA' = 0.55 ;
TAB1.'PERMEABILITE' = PER ;
*
*  appel a PASAPAS
*
PASAPAS TAB1;
*
*=====================================================*
*             affichage des resultats                 *
*=====================================================*
*
MESS ' RESULTATS TEST PORE3 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS
'  On cherche le deplacement vertical et la pression ';
MESS
'  on compare aux solutions analytiques  ';
SAUT 1 LIGN;
SAUT 1 LIGN;
*
TTA = INDEX ( TAB1.DEPLACEMENTS);
NT = DIME TTA ;
LIST NT;
IT = 0 ;
  REPETER BOPI NT;
    IT = IT + 1 ;
    LET = TTA . IT;
    DD= TAB1.DEPLACEMENTS.LET;
SI ( EGA IT 1 ) ;
   PRT = PROG LET ;
   PRD = PROG ( EXTR DD UY P3);
   EV = EVOL CHPO DD P LI2 ;
SINON;
   PRT = PRT ET (PROG LET) ;
   PRD = PRD ET (PROG ( EXTR DD UY P3)) ;
   EV = EV ET (EVOL CHPO DD P LI2) ;
FINSI;
   FIN BOPI ;
FV = EVOL ROSE MANU 'T' PRT 'UY'  PRD;
*
DD= PECHE TAB1 DEPLACEMENTS ;
EV1= EVOL ROSE CHPO DD P LI2 ;
EVD1= EVOL ROSE CHPO DD UY LI2 ;
*
*=====================================================*
*  determination de la solution analytique
*=====================================================*
*
X = COOR 2 LI2;
XX =  ( MANU CHPO LI2 1 SCAL 1 ) - (X / H) ;
XX = NOMC XX P ;
FACT =    (LAM + ( 2. * MU )) * ( PK / H2 ) ;
FACU = -1. *  ( (LAM + ( 2. * MU )) / ( P0 * H ) ) ;
PI =  3.141592654D0;
PI2 = PI / 2.D0 ;
DEGPI = 180. / PI ;
*
ITT = 0 ;
REPE BOUT NBT;
   ITT = ITT + 1 ;
   T=  EXTR LETEMP ITT ;
   PP = MANU CHPO LI2 1 P 0. ;
   UU = X / H ;
   UU = NOMC UU P ;
   TT = FACT * T;
   N =  0;
si complet ;
nfoi = 50;
sinon;
nfoi = 10;
finsi;
  REPE BOU nfoi;
   M = PI2 * ( (2. * N ) + 1. ) ;
   M2 = M*M ;
   EX = EXP  ( ( -1. * M2) * TT ) ;
   SSI =  SIN ( (M *  DEGPI)  * XX )  ;
   CCO =  COS ( (M *  DEGPI)  * XX )  ;
   PP = PP + ( ( ( 2. / M ) * EX) * SSI) ;
   UU = UU - ( ( ( 2. / M2) * EX) * CCO) ;
   N=N + 1 ;
  FIN BOU ;
   PP = PP * P0 ;
   UU = UU / FACU ;
SI ( EGA ITT 1 ) ;
   PGT = PROG T ;
   PGU = PROG ( EXTR UU 'P' P3) ;
SINON ;
   PGT = PGT ET (PROG T) ;
   PGU = PGU ET (PROG ( EXTR UU 'P' P3)) ;
FINSI;
*
SI ( EGA ITT NBT ) ;
   EV2= EVOL TURQ CHPO PP 'P' LI2;
 SI (NEG GRAPH 'N');
   TITRE ' COMPARAISON DES PROFILS DE PRESSION ' ;
   DESS (EV1 ET EV2 );
 FINSI;
  EVD2= EVOL TURQ CHPO UU 'P' LI2;
 SI (NEG GRAPH 'N');
   TITRE
   ' COMPARAISON DES PROFILS DE DEPLACEMENT VERTICAL';
   DESS (EVD1 ET EVD2 );
 FINSI;
FINSI;

FIN BOUT;
FVT= EVOL TURQ MANU 'T' PGT 'UY'  PGU;
SI (NEG GRAPH 'N');
 DESS ( FV ET FVT) ;
FINSI;
*
*   ecarts
*
EVY1 = EXTR EV1 ORDO ;
EVY2 = EXTR EV2 ORDO ;
EVDY1 = EXTR EVD1 ORDO ;
EVDY2 = EXTR EVD2 ORDO ;
ERPP    =(MAXI ABS ( EVY1 - EVY2   ))/(MAXI ABS EVY2 );
ERDUY   =( MAXI ABS (EVDY1 - EVDY2 ))/(MAXI ABS EVDY2);
*
*
SAUT 1 LIGN;
MESS
'Ecart relatif en pression                       :' ERPP ;
SAUT 1 LIGN;
MESS
'Ecart relatif en profil de deplacement vertical :' ERDUY  ;
*
*
*=====================================================*
*                  code fonctionnement                *
*=====================================================*
*
* L'ecart maximum entre valeurs de reference et
* calculee doit etre inferieur a 6.d-3
*
SAUT 2 LIGNE;
SI (ERDUY   <EG 0.1) ;
    SI (ERPP <EG 0.1) ;
        ERRE 0;
    SINON;
        ERRE 5;
    FINSI;
SINON;
  ERRE 5;
FINSI;
*
SAUT 1 LIGN;
*
FIN ;

















