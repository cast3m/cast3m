* fichier :  ccar7.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************
**
**  --- 10 Juin 2000 ---
**
**   TEST CAVITE CARREE A PAROI DEFILANTE RE=400
**   teste V normale et algo de projection
**
**   teste LAPL KONV (CENTREE) KBBT FPU VNIMP
**   pression continue et discontinue
**   Elements P1-P1 Q1-Q1 2D stables si DT >= DT limite
**   Elements  QUA4
**   Pression continue MSOMMET -> LINE,MACRO,QUAF
**   Pression discontinue CENTREP1 -> MACRO,QUAF
**   Pression discontinue CENTRE   -> MACRO

OPTION ISOV 'SULI' ;
OPTION TRACE X;
 GRAPH = VRAI   ;
 GRAPH = FAUX   ;
COMPLET = FAUX ;
Rey = 400. ;

SI ( COMPLET ) ;

ds1=0.01 ; ds2=0.1 ;
ITMAX  = 20 ;

SINON        ;

err1=1.e-5 ;
ds1=0.03 ; ds2=0.3 ;
ITMAX  = 5  ;

FINSI ;

 KPRES = 'MSOMMET' ;
 KPRES = 'CENTREP1';
 KPRES = 'CENTRE'  ;
 DISCR  = 'LINE'   ;
 DISCR  = 'QUAF'   ;
 DISCR  = 'MACRO'  ;
 KSUPG  = 'CENTREE';


 DEBPROC TEST KPRES*MOT DISCR*MOT GRAPH*LOGIQUE ;

 option dime 2 elem QUA4 ;
*option dime 2 elem TRI3 ;

 p1= 0 0 ; p12=0.5 0. ; p2= 1 0 ;

 ab=p1 d dini ds1 dfin ds2 p12 d dini ds2 dfin ds1 p2 ;

 mt0 = ab trans dini ds1 dfin ds2 (0 0.5)
 trans dini ds2 dfin ds1 (0 0.5) ;

 mt1= ab trans dini ds1 dfin ds2 (0 0.5);

 opti elem TRI4 ;
*opti elem TRI3 ;

 mt2= (cote 3 mt1) trans dini ds2 dfin ds1 (0 0.5) ;
 mt2= inve mt2 ;
 bc=cote 2 mt0; cd=cote 3 mt0; da=cote 4 mt0;
 AA=bc moin (0.5 0.) ;
 BB=ab plus (0. 0.5) ;

 elim (mt0 et mt1 et mt2) 1.e-5 ;

 mt = mt1 et mt2 ;
 elim (AA et BB et mt) 1.e-5 ;

 ctit= chai discr '  ' kpres ;
*Si Graph ; trace mt TITR ctit ; Finsi ;

 cd=cote 3 mt2 ;
 cnt= diff (cont mt) cd ;
*trace cnt ;
 mt1=mt1 coul bleu ;
* trace (mt1 et mt2) ;

 Mcnt=chan cnt QUAF ;
 Mmt= chan mt QUAF ;
 Mcd=chan cd QUAF ;
 Mmt1= chan mt1 QUAF ;

 elim (Mcnt et Mmt et Mcd et Mmt1) 1.e-4 ;

 $mt=mode Mmt 'NAVIER_STOKES' DISCR ;
 $mt1=mode Mmt1 'NAVIER_STOKES' DISCR ;
 $cnt=mode Mcnt 'NAVIER_STOKES' DISCR ;
 $cd=mode Mcd 'NAVIER_STOKES' DISCR ;

 doma $mt 'IMPR' ;

cd=doma $cd maillage ;
CD1= chan CD POI1 ;
*CD = elem CD1 (lect 2 pas 1 ((nbel cd1) - 1) );

cnt=doma $cnt maillage  ;

MU  =1. ;
RO= Rey  ;
DT=1.   ;
YP=5.E-3;
beta =  2.0  2.0   ;

* La cavité est fermée il faut imposer la pression en 1 point !
prep1=doma $mt kpres;
bcp=elem  prep1 POI1 (lect 1 ) ;

   RV= eqex  'OMEGA' 1.    'NITER' 1  ITMA itmax   'FIDT' 1
  'OPTI' 'EF' 'IMPL'   KSUPG  KPRES
  ZONE  $mt    'OPER'  'LAPN'  MU  'INCO' 'UN'
  ZONE  $mt    'OPER'  'KONV' RO 'UNM'     MU DT 'INCO' 'UN'
  ZONE  $mt1   'OPER'  'FROT' KFL beta 'UN'      'INCO' 'UN'
  ZONE  $cnt   'OPER'  'FPU'  RO 'UN' MU 'UET' YP  'INCO' 'UN'
  'OPTI' 'EF' 'BDF2'
  ZONE  $mt    OPER  DFDT RO  'UNM' 'UNMM' DT  'UN' MU INCO 'UN'
  ;

  RV= eqex RV  CLIM
  UN UIMP CD  1.  UN VIMP CD 0.
* UN VIMP cnt 0.  UN UIMP cnt   0.
 ;

 rv.'METHINV'.TYPINV=3 ;
 rv.'METHINV'.IMPINV=0 ;
 rv.'METHINV'.NITMAX=400;
 rv.'METHINV'.PRECOND=3 ;
 rv.'METHINV'.RESID  =1.e-8 ;
 rv. 'METHINV' . 'FCPRECT'=1   ;
 rv. 'METHINV' . 'FCPRECI'=1   ;

    RVP= EQEX
    'OPTI' 'EF' KPRES
    ZONE  $mt    OPER  KBBT  (-1.)     INCO 'UN' 'PRES'
    ZONE  $cnt   OPER  VNIMP $mt (0. ) INCO 'UN' 'PRES'
    CLIM PRES TIMP bcp 0.
    ;

    rvp.'METHINV'.TYPINV=2 ;
    rvp.'METHINV'.IMPINV=0 ;
    rvp.'METHINV'.NITMAX=300;
    rvp.'METHINV'.PRECOND=3 ;
    rvp.'METHINV'.RESID  =1.e-8 ;
    rvp.'METHINV' . 'FCPRECT'=100 ;
    rvp.'METHINV' . 'FCPRECI'=100 ;

    RV.'PROJ'= RVP ;
    RV.'ITMA' = ITMAX ;

rv.inco= table inco ;
rv.inco.'UN' =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.'UNM' =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.'UNMM' =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.'PRES'= kcht $mt scal kpres 0.;
rv.INCO.'KFL'   = kcht $mt1  vect centre (1.  1.E6);

 exec rv ;
 exec rv ;

 AA = chan AA quaf ;
 BB = chan BB quaf ;
 elim (mt et aa et bb ) 1.e-3 ;
 $AA=mode AA 'NAVIER_STOKES' DISCR ;
 $BB=mode BB 'NAVIER_STOKES' DISCR ;
srti=doma $AA 'MAILLAGE'        ;
srth=doma $BB 'MAILLAGE'        ;
evolV = EVOL 'CHPO' (rv.'INCO'.'UN') UX (srti  ) ;
evolH = EVOL 'CHPO' (rv.'INCO'.'UN') UY (srth  ) ;
evx='EXTR' evolV 'ORDO' ;

evy='EXTR' evolV 'ABSC' ;
evolV= evol 'MANU' 'Vitesse' evx 'Hauteur' evy ;
rv.'EVOLV'=evolV ;
rv.'EVOLH'=evolH ;

si graph ;
 ctit= chai discr '  ' kpres ;
TAB1=TABLE;
TAB1.'TITRE'=TABLE ;
TAB1 . 1 ='MARQ      REGU '     ;
TAB1.'TITRE' . 1 = mot 'Composante_UX '     ;
DESS evolV 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1
TITR ctit ;

TAB1.'TITRE' . 1 = mot 'Composante_UY '     ;
DESS evolH 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1
TITR ctit ;

 c1=vect (rv.inco.'UN') 0.3 ux uy jaune ;
 trace c1 mt TITRE ctit;

Si (EGA KPRES 'MSOMMET') ;
mtp= doma $mt 'MMAIL' ;
trace (rv.inco.pression) mtp (cont mtp) ;
Sinon;
p=elno $mt (kcht $mt scal kpres
 (exco (rv.inco.pression) 'PRES'))  kpres;
trace p mt TITRE ctit;
finsi ;

finsi ;

FINPROC RV ;

RV= test 'CENTRE'   'MACRO' GRAPH ;
RV= test 'CENTREP1' 'MACRO' GRAPH ;
RV= test 'MSOMMET'  'MACRO' GRAPH ;
RV= test 'CENTREP1' 'QUAF'  GRAPH ;
RV= test 'MSOMMET'  'QUAF'  GRAPH ;
RV= test 'MSOMMET'  'LINE'  GRAPH ;


FIN ;

















