
************************************************************************
* Section : Mecanique Transitoire
************************************************************************
*
* CAS TEST DU 13/11/14     PROVENANCE : TEST
SAUT PAGE;
*
*******************************************************
*                                                     *
*  TEST ELEMENT COAXIAL COS2 POUR MODELE DE LIAISON   *
*  ACIER-BETONEN REGIME LINEAIRE                      *
*                                                     *
*  TIRANT 3 m SECTION CARRE 0.1 * 0.1                 *
*                                                     *
*                                                     *
*      Le tirant est maille en 3D a l'aide d'elements *
*  massif CUB8. L'acier est maille a l'aide d'elements*
*  barre. Le tirant est soumise sous un chargemet     *
*  monotone.                                          *
*******************************************************
*


OPTI ECHO 0;
***parametres
xp1 = 3.;
xp2 = xp1 + 0.1;
yp1 = 0.1;
yp2 = yp1/2.;
nelx = 100;
nely = 1;
nelz = 1;
*
opti dime 3 elem cub8;
***geometrie et maillage
b1 = 0. 0. 0.;
b2 = xp1 0. 0.;
lib1 = b1 droi nelx  b2 ;
sb1 = lib1 tran nely (0. yp1 0.) ;
vol1 = sb1 volu tran nelz (0. 0. yp1) ;
a1 = 0. yp2 yp2; a2 = xp1 yp2 yp2;
PA1 = (-0.1 yp2 yp2) ;
PA2 = (xp2 yp2 yp2) ;
*
lia1 = droi nelx a1 a2 ;
lia2 = lia1 plus (0. 0. 0.);
*
D1 = DROI 1 PA1 A1 ;
D2 = DROI 1 A2 PA2 ;
ACI = D1 et D2 et LIA1 ;
*
acibet = racc lia1 lia2 1.e-4;
vtot = vol1 et acibet;
***loi d adherence
pulo1 = prog 0. 1.;
pulo2 = prog 0. 1.e11;
*
pulop = evol manu pulo1 pulo2;
*
***materiaux
***acier
yga = 210.e9;
nua = 0.3;
secc1 = 7.854e-5;
secc2 = 7.854e-5;
***beton
ygb = 30.e9;
nub = 0.2;
***model
***acier
moa= model aci mecanique elastique isotrope barre;
maa = mate moa young yga nu nua SECT secc1;
***beton
mob = model vol1 mecanique elastique isotrope;
mab =MATE mob youn ygb nu nub;
***beton acier
moab = model acibet mecanique elastique isotrope plastique
liaison_acbe cos2;
maab = mate moab 'PULO' pulop 'KN' 1e15 'KS' 1.e11 'SECT' secc2 ;
*
motot = moa et mob et moab;
matot = maa et mab et maab;
***conditions aux limites
cl1 = rela accro lia2 vol1;
cl2 = bloqu UZ sb1;
cl3 = bloqu depl PA1;
cl4 = bloqu ux PA2;
cl5 = bloqu uz pa2;
cl6 = bloqu uy pa2;
pp1 = 0. 0. 0.;
pp2 = 1. 0. 0.;
pp3 = 1. 0. yp1;
sy1 = vol1 poin plan pp1 pp2 pp3 1.e-4;
cl7 = bloq uy sy1;
*
cltot = cl1 et cl2 et cl3 et cl4 et cl5 et cl6 et cl7;
***chargement
evo1 = evol manu (prog 0. 1.) (prog 0. 2.e-3);
for1 = depi cl4 1.;
char1 = chargement (for1 ) evo1 dimp;
***table
tab1 = table;
tab1.modele = motot;
tab1.caracteristiques = matot;
tab1.blocages_mecaniques = cltot;
tab1.chargement= char1;
tab1.'PRECISION' = 1.e-5;
tab1.temps_calcules =
prog 0. pas 0.5 1.;
*
pasapas tab1;
*
*********************
***post traitement***
*********************
***force
rr0 = reac cl4 tab1.deplacements.1;
rr1 = resu rr0;
fff = extr rr1 'FX' (poin ( extr rr0 mail ) initial );
*
errf = abs(fff - 33.75e3)/33.75e3;
*
si (> errf 1e-3);
erreur (5);
finsi;

fin;




