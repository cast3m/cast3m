* fichier : rela.dgibi
'OPTI' echo 0 ;
************************************************************************
* NOM         : RELA
* DESCRIPTION : Cas-test elementaire RELA où un des maillages est
*               un point (nouvelle syntaxe du 2019/01)
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stephane GOUNAND (CEA/DEN/DM2S/SEMT/LTA)
*               mel : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 12/02/2019, version initiale
* HISTORIQUE : v1, 12/02/2019, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
interact = faux ;
*
'OPTI' 'DIME' 2 ;
lok = vrai ; tol = '*' ('VALE' 'PREC') 2. ;
*
p0 = 0. 0. ; p1 = 1. 0.; p2 = 1. 1. ;
p3 = 2. 0. ; p4 = 2. 1. ;
p5 = pi pi ;
*
m1 = 'MANU' 'SEG2' p2 p4 ;
m2 = 'MANU' 'TRI3' p0 p1 p2 ;
m3 = 'MANU' 'SEG2' p1 p3 ;
*
mt = m1 'ET' m2 'ET' m3 ;
mtb = mt 'PLUS' (0. 2.) ;
*
* Test 1 : deux maillages complexes
*
alf = '**' 2. 0.5 ;
bet = PI ;
rel = 'RELA' 'T' mtb '-' 'T' mt ;
ch  = 'MANU' 'CHPO' mt  1 'T' alf ;
matd = 'MANU' 'RIGI' ch 'DIAG' ;
chf  = 'MANU' 'CHPO' mt  1 'Q' bet ;
solex = 'MANU' 'CHPO' mt  1 'T' ('/' bet alf) ;
*
mat = matd 'ET' rel ;
smb = chf ;
*
sol = 'RESO' mat smb ;
* On vérifie que les valeurs sur mt et mtb sont les memes egales à PI/2
* et que les multiplicateurs de Lagrange sont nuls.
chb = 'REDU' sol mtb ;
err1 = 'MAXI' ('-' chb (bet '/' alf)) 'ABS' ;
'MESS' ('CHAI' 'err1=' err1 ' tol=' tol) ;
'SI' ('>' err1 tol) ;
   'MESS' ('CHAI' '!!!  err1=' err1 ' > tol=' tol) ;
   lok = lok 'ET' faux ;
'FINS' ;
*
chbb = 'DEDU' 'TRAN' chb mtb mt ;
err2 = 'MAXI' ('-' chbb ('REDU' solex mt)) 'ABS'  ;
'MESS' ('CHAI' 'err2=' err2 ' tol=' tol) ;
'SI' ('>' err2 tol) ;
   'MESS' ('CHAI' '!!!  err2=' err2 ' > tol=' tol) ;
   lok = lok 'ET' faux ;
'FINS' ;
*
err3 = 'MAXI' ('EXCO' 'LX' sol) 'ABS' ;
'MESS' ('CHAI' 'err3=' err3 ' tol=' tol) ;
'SI' ('>' err3 tol) ;
   'MESS' ('CHAI' '!!!  err3=' err3 ' > tol=' tol) ;
   lok = lok 'ET' faux ;
'FINS' ;
*
* Test 2 : un maillage complexe en relation avec un noeud
*
rel2 = 'RELA' 'T' mt '-' 'T' p5 ;
smb2 = 'NOMC' 'Q' ('+' ('COOR' 1 mt) ('*' bet ('COOR' 2 mt))) ;
mat2 = matd 'ET' rel2 ;
sol2 = 'RESO' mat2 smb2 ;
*
vp5 = 'EXTR' sol2 'T' p5 ;
vp5ex = ('MAXI' ('RESU' smb2)) '/' alf '/' ('NBNO' mt) ;
err4 = 'ABS' ('-' vp5 vp5ex) ;
'MESS' ('CHAI' 'err4=' err4 ' tol=' tol) ;
'SI' ('>' err4 tol) ;
   'MESS' ('CHAI' '!!!  err4=' err4 ' > tol=' tol) ;
   lok = lok 'ET' faux ;
'FINS' ;
*
* Test 3 : un mix
*
rel3 = 'RELA' 'T' mt '-' 2. 'T' p5 '+' 'T' mtb ;
mtt  = mt 'ET' mtb ;
smb3 = 'NOMC' 'Q' ('+' ('COOR' 1 mtt) ('*' bet ('COOR' 2 mtt))) ;
cht  = 'MANU' 'CHPO' mtt  1 'T' alf ;
matdt = 'MANU' 'RIGI' cht 'DIAG' ;
mat3 = matdt 'ET' rel3 ;
sol3 = 'RESO' mat3 smb3 ;
*
vp5b = 'EXTR' sol3 'T' p5 ;
vp5exb = ('MAXI' ('RESU' smb3)) '/' alf '/' ('NBNO' mtt) ;
err5 = 'ABS' ('-' vp5b vp5exb) ;
'MESS' ('CHAI' 'err5=' err5 ' tol=' tol) ;
'SI' ('>' err5 tol) ;
   'MESS' ('CHAI' '!!!  err5=' err5 ' > tol=' tol) ;
   lok = lok 'ET' faux ;
'FINS' ;
*
'SAUT' 1 'LIGNE' ;
'SI' lok ;
   'MESSAGE' 'Tout sest bien passe' ;
'SINON' ;
   'MESSAGE' '!!! Il y a eu des erreurs' ;
'FINSI' ;
'SAUT' 1 'LIGNE' ;
*
'SI' interact ;
   'OPTION' 'DONN' 5 'ECHO' 1 ;
'FINSI' ;
'SI' ('NON' lok) ;
   'ERREUR' 5 ;
'FINSI' ;
*
* End of dgibi file RELA
*
'FIN' ;



