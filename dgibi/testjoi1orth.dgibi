* fichier : testjoi1orth.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
opti elem seg2 dime 3 mode trid;
OPTI EPSILON LINEAIRE ;
*****************************
*                           *
*    SOLUTION ANALYTIQUE    *
*                           *
*****************************
      UXANA = 0.007;
      UYANA = -83.5455;
      UZANA = 0.00;
*                           *
      RXANA = 0.00;
      RYANA = 0.00;
      RZANA = -4.175625;
*                           *
*****************************
*
*
*********CREATION DES POUTRES****
*
*****PROPRIETES GEOMETRIQUES*****
*
l1=10.;
lt=30.;
torp = 5;
iyp = 3;
izp = 4;
secp = 0.2;
*
*****PROPRIETES MATERIELLES*****
*
youp = 32;
nup = 0.2;
*
dir1 = 0 1 0;
dir2 = 1 0 0;
*
****POUTRE GAUCHE****
*
pi1 = 0 0 0;
pi2 = l1 0 0;
li = pi1 d 1 pi2;
*
*
****POUTRE DROITE****
*
ps1 = l1 0 0;
ps2 = lt 0 0;
ls = ps1 d 1 ps2;
*
lt = ls et li;
*trac lt;
*
mop = mode lt mecanique elastique poutre;
map = mate mop YOUN youp NU nup
           TORS torp INRY iyp INRZ izp SECT secp;
*
********JOINT*********
*
lj = pi2 d 1 ps1;
moj = mode lj mecanique elastique orthotrope joi1;
maj = mate moj direction dir1 dir2
         KN 30 KS1 40 KS2 30 QN 40 QS1 20 QS2 30;
*
*********************************
*
mo1 = moj et mop;
ma1 = maj et map;
*
cl1 = bloq depl rota pi1;
ri1 = rigi mo1 ma1;
ri2 = ri1 et cl1;
fo1 = force (0. -1. 0.) ps2;
res = reso ri2 fo1;
ss1 = sigma mo1 ma1 res;
ee1 = epsi mo1 ma1 res;
gr1 = grad mo1 ma1 res;
gf1 = graf mo1 ma1 res;
sss = elas mo1 ee1 ma1;
fi1 = bsig mo1 ss1 ma1;
*
*
* ----------    CODE DE FONCTIONNEMENT       ----------
*
*opti donn 5;
DEPA1 = EXTR res UY ps2 ;
RESI = ABS( (DEPA1-UYANA)/UYANA ) ;
list resi;

SI (RESI <EG 3E-3 ) ;
  ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;

FIN;






