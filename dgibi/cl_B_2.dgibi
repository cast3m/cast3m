* fichier :  cl_B_2.dgibi
************************************************************************
* Section : Fluides Permanent
************************************************************************
*C***********************************************************************
*C*                                                                     *
*C* PROJET      : Opérateur CLMI                                        *
*C* NOM         : profil_B_2.dgibi                                      *
*C* DESCRIPTION : Jeu de données pour le calcul d'un écoulement en      *
*C*               fluide visqueux entre 2 plaques                       *
*C* LANGAGE     : Gibiane                                               *
*C* AUTEUR      : Guillaume VENCO - DRN/DMT/SEMT/LTMF                   *
*C*                                                                     *
*C***********************************************************************
*C*                                                                     *
*C* APPELES     : Opérateur CLMI                                        *
*C*                                                                     *
*C***********************************************************************
*C*                                                                     *
*C*                                                                     *
*C***********************************************************************
*C*                                                                     *
*C* VERSION     : 15/05/2000                                            *
*C* CREATION    : 18/02/2000                                            *
*C*                                                                     *
*C***********************************************************************

'OPTION' dime 2 elem seg2 ;
'OPTION'  trace x ;
GRAPH=FAUX;
COMPLET=FAUX;

*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>Maillage et domaine>>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
P1 = 0.1 0.;
p2 =  10. 0. ;
nb1 = 10;
p1p2 = 'DROIT' p1 p2 'DINI' 0.00001 'DFIN' 1. ;
*p1p2 = 'DROIT' p1 p2 20;
mt = p1p2 ;
* 'TRACER'  mt ;

*Définition du modèle
Mmt = 'CHANGER'  mt 'QUAF' ;
Mail = 'CHANGER' Mmt 'POI1';
Pt0 = Mail 'ELEM' 1;
$mt = 'MODELISER'  Mmt 'NAVIER_STOKES' 'LINE' ;

*Données
Ue0 = 0.001;
NU = 1.d-6 ;

*valeurs initiales: valeurs données par Falkner-Skan
D20 =0.66411*((0.1*(NU/Ue0))**(0.5));
D30 = 1.57257*D20;


*Pas de temps et nombre de pas
DT = 0.1;
NBIT = 30;
Si Complet ;
NBIT = 300;
Finsi ;
*Champ de vitesse constant
X = 'COORDONNEE' 1 mt;
UE = 'KCHT' $mt scal 'SOMMET' Ue0;
DUE = 'KCHT' $mt scal 'SOMMET' 0.;

*Coefficients de l'équation
A = 'KCHT' $mt scal sommet 1.;
B = 'KCHT' $mt scal sommet 1.;
SM  = 'KCHT'  $mt scal sommet 1. ;

*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>Création de la table RV et de la table RK>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*               dY         Cf               >>>>>>>>>>>>>>>
*CLMI résout A ---- + B = ---- = SM         >>>>>>>>>>>>>>>>
*               dX          2               >>>>>>>>>>>>>>>>
* Les coef A, B et SM sont calculés dans    >>>>>>>>>>>>>>>>
*        CLMI suivant les cas               >>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

rv = 'EQEX'  'OMEGA' 1.  'NITER' 1  'ITMA' 0   'FIDT' 1
     'OPTI' 'EF' 'IMPL' 'CENTREE' 'BDF2'
     'ZONE' $mt
     'OPER' 'DFDT' 1. 'D2NM' 'D2N2' DT  'INCO' 'D2' ;
*     'OPER' 'DFDT' 1. 'D2NM' DT  'INCO' 'D2' ;

rv = 'EQEX' rv
     'OPTI' 'EF' 'IMPL' 'SUPG'
     'ZONE'  $mt
     'OPER'  'CLMI' 3 1 UE DUE 'D2NM' 'D3NM' 'INCO' 'D2';

rv = 'EQEX' rv
     'OPTI' 'EF' 'IMPL' 'CENTREE' 'BDF2'
     'ZONE' $mt
*    'OPER' 'DFDT' 1. 'D3NM' DT  'INCO' 'D3' ;
     'OPER'  'DFDT' 1. 'D3NM' 'D3N2' DT  'INCO' 'D3';

rv = 'EQEX' rv
     'OPTI' 'EF' 'IMPL' 'SUPG'
     'ZONE'  $mt
     'OPER'  'CLMI' 3 2 UE DUE 'D3NM' 'D2NM' 'INCO' 'D3';

rv = 'EQEX' rv
      'CLIM' 'D2' 'TIMP' pt0 D20
      'CLIM' 'D3' 'TIMP' pt0 D30;


rv.inco = 'TABLE'  inco ;
rv.inco.'D2'=  'KCHT'  $mt scal sommet D20;
rv.inco.'D2NM'=  'KCHT'  $mt scal sommet D20;
rv.inco.'D2N2'=  'KCHT'  $mt scal sommet D20;
rv.inco.'D3'=  'KCHT'  $mt scal sommet D30;
rv.inco.'D3NM'=  'KCHT'  $mt scal sommet D30;
rv.inco.'D3NM'=  'KCHT'  $mt scal sommet D30;
rv.inco.'D3N2'=  'KCHT'  $mt scal sommet D30;

ltps = 'PROG' 0. ;
lteta = 'PROG' 0. ;
TPS = 0. ;
nupt = 0 ;
mts1 = 'DOMA'  $mt 'SOMMET' ;
pt1 = 'POIN'  mts1 2 ;
'LISTE'  pt1 ;

*'OPTION'  donn 5 ;

'REPETER'  BCLT nbit ;
nupt=nupt+1;
tps=tps+dt ;
mess ' Pas de temps DT=' dt ' Temps = ' tps ;
ltps=ltps et (prog tps) ;
EXEC  rv ;
teta='EXTRAIRE'  (rv.inco.'D2') 'SCAL' pt1 ;
mess ' Teta=' teta ;
lteta= lteta et ('PROG' teta);


YNM = rv.'INCO'.'D2NM';
ZNM = rv.'INCO'.'D3NM';
rv.'INCO'.'D2N2' = rv.'INCO'.'D2NM';
rv.'INCO'.'D2NM' = rv.'INCO'.'D2';
rv.'INCO'.'D3N2' = rv.'INCO'.'D3NM';
rv.'INCO'.'D3NM' = rv.'INCO'.'D3';

'FIN'  bclt ;

li4 = 'PROG' 'EXPO' 'A' -1. 'B' 0. ltps;
ev2 = 'EVOL' 'ROUGE' manu ltps li4 ;
evt= evol manu ltps lteta;



*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>> résultats>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*quantité de mouvement
d21 = (rv.inco.'D2');
ev = 'EVOL' 'ROUGE' 'CHPO' d21 mt;
RD2 = (UE*d21)*(1./NU);
ev = 'EVOL' 'ROUGE' 'CHPO' RD2 mt;
*épaisseur d'énergie cinétique
d31= (rv.inco.'D3');
ev10 = 'EVOL' 'ROUGE' 'CHPO' d31 mt;
RD3 = UE*d31/NU;
ev10 = 'EVOL' 'ROUGE' 'CHPO' RD3 mt;
*Epaisseur de déplacement
d11 = (rv.inco.'D1');
RD1 = UE*d11/NU;
evd1 = 'EVOL' 'ROUGE' 'CHPO' RD1 mt;
*évolution de la vitesse extérieure et du gradient
vit = 'EVOL' 'CHPO' UE mt;
gvit = 'EVOL' 'ROUG' 'CHPO' DUE mt;
*évolution du frottement à la paroi
Cf=2*(0.22052D-6)*((YNM*UE)**(-1));
f = 'EVOL' 'ROUGE' 'CHPO' Cf mt;
*évolution du facteur de forme H final
*hev = 'EVOL' 'ROUGE''CHPO' H mt;
*évolution du facteur H32
H32=d31*(d21**(-1));
ev32 = 'EVOL' 'ROUGE' 'CHPO' H32 mt;

*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*solution théorique de FALKNER SKAN>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*caractéristique des solutions de FALKNER-SKAN
cteta=0.66411;
cd1 = 1.72079;
ccf=0.33206;
Hfs=2.5911;
*coefficient de frottement
RX = X*UE*(1./NU);
Cfb = 2*ccf*(RX**(-0.5));
ev3 = 'EVOL' 'BLEU' 'CHPO' Cfb mt;
*quantité de mvt
* Rteta = 0.664(Rx)^0.5
RD2b = (cteta*(RX**0.5));
ev4 =  'EVOL' 'BLEU' 'CHPO' RD2b mt;
*facteur de forme
Hb = 'KCHT' $mt scal sommet Hfs;
ev5 = 'EVOL' 'BLEU' 'CHPO' Hb mt;
*coef H32 théorique
H32b='KCHT' $mt scal sommet 1.57257;
ev11= 'EVOL' 'BLEU' 'CHPO' H32b mt;
*épaisseur de'énergie cinétique
RD3t=H32b*RD2b;
ev12='EVOL' 'BLEU' 'CHPO' RD3t mt;
*épaisseur de déplacement théorique
RD1b = (cd1*(RX**0.5));
evdb =  'EVOL' 'BLEU' 'CHPO' RD1b mt;
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*Courbes>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*'OPTION' trace psc;

Si GRAPH ;
'DESSIN'  (evt) 'TITR' 'Convergence de la valeur au dernier noeud'
           'TITY' 'Valeurs de Y' 'TITX' 'Temps';

'DESSIN' (vit 'ET' gvit)
     'TITRE' 'Vitesse et gradient de vitesse extérieure';

TAB1 = 'TABLE' ;
TAB1.'TITRE' = 'TABLE' ;
TAB1.'TITRE' . 1 = MOT ' Simulation';
TAB1.'TITRE' . 2 = MOT ' Falkner-Skan';

'DESSIN' (f 'ET' ev3) 'TITRE' 'Coefficient de frottement à la paroi'
           'TITX' 'Abscisses'
           'TITY' 'Cf'
           'GRIL'
           'LEGE' TAB1;

'DESSIN' (ev 'ET' ev4) 'TITRE' 'Epaisseur de quantité de mouvement'
       'TITY' 'Rteta'
       'TITX' 'X'
       'GRIL'
       'LEGE' TAB1;

'DESSIN' (ev10 'ET' ev12) 'TITRE' 'Epaisseur d énergie cinétique'
       'TITY' 'Rdelta3'
       'TITX' 'X'
       'GRIL'
       'LEGE' TAB1;

'DESSIN' (evd1 'ET' evdb) 'TITRE' 'Epaisseur de déplacement'
       'TITY' 'Rdelta1'
       'TITX' 'X'
       'GRIL'
       'LEGE' TAB1;

*'DESSIN' (hev 'ET' ev5) 'TITRE' 'Evolution du favteur de forme'
*        'TITX' 'Abscisses'
*        'TITY' 'Cf'
*        'LEGE' TAB1;

'DESSIN' (ev32 'ET' ev11) 'TITRE' 'Evolution du facteur H32'
        'TITX' 'Abscisses'
        'TITY' 'H32'
        'LEGE' TAB1;

Finsi ;
*calcul de l'erreur au sens de la norme L2
diag = 'DOMA' $mt 'XXDIAGSI';
r = (RD2-RD2B)**2;
erl2 = 'SOMT'(diag*r);
'MESSAGE' 'Erreur au sens de la norme L2=';
'LISTE'  erl2;
**************************
*autres verif ************
**************************
w=RD2*(Rx**(-0.5));
wev = 'EVOL' 'ROUGE' 'CHPO' mt w;
wtheo = 'KCHT'$mt 'SCAL' 'SOMMET' cteta;
wtev = 'EVOL' 'BLEU' 'CHPO' mt wtheo;

Si GRAPH ;
'DESSIN' (wev 'ET' wtev) 'TITRE' 'comparaison theo/simul coef 1'
        'TITX' 'Abscisses'
        'TITY' 'Rteta/(Rx**0.5)'
        'LEGE' TAB1;
Finsi ;

*q=(CfKP/2.)*(Rx**(0.5));
*qev = 'EVOL' 'ROUGE' 'CHPO' mt q;
*qtheo = 'KCHT'$mt 'SCAL' 'SOMMET' ccf;
*qtev = 'EVOL' 'BLEU' 'CHPO' mt qtheo;
*'DESSIN' (qev 'ET' qtev) 'TITRE' 'comparaison theo/simul coef 2'
*        'TITX' 'Abscisses'
*        'TITY' '0.5*Cf*(Rx**0.5)'
*        'LEGE' TAB1;
 'FIN';
















