* fichier :  injairAC.dgibi
*
*  Enceinte 2D Axisymetrique
*  Injection d'air en adiabatique
*  Comparaison Castem 2D axi / Cathare / Solution analytique
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
'OPTI' 'MODE' 'AXIS' ;
opti trace 'X';
opti EPTR 10 ;

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

 Tmax=500.;

Si COMPLET;
  DTC=0.;
  n1=5 ;
  n2=10;
  n3=20;
Sinon;
  DTC=2.;
  n1=3;
  n2=3;
  n3=3;
Finsi;

*=======================================================================
* Feuille inox de protection
e4=(2.36-2.3588)/2.;
lb4=17.;
h4i=e4/lb4;
* Isolant
e1=(2.46-2.36)/2.;
lb1=0.025;
h1i=e1/lb1;
* paroi inox Riva
e3=0.02;
lb3=18.;
h3i=e3/lb3;
* Echange externe
hext=10.;
Text=20.;
* H equivalent
Heq=(h4i + h1i + h3i + (1./hext))**(-1.);
mess 'Heq=' Heq;

*=======================================================================
*=======================================================================
* Solution analytique
DEBPROC SOREF Tmax*'FLOTTANT' DT0*'FLOTTANT' QM*'FLOTTANT' Vr*'FLOTTANT'
 Sr*'FLOTTANT' Pnm*'FLOTTANT' tetai*'FLOTTANT' INCOND*'MOT'
 HEXT*'FLOTTANT' TEXT*'FLOTTANT' ;
* Les températures son en °C

Tps=0.;
nbit = 50000;

Si('EGA' INCOND 'AIR');
a1 a2 a3 a4 a5 a6 Cp=calcp tetai ;
Rinc=287.1;
SINON;
Si('EGA' INCOND 'N2');
a1 a2 a3 Cp a5 a6 a7=calcp tetai ;
Rinc=296.9;
SINON;
mess ' Incondensable inconnu';
QUITTER SOREF;
FINSI;
FINSI;
*  Rinc=292.;  Valeur pour faire mieux fiter la solution analytique avec
*  cathare en air.

mess ' Incondensable R=' Rinc;
Rhonm=Pnm/Rinc/(tetai + 273.15);
a=Cp/Rinc-1.;

Lrho= prog Rhonm;
Ltps= prog Tps  ;
Lp  = prog Pnm  ;
LT  = prog tetai;

repeter BLOC nbit;
tps=tps + dt0;
Si (tps > Tmax); quitter Bloc ; Finsi ;
Rhon=Rhonm + (DT0*Qm/Vr);
Fd=(Qm**3.)/(Rhon**2.)/(Vr**(10./3.));

A    = Cp/Rinc - 1.     ;
Alfa = Sr*hext/Rhon/Rinc;
Beta = Sr*hext*(Text + 273.15)     ;
AA = DT0/(Alfa*DT0+A)  ;

Pn = (A*DT0/(A/DT0 + Alfa)*Pnm) + (AA/Vr*Qm*Cp*(tetai + 273.15))
 + (AA*Beta);

**Pn  =Pnm + (DT0*Qm/Vr*(Rinc*Cp)/(Cp-Rinc)*(tetai + 273.15));

Tn  =(Pn/Rhon/Rinc) - 273.15;

Si('EGA' INCOND 'AIR');
a1 a2 a3 a4 a5 a6 Cp=calcp tn;
Finsi;
Si('EGA' INCOND 'N2');
a1 a2 a3 Cp a5 a6 a7=calcp tn;
FINSI;

Lrho= Lrho et (prog Rhon);
Ltps= Ltps et (prog Tps)  ;
Lp  = Lp   et (prog Pn)   ;
LT  = LT   et (prog Tn)   ;
Rhonm = Rhon;
Pnm = pn;

Fin BLOC;

evrho1 = (evol manu ltps lrho) coul rouge;
evp1   = (evol manu ltps lp) coul rouge  ;
evt1   = (evol manu ltps lt) coul rouge  ;
evmair1= evrho1* Vr                      ;
FINPROC evp1 evt1 evrho1 evmair1 ;

DT0=1. ;
Qm=0.2  ;
Vr=17.4;
Sr=35.914;
Pnm=1.e5;
tetai=300. - 273.15;
hext=0.; Text = 0.;
*hext=Heq; Text = 20.;
evp1 evt1 evrho1 evmair1 = SOREF Tmax DT0 QM Vr Sr Pnm tetai 'AIR'
 HEXT TEXT ;


*=======================================================================
Si GRAPH;
TAB1 =TABLE ;
TAB1.'TITRE'=TABLE ;
tab1 . 1 = 'MOT' 'MARQ CROI REGU ';
tab1 . 'TITRE' . 1 = 'Sol Ref';

tit= chai 'Pression enceinte' ;
dess evp1 titr tit
'GRIL' titx 's' tity 'Pa' lege tab1;

tit= chai 'Température enceinte' ;
dess evt1 titr tit
'GRIL' titx 's' tity '°C' lege tab1;

tit = chai 'Densité moyenne enceinte';
dess evrho1 titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;

tit = chai 'Masse d air';
dess evmair1 titr tit
'GRIL' titx 's' tity 'kg' lege tab1;
Finsi;


epsi=1.e-5;

episo=0.044;
db=0.1 ;
R1=1.2 ;
R1=1.2 - episo;
h1=1.605;
h2=2.342 - episo;
hb=0.835;
PA0=R1 0.;
PAb=PA0 moins (0. hb);
PAbm=PAb moins ((R1/2.) 0.);
PA1=R1 h1;
PB1=R1 ((-1.)*h1);
PA1b=PA1 moins (0. hb);
CA1=0. 0.275     ;
CA2=(0.33*R1) H1   ;
PAX2=0. h2;
PA20= PAX2 tour -5. CA1;
PA2= PA20 tour -15. CA1;
p0=0 0 ;
paxb= p0 moins (0. hb);
p2=db 0. ;
p2b=p2 moins (0. hb);
breche = paxb  'DROI' n1 p2b ;
bas= p2b 'DROI' dini (db/n1) dfin (R1/10.) PABM
         'DROI' dini (R1/10.) dfin (db/n1) PAB;
n2= nbel bas;
plaf0 = PAX2 C n1 CA1 PA20;
plaf1 = PA20 C (n2/2) CA1 PA2;
plaf2 = PA1  C (n2/2) CA2 PA2;

plaf  =plaf0 et plaf1 et plaf2;
axeh = PAX2 'DROI' n3 paxb ;
PAXM=(PAX2 plus PAXB)*0.5;

axeh = PAX2 DROI dini (h2/n3/4.) dfin (h1/n3) PAXM
            DROI dini (h1/n3) dfin (h2/n3/4.) PAXB;
n3n= nbel axeh ;
paroih=PAb d n3n PA1;

pla=plaf et paroih ;
pla = pla et (syme pla droit p0 pax2);
Si GRAPH;
trace (pla et ca1 et ca2 et PA1);
trace (plaf et paroih et axeh et breche et bas
et ca1);
Finsi;
mth= 'DALLER' (breche 'ET' bas) paroih plaf axeh;

entb = breche plus (0. 0.);
DEPL entb tour 15. P2b;
pbx2= pax2 syme 'DROITE' P0 PA0;
p0b= poin 1 entb ;
axeb = PBX2 'DROI' n3 p0b;
paroib =pab d n3 pb1;
fond =  plaf SYME 'DROITE' P0 PA0;
elim  (entb 'ET' bas et paroib et fond et axeb) epsi;
mtb= 'DALLER' (entb 'ET' bas) paroib fond axeb;
axe=axeb et axeh;
mt = mth et mtb ;
elim (mtb et bas et entb et bas) epsi;
Parois = paroib et paroih et plaf et fond et entb ;
parext = paroib et paroih et plaf et fond ;
Si Graph;
trace (elim (mt et (syme mt 'DROIT' P0 pax2)) epsi);
Finsi;
************************************************************************
* Petite disgression pour calculer la surface de l'enceinte
Si FAUX;
opti dime 3 ;

la= prog;
ln=prog;
nn=25;
repeter BLOC 40;
a= rota parext nn 90. pax2 pbx2;
sa=((mesu a 'SURF' ) * 4.);
la = la et (prog sa);
ln = ln et (prog nn);
nn= nn + 25;
fin bloc;
evmes= evol manu ln la ;
dess evmes GRIL TITR ' Surface interne de l enceinte RIVA';
*opti donn 5;
Finsi ;
************************************************************************

rxt = 'TABLE' ;
rxt.'vtf'=mt ;
rxt.'axe'   = axe ;
rxt.'breche'=breche ;
rxt.'diru1'=0. 1. ;
rxt.'epsi'= 1.e-4 ;
rxt.'pi'  = 0.5 0.5 ;
rxt.'DT0'  = (maxi (prog DT0 DTC));
rxt.'DETMAT'= VRAI ;
rxt.'RENU'  = 'RIEN' ;
rxt.'DISCR'= 'LINE';
rxt.'KPRE' = 'MSOMMET';
rxt.'MODTURB'  = 'LMEL'  ;
rxt.'LMEL'  = 0.01 ;
rxt.'GRAPH'  = GRAPH ;


 TF0          =  Tetai                                 ;
 rxt.'TF0'    =  TF0                                   ;
 rxt.'PT0'    =        1.0e5                       ;

 Qinc = Qm  ;
 rxt.'scenario'          = table                              ;
 rxt.'scenario'.'t'      = prog   0.0 1000.                   ;
 rxt.'scenario'.'qair'   = prog   Qinc Qinc                   ;
 rxt.'scenario'.'tinj'   = prog   TF0  TF0                    ;

rxt.'GRAPH' = FAUX ;
execrxt  0 rxt ;

Si COMPLET;
rxt.'TBT'.'RTF'.'1TSCA'.'KOPT'.'CMD'=0.;
Finsi;

Diag=rxt.'GEO'.'Diag';
mess ' Volume de RIVA ' (somt diag);
rxt.'GRAPH' = GRAPH ;

nbit = ENTI (Tmax / rxt.'DT0') ;
execrxt  nbit rxt ;

TAB1 =TABLE ;
TAB1.'TITRE'=TABLE ;
tab1 . 1 = 'MOT' 'MARQ CROI REGU ';
tab1 . 2 = 'MOT' 'MARQ CARR REGU TIRC';
tab1 . 3 = 'MOT' 'MARQ LOSA REGU TIRC';
tab1 . 4 = 'MOT' 'MARQ TRIU REGU TIRC';
tab1 . 5 = 'MOT' 'MARQ TRID REGU TIRC';
tab1 . 'TITRE' . 1 = 'Sol Ref';
tab1 . 'TITRE' . 2 = 'Nautilus CFD';
*.................................................................
evrho2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'Rhomn');
evp2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'PT');
evt2= evol manu (rxt.'TIC'.'LTPS') (rxt.'TIC'. 'Tfm');
evmair2=evrho2 * Vr;

Si GRAPH;
tit= chai 'Pression enceinte' ;
dess (evp1 et evp2) titr tit
'GRIL' titx 's' tity 'Pa' lege tab1;

tit= chai 'Température moyenne enceinte';
dess (evt1 et evt2) titr tit
'GRIL' titx 's' tity '°C' lege tab1;

tit= chai 'Densité moyenne enceinte ' ;
dess (evrho1 et evrho2) titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;

tit= chai 'Masse d air '  ;
dess (evmair1 et evmair2) titr tit
'GRIL' titx 's' tity 'kg/m3' lege tab1;
Finsi;

devp=evp1 - evp2 abs;
erp= maxi (extr devp 'ORDO');
mess ' ERP =' ERP;
 Si (ERP '>' 5000.  ) ; mess ' Problème avec dr = ... + DPDT';
* erreur 5 ;
   Finsi ;

devt=evt1 - evt2 abs;
ert= maxi (extr devt 'ORDO');
mess ' ERT =' ERT;
 Si (ERT '>' 1.2  ) ;
* erreur 5 ; mess 'Problème avec dr = ... + DPDT';
 Finsi ;


 FIN ;






