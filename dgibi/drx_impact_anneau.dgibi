* fichier :  drx_impact_anneau.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Elastique
************************************************************************
* chute et rebond d'un anneau dans un cone rigide
* calcul drexus explicite avec impact ligne-ligne
*
* hypotheses : - materiau elastique lineaire
*              - grandes deformations
*
*
*
*                *******
*               *       *
*         *     *       *     *
*          *    *       *    *
*           *    *******    *
*            *      |      *
*             *     |     *
*              *    v    *
*               *       *
*                *     *
*                 *   *
*                  * *
*                   *
*
*
graph= faux;
opti dime 2 elem seg2 mode plan cont;

* maillage

pc = 0. 10. ; p1 = 0 0 ;
l1 = lign rota 60 pc p1 360. ;
elim l1 .1 ;

tg20 = (sin 20.) / (cos 20.) ;
e = 10 - (10.1/(sin 20.)) ;
p2 = 0  e ; p3 = ((20 - e) * tg20) 20 ;
p4 = (-1*(20 - e) * tg20) 20 ;
l2 = p3 d 1 p2 d 1 p4;
tout = l1 et l2 ;

* donnees drexus

mod1 = mode l1 mecanique coq2 ;
mat1 = mate mod1 youn 100 nu 0. rho .01 epai 1. dim3 1. ;
vit0 = manu chpo l1 3 ux 0 uy -4 rz 0.;

etab = table;
etab . modele = mod1 ;
etab . grandes_deformations = vrai ;
etab . caracteristiques = mat1;
etab . vitesse_initiale = vit0;
etab . frequence_sortie = 20 ;
etab . pas_temps = 2e-3 ;
etab . npasmax = 2000;
etab . impact = table;
etab . impact . maitre = l2 ;
etab . impact . esclave = l1 ;

drexus etab ;

* post-traitement

i = 1 ;
lt = prog 0.;
lz = prog 0. ;

repeter bou1 ( (dime etab . deplacements ) - 1) ;
  lt = lt et ( prog etab . temps . i ) ;
  dep1 = etab . deplacements . i;
  def1 = defo tout dep1 1;
  si (ega i 1);
    def = def1;
  sinon;
    def = def et def1;
  finsi;
  lz1=extr dep1 p1 UY;
  lz = lz et ( prog lz1 );
  i = i + 1 ;
fin bou1 ;

evdz = evol bleu  manu 'Temps' lt 'Dy' lz  ;
si  graph ;
dess evdz;

trac def anime;
finsi;

* test de fonctionnement

err1 = 1. + (lz1 / 15.902) ;
mess 'Erreur sur la force ' (err1 * 100. ) '%' ;
lerr1 = err1 >eg 0.05 ;

si lerr1 ;
  mess 'Erreur dans le cas test impact_anneau' ;
  erreur 5 ;
finsi ;


fin;







