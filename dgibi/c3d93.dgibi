* fichier :  c3d93.dgibi
************************************************************************
* Section : Magnetodynamique Magnetodynamique
************************************************************************

**************************************************************
*        3D MAGNETIC FIELD COMPUTATION  2 POTENTIALS METHOD
*
*          REDUCED POTENTIAL  (VOLUME CONTAINING INDUCTORS)
*          TOTAL POTENTIAL    (VOLUME WHITHOUT  CURRENTS  )
*
**************************************************************
option echo 1 ;
    TYPEL = 2 ;
GRAPH = N ;
   si ( ega typel 2 ) ;
 OPTION DIME 2  ELEM  QUA8  COUL VERT echo 0 ;
   sinon ;
 OPTION DIME 2  ELEM  QUA4  COUL VERT echo 0 ;
   finsi ;
*---------------------------MESH -----------------------------------
 R1 =20. ; R2= 25. ; R3= 27. ; R4= 29. ; R5=39.;R6 = 130.;
 Z1= 2.5;  Z2=  5. ; Z3= 20. ; Z4= 130.;Z31=30.;Z32=50.;
 DI1= 1.; DI2=5.;DI3=60. ;DI4= 10.;NET2= 6;

  DENS DI1 ;
 OZ3= 0.  Z3; OZ2= 0.  Z2;OZ1= 0.  Z1;OZ4=0. Z4;
 OO= 0.  0.;R0Z0= DI1  0 ;
 R1Z0=  R1  0. ;R2Z0= R2  0. ;R3Z0 = R3  0. ;R4Z0 = R4  0.;
 R0Z2= DI1 Z2 ; R4Z2 = R4 Z2 ;
  DENS DI2 ;
 R5Z0=  R5  0. ;R5Z3= R5 Z3 ;R0Z3 =DI2 Z3 ;OZ3 = 0. Z3 ;
  DENS DI3 ;
 R6Z0=R6  0.; R6Z4=R6 Z4 ;
  DENS 10 ;
 R2Z3= R2  Z3 ; R1Z3= R1  Z3; R4Z1 = R4   Z1;
 R3Z1= R3  Z1 ;
 DENSITE DI4 ;
 R0Z4 = DI4 Z4 ; OZ4 = 0. Z4 ;
 NTRA= 1;
 FLAN1 =( D OO R0Z0 D R1Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;
 BOBI  =( D R1Z0 R2Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
                                                     COUL BLEU ;
 FLAN2 =(INVE( BOBI COTE 3)) TRAN DINI DI1 DFIN DI1
                            ( OZ2 MOINS OZ1) COUL VERT ;
 FLAN3 =( D R2Z0 R3Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;
 FLAN4 =( D R3Z0 R4Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;
  FER = (D OZ2 R0Z2 D R4Z2 D 6 R4Z0 D R5Z0 D R5Z3 D R0Z3 D OZ3 D OZ2 )
  SURF PLANE COUL ROUG ;

 FLAN5 = ( D   R5Z0 R5Z3  D R6Z4 D R6Z0 D R5Z0 ) SURF PLANE ;
*
 FLAN6 =( D R0Z3  R5Z3 D R6Z4  D R0Z4 D R0Z3 ) SURF PLANE
                        COUL VERT ;
 TUB6 = (D OZ3 R0Z3 D R0Z4 D OZ4 D OZ3) SURF PLANE COUL VERT ;
 AIREXT =( FLAN6 ET TUB6 ET FLAN5 ) coul  blan  ;
 AIRIN  =(FLAN1 ET FLAN2 ET FLAN3 ET BOBI ET FLAN4) coul vert   ;
 TFLAN = AIRIN ET FER ET AIREXT ;ELIM .1 TFLAN ;
 PTAX = TFLAN POINT DROITE OO  ( 0 1. ) .01 ;
 TU1 = TFLAN ELEM APPU LARG PTAX ;
 TOUT = DIFF TFLAN TU1 ;
*
 C1 = (TOUT CONT ) 'ELEM' 'COMP' R0Z4 R0Z0 ;
 C2 = C1 PROJETER (1 0 ) DROITE OO   OZ4  ;
 OZ4 = C2 POINT PROCHE OZ4 ;
 OO = C2 POINT PROCHE OO ;
 TU1 = DALLER C1 ( D 1 R0Z0 OO ) C2 (D OZ4 R0Z4 ) PLAN ;
 TUA = (COOR 2 TU1) POINT SUPER Z3 ;
 TUA = (TU1 ELEM APPU LARG TUA ) COUL BLAN ;
 TUB = (COOR 2 TU1) POINT INFER Z2 ;
 TUB= (TU1 ELEM APPU LARG TUB ) COUL  VERT  ;
 TUF = DIFF TU1 ( TUA ET TUB ) COUL ROUG ;
 AIRIN = (TOUT ELEM VERT ) ET TUB ;
 FER = (TOUT ELEM ROUG ) ET TUF ;
 AIREXT = ( TOUT ELEM BLAN) ET TUA ;
*  TRAC (AIRIN ET FER ET AIREXT )  FACE ;
*-----------
*------------------ SHIFT TO  3D --------------------------------
*------------
 OPTION ELEM CUB8 ;
 SI ( EGA TYPEL 2 )
 OPTION DIME 3 ELEM CU20 ;
 FINSI ;
 ANG = 20. ;
*
*-------------  DEFINITION OF THE 2 DOMAINS -----------

*------  REDUCED POTENTIAL  VOLUME --------------------------
 DPHI = AIRIN VOLU 1 ROTA ANG (0 0 0 ) (0 1 0 ) ;

**------ TOTAL   POTENTIAL  VOLUME --------------------------
 DPSI = (AIREXT ET FER) VOLU 1 ROTA ANG (0 0 0 ) (0 1 0 ) ;
*
  ELIM .001  DPHI ; DPHI = REGE DPHI ;
  ELIM .001  DPSI ; DPSI = REGE DPSI ;
  ELIM .001  ( DPHI ET DPSI) ;

*---- ---------SEPARATION SURFACE  IDENTIFICATION -------------
* ----------  BELONGS TO REDUCED POTENTIAL  VOLUME ---------------
 SEP_PHI = INTE ( DPSI ENVEL ) ( DPHI ENVEL ) ;
*-------------  SPLITTING OF THE 2 DOMAINS ------------------
 DPSI = DPSI PLUS ( 0 0 0 ) ;
 SEP_PSI = SEP_PHI PLUS ( 0 0 0 ) ; ELIM .01 DPSI  SEP_PSI ;
*--------------------IRON-------------------------------------
 DFER = DPSI  ELEM ROUG ;
*
 VOL1 = DPHI ET DPSI ;
 DAIR = DPSI ELEM BLAN  ;
 AA = DPHI POINT PLAN (0 0 0 ) ( 10 0 0 ) (0 0 10 ) .00001 ;
 AB =( DPHI ENVEL ) ELEM APPU STRIC AA ;
 AA = (AB ENVEL ) POINT DROITE  (0 0 0 ) ( 10 0 0 ) .00001 ;
 AA =  ( (AB CONTOUR  ) ELEM APPU STRIC AA );
 deplacer vol1 tourner 90 ( 0 0 0 ) ( 1 0 0 ) ;
*
 ORIG=  SEP_PHI POINT PROCHE (29. 0 0  ) ;
 BANTI= SEP_PHI POINT PLAN (0. 0. 0.) (10. 0. 0.) (0. 10. 0.)  .1 ;
 geomed = (dpsi et dphi ) point plan (0 0 0) ( 1 0 0) (0 1 0 ) .1 ;
 MU0 = 4. * PI * 1.E-4 ;
 RINT = 20. ;REXT =25. ; HAUT = 5. ;  J = 800. ;
 C1 = 0. 0. 0. ; ZP1 = 20. 0. 0. ;ZP2 = 0. 20. 0.  ;
 E  = Z1 * 4  ;
 UNIT = M  ;
   si ( ega UNIT  M) ;
 deplacer vol1  homo  .001  (0. 0. 0.) ;
 MU0 = 4. * PI  * 1.E-7 ;
 RINT = RINT/1000; REXT = REXT/1000 ; HAUT = HAUT/1000 ;
 ZP1 = ZP1/1000;ZP2 = ZP2 / 1000 ;J = J*1.E6 ;
 E = E /1000 ;
  finsi ;
 option echo 1 ;
*-------
*-- BIOT and SAVART COMPUTATION  of Hs  ON REDUCED POTENTIAL DOMAIN
*-------
 BS  = BIOT SEP_PHI  CERC C1 ZP1 ZP2  RINT REXT HAUT J  MU0 ;
 HS = BS / MU0 ;


  SI (NEG  GRAPH 'N')  ;
 OEIL = 1000 1000 500 ;
 TITRE  ' HS ON SEPARATION SURFACE ' ;
 VVVV = VECTEUR BS  1.e-3  'BX' 'BY' 'BZ'  ROUG ;
 TRAC OEIL  SEP_PHI VVVV ;
  FINSI ;
*----------------  FILLING OF A TABLE -----------------------------
*-in this computation  there is only one isotropic material but it
* is shown how to coope with multi-material (even orthotropic )
*------------------------------------------------------------------
 TABB= TABLE ;
*
 FER1 = DFER  ELEM CU20      ;
 FER2 = DFER  ELEM PR15     ;
*
 KEVOL1 = H_B MU0         ;
 KEVOL2 = H_B MU0         ;
 OBFER1 = FER1  MODE THERMIQUE ISOTROPE ;
 OBFER2 = FER2  MODE THERMIQUE ORTHOTROPE  ;
 TABDEF  = TABLE ;
*----------------- isotropic material -----------------
 STN = TABLE ;
 STN.'EV1' = KEVOL1     ;
 TABDEF.OBFER1 = STN ;
*-------------   orthotropic material -------------------
 STN = TABLE ;
 DI1 = (0 1 0 ) ;DI2 = (1 0 1 ) ;
 STN.'EV1' = KEVOL1     ;STN.'DIR1' = DI1     ;
 STN.'EV2' = KEVOL2     ;STN.'DIR2' = DI2     ;
 TABDEF.OBFER2 = STN ;
 TABB.'TABNUSEC'= TABDEF;
*----------------------------------------------------------------
 TABB.'BIOT'=HS  ;
*-----------------    REDUCED POTENTIAL  AREA ----------------
*---
 TABB.'DPHI' = DPHI ;
    imet = 0 ;
      si (ega imet 1 ) ;
 MOD1 = DPHI MODE THERMIQUE ISOTROPE ;
 MAT1 = MATE MOD1  'K' MU0 ;
 RIGC1 = CONDUC MOD1 MAT1 ;
 TABB.'RIGCON' = RIGC1 ;
      finsi ;
*
 TABB.'SEPPHI' = SEP_PHI ;
 TABB.'ORIG' = ORIG ;
 TABB.'MUAIR' = MU0;
*
*-------------------   TOTAL POTENTIAL AREA --------------------
*---------
 TABB.'DPSI' = DPSI ;
 TABB.'AIRPSI' = DPSI ELEM BLAN ;
     si ( ega imet 1 ) ;
 MOD2 = DAIR MODE THERMIQUE ISOTROPE ;
 MAT2 = MATE MOD2  'K' MU0 ;
 RIGC2 = CONDUC MOD2 MAT2 ;
 TABB.'RIGCPSI' = RIGC2 ;
      finsi ;

* TABB.'SEP_PSI' = SEP_PSI ;
*------------------  GENERAL BOUBDARY CONDITIONS ---------
 TABB.'BLOQUE' = BLOQUER 'T' GEOMED ;
*
*------ BOUNDARY CONDITION LINKED TO POTENTIAL JUMP  ------------
*
 TABA = TABLE ;
 STN = TABLE ;TABA.1 = STN ;
 STN.'LGEO' = BANTI  ;STN.'MTYP' = 'TBLOQ' ;
 TABB.'POTSYM'= TABA  ;
 TABB.ISTEP = 1 ;
*-------------------- FIRST STEP  POTENTIALS COMPUTATION ----------
 POT_SCAL TABB     'SOLIN';
*-------------------------------------------------------------------
 SOL1= TABB.'POTENTIEL';
 PREF= 0. 0. 0. ;
*----------- FIELD COMPUTATION         (B) -----------------------
 OBJM= MODE DPHI THERMIQUE ISOTROPE ;
 BA= CHAN CHPO (  GRAD OBJM  SOL1 ) OBJM   ;
 BA=  BA * -1. * MU0 ;
 BM= EXTR BA 'T,Z' OO ;
 option echo 0 ;
 SAUTER  5 LIGNES ;
 MESS '***********************************************************';
 MESS ' CIRCULAR COIL INTERNAL RADIUS'  RINT                'mm  *';
 MESS '*      CROSS SECTION          ' ( REXT - RINT) '*' HAUT  'mm2';
 MESS '*      CURRENT DENSITY        '  J                 'A/mm2 *';
 MESS '***********************************************************';
 BS  = BIOT AA  CERC C1 ZP1 ZP2  RINT REXT HAUT J   MU0 ;
 MESS '*   INDUCTION  ON AXIS  WITHOUT IRON    (BIOT SAVART )';
 BS0= EXTR BS 'BZ' OO ;

 MESS '**************************************************************';
 MESS '*  AMPERE  mufer >> muo  ALL AMPERE*TURNS  IN THE GAP         ';
 MESS '*         GAP  = ' E  'mm                                     ';
 MESS '*       J * EP * HAUT = B/ MU0 * e                            ';
 MESS '**************************************************************';
 BTH = MU0 * J * (REXT - RINT ) * HAUT / E  ; BCAL = BM + BS0 ;
 MESS '**************************************************************';
 MESS ' BIOT  ET SAVART                            '  BS0       ;
 MESS ' TOTAL B   on AXIS ( z=0 )    EXPECTED      '  BTH       ;
 MESS '                              COMPUTED      '  BCAL      ;
*******************************************************************
   SI (NEG GRAPH 'N' ) ;
   TABLEG= TABLE ;TATT = TABLE ;
   TABLEG.1 = 'MARQ CROI' ;TATT.1=  'B_COIL' ;
   TABLEG.2 = 'MARQ PLUS' ;TATT.2=  'B_IRON' ;
   TABLEG.3 = 'MARQ CARR' ;TATT.3=  'B_TOTAL ' ;
   TABLEG.TITRE = TATT  ;
  BRBIO = ( EXCO BS 'BZ')  NOMC 'SCAL' ;
  BMRAY = EXCO (REDU BA AA) 'T,Z';
 EVBI = EVOL ROUG  CHPO BRBIO  'SCAL' AA ;
 EVBM = EVOL VERT  CHPO BMRAY  'SCAL' AA ;
 EVBT = EVOL TURQ  CHPO (BRBIO + BMRAY ) 'SCAL' AA ;
 TITRE ' BZ(R) for Z=0 ' ;
 DESS (EVBI ET EVBM ET EVBT ) LEGE TABLEG TITY TESLAS  ;
   FINSI ;
 RAP = ABS ((BCAL - BTH ) / BTH );
      SI ( RAP  > .01 ) ; ERREUR 5 ; FINSI ;
*-----------------------------------------------------------------
*                    NON LINEAR  COMPUTATION
*-----------------------------------------------------------------
 TABB.SOUSTYPE = THERMIQUE ;
 TABB.CRITERE =1.E-4 ;
 TABB.NITER   =1;
 TABB.'OME'= .99 ;
* ----------2 itÃ©rations only for the test ------------------------
 TABB.ITERMAX=2;
 TABB.NIVEAU =1;
*-----------------------------------------------------------------
  MAG_NLIN  TABB  ;
*-----------------------------------------------------------------
 SOL2 = TABB.'POTENTIEL';
 BA= CHAN CHPO (  GRAD OBJM  SOL2 ) OBJM   ;
 BA=  BA * -1. * MU0 ;
 BM= EXTR BA 'T,Z' OO ;
*---------------
    ITEST = 1 ;
            SI ( EGA ITEST 1 ) ;
 MESS '***********************************************************';
 MESS '       VERIFICATION AU CENTRE                              ';
 MESS ' CONTRIBUTION FER  ATTENDUE  1.9496   CALCULEE ' BM        ;
*****************************************************************
 BAT = 1.9496 ;
*******************************************************************
 RAP = ABS ((BM - BAT ) / BAT );
      SI ( RAP  > .01 ) ; ERREUR 5 ; FINSI ;
*******************************************************************
   FIN ;
           FINSI ;
*------------
*------  CONTINUATION TILL CONVERGENCE OR UP TO 100 ITERATION ---
*----------
 TABB.ITERMAX=100 ;
*------------------------------------------------------------------
 MAG_NLI3  TABB  ;
*------------------------------------------------------------------
 SOL2 = TABB.'POTENTIEL';
 BA= CHAN CHPO (  GRAD OBJM  SOL2 ) OBJM   ;
 BA=  BA * -1. * MU0 ;
 BM= EXTR BA 'T,Z' OO ;
 BCAL = BM + BS0 ;
 MESS '***********************************************************';
 MESS '      CHECK AT CENTER OF THE DEVICE                        ';
 MESS ' EXPECTED IRON CONTRIBUTION   1.9059  COMPUTED '  BM        ;
 MESS ' TOTAL  BZ  COMPUTED AT CENTER                 '  BCAL      ;
 MESS '***********************************************************';
 FIN;



















