* fichier :  enc2dFP.dgibi
*
*  Enceinte 2D Axisymetrique
*  Relachement d'un melange d'hydrogene et d'helium
*  comme avec enc2d.dgibi
*  Ici on teste le bon fonctionnement des fonctions de parois
*  et de la vitesse normale imposée.
*  Testé avec LINE-MSOMMET et MACRO-MSOMMET
*  Les couples QUAF-MSOMMET et QUAF-CENTREP1 ne marchent pas

'OPTI' 'DIME' 2 'ELEM' 'CU20' ;
 'OPTI' 'MODE' 'AXIS' ;

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

 'SI' COMPLET ;
  nbit=100    ;
  nbit=20     ;
  DT0 = 1.    ;
  n1=5 ;
  n2=10;
  n3=20;
 'SINON'      ;
  nbit=20     ;
  DT0 = 1.    ;
  n1=5 ;
  n2=10;
  n3=20;
 'FINSI'      ;


p1=0 0 ;
p11=0.4 0 ;
p2=0.1 0. ;
p3=1 0 ;
p4=1 2;
p5=0 2 ;


breche = p1  'DROI' n1 p2 ;
bas= p2 'DROI' n2 p3 ;
paroid=p3 'DROI' n3 p4 ;
plaf=p4 'DROI' (n1 + n2) p5 ;
axe = p5 'DROI' n3 p1 ;
vtp = bas 'ET' paroid 'ET' plaf ;
mt= 'DALLER' (breche 'ET' bas) paroid plaf axe ;

rxt = 'TABLE' ;
rxt.'vtf'=mt ;
rxt.'axe'   = axe ;
rxt.'breche'=breche ;
rxt.'diru1'=0. 1. ;
rxt.'epsi'= 1.e-4 ;
rxt.'pi'  = 0.5 0.5 ;
rxt.'DT0'  = DT0     ;
rxt.'DETMAT'= VRAI ;
rxt.'RENU'  = 'RIEN' ;
rxt.'DISCR'= 'LINE';
rxt.'DISCR'= 'MACRO';
rxt.'KPRE' = 'MSOMMET';
*rxt.'DISCR'= 'QUAF';
*rxt.'KPRE' = 'CENTREP1';
rxt.'MODTURB'  = 'LMEL'  ;
rxt.'LMEL'  = 0.05  ;
rxt.'FPAROI' = VRAI ;
rxt.'YP'=5.e-3 ;
rxt.'GRAPH'  = GRAPH ;
rxt.'HE'     = VRAI ;
rxt.'H2'     = VRAI ;
rxt.'CO'     = VRAI ;

 rxt.'TF0'    =       20.0                         ;
 rxt.'PT0'    =        1.0e5                       ;
 rxt.'Yhe0'   =        0.0                         ;
 rxt.'Yh20'   =        0.0                         ;
 rxt.'Yco0'   =        0.0                         ;

 rxt.'scenario'          = table                              ;
 rxt.'scenario'.'t'      = prog   0.0 100.0                   ;
 rxt.'scenario'.'qair'   = prog   0.0   0.0                   ;
 rxt.'scenario'.'qhe'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'qh2'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'qco'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'tinj'   = prog  20.0  20.0                   ;

EXECRXT nbit rxt ;

un=rxt.'TIC'.'UN';
une=noel un rxt.'GEO'.'$vtf';
un =elno une rxt.'GEO'.'$vtf';
menvf=rxt.'GEO'.'menvf';
uenv=redu un menvf;
evunv1=evol chpo uenv 'UX' menvf;
evunv2=evol chpo uenv 'UY' menvf;

Si GRAPH; dess (evunv1 et evunv2); Finsi;

Lunv1= extr (lect 1 pas 10 141) (extr evunv1 'ORDO');
Lunv2= extr (lect 1 pas 10 141) (extr evunv2 'ORDO');

list lunv1;
list lunv2;


LRunv1=Prog
 -4.77452E-02  2.58242E-04  3.73809E-03  4.63039E-04  0.18522
  0.86811      0.98377      0.22216     -1.96206E-02 -8.11362E-03
  1.92531E-04 -1.41814E-02 -8.63366E-02 -0.46183     -4.77452E-02;

LRunv2=prog
  0.97878       3.3448       4.1101       4.8215       2.4430
  0.14722      1.73137E-02 -0.12874     -0.45017     -0.12690
 -5.95941E-02 -3.44642E-02  1.34808E-03  3.06570E-02  0.97878;


ERPS1=SOMM( abs (LRunv1 - Lunv1) ) ;
ERPS2=SOMM( abs (LRunv2 - Lunv2) ) ;

Mess 'ERPS1=' ERPS1 'ERPS2=' ERPS2 ;

Si (ERPS1 '>' 5.e-4) ; erreur 5 ; Finsi ;
Si (ERPS2 '>' 5.e-4) ; erreur 5 ; Finsi ;



mess ' Max U ' (maxi un) ' Min U ' (mini un) ;
test = abs ((maxi un) - 1.9    );
mess ' Test =' test;
*Si (test '>' 0.1  ) ; erreur 5 ; Finsi ;

tf =rxt.'TIC'.'TF' ;
mess ' Max Tf' (maxi tf) ' Min Tf' (mini tf) ;
test = abs ((maxi tf) - 29.456 );
mess ' Test =' test;
*Si (test '>' 0.5  ) ; erreur 5 ; Finsi ;



$vtf=rxt.'GEO'.'$vtf' ;
vtf=doma $vtf maillage;
rho=rxt.'TIC'.'RHO' ;
rhe=rxt.'TIC'.'RHE';
rh2=rxt.'TIC'.'RH2';
rco=rxt.'TIC'.'RCO';
un =rxt.'TIC'.'UN';

axe = chan line axe;
ixe=inve axe ;
evauz = evol chpo (exco un 'UY') ixe ;
evarh = evol chpo rho ixe ;
evatf = evol chpo tf  ixe ;
evahe = evol chpo rhe ixe ;
evah2 = evol chpo rh2 ixe ;


Si GRAPH ;

 dess evauz 'TITRE' 'Vitesse sur l axe';
 dess evarh 'TITRE' 'Densite melange sur l axe';
 dess evatf 'TITRE' 'Temperature sur l axe';
 dess evahe 'TITRE' 'Densite Helium sur l axe';
 dess evah2 'TITRE' 'Densite Hydrogene l axe';

ung= vect un 0.1 ux uy jaune;
trace ung vtf;
opti isov suli ;
trace rho vtf 'TITRE' ' Rho' ;
trace rhe vtf 'TITRE' ' Rhe' ;
*trace rh2 vtf 'TITRE' ' Rh2' ;
*trace rco vtf 'TITRE' ' Rco' ;
*trace tf  vtf 'TITRE' ' Tf ' ;
Finsi ;

 FIN ;








