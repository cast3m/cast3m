* fichier :  prot.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
********************************************************************
*                                                                  *
*    Test de la procedure PROT : Projection de Temperature d'un    *
*    massif sur une coque                                          *
*                                                                  *
********************************************************************
*
OPTI ECHO 0;
GRAPH ='N';
*
OPTI DIME 3 ;
OPTI ELEM SEG2;
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
* Dimension en metres
*
* Points
*
P1 = -3. -1. 5.; P1B = -3. -1. 5.5; P1C = -3. -1. 4.5;
P2 =  0. -1. 5.; P2B =  0. -1. 5.5; P2C =  0. -1. 4.5;
P3 =  3. -1. 5.; P3B =  3. -1. 5.5; P3C =  3. -1. 4.5;
P4 = -3.  1. 5.; P4B = -3.  1. 5.5; P4C = -3.  1. 4.5;
P5 =  0.  1. 5.; P5B =  0.  1. 5.5; P5C =  0.  1. 4.5;
P6 =  3.  1. 5.; P6B =  3.  1. 5.5; P6C =  3.  1. 4.5;

L1 = P1 D 5 P2 D 3 P5 D 5 P4 D 3  ;
L2 = P6 D 5 P5 D 3 P2 D 5 P3 ;
L3 = P3 D 3 P6  ;
ELIM 1.E-3 (L1 ET L2);
L1B = P1B D 5 P3B D 3 P6B D 5 P4B D 3  ;
L2B = P3B D 3 P6B  ;
ELIM 1.E-3 (L1B ET L2B);
*L1C = P1C D 5 P3C D 3 P6C D 5 P4C D 3 ;
L2C = P1C D 3 P4C  ;
*ELIM 1.E-3 (L1C ET L2C);

*  Creation des objets

OPTI ELEM TRI6 ;
PLAN1 = SURF  L1 PLANE ;
OPTI ELEM QUA8 ;
PLAN2 = SURF (L2 ET L3) PLANE ;

OPTI ELEM TRI3 ;

PLAN1B = SURF L1B PLANE ;
PLAN1C = PLAN1B 'PLUS' (0. 0. -1.) ;
ELIM PLAN1C L2C 0.00001;
*PLAN1C = SURF L1C PLANE ;

GEO  = (PLAN1 ET PLAN2) COUL ROUG ;
ELIM GEO 1.E-7;

OPTI ELEM PRI6 ;

GEOV = PLAN1B VOLU PLAN1C ;

* Definition du modele coque
*----------------------------

MO1 = MODELI PLAN1 THERMIQUE COQ6 ;
MO2 = MODELI PLAN1 THERMIQUE COQ6 ;
MO3 = MODELI PLAN2 THERMIQUE COQ8 ;
MO4 = MODELI PLAN2 THERMIQUE COQ8 ;

MO13 =  MO1 ET MO3 ;
MO24 =  MO2 ET MO4 ;
MO = MO13 ET MO24 ;

* Definition du materiau et de l'epaisseur

MA1 = MATE MO13 EPAI 0.5 EXCE 2.5E-1 ;
MA2 = MATE MO24 EPAI 0.5 EXCE -2.5E-1 ;
MA  = MA1 ET MA2 ;

* Definition du modele massif
*-----------------------------

MOV = MODELI GEOV THERMIQUE  ;

* Definition du materiau et de l'epaisseur

MAV = MATE MOV K 15. 'C' 460. RHO 8000. ;

* Definition des conditions aux limites
*---------------------------------------

CL1 = BLOQ T L2B ;
CL2 = BLOQ T L2C ;

RIT =  CL1 ET CL2;

* Temperature imposee
*---------------------

TEM1 = DEPI CL1 500.;
TEM2 = DEPI CL2 0.;

* Matrice conductivite
*----------------------

CON1 = CONDUCTIVITE MOV MAV ;
CONT = CON1 ET RIT ;

* Second membre
*---------------

TEMT = TEM1 ET TEM2 ;

* Resolution du systeme
*-----------------------

CHT1 = RESO CONT TEMT ;

CHT2 = CHAN CHAM CHT1 GEOV ;

*trace CHT1 (PLAN1B ET PLAN1C) ;

* Projection sur la coque
*-------------------------

*CHT3 = PROI MO MA CHT2 ;

CHT31 = CALP  MO13 MA1 CHT2 ;
CHT32 = CALP  MO24 MA2 CHT2 ;
CHP31 = CHANGE 'CHPO'  MO13 CHT31;
CHP32 = CHANGE 'CHPO'  MO24 CHT32;

CHT31T = EXCO CHT31 T ;
CHT31TI = EXCO CHT31 TINF ;
CHT31TS = EXCO CHT31 TSUP ;
CHT32T = EXCO CHT32 T ;
CHT32TI = EXCO CHT32 TINF ;
CHT32TS = EXCO CHT32 TSUP ;
*
SI (NEG GRAPH 'N') ;
  trace CHT32TI MO13 ;
  trace CHT32T MO13 ;
  trace CHT32TS MO13 ;
  trace CHT31TI MO13 ;
  trace CHT31T MO13 ;
  trace CHT31TS MO13 ;
FINSI ;
*
T31S3 = EXTR CHP31 TSUP P3;
T32I1 = EXTR CHP32 TINF P1;
*
* Les temperatures TINF sur la coque associee au modele MO13 doit
* etre nulle au point P3 ( Condition  au limite), et la temperature
* TSUP sur la coque associee au modele MO24 doit etre egale a 500 en P1
*
DIF = (ABS(T32I1)) + (ABS(T31S3 - 500.)) ;
SI (DIF <EG 1.E-2) ;
  ERRE 0 ;
SINO ;
  ERRE 5 ;
FINSI ;

FIN ;











