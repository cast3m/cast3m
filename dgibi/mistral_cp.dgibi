* fichier :  mistral_cp.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test mistral_cp.dgibi: Jeux de donnees          *
*     ---------------------------------------         *
*                                                     *
  opti echo 0 ;
  opti dime 2 elem qua8 ;
  opti mode plan cont ;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;
*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*  MODELE MISTRAL                                     *
*    ELASTICITE ET PLASTICITE INSTANTANE              *
*                                                     *
*  MAILLAGE:                                          *
*     EPROUVETTE RECTANGULAIRE                        *
*                                                     *
*  CHARGEMENT:                                        *
*     DEPLACEMENT LATERAL IMPOSE MONOTONE CROISSANT   *
*******************************************************
*
* Geometrie
*
* ------------------------------------------
  XX = 1. ; YY = 0.5 ;
* ------------------------------------------
*
   P00 = 0. 0. ; P10 = XX 0.  ;
   P11 = XX YY   ;  P01 = 0. YY  ;
*
   LX0 = droi 1 P00 P10 ;
   LY1 = droi 1 P10 P11 ;
   LX1 = droi 1 P11 P01 ;
   LY0 = droi 1 P01 P00 ;
*
* Maillage
*
   EPROU = dall LX0 LY1 LX1 LY0 ;
*   trac EPROU ;
*
* Modele et materiau
*
   MODL = mode EPROU mecanique elastique orthotrope
                                 viscoplastique mistral ;
* ------------------------------------------------------------
  VEC1 = 1. 1. ;  TETA = 45. ;
  SENSIP1 = -2 ; SENSIP2 = 1 ; ICBASE = 0 ;
  fichier = '../divers/mimatcp_par' ;
  PDILT    E1 E2 E3 NU12 NU23 NU13 MU12 MU23 MU13
  PNBRE PCOHI PECOU PEDIR PRVCE PECRX PDVDI PCROI PINCR
    = @mistpar fichier SENSIP1 SENSIP2 ;
* ------------------------------------------------------------
   MATER = mate MODL
             'YG1 ' E1 'YG2 ' E2 'YG3 ' E3
             'NU12' NU12 'NU23' NU23 'NU13' NU13
             'G12 ' MU12
             'ALP1' 0.     'ALP2' 0.
 'DILT' PDILT 'NBRE' PNBRE 'COHI' PCOHI 'ACOU' PECOU 'EDIR' PEDIR
 'RVCE' PRVCE 'ECRX' PECRX 'DVDI' PDVDI 'CROI' PCROI 'INCR' PINCR
                    'SIP1' SENSIP1  'SIP2' SENSIP2  'IBAS' ICBASE
             'DIRECTION' VEC1 'INCLINE' TETA ;
*
* Conditions aux limites
*
   CLX0 = bloq ux LX0 ;
   CLX1 = bloq ux LX1 ;
   CLY0 = bloq uy LY0 ;
   CLY1 = bloq uy LY1 ;
   CL = CLX0 et CLX1 et CLY0 et CLY1 ;
*
* Chargement
*
* -------------------------------------------------------
   T1 = 100. ; DT1 = 10. ;
   EPSXY1 = 0.01 ;
   TT0 = 300. ; TT1 = TT0 ;
   PHIT0 = 0. ;
* -------------------------------------------------------
   TEMPS = prog 0. T1 ;
   TEMPSCAL = prog 0. pas DT1 T1 ;
*
   DEPX = depi CLX1 YY ;
   DEPY = depi CLY1 XX ;
   EVEPS = evol manu TEMPS (prog 0. EPSXY1) ;
   CHADE = char dimp (DEPX et DEPY) EVEPS ;
*
   TT = manu chpo EPROU 1 'T' 1. ;
   EVTT = evol manu TEMPS (prog TT0 TT1) ;
   CHTT = char 'T' TT EVTT ;
*
   PHI = manu chpo EPROU 1 'FI' 1. ;
   EVFI = evol manu TEMPS (prog 0. 0.) ;
   CHFI = char 'FI' PHI EVFI ;
*
   CHA = CHADE et CHTT et CHFI ;
*
* Valeurs initiales
*
   VINT0 = zero MODL 'VARINTER' ;
   FIT0 = manu chml MODL 'FIT ' PHIT0 type 'SCALAIRE' 'STRESSES' ;
   VINT0 = VINT0 + FIT0 ;
*
* Calcul
*
   TAB = TABLE ;
   TAB.'VARIABLES_INTERNES' = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL ;
   TAB.'CARACTERISTIQUES' = MATER ;
   TAB.'MODELE' = MODL ;
   TAB.'CHARGEMENT' = CHA ;
   TAB.'VARIABLES_INTERNES' . 0 = VINT0 ;
   TAB.'TEMPS_CALCULES' = TEMPSCAL ;
   PASAPAS TAB ;
*
* Traitement des resultats
*
   SIG = TAB.'CONTRAINTES' ;
   DEP = tab.'DEPLACEMENTS' ;
   VI  = tab.'VARIABLES_INTERNES' ;
   NT = dime TEMPSCAL-1 ;
* ----------------
   ERMAX = 0.001 ;
* ----------------
*
   SIGT = SIG.NT ;
   SIGT_PO = chang chpo MODL SIGT ;
   SIGXX = extr SIGT_PO SMXX P00 ;
   SIGYY = extr SIGT_PO SMYY P00 ;
   SIGZZ = extr SIGT_PO SMZZ P00 ;
   SIGXY = extr SIGT_PO SMXY P00 ;
*
   DEPT = DEP.NT ;
   EPSIT = epsi DEPT MODL ;
   EPSIT_PO = chang chpo MODL EPSIT ;
   EPSXX = extr EPSIT_PO EPXX P00 ;
   EPSYY = extr EPSIT_PO EPYY P00 ;
   EPSZZ = extr EPSIT_PO EPZZ P00 ;
   EPSXY = (extr EPSIT_PO GAXY P00)/2. ;
*
   VIT = VI.NT ;
   VIT_PO = chang chpo MODL VIT ;
   EPSP11 = extr VIT_PO EP01 P00 ;
   EPSP22 = extr VIT_PO EP02 P00 ;
   EPSP33 = extr VIT_PO EP03 P00 ;
   EPSP12 = extr VIT_PO EP04 P00 ;
   EPSP13 = extr VIT_PO EP05 P00 ;
   EPSP23 = extr VIT_PO EP06 P00 ;
*
   mess ;
   mess 'SIGXX SIGYY SIGZZ :    ' SIGXX SIGYY SIGZZ ;
   mess 'SIGXY :                ' SIGXY ;
   mess ;
   mess 'EPSXX EPSYY EPSZZ :    ' EPSXX EPSYY EPSZZ ;
   mess 'EPSXY :                ' EPSXY ;
   mess ;
   mess 'EPSP11 EPSP22 EPSP33 : ' EPSP11 EPSP22 EPSP33 ;
   mess 'EPSP12 EPSP13 EPSP23 : ' EPSP12 EPSP13 EPSP23 ;
   mess ;

*   SIGT_XY = exco SIGT 'SMXY' ;
*   trac SIGT_XY MODL ;
*   GAMA_XY = exco EPSIT 'GAXY' ;
*   trac GAMA_XY MODL ;
* ------------------------------------------------------------
   MUXY = 4.E10 ;
* ------------------------------------------------------------
   AA = 400.E6 ; BB = 1.E10 ;
   A0 = AA/(2.*MUXY) ;
   A1 = 1. + (BB/MUXY) ;
   EPSPXY1 = (EPSXY1-A0)/A1 ;
   SIGXY1 = AA + (2.*BB*EPSPXY1) ;
   EPSEXY1 = SIGXY1/(2.*MUXY) ;
* -----------------------------------------------------
*
   ERSIGXY = ABS( (SIGXY/SIGXY1) - 1. ) ;
   EREPSXY = ABS( (EPSXY/EPSXY1) - 1. ) ;
   EREPSPXY = ABS( (EPSP12/EPSPXY1) - 1. ) ;
*
   si (ERSIGXY > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur la contrainte > ' ERMAX ;
    mess  ERSIGXY ;
   finsi ;
   si (EREPSXY > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur la deformation > ' ERMAX ;
    mess EREPSXY ;
   finsi ;
   si (EREPSPXY > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur la deformation plastique > ' ERMAX ;
    mess EREPSPXY ;
    mess ;
   finsi ;
   P_ER = prog ERSIGXY EREPSXY EREPSPXY ;
   ERMA = maxi P_ER ;
   si (ERMA > ERMAX) ;
    ERRE 5 ;
   finsi ;

   fin ;







