* fichier :  aitr_2D.dgibi
************************************************************************
* Section : Fluides Transitoire
* Section : Fluides Advection
************************************************************************

GRAPH = FAUX ;
*
*----------------------- aitr_2D.dgibi
*
* Scénario de type LOVA dans ITER
*
*
ELI1 = 1.D-5 ;
D1   = 6 ;
*GRAPH = VRAI ;
'OPTI' 'DIME' 2 'ELEM' 'QUA8' ;
'OPTI' 'TRAC' 'PSC'           ;
*
*=========
* Maillage
*=========
*
*
*= Points definition
*
*
*-----------------------> Vacuum Vessel
*      Modif pour avoir un raccord avec le LP plus propre
P1  =  5.6 -4.8 ;
P01 =  6.8 -4.1 ;
*                                      P1  = 5.00 -4.80 ;
*                                      P01 = 6.50 -4.30 ;
P2  =  7.3 -3.1 ;
P3  =  6.1 -2.9 ;
P4  =  7.9 -0.3 ;
P02 =  7.9  0.8 ;
P5  =  7.9  1.9 ;
P6  =  7.1  3.4 ;
*                         Modif pour n avoir que des QUA4
P7  =  6.5  4.2 ;
*                                       P7  =  6.3  4.2 ;
*                 Modif pour avoir avec le UP plus propre
P8  =  5.6  4.6 ;
*                                      P8  = 5.60  4.80 ;
P9  =  4.7  4.8 ;
P10 =  4.0  4.2 ;
P11 =  4.0  1.9 ;
P03 =  4.0  0.8 ;
P12 =  4.0 -0.3 ;
P13 =  4.0 -1.3 ;
P14 =  4.3 -2.3 ;
P15 =  4.0 -2.3 ;
P16 =  4.0 -3.4 ;
*
*-----------------------> Lower Port
L1  =  9.8 -5.4 ;
L2  = 10.7 -5.4 ;
L3  = 10.7 -3.4 ;
L4  =  9.8 -3.4 ;
*
*-----------------------> Equatorial Port
E1  = 10.7 -0.3 ;
E2  = 10.7  1.9 ;
*
*-----------------------> Upper Port
U1  = 10.7  5.0 ;
U2  = 10.5  6.1 ;
*
*
*= Lines definition
*
*
*-----------------------> Vacuum Vessel
P01P2  = P01 'DROI' D1 P2  ;
P2P3   = P2  'DROI' D1 P3  ;
P3P4   = 'CERC' (3*D1) P3 (5. 0.) P4 ;
*
*P4P02  = P4  'DROI' D1 P02 ;
*
PI1  =  7.9 -0.05 ;
PI2  =  7.9  0.05 ;
NN2  =  ENTIER (D1/2.) ;
P4PI1  = P4  'DROI' NN2 PI1 ;
PI1PI2 = PI1 'DROI' 4   PI2 ;
PI2P02 = PI2 'DROI' NN2 P02 ;
P4P02  = P4PI1 'ET' PI1PI2 'ET' PI2P02 ;
*
P02P5  = P02 'DROI' D1 P5 ;
P5P7   = 'CER3' (3*D1) P5 P6 P7 ;
P7P8   = P7  'DROI' D1 P8 ;
P8P9   = P8  'DROI' D1 P9 ;
P9P10  = P9  'DROI' D1 P10;
P10P11 = P10 'DROI' (3*D1) P11 ;
P11P03 = P11 'DROI' D1 P03 ;
*
*P03P12 = P03 'DROI' D1 P12 ;
*
PI3  =  4.0  0.05 ;
PI4  =  4.0 -0.05 ;
P03PI3 = P03 'DROI' NN2 PI3 ;
PI3PI4 = PI3 'DROI' 4   PI4 ;
PI4P12 = PI4 'DROI' NN2 P12 ;
P03P12 = P03PI3 'ET' PI3PI4 'ET' PI4P12 ;
*
P12P13 = P12 'DROI' D1 P13 ;
P13P14 = P13 'DROI' (2*D1) P14 ;
*                 Suppression du support de blanket (mur interne)
P14P16 = P14 'DROI' (2*D1) P16 ;
*                                    P14P15 = P14 'DROI' D1 P15 ;
*                                    P15P16 = P15 'DROI' D1 P16 ;
*                   Suppression du support de blanket (ancien 2D)
*                                P13P16 = P13 'DROI' (3*D1) P16 ;
P16P1  = 'CERC' (3*D1) P16 (5. -3.1) P1 ;
P1P01  = 'CERC' D1 P1 (5. -3.1) P01 ;
*
* P02P03: Séparation entre les 2 parties hautes du VV
* P14P3 : Séparation entre les parties haute et basses du VV
* P1P3  : Séparation entre les 2 parties basses du VV
*
P02P03 = P02 'DROI' (3*D1) P03 ;
P03P02 = 'INVE' P02P03         ;
P14P3  = P14 'DROI' (3*D1) P3  ;
P3P14  = 'INVE' P14P3          ;
P1P3   = P1  'DROI' (2*D1) P3  ;
P3P1   = 'INVE' P1P3           ;
*
* P1P2 : LP Entry
* P4P5 : EP Entry
* P7P9 : UP Entry
*
P1P2   = P1P01  'ET' P01P2  ;
P4P5   = P4P02  'ET' P02P5  ;
P7P9   = P7P8   'ET' P8P9   ;
*                                           Ancienne définition du VV
*                               (en une fois via SURF -> TRI3 & QUA4)
*                             CBOT0  = P16P1  'ET' P1P2   'ET' P2P3 ;
*                             CROUT0 = P3P4   'ET' P4P5   'ET' P5P7 ;
*                                        CTOP0  = P7P9   'ET' P9P10 ;
*   CRIN0  = P10P11 'ET' P11P03 'ET' P03P12 'ET' P12P13 'ET' P13P16 ;
*                CONT0  = CBOT0  'ET' CROUT0 'ET' CTOP0  'ET' CRIN0 ;
*                                                 VV00 = SURF CONT0 ;
*
*                        Ancienne définition de la partie haute du VV
*                                      (en une fois via DALL -> QUA4)
*                                                    CBOT0  = P14P3 ;
*                             CROUT0 = P3P4   'ET' P4P5   'ET' P5P7 ;
*                                        CTOP0  = P7P9   'ET' P9P10 ;
*   CRIN0  = P10P11 'ET' P11P03 'ET' P03P12 'ET' P12P13 'ET' P13P14 ;
*                        VV01   = 'DALL' CBOT0 CROUT0  CTOP0  CRIN0 ;
CTOP0H  = P7P9   'ET' P9P10 ;
CRIN0H  = P10P11 'ET' P11P03 ;
CBOT0H  = P03P02 ;
CROUT0H = P02P5  'ET' P5P7 ;
VV01H   = 'DALL' CBOT0H CROUT0H CTOP0H CRIN0H ;
CTOP0B  = P02P03 ;
CRIN0B  = P03P12 'ET' P12P13 'ET' P13P14 ;
CBOT0B  = P14P3 ;
CROUT0B = P3P4   'ET' P4P02 ;
VV01B   = 'DALL' CBOT0B CROUT0B CTOP0B CRIN0B ;
VV01    = VV01H 'ET' VV01B ;
*
VV021   = 'DALL' P16P1 P1P3 P3P14 P14P16 ;
VV022   = 'SURF' (P1P2 'ET' P2P3 'ET' P3P1) ;
VV02    = (VV021 'ET' VV022) 'COUL' 'JAUN' ;
VV00    = VV01 ET VV02 ;
*
*-----------------------> Upper Port
P7U1 = P7 'DROI' (4*D1) U1 ;
U1U2 = U1 'DROI' (2*D1) U2 ;
U2P9 = U2 'DROI' (4*D1) P9 ;
P9P7 = 'INVE' P7P9 ;
*
UP00 = 'DALL' P7U1 U1U2 U2P9 P9P7 ;
UP00 = UP00 'COUL' 'TURQ' ;
*
*-----------------------> Equatorial Port
P4E1 = P4 'DROI' (4*D1) E1 ;
E1E2 = E1 'DROI' (2*D1) E2 ;
E2P5 = E2 'DROI' (4*D1) P5 ;
P5P4 = 'INVE' P4P5 ;
*
EP00 = 'DALL' P4E1 E1E2 E2P5 P5P4 ;
EP00 = EP00 'COUL' 'ROUG' ;
*
*-----------------------> Lower Port + Raccord Bas du VV
P1L1 = P1 'DROI' (3*D1) L1 ;
L1L2 = L1 'DROI' D1 L2 ;
L2L3 = L2 'DROI' (2*D1) L3 ;
L3L4 = L3 'DROI' D1 L4 ;
L4P2 = L4 'DROI' (3*D1) P2 ;
P1L2 = P1L1 'ET' L1L2 ;
L3P2 = L3L4 'ET' L4P2 ;
P2P1 = 'INVE' P1P2 ;
LP00 = 'DALL' P1L2 L2L3 L3P2 P2P1 ;
LP00 = LP00 'COUL' 'VERT' ;
*
*- Tracé du maillage 2D
*
*
*TRAC (VV00 ET LP00 ET EP00 ET UP00) ;
*
* Maillage de la brêche
VV0   = VV00  ELEM 'JAUNE' ;
VV0   = 'DIFF' VV00 VV0 ;
X1 Y1 = 'COOR' VV0;
Xmax  = 'MAXI' X1;
*
* Géométrie considéré : VV + UP
VV0 = VV0 'ET' UP00 ;
*
EVV2 = 'CONT' VV0 ;
VV2  = EVV2 'ELEM' 'TURQ' ;
EVV0 = 'DIFF' EVV2 VV2 ;
*
Pt0  = Xmax 0.0  ;
pjg  =  'POIN' EVV0 'PROC' Pt0                 ;
jg   = ('ELEM' EVV0 'APPUYE' 'LARGEMENT' pjg) 'COUL' 'VERT';
jg   = ('ELEM' EVV0 'APPUYE' 'LARGEMENT'  jg) 'COUL' 'VERT';
jg2  = ('ELEM' EVV0 'APPUYE' 'LARGEMENT'  jg) 'COUL' 'VERT';
*
EVV1 = DIFF EVV0 jg2 ;
src  = jg ;
*
*=================
* DATA du scenario
*=================
*
*
 rxt          =  table                                        ;
 rxt.'vtf'    =  VV0                                          ;
*
* Definition du mur chaud :
 rxt.'TIMP1'            = table                             ;
 rxt.'TIMP1'.'MAILLAGE' = EVV1                              ;
 rxt.'TIMP1'.'t'     = prog 0.0   40.0  160.0 350.0 718.0 1150. 1400. ;
 rxt.'TIMP1'.'TIMP'  = prog 1080. 1077. 1077. 1070. 1000. 935.0 900.0 ;
 rxt.'TIMP1'.'ECHAN' = 10. ;
*
* Definition de la breche :
 rxt.'breche' =  src                                         ;
 rxt.'diru1'  =  (-1.0 0.0)                                  ;
*
* Point intérieur
 rxt.'pi'     =  (5.0 2.0)                                   ;
*
 rxt.'VAPEUR' =  VRAI                                        ;
 rxt.'FRPREC' = 3                                            ;
 rxt.'IMPR'   = 1                                            ;
*
* Conditions initiales (data bidon)
 rxt.'TF0'      =       500.0             ;
 rxt.'PT0'      =       0.02E5            ;
 rxt.'Yvap0'    =       0.95              ;
*
* Scenario
 rxt.'scenario' = table                    ;
 rxt.'scenario'.'t'      = PROG 0.0  100. 330.0  335.0 1300. ;
 rxt.'scenario'.'qair'   = PROG 0.0  0.0  0.0    0.0   0.0   ;
 rxt.'scenario'.'qeau'   = PROG 0.011 0.005 0.001  0.0005 0.0002 ;
 rxt.'scenario'.'tinj'   = PROG 150.  150. 150.  150.  150.  ;
*
* Eléments Vitesse/pression
 rxt.'DISCR'  =  'LINE'                  ;
 rxt.'KPRE'   =  'MSOMMET'                ;
*
 rxt.'epsi'   =  1.0e-2                   ;
*
* Viscosite turbulente (m2/s)
 rxt.'MODTURB' =  'NUTURB'      ;
 rxt.'NUT'     =  8.E-2         ;
*
 rxt.'GRAPH' =  FAUX                    ;
 rxt.'DT0'    = 0.2                     ;
*
*
*==========
* Execution
*==========
*
*
 execrxt 10  rxt                        ;
 rxt.'GRAPH' =  GRAPH                   ;
 execrxt 10  rxt                        ;
*
*
*================
* Post-traitement
*================
*
*
'SI' GRAPH ;
   vtf = rxt . 'GEO' . 'vtf' ;
   tps = rxt . 'TIC' . 'Tps' ;
*
***   -----------------------    TRACE DE LA TEMPERATURE
     TF   = rxt.'TIC'.'TF'   ;
     TIT1 = 'CHAINE' 'Temperature a t = ' tps;
     'TRAC' TF vtf ('CONT' vtf) 'TITR' TIT1 ;
*
***   -----------------------    TRACE DE LA VITESSE
     UN=rxt.'TIC'.'UN';
     UNG2 = VECT UN  0.3 UX UY  'ROUGE'  ;
     TIT1 = 'CHAINE' 'Vecteur vitesse a t = ' tps;
     'TRAC' UNG2 vtf ('CONT' vtf) 'TITR' TIT1 ;
*
***   -----------------------    TRACE DE YVAP
     YVAP = rxt.'TIC'.'YVAP'   ;
     TIT1 ='CHAINE' 'Fraction massique de Vapeur a t = ' tps;
     'TRAC' YVAP vtf ('CONT' vtf) 'TITR' TIT1 ;
*
***   -----------------------    TRACE DE YAIR
     YAIR  = rxt.'TIC'.'YAIR'   ;
     TIT1='CHAINE' 'Fraction massique d air a t = ' tps;
     trace YAIR vtf (CONT vtf) 'TITR' TIT1 ;
'FINSI';
*
*========================
* Tests de non-regression
*========================
*
ERR = 0 ;
*
* Tests sur les fractions massiques
YAIR  = rxt.'TIC'.'YAIR'   ;
MinYa = MINI YAIR          ;
MaxYa = MAXI YAIR          ;
YVAP  = rxt.'TIC'.'YVAP'   ;
MinYv = MINI YVAP          ;
MaxYv = MAXI YVAP          ;
V1    = MinYa + MaxYv      ;
V2    = MaxYa + MinYv      ;
'SI' (V1 'NEG' 1.0  )      ;
   ERR = ERR + 1              ;
   'MESS' 'Erreur sur les fractions massiques' ;
'FINSI';
'SI' (V2 'NEG' 1.0  )      ;
   ERR = ERR + 1              ;
   'MESS' 'Erreur sur les fractions massiques' ;
'FINSI';
*
* Test sur la pression, température, vitesse maxi
* à la fin du calcul (t=4s)
PT4    = 3258.3 ;
Tfm4   = 726.08 ;
LMAXU4 = 8.7266 ;
*
N1     = 'DIME' (rxt.'TIC'.'LTPS') ;
PTc    = 'EXTR' (rxt.'TIC'.'PT')    N1 ;
Tfmc   = 'EXTR' (rxt.'TIC'.'Tfm')   N1 ;
LMAXUc = 'EXTR' (rxt.'TIC'.'LMAXU') N1 ;
*
EPSS   = 1.E-4 ;
*
ERRPT  = (PTc-PT4)/PT4 ; 'LIST' ERRPT ; 'LIST' PT4 ;
'SI' ( (ABS ERRPT) '>EG' EPSS ) ;
   ERR = ERR + 1 ;
   'MESS' 'Erreur sur la pression'     ;
'FINSI';
ERRTF = (Tfmc-Tfm4)/Tfm4 ; 'LIST' ERRTF ; 'LIST' Tfm4 ;
'SI' ( (ABS ERRTF) '>EG' EPSS )   ;
   ERR = ERR + 1 ;
   'MESS' 'Erreur sur la temperature'  ;
'FINSI';
ERRVM = (LMAXUc-LMAXU4)/LMAXU4 ; 'LIST' ERRVM ; 'LIST' LMAXU4 ;
'SI' ( (ABS ERRVM) '>EG' EPSS  )   ;
   ERR = ERR + 1              ;
   'MESS' 'Erreur sur la vitesse maxi' ;
'FINSI';
*
'SI' (ERR > 0);
'MESS' 'Il y a ' ERR ' erreurs';
*'ERRE' 5 ;
'FINSI';
*
'FIN';











