* fichier :  ASSI_01.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************

************************************************************************
*                 Test ASSI_01.dgibi: Jeux de données                  *
*                 -----------------------------------                  *
************************************************************************
* CAS TEST DU 05/11/2018     PROVENANCE : TEST


*_______________________________________________________________________
*   TEST ASSI_01                                                       *
*                                                                      *
*   Verification & Validation                                          *
*                                                                      *
*   TEST SUR L'OPERATEUR ASSI qui lance des commandes GIBIANE en       *
*   asynchrone                                                         *
*______________________________________________________________________*
*Lancement d'operations elementaires "a la main" sur les ASSISTANTS

*IMPORTANT : La premiere instruction utilise ASSI pour verifier que
*            irt est bien initialise dans pilot.eso (pas bon en 2018)
*             ==> Ne rien mettre avant RES1 ='ASSI' 1 1 + 1 ;

*Addition     d' ENTIERS   sur l'ASSISTANT n° 1
 RES1 ='ASSI' 1 1 + 1 ;
 SOL1 = 1 + 1         ;
'SI' ('NEG' RES1 SOL1);
  'MESS' 'ERREUR 1 ASSISTANT' ' ' 1 ;
  'MESS' 'RES1 est faux' ' ' RES1 ' ' 'au lieu de' ' ' SOL1 ;
'FINS';

*On recupere le nombre d'ASSISTANTS
 NBASS = 'VALE' 'ASSI' ;
 APO   = 'CHAI' '';
 MESS 'Nombre d' APO 'ASSISTANTS :'  NBASS;

************************************************************************
*Operations avec des ENTIERS dans les ASSISTANTS
************************************************************************
*Initialise RESj = &SURi ;
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES2' &SURi);
  'NOMM' TEXT1 &SURi;
'FIN'  SURi ;

*Calcule RESj = RESj ** &SURi;
 NBREP = 5;
'REPE' SURj NBREP ;
  'REPE' SURi NBASS ;
     TEXT1='TEXT' ('CHAI' RES2 &SURi) ;
     RESj ='ASSI' &SURi TEXT1 * &SURi ;
    'NOMM' TEXT1 RESj ;
  'FIN'  SURi ;
'FIN'  SURj ;

*Teste si les résultats sont BONS
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES2' &SURi) ;
   SOLi =&SURi ** (NBREP + 1) ;
  'SI' ('NEG' TEXT1 SOLi);
    'MESS' 'ERREUR 2 ASSISTANT' ' ' &SURi;
    'MESS' ('CHAI' 'RES2' &SURi) ' est faux' ' ' TEXT1 ' ' 'au lieu de' ' ' SOLi ;
    'ERREUR' 21;
  'FINS' ;
'FIN'  SURi ;

************************************************************************
*Operations avec des FLOTTANTS dans les ASSISTANTS
************************************************************************
*Initialise RESj = &SURi ;
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES3' &SURi);
  'NOMM' TEXT1 ('FLOT' &SURi);
'FIN'  SURi ;

*Calcule RESj = RESj ** &SURi;
 NBREP = 5;
'REPE' SURj NBREP ;
  'REPE' SURi NBASS ;
     TEXT1='TEXT' ('CHAI' 'RES3' &SURi) ;
     RESj ='ASSI' &SURi TEXT1 * &SURi ;
    'NOMM' TEXT1 RESj ;
  'FIN'  SURi ;
'FIN'  SURj ;

*Teste si les résultats sont BONS
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES3' &SURi) ;
   SOLi =('FLOT' &SURi) ** (NBREP + 1) ;
  'SI' ('NEG' TEXT1 SOLi);
    'MESS' 'ERREUR 3 ASSISTANT' ' ' &SURi;
    'MESS' ('CHAI' 'RES3' &SURi) ' est faux' ' ' TEXT1 ' ' 'au lieu de' ' ' SOLi ;
    'ERREUR' 21;
  'FINS' ;
'FIN'  SURi ;

************************************************************************
*Operations avec des MOTS dans les ASSISTANTS
************************************************************************
*Initialise RESj = 'CHAI' 'TOTO' &SURi ;
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES4' &SURi);
  'NOMM' TEXT1 ('CHAI' 'TOTO' &SURi);
'FIN'  SURi ;

*Calcule RESj = 'CHAI' RESj RESj;
 NBREP = 2;
'REPE' SURj NBREP ;
  'REPE' SURi NBASS ;
     NOMi ='CHAI' 'RES4' &SURi ;
     TEXT1='TEXT' NOMi ;
     RESj ='ASSI' &SURi 'CHAI' TEXT1 TEXT1 ;
    'NOMM' ('MOT' NOMi) RESj ;
  'FIN'  SURi ;
'FIN'  SURj ;

*Teste si les résultats sont BONS
'REPE' SURi NBASS ;
   TEXT1='TEXT' ('CHAI' 'RES4' &SURi) ;
   SOLi ='CHAI' 'TOTO' &SURi ;
  'REPE' SURj NBREP ;
     SOLi ='ET' SOLi SOLi ;
  'FIN'  SURj;
  'SI' ('NEG' TEXT1 SOLi) ;
    'MESS' 'ERREUR 4 ASSISTANT' ' ' &SURi;
    'MESS' ('CHAI' 'RES4' &SURi) ' est faux' ' ' TEXT1 ' ' 'au lieu de' ' ' SOLi ;
    'ERREUR' 21;
  'FINS' ;
'FIN'  SURi ;

 FIN;


