* fichier :  ther71.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

SAUT PAGE ;
*-------------------------------------------------------------------*
* TEST THER7                                                        *
*                                                                   *
*    TEST DES CL DE TEMPERATURE POUR COQ8 ET COQ6                   *
*    ------------------------------------------------------------   *
*                                                                   *
*               TEMPERATURE IMPOSEE + CONVECTION                    *
*                                   + FLUX                          *
*                                   + SOURCE                        *
*                                                                   *
* Ce test permet de v{rifier le bon fonctionnement des divers       *
* op{rateurs thermiques de CASTEM2000.                              *
*                                                                   *
* UNE PLAQUE RECTANGULAIRE CONSTITUEE D'{L{ments COQ8 et coq6 est   *
* soumise @ une temp{rature impos{e @ une de ses extr{mit{s et suc- *
* essivement @ une condition de convection forc{e, @ une condition  *
* de flux impos{ et @ une condition de source volumique impos{e.    *
*                                                                   *
* Les r{sultats  sont compares a ceux obtenus                       *
* par un calcul {quivalent utilisant les elements massifs cu20      *
* On compare les temperatures des points situ{s sur la face externe *
* du cylindre , le long de la ligne AS2 DS2.                        *
* Test elabore par Mr Dowlatyari 03/91.                             *
* Test modifie le 20/05/92.                                         *
*-------------------------------------------------------------------*
opti echo 1;
********************************************************************
**************** 1/ calcul en massif *******************************
********************************************************************
OPTION DIME 3 ELEM QUA8 ;
*tri6 ;
opti elem cu20;

opti trac bens;
graph = 'N';

*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
As1= 0.25 0. 0.; AS2 =0.25 1.5 0. ; AS3 = 0.25 3. 0.;

BS1= 0.25 0. 2.; BS2 =0.25 1.5 2.; BS3 = 0.25 3. 2.;

CS1= 0.25 0. 4.; CS2 =0.25 1.5 4; CS3 = 0.25 3. 4.;

DS1= 0.25 0. 6.; DS2 =0.25 1.5 6.; DS3 = 0.25 3. 6.;


*-------------  CREATION DES SURFACES ET VOLUMES ------------------     -
*
DAD = AS3 d 3 DS3;
DDD = DS3 D 2 DS1;
DDA = DS1 D 3 AS1;
DAA = AS1 D 2 AS3;
QTOTS = DALL DAD DDD DDA DAA;
V = -0.5 0. 0.;
QTOT = QTOTS VOLU 1 TRAN V;

OEIL = 5. 1.5 3.;

ge0 = face 1 qtot;
scon = elem ge0 appu larg ddd;

ge1 = face 3 qtot;
dfa = qtot poin proc ( 0. 1.5 0. );
sinf = elem ge1 appu larg dfa;


ssou = elem qtot appu larg ddd;


si ( NEG GRAPH 'N' );
  trac oeil qtot;
  trac oeil scon;
  trac oeil sinf;
finsi;

*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE QTOT THERMIQUE ISOTROPE ;
MOD2 = MODE SCON thermique CONVECTION  ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2  ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE SINF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE SINF 'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE SINF 'T   ' ;
EE3 = DEPI BB3 10. ;
BB  = BB1 ET BB2 ET BB3 ;
EE  = EE1 ET EE2 ET EE3  ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'T' 200. ;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200.  SCON;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1  1000. SSOU ;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET BB ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
*---------------- POST-TRAITEMENT:                                      -
*---------------- EXTRACTION DES VALEURS CARACTERISTIQUES -----------
*
*LISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
*
AS2 = QTOT POIN PROC ( 0.25 1.5 0.);
BS2 = QTOT POIN PROC ( 0.25 1.5 2.);
CS2 = QTOT POIN PROC ( 0.25 1.5 4.);
DS2 = QTOT POIN PROC ( 0.25 1.5 6.);


T1Aq = EXTR CHTER1 T AS2 ;T1Bq =EXTR CHTER1 T BS2 ;
T1Cq = EXTR CHTER1 T CS2 ;T1Dq =EXTR CHTER1 T DS2 ;
T2Aq = EXTR CHTER2 T AS2 ;T2Bq =EXTR CHTER2 T BS2 ;
T2Cq = EXTR CHTER2 T CS2 ;T2Dq =EXTR CHTER2 T DS2 ;
T3Aq = EXTR CHTER3 T AS2 ;T3Bq =EXTR CHTER3 T BS2 ;
T3Cq = EXTR CHTER3 T CS2 ;T3Dq =EXTR CHTER3 T DS2;


LCON = PROG T1Aq T1Bq T1Cq T1Dq;
LFLU = PROG T2Aq T2Bq T2Cq T2Dq;
LSOU = PROG T3Aq T3Bq T3Cq T3Dq;
*LIST LCON; LIST LFLU; LIST LSOU;

*******************************************************************
********************* 2/ CALCUL EN COQ6 ET COQ8 *******************
*******************************************************************

SAUT PAGE ;

TITRE 'C.L. DE TEMPERATURE - ELEMENTS  COQ8 , COQ6 ' ;
OPTION DIME 3 ELEM QUA8 ;

*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
A1 = 0. 0. 0. ; B1 = 0. 3.0 0. ;AB1 =0. 1.5  0.  ;
A2= 0. 0. 1. ;  B2 = 0. 3. 1. ; AB2 = 0. 1.5 2.  ;
A3 = 0. 0. 2. ; B3 = 0. 3. 2. ; AB4 = 0. 1.5 4.  ;
A4 = 0. 0. 3. ; B4 = 0. 3. 3. ; AB5 = 0. 1.5 6.  ;
A5 = 0. 0. 4. ;B5 = 0. 3. 4. ; AB3= 0. 1.5 3.  ;
A6 = 0. 0. 5. ;B6 = 0. 3. 5. ;
A7 = 0. 0. 6. ;B7 = 0. 3. 6. ;
*
*-------------  CREATION DES QUADRANGLES ET TRIANGLES -------------------
*
QH01 = MANU QUA8 A1 AB1 B1 B2 B3 AB2  A3 A2   ;
QH02 = MANU TRI6 A3 AB2 B3 B4 B5 AB3      ;
QH03 = MANU TRI6 A3 AB3 B5 AB4 A5 A4      ;
QH04 = MANU QUA8 A5  AB4 B5 B6  B7 AB5  A7 A6 ;
*
*
QTOT = QH01 ET QH02 ET QH03 ET QH04 ;
OEIL = 100. 100. 100. ;
SI ( NEG GRAPH 'N' );
TRACE OEIL QTOT;
FINSI;
QSOUR = QH04 ;
*
*
L_INF = MANU SEG3 A1 AB1 B1 ;
*
S_COT = QH04;
*
* ATTENTION : il faut r{orienter les {l{ments (OPERATEUR ORIENT)
* si on n'est pas sur que tous les {l{ments sont orient{s d'une
* facon coherante
*
*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE QTOT THERMIQUE ISOTROPE COQ8 COQ6 ;
MOD2 = MODE S_COT thermique CONVECTION SUPERIEURE COQ8 COQ6 ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. 'EPAI' 0.5 ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
*OPTION DONN 5 ;
CND1 = CONDUCTIVITE MOD1 MAT1 ;

CND2 = CONDUCTIVITE MOD2 MAT2 ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE L_INF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE L_INF 'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE L_INF 'T   ' ;
EE3 = DEPI BB3 10. ;
BB  = BB1 ET BB2  ;
EE  = EE1 ET EE2  ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'TSUP' 200. ;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200.  S_COT SUPE ;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1  1000. QSOUR ;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET  BB  ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*FFF1 = EE ET CNV1  ;FFF2 = EE ET FLU1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
*OPTION DONN 5 ;
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
************************************************************************-
****************** 3/COMPARAISON DES 2 CALCULS *************************
************************************************************************
*LISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
*
OEIL= 1000. 0. 0. ;
*
SI ( NEG GRAPH 'N');

TITRE 'ELEMENTS COQ6,COQ8 : CONVECTION' ;
CHTER1I = 'EXCO' CHTER1 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTER1I ;
TITRE 'ELEMENTS COQ6,COQ8 : FLUX IMPOSE' ;
CHTER2I = 'EXCO' CHTER2 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTER2I ;
TITRE 'ELEMENTS COQ6,COQ8  : SOURCE' ;
CHTER3I = 'EXCO' CHTER3 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTER3I ;
FINSI;

T1AB1 = EXTR CHTER1 TSUP AB1 ;T1AB2=EXTR CHTER1 TSUP AB2 ;
T1AB3 = EXTR CHTER1 TSUP AB3 ;T1AB4=EXTR CHTER1 TSUP AB4 ;
T1AB5 = EXTR CHTER1 TSUP AB5 ;
T2AB1= EXTR CHTER2 TSUP AB1 ;T2AB2=EXTR CHTER2 TSUP AB2 ;
T2AB3 = EXTR CHTER2 TSUP AB3 ;T2AB4=EXTR CHTER2 TSUP AB4 ;
T2AB5 = EXTR CHTER2 TSUP AB5 ;
T3AB1 = EXTR CHTER3 TSUP AB1 ;T3AB2=EXTR CHTER3 TSUP AB2 ;
T3AB3 = EXTR CHTER3 TSUP AB3 ;T3AB4=EXTR CHTER3 TSUP AB4 ;
T3AB5 = EXTR CHTER3 TSUP AB5 ;
*
si ( NEG GRAPH 'N');

DIST = PROG 0. 2. 4. 6. ;
DIST1 = PROG 0. 2. 3. 4. 6. ;
TEMPR1=PROG 10. 71.89 134.62 163.20 ;
TEMPC1= PROG T1AB1 T1AB2 T1AB3 T1AB4 T1AB5 ;
EV1R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lcon ;
EV1C = EVOL MANU 'DISTANCE' DIST1 'TEMPERATURE' TEMPC1 ;
TAB = 'TABLE' ;
TAB.1 = 'MARQ PLUS' ;
TAB.2 = 'MARQ ETOI' ;
TITRE 'COQ6 ET COQ8 ;TEMPERATURE + CONVECTION ;+ : MASSIF,* : COQUE' ;
DESSIN TAB (EV1R ET EV1C )  ;
TEMPR2=PROG 10. 26.16 42.49  50.79  ;
TEMPC2= PROG T2AB1 T2AB2 T2AB3 T2AB4 T2AB5 ;
EV2R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lflu;
EV2C = EVOL MANU 'DISTANCE' DIST1 'TEMPERATURE' TEMPC2 ;
TITRE 'COQ6 ET COQ8;TEMPERATURE + FLUX IMPOSE ;+ : MASSIF,* : COQUE';
DESSIN TAB (EV2R ET EV2C )  ;
TEMPR3=PROG 10. 50. 90.  110.   ;
TEMPC3= PROG T3AB1 T3AB2 T3AB3 T3AB4  T3AB5 ;
EV3R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lsou;
EV3C = EVOL MANU 'DISTANCE' DIST1 'TEMPERATURE' TEMPC3 ;
TITRE 'COQ6 ET COQ8 ;TEMPERATURE + SOURCE ;+  MASSIF,* : COQUE ';
DESSIN TAB (EV3R ET EV3C )  ;
FINSI;

*************************************************************
*************** 4/ TEST DE FONCTIONNEMENT *******************
*************************************************************

ERCA= ABS (( T1AB1-T1Aq)/T1AB1);
ERCB= ABS (( T1AB2-T1Bq)/T1AB2);
ERCC= ABS (( T1AB4-T1Cq)/T1AB4);
ERCD= ABS (( T1AB5-T1Dq)/T1AB5);
ERFA= ABS (( T2AB1-T2Aq)/T2AB1);
ERFB= ABS (( T2AB2-T2Bq)/T2AB2);
ERFC= ABS (( T2AB4-T2Cq)/T2AB4);
ERFD= ABS (( T2AB5-T2Dq)/T2AB5);
ERSA= ABS (( T3AB1-T3Aq)/T3AB1);
ERSB= ABS (( T3AB2-T3Bq)/T3AB2);
ERSC= ABS (( T3AB4-T3Cq)/T3AB4);
ERSD= ABS (( T3AB5-T3Dq)/T3AB5);

ERTOT = PROG ERCA ERCB ERCC ERCD ERFA ERFB ERFC  ERFD ERSA ERSB
ERSC ERSD;
ermax = maxi ertot;
temps;
SI ( ERMAX <EG 0.10 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

FIN ;



