* fichier :  dy_dev10.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
******************************************************************
*
*    Cas-Test de la liaison palier de l'operateur DYNE
*
* Arbre rigide L=1m sur 2 paliers cylindriques
* Chargement statique des paliers Fz = 1000 N
*
* Auteur : Inconnu
* Modifs : - JK, ajout resolution par dyne ou pasapas avec table pasapas
*            et modele modal
*        : - BP, 2015-01-23: on teste les 3 syntaxes
*
******************************************************************
*
GRAPH = FAUX;
* GRAPH = VRAI; OPTI TRAC PSC POTR HELVETICA_16 EPTR 6;

* FLCOUR=VRAI pour utiliser le modele de palier_court (sinon rodeli)
* FLCOUR = vrai;
FLCOUR = faux;

* FLPAP=VRAI pour utiliser pasapas
FLPAP = vrai et (non FLCOUR);
* FLPAP = faux;
*bp : le calcul dans DYNE avec une table pasapas fonctionne,
*     mais les resultats (UZ) du calcul avec pasapas sont faux...!

* COMPLET=VRAI pour atteindre l'etat stationnaire
* COMPLET = VRAI;
COMPLET = FAUX;

OPTI 'DIME' 3 'ELEM' SEG2 echo 1;
OPTI EPSI LINEAIRE;
*
* caracteristiques des paliers (identiques)
OMEGHZ = 100. ;
xjeu   = 1.E-4;
Wcharg = 1000.;
Lpal = 0.01;
Ra1= 0.02 ;
rhopal = 960.;
mupal = 0.2;

* nombre de pas, dt , nbre de sortie, de modes, ...
si (COMPLET);  NPas1 = 9600 ;
sinon;         NPas1 = 4800 ;
finsi;
PTps1 = 6.25e-5/30. ;
NSor1 = 30 ;
NbMod1 = 6 ;
*
E1 = 2.e+15 ;
Rho1 = 7800. ;
Nu1 = 0.3 ;
*
P0 = 0.  0. 0. ;
P1 = 0.5 0. 0. ;
P2 = 1.  0. 0. ;
*
El1 = DROI P0 P1 1 ;
El2 = DROI P1 P2 1 ;
Mail1 = El1 ET El2 ;
*
Modl1 = MODE Mail1 'MECANIQUE' 'TUYA' ;
*
Cara1 = CARA Modl1 'RAYO' Ra1 'EPAI' Ra1 ;
*
Mate1 = MATE Modl1 'YOUNG' E1 'NU' Nu1 'RHO' Rho1 ;
*
Blo1 = BLOQ 'UX' 'RX' 'RY' 'RZ' Mail1 ;
*
MMass1 = MASS Modl1 (Cara1 ET Mate1) ;
MRigi1 = RIGI Modl1 (Cara1 ET Mate1) ;
*
TBasR1 = VIBR 'SIMU' 10. NbMod1 (MRigi1 ET Blo1) MMass1  ;
TBasR1.MODES = NNOR TBasR1.MODES 'AVEC' (MOTS 'UY' 'UZ') ;

*
* Creation de la table des CONDITIONS INITIALES
************************************************
TInit1 = TABLE 'INITIAL' ;
*
UY0 = 0. ;
UZ0 = -0.0001e-3 ;
Dep0 = MANU 'CHPO' Mail1 2 'UY' UY0 'UZ' UZ0 NATURE DIFFUS ;
*
REPE bb1 NbMod1 ;
 i1 = &bb1 ;
 Def1 = TBasR1 . 'MODES' . i1 . 'DEFORMEE_MODALE' ;
 M_G1 = TBasR1 . 'MODES' . i1 . 'MASSE_GENERALISEE' ;
 P_R1 = TBasR1 . 'MODES' . i1 . 'POINT_REPERE' ;
 qi1 = (YTMX Def1 MMass1 Dep0) / M_G1 ;
*bp, 2017-10-03 : on interdit dans MATE les frequences<0
*--> petit ajustement :
  si (TBasR1 . 'MODES' . i1 . 'FREQUENCE' < 0.);
      TBasR1 . 'MODES' . i1 . 'FREQUENCE' = 0.;
  finsi;
 f_r1 = TBasR1 . 'MODES' . i1 . 'FREQUENCE';
mai4 = manu poi1 p_r1 ;
mo_mod4 = modele mai4 mecanique elastique modal ;
ca_mod4 = mate mo_mod4 freq f_r1 mass m_g1 defo Def1;
 SI (EGA i1 1) ;
  TInit1.'DEPLACEMENT' = MANU 'CHPO' P_R1 1 'ALFA' qi1 NATURE DIFFUS ;
  TInit1.'VITESSE' = MANU 'CHPO' P_R1 1 'ALFA' 0. NATURE DIFFUS ;
  mo_1 = mo_mod4 ; ca_1 = ca_mod4 ;
 SINON ;
  TInit1.'DEPLACEMENT' = TInit1.'DEPLACEMENT' ET
                      (MANU 'CHPO' P_R1 1 'ALFA' qi1 NATURE DIFFUS) ;
  TInit1.'VITESSE' = TInit1.'VITESSE' ET
                  (MANU 'CHPO' P_R1 1 'ALFA' 0. NATURE DIFFUS) ;
  mo_1 = mo_1 et mo_mod4 ; ca_1 = ca_1 et ca_mod4 ;
 FINSI ;
FIN bb1 ;
*
* Creation de la table des LIAISONS (paliers)
**********************************************
*
TLiai1 = TABLE 'LIAISON' ;
TLiai1 . LIAISON_B = TABLE 'LIAISON_B' ;
PAL1 = TABLE 'LIAISON_ELEMENTAIRE' ;
PAL1 . TYPE_LIAISON = 'PALIER_FLUIDE' ;
PAL1 . POINT_SUPPORT = P0 ;
PAL1 . LONGUEUR_PALIER = Lpal ;
PAL1 . RAYON_ARBRE = Ra1 ;
PAL1 . VISCOSITE_FLUIDE = mupal ;
PAL1 . RHO_FLUIDE = rhopal ;
PAL1 . PRESSION_ADMISSION = 0. ;
PAL1 . VITESSE_ROTATION = 2 * pi * OMEGHZ;
PAL1 . EPSI = 0.;
PAL1 . PHII = 0. ;
si (FLCOUR);
  PAL1 . MODELE_PALIER = 'PALIER_COURT' ;
  PAL1 . JEU_USINAGE = xjeu ;
sinon;
  PAL1 . MODELE_PALIER = 'RODELI' ;
  PAL1 . AFFICHAGE = 0 ;
  PAL1 . GEOMETRIE_PALIER = TABLE ;
  PAL1 . GEOMETRIE_PALIER . NOMBRE_LOBES = 1 ;
  PAL1 . GEOMETRIE_PALIER . CRITERE_ARRET = 1e-6 ;
  PAL1 . GEOMETRIE_PALIER . 1 = TABLE ;
  PAL1 . GEOMETRIE_PALIER . 1 . ASYMETRIE = 0. ;
  PAL1 . GEOMETRIE_PALIER . 1 . JEU_USINAGE = xjeu ;
  PAL1 . GEOMETRIE_PALIER . 1 . PRECHARGE = 0. ;
  PAL1 . GEOMETRIE_PALIER . 1 . ANGLE_DEBUT = 0. ;
  PAL1 . GEOMETRIE_PALIER . 1 . AMPL_ANGLE = 2 * pi ;
  PAL1 . GEOMETRIE_PALIER . 1 . COEF_SUR = 1.715 ;
  PAL1 . GEOMETRIE_PALIER . 1 . NB_MAILLES = 120 ;
*   PAL1 . GEOMETRIE_PALIER . 1 . NB_MAILLES = 360 ;
finsi;
*
si (FLPAP);
  maifl1 = manu poi1 p0 ;
  mpafl1 = modele maifl1 liaison 'PALIER_FLUIDE' 'RHODE_LI';
  tfl1 = table ; tfl1 . mpafl1 = vrai ;
  cpafl1 = mate mpafl1 'LONG' Lpal 'RAYO' Ra1 'VISC' mupal 'RHOF' rhopal
      'PADM' 0. 'VROT' (2*pi*OMEGHZ) 'EPSI' 0. 'PHII' 0. 'AFFI' 0
      'TLOB' PAL1 . GEOMETRIE_PALIER 'SORT' tfl1 ;
finsi;
*
PAL2 = TABLE 'LIAISON_ELEMENTAIRE' ;
PAL2 . TYPE_LIAISON = 'PALIER_FLUIDE' ;
PAL2 . POINT_SUPPORT = P2 ;
PAL2 . LONGUEUR_PALIER = Lpal ;
PAL2 . RAYON_ARBRE = Ra1 ;
PAL2 . VISCOSITE_FLUIDE = mupal ;
PAL2 . RHO_FLUIDE = rhopal ;
PAL2 . PRESSION_ADMISSION = 0. ;
PAL2 . VITESSE_ROTATION = 2 * pi * OMEGHZ ;
PAL2 . EPSI = 0.;
PAL2 . PHII = 0. ;
si (FLCOUR);
  PAL2 . MODELE_PALIER = 'PALIER_COURT' ;
  PAL2 . JEU_USINAGE = xjeu ;
sinon;
  PAL2 . MODELE_PALIER = 'RODELI' ;
  PAL2 . AFFICHAGE = 0 ;
  PAL2 . GEOMETRIE_PALIER = TABLE ;
  PAL2 . GEOMETRIE_PALIER . NOMBRE_LOBES = 1 ;
  PAL2 . GEOMETRIE_PALIER . CRITERE_ARRET = 1e-6 ;
  PAL2 . GEOMETRIE_PALIER . 1 = TABLE ;
  PAL2 . GEOMETRIE_PALIER . 1 . ASYMETRIE = 0. ;
  PAL2 . GEOMETRIE_PALIER . 1 . JEU_USINAGE = xjeu ;
  PAL2 . GEOMETRIE_PALIER . 1 . PRECHARGE = 0. ;
  PAL2 . GEOMETRIE_PALIER . 1 . ANGLE_DEBUT = 0. ;
  PAL2 . GEOMETRIE_PALIER . 1 . AMPL_ANGLE = 2 * pi ;
  PAL2 . GEOMETRIE_PALIER . 1 . COEF_SUR = 1.715 ;
  PAL2 . GEOMETRIE_PALIER . 1 . NB_MAILLES = 120 ;
*   PAL2 . GEOMETRIE_PALIER . 1 . NB_MAILLES = 360 ;
finsi;
*opti donn 3 ;

si (FLPAP);
  maifl2 = manu poi1 p2 ;
  mpafl2 = modele maifl2 liaison 'PALIER_FLUIDE' 'RHODE_LI';
  tfl2 = table ; tfl2 . mpafl2 = vrai ;
  cpafl2 = mate mpafl2 'LONG' Lpal 'RAYO' Ra1 'VISC' mupal 'RHOF' rhopal
      'PADM' 0. 'VROT' (2*pi*OMEGHZ) 'EPSI' 0. 'PHII' 0. 'AFFI' 0
      'TLOB' PAL2 . GEOMETRIE_PALIER 'SORT' tfl2 ;
finsi;
*
TLiai1 . LIAISON_B . 1 = PAL1 ;
TLiai1 . LIAISON_B . 2 = PAL2 ;
*
* Creation de la table des VARIABLES DE SORTIE
***********************************************
TSort1 = TABLE 'SORTIE' ;
TSort1 . 'VARIABLE' = TABLE 'VARIABLE' ;
TSort1 . 'LIAISON_B' = TABLE 'LIAISON_B' ;
TSort1 . 'LIAISON_B' . PAL1 = VRAI ;
TSort1 . 'LIAISON_B' . PAL2 = VRAI ;
tsort1 . 'VARIABLE' . vitesse = vrai ;
tsort1 . 'VARIABLE' . deplacement = vrai ;
*
* Creation de la table du CHARGEMENT (chargement statique)
*************************************
TChar1 = TABLE 'CHARGEMENT' ;
LTps1 = PROG 0.0 'PAS' PTps1 'NPAS' (NPas1+1) ;
FsY1 = 0. ;
FsZ1 = -1.*Wcharg ;
Chp_Fs1 = MANU CHPO 3 (P0 ET P2)
          'FX' 0. 'FY' FsY1 'FZ' FsZ1 ;
Ev_Fs1 = EVOL MANU LTps1 (PROG (NPas1+2) * 1.) ;
Char_Fs1 = CHAR Chp_Fs1 Ev_Fs1 ;
TChar1 . 'BASE_A' = PJBA TBasR1 Char_Fs1 ;
char_fs2 = pjba char_fs1 mo_1 ca_1 ;


* Calcul temporel : Appel a DYNE avec une table DYNE (calcul original)
********************************
* opti impi 333;
TResu1 = DYNE 'DE_VOGELAERE' TBasR1 TChar1 TInit1 TSort1
*TResu1 = DYNE 'DIFFERENCES_CENTREES' TBasR1 TChar1 TInit1 TSort1
              TLiai1 PTps1 NPas1 NSor1 ;
* opti impi 0;
*
Abs1 = TResu1 . PAL1 . 'UY_ARBRE' / xjeu;
Ord1 = TResu1 . PAL1 .'UZ_ARBRE' / xjeu;
Abs2 = TResu1 . PAL1 . 'FY_PALIER';
Ord2 = TResu1 . PAL1 . 'FZ_PALIER';
tprog = TResu1 . 'TEMPS_DE_SORTIE';


si (FLPAP);

* Creation d'une table PASAPAS (JK)
*************************************
tabdyn = table 'PASAPAS' ;
tabdyn . modele = mo_1 et mpafl1 et mpafl2 ;
tabdyn . caracteristiques = ca_1 et cpafl1 et cpafl2 ;
tabdyn . chargement = char_fs2 ;
tabdyn . deplacements = table ;
tabdyn . vitesses = table ;
tabdyn . deplacements . 0 = tinit1 . deplacement ;
tabdyn . vitesses . 0 = tinit1 . vitesse ;


* Calcul temporel : Appel a PASAPAS avec une table pasapas
********************************
tabpap = copi tabdyn;
tabpap . dynamique = vrai ;
* on calcule avec un pas xdt*plus grand sur une duree/8
xdt = 10.;
* xdt = 1.;
*--> resultats faux meme avec un pas de temps egal a l'explicite
dtpap   = ptps1*xdt;
tfinpap = (ptps1*NPas1) / 8.;
tabpap . temps_calcules = prog 0. pas dtpap tfinpap ;
* on sauve aux memes instants que DYNE
tabpap . temps_sauves = prog 0. pas (ptps1*NSor1) tfinpap;

pasapas tabpap ;

Abs12 = prog 0.; Abs22 = prog 0.; Ord12 = prog 0.; Ord22 = prog 0.;
repeter b_p ((dime tabpap . temps) - 1) ;
  tuu1 = 'EXTR' ('REDU' tabpap . variables_internes . &b_p mpafl1)
              'SLIA' 1 1 1 ;
  Abs12 = Abs12 et tuu1 . 'UY_ARBRE' ;
  Ord12 = Abs22 et tuu1 . 'UZ_ARBRE' ;
  Abs22 = Abs22 et tuu1 . 'FY_PALIER';
  Ord22 = Ord22 et tuu1 . 'FZ_PALIER';
fin b_p ;
Abs12 = Abs12 / xjeu;
Ord12 = Ord12 / xjeu;
tprog2 = tabpap . 'TEMPS_SAUVES';

* Calcul temporel : Appel a DYNE avec une table pasapas
********************************
tabdyn . sortie  = table ;
tabdyn . sortie . mpafl1 = vrai ;
tabdyn . sortie . mpafl2 = vrai ;
tabdyn . pas_de_temps = ptps1 ;
tabdyn . nombre_pas = npas1 ;
tabdyn . pas_de_sortie = nsor1 ;

dyne de_vogelaere tabdyn ;

Abs11 = tabdyn . liaisons . mpafl1 . 'UY_ARBRE' / xjeu;
Ord11 = tabdyn . liaisons . mpafl1 .'UZ_ARBRE' / xjeu;
Abs21 = tabdyn . liaisons . mpafl1  . 'FY_PALIER';
Ord21 = tabdyn . liaisons . mpafl1  . 'FZ_PALIER';
*tprog1 = tabdyn . 'TEMPS_DE_SORTIE';

finsi;


****************************************************************
* Graphiques
****************************************************************
SI GRAPH;

  tt = tabl;
  tt . 2 = mot 'TIRR';
  tt . 3 = mot 'NOLI MARQ S PLUS';
  tt . 'TITRE' = tabl;
  tt . 'TITRE' . 1 = mot 'DYNE tables dyne';
  tt . 'TITRE' . 2 = mot 'DYNE table PAP';
  tt . 'TITRE' . 3 = mot 'PASAPAS';

*--- trace de l orbite ---*
  ev1yt  = evol BLEU manu 't' tprog 'UY/C' Abs1;
  ev1zt  = evol VIOL manu 't' tprog 'UZ/C' Ord1;
  dess (ev1yt et ev1zt) 'TITR' 'UY(bleu) UZ(viol)';
  ev1yz  = evol BLEU manu 'UY/C' Abs1 'UZ/C' Ord1;
  prteta = prog -90. PAS 1. 0.;
  evjeu = evol DEFA manu 'UY/C' (cos prteta) 'UZ/C' (sin prteta);

si (FLPAP);
  ev11yz = evol TURQ manu 'UY/C' Abs11 'UZ/C' Ord11;
  ev12yz = evol ROSE manu 'UY/C' Abs12 'UZ/C' Ord12;
  dess (ev1yz et ev11yz) tt 'LEGE' NE;
  dess (ev1yz et ev11yz et ev12yz) tt 'LEGE' NE;
  dess (ev1yz et ev11yz et ev12yz et evjeu) tt 'LEGE' NE;
* bp : le calcul dans DYNE avec une table pasapas fonctionne,
*      mais les resultats (UZ) du calcul avec pasapas sont faux...!
*      --> ev11yz ok, mais ev12yz faux
sinon;
  dess (ev1yz);
  dess (ev1yz et evjeu) ;
finsi;

*--- trace des efforts FY ---*
  ev1fy  = evol BLEU manu 't' tprog  'FY' Abs2  ;
si (FLPAP);
  ev11fy = evol TURQ manu 't' tprog2 'FY' Abs21 ;
  ev12fy = evol ROSE manu 't' tprog2 'FY' Abs22 ;
  dess (ev1fy et ev11fy et ev12fy) tt;
sinon;
  dess (ev1fy) tt;
finsi;

*--- trace des efforts FZ ---*
  ev1fz  = evol BLEU manu 't' tprog  'FZ' ord2  ;
si (FLPAP);
  ev11fz = evol TURQ manu 't' tprog2 'FZ' ord21 ;
  ev12fz = evol ROSE manu 't' tprog2 'FZ' ord22 ;
  dess (ev1fz et ev11fz et ev12fz) tt;
sinon;
  dess (ev1fz) tt;
finsi;

FINSI;


****************************************************************
* Tests de Non Regression
****************************************************************
opti echo 0;

* tolerance
errtol = 0.01;
errpap = 0.02;

* reference [Frene et al., Lubrification hydrodynamique, p.139, L/D=1/4]
W1pal =  0.5 * Wcharg;
INTGr = mupal * Lpal * Ra1 * OMEGHZ * ((Ra1/xjeu)**2) / W1pal;
mess 'S='INTGr ;
Sref  = prog 16.2 7.57 4.49 2.83 1.78 1.07 0.58 0.263 0.0728 0.0221;
phiref= prog 82.5 75.5 68.5 61.5 54. 47. 39.5 31.5 21.5 15.5;
excref= prog 0.1 pas 0.1 0.9 0.95;
evexc = evol defa manu 'S' Sref '\e' excref;
eth   = ipol INTGr Sref excref;
phith = ipol INTGr Sref phiref;
mess '               excentricite et angle de calage';
mess 'theoriques:    ' eth phith;

* valeurs calculees par DYNE
n34 = dime Abs1;
edyne = (((EXTR Abs1 n34)**2) + ((EXTR Ord1 n34)**2)) ** 0.5;
pdyne = ATG (EXTR Abs1 n34) (-1.*(EXTR Ord1 n34));
mess 'calcules (DYNE):' edyne pdyne;
* erreur relative :
err1 = abs ((eth - edyne) / eth);
err2 = abs ((phith - pdyne) / phith);
mess 'erreurs relatives:' err1 err2;

* valeurs calculees par PASAPAS :
si (FLPAP);
  n341 = dime Abs11;
  edyne1 = (((EXTR Abs11 n341)**2) + ((EXTR Ord11 n341)**2)) ** 0.5;
  pdyne1 = ATG (EXTR Abs11 n341) (-1.*(EXTR Ord11 n341));
  mess 'calcules (DYNE avec table PAP):' edyne1 pdyne1;
* erreur relative :
  err11 = abs ((eth - edyne1) / eth);
  err21 = abs ((phith - pdyne1) / phith);
  mess 'erreurs relatives:' err11 err21;
  si (err11 > err1); err1 = err11; finsi;
  si (err21 > err2); err2 = err21; finsi;
* to do
* bp : le calcul dans DYNE avec une table pasapas fonctionne,
*      mais les resultats (UZ) du calcul avec pasapas sont faux...!
*      --> TNR sur ev11yz ok, mais pas sur ev12yz (faux)
finsi;

opti echo 1;
TEMP ;

* TNR
si ((err1 > errtol) ou (err2 > errtol)) ;
 erre 5 ;
sinon ;
 erre 0 ;
finsi ;


FIN ;















