* fichier : GLRC_DM.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*******************************************************
*  Cas test de l'implantation numérique du modele
*  GLRC_DM
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
*     Benjamin.Richard@lmt.ens-cachan.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
*******************************************************
* Choix du cas de charge
* - 1 : Traction/compression - charge dech. (membrane)
* - 2 : Idem - sens oppose

* - 3 : Traction/compression - charge dech. (flexion )
* - 4 : Idem - sens oppose
* - 5 : Idem avec le deux cycle en plus

* - 6 : Distorsion et cisaillement pur plan

* - 7 : Couplage traction/compression
* - 8 :
* - 9 :
*******************************************************
* Choix du cas de charge
ncas  = 1;
nfat  = 0;
*******************************************************
* Choix du modele
*******************************************************
SI (EGA nfat 0);
ric0 = VRAI;
sme0 = FAUX;
FINSI;

SI (EGA nfat 1);
sme0 = VRAI;
ric0 = FAUX;
FINSI;

graph0 = 'N';

*******************************************************
* Parametres materiaux
*******************************************************

* Parametres physiquement accessibles------------------

* Proprietes elastiques du beton
EBN  = 32308E6;
NUBN = 0.2;

* Proprietes elastiques de l'acier
EAN  = 200000E6;
NUAN = 0.3;

* Epaisseur de la dalle (ou du voile)
THN = 0.10;

* Section totale d'acier par metre lineaire
XIA = 8.0E-4;

* Position relative de la nappe dans l'epaisseur
EPR = 0.04;

* Resistance en traction du beton
FTN = 3.4E6;

* Effort limite en compression par metre lineaire
NMAX = 1.0E6;

* Parametres intrinseques au modele BA-----------------
* Le parametre GAMMA_MT
GAMMT = 0.02 ;

* Le parametre GAMMA_F
GAMMF = 0.32 ;

* Parametres intrinseques au modele MAZARS-------------
** Le parametre AT
*AT = 1.e0;

** Le parametre AC
*AC = 1.2e0;

** Le parametre BT
*BC = 1.7e+03;

** Le parametre BC
*BT = 10000.;

** Le parametre de seuil EPSD0
*EPSD0 = (FTN/EBN);

* Energie de fissuration
gft  = 160;

* Resistance en compression
fcn  = 10E6;
*fcn  = 36E6;

* Energie de fissuration
gfc  = 2000;


*******************************************************
* Options de calcul
*******************************************************
OPTION DIME 3 ELEM QUA4;
DENSITE 0.1;

*******************************************************
* Maillage
*******************************************************
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;

L1 = P1 DROIT P2 ;
L2 = P2 DROIT P3 ;
L3 = P3 DROIT P4 ;
L4 = P4 DROIT P1 ;

LTOT = L1 ET L2 ET L3 ET L4 ;

S1   = SURF LTOT PLANE;

SI (EGA graph0 'O');
TRAC s1;
FINSI;

*******************************************************
* Conditions aux limites
*******************************************************

* Conditions selon le chargement-----------------------

SI (ET (> ncas 0) (< ncas 3));
CL1 = BLOQ L4 UX;
CL2 = BLOQ P1 DEPL ROTA;
CL3 = BLOQ L2 UX;
CLTOT = CL1 ET CL2 ET CL3;
D1    = DEPI CL3 1;
FINSI;

SI (ET (> ncas 2) (< ncas 6));
CL1 = BLOQ L4 RY UX ;
CL2 = BLOQ P1 DEPL ROTA;
CL3 = BLOQ L2 RY;
CLTOT = CL1 ET CL2 ET CL3;
D1    = DEPI CL3 1;
FINSI;

SI (ET (> ncas 5) (< ncas 7));

CL1 = BLOQ P1 DEPL ROTA;
NN = NBEL L4;
D0 = 3.3E-4;
CHPO1 = MANU CHPO (INVE L4) 1 'UX'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO1 = CHPO1 * D0;
CL21 = BLOQ L4 UY;
CL22 = BLOQ L4 UX;
D2  = DEPI CL22 CHPO1;

NN = NBEL L1;
D0 = 3.3E-4;
CHPO2 = MANU CHPO (L1) 1 'UY'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO2 = CHPO2 * D0;
CL31 = BLOQ L1 UX;
CL32 = BLOQ L1 UY;
D3  = DEPI CL32 CHPO2;

NN = NBEL L2;
D0 = 3.3E-4;
CHPO3 = MANU CHPO (L2) 1 'UX'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO3 = CHPO3 * D0;
CL41 = BLOQ L2 UX;
CL42 = BLOQ L2 UY;
D41  = DEPI CL41 CHPO3;
D42  = DEPI CL42 D0;

NN = NBEL L3;
D0 = 3.3E-4;
CHPO4 = MANU CHPO (INVE L3) 1 'UY'
(PROG 0. PAS (1.0 / NN) 1.0);
CHPO4 = CHPO4 * D0;
CL51 = BLOQ L3 UX;
CL52 = BLOQ L3 UY;
D51  = DEPI CL51 D0;
D52  = DEPI CL52 CHPO4;

DF1 = FORCE ((-1 * D0) 0 0) L1;
DF2 = FORCE (0 D0 0) L2;
DF3 = FORCE (D0 0 0) L3;
DF4 = FORCE (0 (-1 * D0) 0) L4;
DFT = DF1 ET DF2 ET DF3 ET DF4;
opti trac ps;
SI (EGA graph0 'O');
vect1 = vect chpo1 500 'DEPL' ROUG;
vect2 = vect chpo2 500 'DEPL' VERT;
vect3 = vect chpo3 500 'DEPL' BLEU;
vect4 = vect chpo4 500 'DEPL' JAUN;
TRAC S1 (VECT1 ET VECT2 ET VECT3 ET VECT4);
FINSI;

CLTOT = CL1 ET CL21 ET CL22 ET CL31 ET CL32 ET CL41 ET
CL42 ET CL51 ET CL52;

DTOT = D2 ET D3 ET D41 ET D42 ET D51 ET D52;
FINSI;

SI (ET (> ncas 6) (< ncas 10));
CL1 = BLOQ P1 DEPL ROTA;
CL2 = BLOQ L4 UX RY;
CL3 = BLOQ L2 RY;
CL4 = BLOQ L2 UX;
CLTOT = CL1 ET CL2 ET CL3 ET CL4;
D1    = DEPI CL3 6.0E-3;
D2    = DEPI CL4 2.0E-4;
DTOT  = D1 ET D2;
FINSI;

*******************************************************
* Choix des modeles et des materiaux associes
*******************************************************

* Cas du modele BA-------------------------------------
SI (ric0);

* Identification automatique---------------------------
EEQM NUM EEQF NUF0 XK0 GAMC0 XALFA
= IDENTI 'GLRC_DM'
EBN NUBN EAN NUAN THN XIA
EPR FTN NMAX GAMMT GAMMF 0.;

* Modele-----------------------------------------------
MODTOT  = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
          ENDOMMAGEMENT GLRC_DM DKT COQ4 CONS COUCHE1;

* Materiaux--------------------------------------------
MATTOT  = MATE MODTOT YOUN  EEQM  NU   NUM
                      YOUF  (EEQF * 1.4)  NUF  NUF0
                      GAMT  GAMMT GAMC GAMC0
                      GAMF  GAMMF SEUI XK0
                      ALF   XALFA;

CAR1 = CARA MODTOT EPAI THN;
MATTOT = MATTOT ET CAR1;
FINSI;


* Cas du modele multicouche----------------------------
SI (sme0);

MOD1=TABLE;
mat1=TABLE;
NCOUCH = 9;
I=0;
REPETER BOU1 NCOUCH;
I=I+1;
NAME = CHAINE 'BET' I;
MOD1.I   = MODE S1 MECANIQUE ELASTIQUE
ENDOMMAGEMENT RICCOQ DKT COQ4 CONS NAME;
EPI  = ((THN-XIA) / NCOUCH);
CHAM1  = MANU CHML MOD1.I BTRA 1. TYPE CARACTERISTIQUES RIGIDITE;
CHAM2  = INTG CHAM1 MOD1.I ELEM BTRA;
TAIL1  = (2 ** (1.0 / 2.0)) * CHAM2 ** (1.0 / 2.0);
GFT = GFT / EPI;
EPSUT = (TAIL1) / GFT;
GFC = GFC / EPI;
EPSUC = (TAIL1) / GFC;

mat1.I = MATE mod1.I YOUN EBN  NU   NUBN
                  FT   ftn  EPUT EPSUT
                  FC   fcn  EPUC EPSUC;

EXCN = (4.0 * EPI) + (I - 1) * EPI;

car1 = CARA MOD1.I EPAI EPI EXCE EXCN;
mat1.I = mat1.I et car1;

FIN BOU1;

MOD2   = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
DKT COQ4 CONS ACI1;

MOD3   = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
DKT COQ4 CONS ACI2;

MODTOT = MOD2 ET MOD3;

mat2 = MATE mod2 YOUN EAN  NU NUAN;
car2 = CARA MOD2 EPAI XIA EXCE (-1 * EPR);
mat2 = mat2 et car2;

mat3 = MATE mod3 YOUN EAN  NU NUAN;
car3 = CARA MOD3 EPAI XIA EXCE EPR;
mat3 = mat3 et car3;

mattot = mat2 et mat3;

I=0;
REPETER BOU1 NCOUCH;
I=I+1;
modtot = modtot et mod1.I;
FIN BOU1;

I=0;
REPETER BOU1 NCOUCH;
I=I+1;
mattot = mattot et mat1.I;
FIN BOU1;

FINSI;

*******************************************************
* Choix des cas de charge
*******************************************************
SI (EGA NCAS 1);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. 3E-4 0. -3E-4 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 2);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. -3E-4 0. 3E-4 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 3);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. 6E-3 0. -6E-3 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 4);
LI1  = PROG 0. 1.   2.  3.   4.;
LI2  = PROG 0. -6E-3 0. 6E-3 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 5);
LI1  = PROG 0. PAS 1 8;
LI2  = PROG 0. -6E-3 0. 6E-3 0. -6E-3 0. 6E-3 0.;
LIS1 = PROG 0. pas 0.05 8;
FINSI;

SI (EGA NCAS 6);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. -1. 0. 1. 0.;
LIS1 = PROG 0. pas 0.05 4;
FINSI;

SI (EGA NCAS 7);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. 1. 0. -1. 0.;
LIS1 = PROG 0. pas 0.05 4;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (EGA NCAS 8);
LI1  = PROG 0. 1. 2.  3. 4.;
LI2  = PROG 0. -1. 0. 1. 0.;
LIS1 = PROG 0. pas 0.05 4;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (EGA NCAS 9);
LI1  = PROG 0. PAS 1 8;
LI2  = PROG 0. 1 0. -1 0. 1 0. -1 0.;
LIS1 = PROG 0. pas 0.05 8;
EV   = EVOL MANU LI1  LI2;
CHA1 = CHAR MECA DTOT EV;
CHATOT = CHA1;
FINSI;

SI (< ncas 6);
EV   = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;
CHATOT = CHA1;
FINSI;

SI (ET (> ncas 5) (< ncas 7));
EV   = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' DTOT EV ;
CHA2 = CHAR 'MECA' DFT  EV ;
CHATOT = CHA1 ET CHA2;
FINSI;

*******************************************************
* Preparation au calcul
*******************************************************
TAB1                      = TABLE  ;
TAB1.BLOCAGES_MECANIQUES  = CLTOT  ;
TAB1.MODELE               = MODTOT ;
TAB1.MOVA                 = RIEN   ;
TAB1.CHARGEMENT           = CHATOT ;
TAB1.CARACTERISTIQUES     = MATTOT ;
TAB1.TEMPS_CALCULES       = LIS1   ;

*******************************************************
* Calcul
*******************************************************
PASAPAS TAB1 ;

*******************************************************
* Post traitement
*******************************************************
nn=dime tab1.deplacements;
nn=nn-1;

SI (EGA graph0 'O');
SI (ric0);
trac modtot (exco tab1.variables_internes.nn 'XD1 ');
trac modtot (exco tab1.variables_internes.nn 'XD2 ');
FINSI;

SI (sme0);
trac modtot (exco tab1.variables_internes.nn 'DT  ');
FINSI;
FINSI;

SI (OU (EGA ncas 1) (EGA ncas 2));
ev2=@global tab1 CL3 EV FX;
SI (EGA graph0 'O');
DESS EV2;

@excel1 ev2 (chaine 'f_delta_' nfat '.txt');

FINSI;
FINSI;

SI (ET ( ET (> ncas 2) (< ncas 6)) (EGA nfat 1));
evm = prog 0;

REPETER BOU3 nn;
TPS = &bou3;
CHPS1 = TABLE;
CHPS2 = TABLE;
CHPS3 = TABLE;
CHPS4 = TABLE;
I=0;

REPE BOU1 NCOUCH;
I=I+1;
CHPS1.I = REDU (TAB1.CONTRAINTES.TPS) (mod1.I);
CHPS1.I = CHAN CHPO CHPS1.I MOD1.I;
CHPS2.I = @TOTAL CHPS1.I S1 'N11 ';
*CHPS3.I = REDU TAB1.REACTIONS.TPS (mod1.I);
CHPS4.I = @TOTAL TAB1.REACTIONS.TPS L4 'MY  ';
list CHPS4.I;
FIN BOU1;

I=0;
REPE BOU1 NCOUCH;
I=I+1;
EXCN = (4.0 * EPI) + (I - 1) * EPI;
TOT=0;
TOT=TOT+(CHPS2.I * EXCN) * 0.0 + CHPS4.I;
FIN BOU1;



evm = evm ET (prog TOT);

fin bou3;

evg=@global tab1 CL3 EV FX;
lis1 = extr evg 'ABSC';
ev2 = evol manu lis1 evm;
@excel1 ev2 (chaine 'f_delta_' nfat '.txt');
SI (ega graph0 'O');
dess ev2;
FINSI;
FINSI;

SI (ET (ega ncas 6) (EGA nfat 0));
evm = prog 0;
I = 1;
REPETER BOU1 nn;
*var0 = tab1.contraintes.I;
*tot  = EXTR VAR0 'N12 ' 1 1 1;
var0 = tab1.reactions.I;
PPP = MANU P1 POI1;
tot=@total var0 PPP 'FX  ';
evm = evm et (prog tot);
I = I + 1;
FIN BOU1;
ev2 = evol manu lis1 evm;
SI (ega graph0 'O');
dess ev2;
FINSI;
@excel1 ev2 (chaine 'f_delta_' nfat '.txt');
FINSI;








SI (ET (ega ncas 6) (EGA nfat 1));
evm = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.reactions.I;
*tot  = EXTR VAR0 'N12 ' 1 1 1;
PPP = MANU P1 POI1;
tot=@total var0 PPP 'FY  ';
evm = evm et (prog tot);
I = I + 1;
FIN BOU1;
ev2 = evol manu lis1 evm;
SI (EGA graph0 'O');
dess ev2;
FINSI;
@excel1 ev2 (chaine 'f_delta_' nfat '.txt');
FINSI;













SI (ET ( ET (> ncas 2) (< ncas 6)) (EGA nfat 0));
evm = prog 0;
I=1;

REPETER BOU1 nn;
var0=tab1.reactions.I;
tot=@total var0 L4 'MY  ';
evm=evm et (prog tot);
i=i+1;
fin bou1;

evg=@global tab1 cl3 ev fx;
lis1=extr evg absc;
ev2=evol manu lis1 evm;
@excel1 ev2 (chaine 'f_delta_' nfat '.txt');
SI (EGA graph0 'O');
dess ev2;
FINSI;
FINSI;

SI (ET (> ncas 6) (< ncas 10));
EVG1 = @GLOBAL TAB1 EV CL4 FX;
LISG1 = EXTR EVG1 ABSC;
LISG1 = (LISG1 * 2.0E-4);

EVG2 = @GLOBAL TAB1 EV CL3 FX;
LISG2 = EXTR EVG2 ABSC;
LISG2 = (LISG2 * 6E-3);

evm1 = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.reactions.I;
tot  = @total var0 L4 'FX  ';
evm1 = evm1 et (prog tot);
I = I + 1;
FIN BOU1;

evm2 = prog 0;
I = 1;
REPETER BOU1 nn;
var0 = tab1.reactions.I;
tot  = @total var0 L4 'MY  ';
evm2 = evm2 et (prog tot);
I = I + 1;
FIN BOU1;

EV21 = EVOL MANU LISG1 evm1;
EV22 = EVOL MANU LISG2 evm2;

SI (EGA graph0 'O');
DESS EV21;
DESS EV22;
FINSI;

FINSI;

SI (EGA graph0 'O');
def0 = defo s1 tab1.deplacements.nn 1 bleu;
def1 = defo s1 tab1.deplacements.nn   rouge;
trac (def0 et def1);
FINSI;

ee=tab1.variables_internes.nn;
aa=extr ee 'XD1 ' 1 1 1;

message 'erreur relative';
err = (aa - 1.9650) / 1.9650;
list err;

si (> err 1.0E-3);
erreur(5);
finsi;

*******************************************************
* FIN DE L ANALYSE
*******************************************************
FIN;








