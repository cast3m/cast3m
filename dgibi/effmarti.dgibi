OPTI DIME 3 MODE TRID ELEM QUA4;
*
**********************************************************
*  Test sur la procedure de EFFMARTI pour tester la 
*  projection des efforts globaux sur un element coque 
*  vers le modele à trois couches de MARTI.
*  On teste un seul element avec les caracteristiques 
*  suivantes:
*
*  Epaisseur        0.1 m
*  Enrobage externe 0.025 m
*  Enrobage interne 0.025 m
*  cotg(th)         1.43
*
*  Le cas test consiste à projeter le tenseur des efforts suivant:
*  
*  N11 = 1 N/m
*  N22 = 2 N/m
*  N12 = 3 N/m
*  M11 = 4 Nm/m
*  M22 = 5 Nm/m
*  M12 = 6 Nm/m
*  V1 =  7 N/m
*  V2 =  8 N/m
*
*  On laisse l'option pour effectuer le meme calcul sur un element 
*  plus complexe (CAS1 = 2)
*
*  Develloppé par Alberto FRAU /DEN/DANS/DM2S/SEMT/EMSI
*              et Nicolas ILE  /DEN/DANS/DM2S/SEMT/EMSI
*
**********************************************************
*
* selection du calcul
* si CAS1 egal à 1 -> alors on lance le cas test
* si CAS1 egal à 2 -> alors on lance le cas test
CAS1 = 1.;
*
SI (CAS1 EGA 1);
* Calcul de test pour la procedure EFFMARTI
*
* Proprietés geometriques
  H1 = 0.1;
  ER1 = 0.025;
  ER2 = 0.025;
  COT1 = 1.43;
*
* Definition de l'element
  P0 = 0. 0. 0.;
  P1 = 1. 0. 0.;
  P2 = 1. 1. 0.;
  P3 = 0. 1. 0.;
*
  L1 = D 1 P0 P1;
  L2 = D 1 P1 P2;
  L3 = D 1 P2 P3;
  L4 = D 1 P3 P0;
*
* definition de l'element
  SUR1 = DALL L1 L2 L3 L4;
*
*  Contruction du modele et du materiau
  MOD1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
  MAT1 = MATE MOD1 YOUNG 30000.E6 NU 0.2 RHO 2500. EPAI H1;
*
* Definition du tensueur des efforts
*
  N11_A = 1;
  N22_A = 2;
  N12_A = 3;
  M11_A = 4;
  M22_A = 5;
  M12_A = 6;
  V1_A = 7;
  V2_A = 8;
  SIG1 = MANU CHML MOD1 'TYPE' 'CONTRAINTES' 
                         'N11' N11_A 'N22' N22_A 'N12' N12_A
                         'M11' M11_A 'M22' M22_A 'M12' M12_A
                         'V1'  V1_A 'V2'  V2_A;
*
  SIG1 = RTENS SIG1 MOD1 MAT1 (1.0 0. 0.) (0. 1. 0.);
*
* Projection des efforts sur les trois couches de MARTI
  SIG2 = EFFMARTI SIG1 MOD1 MAT1 (1. 0. 0.) (0. 1. 0.)
   H1 ER1 ER2 COT1;
*
* extractions des efforts projetés
  N11E_A = EXTR SIG2 'N11E' 1 1 1;
  N22E_A = EXTR SIG2 'N22E' 1 1 1;
  N12E_A = EXTR SIG2 'N12E' 1 1 1;
  N11I_A = EXTR SIG2 'N11I' 1 1 1;
  N22I_A = EXTR SIG2 'N22I' 1 1 1;
  N12I_A = EXTR SIG2 'N12I' 1 1 1; 
  VR_A = EXTR SIG2 'VR' 1 1 1; 
*
*  calcul des efforts projétés 
  DD1 = H1 - ER1 - ER2;
  VR_B = ((V1_A*V1_A) + (V2_A*V2_A))**(0.5);
  N11E_B = (N11_A*0.5) + (M11_A/DD1) + 
             ((V1_A*V1_A)/((2*VR_B)*(COT1)));
  N22E_B = (N22_A*0.5) + (M22_A/DD1) + 
             ((V2_A*V2_A)/((2*VR_B)*(COT1)));
  N12E_B = (N12_A*0.5) + (M12_A/DD1) + 
             ((V1_A*V2_A)/((2*VR_B)*(COT1)));
  N11I_B = (N11_A*0.5) - (M11_A/DD1) + 
             ((V1_A*V1_A)/((2*VR_B)*(COT1)));
  N22I_B = (N22_A*0.5) - (M22_A/DD1) + 
             ((V2_A*V2_A)/((2*VR_B)*(COT1)));
  N12I_B = (N12_A*0.5) - (M12_A/DD1) + 
             ((V1_A*V2_A)/((2*VR_B)*(COT1)));
*
* test
  SI (ABS(N11E_B - N11E_A)>(1.E-10));
    ERRE 5;
  FINSI;
  SI (ABS(N22E_B - N22E_A)>(1.E-10));
    ERRE 5;
  FINSI;
  SI (ABS(N12E_B - N12E_A)>(1.E-10));
    ERRE 5;
  FINSI;
  SI (ABS(N11I_B - N11I_A)>(1.E-10));
    ERRE 5;
  FINSI;
  SI (ABS(N22I_B - N22I_A)>(1.E-10));
    ERRE 5;
  FINSI;
  SI (ABS(N12I_B - N12I_A)>(1.E-10));
    ERRE 5;
  FINSI;
SINON;
* Calcul des efforts projetés sur une plaque
* de dimensions 2x4 chargée par une force sur 
* un cote et encastrée dans l'autre
* Proprietés geometriques
OPTI EPSI LINEAIRE;
  H1 = 0.1;
  ER1 = 0.025;
  ER2 = 0.025;
  COT1 = 1.43;
*
* Definition de l'element
  P0 = 0. 0. 0.;
  P1 = 4. 0. 0.;
  P2 = 4. 2. 0.;
  P3 = 0. 2. 0.;
*
  L1 = D 20 P0 P1;
  L2 = D 10 P1 P2;
  L3 = D 20 P2 P3;
  L4 = D 10 P3 P0;
*
* definition de l'element
  SUR1 = DALL L1 L2 L3 L4;
*
*  Construction du modele et du materiau
  MOD1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
  MAT1 = MATE MOD1 YOUNG 30000.E6 NU 0.2 RHO 2500. EPAI H1;
*
* Forces 
  FOR1 = FORCE L2 'FZ' -1000.;
*
* Blocages 
  BL1 = (BLOQUER 'UZ' L4) ET 
        (BLOQUER 'UX' L4) ET 
        (BLOQUER 'RY' L4) ET 
        (BLOQUER 'UY' P0);
*
* Resolution
  RIG1 = RIGI MOD1 MAT1;
  RES1  = RESO (RIG1 ET BL1) (FOR1);
*
* Determination des efforts generalisés
  SIG1 = SIGM RES1 MOD1 MAT1;
  SIG1 = RTENS SIG1 MOD1 MAT1 (1.0 0. 0.) (0. 1. 0.);
*
  SIG2 = EFFMARTI SIG1 MOD1 MAT1 (1. 0. 0.) (0. 1. 0.)
   H1 ER1 ER2 COT1;
*
  TRAC (EXCO SIG1 'M11') MOD1 TITR 'Mxx';
  TRAC (EXCO SIG1 'M22') MOD1 TITR 'Myy';
  TRAC (EXCO SIG1 'M12') MOD1 TITR 'Mxy';
*
  TRAC (EXCO SIG1 'V1') MOD1 TITR 'Vx';
  TRAC (EXCO SIG1 'V2') MOD1 TITR 'Vy';
*
  TRAC (EXCO SIG2 'N11E') MOD1 TITR 'Nxx Externe';
  TRAC (EXCO SIG2 'N22E') MOD1 TITR 'Nyy Externe';
  TRAC (EXCO SIG2 'N12E') MOD1 TITR 'Nxy Externe';
*
  TRAC (EXCO SIG2 'N11I') MOD1 TITR 'Nxx Interne';
  TRAC (EXCO SIG2 'N22I') MOD1 TITR 'Nyy Interne';
  TRAC (EXCO SIG2 'N12I') MOD1 TITR 'Nxy Interne';  
*
FINSI;
*
FIN;


