* fichier :  elas6.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas6.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*___________________________________________________________________
*   TEST ELAS6                                                     *
*                                                                  *
*   COQUE AXISYMETRIQUE                                            *
*                                                                  *
*   cas-test NAFEMS : test numero LE8                              *
*                                                                  *
*     La structure est constituee d'un cylindre surmonte d'une     *
*   partie de tore et d'une partie de sphere. Elle est soumise a   *
*   un pression interne de 1MPa.                                   *
*                                                                  *
*     Le probleme presentant une symetrie axiale (geometrie et     *
*   chargement), on se place en mode axisymetrique.                *
*                                                                  *
*     On se propose de calculer la contrainte tangentielle au      *
*   point D sur la surface exterieure. La valeur trouvee est       *
*   ensuite comparee a une valeur theorique trouvee analytiquement.*
*___________________________________________________________________

OPTI DIME 2;
OPTI MODE AXIS;
OPTI ELEM SEG2;

*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
* Dimension en metres
*
* Points
*
RO2 = 0.;    ZO2 = 0.75;                   PO2 = RO2 ZO2;
RO1 = 0.25 - 0.0625; ZO1 = 0.5;            PO1 = RO1 ZO1;
*
RA = 0.25;           ZA = 0.;              PA = RA ZA;
RB = 0.25;           ZB = 0.5;             PB = RB ZB;
RC = 0.25 - 0.0625;  ZC = 0.5 + 0.0625;    PC = RC ZC;
RD = 0.25 - (0.0625 * (1 + (SIN 36))); ZD = 0.5 + (0.0625 * (COS 36));
                                           PD = RD ZD;
RE = 0.;             ZE = 0.5;             PE = RE ZE;

* Droites, cercles et lignes

* coefficient de finesse du maillage
K = 1;

SI (K EGA 1);
  TITR ' LE8 : MAILLAGE GROSSIER';
SINON;
  TITR ' LE8 : MAILLAGE FIN';
FINSI;

N1 = 6 * K;
AB = PA DROI N1 PB;
*
N2 = 5 * K;
BC = PB CERC N2 PO1 PC;
*
CD = PC CERC N1 PO1 PD;
*
N3 = 8 * K;
DE = PD CERC N3 PO2 PE;
*
* Maillage
*
SUR1 = AB ET BC ET CD ET DE;

SI (NEG GRAPH 'N');
   TITR ' TEST ELAS6 : MAILLAGE';
   TRAC QUAL SUR1;
FINSI;
*
ELSUR1 = NBEL SUR1;
NOSUR1 = NBNO SUR1;

*==================================================================*
*      modele - affecte - materiau - caracteristique               *
*           rigidite - conditions aux limites                      *
*==================================================================*

MODL1 = MODE SUR1 MECANIQUE ELASTIQUE ISOTROPE COQ2;
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3 EPAI 0.01;
RI1 = RIGI MODL1 MATR1;

* Conditions aux limites
*    - point pe : pas de deplacement radial et pas de rotation,
*    - point pa : pas de deplacement vertical.

CL1 = BLOQ ROTA PE;
CL2 = BLOQ UR PE;
CL3 = BLOQ UZ PA;
*
CLI1 = CL1 ET CL2 ET CL3;
RIG1 = RI1 ET CLI1;
*==================================================================*
*                      Chargement                                  *
*==================================================================*
*   Pression interne uniforme de 1e6Pa.
*
MOP  = 'MODE' SUR1 'CHARGEMENT' 'PRESSION' 'COQ2' ;
MAP  = 'PRES' MOP 'PRES' -1.E6 ;
cha1 = bsig mop map ;

*==================================================================*
*            resolution : champs de deplacements                   *
*==================================================================*

DEP1 = RESO RIG1 CHA1;
*
* Trace facultatif de la deformee et des reactions
*
SI (NEG GRAPH 'N');
  AMPVEC = .4E-6;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT CHA1 (AMPVEC * 5) FR FZ ROUGE ;
  VECT2 = VECT REA1 (AMPVEC * 5) FR FZ BLEU  ;
  DEF0 = DEFO 0.  DEP1 SUR1;
  DEF1 = DEFO 20. DEP1 SUR1 (VECT1 ET VECT2) VERT;
  TITR 'ELAS6 : DEFORMEE, CHARGEMENT, REACTIONS';
  TRAC SUR1 (DEF0 ET DEF1);
FINSI;

*==================================================================*
*                  extraction d'une contrainte                     *
*            et comparaison avec solution analytique               *
*==================================================================*
*      On cherche la contrainte tangentielle sigtt au noeud pd et
* sur la surface exterieure.
*
* Calcul du champs de contraintes generalisees.
*
CHAM1 = SIGM MODL1 MATR1 DEP1;
*
* Calcul du champs de contraintes au sens des milieux continus :
*  Utilisation l'operateur CALP.
*
CHAM2 = CALP CHAM1 MATR1 MODL1 INFE;
*
* Interpolation aux noeuds du maillage
*
CHAM3 = CHAN NOEUD MODL1 CHAM2;
CHPO3 = CHAN CHPO MODL1 CHAM3;
*
* Extraction de la contrainte
*
SMTTD = EXTR CHPO3 SMYY PD;
SMTTD = SMTTD / 1.E6;
*
SMTTD1 = 94.55;
*
* Calcul de l'ecart avec la solution de reference.
*
ERGTTD = 100 * (ABS ((SMTTD - SMTTD1) / SMTTD1));

*==================================================================*
*                   affichage des resultats                        *
*==================================================================*
MESS ' Resultats du test ELAS6_2 ';
MESS ' ------------------------- ';
SAUT 2 LIGN;
*
MESS ' La valeur cherchee est la contrainte tangentielle au point D,';
MESS
  ' elle est comparee a une valeur theorique obtenue analytiquement.';
saut 1 lign;
MESS ' Contrainte tangentielle theorique en D :' SMTTD1 'MPa';
saut 1 lign;
MESS ' Contrainte tangentielle calculee en D  :' SMTTD 'MPa';
SAUT 1 LIGN;
MESS '          Soit un ecart de :' ERGTTD '%';
SAUT 2 LIGN;
*
SI (K EGA 1);
  MESS ' Maillage grossier';
SINON;
  MESS ' Maillage fin';
FINSI;
*
MESS '    Nombre d elements :' ELSUR1;
MESS '    Nombre de noeuds  :' NOSUR1;
MESS '       soit ' (NOSUR1 * 3) 'd.d.l.';

*==================================================================*
*                  code fonctionnement                             *
*==================================================================*
*     L'ecart maximum entre valeur theorique et calculee doit etre
* inferieure a 1,5%.
*
SAUT 2 LIGNE;
SI (ERGTTD <EG 1.5);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;

*==================================================================*
*                 Temps de calcul et fin                           *
*==================================================================*
SAUT 2 LIGN;
TEMPS;
SAUT 2 LIGN;

FIN;










