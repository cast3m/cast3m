* fichier :  elas1.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas1.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

*______________________________________________________
* TEST ELAS1
*
* Une calotte sph}rique est soumise :
*                 - @ une pression interne
*                 - @ son poids propre
*                 - @ une force radiale en tete
*
* En élasticité et en supposant le probleme
* axisymétrique, on se propose de calculer le
* déplacement radial en tete de la calotte.
*
* Pour résoudre ce probleme on utilise le principe
* de superposition. Les forces équivalentes pour chaque
* chargement sont calculées puis ajoutées avant de
* résoudre le systeme.
*
* Le déplacement radial en tete calculé est comparé à
* la valeur théorique égale à 4.677 microns.
*
*______________________________________________________
TITRE 'CALOTTE SPHERIQUE SOUS PLUSIEURS TYPES DE CHARGEMENT';
OPTION DIME 2 ELEM SEG2 MODE AXIS ;

*______________________________________________________
*------------------ DEFINITION DE LA GEOMETRIE --------
*______________________________________________________
*
A = 0 0 ;
B = 1 1 ;
O = 0 1 ;
*
L1 = CERC 100 A O B ;
*
SI (NEG GRAPH 'N');
  TRAC (L1 ET O) 'QUAL' ;
FINSI;

*______________________________________________________
*----- DEFINITION DES CONDITIONS AUX LIMITES ---------
*______________________________________________________

CL1 = BLOQ B UZ ;
CL2 = BLOQ A UR ;
CL = CL1 ET CL2 ;

*______________________________________________________
* DEFINITION DU MODELE, DU MATERIAU
*   ET DES CARACTERISTIQUES
*______________________________________________________
MO = MODE L1 MECANIQUE ELASTIQUE COQ2 ;

MA = MATE MO YOUN 2.1E11 NU 0.3 RHO 7.85E4 ;
CA = CARA MO EPAI 0.02 ;
MA=MA ET CA;
*______________________________________________________
*------- CALCUL DE LA MATRICE DE RIGIDITE -------------
*______________________________________________________
RI1 = RIGI MO MA ;
RI2 = RI1 ET CL ;
*______________________________________________________
*-------------- DEFINITION DES CHARGES ----------------
*______________________________________________________
*
*
*------------ PRESSION INTERNE UNIFORME ---------------
*
MOP = 'MODE' L1 'CHARGEMENT' 'PRESSION' 'COQ2' ;
MAP = 'MATE' MOP 'EPAI' 0.02 ;
CHP = 'PRES' MOP 'PRES' -10000 ;
FO1 = 'BSIG' MOP CHP ;
*
* -- POIDS PROPRE (IL FAUT CALCULER LA MATRICE MASSE) -
*
MAS = MASSE MO MA;
EL1 = CHANGE L1 POI1 ;
POP = MANU CHPO EL1 1 UZ -1 ;
FO2 = MAS*POP ;
*
*------------- FORCE RADIALE EN TETE ------------------
*
F=1000*2*PI;
FO3 = FORC FR F B ;
*
FO = FO1 + FO2 + FO3 ;
*______________________________________________________
*--CALCUL PAR RESO DES DEPLACEMENTS ET POST-TRAITEMENT-
*______________________________________________________
RE = RESO RI2 FO ;
DR = EXTR RE UR B ;
DR = 1000000*DR ;
SAUT PAGE ;
SAUT 2 LIGN ;
MESS ' DEPLACEMENT RADIAL EN TETE   THEORIQUE CALCULE ' ;
MESS '                            UR=4.677 MICRON UR =' DR 'MICRON';
TEMPS ;
*______________________________________________________
*--------- CODE DE BON FONCTIONNEMENT -----------------
*______________________________________________________

ERR=100*(ABS(4.677 -DR)/4.677);
SI (ERR < 5);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;

FIN;










