* fichier :  vibr5.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************

*******************************************************
*           Test vibr5.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*********************************************************************
*                               VIBR5
*********************************************************************
*                                                                   *
*  Mots-clés : Vibrations, calcul modal, axisymetrique              *
*                                                                   *
*  TEST VIBR5                                                       *
*                                                                   *
*  CAS TEST DU 91/06/13     PROVENANCE : TEST                       *
*                                                                   *
*  CYLINDRE LIBRE : VIBRATION AXISYMETRIQUE                         *
*                                                                   *
*  Test NAFEMS numero FV41                                          *
*                                                                   *
*   On cherche les frequences et modes propres d'un cylindre        *
*   libre. Le diametre interieur du cylindre est de 1.8 m, son      *
*   diametre exterieur est de 2.2 m et sa hauteur est de 10.0 m     *
*                                                                   *
*   Les valeurs trouvees sont ensuite comparees aux valeurs         *
*   theoriques.                                                     *
*                                                                   *
*********************************************************************
OPTI DIME 2;
OPTI MODE AXIS;
OPTI ELEM QUA8;
OPTI EPSI LINEAIRE;
*================================================================*
*                  geometries et maillages                       *
*================================================================*
*
* Dimensions en metres
*
* Points
*
P0 = 0.    0.;
P1 = 1.8   0.;
P2 = 2.2   0.;
P3 = 2.2  10.;
P4 = 1.8  10.;
*
* Droites
*
N1 = 1; N2 = 8;
D12 = P1 DROI N1 P2;
D23 = P2 DROI N2 P3;
D34 = P3 DROI N1 P4;
D41 = P4 DROI N2 P1;

*
SUR1 = DALL D12 D23 D34 D41 PLAN;
*
* Trace facultatif du maillage
*
SI (NEG GRAPH 'N');
  TITR ' FV32 : MAILLAGE';
  TRAC 'QUAL' (SUR1 ET P0);
FINSI;
*
*================================================================*
*      MODE - materiau - rigidite - masse                        *
*================================================================*
*
MOD1 = MODE SUR1 MECANIQUE QUA8;

MAT1 = MATE MOD1 YOUN 2.E11 NU 0.3 RHO 8.E3;
RIG1 = RIGI MOD1 MAT1;
MAS1 = MASS MOD1 MAT1;
*
*==============================================================*
*                  conditions aux limites                      *
*==============================================================*
*
* Pas de conditions autre que la symetrie axiale.
*
*==============================================================*
*              calcul des frequences propres                   *
*                           et                                 *
*               extraction des resultats                       *
*==============================================================*
*
* Utilisation de l'operateur VIBR. (option PROC)
*
FRE1 = TABLE;
FRE1.1 = 0. ;
FRE1.2 = 243.53;
FRE1.3 = 377.41;
FRE1.4 = 394.11;
FRE1.5 = 397.72;
FRE1.6 = 405.28;

RESUL = VIBR PROC (prog 50) (lect 6) RIG1 MAS1 'IMPR';
*
*==============================================================*
*                   affichache des resultats                   *
*==============================================================*
*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;
*
FRE2 = TABLE; MOD = TABLE; DEF = TABLE;ERG = TABLE;
*
I = 0;
REPETER BLOC1 6;
  I = I + 1;
  FRE2.I = RESUL . MODES . I . FREQUENCE;
  MESS ' MODE ' (I + 1);
  MESS ' ----------';
  MESS ' Frequence theorique :' FRE1.I 'Hz';
  MESS ' Frequence calculee  :' FRE2.I 'Hz';
  SI (i > 1) ;   ERG.I = 100 * (ABS ((FRE1.I - FRE2.I) / FRE1.I));
    MESS '    Soit un ecart de : ' ERG.I '%';
  FINSI ;
  SAUT 1 LIGN;
*
* Extraction et trace des modes propres et calcul des deformees.
*    (facultatif)
*
  SI (NEG GRAPH 'N');
    MOD.I = RESUL . MODES . I . DEFORMEE_MODALE;
    DEF0 = DEFO SUR1 MOD.I 0.;
    DEF.I = DEFO SUR1 MOD.I BLEU;
    TRAC (DEF.I ET DEF0);
  FINSI;
FIN BLOC1;
*
*==============================================================*
*                   code fonctionnement                        *
*==============================================================*
*
ERGMAX = MAXI (PROG ERG.6 ERG.2 ERG.3 ERG.4 ERG.5);

SI (ERGMAX <EG 0.5);
  ERRE 0;
SINON;
  ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






