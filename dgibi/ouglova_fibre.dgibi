* fichier : ouglova_fibre.dgibi
opti epsi lineaire ;
*******************************************************
*******************************************************
*******************************************************
*  Cas test de l'implantation numerique du modele
*  OUGLOVA 1D MULTIFIBRES
*******************************************************
* Developpe par :
*     Romili PAREDES
*     Benjamin RICHARD
* Contact :
*     Romili.Paredes@cea.fr
*     Benjamin.Richard@cea.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
******************************************************

******************************************************
*** Options
******************************************************
OPTION DIME 2 ELEM QUA4 TRAC PSC;

* Nombre d'elements
nelem = 1;

******************************************************
*** Definition de la geometrie
******************************************************
SACIER1 = 0. 0.;
SACIER2 = 1. 0.;
SACIER3 = 1. 1.;
SACIER4 = 0. 1.;

LL1 = D 1 SACIER1 SACIER2;
LL2 = D 1 SACIER2 SACIER3;
LL3 = D 1 SACIER3 SACIER4;
LL4 = D 1 SACIER4 SACIER1;

SACIER  = DALL LL1 LL2 LL3 LL4;

******************************************************
*** Définition du modele
******************************************************

OPTI DIME 3 ELEM SEG2 TRAC PSC;

MOD1 = MODE SACIER MECANIQUE ELASTIQUE PLASTIQUE OUGLOVA
      QUAS;

******************************************************
*** Parametres materiaux
******************************************************

* Module d'Young
ygn = 200.e9;

* Coefficient de Poisson
nun = 0.33;

* Limite elastique
sigym = 500.e6;

* Coefficient d'ecrouissage
km = 500.e6;

* Exposant d'ecrouissage
mm = 2.786;

* Taux de corrosion en percentage
tcm = 15;

* Endommagement critique
dcm = 0.2;


MAT1 = MATE MOD1 'YOUN' ygn 'NU' nun
                 'SIGY' sigym 'K   ' km 'M   ' mm
                 'TC  ' tcm 'DC  ' dcm;

CAR1 = CARA MOD1 ALPY .66 ALPZ .66;

MODTOT = MOD1;
MATTOT = MAT1 et CAR1;

******************************************************
*** Element poutre
******************************************************

OPTI ELEM SEG2;

P1 = 0. 0. 0.;
P2 = 0. 0. 1.;

Lpoutre = D nelem P1 P2;

MODPOU = MODE Lpoutre MECANIQUE ELASTIQUE SECTION
               PLASTIQUE SECTION TIMO;
MATPOU = MATE MODPOU MODS MODTOT MATS MATTOT
               VECT (0 1 0);

******************************************************
*** Définition des conditions aux limites
*** et des déplacements imposés
******************************************************

cl1 = BLOQ DEPL ROTA P1;
cl2 = BLOQ UX UY RX RY RZ P2;
cl3 = BLOQ UZ P2;

d1  = DEPI cl3 1.0;

******************************************************
*** Définition de chargement
******************************************************

LI1  = PROG 0. 1.;
LI2  = PROG 0. 2.5e-1;
LIS1 = PROG 0. PAS 0.01 1.;

EV1  = EVOL MANU LI1 LI2;
CHA1 = CHAR DIMP d1 EV1;

******************************************************
*** Resolution
******************************************************

TAB1 = TABLE;
TAB1 . BLOCAGES_MECANIQUES = cl1 et cl2 et cl3;
TAB1 . MODELE              = MODPOU;
TAB1 . CARACTERISTIQUES    = MATPOU;
TAB1 . CHARGEMENT          = CHA1;
TAB1 . TEMPS_CALCULES      = LIS1;
TAB1 . MAXDEFOR            = 1.E-5;
TAB1 . MOVA                = RIEN;

PASAPAS TAB1;

******************************************************
*** Verification
******************************************************

EV2 = @GLOBAL TAB1 cl1 EV1 FZ;
liste EV2;

vref = EXTR EV2 'ORDO';
vref = EXTR 29 vref;

err =  abs(vref  + 5.68172e8) / 5.68172e8;
message ' erreur relative ';
list err;

si (err > 1.e-3);
   erreur (5);
finsi;

fin;


























