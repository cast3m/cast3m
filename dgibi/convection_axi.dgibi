* fichier : convection_axi.dgibi

*******************************************
* Section : Thermique Convection
*******************************************

************************************************************************
*                 DESCRIPTION DE CONVECTION_AXI.DGIBI                  *
************************************************************************
* Ce cas-test unitaire permet de VERIFIER et VALIDER le fonctionnement
* de l'operateur 'CONV' dans les conditions suivantes :
*    MODE     : Axisymetrique
*    MAILLAGE : Ligne verticale constituee de 1 element et de 2 noeuds
*    EF       : COQ2, SEG2
*
*    VERIFICATIONS : Le MMODEL CONTIENT 1 MMODEL DE CONVECTION CLASSIQUE
*                    Le MMODEL CONTIENT 1 MMODEL DE CONVECTION COQUE INF
*                    Le MMODEL CONTIENT 1 MMODEL DE CONVECTION COQUE SUP
*                    Le MMODEL CONTIENT 2 MMODEL DE CONVECTION COQUE
*                    Le MMODEL CONTIENT 3 MMODEL DE CONVECTION CLASSIQUE
*                                                              COQUE
*
*    VALIDATION    : Calcul analytique du flux et comparaison a Cast3M
*
************************************************************************


************************************************************************
*                              OPTIONS                                 *
************************************************************************
OPTI DIME 2 MODE AXIS;
OPTI ECHO 0          ;
SAUT 2 LIGN          ;

************************************************************************
*                             PARAMETRES                               *
************************************************************************
RAYO1 = 1.125    ;
HAUT1 = 3.245    ;
HCON  = 1.568    ;
HINF  = 2.**0.5  ;
HSUP  = PI**0.33 ;
TECONV= 1.456    ;
TEINFE= 13.4568  ;
TESUPE= 800.     ;


************************************************************************
*                              MAILLAGE                                *
************************************************************************
P1 = RAYO1 0.    ;
P2 = RAYO1 HAUT1 ;

OPTI 'ELEM' 'SEG2' ;
L1 = 'DROI' 1 P1 P2 ;

************************************************************************
*                         MODELE & MATERIAUX                           *
************************************************************************
MODCON = MODE L1 'THERMIQUE' 'CONVECTION'                     ;
MODINF = MODE L1 'THERMIQUE' 'CONVECTION'  'INFERIEURE' 'COQ2';
MODSUP = MODE L1 'THERMIQUE' 'CONVECTION'  'SUPERIEURE' 'COQ2';
MODTOT2= MODINF ET MODSUP                                     ;
MODTOT3= MODINF ET MODSUP ET MODCON                           ;

MATCON = MATE MODCON 'H' HCON       ;
MATINF = MATE MODINF 'H' HINF       ;
MATSUP = MATE MODSUP 'H' HSUP       ;
MATTOT2= MATINF ET MATSUP           ;
MATTOT3= MATINF ET MATSUP ET MATCON ;

************************************************************************
*                           APPEL A 'CONV'                             *
************************************************************************
TECON  = MANU 'CHPO' L1 1 'T   ' TECONV  'NATU' 'DIFFUS';
TEINF  = MANU 'CHPO' L1 1 'TINF' TEINFE  'NATU' 'DIFFUS';
TESUP  = MANU 'CHPO' L1 1 'TSUP' TESUPE  'NATU' 'DIFFUS';
TETOT2 = TEINF  ET TESUP          ;
TETOT3 = TEINF  ET TESUP ET TECON ;

FLUCON1 = CONV MODCON  MATCON 'T'    TECONV;
FLUINF1 = CONV MODINF  MATINF 'TINF' TEINFE;
FLUSUP1 = CONV MODSUP  MATSUP 'TSUP' TESUPE;
FLUCON  = CONV MODCON  MATCON  TECON       ;
FLUINF  = CONV MODINF  MATINF  TEINF       ;
FLUSUP  = CONV MODSUP  MATSUP  TESUP       ;
FLUTOT2 = CONV MODTOT2 MATTOT2 TETOT2      ;
FLUTOT3 = CONV MODTOT3 MATTOT3 TETOT3      ;


************************************************************************
*                  VALIDATION : COMPOSANTES ATTENDUES                  *
************************************************************************
LCTES = EXTR FLUCON  'COMP';
LCINF = EXTR FLUINF  'COMP';
LCSUP = EXTR FLUSUP  'COMP';
LCTOT2= EXTR FLUTOT2 'COMP';
LCTOT3= EXTR FLUTOT3 'COMP';

* Nombre de composantes
SI ((DIME LCINF) NEG 1 );
  MESS 'LE NOMBRE DE COMPOSANTES LCINF N EST PAS CORRECT ' (DIME LCINF);
  ERRE 5;
FINS;
SI ((DIME LCSUP) NEG 1 );
  MESS 'LE NOMBRE DE COMPOSANTES LCSUP N EST PAS CORRECT ' (DIME LCSUP);
  ERRE 5;
FINS;
SI ((DIME LCTOT2) NEG 2 );
  'LE NOMBRE DE COMPOSANTES LCTOT2 N EST PAS CORRECT' (DIME LCTOT2) ;
  ERRE 5;
FINS;
SI ((DIME LCTOT3) NEG 3 );
  'LE NOMBRE DE COMPOSANTES LCTOT3 N EST PAS CORRECT' (DIME LCTOT3) ;
  ERRE 5;
FINS;
* Noms des composantes
SI (NEG (EXTR LCINF 1 ) 'QINF' );
  MESS 'LE NOM DE COMPOSANTES LCINF N EST PAS CORRECT ' (EXTR LCINF 1 );
  ERRE 5;
FINS;
SI (NEG (EXTR LCSUP 1 ) 'QSUP' );
  MESS 'LE NOM DE COMPOSANTES LCSUP N EST PAS CORRECT ' (EXTR LCSUP 1 );
  ERRE 5;
FINS;
SI ((NEG (EXTR LCTOT2 1 ) 'QSUP') OU (NEG (EXTR LCTOT2 2) 'QINF' ))  ;
  MESS 'LE NOM DES COMPOSANTES LCTOT2 N EST PAS CORRECT '
 (EXTR LCTOT2 1 ) ' ' (EXTR LCTOT2 2 ) ;
  ERRE 5;
FINS;
SI ((NEG (EXTR LCTOT3 1) 'Q'      ) OU (NEG (EXTR LCTOT3 2) 'QSUP' ) OU
    (NEG (EXTR LCTOT3 3) 'QINF'   )) ;
  MESS 'LE NOM DES COMPOSANTES LCTOT3 N EST PAS CORRECT '
 (EXTR LCTOT3 1 ) ' ' (EXTR LCTOT3 2 ) ' ' (EXTR LCTOT3 3 );
  ERRE 5;
FINS;


************************************************************************
*          VALIDATION : COMPARAISON A DES VALEURS ANALYTIQUES          *
************************************************************************
VANACON = 2*PI*RAYO1*HAUT1*HCON*TECONV/2;
VANAINF = 2*PI*RAYO1*HAUT1*HINF*TEINFE/2;
VANASUP = 2*PI*RAYO1*HAUT1*HSUP*TESUPE/2;

VCON1 = MAXI FLUCON;
VCON2 = MINI FLUCON;
VINF1 = MAXI FLUINF;
VINF2 = MINI FLUINF;
VSUP1 = MAXI FLUSUP;
VSUP2 = MINI FLUSUP;

MESS 'CONVECTION FLUX CALCULE' VCON1
 'FLUX THEORIQUE' VANACON
 'ECART' (ABS (VCON1 - VANACON))
 'RELATIF' (ABS ((VCON1 - VANACON)/VCON1));

MESS 'INFERIEUR  FLUX CALCULE' VINF1
 'FLUX THEORIQUE' VANAINF
 'ECART' (ABS (VINF1 - VANAINF))
 'RELATIF' (ABS ((VINF1 - VANAINF)/VINF1));

MESS 'SUPERIEUR  FLUX CALCULE' VSUP1
 'FLUX THEORIQUE' VANASUP
 'ECART' (ABS (VSUP1 - VANASUP))
 'RELATIF' (ABS ((VSUP1 - VANASUP)/VSUP1));


SI ((ABS ((VINF1 - VINF2)/VINF1)) > 1e-15 );
 MESS 'VALEUR VINF1 ET VINF2  NON VALIDE' (ABS ((VINF1 - VINF2)/VINF1));
 ERRE 5;
FINS;
SI ((ABS ((VSUP1 - VSUP2)/VSUP1)) > 1e-15 );
 MESS 'VALEUR VSUP1 ET VSUP2  NON VALIDE' (ABS ((VSUP1 - VSUP2)/VSUP1));
 ERRE 5;
FINS;

SI ((ABS ((VINF1 - VANAINF)/VINF1)) > 1e-15 );
  MESS 'VALEUR NON VALIDE' (ABS ((VINF1 - VANAINF)/VINF1));
  ERRE 5;
FINS;
SI ((ABS ((VSUP1 - VANASUP)/VSUP1)) > 1e-15 );
  MESS 'VALEUR NON VALIDE' (ABS ((VSUP1 - VANASUP)/VSUP1));
  ERRE 5;
FINS;


SI ((MAXI (ABS (FLUTOT2 - FLUINF - FLUSUP))) > 1e-15 );
  MESS 'VALEUR NON VALIDE DANS FLUTOT2'
 (MAXI (ABS (FLUTOT2 - FLUINF - FLUSUP)));
  ERRE 5;
FINS;

SI ((MAXI (ABS (FLUTOT3 - FLUINF - FLUSUP - FLUCON))) > 1e-15 );
  MESS 'VALEUR NON VALIDE DANS FLUTOT3'
 (MAXI (ABS (FLUTOT3 - FLUINF - FLUSUP - FLUCON)));
  ERRE 5;
FINS;

FIN;



