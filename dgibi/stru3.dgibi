* fichier :  stru3.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
OPTI ECHO 1;
GRAPH = 'N';
*
*********************************************************************
*  TEST : SSLS34/91 DE LA COMMISSION "STRUCTURES ASSEMBLEES"        *
*  ----                                                             *
*                                                                   *
*  DESCRIPTION : PLAQUE ORTHOTROPE RAIDIE SUR APPUIS SIMPLES,       *
*                SOUS UNE CHARGE UNIFORMEMENT REPARTIE.             *
*                Elements DKT                                       *
*                                                                   *
*                                                                   *
*  REF. : JL. BATOZ,J.CHATELAIN,JP. JAMEUX                          *
*         ETUDE D'UNE PLAQUE RAIDIE - BULLETIN DU CLUB S.A.P -      *
*         VOLUME VIII, N1.                                          *
*  Comparaison avec les codes CA.ST.OR, ASELF, ELFINI, PERMAS       *
*  Test envoye pour evolution le 17/09/92                           *
*                                                                   *
*********************************************************************
OPTI ECHO 1;
OPTI DIME 3;
OPTI ELEM tri3;
*
*  QUELQUES DONNEES
*
OEIL = 10000 -10000 10000;
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                             GEOMETRIE                              *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*            PLAQUE SUPERIEURE
*
PA = 0 .5 0; PB =1. .5 0; PD = 1. 0 0; PC = 0 0 0;
PF = .6667 0 0; PE = .3333 0 0;
*
DAB = PA DROIT 36 PB; DBD = PB DROIT 20 PD;
DDC = PD DROIT 36 PC; DCA = PC DROIT 20 PA;
SU1 = DALLER DAB DBD DDC DCA PLAN;
*
*            RAIDISSEUR CENTRAL
*
PA2 = 0 .5 -.1; PC2 = 0 0 -.1;
D1 = PA DROIT 20 PC;
D3 = PC2 DROIT 20 PA2;
D2 = PC DROIT 4 PC2;
D4 = PA2 DROIT 4 PA;
RA1 = DALL D1 D2 D3 D4 PLAN;
*
* SEGMENTS DE DROITES POUR CONDITIONS DE SYMETRIE
*
D5 = D2 PLUS (.33333 0. 0.);
D6 = D2 PLUS (.66667 0. 0.);
*
*            RAIDISSEURS 2 ET 3
*
RA2 = RA1 PLUS (.33333 0 0);
RA3 = RA1 PLUS (.66667 0 0);
SURF = SU1 ET RA1 ET RA2 ET RA3;
ELIM (SURF ET D1 ET D2 ET D3 ET D4 ET D5 ET D6) .001;
*
SI (NEG GRAPH 'N');
  TRAC OEIL SURF;
FINSI;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*              MODELE
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MODL1 = MODE RA1 MECANIQUE ELASTIQUE ORTHOTROPE dkt;
MODL2 = MODE RA2 MECANIQUE ELASTIQUE ORTHOTROPE dkt;
MODL3 = MODE RA3 MECANIQUE ELASTIQUE ORTHOTROPE dkt;
MODL4 = MODE SU1 MECANIQUE ELASTIQUE ORTHOTROPE dkt;
MODL5 = MODL1 ET MODL2 ET MODL3 ET MODL4;

*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*          MATERIAUX - CARACTERISTIQUES                         *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
DIR1 = 0. 1. 0.;
*
MAT1 = MATE MODL1 DIRECTION DIR1 PARALLELE YG1 2.E11 YG2 0.74E11 NU12 .3 G12 .222E11 EPAI 0.005;
*
MAT2 = MATE MODL2 DIRECTION DIR1 PARALLELE YG1 2.E11 YG2 0.74E11 NU12 .3 G12 .222E11 EPAI 0.01 ;
*
MAT3 = MATE MODL3 DIRECTION DIR1 PARALLELE YG1 2.E11 YG2 0.74E11 NU12 .3 G12 .222E11 EPAI 0.01 ;
*
MAT4 = MATE MODL4 DIRECTION DIR1 PARALLELE YG1 2.E11 YG2 0.74E11 NU12 .3 G12 .222E11 EPAI 0.006 ;

MAT = MAT1 ET MAT2 ET MAT3 ET MAT4;
*

*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*             CONDITIONS AUX LIMITES                                *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = BLOQUE DAB UZ;
CL2 = BLOQUE DAB RY;
CL3 = BLOQUE DBD UZ;
CL4 = BLOQUE DBD RX;
CL5 = SYMT DEPL ROTA (0 0 0) (0 1 0) (0 0 1) SURF 0.0001;
CL6 = SYMT DEPL ROTA (0 0 0) (1 0 0) (0 0 1) SURF 0.0001;
CL = CL1 ET CL2 ET CL3 ET CL4 ET CL5 ET CL6;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                    CHARGEMENT                                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MOP = 'MODE' SU1 'CHARGEMENT' 'PRESSION' 'DKT' ;
MAP = 'PRES' MOP 'PRES' 5.E4 ;
CH  = 'BSIG' MOP MAP ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*           RESOLUTION - CALCUL ELASTIQUE                           *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
RI = RIGI MODL5 MAT;
RIG = RI ET CL ;
DEP = RESO RIG CH ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
* RESULTATS                                                         *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
NOSURF = NBNO SURF ;
ELSURF = NBEL SURF ;
*
SI (NEG GRAPH 'N');
  TITR ' PLAQUE RAIDIE : DEFORMEE ' ;
  DEF0 = DEFO SURF DEP 0. ;
  DEF1 = DEFO SURF DEP 25. ROUGE;
  TRACE OEIL (DEF0 ET DEF1) ;
  OEIL =0.25 -1.E6 0.;
  TRACE OEIL (DEF0 ET DEF1) ;
FINSI ;
*
UZC = EXTR DEP UZ PC ;
UZC1 = -.603e-3;
ERG1 = ABS ((UZC - UZC1) / UZC1) * 100 ;
*
DZ = EXCO DEP UZ;
ZMIN = MINI DZ;
ZMIN1 = -.319e-2;
ERG2 = ABS ((ZMIN - ZMIN1) / ZMIN1) * 100 ;


*
X1  = DZ POIN 'MINI';
XX  = MAXI ( COOR 1 X1);
XX1 = 0.833;
ERG3 = ABS ((XX - XX1) / XX1) * 100 ;

SAUT PAGE ;
SAUT 2 LIGN;
OPTI ECHO 1;
*
MESS ' TEST : SSLS34/90 (FICHIER stru3.DGIBI)';
MESS ' ----';
MESS '  PLAQUE ORTHOTROPE RAIDIE                                                 CHARGEE UNIFORMEMENT SUR APPUIS SIMPLES '                                        *                                                                                SAUT 2 LIGN ;
MESS ' RESULTATS ';
MESS ' --------- ';
*
SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE AU POINT PC : ' UZC1 'METRES';
MESS ' DEPLACEMENT CALCULE AU POINT PC   : ' UZC  'METRES' ;
MESS '     SOIT UN ECART DE              : ' ERG1 '%' ;
*
SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE MAXIMUM (ABS) : ' ZMIN1 'METRES';
MESS ' DEPLACEMENT CALCULE MAXIMUM (ABS)   : ' ZMIN 'METRES';
MESS '     SOIT UN ECART DE                : ' ERG2 '%' ;
*
SAUT 1 LIGNE;
MESS ' ABSICE DU POINT OU SE TROUVE LE MAXIMUM : ' ;
MESS '         - ABSICE THEORIQUE : ' XX1 'METRES';
MESS '         - ABSICE CALCULEE  : ' XX  'METRES';
MESS '     SOIT UN ECART DE       : '  ERG3  '%' ;
*
SAUT 1 LIGNE;
MESS ' NOMBRE DE NOEUDS  : ' NOSURF ;
MESS ' NOMBRE D ELEMENTS : ' ELSURF ;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*            CODE FONCTIONNEMENT                                     *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
ERGMAX = MAXI (PROG ERG1 ERG2);
*
* Il y a erreur si un des pourcentages ERG* est suprieur  5%
*
SAUT 1 LIGN;
*
SI (ERGMAX <EG 5.);
    ERRE  0 ;
SINON;
    ERRE  5 ;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;














