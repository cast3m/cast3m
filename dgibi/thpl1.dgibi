* fichier :  thpl1.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*   Test Thpl1.dgibi: Jeux de données     *
*   ---------------------------------     *
*                                         *
* CAS TEST DU 92/03/20     PROVENANCE : TC1


*******************************************
*    Test thpl1.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

*******************************************
*                   THPL1                 *
*                                         *
*  BARREAU CYLINDRIQUE SOUMIS A UN CHAMPS *
*            DE TEMPERATURE               *
*                                         *
*    Test de la Commission V.P.C.S.       *
*    Groupe Thermo-plasticite  Statique   *
*    non-lineaire                         *
*    Test numero SSNA01/90                *
*                                         *
*        Un barreau cylindrique de 100 mm *
*    de hauteur et de 20 mm de diametre   *
*    est bloque axialement a ces deux     *
*    extremites. Il est soumis a un champ *
*    constant de temperature, de 500°C.   *
*                                         *
*        On traite le probleme en mode    *
*    axisymetrique avec la procedure      *
*    PASAPAS car le materiau est plastique*
*                                         *
*        On se propose de calculer        *
*     - la contrainte axiale              *
*     - la deformation plastique axiale   *
*******************************************
OPTION DIME 2;
OPTION MODE AXIS ELEM QUA4;

*=========================================*
*         geometrie : maillage            *
*=========================================*
* Dimensions en millimetres.
RA = 0.0 ;  ZA = 0. ;     PA = RA ZA ;
RB = 10. ;  ZB = 0. ;     PB = RB ZB ;
RC = 10. ;  ZC = 100.0 ;  PC = RC ZC ;
RD = 0.0 ;  ZD = 100.0 ;  PD = RD ZD ;

DAB = DROI 1  PA PB;
DBC = DROI 10 PB PC;
DCD = DROI 1  PC PD;
DDA = DROI 10 PD PA;

SUR = DALL DAB DBC DCD DDA PLAN;

ELIM SUR 0.001;

SI (NEG GRAPH 'N');
   TITR 'THPL1 : MAILLAGE';
   TRAC QUAL SUR;
FINSI;

ELSUR = NBEL SUR;
NOSUR = NBNO SUR;

*=========================================*
*        conditions aux limites           *
*=========================================*
CL1 = BLOQ UZ DAB ;
CL2 = BLOQ UZ DCD ;
CL  = CL1 ET CL2  ;

*=========================================*
*         MODE - materiau                 *
*=========================================*
MOD = MODE SUR MECANIQUE ELASTIQUE
               PLASTIQUE CINEMATIQUE QUA4;

MAT = MATE MOD YOUN 200E3 NU 0.3 SIGY 300
               H 2000     ALPH 1.2E-5;

*=========================================*
* champs relatifs a la procedure PASAPAS  *
*=========================================*
* Liste des pseudo-temps ou l'on desire des
* resultats

t0 = 0. ;
t1 = 1. ;
LISTE = PROG T0 T1 ;

* Champs-point de temperature aux instants
* t0 et t1.
TEMP = TABL;
TEMP . 0 = t0;
TEMP . 1 = t1;

TEMPER = TABL;
TEMPER . 0 = MANU CHPO SUR 1 T 0.  ;
TEMPER . 1 = MANU CHPO SUR 1 T 500.;
CHA2       = CHAR 'T' TEMP TEMPER;

* Chargement mecanique de la structure:
* chargement nul.
LFORCE= PROG 0. 0.;
FOR   = FORCE FR 1000. PC;
EVOL1 = EVOL MANU TEMPS LISTE FORCE LFORCE;
CHA1  = CHAR 'MECA' FOR EVOL1;

* parametres pour la procedure PASAPAS
TABDEF          = TABL ;
TABDEF .'DEFTO' = VRAI;
TABDEF .'DEFIN' = VRAI;

TAB1                       = TABL  ;
TAB1.'CHARGEMENT'          =(CHA1 ET CHA2);
TAB1.'MODELE'              = MOD   ;
TAB1.'CARACTERISTIQUES'    = MAT   ;
TAB1.'BLOCAGES_MECANIQUES' = CL    ;
TAB1.'TEMPS_CALCULES'      = LISTE ;
TAB1.'TEMPS_SAUVES'        = LISTE ;
tab1.'MES_SAUVEGARDES'     = TABDEF;

* procedure PASAPAS
PASAPAS TAB1;

*=========================================*
*       extractions des resultats         *
*=========================================*

* Contraintes
nn = 1;
CHEL1 = TAB1. 'CONTRAINTES'.nn;

CHAM2 = CHAN NOEUD MOD CHEL1;
CHPO1 = CHAN CHPO  MOD CHAM2;

* Extraction du la contrainte SMZZ
SMZZ1 = EXTR CHPO1 SMZZ PA;
SMZZ2 = -309.0 ;

* Deformations plastiques
CHEL10 = TAB1.
           'DEFORMATIONS_INELASTIQUES' .nn;

* Interpolations aux noeuds du maillage
* Utilisation des nouveaux chamelems.
CHAM20 = CHAN NOEUD MOD CHEL10;
CHPO10 = CHAN CHPO  MOD CHAM20;

* Extraction de la deformation plastique
* EPZZ
EPZZ1 = EXTR CHPO10 EIZZ PA;
EPZZ2 = -.4455E-2 ;

*=========================================*
*       affichage des resultats           *
*=========================================*
SAUT 1 LIGN ;
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN ;

MESS '   1) Contraintes ' ;
MESS '      ----------- ' ;
SAUT 1 LIGN ;

MESS ' Contrainte theorique smzz:'
                              SMZZ2 'Mpa' ;
MESS ' Contrainte calculee smzz :'
                              SMZZ1 'Mpa' ;
ERG1 = (ABS((SMZZ2-SMZZ1)/SMZZ2)) * 100. ;
MESS '       Soit un ecart de  :' ERG1 '%';
SAUT 1 LIGN;
MESS '   2) Deformations plastiques ';
MESS '      ----------------------- ' ;
SAUT 1 LIGN ;

MESS ' Deformation plastique theorique:'
                                     EPZZ2;
MESS ' Deformation plastique calculee :'
                                     EPZZ1;
ERG2 = (ABS((EPZZ2-EPZZ1)/EPZZ2)) * 100. ;
MESS
'             Soit un ecart de :' ERG2 '%';

SAUT 1 LIGN;
MESS '   Maillage ';
MESS '   -------- ';
MESS '   Nombre de noeuds        : ' NOSUR;
MESS '   Nombre d elements (qua4): ' ELSUR;
SAUT 1 LIGN;

SI (NEG GRAPH 'N');
  DEP1 = TAB1. 'DEPLACEMENTS' . nn;
  DEF0 = DEFO SUR DEP1 0.;
  DEF1 = DEFO SUR DEP1 10. BLEU;
  TITR 'THPL1 : DEFORMEE';
  TRAC (DEF0 ET DEF1);
FINSI;

*=========================================*
*       code fonctionnement et fin        *
*=========================================*

* Maximum des erreurs relatives
ERGMAX = MAXI (PROG ERG1 ERG2);

* Il y a erreur si un des pourcentage erg*
* est superieur à 5%.

SI (ERGMAX <EG 0.03);
     ERRE  0 ;
SINON;
     ERRE  5 ;
FINSI;

SAUT 1 LIGN;
TEMPS ;
SAUT 1 LIGN;

FIN;




