* fichier :  soret_2.dgibi
************************************************************************
* Section : Diffusion Soret
* Section : Diffusion Fick
************************************************************************

************************************************************************
*                        CAS TEST soret_2.dgibi                        *
************************************************************************
*   Test effet Soret
* - 2D PLAN
* - ELEMENTS FINIS TESTES : 'QUA8'
* - Regime permanent
* - C(X0) = C0
* - C(X1) = C1
* - GRAD(T) choisi constant (T=GT*X ==> GRAD(T)= A)
* - CONDUCTION et SORET sont en concurrence
************************************************************************


************************************************************************
* OPTIONS & REGLAGES
************************************************************************
  GRAPH = 'N' ;
 'OPTI' 'DIME' 2 'ELEM' 'QUA8'  ;

 'SI' ('NEG' GRAPH 'N');
   'OPTI' 'TRAC' 'X';
 'SINO';
   'OPTI' 'TRAC' 'PSC';
 'FINS';

  NBE1 = 15   ;
  NBE2 = 15   ;

  K    = -7.D0;
  GT   =  1.D0;

  X0   = 1.   ;
  X1   = 2.   ;

  C0   = 0.   ;
  C1   = 2.   ;

************************************************************************
* MAILLAGE
************************************************************************
  P1 = X0 0. ;
  P2 = X0 1. ;
  P3 = X1 1. ;
  P4 = X1 0. ;

  L1 ='DROI' NBE1 P1 P2;
  L2 ='DROI' NBE1 P4 P3;

  S1 ='REGL' NBE2 L1 L2;
  L3 ='COTE' 2 S1;

 'TRAC' 'QUAL' S1;

************************************************************************
* MODELE & MATERIAU
************************************************************************
  MOD1 ='MODE'  S1   'DIFFUSION' 'FICK' 'INCO' 'HI  ' 'QHI ';
  MAT1 ='MATE'  MOD1 'KD  ' 1.D0 ;
  MAT2 ='MANU' 'CHML' MOD1 'SCAL' K 'RIGIDITE' ;

************************************************************************
* Champ de temperature pour que le GRAD(T) soit constant
************************************************************************
  COO1  ='COOR' 1 S1 ;
  EVO1  ='EVOL' 'BLEU' 'CHPO' COO1 'SCAL' L3  ;
  TEMP1 ='CHAN' 'COMP' (COO1 * GT) 'T' ;
 'TITR' 'Evolution de la TEMPERATURE avec UX';
  EVOT  ='EVOL' 'BLEU' 'CHPO' TEMP1 'T' L3  ;
 'DESS' EVOT ;

************************************************************************
* RIGIDITE
************************************************************************
  RIG1 ='COND' MOD1 MAT1;
  RIG2 ='SORE' MOD1 MAT1 MAT2 TEMP1;

************************************************************************
* CONDITIONS AUX LIMITES
************************************************************************
  CLIM1 ='BLOQ' 'HI'  L1   ;
  CLIM2 ='BLOQ' 'HI'  L2   ;


  CIMP1 ='DEPI' CLIM1 C0   ;
  CIMP2 ='DEPI' CLIM2 C1   ;

************************************************************************
* RESOLUTION
************************************************************************
  SOL1  ='RESO' (RIG1 ET RIG2 ET CLIM1 ET CLIM2) (CIMP1 ET CIMP2);
 'TITR' 'Isovaleurs de concentration en HI';
 'TRAC' SOL1 S1;

************************************************************************
* POST-TRAITEMENT
************************************************************************
* Solution NUMERIQUE
  EVONUM ='EVOL' 'ROUG' 'CHPO' SOL1 'HI  ' L3  ;
  LCNUM  ='EXTR' EVONUM 'ORDO' 1 ;

* Solution ANALYTIQUE
  EVO2   ='EVOL' 'CHPO' TEMP1 'HI' L3  ;
  LRX    ='EXTR'  EVO1 'ORDO' 1;
  LCANA  = ((('EXP' (-1*K*GT*X1 ))*C0) - (('EXP' (-1*K*GT*X0 ))*C1) +
            (('EXP' (-1*K*GT*LRX))*C1) - (('EXP' (-1*K*GT*LRX))*C0)  ) /
           ( ('EXP' (-1*K*GT*X1))      -  ('EXP' (-1*K*GT*X0))       ) ;

  EVOANA ='EVOL' 'BLEU' 'MANU' 'X' LRX 'HICA' LCANA;
 'DESS' (EVONUM ET EVOANA);

* Comparaison
  LCDIFF = LCNUM - LCANA ;
 'TITR' 'Difference entre la Solutions NUMERIQUE et ANALYTIQUE';
  EVODIF ='EVOL' 'VERT' 'MANU' 'X' LRX 'HICA' LCDIFF;
 'DESS' EVODIF;


* Critere d'erreur
 'OPTI' 'ECHO' 0;
  ERRABS ='MAXI' 'ABS' LCDIFF;
  ERRREL = ERRABS / ('ABS' (C1 - C0))      ;
 'MESS' '*********************************************' ;
 'MESS' '* Erreur MAXIMALE absolue  :' ERRABS '*';
 'MESS' '* Erreur MAXIMALE relative :' ERRREL '*';
 'MESS' '*********************************************' ;
 'SI' (ERRREL >EG 8.8D-5);
   'ERRE' 5;
 'FINS';

 'FIN';


