
************************************************************************
* Section : Langage Objets
************************************************************************
* Ce Cas-Test vérifie le bon fonctionnement de l'opérateur DIFF        *

OPTI DIME 2 ELEM SEG2;

P1 = 0. 0. ;
P2 = 0. 1. ;
P3 = 0. 2. ;

L1 = DROIT 10 P1 P2 ;
L2 = DROIT 12 P2 P3 ;
L3 = L1 ET L2 ;

*----------------------------------------------------------------------*
*               Cas de MELEME SIMPLE de TYPE identiques                *
*----------------------------------------------------------------------*
OPTI ELEM QUA4;

* Création d'un MELEME SIMPLE VIDE de type QUA4
MAILQ4VI= VIDE 'MAILLAGE';
DIFFQ4VI  = DIFF MAILQ4VI MAILQ4VI;
LISTYP  = DIFFQ4VI ELEM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP  = EXTR LISTYP 1;

* Test de Validation de l'opérateur
SI ('NEG' NOMTYP 'QUA4');
  MESS 'Erreur de DIFF : Type d_Elements avec MAILLAGES VIDE ';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 1);
  MESS 'Erreur de DIFF : nombre de Sous-Type avec MAILLAGES VIDES';
  ERRE 5;
FINS;


* Création d'un MELEME SIMPLE de type QUA4 (NON VIDE)
MAILQ4 = L1 TRAN 4 (1. 0.);
DIFFQ4 = DIFF MAILQ4 MAILQ4;
LISTYP = DIFFQ4 ELEM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP = EXTR LISTYP 1;

* Test de fonctionnement de l'opérateur
SI ('NEG' NOMTYP 'QUA4');
  MESS 'Erreur de DIFF : Type d_Elements QUA4 attendu';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 1);
  MESS 'Erreur de DIFF : 1 Sous-Type';
  ERRE 5;
FINS;


* Vérification qu'avec ET on retrouve bien la même chose
MAILET   = MAILQ4VI ET MAILQ4;
LISTYP   = MAILET ELEM 'TYPE';
DIMLIST1 = DIME LISTYP;

MAILETDI = DIFFQ4VI ET MAILQ4;
LISTYP   = MAILETDI ELEM 'TYPE';
DIMLIST2 = DIME LISTYP;

* Test de fonctionnement de l'opérateur
SI ('NEG' DIMLIST1 DIMLIST2);
  MESS 'Erreur de DIFF : Les Sous-Types QUA4 ne sont pas fusionnes';
  ERRE 5;
FINS;


*----------------------------------------------------------------------*
*               Cas de MELEME SIMPLE de TYPE différent                 *
*----------------------------------------------------------------------*
OPTI ELEM TRI3;

* Création d'un MELEME SIMPLE VIDE de type TRI3
MAILT3 = VIDE 'MAILLAGE';
DIFFT3Q4 = DIFF MAILT3 MAILQ4;
LISTYP = DIFFT3Q4 ELEM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP1 = EXTR LISTYP 1;

* Test de fonctionnement de l'opérateur
SI ('NEG' NOMTYP1 'QUA4');
  MESS 'Erreur de DIFF : Type d_Elements QUA4 absents';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 1);
  MESS 'Erreur de DIFF : nombre de Sous-Type, 1 attendu';
  ERRE 5;
FINS;



* Création d'un MELEME SIMPLE de type TRI3 NON VIDE
MAILT3 = CHAN MAILQ4 TRI3;
DIFFT3Q4 = DIFF MAILT3 MAILQ4;
LISTYP = DIFFT3Q4 ELEM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP1 = EXTR LISTYP 1;
NOMTYP2 = EXTR LISTYP 2;

* Test de fonctionnement de l'opérateur
SI ('NEG' NOMTYP1 'TRI3');
  MESS 'Erreur de DIFF : Type d_Elements TRI3 absents';
  ERRE 5;
FINS;
SI ('NEG' NOMTYP2 'QUA4');
  MESS 'Erreur de DIFF : Type d_Elements QUA4 absents';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 2);
  MESS 'Erreur de DIFF : nombre de Sous-Type';
  ERRE 5;
FINS;


*----------------------------------------------------------------------*
*                        Cas de MELEME COMPLEXE                        *
*----------------------------------------------------------------------*
MAILC1  = MAILT3 ET MAILQ4;
DIFFCOM = DIFF MAILC1 MAILC1 ;
LISTYP  = ELEM DIFFCOM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP  = EXTR LISTYP 1;

* Test de fonctionnement de l'opérateur
* Modif SG 2016/12/01 : maintenant, en cas de maillage vide, on met le
* type d'element courant plutot que POI1...
*SI ('NEG' NOMTYP 'POI1');
itycou = 'VALE' 'ELEM' ;
'SI' ('NEG' NOMTYP itycou);
  MESS 'Erreur de DIFF : Type d_Elements ' itycou ' absents';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 1);
  MESS 'Erreur de DIFF : nombre de Sous-Type, 1 attendu';
  ERRE 5;
FINS;


OPTI ELEM SEG3;
VIDSEG3 = VIDE MAILLAGE;

OPTI ELEM CUB8;
VIDCUB8 = VIDE MAILLAGE;

MAILC1  = MAILT3 ET MAILQ4;
MAILC2  = VIDSEG3 ET VIDCUB8 ET MAILC1;
DIFFCOM = DIFF MAILC1 MAILC2 ;
LISTYP  = ELEM DIFFCOM 'TYPE';
DIMLIST = DIME LISTYP;
NOMTYP  = EXTR LISTYP 1;

* Test de fonctionnement de l'opérateur
*old SI ('NEG' NOMTYP 'POI1');
*old  MESS 'Erreur de DIFF : Type d_Elements POI1 absents';
itycou = 'VALE' 'ELEM' ;
'SI' ('NEG' NOMTYP itycou);
  MESS 'Erreur de DIFF : Type d_Elements ' itycou ' absents';
  ERRE 5;
FINS;

SI ('NEG' DIMLIST 1);
  MESS 'Erreur de DIFF : nombre de Sous-Type, 1 attendu';
  ERRE 5;
FINS;

FIN;






