************************************************************************
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
*......../........./........./........./........./........./........./72
* Calcul de facteurs de forme en axisymétrique pour une cavite
* comportant un jeu
* Comparaison a des expressions analytiques (P.YALA)
*
************************************************************************

OPTI DIME 2 MODE AXIS ELEM QUA4 ;
OPTI ECHO 0 ;

************************************************************************
* CALCUL TEST EN GEOMETRIE 2D
************************************************************************
MESS ' CALCUL TEST ERREUR CALCUL FACTEUR de FORME sur CAVITE AXI '  ;
MESS ;
*

* MAILLAGE CAVITE avec NOYAU CENTRAL

xra1 = 4.0 ; xra2 = 2.0 ;
larg = 0.01 ; haut = 0.01 ;
ptA = 0.0 0.0 ; ptB = larg 0.0 ; ptC = larg haut  ; ptD = 0.0 haut  ;
*
* jeux
jbas = 0.00025 ; jcot = 0.0005 ; jhau = 0.001 ;
* autre cas: on minimise tous les jeux
*jbas = 0.0001 ; jcot = 0.0001 ; jhau = 0.0001 ;

* maillage
ciHAU = droit ptD ptC  dini ( larg / xra1 ) dfin ( larg / xra2 ) ;
ciCOT  = droit ptC ptB  dini ( haut / xra1 ) dfin ( haut / xra2 ) ; ;
ciBAS  = droit ptB ptA  dini ( larg / xra2 ) dfin ( larg / xra1 ) ;  ;
cavint = cibas et cicot et cihau ;
*trac elem cavint ;
ceBAS = ( inve ciBAS ) moins ( 0.0 jbas ) ;
ceCOT = ( inve ciCOT ) plus  ( jcot 0.0 ) ;
ceHAU = ( inve ciHAU ) plus  ( 0.0 jhau ) ;
cavext = ceBAS droit 1 ceCOT droit 1 ceHAU  ;
*trac elem cavext ;

cavi = cavint et cavext ;
*trac elem cavi ;


************************************************************************
* CALCUL ANALYTIQUE des FACTEURS de FORME
*solutions analytiques sur les faces basses  element 9 8 7 de CAVI
************************************************************************
xr = table ; tY = table ; tZ =  table ; FFD = table ; FFA  = table ;
xr. 1 = coor 1 ( ( cavi elem 9) poin initial ) ;
xr. 2 = coor 1 ( ( cavi elem 8) poin initial ) ;
xr. 3 = coor 1 ( ( cavi elem 7) poin initial ) ;
*
* CALCUL des FACTEURS de FORME des couples de DISQUES ELEMENTAIRES
REPETER Bdef  3 ;
FFD. &Bdef  = table  ;
fin Bdef ;
*
REPETER BLF  3 ;
X1 = xr. &BLF / jbas    ;
 REPETER BLFF  3 ;
 Y1 = jbas / xr. &BLFF  ;
 Z1 = 1.0 + ( ( 1.0 + ( X1 * X1 ) ) * ( Y1 * Y1 ) ) ;
 FFD. &BLFF . &BLF  =
 0.5 * ( Z1 - ( ( Z1 * Z1 ) - (( 4.0 * X1 * X1 * Y1 * Y1) ) ** 0.5 )) ;
 fin BLFF ;
fin BLF ;
*
 F9s10 = FFD. 1 . 1 ;
*
* CALCUL des FACTEURS de FORME des ANNEAUX
REPETER BLFA  2 ;
IN1 = &BLFA ; IN2 = &BLFA + 1 ;
SA2 = ( xr. IN2 - xr. IN1 ) * ( xr. IN2 + xr. IN1 ) ;
SD1 = xr. IN1 * xr. IN1 ; SD2 = xr. IN2 * xr. IN2 ;
*
FFA. &BLFA = ( ( SD2  * FFD. IN2 . IN2 )
 + ( SD1  * FFD. IN1 . IN1 )
 -   ( 2.0 * SD1  * FFD. IN1 . IN2 )  ) /  SA2 ;
fin BLFA ;
*
F8s11 = FFA. 1  ;
F7s12 = FFA. 2  ;
*
 AL1 = xr. 1 / jbas ;
 AL2 = ( xr. 2 - xr. 1 ) / jbas ;
 AL3 = ( xr. 3 - xr. 2 ) / jbas ;
*
MESS ' RESULTATS du CALCUL ANALYTIQUE ';
MESS ;
MESS '* Allongement pour FF(9,10) ' AL1 ' FF(9,10) = ' F9s10 ;
MESS ;
MESS '* Allongement pour FF(8,11) ' AL2 ' FF(8,11) = ' F8s11 ;
MESS ;
MESS '* Allongement pour FF(7,12) ' AL3 ' FF(7,12) = ' F7s12 ;
MESS ;
MESS ;

************************************************************************
* CALCUL CAST3M des FACTEURS de FORME
************************************************************************
mess ' CALCUL NUMERIQUE ';

EMAC = 0.4 ;

MDRX2 = MODE cavi 'THERMIQUE' 'RAYONNEMENT' 'CAVITE'   ;
MARX2 = MATE MDRX2 'EMIS' EMAC ;
*

*opti IMPI 2; comm pour controle du calcul;
 CHVRC2 = ffor MDRX2 MARX2  ;
opti impi 0;

* pour les jeux faibles il peut etre necessaire d'augmenter le nombre
* de points d integration:
ng = 10 ; comm integration de type Gauss pour faces  eloignees ;
np = 50 ; comm integration de type Trapezes pour faces proches ;
* opti impi 2;
* CHVRC2 = ffor MDRX2 MARX2 ng np ;
* opti IMPI 0 ;
*

fft = CHVRC2 ;

* impression ?

 nface = nbel (extr fft 'MAIL');
* mess 'nface' nface;

repe iface nface;
   fi = extr fft MIDL 1 &iface 1;
*   mess ' ';
*   mess ' elem: ' &iface ' Fij: ' ;
*    list fi ;
fin iface ;
mess ;

* elements (7,12)
i = 7 ; j = 12 ;
fi = extr fft 'MIDL' 1 i 1;
*list fi ;
fij = extr fi 'MIDL' 1 j 1 ;
mess 'faces: ' i j  F  fij ;

ecart1 = (fij/F7s12 ) - 1.  ;
mess 'faces: ' i j ' ecart relatif: ' ecart1;


* elements (8,11)
i = 8 ; j = 11 ;
fi = extr fft 'MIDL' 1 i 1;
*list fi ;
fij = extr fi 'MIDL' 1 j 1 ;
mess 'faces: ' i j  F  fij ;
ecart2 = (fij/F8s11 )  - 1. ;
mess 'faces: ' i j ' ecart relatif: ' ecart2;


* elements (9,10)
i = 9 ; j = 10 ;
fi = extr fft 'MIDL' 1 i 1;
*list fi ;
fij = extr fi 'MIDL' 1 j 1 ;
mess 'faces: ' i j  F  fij ;

ecart3 = (fij/F9s10 )  - 1. ;
mess 'faces: ' i j ' ecart relatif: ' ecart3;

crit = 5e-3;
si( ((abs ecart1) > crit) ou ((abs ecart2) > crit)
     ou ((abs ecart1) > crit) );
   mess ' probleme facteurs de forme en axis config. face a face ';
   erre 5 ;
sinon;
   erre 0 ;
finsi;


FIN ;




