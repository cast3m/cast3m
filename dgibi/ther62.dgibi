* fichier :  ther62.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

SAUT PAGE ;
*-------------------------------------------------------------------*
* TEST THER62
*                                                                   *
*    TEST DES CL DE TEMPERATURE POUR coq4 ET coq3                   *
*    ------------------------------------------------------------   *
*                                                                   *
*               TEMPERATURE IMPOSEE + CONVECTION                    *
*                                   + FLUX                          *
*                                   + SOURCE                        *
*                                                                   *
* Ce test permet de v{rifier le bon fonctionnement des divers       *
* op{rateurs thermiques de CASTEM2000 dans le cas des {l{ments      *
* coq4 et coq3 .                                                    *
*                                                                   *
* UNE PLAQUE RECTANGULAIRE CONSTITUEE D'{L{ments COQ3 et coq4 est   *
* soumise @ une temp{rature impos{e @ une de ses extr{mit{s et suc- *
* essivement @ une condition de convection forc{e, @ une condition  *
* de flux impos{ et @ une condition de source volumique impos{e.    *
*                                                                   *
* Les r{sultats   sont compar{s a ceux  obtenus                     *
* par un calcul {quivalent utilisant les {l{ments massifs. Le mail- *
* -lage n'utilise que des cub8 et est oriente differement pour sim- *
* -plifier le calcul.                                               *
* On compare les temperatures sur la face superieure; le long de la *
* ligne AS2 DS2                                                     *
* Test elabore par Mr Dowlatyari 03/91.                             *
* Test modifie le 14/05/91                                          *
*                                                                   *
*                                                                   *
*                                                                   *
*-------------------------------------------------------------------*
********************************************************************
**************** 1/ calcul en massif *******************************
********************************************************************
OPTION DIME 3 ELEM QUA4 ;
*qua3 ;
opti elem cub8;
opti trac bens;
graph = 'N';
TEMPS ;
*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
As1= 0.25 0. 0.; AS2 =0.25 1.5 0. ; AS3 = 0.25 3. 0.;

BS1= 0.25 0. 2.; BS2 =0.25 1.5 2.; BS3 = 0.25 3. 2.;

CS1= 0.25 0. 4.; CS2 =0.25 1.5 4; CS3 = 0.25 3. 4.;

DS1= 0.25 0. 6.; DS2 =0.25 1.5 6.; DS3 = 0.25 3. 6.;


*-------------  CREATION DES SURFACES ET VOLUMES ------------------     -
*
DAD = AS3 d 3 DS3;
DDD = DS3 D 2 DS1;
DDA = DS1 D 3 AS1;
DAA = AS1 D 2 AS3;
QTOTS = DALL DAD DDD DDA DAA;
V = -0.5 0. 0.;
QTOT = QTOTS VOLU 1 TRAN V;

OEIL = 5. 1.5 3.;

ge0 = face 1 qtot;
scon = elem ge0 appu larg ddd;

ge1 = face 3 qtot;
dfa = qtot poin proc ( 0. 1.5 0. );
sinf = elem ge1 appu larg dfa;


ssou = elem qtot appu larg ddd;


si ( NEG GRAPH 'N' );
  trac oeil qtot;
  trac oeil scon;
  trac oeil sinf;
finsi;

*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE QTOT THERMIQUE ISOTROPE ;
MOD2 = MODE SCON thermique  CONVECTION  ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2  ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE SINF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE SINF 'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE SINF 'T   ' ;
EE3 = DEPI BB3 10. ;
BB  = BB1 ET BB2 ET BB3 ;
EE  = EE1 ET EE2 ET EE3  ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'T' 200. ;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200.  SCON;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1  1000. SSOU ;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET BB ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
*---------------- POST-TRAITEMENT:                                      -
*---------------- EXTRACTION DES VALEURS CARACTERISTIQUES -----------
*
*LISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
*
AS2 = QTOT POIN PROC ( 0.25 1.5 0.);
BS2 = QTOT POIN PROC ( 0.25 1.5 2.);
CS2 = QTOT POIN PROC ( 0.25 1.5 4.);
DS2 = QTOT POIN PROC ( 0.25 1.5 6.);


T1Aq = EXTR CHTER1 T AS2 ;T1Bq =EXTR CHTER1 T BS2 ;
T1Cq = EXTR CHTER1 T CS2 ;T1Dq =EXTR CHTER1 T DS2 ;
T2Aq = EXTR CHTER2 T AS2 ;T2Bq =EXTR CHTER2 T BS2 ;
T2Cq = EXTR CHTER2 T CS2 ;T2Dq =EXTR CHTER2 T DS2 ;
T3Aq = EXTR CHTER3 T AS2 ;T3Bq =EXTR CHTER3 T BS2 ;
T3Cq = EXTR CHTER3 T CS2 ;T3Dq =EXTR CHTER3 T DS2;


LCON = PROG T1Aq T1Bq T1Cq T1Dq;
LFLU = PROG T2Aq T2Bq T2Cq T2Dq;
LSOU = PROG T3Aq T3Bq T3Cq T3Dq;
*LIST LCON; LIST LFLU; LIST LSOU;

*******************************************************************
********************* 2/ CALCUL EN COQ3 ET COQ4 *******************
*******************************************************************


TITRE 'C.L. DE TEMPERATURE - ELEMENTS  COQ3 COQ4' ;
OPTION DIME 3 ELEM QUA4 ;
*TRI3 ;
TEMPS ;
*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
A1 = 0. 0. 0. ;         A2 = 0. 1.5 0.         ;
B1 = 1.4142 0. 1.4142 ; B2 = 1.4142 1.5 1.4141 ;
C1 = 2.8284 0. 2.8284 ; C2 = 2.8284 1.5 2.8284 ;
D1 = 4.2426 0. 4.2426 ; D2 = 4.2426 1.5 4.2426 ;
A3 = 0. 3. 0. ;
B3 = 1.4142 3. 1.4142 ;
C3 = 2.8284 3. 2.8284 ;
D3 = 4.2426 3. 4.2426 ;
*-------------  CREATION DES QUADRANGLES ET TRIANGLES -------------------
*
QQ01 = MANU QUA4 A1 A2 B2 B1 ;
QQ02 = MANU TRI3 B1 B2 C2  ;
QQ03 = MANU TRI3 B1 C2 C1  ;
QQ04 = MANU QUA4 C1 C2 D2 D1  ;
QQ05 = MANU QUA4 A2 A3 B3 B2 ;
QQ06 = MANU TRI3 B2 B3 C3  ;
QQ07 = MANU TRI3 B2 C3 C2  ;
QQ08 = MANU QUA4 C2 C3 D3 D2 ;
*
QTOT = QQ01 ET QQ02 ET QQ03 ET QQ04 ET QQ05 ET QQ06 ET QQ07 ET QQ08 ;

SI ( NEG GRAPH 'N');
OEIL = -1000. 0. 1000. ;
TRACE OEIL QTOT ;
FINSI;

QSOUR = QQ04 ET QQ08 ;

*
DD3 = D 1 A3 A2 ;
DD4 = D 1 A2 A1 ;
L_INF = DD3 ET DD4 ;
*
S_COT = QQ04 ET QQ08 ;
*
* ATTENTION : il faut r{orienter les {l{ments (OPERATEUR ORIENT)
* si on n'est pas sur que tous les elements sont orientes d'une
* facon coh{rante
*
*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE QTOT THERMIQUE ISOTROPE COQ4 COQ3 ;
MOD2 = MODE S_COT thermique CONVECTION SUPERIEURE COQ4 COQ3 ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. 'EPAI' 0.5 ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE L_INF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE L_INF 'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE L_INF 'T   ' ;
EE3 = DEPI BB3 10. ;
BB  = BB1 ET BB2 ET BB3 ;
EE  = EE1 ET EE2 ET EE3  ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'TSUP' 200.;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200.  S_COT SUPE ;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1  1000. QSOUR ;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET BB ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
*---------------- POST-TRAITEMENT: TRACE DES CHAMPS RESULTATS -----------
*---------------- ET EXTRACTION DES VALEURS CARACTERISTIQUES -----------
*
*LISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
*
OEIL= -1000. 0. 1000. ;
*
SI ( NEG GRAPH 'N');

TITRE 'ELEMENTS COQ3,COQ4 : CONVECTION' ;
CHTR1I = 'EXCO' CHTER1 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTR1I ;
TITRE 'ELEMENTS COQ3,COQ4 : FLUX IMPOSE' ;
CHTR2I = 'EXCO' CHTER2 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTR2I ;
TITRE 'ELEMENTS COQ3,COQ4  : SOURCE' ;
CHTR3I = 'EXCO' CHTER3 'TINF' 'NOID' 'TINF' ;
TRACER OEIL QTOT CHTR3I ;

FINSI;
*
T1A = EXTR CHTER1 TSUP A2 ;T1B=EXTR CHTER1 TSUP B2 ;
T1C = EXTR CHTER1 TSUP C2 ;T1D=EXTR CHTER1 TSUP D2 ;
T2A = EXTR CHTER2 TSUP A2 ;T2B=EXTR CHTER2 TSUP B2 ;
T2C = EXTR CHTER2 TSUP C2 ;T2D=EXTR CHTER2 TSUP D2 ;
T3A = EXTR CHTER3 TSUP A2 ;T3B=EXTR CHTER3 TSUP B2 ;
T3C = EXTR CHTER3 TSUP C2 ;T3D=EXTR CHTER3 TSUP D2 ;
*
*************************************************************
********************* 3/ comparaison entre les 2 calculs ****
*************************************************************
si ( NEG graph 'N');
DIST = PROG 0. 2. 4. 6. ;

TEMPC1= PROG T1A T1B T1C T1D ;
EV1R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lcon ;
EV1C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC1 ;
TAB = 'TABLE' ;
TAB.1 = 'MARQ PLUS' ;
TAB.2 = 'MARQ ETOI' ;
TITRE 'COQ3 ET COQ4 ;TEMPERATURE + CONVECTION ;+ : MASSIF,* : COQUE' ;
DESSIN TAB (EV1R ET EV1C )  ;

TEMPC2= PROG T2A T2B T2C T2D ;
EV2R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lflu ;
EV2C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC2 ;
TITRE 'COQ3 ET COQ4;TEMPERATURE + FLUX IMPOSE ;+ : MASSIF,* : COQUE';
DESSIN TAB (EV2R ET EV2C )  ;
TEMPC3= PROG T3A T3B T3C T3D ;
EV3R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' lsou ;
EV3C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC3 ;
TITRE 'COQ3 ET COQ4 ;TEMPERATURE + SOURCE ;+  MASSIF,* : COQUE ';
DESSIN TAB (EV3R ET EV3C )  ;
TEMPS ;
finsi;
*************************************************************
*************** 4/ TEST DE FONCTIONNEMENT *******************
*************************************************************

ERCA= ABS (( T1A-T1Aq)/T1A);
ERCB= ABS (( T1B-T1Bq)/T1B);
ERCC= ABS (( T1C-T1Cq)/T1C);
ERCD= ABS (( T1D-T1Dq)/T1D);
ERFA= ABS (( T2A-T2Aq)/T2A);
ERFB= ABS (( T2B-T2Bq)/T2B);
ERFC= ABS (( T2C-T2Cq)/T2C);
ERFD= ABS (( T2D-T2Dq)/T2D);
ERSA= ABS (( T3A-T3Aq)/T3A);
ERSB= ABS (( T3B-T3Bq)/T3B);
ERSC= ABS (( T3C-T3Cq)/T3C);
ERSD= ABS (( T3D-T3Dq)/T3D);

ERTOT = PROG ERCA ERCB ERCC ERCD ERFA ERFB ERFC  ERFD ERSA ERSB
ERSC ERSD;
ermax = maxi ertot;
temps;
SI ( ERMAX <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
fin;
























