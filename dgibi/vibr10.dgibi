* fichier :  vibr10.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*
************************************************************************
*
* Mots-clés : Vibrations, calcul modal, machines tournantes,
*             reponse frequentielle
*
* Calcul de la reponse au balourd
*
* Creation : J Charpentier ?
*            Test de l'option 'BALOURD' de l'operateur VIBC
*
* Modif : B Prabel, 2015-09-09 : plutot que d'utiliser VIBC 'BALOU',
*         on utilisera desormais IMPE, RESOU et RECO
*
************************************************************************

OPTI 'DIME' 3 'ELEM' SEG2 ;
*
*donnees
P1 =  1. 0. 0. ;
m1 = 40. ;
k1 = 4.e5 ;
f1 = ((k1 / m1) ** 0.5) / (2. * pi) ;
MESS 'f1 =' f1 ;
*
TR1 = MANU 'POI1' P1 ;
*
*Matrices de masse et de rigidite :
MMass = MANU 'RIGI' TR1 (MOTS 'UY' 'UZ')
        (PROG m1 0. 0. m1) ;
MRigi = MANU 'RIGI' TR1 (MOTS 'UY' 'UZ')
        (PROG k1 0. 0. k1) ;
*
*deformees modales :
Phi1 = MANU 'CHPO' TR1 2 'UY' 1. 'UZ' 0. ;
Phi2 = MANU 'CHPO' TR1 2 'UY' 0. 'UZ' 1. ;
*
*points reperes des modes
PR1 = 0. 0. 0. ;
PR2 = 0. 0. 0. ;
*
*base des mods reels :
TBasR = TABLE 'BASE_MODALE' ;
TBasR.MODES = TABLE 'BASE_DE_MODES' ;
TBasR.MODES . 'MAILLAGE' = TR1 ;
TBasR.MODES . 1 = TABLE 'MODE' ;
TBasR.MODES . 1 . 'NUMERO_MODE' = 1 ;
TBasR.MODES . 1 . 'POINT_REPERE' = PR1 ;
TBasR.MODES . 1 . 'FREQUENCE' = f1 ;
TBasR.MODES . 1 . 'MASSE_GENERALISEE' = m1 ;
TBasR.MODES . 1 . 'DEPLACEMENTS_GENERALISES' =
  k1 - (2 * pi * f1) ;
TBasR.MODES . 1 . 'DEFORMEE_MODALE' = Phi1 ;
TBasR.MODES . 2 = TABLE 'MODE' ;
TBasR.MODES . 2 . 'NUMERO_MODE' = 2 ;
TBasR.MODES . 2 . 'POINT_REPERE' = PR2 ;
TBasR.MODES . 2 . 'FREQUENCE' = f1 ;
TBasR.MODES . 2 . 'MASSE_GENERALISEE' = m1 ;
TBasR.MODES . 2 . 'DEPLACEMENTS_GENERALISES' =
  k1 - (2 * pi * f1) ;
TBasR.MODES . 2 . 'DEFORMEE_MODALE' = Phi2 ;
*
*vitesse (rad/s)
omeg1 = 19.0985 ;
*
*-projection des matrices sur la base réelle :
  MMasP = PJBA TBasR MMass ;
  MRigP = PJBA TBasR MRigi ;
mess 'MMasP='; list MMasP;
mess 'MRigP='; list MRigP;

*-ecriture impedance
  MMasP2 = IMPE MMasP 'MASSE';
  MRigP2 = IMPE MRigP 'RAIDEUR';
mess 'MMasP2='; list MMasP2;
mess 'MRigP2='; list MRigP2;
  MdynP2 = MRigP2 et ((omeg1 ** 2) * MMasP2);
*
*-calcul de la force d'excitation du balourd
  MxD = 1e-4 ;
  Fy1s = MxD * (omeg1 ** 2) ;
  Fz1s = (-1) * MxD * (omeg1 ** 2) ;
  Fy1c = MxD * (omeg1 ** 2) ;
  Fz1c = MxD * (omeg1 ** 2) ;
*
  F1s = MANU 'CHPO' P1 2 'FY' Fy1s 'FZ' Fz1s ;
  F1c = MANU 'CHPO' P1 2 'FY' Fy1c 'FZ' Fz1c ;
  PjF1s = PJBA F1s TBasR ;
  PjF1c = (PJBA F1c TBasR) 'EXCO' 'FALF' 'IFAL' ;
*
  PjFBal1 = PjF1s et PjF1c;
mess 'PjFBal1='; list PjFBal1;
*
*-calcul de la reponse :
  PjQBal1 = RESOU MdynP2 PjFBal1;
mess 'PjQBal1='; list PjQBal1;

*-recombinaison modale
  QRe1 = EXCO PjQBal1 'ALFA' 'ALFA';
  QIm1 = EXCO PjQBal1 'IALF' 'ALFA';
  RepRe1 = RECO QRe1 TBasR;
  RepIm1 = RECO QIm1 TBasR;
mess 'RepRe1='; list RepRe1;
mess 'RepIm1='; list RepIm1;
*
*-calcul des pt et gd axes de l'ellipse :
   YR1 = EXTR RepRe1 'UY' P1 ;
   ZR1 = EXTR RepRe1 'UZ' P1 ;
   MESS YR1 ZR1 ;
* systeme isotrope --> ellipse = cercle

*-resultats attendus :
An1= (MxD * (omeg1 ** 2)) / (k1 - ((omeg1 ** 2) * m1));
MESS 'resultats attendus : ' An1 ;
*
Si (((Abs(ZR1+An1)/An1) < 0.001)
 et ((Abs(YR1-An1)/An1) < 0.001));
  Erre 0;
Sinon;
  Erre 5;
Finsi;

FIN ;










