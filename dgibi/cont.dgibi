* fichier : cont.dgibi
************************************************************************
* Section : Maillage Autres
************************************************************************
'OPTION' 'ECHO' 1 ;
************************************************************************
* NOM         : CONT
* DESCRIPTION : Quelques cas-tests pour l'operateur CONT
*
*               Cas 1 : avant la fiche 9607, le résultat contenait des
*                       noeuds nuls.
*
*               Cas 2 : avant la fiche ????, plantage car résultat
*                       correct=maillage vide.
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SEMT/LTA)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 04/12/2017, version initiale
* HISTORIQUE : v1, 04/12/2017, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
interact = faux ;
graph = faux ;
*
*
* verifie que deux maillages sont identiques au sens de DIFF
* cad en etant tolerant
*
'DEBPROC' EGAMAIL ;
  'ARGUMENT' mail1*'MAILLAGE' ;
  'ARGUMENT' mail2*'MAILLAGE' ;
  lega = 'EGA' ('NBEL' ('DIFF' mail1 mail2)) 0 ;
  'RESPRO' lega ;
'FINPROC' ;
*
lok = vrai ;
*
* Cas-test 1
*
'OPTI' 'DIME' 2 'ELEM' 'TRI3' ;
*
* Créer 10 noeuds
*
tn = 'TABL' ;
'REPE' nn 10 ;
   tn . &nn = (0. 0.) ;
'FIN' nn ;
*
* Liste des noeuds des triangles
*
ln1 = 'LECT' 1 4 2 6 4 7 6 2 4 2 8 ;
ln2 = 'LECT' 2 2 1 4 7 8 2 10 2 3 2 ;
ln3 = 'LECT' 3 5 5 2 2 2 9 9 9 9 9 ;
*
mail = 'VIDE' 'MAILLAGE'/'TRI3' ;
*
'REPE' mm ('DIME' ln1) ;
*
   n1 = 'EXTR' ln1 &mm ; p1 = tn . n1 ;
   n2 = 'EXTR' ln2 &mm ; p2 = tn . n2 ;
   n3 = 'EXTR' ln3 &mm ; p3 = tn . n3 ;
*
   mail = 'ET' mail ('MANU' 'TRI3' p1 p2 p3) ;

'FIN' mm ;
*
t1 = 'CONT' mail 'TOUT' ;
t2 = 'CONT' mail ;
t3 = 'CONT' mail 'INTE' ;
*
next1 = 'LECT' 1  1  2  3  4  4  4  4  6  7  8  9   ;
next2 = 'LECT' 3  5  10 9  5  6  7  9  9  8  9  10  ;
mcext = 'VIDE' 'MAILLAGE'/'SEG2' ;
*
'REPE' mm ('DIME' next1) ;
   n1 = 'EXTR' next1 &mm ; p1 = tn . n1 ;
   n2 = 'EXTR' next2 &mm ; p2 = tn . n2 ;
   mcext = 'ET' mcext ('MANU' 'SEG2' p1 p2) ;
'FIN' mm ;
*
nint1 = 'LECT' 2  2 ;
nint2 = 'LECT' 4  9 ;
mcint = 'VIDE' 'MAILLAGE'/'SEG2' ;
*
'REPE' mm ('DIME' nint1) ;
   n1 = 'EXTR' nint1 &mm ; p1 = tn . n1 ;
   n2 = 'EXTR' nint2 &mm ; p2 = tn . n2 ;
   mcint = 'ET' mcint ('MANU' 'SEG2' p1 p2) ;
'FIN' mm ;
*
mctot = mcext 'ET' mcint ;
*
'SI' ('NON' (egamail t1 mctot)) ;
   'MESS' '!!! Cas 1 : CONT TOUT incorrect' ;
   lok = lok 'ET' faux ;
'FINS' ;
*
'SI' ('NON' (egamail t2 mcext)) ;
   'MESS' '!!! Cas 1 : CONT EXTE incorrect' ;
   lok = lok 'ET' faux ;
'FINS' ;
*
'SI' ('NON' (egamail t3 mcint)) ;
   'MESS' '!!! Cas 1 : CONT INTE incorrect' ;
   lok = lok 'ET' faux ;
'FINS' ;
*
* Cas-test 2 : on vérifie que le contour d'une enveloppe est vide
*              (théorème de Stokes)
*
'OPTI' 'DIME' 3 'ELEM' 'CUB8' ;
* maillage repris de volu.dgibi mais QUA4 au lieu de TRI3 plus
* paramètres différents
nx = 3 ; rcyl = 0.58 ; ang = 31.4 ;
*
* Maillage de l'enveloppe
*
   nx2 = '*' nx 2 ;
   mrcyl = ('*' rcyl -1.) ;
   pA = rcyl 0. 0. ; pB = 0. rcyl 0. ; pC = mrcyl 0. 0. ;
   pD = 0. mrcyl 0. ;
   pA pB pC pD = 'TOURNER' pA pB pC pD (0. 0. 0.) (0. 0. 1.) ang ;
   pE = 1. -1. 0. ; pF = 1. 1. 0. ; pG = -1. 1. 0. ; pH = -1. -1. 0. ;
*
   ctc1 = 'CER3' nx2 pA pB pC ;   ctc2 = 'CER3' nx2 pC pD pA ;
   cerb = ctc1 'ET' ctc2 ;
   carb = 'DROIT' nx pE pF 'DROIT' nx pG 'DROIT' nx pH 'DROIT'
      nx pE ;
   bas = 'SURFACE' ('ET' carb ('INVERSE' cerb)) 'PLANE' ;
   hau cerh carh = 'PLUS' bas cerb carb (0. 0. 2.) ;
   suri = 'REGLER' nx cerb cerh ;
   sure = 'REGLER' nx carb carh ;
* On fait attention à l'orientation (normale rentrante pour env)
   envo = bas 'ET' ('INVE' hau) 'ET' suri 'ET' ('INVE' sure) ;
   'SI' graph ;
      'TRAC' 'CACH' envo 'TITR' 'Enveloppe' ;
   'FINS' ;
*
   vol = 'VOLU' envo ;
   env = 'ENVE' vol ;
   cnt = 'CONT' env 'NOID' ;
*   cnt = 'CONT' env ;
*
'SI' ('NON' (egamail env envo)) ;
   'MESS' '!!! Cas 2 : ENVE incorrect' ;
   lok = lok 'ET' faux ;
'FINS' ;
*
'SI' ('NON' ('EGA' ('NBEL' cnt) 0)) ;
   'MESS' '!!! Cas 2 : CONT incorrect' ;
   lok = lok 'ET' faux ;
'FINS' ;
*
* Test final
*
'SI' ('NON' lok) ;
   'ERREUR' 5 ;
'SINON' ;
   'SAUT' 1 'LIGN' ;
   'MESSAGE' ('CHAINE' 'Tout sest bien passe !') ;
'FINSI' ;
*
'SI' interact ;
   'OPTION' 'ECHO' 1 ;
   'OPTION' 'DONN' 5 ;
'FINSI' ;

*
* End of dgibi file CONT
*
'FIN' ;





