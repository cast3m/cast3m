* fichier :  mistral_axi2.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test mistral_axi2.dgibi: Jeux de donnees        *
*     ---------------------------------------         *
*                                                     *
  opti echo 0 ;
  opti dime 2 mode axis elem qua8 ;
*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*                                                     *
*  MODELE MISTRAL                                     *
*    ELASTICITE ET VISCOPLASTICITE                    *
*                                                     *
*  MAILLAGE:                                          *
*    TUBE CYLINDRIQUE (AXISYMETRIQUE)                 *
*                                                     *
*  CHARGEMENT:                                        *
*    PRESSION IMPOSEE : RAMPE RAPIDE ET PLATEAU       *
*******************************************************
*
* Geometrie
*
* ------------------------------------------
  RI = 49.5E-3 ; RE = 50.5E-3 ; H = 0.010 ;
* ------------------------------------------
   RM = (RI+RE)/2. ;
   RE2MRI2 = (RE**2)-(RI**2) ;
   COEFI = (RI**2)/RE2MRI2 ;
   E = RE-RI ;
   VECR = 1. 0. ;
*
   PIB = RI 0. ;  PEB = RE 0. ;
   PIH = RI H  ;  PEH = RE H  ;
*
   LB = droi 2 PIB PEB ;
   LE = droi 1 PEB PEH ;
   LH = droi 2 PEH PIH ;
   LI = droi 1 PIH PIB ;
*
* Maillage
*
   EPROU = 'DALLER' LB LE LH LI ;
*  'TRAC' EPROU ;
*
   POMB = RM 0. ; POMH = RM H ;
   PMB = EPROU poin proc POMB ;
   PMH = EPROU poin proc POMH ;
*
* Modele et materiau
*
   MODG = mode EPROU mecanique elastique orthotrope
                               viscoplastique mistral ;
*
* ------------------------------------------------------------
  SENSIP1 = 1 ; SENSIP2 = -3 ; ICBASE = 0 ;
  fichier = '../divers/mimataxi2_par' ;
  PDILT    E1 E2 E3 NU12 NU23 NU13 MU12 MU23 MU13
  PNBRE PCOHI PECOU PEDIR PRVCE PECRX PDVDI PCROI PINCR
    = @mistpar fichier SENSIP1 SENSIP2 ;
* ------------------------------------------------------------
   MAZY = mate MODG 'YG1 ' E1   'YG2 ' E2   'YG3 ' E3
                    'NU12' NU12 'NU23' NU23 'NU13' NU13
                    'G12 ' MU12
                    'ALP1' 0.     'ALP2' 0.      'ALP3' 0.
 'DILT' PDILT 'NBRE' PNBRE 'COHI' PCOHI 'ACOU' PECOU 'EDIR' PEDIR
 'RVCE' PRVCE 'ECRX' PECRX 'DVDI' PDVDI 'CROI' PCROI 'INCR' PINCR
                    'SIP1' SENSIP1  'SIP2' SENSIP2  'IBAS' ICBASE
                    'DIRECTION' VECR ;
*
* Conditions aux limites
*
   CLB = bloq uz LB ;
   CLH = rela ense uz LH ;
   CL = CLB et CLH ;
*
* Chargement
*
   T1 = 0.001 ; T2 = 2000.001 ;
   DT0 = 0.0001 ;
   DT1 = 5. ;
   TEMPSCAL = prog 0. pas DT0 T1 pas DT1 100.001
                   150.001 200.001 300.001
                   500.001 700.001 1000.001 1400.001 T2 ;
   SIGTT1 = 400.E6 ;
   TT0 = 623.15 ; TT1 = 623.15 ; TT2 = 623.15 ;
   PHIT0 = 2.E25 ;
   PPSI0 = 112.23 ;
*
   TEMPS = prog 0. T1 T2 ;
*
   PRI = E/RI ;
   PRESI = pres mass MODG (PRI) LI ;
   SZPRI = PRI*COEFI ;
   PRHPI = (-1.)*SZPRI ;
   PRESHPI = pres mass MODG (PRHPI) LH ;
   EVPI = evol manu TEMPS (prog 0. SIGTT1 SIGTT1) ;
   CHAPI = char meca (PRESI et PRESHPI) EVPI ;
*
   TT = manu chpo EPROU 1 'T' 1. ;
   EVTT = evol manu TEMPS (prog TT0 TT1 TT2) ;
   CHTT = char 'T' TT EVTT ;
*
   PHI = manu chpo EPROU 1 'FI' 1. ;
   EVFI = evol manu TEMPS (prog 0. 0. 0.) ;
   CHFI = char 'FI' PHI EVFI ;
*
   CHA = CHAPI et CHTT et CHFI ;
*
* Valeurs initiales
*
   VINT0 = zero MODG 'VARINTER' ;
   FIT0 = manu chml MODG 'FIT ' PHIT0 type 'SCALAIRE' 'STRESSES' ;
   VINT0 = VINT0 + FIT0 ;
   PSI0 = manu chml MODG 'PSI ' PPSI0 type 'SCALAIRE' 'STRESSES' ;
   VINT0 = VINT0 + PSI0 ;
*
* Calcul
*
   TAB = TABLE ;
   TAB.'VARIABLES_INTERNES' = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL ;
   TAB.'CARACTERISTIQUES' = MAZY ;
   TAB.'MODELE' = MODG ;
   TAB.'CHARGEMENT' = CHA ;
   TAB.'VARIABLES_INTERNES' . 0 = VINT0 ;
   TAB.'TEMPS_CALCULES' = TEMPSCAL ;
   PASAPAS TAB ;
*
* Traitement des resultats
*
   TEM = TAB.'TEMPS' ;
   SIG = TAB.'CONTRAINTES' ;
   DEP = tab.'DEPLACEMENTS' ;
   VI  = tab.'VARIABLES_INTERNES' ;
   NT = dime TEMPSCAL-1 ;
* ---------------
   ERMAX = 0.01 ;
* ---------------
   T = TEM.NT ;
*
   SIGT = SIG.NT ;
   SIGT_PO = chang chpo MODG SIGT ;
   SIGRR = extr SIGT_PO SMRR PMB ;
   SIGTT = extr SIGT_PO SMTT PMB ;
   SIGZZ = extr SIGT_PO SMZZ PMB ;
   SIGRZ = extr SIGT_PO SMRZ PMB ;
*
   DEPT = DEP.NT ;
   EPSIT = epsi DEPT MODG ;
   EPSIT_PO = chang chpo MODG EPSIT ;
   EPSRR = extr EPSIT_PO EPRR PMB ;
   EPSTT = extr EPSIT_PO EPTT PMB ;
   EPSZZ = extr EPSIT_PO EPZZ PMB ;
   EPSRZ = (extr EPSIT_PO GARZ PMB)/2. ;
*
   VIT = VI.NT ;
   VIT_PO = chang chpo MODG VIT ;
   EPSFRR = extr VIT_PO EP11 PMB ;
   EPSFTT = extr VIT_PO EP12 PMB ;
   EPSFZZ = extr VIT_PO EP13 PMB ;
   EPSFRZ = extr VIT_PO EP15 PMB ;
*
   mess ;
   mess 'T :                          ' T ;
   mess ;
   mess 'SIGRR SIGTT SIGZZ :    ' SIGRR SIGTT SIGZZ ;
   mess 'SIGRZ :                ' SIGRZ ;
   mess 'EPSRR EPSTT EPSZZ :    ' EPSRR EPSTT EPSZZ ;
   mess 'EPSRZ :                ' EPSRZ ;
   mess 'EPSFRR EPSFTT EPSFZZ : ' EPSFRR EPSFTT EPSFZZ ;
   mess 'EPSFRZ               : ' EPSFRZ ;
   mess ;
*
*   SIGT_RR = exco SIGT 'SMRR' ;
*   trac SIGT_RR MODG ;
*   SIGT_TT = exco SIGT 'SMTT' ;
*   trac SIGT_TT MODG ;
*   SIGT_ZZ = exco SIGT 'SMZZ' ;
*   trac SIGT_ZZ MODG ;
*   SIGT_RZ = exco SIGT 'SMRZ' ;
*   trac SIGT_RZ MODG ;
*
* ------------------------------------------------------------
   YOUNGR = 1.1E11 ; YOUNGZ = 0.9E11 ; YOUNGT = 1.E11 ;
   POISRZ = 0.3 ; POISZT = 0.3 ; POISRT = 0.3 ;
   MURZ = 4.E10 ;
* ------------------------------------------------------------
   SIGRR1 = (-1.)*SIGTT1*E/(2.*RM) ;
   SIGZZ1 = SIGTT1*SZPRI ;
   EPSERR1 = (SIGRR1/YOUNGR)-((POISRT/YOUNGR)*SIGTT1)
                            -((POISRZ/YOUNGR)*SIGZZ1) ;
   EPSETT1 = (SIGTT1/YOUNGT)-((POISZT/YOUNGZ)*SIGZZ1)
                            -((POISRT/YOUNGR)*SIGRR1) ;
   EPSEZZ1 = (SIGZZ1/YOUNGZ)-((POISRZ/YOUNGR)*SIGRR1)
                            -((POISZT/YOUNGZ)*SIGTT1) ;
* --------------------------
   EPSFRR1 = -0.00160863 ;
   EPSFTT1 = 0.001695925 ;
   EPSFZZ1 = -0.00008729 ;
* --------------------------
   EPSRR1 = EPSERR1 + EPSFRR1 ;
   EPSTT1 = EPSETT1 + EPSFTT1 ;
   EPSZZ1 = EPSEZZ1 + EPSFZZ1 ;
*
   ERSIGRR = (SIGRR/SIGRR1) - 1. ;
   ERSIGTT = (SIGTT/SIGTT1) - 1. ;
   ERSIGZZ = (SIGZZ/SIGZZ1) - 1. ;
   P_ER = prog ERSIGRR ERSIGTT ERSIGZZ ;
   ERSIGMA = maxi P_ER 'ABS' ;
*
   EREPSRR = (EPSRR/EPSRR1) - 1. ;
   EREPSTT = (EPSTT/EPSTT1) - 1. ;
   EREPSZZ = (EPSZZ/EPSZZ1) - 1. ;
   P_ER = prog EREPSRR EREPSTT EREPSZZ ;
   EREPSMA = maxi P_ER 'ABS' ;
*
   EREPSFRR = (EPSFRR/EPSFRR1) - 1. ;
   EREPSFTT = (EPSFTT/EPSFTT1) - 1. ;
   EREPSFZZ = (EPSFZZ/EPSFZZ1) - 1. ;
   P_ER = prog EREPSFRR EREPSFTT EREPSFZZ ;
   EREPSFMA = maxi P_ER 'ABS' ;
*
   si (ERSIGMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur SIGRR, SIGTT ou SIGZZ > ' ERMAX ;
    mess ERSIGRR ERSIGTT ERSIGZZ ;
   finsi ;
   si (EREPSMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur EPSRR, EPSTT ou EPSZZ > ' ERMAX ;
    mess EREPSRR EREPSTT EREPSZZ ;
   finsi ;
   si (EREPSFMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur relative) sur EPSFRR, EPSFTT ou EPSFZZ > ' ERMAX ;
    mess EREPSFRR EREPSFTT EREPSFZZ ;
    mess ;
   finsi ;
   P_ER = prog ERSIGMA EREPSMA EREPSFMA ;
   ERMA = maxi P_ER ;
   si (ERMA > ERMAX) ;
    ERRE 5 ;
   finsi ;

   fin ;





