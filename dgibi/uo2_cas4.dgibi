* fichier :  uo2_cas4.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Endommagement
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test uo2_cas4.dgibi: Jeux de données
*     ---------------------------------------
*
'OPTI' 'DIME' 2 'MODE' 'PLAN' 'GENE' ;
'OPTI' 'ELEM' 'QUA4'                 ;
**'OPTI' 'ECHO' 0 ;
'TEMPS' 'ZERO'                       ;
L = 'MOT' LIST ; F = 'MOT' FIN       ;
*
***********************************************************
*                TEST DE VALIDATION                       *
*                ------------------                       *
*               MODELE UO2                                *
*                                                         *
*  MAILLAGE:                                              *
*     EPROUVETTE CARRE                                    *
*                                                         *
*  CHARGEMENT:                                            *
*     DEPLACEMENT IMPOSE                                  *
*     TEMPERATURE                                         *
*     POROSITE CONSTANTE                                  *
*     DENSITE DE FISSIONS CONSTANTE                       *
*     PRIMAIRE - MECANISMES 1 ET 2 - IRRADIATION          *
*     DENSIFICATION - GONFLEMENT                          *
***********************************************************
*
H    = 1. ;
*

OO   = 0.              0.            ;
A1   = H               0.            ;
A2   = H               H             ;
A3   = 0.              H             ;
*
LB   = 'DROIT'  2 OO A1             ;
LD   = 'DROIT'  5 A1 A2             ;
LH   = 'DROIT' 3  A2 A3             ;
LG   = 'DROIT' 10  A3 OO             ;

*
SU1  = surf ( LB et  LD et LH et  LG ) 'PLAN' ;
**trac su1 ;

*
* -------*
* Modele *
* -------*
MODL1= MODE SU1 MECANIQUE ELASTIQUE VISCOPLASTIQUE UO2 'DPGE' OO ;
*
* ----------------------*
* Materiau GATT_MONERIE *
* ----------------------*
TE1  = 1300. + 273.  ;
PO   = 4.9E-2        ;
DF1  = 1.E18         ;
*
TA = @GATTPAR '../divers/fichier_gatt' ;
*
TA.'POR0' = PO      ;

MATR11 = 'MATE' MODL1  'YOUN' TA.YOUN  'NU' (TA.NU)  'RHO' (TA.RHO)
'ALPH' (TA.ALPH) ;
*
MATR12 = 'MATE' MODL1
 'R'    (TA.'R')    'DG0' (TA.'DG0') 'DG'  (TA.'DGCR')
 'K1'   (TA.'K1')   'M1'  (TA.'M1')  'Q1'  (TA.'Q1')  'N1'   (TA.'N1')
 'K2'   (TA.'K2')   'M2'  (TA.'M2')  'Q2'  (TA.'Q2')  'N2'   (TA.'N2')
 'OMEG' (TA.'OMEG') 'H'   (TA.'H')   'Q'   (TA.'Q')   'BETA' (TA.'BETA')
 'K'    (TA.'K')    'A'   (TA.'A')   'Q3'  (TA.'Q3')  'N3'   (TA.'N3')
 'CR'   (TA.'CR')   'CR1' (TA.'CR1') 'CR2' (TA.'CR2') 'CR3'  (TA.'CR3');
*
MATR13 = 'MATE' MODL1
 'KP'   (TA.'KPUO')   'AP' (TA.'AP')  'BP'  (TA.'BP')  'QP'  (TA.'QP') ;
*
bumi = 1.e10*(TA.'BUMI') ;
MATR14 = 'MATE' MODL1
'ADEN' TA.'ADEN' 'KGON' TA.'KGON'.
 'POR0' (TA.'POR0') 'BUMI' bumi 'EFIS' (TA.'EFIS') ;

*
* -----------------------------*
* Materiau partie OTTOSEN      *
* -----------------------------*
 MATR17 = 'TAIL' MODL1 ;
*
* Resistance au cisaillement 'GS  ' par defaut: 'YOUN'*1.8E-4
* Ouverture a rupture 'WRUP' = 0 --> 'WRUP' = 2*'GFTR'/'LTR '
* Relation bilineaire 'BILI' ou 'EPSB' = 0 --> 'BILI' = 0 (ouverture)
*
 XGFTR = 100.8  ; XGFTR1=XGFTR ; XGFTR2=XGFTR ;
 XLTR  = 5.E6 ; XLTR1 =XLTR  ; XLTR2 =XLTR  ;
* Deformation a rupture 'EPSR' par defaut: 3*'LTR '/'YOUN'
* XYOUN = 'MAXI' ('VARI' 'NUAG' MODL1 MATREE PP)  ;
* NEPSR = 3. ; XEPSR = NEPSR * (XLTR / XYOUN) ;
*
 MATR18 = 'MATE' MODL1  'GFTR' XGFTR    'LTR ' XLTR     'BTR ' 0.2
                        'WRUP' 0.       'BILI' 0.
                        'GFT1' XGFTR1   'GFT2' XGFTR2
                        'GS1 ' 0.       'GS2 ' 0.
                        'LTR1' XLTR1    'LTR2' XLTR2
                        'WRU1' 0.       'WRU2' 0.
                        'BIL1' 0.       'BIL2' 0.                     ;
*
MATR1 = MATR11 'ET' MATR12 'ET' MATR13 'ET' MATR14 'ET'
        MATR17 'ET' MATR18      ;
*
* -----------------------*
* Conditions aux limites *
* -----------------------*
CLYB = 'BLOQ' 'UY' LB                  ;
CLXG = 'BLOQ' 'UX' LG                  ;
CLT  = CLYB 'ET' CLXG                  ;
*
TMIL = 1.E6 ;
TFIN = 2.E6 ;
*


* ------------------------------*
* Chargement en temperature     *
* ------------------------------*
CHTEMP   = 'MANU' 'CHPO' SU1 1 'T' 1.           ;
EVT      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                         ('PROG' 400. 500.)       ;
CHARTEMP = 'CHAR' 'T' CHTEMP EVT                ;


chpor0   = 'MANU' 'CHPO' SU1 1 'PORO' 1.;
EVT      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                         ('PROG' 0.049  0.049) ;
CHARPOR = 'CHAR' 'PORO' chpor0 EVT  ;
* -------------------------------------*
* Chargement cte en densite de fission *
* -------------------------------------*
CHFISS   = 'MANU' 'CHPO' SU1 1 'DFIS' 1.        ;
EVF      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                         ('PROG' DF1 DF1)       ;
CHARFISS = 'CHAR' 'DFIS' CHFISS EVF             ;
* ---------------------------------*
* Chargement en deplacement impose *
* ---------------------------------*

EVP      = 'EVOL' 'MANU' ('PROG' 0.  (2.*TFIN))
                         ('PROG' 0.  1.) ;
CL3 = 'BLOQ' LH 'UY' ;
DEP3 = 'DEPI' cl3 100. ;
CHARMECA = 'CHAR' 'DIMP' dep3 evp ;

CHARTOT  = CHARMECA 'ET' CHARTEMP 'ET' CHARFISS 'ET' CHARPOR ;
* ---------------------------------------*
* Variables internes initiales 'PORO'=PO *
* ---------------------------------------*
VAR00 = 'ZERO' MODL1 'VARINTER'                                    ;
VAR01 = 'MANU' 'CHML' MODL1 'PORO' PO
                            'TYPE' 'VARIABLES INTERNES' 'STRESSES' ;
VAR0  = VAR00 + VAR01                                              ;
*
*
TAB1 = TABLE                           ;
TAB1.'TEMPERATURES' = TABLE            ;
TAB1.'VARIABLES_INTERNES'= TABLE       ;
TAB1.'BLOCAGES_MECANIQUES' = CLT   et cl3;
TAB1.'MODELE' = MODL1                  ;
TAB1.'CHARGEMENT' = CHARTOT            ;
TAB1.'TEMPERATURES' . 0 = 'MANU' 'CHPO' SU1 1 'T' 400.  ;
TAB1.'VARIABLES_INTERNES' . 0 = VAR0   ;
TAB1.'CARACTERISTIQUES' = MATR1        ;
TAB1.'TEMPS_CALCULES' = 'PROG' 0  PAS 0.1 1.2 ;

PASAPAS TAB1                           ;

**-- Verification de la valeur du taux de combustion (variable interne BU)

* Valeur theorique du taux de combustion
*  bu_th = 3.2e-11*0.270*df1*t/(0.238*10950.*(1.-0.049))

AA = 3.2e-11*0.270*DF1/(0.238*10950.*(1.-0.049)) ;

**-- Taux de combustion a l instant 1.1 s (pas de non convergence)

BU_CALC1 = 'MINI' ('EXCO' TAB1.'VARIABLES_INTERNES' . 11 'BU') ;
BU_CALC2 = 'MAXI' ('EXCO' TAB1.'VARIABLES_INTERNES' . 11 'BU') ;

ECART1 = 'ABS' (BU_CALC1-BU_CALC2) / BU_CALC1 ;

BU_TH = AA*1.1 ;
ECART2 = 'ABS' (BU_CALC1-BU_TH) / BU_TH ;

**-- Taux de combustion a l instant 1.2 s (apres non convergence)

BU_CALC = 'MAXI' ('EXCO' TAB1.'VARIABLES_INTERNES' . 12 'BU') ;
BU_TH = AA*1.2 ;
ECART3 = 'ABS' (BU_CALC-BU_TH) / BU_TH ;

'SI' (ECART1 '<EG' 1.E-8) ;
   'ERRE' 0 ;
'SINON' ;
   'ERRE' 5 ;
'FINSI' ;

'SI' (ECART2 '<EG' 1.E-8) ;
   'ERRE' 0 ;
'SINON' ;
   'ERRE' 5 ;
'FINSI' ;

'SI' (ECART3 '<EG' 1.E-8) ;
   'ERRE' 0 ;
'SINON' ;
   'ERRE' 5 ;
'FINSI' ;
'FIN' ;










