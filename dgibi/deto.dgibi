* fichier :  deto.dgibi
************************************************************************
* Section : Chimie Melange
************************************************************************
*
*---------------------------------------------------------------------
* Validation de l'opérateur DETO : Comparaison de la pression, de la
* température et de la vitesse de Chapman-Jouguet pour deux mélanges
* H2/O2/N2 entre des données expérimentales, un calcul de référence
* et l'opérateur DETO.
*---------------------------------------------------------------------
* On crée un CHAMPOINT contenant les conditions suivantes
*    Pression et température du mélange 1.atm et 291.K
*    Nombre de moles des différents constituants
*       Point P1  H2=2. O2=1. N2=3. H2O=0.
*       Point P2  H2=2. O2=1. N2=5. H2O=0.
*    Valeurs de référence à 291K ET 1atm
*                                     Vcj(m/s)
*                PCJ(atm) TCJ(K)  Calculée et mesurée
*       Point P1  15.63    3003   2033.       2055.
*       Point P2  14.39    2685   1850.       1822.
*    Référence : Combustion, flames and explosion of gases,
*                B.Lewis and G.von Elbe, page 545, Academic Press ed.
*---------------------------------------------------------------------
* Les différences observées entre le résultat du calcul DETO et le
* résultat du calcul de référence s'expliquent essentiellement par
* le fait que DETO utilise une cinétique chimique à une seule réation.
*---------------------------------------------------------------------
*
*- Initialisations
*
OPTI DIME 2 ELEM QUA4 ECHO 0 ;
P1   = 0. 0. ;
P2   = 2. 0. ;
P1P2 = P1 'DROI' 1 P2 ;
X Y  = 'COOR' P1P2 ;
CHP3 = 'MANU' 'CHPO' P1P2 1 'H2' 2. ;
CHP1 = 'MANU' 'CHPO' P1P2 1 'O2' 1. ;
CHP4 = 'MANU' 'CHPO' P1P2 1 'H2O' 0. ;
CHP2 = 'EXCO' 'SCAL' (x + 3.) 'N2' ;
CHP5 = 'MANU' 'CHPO' P1P2 1 'P' 101325. ;
CHP6 = 'MANU' 'CHPO' P1P2 1 'T' 291. ;
*
*- Calcul
*
CHPTOT = CHP1 + CHP2 + CHP3 + CHP4 + CHP5 + CHP6 ;
CHPR1 CHPR2 CHPR3 = 'DETO' CHPTOT ;
*
*- Récupération des valeurs pour comparaison
*
P01 = ('EXTR' CHPR1 'PCJ' P1) / 101325.;
T1  = 'EXTR' CHPR1 'TCJ' P1 ;
V1  = 'EXTR' CHPR1 'VCJ' P1 ;
*
P02 = ('EXTR' CHPR1 'PCJ' P2) / 101325. ;
T2  = 'EXTR' CHPR1 'TCJ' P2 ;
V2  = 'EXTR' CHPR1 'VCJ' P2 ;
*
*- Affichage
*
P1ref = 15.63 ; T1ref = 3003. ; V1cref = 2033. ; V1mref = 2055. ;
P2ref = 14.39 ; T2ref = 2685. ; V2cref = 1850. ; V2mref = 1822. ;
F1 = '(F6.2)' ; F2 = '(F5.0)' ;
'MESS' '  ' ;
CH1 = 'CHAI' 'FORMAT' F1 'Ref. Pt1 P=' P1ref
             'FORMAT' F2 ' T=' T1ref ' V=' V1cref ' , ' V1mref ;
'MESS' CH1 ;
CH1 = 'CHAI' 'FORMAT' F1 'DETO Pt1 P=' P01
             'FORMAT' F2 ' T=' T1 ' V=' V1 ;
'MESS' CH1 ;
'MESS' '  ' ;
CH1 = 'CHAI' 'FORMAT' F1 'Ref. Pt2 P=' P2ref
             'FORMAT' F2 ' T=' T2ref ' V=' V2cref ' , ' V2mref ;
'MESS' CH1 ;
CH1 = 'CHAI' 'FORMAT' F1 'DETO Pt2 P=' P02
             'FORMAT' F2 ' T=' T2 ' V=' V2 ;
'MESS' CH1 ;
'MESS' '  ' ;
*
*- Test de bon fonctionnement : Erreur relative sur la Pcj
*
EPS1  = 0.05 ;
TEST1 = ((P01 - P1ref) / P1ref 'ABS') '<' EPS1 ;
TEST2 = ((P02 - P2ref) / P2ref 'ABS') '<' EPS1 ;
'SI' (TEST1 'ET' TEST2) ;
    'ERRE' 0 ;
'SINO' ;
    'ERRE' 5 ;
'FINS' ;
'FIN' ;




