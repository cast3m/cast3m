************************************************************************
************************************************************************
* Section : Langage Objets
************************************************************************
* NOM         : VIDE
* DESCRIPTION : Quelques tests sur les objets vides suite à la fiche
*               7810 (Bug de RESULT, POIN et CHAN 'CHAM')
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 31/07/2013, version initiale
* HISTORIQUE : v1, 31/07/2013, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
'OPTION' 'DIME' 2 'ELEM' 'QUA4' ;
*
* On 'LISTE' les objets car cela peut provoquer un plantage
* souhaité quand l'objet est incorrect
*
mailv = VIDE 'MAILLAGE' ;
'LISTE' mailv ;
mails = 'MANUEL' 'QUA4' (0. 0.) (1. 0.) ( 1. 1.) (0. 1.) ;
mods = 'MODE' mails 'THERMIQUE' 'ISOTROPE' ;
'LISTE' mods ;
mats = 'MATE' mods 'K' 100. ;
'LISTE' mats ;
rigs = 'COND' mods mats ;
'LISTE' rigs ;
*
chpov = VIDE 'CHPOINT'/'DIFFUS' ;
'LISTE' chpov ;
chamv = 'CHANGER' 'CHAM' chpov mails ;
'LISTE' chamv ;
chpodv = '*' rigs chpov ;
'LISTE' chpodv ;
rc = 'RESULT' chpodv ;
'LISTE' rc ;
mailrc = 'EXTRAIRE' rc 'MAIL' ;
'LISTE' mailrc ;
nmailrc = 'NBEL' mailrc ;
'SI' ('>' nmailrc 0) ;
   ipoi = 'POIN' mailrc 'INITIAL' ;
   'LISTE' ipoi ;
   'SI' ('EGA' ('NOEUD' ipoi) 0) ;
      'MESSAGE' 'Maillage incorrect genere par RESULT' ;
      'ERREUR' 5 ;
   'FINSI' ;
'FINSI' ;
mrc = 'MAXIMUM' rc ;
'LISTE' mrc ;
'SI' ('NEG' mrc 0.) ;
   'MESSAGE' ('mrc=' mrc '  non nul') ;
   'ERREUR' 5 ;
'FINSI' ;
*
* Je n'arrive pas à faire fonctionner cette partie du cas-test
* car le niveau d'erreur GIBI est augmenté malgré la ligne suivante
*'OPTI' 'ERREUR' 'IGNORE' ;
*p1 = 'POIN' mailv 'INITIAL' ;
*p2 = 'POIN' mailv 'FINAL' ;
*p3 = 'POIN' mailv 'PROC' (0. 0.) ;
*'OPTI' 'ERREUR' 'NORMAL' ;
** Les objets p1...3 doivent être de type ANNULE car mailv est vide
*lp1 = 'EGA' ('TYPE' p1) 'POINT' ;
*lp2 = 'EGA' ('TYPE' p2) 'POINT' ;
*lp3 = 'EGA' ('TYPE' p3) 'POINT' ;
*'SI' (lp1 'OU' lp2 'OU' lp3) ;
*   'MESSAGE' 'Erreur dans loperateur POINT' ;
*   'ERREUR' 5 ;
*'FINSI' ;
*

* on teste les autres types d'objets cree par VIDE
chamvide = VIDE 'MCHAML'  ;
modevide = VIDE 'MMODEL'  ;
rigivide = VIDE 'RIGIDITE';
evolvide = VIDE 'EVOLUTIO';
progvide = VIDE 'LISTREEL';
lectvide = VIDE 'LISTENTI';
lmotvide = VIDE 'LISTMOTS';
lchpvide = VIDE 'LISTCHPO';
tablvide = VIDE 'TABLE'   ;
defovide = VIDE 'DEFORME' ;
vectvide = VIDE 'VECTEUR' ;
chrgvide = VIDE 'CHARGEME';

LIST chamvide  ;
LIST modevide  ;
LIST rigivide  ; mess (DIME rigivide);
LIST evolvide  ; mess (DIME evolvide);
LIST progvide  ; mess (DIME progvide);
LIST lectvide  ; mess (DIME lectvide);
LIST lmotvide  ; mess (DIME lmotvide);
LIST lchpvide  ; mess (DIME lchpvide);
LIST tablvide  ; mess (DIME tablvide);
LIST defovide  ;
LIST vectvide  ;
LIST chrgvide  ; mess (DIME chrgvide);

* End of dgibi file VIDE
*
'FIN' ;







