* fichier :  kp2_test.dgibi
************************************************************************
* Section : Mecanique Flambage
************************************************************************
******************************************************
*
* test de la matrice de rigidité associée à un champ
* de pression linéaire.
* Il s'agit de calculer la variation de la composante
* verticale des forces de pression  d'un reservoir
* semispherique, due à un mouvement verticale dans un champ
* de pression hydrostatique.
*
*
* reference : rapport DMT/95/
*
******************************************************
r1 = 1. ; comm 'rayon de la sphere' ;
d1 = 1. ; comm 'deplacement verticale' ;
rg = 1. ; comm 'module du gradient de la pression' ;
******************************************************
opti dime 3 elem qua4 ;
p1 = 0.001 0 0 ;
p2 = r1  0. r1 ;
c1 = 0 0 r1 ;
l1 = cerc  p1 c1  p2 dini .05 dfin .15 ;
*
** on maille un secteur de 2 degrees
surf1 = rota l1 2. 1 (0 0 0) c1 ;
elim surf1 .0015;
surf1 = rege surf1 ;
modl1 = mode surf1 mecanique coq4 dkt ;
*
rig1 = kp modl1 rg (0 0 -1) ;
dep1 = manu chpo surf1 1 uz d1 ;
f1 =  resu (rig1 * dep1) ;
fz1 = 180 * (maxi (exco fz f1));
mess 'analytique :'(-1*pi) 'numerique  :'fz1 ;
err1 = ((fz1 + pi)/fz1)*100 ;
mess ' erreur :'err1'%';

 si ((abs err1) > .03) ;
   erre 5 ;
 sinon ;
 erre 0 ;
finsi ;

fin ;






