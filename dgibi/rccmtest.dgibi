* fichier :  rccmtest.dgibi
*******************************************************************
*          TEST DES ROUTINES INTERNES DE @RCCM                    *
*                                                                 *
*      CALCUL EN AXISYMETRIQUE  ET EN 3D   SUR UN SEGMENT D APPUI *
*      COMPARAISON DES RESULTATS                                  *
*******************************************************************
OPTION DIME 2 ELEM QUA8 ;
GRAPH = 'N' ;
OO = 0 0  ; OX = 1 0 ; OY = 0 1 ;
DENS 1. ;
P1 = 10 0 ; P2 = 15 0 ;HT = 50. ;
LIG  = D DINI 3 DFIN 1. OO P1 D P2 ;
TOUT = D P1 P2  TRAN 5 ( 0 HT ) ;
CCONT = TOUT CONTOUR ;
LIINT = TOUT COTE 4 ;
*-------------------------------------------------------------
OPTION MODE AXIS ;
*-------------------------------------------------------------
MOD1 = MODE TOUT MECANIQUE ELASTIQUE ISOTROPE ;
MAT1 = MATE MOD1 YOUN  2.E5  NU .3 ;
*
FPRE = PRESSION MASS  5. MOD1  LIINT  ;
RIG1 = RIGI MOD1 MAT1 ;
C1 = (TOUT COTE 1 ) ET ( TOUT COTE 3) ;
*  -------  conditions limites -----------------------
CLM = BLOQUER DEPLA  C1  ;
SOL1 = RESO ( RIG1 ET CLM) FPRE ;
DEF0 = DEFO CCONT SOL1 0. BLAN ;
DE1 = MAXI SOL1 AVEC ( MOTS UR) ;
AMP = 1./DE1 ;
DEF1 = DEFO CCONT SOL1 AMP ROUG ;

'SI' ( NEG GRAPH N) ;
TRAC ( DEF0 ET DEF1) ;
'FINSI' ;
SIG1 = SIGMA SOL1 MOD1 MAT1 ;
*
H = 25. ;
PP1 = P1 PLUS ( 0 H ) ; PP2 = P2 PLUS (0 H ) ;
NSEG = 12 ;
**********  NI TRACES NI ECRITURES DE FICHIERS EN INTERNE *****
GRAPL*LOGIQUE =FAUX ;
ECRI*LOGIQUE  =FAUX ;NOMFICH = 'BLANC' ;
ETAT = 1 ; ICOU = 1 ;
TITRE ' RCCMCO2   '  ;
TABB1 = @RCCMCO2 ETAT  ICOU  PP1 PP2  NSEG SIG1  MOD1
                       .1  GRAPL  ECRI  NOMFICH  (0 0 ) ;
*--------------------------------------------------------------
*          3D
*--------------------------------------------------------------
OPTION DIME 3 ELEM CU20 ;
OZ = 0 0 1 ;
VOL1 = TOUT VOLU ROTA  3  20  OO OY ;
ELIM .001 VOL1 ;
VOL1 = REGE VOL1 ;
DEPLACER VOL1 TOURNER -10 OO OY ;

MOD2  =  MODE VOL1 MECANIQUE ELASTIQUE  ;
MAT2  =  MATE MOD2 YOUN  2.E5  NU .3 ;
VOENV =  VOL1 ENVEL ;
SUI1  =  VOENV POINT CYLI OO OY P1 .01 ;
SUINT = VOENV ELEM APPU STRIC   SUI1 ;
FPRE = PRESSION MASS  5. MOD2   SUINT   ;
RIG2 = RIGI MOD2 MAT2 ;
P11 = P1 TOURNER 20  OO OY ;P3 = P1 TOURNET 20  OO OY ;
OP = OO PLUS ( 0  HT 0) ;OP1 = P1 PLUS ( 0  HT 0) ;
OP3 = P3 PLUS ( 0  HT 0) ;
*------------------  conditions limites ---------------------
CL1  = (BLOQUER  DEPLA ( VOL1 POINT PLAN  OO P1  P3   .001 ))
   ET  (BLOQUER  DEPLA ( VOL1 POINT PLAN  OP OP1 OP3   .001)) ;
CL2  = (SYMT DEPLA   OO OP  P1   VOL1 .001 )
  ET   (SYMT DEPLA   OO OP  P3   VOL1 .001 )  ;
SOL2 = RESO ( RIG2 ET CL1 ET CL2 ) FPRE ;
SIG2 = SIGMA SOL2 MOD2 MAT2 ;
*
VPER = 0  0  1 ;
'SI' ( NEG GRAPH N) ;
TRAC ( VOENV ET ( D 2 PP1 PP2 COUL ROUG) ) ;
'FINSI' ;

TABB2 = @RCCMCO2 ETAT ICOU  PP1 PP2  NSEG SIG2 MOD2
                    .1  GRAPL  ECRI  NOMFICH  VPER ;


* ------------ COMPARAISON DES RESULTATS SIGNIFICATIF -------
*   ------------  ENTRE 2D  ET 3D --------------------
*    ATTENTION SMTT  2D   CORRESPOND A SMYY EN 3D
OPTION ECHO 0 ;
III = INDEX     TABB2;
REPETER BAB 3  ;
LRE1 = TABB1.&BAB ;
LRE2 = TABB2.&BAB ;
EPS1 = ABS(((EXTR LRE1 1 ) - (EXTR LRE2 1))/(EXTR LRE2 1)) ;
EPS2 = ABS(((EXTR LRE1 2 ) - (EXTR LRE2 3))/(EXTR LRE2 3)) ;
EPS3 = ABS(((EXTR LRE1 3 ) - (EXTR LRE2 2))/(EXTR LRE2 2)) ;

 MESS  ' ERREURS  ' EPS1 EPS2 EPS3 ;
*
'SI' ((EPS1 > 0.01) OU (EPS2 > 0.01) OU (EPS3 > 0.01)) ;
 ERRE  5 ;
 'SINON' ;
 ERRE  0 ;
'FINSI' ;
'FIN' BAB ;
'FIN' ;





