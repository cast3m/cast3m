* fichier :  fsi2.dgibi
************************************************************************
* Section : Fluides Vibration
************************************************************************
* CAS TEST DU 91/10/04     PROVENANCE : PETI

*******************************************
*    Test fsi2.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************
*                                         *
*              TEST FSI2                  *
*                                         *
*     CYLINDRICAL FLUID CAVITY WITHOUT    *
*                FREE SURFACE             *
*                                         *
* Calculation of the acoustic frequencies,*
* for m = 1, of cylindrical water volume  *
* of radius 1.43m and height 1.039m       *
*                                         *
*                                         *
*      P8+----------+P5                   *
*        |          |                     *
*        |          |                     *
*        |          |                     *
*        |          |                     *
*        |          |                     *
*      P7+----------+P6                   *
*                                          *
*                                         *
*    The boundary conditions are          *
*                                         *
*      dp  |                              *
*     ---- |         =  0                 *
*      dz  | h = 0.                       *
*                                         *
*      dp  |                              *
*     ---- |         =  0                 *
*      dr  | r = R                        *
*******************************************
TEMPS;

OPTI DIME 2;
OPTI MODE FOUR 1;
OPTI ELEM QUA4;
OPTI EPSI LINEAIRE;

*=========================================*
*               geometry                  *
*=========================================*

* Dimensions en metres

* Points

P5 = 1.43 1.039  ;
P6 = 1.43 0.0    ;
P7= 0. 0.        ;
P8 = 0. 1.039    ;

N1 = 10       ;
S4 = P6 D N1 P5 ; S5 =P7 D N1 P6 ;
S6 = P7 D N1 P8 ; S7 =P8 D N1 P5 ;

WATER = DALL S4 S5 S6 S7  QUELC   ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI2 : MAILLAGE';
  TRAC QUAL (WATER ET (0 0));
FINSI;

*=========================================*
*      MODE - materiau - rigidite - masse *
*=========================================*

MODLIQ = MODE WATER  LIQUIDE LQU4 ;

MATLIQ = MATE  MODLIQ
         RHO 1.E3 RORF 1.E3 CSON 1435.
         CREF 1435. LCAR 1 G 0.;

RIG1 = RIGI  MODLIQ MATLIQ  ;
MAS1 = MASS  MODLIQ MATLIQ  ;

*=========================================*
*                boundary conditions      *
*=========================================*

* No explicit boundary condition
* the boundary conditions are natural .
*=========================================*
*     calculation of the frequencies      *
*                   and                   *
*       extraction of some results        *
*=========================================*

* Use the operator VIBR. (option PROC)

FRE1 = TABLE;
FRE1.1 = 294.06;
FRE1.2 = 750.56;
LIST1 = PROG FRE1.1 FRE1.2 ;
*
RESUL = VIBR PROC LIST1 RIG1  MAS1 ;

*=========================================*
*                   results               *
*=========================================*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

FRE2 = TABL;
MOD  = TABL;
DEF  = TABL;
ERG  = TABL;

I = 0;
REPETER BLOC1 2;
  I = I + 1;
  FRE2.I = RESUL . MODES . I . FREQUENCE;
  ERG.I = 100 *
        (ABS ((FRE1.I - FRE2.I) / FRE1.I));
  MESS ' MODE '  I     ;
  MESS ' ----------';
  MESS 'Frequence theorique :' FRE1.I 'Hz';
  MESS 'Frequence calculee  :' FRE2.I 'Hz';
  MESS '    Soit un ecart de : ' ERG.I '%';
  SAUT 1 LIGN;
FIN BLOC1;

*=========================================*
*            code validation              *
*=========================================*

ERGMAX = MAXI (PROG ERG.1 ERG.2 );

SI (ERGMAX <EG 5.);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






