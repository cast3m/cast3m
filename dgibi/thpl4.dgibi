* fichier :  thpl4.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
***************************************************
* CAS TEST DU 92/03/20     PROVENANCE : TC1
* CAS TEST DU 92/03/19     PROVENANCE : PHIL
OPTI ECHO 0;
*
*********************************************************************
*   COMMISSION V.P.C.S.  : THERMO-PLASTICITE                        *
*                          THPL4.DGIBI                              *
*                                                                   *
*   SPHERE CREUSE EPAISSE EN THERMO-PLASTICITE                      *
*   CALCUL AXISYMETRIQUE                                            *
*                                                                   *
* FONCTIONS TESTEES : ELASTO-PLASTICITE PARFAITE SOUS               *
*       CHARGEMENT THERMIQUE.                                       *
*                                                                   *
*********************************************************************

OPTION DIME 2 ;
OPTION MODE AXIS;
OPTION TRACE BENS;
GRAPH='N';
OPTION ELEM QUA8;

*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  GEOMETRIE DU CYLINDRE                          *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
DENS 1. ; RA = 1. ; ZA = 0. ; PA = RA ZA;
dens 1.1; RM = 1.5; ZM = 0. ; PM1 = RM ZM;
DENS 1.11; RB = 2. ; ZB = 0. ; PB = RB ZB;
DENS 1. ; RC = 0. ; ZC = 1. ; PC = RC ZC;
DENS 1.1;                     PM2 = 0. 1.5;
DENS 1.11; RD = 0. ; ZD = 2. ; PD = RD ZD;
DENS 1. ; RO = 0. ; ZO = 0. ; PO = RO ZO ;
*

DAM1 = PA DROI -7 PM1;   DM1B = PM1 D -4 PB; DAB = DAM1 et DM1B;
DDM2 = PD DROI -4 PM2;   DM2C = PM2 D -7 PC; DDC = DDM2 et DM2C;
*
N2 = 10;
SUR1 = DAB ROTA N2 90 PO;
*
ELIM SUR1  0.0001;
*
SI(NEG GRAPH 'N');
  TITR ' SPHERE CREUSE EPAISSE : MAILLAGE';
  TRACE  SUR1;

FINSI;

list ( NBNO SUR1);
list ( NBEL SUR1);

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  CONDITIONS AUX LIMITES                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = SYMT DEPL ROTA PA PB SUR1 0.0001;
CL2 = SYMT DEPL ROTA PC PD SUR1 0.0001;
CL = CL1 ET CL2 ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*          MODELES - MATERIAUX - CARACTERISTIQUES
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MO = MODE SUR1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'PARFAIT' QUA8;

MAT = MATE MO YOUN 200e3 NU 0.3 ALPHA 1.E-5 SIGY 200 ;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*   LISTE DE VALEURS DE TEMPS OU L'ON VEUT DES RESULTATS : LIS1
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*    LE NOMBRE DE PAS DE CALCUL (PAS DE CHARGEMENT), CORRESPOND AU
* NOMBRE DE VALEURS DE TEMPS OU L'ON VEUT DES RESULTATS.
*    UN PAS ELASTIQUE (DEBUT PLASTIFICATION) JUSQU'A 100C.
*    UN PAS PLASTIQUE TOUT LES 10C APRES.
*
*
T0 = 0.;    T1 = 1.;     T2 = 2.;    T3 = 3.;
T4 = 4.;    T5 = 5.;     T6 = 6.;    T7 = 7.;
T8 = 8.;    T9 = 9.;     T10 = 10.;  T11 = 11.;
T12 = 12.;  T13 = 13.;   T14 = 14.;  T15 = 15.;
T16 = 16.;  T17 = 17.;   T18 = 18.;
*
si complet;
LIS1 = PROG T0 T1 T2 T3 T4 T5 T6 T7 T8 T9 T10 T11 T12 T13
              T14 T15 T16 T17 T18;
sinon;
LIS1 = PROG T0 T1 ;
finsi;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*   CHAMPS RELATIFS A LA PROCEDURE NONLIN                           *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*  CHAMPS DE TEMPERATURE REPARTIE SUR SUR1 AUX DIFFERENTS INSTANTS
*     DE LA LISTE OU L'ON VEUT DES RESULTATS (CF. LIST1)
*
*===================================================================*
*                                                                   *
* CALCUL D'UN CHPOINT CHP1 DE TEMPERATURE POUR UN GRADIENT DE       *
*   TEMPERATURE DE UN DEGRE CELSIUS, SOIT DT = 1C                  *
*                                                                   *
* DONNEES : - DT EST CROISSANT DE 0 @ DTMAX,                        *
*           - POUR A=1. < R < B=2., LA REPARTITION DE TEMPERATURE   *
*          EST : DT = T(A) - T(B) > 0.                              *
*                T(R) = DT/R * AB/(A-B)                             *
*                                                                   *
* CALCUL POUR DT = 1C, DE LA REPARTITION DE TEMPERATURE            *
*      SOIT      T(R) = 2. / R                                      *
*                                                                   *
*===================================================================*
*
* EXTRACTION DES CHPOINTS DES COORDONNEES R ET Z DU MAILLAGE SUR1.
*
CHPSUR1 = COOR 1 SUR1;
CHPSUR2 = COOR 2 SUR1;
*
* CREATION DU CHPOINT DE TEMPERATURE POUR DT = 1C
*
CHPSUR = ( (CHPSUR1**2 ) + (CHPSUR2**2) )**(-0.5);
CHP2 = 2. * CHPSUR;
*
* TRANSFORMATION DU SOUS-TYPE "SCAL" EN SOUS-TYPE "T" POUR CHP2
*    UTILISATION DE L'OPERATEUR VARI
*
LIS10 = PROG 0. 1000.;
LIS20 = PROG 0. 1000.;
EVOL2 = EVOL MANU SCAL LIS10 T LIS20;
*
CHP1 = VARI CHP2 EVOL2 T;
*
* CHARGEMENT DE LA TABLE DES CHPOINTS DE TEMPERATURE
*
*
TEMP = TABLE;TEMPER = TABLE;
TEMP . 0 = T0;TEMP . 1 = T1;
TEMP . 2 = T2;TEMP . 3 = T3;
TEMP . 4 = T4;TEMP . 5 = T5;
TEMP . 6 = T6;TEMP . 7 = T8;
TEMP . 8 = T8;TEMP . 9 = T9;
TEMP . 10 = T10;TEMP . 11 = T11;
TEMP . 12 = T12;TEMP . 13 = T13;
TEMP . 14 = T14;TEMP . 15 = T15;
TEMP . 16 = T16;TEMP . 17 = T17;
TEMP . 18 = T18;

TEMPER . 0 =  MANU CHPO SUR1 1 T 0.;DT = 98.;
TEMPER . 1 = (CHP1 * DT);DT = 110.;
TEMPER . 2 = (CHP1 * DT);DT = 120.;
TEMPER . 3 = (CHP1 * DT);DT = 130.;
TEMPER . 4 = (CHP1 * DT);DT = 140.;
TEMPER . 5 = (CHP1 * DT);DT = 150.;
TEMPER . 6 = (CHP1 * DT);DT = 160.;
TEMPER . 7 = (CHP1 * DT);DT = 170.;
TEMPER . 8 = (CHP1 * DT);DT = 180.;
TEMPER . 9 = (CHP1 * DT);DT = 188.;
TEMPER . 10 = (CHP1 * DT);DT = 196.;
TEMPER . 11 = (CHP1 * DT);DT = 210.;
TEMPER . 12 = (CHP1 * DT);DT = 220.;
TEMPER . 13 = (CHP1 * DT);DT = 230.;
TEMPER . 14 = (CHP1 * DT);DT = 240.;
TEMPER . 15 = (CHP1 * DT);DT = 250.;
TEMPER . 16 = (CHP1 * DT);DT = 255.;
TEMPER . 17 = (CHP1 * DT);DT = 264.83;
TEMPER . 18 = (CHP1 * DT);

CHA2 = CHAR 'T' TEMP TEMPER;

*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*   CREATION D'UN CHAMPS DE FORCE NUL : THERMO-ELASTICITE            *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
LFORCE = PROG 0. 0.;
LLIS1  = PROG 0. 18.;
*
FOR1 = FORCE FR 1. PA;
CHPO1 = RESU FOR1;
EVOL1 = EVOL MANU TEMPS LLIS1 FORCE LFORCE;
CHA1 = CHAR 'MECA' CHPO1 EVOL1;
*
*
TAB1 = TABLE;
TAB1.'MODELE' = MO ;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'CHARGEMENT' = (CHA1 ET CHA2);
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'TEMPS_SAUVES' = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;

*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               PROCEDURES PASAPAS                               *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
PASAPAS TAB1;

*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               EXTRACTION DES RESULTATS                       *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MESS ' **************************************************************';
MESS ' *   COMMISSION V.P.C.S.  : THERMO-PLASTICITE                 *';
MESS ' *               FICHIER  : SSNA06PLAPN.DGIBI                 *';
MESS ' *                                                            *';
MESS ' *   SPHERE CREUSE EPAISSE EN THERMO-PLASTICITE               *';
MESS ' *   CALCUL AXISYMETRIQUE                                     *';
MESS ' *                                                            *';
MESS ' * FONCTIONS TESTEES : ELASTO-PLASTICITE PARFAITE             *';
MESS ' *                     SOUS CHARGEMENT THERMIQUE.             *';
MESS ' *                                                            *';
MESS ' **************************************************************';
MESS ' *     CAS PLASTICITE SANS ECROUISSAGE ISOTROPE LINEAIRE      *';
MESS ' **************************************************************';
SAUT 2 LIGN;
*
MESS ' Les rsultats thoriques sont tirs de l article';
MESS ' "Thermo-elastic behaviour of strain-hardening thich-walled';
MESS ' sphere", D Durban, International journal of solids structures';
MESS ' vol 19, nx7, pp 643-652, 1983';
*===================================================================*
* resultats a 98 degres : indice T1
*===================================================================*
*
* on cherche pour r = 1.mm, 1.5mm, et 2.mm les valeurs suivantes :
*      smrr (contrainte radiale), smtt (contrainte tangentielle)
*          et ur (deplacement radial).
*    La temperature de 98 degres celsius correspond a la premiere
* plastification.
*
NN1 = 1; NN2 = 2;
NN3 = 3; NN4 = 4;
NN5 = 5; NN6 = 6;
NN7 = 7; NN8 = 8;
NN9 = 9; NN10 = 10;
NN11 = 11; NN12 = 12;
NN13 = 13; NN14 = 14;
NN15 = 15; NN16 = 16;
NN17 = 17; NN18 = 18;

MESS ' *------------------------------------------------*';
MESS ' * Resultats a 98 degres : premiere plastifiction *';
MESS ' *------------------------------------------------*';
SAUT 1 LIGN;
*
* Deplacements
*
DEP98 = TAB1.DEPLACEMENTS.NN1;
*
UR98 = TABLE; UR980 = TABLE;
UR98.1 = EXTR DEP98 UR PA; UR980.1 = 1.26E-3;
UR98.2 = EXTR DEP98 UR PM1; UR980.2 = 2.08E-3;
UR98.3 = EXTR DEP98 UR PB; UR980.3 = 2.52E-3;
*
ERGUR98 = TABLE;
I = 0;
REPETER BLOC1 3;
  I = I + 1;
  SI (I <EG 1) ; MESS ' r = 1.0 mm';FINSI;
  SI ((I <EG 2) ET (I >EG 2)); MESS ' r = 1.5 mm';FINSI;
  SI (I >EG 3); MESS ' r = 2.0 mm'; FINSI;
*
ERGUR98.I = 100 * ( ABS (( UR98.I - UR980.I) / (UR980.I+1e-6)));
  MESS ' Deplacement radial theorique :' UR980.I 'mm';
  MESS ' Deplacement radial calcule   :' UR98.I 'mm';
  MESS '    Soit un ecart de : ' ERGUR98.I '%';
  SAUT 1 LIGN;
FIN BLOC1;
*
* Contraintes
*
CHEL1 = TAB1.CONTRAINTES.NN1;
CONT98 = CHAN CHPO MO CHEL1;
*
SMRR98 = TABLE; SMRR980 = TABLE;
SMRR98.1 = EXTR CONT98 SMRR PA; SMRR980.1 = 0.;
SMRR98.2 = EXTR CONT98 SMRR PM1; SMRR980.2 = -38.52;
SMRR98.3 = EXTR CONT98 SMRR PB; SMRR980.3 = 0.;
*
SMTT98 = TABLE; SMTT980 = TABLE;
SMTT98.1 = EXTR CONT98 SMTT PA; SMTT980.1 = -200.;
SMTT98.2 = EXTR CONT98 SMTT PM1; SMTT980.2 = 5.93;
SMTT98.3 = EXTR CONT98 SMTT PB; SMTT980.3 = 80.;
*
ERGSRR98 = TABLE; ERGSTT98 = TABLE;
I = 0;
REPETER BLOC2 3;
   I = I + 1;
   SI (I <EG 1); MESS ' r = 1.0 mm';   FINSI;
   SI ((I <EG 2) ET (I >EG 2)); MESS ' r = 1.5 mm';FINSI;
   SI (I >EG 3); MESS ' r = 2.0 mm';   FINSI;
*
   ERGSRR98.I = 100 * ( ABS (( SMRR98.I - SMRR980.I ) /
   ( SMRR980.I+1e-6 )));

   MESS '  Contrainte radiale theorique      :' SMRR980.I 'MPa';
   MESS '  Contrainte radiale calculee       :' SMRR98.I 'MPa';
   MESS '        Soit un ecart de : ' ERGSRR98.I '%';
   si ((( abs smrr980.i ) <eg 10.) et ((abs smrr98.i) <eg 10.));
       mess 'CET ECART EST NON-SIGNIFICATIF';
   finsi;

   SAUT 1 LIGN;
*
   ERGSTT98.I = 100 * ( ABS (( SMTT98.I - SMTT980.I ) /
     (  SMTT980.I+1e-6)));
   MESS '  Contrainte tangentielle theorique :' SMTT980.I 'MPa';
   MESS '  Contrainte tangentielle calculee  :' SMTT98.I 'MPa';
   MESS '        Soit un ecart de : ' ERGSTT98.I '%';
   si ((( abs smtt980.i ) <eg 10.) et ((abs smtt98.i) <eg 10.));
        mess 'CET ECART EST NON-SIGNIFICATIF';
   finsi;

   SAUT 1 LIGN;
FIN BLOC2;
si complet;
*
*===================================================================*
* resultats a 196 degres : indice T11
*===================================================================*
*
* On cherche - pour r = 1. mm et r = 2. mm smtt et smrr,
*            - la valeur du rayon correspondant a la limite de
*    plastification, et pour ce rayon, on veut smtt et smrr. En
*    remarque on peut dire que smeq = 200.MPa = smtt + smrr.
*            - la valeur du  rayon ou il y a un etat de contrainte
*    hydrostatique, et la valeur de cette contrainte.
*
SAUT 1 LIGN;
MESS ' *------------------------*';
MESS ' * Resultats a 196 degres *';
MESS ' *------------------------*';
SAUT 1 LIGN;
*
* smtt et smrr  pour r = 1. mm et r = 2. mm
*
CHEL1 = TAB1.CONTRAINTES.NN11; CHV1 = VMIS MO CHEL1;
CONT196 = CHAN CHPO MO chel1;
*
SMTT196 = TABLE; SMTT1960 = TABLE;
ERTT196  = TABLE;
SMTT196.1  = EXTR CONT196 SMTT PA; SMTT1960.1 = -200.;
SMTT196.2  = EXTR CONT196 SMTT PB; SMTT1960.2 = 153.82;
*
SMRR196 = TABLE; SMRR1960 = TABLE;
SMRR196.1  = EXTR CONT196 SMRR PA; SMRR1960.1 = 0.;
SMRR196.2  = EXTR CONT196 SMRR PB;
* SMRR1960.2 = 'NON-CONNUE';
*
SAUT 1 LIGN;
MESS ' r = 1.0 mm';
MESS ' Contrainte radiale theorique      :' SMRR1960.1 'MPa';
MESS ' Contrainte radiale calculee       :' SMRR196.1 'MPa';
si ((abs SMRR196.1) <eg 5.);
     MESS 'ECART NON-SIGNIFICATIF';
finsi;
SAUT 1 LIGN;
*
MESS ' Contrainte tangentielle theorique :' SMTT1960.1 'MPa';
MESS ' Contrainte tangentielle calculee  :' SMTT196.1 'MPa';
 ERTT196.1 = 100 * ( ABS (( SMTT196.1 - SMTT1960.1 ) /
 (  SMTT1960.1+1e-6)));
MESS '        Soit un ecart de : ' ERTT196.1 '%';
SAUT 1 LIGN;
*
MESS ' r = 2.0 mm';
* MESS ' Contrainte radiale theorique :' SMRR1960.1 'MPa';
MESS ' Contrainte radiale calculee  :' SMRR196.2 'MPa';
SAUT 1 LIGN;
*
MESS ' Contrainte tangentielle theorique :' SMTT1960.2 'MPa';
MESS ' Contrainte tangentielle calculee  :' SMTT196.2 'MPa';
ERTT196.2 = 100 * ( ABS (( SMTT196.2 - SMTT1960.2 ) /
  (  SMTT1960.2+1e-6)));
MESS '        Soit un ecart de : ' ERTT196.2 '%';
SAUT 1 LIGN;
*
* Recherche de la valeur du rayon de limite plastique
*
*     On cherche cette valeur aux points de gauss pour les contraintes
*
*    Creation d'un champs CHEL2 par elements des valeurs des rayons
* (en spherique) aux points de gauss pour les contraintes. Alors le
* champs CHEL2 a la meme structure que le champs de contrainte CHEL1.
*

chsur1 = coor 1 chel1;
chsur2 = coor 2 chel1;
CHEL2 = ((CHSUR1 ** 2) + (CHSUR2 ** 2)) ** 0.5;


*   Boucle sur les elements numerotes de 1 a 11 de la sous-zone 1, la
* valeur maximale etant inferieure a 1.5 mm. On affiche seulement le
* rayon maximum (RAYON1MAX).
*
RAY1MAX = 1.;
IELEM = 0;
REPETER BLOC5 11;
    IELEM = IELEM + 1;
*
* Boucle sur les 9 points de gauss de l'element numerote IELEM
*
    IPGAUS = 0;
    REPETER BLOC4 9;
       IPGAUS = IPGAUS + 1;
       SMTT1 = EXTR CHEL1 SMTT 1 IELEM IPGAUS;
       SMRR1 = EXTR CHEL1 SMRR 1 IELEM IPGAUS;
       SEQU1 = SMRR1 - SMTT1;
*
*  test sur la contrainte equivalente : proche de 200 MPa ?
*
       SI (( SEQU1 <EG 205.) ET (SEQU1 >EG 195.));
*
*    Dans ce cas, on cherche la valeur du rayon pour laquelle la contrainte
* equivalente est proche de la contrainte limite plastique.
*
        RAYON1 = EXTRA CHEL2 SCAL 1 IELEM IPGAUS;
        RAY1MAX = MAXI (PROG RAY1MAX RAYON1);

        Si ( RAYON1 EGA RAY1MAX ) ;
                 SMTTR1 = SMTT1;
                 SMRRR1 = SMRR1;
                 SMVMR1 = extr chv1 'SCAL' 1 IELEM IPGAUS;
                 xel = extr CHSUR1 'SCAL' 1 IELEM IPGAUS;
                 yel = extr CHSUR2 'SCAL' 1 IELEM IPGAUS;

         finsi;
      FINSI;
*
* Fin boucle sur les points de gauss, et reinitialisation de IPGAUSS a 0.
*
   FIN BLOC4;
*
* Fin de boucle sur les elements numerotes de 1 a 11;
*
FIN BLOC5;
RAYTH1 = 1.1328;
SMTTR10 = -249.88;
SMRRR10 = -49.88;
erra1 = 100*(abs ( ( RAYTH1 - RAY1MAX)/RAYTH1));
erttra1 = 100*(abs ( (SMTTR1 - smttr10)/smttr10));
errra1 = 100*(abs ( (SMRRR1 -smrrr10)/smrrr10));


* Affichage de la limite plastique de la premiere zone.
*
MESS ' Rayon definisant la fin de la premiere zone plastique.';
MESS ' ------------------------------------------------------';
SAUT 1 LIGN;
MESS ' Rayon1 calcul   = ' RAY1MAX ' mm';
MESS ' Rayon1 thorique = ' RAYTH1  ' mm';
MESS ' ECART           = ' erra1   '% ';
saut 1 lign;
MESS ' SMTT POUR CE RAYON =' SMTTR1 ' MPA';
MESS ' SMTT thorique     =' SMTTR10 ' MPA';
MESS ' ECART             =' erttra1 ' MPA';
saut 1 lign;
MESS ' SMRR POUR CE RAYON =' SMRRR1 ' MPA';
MESS ' SMRR thorique     =' SMRRR10 ' MPA';
mESS ' ECART             =' errra1 ' MPA';
saut 1 lign;
MESS ' CONTRAINTE DE VON MISES =' SMVMR1 'MPA';
MESS 'LES COORDONNEES DU POINT DE GAUSS VALENT ';
MESS ' X= ';list xel;
MESS ' Y= ';list yel;
 SAUT 1 LIGN;
*===================================================================*
* resultats a 260 dgres : indice T18
*===================================================================*
*
SAUT 1 LIGN;
MESS ' *--------------------------------*';
MESS ' * Resultats a 264 degres celsius *';
MESS ' *--------------------------------*';
MESS ' Apparition de la deuxieme zone plastique (r = 2.)';
MESS ' On cherche la limite de la premiere zone plastique.';
MESS ' La valeur theorique est proche de 1.1968mm';
SAUT 1 LIGN;
*
* On cherche la limite de la premiere zone plastique.
*
CHEL1 = TAB1.CONTRAINTES.NN18;  CP1 = chan CHPO mo chel1;
cVM1  = VMIS mo chel1;


RAY2MAX = 1.;
IELEM = 0;


REPETER BLOC5 10;
    IELEM = IELEM + 1;
    IPGAUS = 0;
    REPETER BLOC4 9;
      IPGAUS = IPGAUS + 1;
      SMTT1 = EXTR CHEL1 SMTT 1 IELEM IPGAUS;
      SMRR1 = EXTR CHEL1 SMRR 1 IELEM IPGAUS;
      SEQU1 = SMRR1 - SMTT1;
*
      SI (( SEQU1 <EG 201.) ET (SEQU1 >EG 199.));
        RAYON2 = EXTRA CHEL2 SCAL 1 IELEM IPGAUS;
        RAY2MAX = MAXI (PROG RAY2MAX RAYON2);
*       MESS ' Pour r =' RAYON2 'mm , sigma equivalente =' SEQU1 'MPa';

        SI (RAYON2 EGA RAY2MAX);
            SMTTR2 = SMTT1;
            SMRRR2 = SMRR1;
            SMVMR2 = extr CVM1 'SCAL' 1 IELEM IPGAUS;
            xel = extr chsur1 'SCAL' 1 IELEM IPGAUS;
            yel = extr chsur2 'SCAL' 1 IELEM IPGAUS;
        FINSI;

      FINSI;
   FIN BLOC4;
FIN BLOC5;
*
* Affichage de la limite plastique de la premiere zone.
*
MESS ' Rayon definisant la fin de la premiere zone plastique.';
MESS ' ------------------------------------------------------';
SAUT 1 LIGN;
MESS '   Rayon2 = ' RAY2MAX 'mm';
RAY2TH = 1.1968;
erra2 = 100*(abs (ray2th -ray2max)/ray2th);
MESS '   RAYON2 thorique = ' RAY2TH ' mm';
MESS 'ECART =' erra2;

SAUT 1 LIGN;
*
MESS 'AU POINT DE GAUSS,LES CONTRAINTES SONT';
MESS 'SMTT =' SMTTR2 'MPa ;SMRR =' SMRRR2 'MPa';
MESS 'SMVM =' SMVMR2 'MPa';
MESS 'LES COORDONNEES DU POINT VALENT';
MESS ' X= ' ;LIST xel;
MESS ' Y= ' ;list yel;
SAUT 1 LIGN;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*


*
* TRACE DES ISO-CONTRAINTES
*
CHELCONT = TABLE;
CHELCONT.1 = TAB1.CONTRAINTES.NN1;
CHELCONT.10 = TAB1.CONTRAINTES.NN10;
CHELCONT.2 = TAB1.CONTRAINTES.NN2;
CHELCONT.11 = TAB1.CONTRAINTES.NN11;
CHELCONT.3 = TAB1.CONTRAINTES.NN3;
CHELCONT.12 = TAB1.CONTRAINTES.NN12;
CHELCONT.4 = TAB1.CONTRAINTES.NN4;
CHELCONT.13 = TAB1.CONTRAINTES.NN13;
CHELCONT.5 = TAB1.CONTRAINTES.NN5;
CHELCONT.14 = TAB1.CONTRAINTES.NN14;
CHELCONT.6 = TAB1.CONTRAINTES.NN6;
CHELCONT.15 = TAB1.CONTRAINTES.NN15;
CHELCONT.7 = TAB1.CONTRAINTES.NN7;
CHELCONT.16 = TAB1.CONTRAINTES.NN16;
CHELCONT.8 = TAB1.CONTRAINTES.NN8;
CHELCONT.17 = TAB1.CONTRAINTES.NN17;
CHELCONT.9 = TAB1.CONTRAINTES.NN9;
CHELCONT.18 = TAB1.CONTRAINTES.NN18;
*
* RECHERCHE DES LIEUX OU IL Y A PLASTIFICATION : TRACEE
SI (NEG GRAPH 'N');

      CHEL2 = VMIS mo CHELCONT.1;
      TITR 'T= 98C : ISOCONTRAINTES DE VON-MISES';
      TRAC CHEL2 mo SUR1;
*
      CHEL2 = VMIS mo CHELCONT.11;
      TITR 'T= 196C : ISOCONTRAINTES DE VON-MISES';
      TRAC CHEL2 mo SUR1;
*
      CHEL2 = VMIS mo CHELCONT.18;
      TITR 'T= 265C : ISOCONTRAINTES DE VON-MISES';
      TRAC CHEL2 mo SUR1;
*
  LLIS10 = PROG 195. 200. 205.;
      CHEL2 = VMIS mo CHELCONT.1;
      TITR 'T= 98C : ISOCONTRAINTES DE VON-MISES';
      TRAC LLIS10 CHEL2 mo SUR1;
*
      CHEL2 = VMIS mo CHELCONT.11;
      TITR 'T= 196C : ISOCONTRAINTES DE VON-MISES';
      TRAC LLIS10  CHEL2 mo SUR1;
*
      CHEL2 = VMIS mo CHELCONT.18;
      TITR 'T= 265C : ISOCONTRAINTES DE VON-MISES';
      TRAC LLIS10 CHEL2 mo SUR1;
finsi;
SAUT 1 LIGN;
   TEMPS    ;
SAUT 1 LIGN;
finsi;

*****************************************************
******   CODE DE FONCTIONNEMENT               *******
*****************************************************
* ON NE TIENT PAS COMPTE DES VALEURS NON-SIGNIFICATIVES  *
* IL Y A ERREUR SI UN DES POURCENTAGES EST SUPERIEUR A 5%*
SUERG1 = PROG ERGUR98.1 ERGUR98.2 ERGUR98.3 ERGSRR98.2
             ERGSTT98.1 ERGSTT98.3;
si complet;
SUERG2 = PROG ERTT196.1  ERTT196.2 ERRA1 ERTTRA1 ERRRA1
              ERRA2;
ERMA1 = MAXI SUERG1;
ERMA2 = MAXI SUERG2;
sinon;
ERMA1 = MAXI SUERG1;
ERMA2 = 0.;
finsi;
ERMA = MAXI ( PROG ERMA1 ERMA2);
SI  (ERMA <EG 3.);
         ERRE 0;
SINON;
         ERRE 5;
FINSI;
MESS 'L ECART MAXIMAL EST DE ' ERMA '%';
FIN;









