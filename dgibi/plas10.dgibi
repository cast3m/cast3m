* fichier :  plas10.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Plas10.dgibi: Jeux de données        *
*           ---------------------------------         *
*                                                     *
OPTION ECHO 0;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;
GRAPH='N';
SAUT PAGE ;
*
*******************************************************
*  TEST PLAS10
*
* Sortie du domaine élastique et phase plastique
* (comportement élasto-plastique modèle CAM-CLAY).
*
* Un parallelépipède est soumis à des déplacements
* imposés sur trois de ses faces et fixé de manière
* isosattique. On compare la courbe de réponse en
* compression hydrostatique à la solution exacte.
*
*******************************************************

TITRE 'PARALLELEPIPEDE EN COMPRESSION HYDROSTATIQUE ' ;
OPTION ECHO 0 ;
OPTION DIME 3 ELEM CUB8 MODE TRID      ;
*------------------------------------------------------
*           DEFINITION DE LA GEOMETRIE
*------------------------------------------------------
A1= 0. 0. 0.;
B1= 1. 0. 0.;
C1= 1. 1. 0.;
D1= 0. 1. 0.;
A2= 0. 0. 1.;
B2= 1. 0. 1.;
C2= 1. 1. 1.;
D2= 0. 1. 1.;
*
L1 = A1 DROIT 1 B1  ;
L2 = B1  DROIT 1 C1  ;
L3 = C1  DROIT 1 D1  ;
L4 = D1  DROIT 1 A1  ;
LL = L1 ET L2 ET L3 ET L4 ;
SU1= SURF LL PLAN ;
M1 = A2 DROIT 1 B2  ;
M2 = B2  DROIT 1 C2  ;
M3 = C2  DROIT 1 D2  ;
M4 = D2  DROIT 1 A2  ;
MM = M1 ET M2 ET M3 ET M4 ;
SU2= SURF MM PLAN ;
SU = SU1 VOLU  1 SU2 ;
SI(NEG GRAPH 'N');
  TRAC (888  1000 2000 )  SU ;
FINSI ;
*------------------------------------------------------
*        DEFINITION DES CONDITIONS AUX LIMITES
*             ET DES DEPLACEMENTS IMPOSES
*------------------------------------------------------
BLX = BLOQ UX ( A1  ET A2 ET D1 ET D2 );
BLY = BLOQ UY ( A1 ET B1 ET B2 ET A2 ) ;
BLZ = BLOQ UZ SU1 ;
BL = BLX ET BLY ET BLZ  ;
*
CLX = BLOQ UX ( B1  ET B2 ET C1 ET C2 );
CLY = BLOQ UY ( D1 ET C1 ET C2 ET D2 ) ;
CLZ = BLOQ UZ SU2 ;
CL = CLX ET CLY ET CLZ  ;
*
VAL = -2.664328E-5 ;
DEPX= DEPI CLX  VAL   ;
DEPY= DEPI CLY  VAL   ;
DEPZ= DEPI CLZ  VAL   ;
*
*------------------------------------------------------
*        DEFINITION DES PROPRIETES MATERIELLES
*------------------------------------------------------
MO  = MODE SU MECANIQUE ELASTIQUE PLASTIQUE CAM_CLAY ;
*
XL = 0.1 ;
CC = 0.5 ;
XK = 0.01 ;
EE = 2. ;
PI0 = 5.;
*
MAT  = MATE MO  'YOUN' 1000.  'NU' 0.2
 'E0' EE  'COHE' CC  'M' 1.2   'P0' PI0   'LAMD' XL
 'KAPA' XK 'G1'  416.666667 ;
*
LITX = PROG 0. PAS 1. 300. ;
LIX =  PROG 0. PAS 1. 300. ;
EVX= EVOL MANU 'TEMP' LITX 'FX'   LIX ;
CHA1 = CHAR 'DIMP' (DEPX ET DEPY ET DEPZ) EVX;
*
*------------------------------------------
*            APPEL A PASAPAS
* AU TEMPS T , LE DEPLACEMENT VAUDRA F(T)*D
*------------------------------------------
 TAB1 = TABLE ;
 LIS1 = PROG 0. PAS 10.  200. ;
 TAB1.'MODELE' = MO;
 TAB1.'CARACTERISTIQUES' = MAT;
 TAB1.'CHARGEMENT' = CHA1;
 TAB1.'TEMPS_CALCULES' = LIS1;
 TAB1.'BLOCAGES_MECANIQUES' = CL 'ET' BL;
 PASAPAS TAB1 ;
*
*------------------------------------------
*           DEPOUILLEMENT ET
*     CALCUL DE LA SOLUTION ANALYTIQUE
*------------------------------------------
*
SI(NEG GRAPH 'N');
NT = (DIME (TAB1.DEPLACEMENTS)) - 1 ;
IT = 0 ;
PRD = PROG 0. ;
PRF = PROG 0. ;
REPETER BOPI NT;
IT = IT + 1 ;
DD= TAB1.DEPLACEMENTS.IT;
RR =  REAC CLY DD;
RRR = RESU RR;
LEP = ( EXTR RRR 'MAIL' 'NOMU'  ) POIN 1  ;
PRD = PRD ET (PROG ( EXTR DD UY D1));
PRF = PRF ET (PROG ( EXTR RRR FY LEP) ) ;
FIN BOPI ;
FV = EVOL ROSE MANU 'D' PRD 'F ' PRF;
DESS FV ;
FINSI;
*
VV = PECHE TAB1 VARIABLES_INTERNES 200. ;
SS = PECHE TAB1 CONTRAINTES 200. ;
PCAL =  EXTR SS SMXX 1 1 1  ;
PTHE =  -1. * ( PI0 + CC) *
        (EXP ( ( -300. * VAL ) * ( 1.+EE ) / XL) );
PTHE = PTHE + CC ;
EVPCAL =  EXTR VV TRAP 1 1 1  ;
EVPTHE = (300. * VAL ) * ( XL - XK ) / XL ;
SAUTER 3 LIGNE ;
MESS ' PRESSION MOYENNE THEORIQUE ' PTHE
'  CALCULEE'  PCAL;
MESS ' TRACE PLASTIQUE THEORIQUE ' EVPTHE
' CALCULEE ' EVPCAL;
*
RESI1 = ABS ( (PCAL-PTHE)/PTHE );
RESI2 = ABS ( (EVPCAL-EVPTHE)/EVPTHE);
SAUTER 3 LIGNE ;
MESS ' ERREURS RELATIVES ' RESI1 RESI2 ;
*
*             CODE FONCTIONNEMENT
*
SI (RESI1 <EG 1.E-6) ;
  SI (RESI2 <EG 1.E-6) ;
    ERRE  0;
  SINO;
    ERRE  5;
  FINSI;
SINON;
    ERRE  5;
FINSI;
FIN;












