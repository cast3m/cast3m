* fichier : formation_debutant_1_maillage.dgibi
*
************************************************************************
*      FORMATION DEBUTER AVEC CAST3M - CALCULS THERMO-MECANIQUES       *
*                                                                      *
* Modelisation du comportement thermo-mecanique d'une plaque perforee  *
*                                                                      *
* Ce fichier est la partie 1 sur 3 de la formation debutant a Cast3M   *
*                                                                      *
* Partie 1 : maillage                                                  *
*            creation de points, lignes, surfaces                      *
*            maillage libre puis regle                                 *
*            definition des parametres materiaux                       *
*            sauvegarde des objets                                     *
*                                                                      *
************************************************************************




* OBJET LOGIQUE POUR INDIQUER SI L'ON FAIT LES TRACES OU NON
* ----------------------------------------------------------
ITRAC    = FAUX ;



* UNITES
* ------
* IL N'Y A PAS DE SYSTEME D'UNITES PREDEFINI DANS CAST3M
* L'UTILISATEUR DOIT S'ASSURER DE LA COHERENCE DES VALEURS INTRODUITES
* DANS LE PRESENT JEU DE DONNEES, ON CHOISIT LES UNITES SUIVANTES :
* MASSE:                        kg
* LONGUEUR:                     m
* TEMPS:                        s
* TEMPERATURE                   degre Celsius
* FORCE:                        N
* ENERGIE                       J
* PUISSANCE                     W
* CONTRAINTE                    Pa (N/m2)
* MASSE VOLUMIQUE               kg/m3
* CAPACITE CALORIFIQUE MASSIQUE J/kg/K
* CONDUCTIVITE THERMIQUE        W/m/K
* COEFFICIENT DE DILATATION     1/K






************************************************************************
* CHAPITRE 1 : GENERALITES ET MAILLAGE                                 *
************************************************************************


* OPTIONS GENERALES ET TYPE D'ELEMENTS GEOMETRIQUES
* -------------------------------------------------
OPTI 'DIME' 2 'ELEM' 'QUA8' ;


* DEFINITION DES LONGUEURS ET DENSITES DE MAILLES
* -----------------------------------------------
LONG     = 24.E-1 ;
HAUT     = 10.E-1 ;
RAYON    = 2.E-1 ;

NLONG    = 24 ;
NHAUT    = 4 ;
NBASG    = 10 ;
NBASD    = 10 ;
NQCG     = 8 ;
NQCD     = 8 ;


* CREATION DES POINTS D'APPUI DU MAILLAGE
* ---------------------------------------
PA       = 0. 0. ;
PB       = ((0.5 * LONG) - RAYON) 0. ;
PC       = (0.5 * LONG) RAYON ;
PD       = ((0.5 * LONG) + RAYON) 0. ;
PE       = LONG 0. ;
PF       = LONG HAUT ;
PG       = 0. HAUT ;
PCEN     = (0.5 * LONG) 0. ;


* CREATION DES LIGNES D'APPUI DU MAILLAGE
* ---------------------------------------

* LIGNES ELEMENTAIRES
LIAB     = DROI NBASG PA PB ;
CE1      = CERC NQCG PB PCEN PC ;
CE       = CERC NQCD CE1 PCEN PD ;
LIDE     = DROI NBASD PD PE ;
LIEF     = DROI NHAUT PE PF ;
LIFG     = DROI NLONG PF PG ;
LIGA     = DROI NHAUT PG PA ;

* CONTOUR FERME OBTENU PAR ASSEMBLAGE DE LIGNES ELEMENTAIRES
CO       = LIAB ET CE ET LIDE ET LIEF ET LIFG ET LIGA ;


* CREATION DU MAILLAGE 2D PAR APPEL AU MAILLEUR LIBRE
* ---------------------------------------------------
SU       = SURF CO ;
SI ITRAC ;
  TRAC SU 'TITR' '[1] Maillage libre de la demi plaque percee' ;
FINSI ;


* CREATION DU MAILLAGE 2D PAR UN MAILLEUR REGLE
* MAILLAGE DU QUART GAUCHE DE LA PLAQUE AVEC REGL
* -----------------------------------------------
NHAUT    = 10 ;
NQCG     = 20 ;
PH       = (0.5 * LONG) HAUT ;
LIHG     = DROI (NQCG - NHAUT) PH PG ;
LIGA     = DROI NHAUT PG PA ;
CE1      = CERC NQCG PB PCEN PC ;
SU1      = REGL  'DINI' 0.05 'DFIN' 0.1 (INVE CE1) (LIHG ET LIGA) ;
SU2      = SU1 SYME 'DROI' PCEN PH ;
SU       = SU1 ET SU2 ;
ELIM SU 1.E-9 ;
SI ITRAC ;
  TRAC SU 'TITR' '[1] Maillage regle de la demi plaque percee' ;
FINSI ;


* RECUPERATION DU MAILLAGE DU DEMI CERCLE, DE LA LIGNE SUPPERIEURE
* ET DES LIGNES INFERIEURES AVEC 'POIN' ET 'ELEM'
* ----------------------------------------------------------------
CSU      = CONT SU ;
PCE      = SU POIN 'SPHE' PCEN PC 1.E-9 ;
CE       = CSU ELEM 'APPU' 'STRI' PCE ;
PLHAUT   = SU POIN 'DROI' PF PG 1.E-9 ;
LHAUT    = CSU ELEM 'APPU' 'STRI' PLHAUT ;
PLBAS    = SU POIN 'DROI' PA PB 1.E-9 ;
LBAS     = CSU ELEM 'APPU' 'STRI' PLBAS ;


* CARACTERISTIQUES DU MATERIAU
* ----------------------------
RHOMAT   = 2700. ;
YOUNGMAT = 70.E9 ;
NUMAT    = 0.36 ;
ALPHAMAT = 24.E-6 ;
CAPAMAT  = 900. ;
CONDUMAT = 210. ;
SIGYMAT  = 130.E6 ;


* SAUVEGARDE DES OBJETS CREES
* ---------------------------
OPTI 'SAUV' 'formation_debutant_1_maillage.sauv' ;
SAUV ;


FIN ;


