* fichier :  ricbet_uni_2.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
******************************************************
* DESCRIPTION
******************************************************
* Local test - Multifiber analysis
*
* Loading path considered:
*
* - 1 : Monotonic tension test
* - 2 : Monotonic compression test
* - 3 : Cyclic tension test
* - 4 : Cyclic compression test
* - 5 : Cyclic tension/compression test
* - 6 : Tension/compression/tension test
*
*
******************************************************
* AUTHOR
******************************************************
* Developped by : Benjamin RICHARD
*                 CEA-DEN/DANS/DM2S/SEMT/EMSI
*                 Benjamin.Richard@cea.fr
******************************************************
graph = 'N';
******************************************************
* Type of loading
ncas  = 1;
******************************************************

******************************************************
* OPTIONS
******************************************************
OPTION DIME 2 ELEM QUA4;

* Number of element------------------------------------
nelem = 10;

* Concrete parameters----------------------------------

* Young
ESSS= 28000E6;

* Poisson ratio
nun = 0.20;

* Tension strength
C1  = 2.1E+6;

* Brittleness in tension
C2  = 9.0E-3;

* Kinematic hardening coefficient 1
C3  = 3.0E+9;

* Kinematic hardening coefficient 2
C4  = 8.0e-6;

* Closure stress
C5  = -3.0e6;

* Resistance en compession
C6  = 10.0e6;

* XXX (unused)
C7  = 1.0;

* XXX (unused)
C8  = 1.0;

* Yield surface 1
C9  = 4.0e10;

* Yield surface 2
C10  = 600.0;

* Parameter that can be implemented in ESOPE
* (not subjected to high variations)
C11  = -6.0e6;

******************************************************
* GEOMETRICAL PARAMETERS
******************************************************
SACIER1 = 0. 0.;
SACIER2 = 1. 0.;
SACIER3 = 1. 1.;
SACIER4 = 0. 1.;

LL1 = D 1 SACIER1 SACIER2;
LL2 = D 1 SACIER2 SACIER3;
LL3 = D 1 SACIER3 SACIER4;
LL4 = D 1 SACIER4 SACIER1;

SACIER  = DALL LL1 LL2 LL3 LL4;

******************************************************
* MODEL
******************************************************
OPTI DIME 3 ELEM SEG2;

*mo1 = mode sacier mecanique elastique quas;

*ma1 = MATE mo1 YOUN ESSS NU nun;

mo1 = mode sacier mecanique elastique plastique RICBET_UNI
      quas;

ma1 = MATE mo1 YOUN ESSS NU nun
'FT  ' C1  'ALDI' C2  'GAM1' C3
'A1  ' C4  'SIGF' C5  'FC  ' C6
'AF  ' C7  'AG  ' C8  'AC  ' C9
'BC  ' C10 'SIGU' C11 'HYST'  2;

car1 = CARA mo1 ALPY .66 ALPZ .66;

modstot = mo1;
matstot = ma1 et car1;

******************************************************
* BEAM ELEMENT
******************************************************
OPTI ELEM SEG2;

P1 = 0. 0. 0.;
P2 = 0. 0. 1.;

Lpoutre = d nelem P1 P2;

modpout = MODE Lpoutre MECANIQUE ELASTIQUE SECTION
               PLASTIQUE SECTION TIMO;
matpout = MATE modpout MODS modsTOT MATS matsTOT
               VECT (0 1 0);

******************************************************
* BOUNDARY CONDITIONS
******************************************************
con1 = BLOQ DEPL ROTA         P1;
con3 = BLOQ UZ                P2;
con2 = BLOQ UX UY RX RY RZ P2 ;

******************************************************
* LOADING CASES
******************************************************
fo1  = depi con3 1.0;

SI (EGA ncas 1) ;
LI1  = PROG 0. 1.;
LI2  = PROG 0. 1.0e-3;
LIS1 = PROG 0. PAS 0.02 1.;
FINSI;

SI (EGA ncas 2) ;
LI1  = PROG 0.  1.;
LI2  = PROG 0. -5.0e-3;
LIS1 = PROG 0. PAS 0.02 1;
FINSI;

SI (EGA ncas 3) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 1.5e-4 9.5e-6 2.0E-4 3.0E-5 10E-4;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 4) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. -3.0e-3 1.5e-4 -5.0E-3 -3.5E-4 -8.0E-3;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 5) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 3.e-4 -3.0e-4 5.0E-4 -1.5E-4 1.0E-3;
LIS1 = PROG 0. PAS 0.02 5;
FINSI;

SI (EGA ncas 6) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 3.e-4 -1.e-5 5.0E-4 -5E-3 -3.0E-3;
LIS1 = PROG 0. PAS 0.02 5;
FINSI;

ev1  = evol manu li1 li2;
cha1 = char dimp fo1 ev1;

******************************************************
* SOLVING PROCEDURE
******************************************************
TAB1 = TABLE;
TAB1 . BLOCAGES_MECANIQUES = con1 et con3 et con2;
TAB1 . MODELE              = modpout;
TAB1 . CARACTERISTIQUES    = matpout;
TAB1 . CHARGEMENT          = cha1;
TAB1 . TEMPS_CALCULES      = lis1;
TAB1 . MAXDEFOR            = 1.E-5;
TAB1 . MOVA                = RIEN;

PASAPAS TAB1;

******************************************************
* POST PROCESSING
******************************************************
dep0 = TAB1.'DEPLACEMENTS';
tim0 = tab1. TEMPS;
sig0 = TAB1.'CONTRAINTES';
var0 = TAB1.'VARIABLES_INTERNES';
ind0 = INDE dep0;
time = PROG (tim0 . 0);

uzp0  = PROG 0.;
fzp0  = PROG 0.;

ep1 = prog 0.;
si1 = prog 0.;
nn = (('DIME' ind0) - 1);

j = 1;
REPE lab1 (('DIME' ind0) - 1);
  j = j + 1;
  indj  = ind0.j;
  depj  = dep0.indj;
  sigj  = sig0.indj;
  epsj  = epsi depj modpout matpout;
  vecj  = bsigma sigj modpout matpout;
  time  = time et (prog tim0. &lab1);
  uzp0  = uzp0 ET (PROG (EXTR depj 'UZ' P2));
  fzp0  = fzp0 ET (PROG (EXTR vecj 'FZ' P2));
FIN lab1;

si (ega graph 'O');
evuzfz = evol manu uzp0 (fzp0);
list evuzfz;
dess evuzfz;
finsi;

aa = (EXTR vecj 'FZ' P2);

message 'erreur relative';
err = (aa - 3.46710E+05) / 3.46710E+05;
list err;

si (> err 1E-3);
erreur(5);
finsi;

fin;






















