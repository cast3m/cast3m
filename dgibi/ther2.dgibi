* fichier :  ther2.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

*******************************************
*    Test ther2.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*-----------------------------------------*
*                  THER2                  *
*                                         *
*        TEST DES CL DE TEMPERATURE       *
*          EN MODE AXISYMETRIQUE          *
* ----------------------------------------*
*                                         *
*    TEMPERATURE IMPOSEE + CONVECTION     *
*                        + FLUX           *
*                        + SOURCE         *
*                                         *
*                                         *
* Comme dans le test THER1, cet exemple   *
* permet de tester les conditions aux     *
* limites de température en calcul        *
* axisymétrique.                          *
*                                         *
* Trois cas sont envisagés ici :          *
*      - un tube épais soumis à une       *
*        température imposée et à un      *
*        flux imposé                      *
*      - un tube épais soumis à une       *
*        température imposée et à une     *
*        source volumique de chaleur      *
*      - un tube épais soumis à une       *
*        température imposée et à une     *
*        convection forcée                *
*-----------------------------------------*
*
*--- 1er problème
*    ------------
*
*---  Description :
*
*     établir le champ de température dans
*     un tube épais soumis à une condition
*     de température imposée et à un flux
*     imposé
*                     -----------
*---  Description de la géométrie :
*
*  |  axe de symétrie
*  .
*  |    _________________________
*  .   |                         |
*  .   |                         |
*  .   |                         |
*  .   |                         |
*  .   |                         |
*  .   |                         |
*  |   |         K=130.          |
*  .   |_________________________|
*  |   TI=0.57                   F=0.72
*      RI=0.0515                 RE=0.2015
*
*---  Commentaires :
*
*  le problème est à symétrie de révolution
*  température imposée sur la paroi interne
*   (TI=0.57)
*  flux imposé sur la paroi externe
*   (F=0.72)
*  conductibilité isotrope
*   (K=130)
*
*---  Solution analytique :
*
*     T(R) = TI + LOG(R/RI) * (F * RE)/K
*     Le test est réalisé en T(R=Re)
*
*------------------------------------------

TITR 'THER2 - C.L. TEMPERATURE -  CALCUL AXISYMETRIQUE';
OPTI DIME 2 ELEM QUA4 MODE AXIS ;
TEMPS ;

*---  CREATION DE LA GEOMETRIE:
P1  = 0.0515 0.  ;P20 = 0.0665 0.  ;
P19 = 0.0665 0.5 ;P2  = 0.0515 0.5 ;
P18 = 0.0815 0.  ;P17 = 0.0815 0.5 ;
P16 = 0.0965 0.  ;P15 = 0.0965 0.5 ;
P14 = 0.1115 0.  ;P13 = 0.1115 0.5 ;
P12 = 0.1265 0.  ;P11 = 0.1265 0.5 ;
P10 = 0.1415 0.  ;P9  = 0.1415 0.5 ;
P8  = 0.1565 0.  ;P7  = 0.1565 0.5 ;
P6  = 0.1715 0.  ;P5  = 0.1715 0.5 ;
P24 = 0.1865 0.  ;P23 = 0.1865 0.5 ;
P4  = 0.2015 0.5 ;P3  = 0.2015 0.  ;

SURF1  = MANUEL QUA4 P1 P20 P19 P2 ;
SURF2  = MANUEL QUA4 P20 P18 P17 P19 ;
SURF3  = MANUEL QUA4 P18 P16 P15 P17 ;
SURF4  = MANUEL QUA4 P16 P14 P13 P15 ;
SURF5  = MANUEL QUA4 P14 P12 P11 P13 ;
SURF6  = MANUEL QUA4 P12 P10 P9 P11 ;
SURF7  = MANUEL QUA4 P10 P8 P7 P9 ;
SURF8  = MANUEL QUA4 P8 P6 P5 P7 ;
SURF9  = MANUEL QUA4 P6 P24 P23 P5 ;
SURF10 = MANUEL QUA4 P24 P3 P4 P23 ;
SURF11 = SURF1 ET SURF2 ET SURF3 ET SURF4 ET SURF5 ET SURF6 ET SURF7 ET SURF8 ET SURF9 ET SURF10;

D1 = DROITE 1 P1 P2 ;
D3 = DROITE 1 P3 P4 ;

*---  DONNEES DU PROBLEME DE THERMIQUE:

*---  MODELISATION:
MOD1 = MODE SURF11 THERMIQUE ISOTROPE ;

*---  CARACTERISTIQUES DU MATERIAU:
KC = 130. ;
MAT1 = MATE MOD1 K KC ;

*---  MATRICE DE CONDUCTIVITE:
CND1 = CONDUCTIVITE MOD1 MAT1 ;

*---  TEMPERATURES IMPOSEES: BLOQUE + DEPI
B1 = BLOQUE T D1 ;
TI = 0.57 ;
E1 = DEPI B1 TI ;

*---  FLUX IMPOSE:
F = 0.72;
F1 = FLUX MOD1 F D3 ;

*---  ASSEMBLAGE:
RIG1 = CND1 ET B1 ;
FF1  = E1   ET F1 ;

*---  RESOLUTION:
CHTER1 = RESO RIG1 FF1 ;
TETA1  = EXTR CHTER1 T P3  ;

SI(NEG GRAPH 'N');
    TRAC 'QUAL' SURF11 ;
    TRAC CHTER1 SURF11 ;
FINSI;

*--- TEST DE NON REGRESSION:
R  = COOR 1 P3;
RI = COOR 1 P1;
RE = COOR 1 P4;

TREF1 = TI +((LOG (R/RI)) * (F * RE) / KC);

RESI1=100. * (ABS((TETA1-TREF1)/TREF1));

MESS 'Temperature theorique :' TREF1' C';
MESS 'Temperature calculee  :' TETA1' C';
MESS '    Soit un ecart de : ' RESI1 '%';
RESITOT = PROG RESI1 ;

SAUTER 1 LIGNES ;

*------------------------------------------
*--- 2eme problème
*
* établir le champ de température dans un
* tube épais soumis à une condition de
* température imposée ainsi qu'à une source
* volumique

*---  Description de la géométrie :
*
*  |  Y (axe de symétrie)
*  .
*  |    ______________________________
*  .   |                              |
*  |   |            Q=20.   K=1.      |
*  .    ------------------------------
*  |   TI=100.                       F=0.
*      RI=6.                         RE=16.
*
*
*---  Commentaires :
*
* Le problème est à symétrie de révolution.
* Température sur la paroi interne (TI=100)
* Flux imposé sur la paroi externe (F=0)
*                           ( DT / DN = 0 )
*     Source volumique imposée (Q=20)
*     Conductibilité isotrope  (K=1)
*
*--- Solution analytique :
*
*     T(R) = TI - (Q/4K)*(R**2-RI**2) +
*            (Q*RE/2)*RE*LOG(R/RI)

*     Le test est réalisé en T(R=Re)
*
*------------------------------------------
*
*---  CREATION DE LA GEOMETRIE:
*
P1 = 6 0 ;
P2 = 16 0 ;
P3 = 16 4 ;
P4 = 6 4 ;
D1 = DROITE 8 P1 P2 ;
D2 = DROITE 6 P2 P3 ;
D3 = DROITE 9 P3 P4 ;
D4 = DROITE 4 P4 P1 ;
LIG1 = D1 ET D2 ET D3 ET D4 ;
SURF1 = SURFACE LIG1 PLANE ;

*---  DONNEES DU PROBLEME DE THERMIQUE:

*---  MODELISATION:
MOD1 = MODE SURF1 THERMIQUE ISOTROPE ;

*---  CARACTERISTIQUES DU MATERIAU:
KC = 1. ;
MAT1 = MATE MOD1 K KC ;

*---  MATRICE DE CONDUCTIVITE:
CND1 = COND MOD1 MAT1 ;

*---  TEMPERATURES IMPOSEES: BLOQUE + DEPI
B1 = BLOQUE T D4 ;
TI = 100. ;
E1 = DEPI B1 TI ;

*---  FLUX IMPOSE:
E2 = FLUX MOD1 0. D2 ;

*---  SOURCE VOLUMIQUE:
Q = 20. ;
E3 = SOURCE MOD1 Q SURF1 ;

*---  ASSEMBLAGE PREMIER ET SECOND MEMBRE:
RIG1 = CND1 ET B1 ;
FF1  = E1   ET E2 ET E3 ;

*---  RESOLUTION:
CHTER2 = RESO RIG1 FF1 ;
TETA2  = EXTR CHTER2 T P3 ;

SI(NEG GRAPH 'N');
    TRAC 'QUAL' SURF1 ;
    TRAC CHTER2 SURF1 ;
FINSI;

*--- TEST DE NON REGRESSION:
R  = COOR 1 P3;
RI = COOR 1 P1;
RE = COOR 1 P3;

C1 = Q * (RE**2)/(2*KC);
TREF2=TI + ((Q /(2*KC)) * ((((RI**2) - (RE**2))/2) + ((RE**2) * (LOG (RE/RI))) )) ;


RESI2=100. * (ABS((TETA2-TREF2)/TREF2));

MESS 'Temperature theorique :' TREF2' C';
MESS 'Temperature calculee  :' TETA2' C';
MESS '    Soit un ecart de : ' RESI2 '%';
RESITOT = RESITOT ET (PROG RESI2) ;

SAUTER 1 LIGNES ;

*------------------------------------------
*--- 3eme problème
*
*---  Description du problème :
*
*  établir le champ de température dans
*  un tube épais soumis à une condition
*  interne de température ainsi qu'à une
*  convection sur sa paroi externe
*
*---  Données du problème :
*
*  Conductibilité isotrope du matériau
*   (K=8)
*  Coefficient d'échange en convection
*   (HC=5)
*  Température imposée sur la paroi
*    interne (TI=100)
*  Température du milieu extérieur
*   (TE=125)
*
*---  Solution :
*  Comparaison à la solution analytique
*------------------------------------------
*---  CREATION DE LA GEOMETRIE:
*     - rayon intéreiur RI = 6.
*     - rayon extérieur RE = 16.
*     - hauteur         H  = 4.
*     - surface interne : D4
*     - surface externe : D2

P1 = 6 0 ;
P2 = 16 0 ;
P3 = 16 4 ;
P4 = 6 4 ;
D1 = DROITE 8 P1 P2 ;
D2 = DROITE 5 P2 P3 ;
D3 = DROITE 9 P3 P4 ;
D4 = DROITE 6 P4 P1 ;
LIG1 = D1 ET D2 ET D3 ET D4 ;
SURF1 = SURF LIG1 PLANE ;

*---  DONNEES DU PROBLEME DE THERMIQUE:

*--- MODELISATION:
MOD1 = MODE SURF1 THERMIQUE ISOTROPE   ;
MOD2 = MODE D2    THERMIQUE CONVECTION ;

* DONNEE DES CARACTERISTIQUES DU MATERIAU:
KC = 8. ;
MAT1 = MATE MOD1 K KC ;
HC = 5. ;
MAT2 = MATE MOD2 H HC ;

*---  MATRICES DE CONDUCTIVITE:
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CNF1 = CONDUCTIVITE MOD2 MAT2 ;

*---  FLUX EQUIVALENTS A LA CONVECTION:
TEX = 125. ;
FF1 = CONVECTION MOD2 MAT2 T TEX ;

*---  TEMPERATURE IMPOSEE: BLOQUE + DEPI
B1 = BLOQUE T D4 ;

TI = 100. ;
E1 = DEPI B1 TI ;

*---  ASSEMBLAGE:
RIG1 = CND1 ET CNF1 ET B1 ;
Q1 = FF1 ET E1 ;

*---  RESOLUTION:
CHTER3 = RESO RIG1 Q1 ;
TETA3  = EXTR CHTER3 T P3 ;

SI(NEG GRAPH 'N');
    TRAC 'QUAL' SURF1 ;
    TRAC CHTER3 SURF1 ;
FINSI;

*--- POST-TRAITEMENT
RI = COOR 1 P1;
RE = COOR 1 P3;

*Valeur Théorique de la température en RE
TREF3=(TI + (HC*RE*TEX/KC*(LOG(RE/RI)))) / ( 1 + (HC*RE/KC*(LOG(RE/RI))));

RESI3=100. * (ABS((TETA3-TREF3)/TREF3));

MESS 'Temperature theorique :' TREF3' C';
MESS 'Temperature calculee  :' TETA3' C';
MESS '    Soit un ecart de : ' RESI3 '%';
RESITOT = RESITOT ET (PROG RESI3) ;
SAUT 1 LIGN ;

TEMPS ;
*         CODE DE FONCTIONNEMENT
SI((MAXI RESITOT) <EG 0.4);
   ERRE  0;
SINO;
   ERRE  5;
FINSI;
FIN;






