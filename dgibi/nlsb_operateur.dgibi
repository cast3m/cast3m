***********************************************************************
*_______________________________________________________________________
*
*                  Simulation COMPACT TENSION TEST
*_______________________________________________________________________
*-----------------------------------------------------------------------
*                      ^ ^   ^  ^  ^  ^
*                      | |   |  |  |  |
*                      ________________
*                     |                |
*                     |                |
*                     |                    |
*                     |                    |
*                     |                    |
*                     |                    |
*                    >|________________|
*                              ^^^^^^^^^
*-----------------------------------------------------------------------
*
* Analyse de la description du champs nonlocal en pointe d'entaille
* C. Giry, F. Dufour, J. Mazars
* -----------------------------
************************************************************************
graph = 'N';

opti dime 2 elem qua4 mode plan cont;

* Taille element
* --------------
telem = 0.02;


dens telem;

*-----------------------------------------------------------------------
* Maillage
*-----------------------------------------------------------------------


*----- Largeur de l'éprouvette
b = 1.;

*------Hauteur de l'éprouvette
h = 1.;

p1    = 0. 0.;
p1bis = (b/2.) 0.;
p2    = b  0.;
p3    = b  h;
p3bis = (b/2.) h;
p4    = 0. h;
d1    = d p1 p1bis ;
d1bis = d p1bis p2 ;
d2    = d p2 p3 ;
d2bis = d p3 p3bis ;
d3    = d p3bis p4 ;
d4    = d p4 p1 ;
s1    = (d1 et d1bis et d2 et d2bis et d3 et d4);
s1    = surf s1 ;

*-----------------------------------------------------------------------
* Modèle et matériau
*-----------------------------------------------------------------------

 MOD1 = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
           NON_LOCAL 'SB' 'V_MOYENNE' (MOTS 'EPTI');
*
* Paramètres
* ----------
* ft    : limite de traction
* younc : module d'Young
* nuc   : coefficient de Poisson
* y0    : seuil en deformation de premiere fissuration
* ll    : longueur interne

 ft    = 3.E+6;
 younc = 3.D+9 ;
 nuc   = 0.2;
 y0    = ft/younc;
 ll    = 0.3   ;


 MAT1 = MATE mod1 'YOUN' younc 'NU' nuc 'RHO' 2.5e+3  ;

*-----------------------------------------------------------------------
* Connectivites
*-----------------------------------------------------------------------
 conn1 = CONN mod1 ll 'NORMAL' 'INTERIEUR' ;
 conn2 = CONN mod1 ll 'DROITE' P1bis P2  'CENTRE' ;
 connt = conn1 et conn2;

*-----------------------------------------------------------------------
* Conditions limites
*-----------------------------------------------------------------------
cl1   = bloq (d1bis) UY;
cl2   = bloq p4 UX;
CL3   = RELA 'ENSE' UY (d2bis et d3);
CLTOT = CL1 ET CL2 ET CL3;
forc1 = FORC (d2bis et d3) (0. 1.) ;

*-----------------------------------------------------------------------
* Rigidité
*-----------------------------------------------------------------------
 rbet = RIGI mod1 mat1 ;
 rigt = rbet ET CLTOT ;

*-----------------------------------------------------------------------
* Analyse elastique
*-----------------------------------------------------------------------
 depl1 = RESO rigt forc1 ;

 cont1 = SIGM mod1 mat1 depl1;
 conp1 = PRIN mod1 cont1;

 contmax = MAXI (EXCO conp1 'SI11');
 f2      = ft/contmax;
 forc1   = FORC (d2bis et d3) (0. f2) ;
 depl1   = RESO rigt forc1 ;

*-----------------------------------------------------------------------
* Analyse du champ nonlocal
*-----------------------------------------------------------------------
 defo1 = EPSI mod1 depl1;
 cont1 = SIGM mod1 mat1 depl1;
 defp1 = PRIN mod1 defo1;
 conp1 = PRIN mod1 cont1;


* Deformation equivalente Mazars
* ------------------------------
 epeq11 = EXCO defp1 'SI11';
 epeq12 = MASQ epeq11 'SUPERIEUR' 0.;
 epeq1  = NOMC 'EPTI' (epeq11 * epeq12);
 epeq21 = EXCO defp1 'SI22';
 epeq22 = MASQ epeq21 'SUPERIEUR' 0.;
 epeq2  = NOMC 'EPTI' (epeq21 * epeq22);
 epeq31 = EXCO defp1 'SI33';
 epeq32 = MASQ epeq31 'SUPERIEUR' 0.;
 epeq3  = NOMC 'EPTI' (epeq31 * epeq32);

 epeqm = ((epeq1)**2) + ((epeq2)**2) + ((epeq3)**2);
 epeqm = (epeqm)**(0.5);

* Calcul de la longueur par element et limite de traction
* -------------------------------------------------------
CHAMLO = MANU 'CHML' MOD1 'LONG' 1.0 'STRESSES';
CHAML1 = INTG CHAMLO MOD1 ELEM 'LONG';
CHAML2 = CHAML1**(1./2.);
CHAML3 = NOMC 'LONG' (CHAN 'STRESSES'  MOD1 CHAML2);
CHAMFT = MANU 'CHML' MOD1 'SBFT' ft 'STRESSES';

* Calcul du champ nonlocal de deformation equivalente
* ---------------------------------------------------
epeqmsb  = epeqm + conp1 + chamft + chaml3;
epeqnlm  = 'NLOC'  epeqmsb connt ;
Pmax1    = POIN (EXCO epeqnlm 'EPTI') 'MAXI';
XX1 YY1  = COOR PMAX1;
lerr     = ABS((MAXI XX1) - (0.5*b));

list lerr;

si (ega graph 'O');
 TRAC epeqnlm mod1;
finsi;

* Test sur la position du maximum du champ nonlocal par rapport a la
* pointe de l'entaille
* --------------------

si (lerr > telem);
  erre 5;
sinon;
  erre 0;
finsi;


fin;









