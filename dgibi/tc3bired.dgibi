*  test snap back dans autopilot
*  on verifie principalement que le pilotage fonctionne correctement
*  on peut accessoirement verifier que la liste des temps calcules n'est pas monotone



opti echo 1 trac ps;;

opti echo 1 ;



opti dime 2 elem QUA8 mode plan defo  ;
h1 = 1.      ; h2 = 1.      ; h3 = 8.      ;
e1 = 5e-2 ; e2 = 1.20 ; e3 = 5e-2 ;

zz = 0. ;
p1 = 0 zz ; p2 = e1 zz ; p3 = (e1 + e2) zz ; p4 = (e1 + e2 + e3) zz ;
x_moy = (e1 + e2 + e3) / 2. ;
zz = h1 ;
p5 = 0 zz ; p6 = e1 zz ; p7 = (e1 + e2) zz ; p8 = (e1 + e2 + e3) zz ;
zz = h1 + h2 ;
p9 = 0 zz ; p10 = e1 zz ; p11 = (e1 + e2) zz ; p12 = (e1 + e2 + e3) zz;
zz = h1 + h2 + h3 ;
p13 = 0 zz ; p14 = e1 zz ; p15 = (e1 + e2) zz; p16 = (e1 + e2 + e3) zz;

ds1 = 75e-3 ; ds2 = 15.e-2 ; ds3 = 225e-3 ;
ds1 = 600e-3 ; ds2 = 120.e-2 ; ds3 = 1350e-3 ;
ds1=ds1/2; ds2=ds2/2; ds3=ds3/2;
ds1=ds1/2; ds2=ds2/2; ds3=ds3/4;
l12   = p1  d p2  dini ds1 dfin ds1 ;
l26   = p2  d p6  dini ds3 dfin ds3 ;
l65   = p6  d p5  dini ds1 dfin ds1 ;
l51   = p5  d p1  dini ds3 dfin ds3 ;
s12   = dall l12 l26 l65 l51 ;
l23   = p2  d p3  dini ds2 dfin ds2 ;
l37   = p3  d p7  dini ds3 dfin ds3 ;
l76   = p7  d p6  dini ds2 dfin ds2 ;
l62   = inve l26  ;
s23   = dall l23 l37 l76 l62 ;
l34   = p3  d p4  dini ds1 dfin ds1 ;
l48   = p4  d p8  dini ds3 dfin ds3 ;
l87   = p8  d p7  dini ds1 dfin ds1 ;
l73   = inve l37  ;
s34   = dall l34 l48 l87 l73 ;
l56   = inve l65  ;
l610  = p6  d p10 dini ds3 dfin ds3 ;
l109  = p10 d p9  dini ds1 dfin ds1 ;
l95   = p9  d p5  dini ds3 dfin ds3 ;
s56   = dall l56 l610 l109 l95 ;
l67   = inve l76  ;
l711  = p7  d p11 dini ds3 dfin ds3 ;
l1110 = p11 d p10 dini ds2 dfin ds2 ;
l106  = inve l610 ;
s67   = dall l67 l711 l1110 l106 ;
l78   = inve l87  ;
l812  = p8  d p12 dini ds3 dfin ds3 ;
l1211 = p12 d p11 dini ds1 dfin ds1 ;
l117  = inve l711 ;
s78   = dall l78 l812 l1211 l117 ;
l910  = inve l109 ;
l1014 = p10 d p14 dini ds3 dfin ds3 ;
l1413 = p14 d p13 dini ds1 dfin ds1 ;
l139  = p13 d p9  dini ds3 dfin ds3 ;
s910  = dall l910 l1014 l1413 l139 ;
l1011 = inve l1110;
l1115 = p11 d p15 dini ds3 dfin ds3 ;
l1514 = p15 d p14 dini ds2 dfin ds2 ;
l1410 = inve l1014;
s1011 = dall l1011 l1115 l1514 l1410 ;
l1112 = inve l1211;
l1216 = p12 d p16 dini ds3 dfin ds3 ;
l1615 = p16 d p15 dini ds1 dfin ds1 ;
l1511 = inve l1115;
s1112 = dall l1112 l1216 l1615 l1511 ;

couch1 = (s12 et s23 et s34) coul bleu ;
couch2 = (s56 et s67 et s78) coul blan ;
couch3 = (s910 et s1011 et s1112) coul roug ;
su = couch1 et couch2 et couch3 ;
xsubto = mesu su surf ;
mess 'xsubto=' xsubto 'm2' ;
titr 'Maillage' (nbno su) 'noeuds' ;
trac su ;
*trac noeud su ; trac elem su ; trac (cont su) ;

lgau = (l139 et l95 et l51) coul vert ;
lbas = (l12 et l23 et l34) coul jaun ;
ldro = (l48 et l812 et l1216) coul turq ;
lhau = (l1615 et l1514 et l1413) coul bleu ;
titr 'Conditions aux limites' ;
*trac (lgau et lbas et ldro et lhau) ;

fer1 = (l26 et l610 et l1014) coul blan ;
fer2 = (l37 et l711 et l1115) coul rose ;
fers = fer1 et fer2 ;
titr 'Ferraillage' ;
*trac fers ;
trac ((cont su) et fers) ;

voile = su et fers ; tass voile ; mena ;
titr 'Maillage complet' (nbel voile) 'elements' ;
*trac ecla voile ;

mo123 = mode su mecanique elastique isotrope plastique
              drucker_prager ;                                                                           "
mof1 = mode(changer  (l26 et l610 et l37 et l711) seg2)
       mecanique elastique isotrope plastique cinematique coq2 ;
mof2 = mode ( changer (l1014 et l1115) seg2)
       mecanique elastique isotrope plastique cinematique coq2 ;
mof = mof1 et mof2 ;
mo123f  = mo123 et mof ;


maf1 = mate mof1 youn 200000e6 nu 0.3 rho 7800. sigy 400.e6 h 100000e6 ;
xep1 = 5. * pi * ((39.e-3 / 2.) ** 2) ;
mess 'Epaisseur nappe 1' xep1 'm2' ;
caf1 = cara mof1 epai xep1 ;
rif1 = rigi mof1 (maf1 et caf1);
mmf1 = mass mof1 (maf1 et caf1);
maf2 = mate mof2 youn 200000e6 nu 0.3 rho 7800. sigy 400.e6 h 100000e6 ;
xep2 = 5. * pi * ((25.e-3 / 2.) ** 2) ;
mess 'Epaisseur nappe 2' xep2 'm2' ;
caf2 = cara mof2 epai xep2 ;
rif2 = rigi mof2 (maf2 et caf2);
mmf2 = mass mof2 (maf2 et caf2);

maf = maf1 et maf2 ;
caf = caf1 et caf2 ;
rif = rif1 et rif2 ;
mmf = mmf1 et mmf2 ;

cl = (bloq depl lbas) et (bloq rota lbas) ;

chprof = (manu chpo lgau 1 scal (h1 + h2 + h3)) - (coor 2 lgau) ;
chpres = chprof * (1000. * 9.81) ;
fopres = pres mass mo123 chpres  ;
vepres = vect fopres 1e-4 fx fy bleu ;
xr = extr (resu fopres) fx ((extr (resu fopres) mail) poin 1) ;
xrt= (h1 + h2 + h3) * 1000. * 9.81 * ((h1 + h2 + h3) / 2.) ;
mess 'press ef=' xr 'press th=' xrt 'ec=' ((xr - xrt) / xrt) ;
titr 'Pression, resultante fx=' xr 'N' ;
trac vepres (cont su) ;

xyo = 35000e6 ; xcp = 50e6 ; xtr = 0.6e6 + (0.06 * xcp) ;
xft = xtr ; xfc = -1 * xcp ;
xal = (xfc + xft) / (xfc - xft) ;
xbe = 1.  ;
xk  = (2. * xfc * xft) / (xfc - xft) ;
xga = xal ;
xde = xbe ;
xet = xal ;
xmu = xbe ;
xkl = xk  ;
xyo2 = xyo / -2. ;
xh  = ((xal + 1.) ** 2) * ((xyo * xyo2) / (xyo - xyo2)) ;
opti echo 0 ;
mess '    ' ;
mess 'Beton : trac=' xtr  'comp=' xcp  'Pa' ;
mess '        Youn=' xyo 'Pa' ;
mess '------------------------------------------------------';
mess 'Param : xft=' xft 'xfc=' xfc 'Pa' ;
mess '------------------------------------------------------';
mess 'Criteres :' ;
mess '        xal=' xal 'xbe=' xbe 'xk=' xk 'Pa' ;
mess '        xga=' xga 'xde=' xde ;
mess '        xet=' xet 'xmu=' xmu 'xkl=' xkl 'Pa' ;
mess '        xh=' xh 'Pa' ;
opti echo 1 ;

ma123 = mate mo123 youn xyo nu 0.2 rho 2500.
                   alfa xal beta xbe k xk gamm xga delt xde
                   eta xet mu xmu kl xkl h xh ;

ri123 = rigi mo123 ma123 ;
mm123 = mass mo123 ma123 ;

mm = mm123 et mmf ;
gg = manu chpo voile 1 uy -9.81 ;
fopoid = mm * gg ;
vepoid = vect fopoid 2.e-4 fx fy jaun ;
xr = extr (resu fopoid) fy ((extr (resu fopoid) mail) poin 1) ;
titr 'Poids , resultante fy=' xr 'N' ;
trac vepoid (cont su) ;
titr 'Evolutions du chargement en temps' ;
ev011 = evol bleu manu 'temps' (prog 0 1 2 10) 'F' (prog 0 1 1 1) ;
chr1 = char meca fopoid ev011 ;
ev001 = evol roug manu 'temps' (prog 0 1 2 4 ) 'F' (prog 0 0 1 2) ;
dess (ev011 et ev001) mima ;
chr2 = char meca fopres ev001 ;
chr = chr1 et chr2 ;

ma123f  = ma123 et maf et caf ;
ri = (ri123 et rif) ;

ma = mm123 ;
cp = manu chpo (ma extr 'MAIL' 'NOMU') 1 ux 1;
po = ma * cp;
po2 = po chan cham mo123 gravite; po = po2 chan chpo mo123;

cpv = (po exco fx) ** 0.5;
**optio trac x isov surf;
trac su cpv;
*list cpv;

tab1 = table ;
tab1.plastique    = vrai ;
tab1.precision = 1e-4;
tab1.precdecharge = 1e-4;
tab1.autoresu  = 1;
tab1.grands_deplacements = vrai ;
tab1.'K_SIGMA'      = vrai ;
tab1.masse = ma;
tab1.modele = mo123f;
tab1.caracteristiques=ma123f;
tab1.chargement=chr;
tab1.BLOCAGES_MECANIQUES=cl;
ltps = prog 0  1.5 1.6 1.65 pas 0.3 1.80     ;
*ltps = prog 0  1.5 1.6 1.65 pas 0.03     1.85;
*ltps = prog 0  1.5 1.6 pas 0.01 1.65 pas 0.006    1.85;
*ltps = prog 0  1.5 1.6 1.65 pas 0.1     1.85;
tab1.temps_calcules=ltps;
tab1.maxdefor=1e-3;
tab1.'PRECISION'=1E-5;
tab1.deltaiter=29;
ripv = ri et cl;
tab1.automatique  = vrai ;
tab1.autopas = 5000  ;
tab1.'MAXSOUSPAS'=5000;
pasapas tab1 ;

*  on se contente de verifier que le calcul est bien passe.

uu = tab1.deplacements.38;
xx = extr uu ux p16;
valref =  3.22079E-03;
list xx    ;
si (abs (xx - valref) > 1e-3); erreur 5; finsi;
list tab1.temps;
fin;












