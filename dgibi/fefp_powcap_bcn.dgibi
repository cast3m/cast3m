* fichier :  fefp_powcap_bcn.dgibi
************************************************************************
* Section : Mecanique Plastique
* Section : Mecanique Elastique
************************************************************************
* COMPACTION OF A FLANGED COMPONENT
*
*-------------  OPCIONES GENERALES  --------------------------------
*
OPTION DIME 2 TRAC x ELEM qua8 MODE axis;
*
*-------------  CREACION DE LA GEOMETRIA  --------------------------
*
r1 = 6.3D0; r2 = r1 + 4.6D0; r3 = r2 + 15.6D0;
h1 = 0.D0; h2 = h1 + 13.7D0; h3 = h2 + 11.7D0;
p1 =  r1  h1;p2 =  r2  h1;p3 =  r2  h2;
p4 =  r3  h2;p5 =  r3  h3;p6 =  r2  h3;
p7 =  r1  h3;p8 =  r1  h2;paux = r3  h3;
den1 = 5;  den2 = 10; den3 = 10; den4 = 8;
l12 = d den1 p1 p2;l23 = d den2 p2 p3;
l38 = d den1 p3 p8;l81 = d den2 p8 p1;
l34 = d den3 p3 p4;l45 = d den4 p4 p5;
l56 = d den3 p5 p6;l63 = d den4 p6 p3;
l83 = d den1 p8 p3;l36 = d den4 p3 p6;
l67 = d den1 p6 p7;l78 = d den4 p7 p8;
malla1 = daller l12 l23 l38 l81;
malla2 = daller l34 l45 l56 l63;
malla3 = daller l83 l36 l67 l78;
malla = malla1 et malla2 et malla3 ;
elim malla 1.D-4;
llbase = l12 et l34; llsupe = l56 et l67;
lllate = l23 et l45 et l78 et l81;
*
*-------------  MODELO  -------------------------------------------
*
 E_ELAS = 2000.D0;
 NUHOR  = 0.37D0;
 ROHOR  = 2.5D3;
 SIGINI = 90.D0;
 EXPN1  = 1.D0;
 EXPN2  = 2.7D0;
 ETAINI = 0.489D0;
 COHES  = 15.D0;
 ANGFR  = 0.52359878D0 * 2.;
 ANGFR0 = 0.52359878D0 * 1.5;
 NNC    = 2.D0;
 mod1   = 'MODELI' malla mecanique elastique plastique
          powdercap_fefp Q8RI;
 mat1   = mater mod1 youn E_ELAS  nu   NUHOR   rho  ROHOR
                     sigy SIGINI  nnn1 EXPN1   nnn2 EXPN2
                     eta0 ETAINI  cohe COHES
                     phi  ANGFR   phi0 ANGFR0  nnnc NNC     ;
*
*--------------  CONDICIONES DE CONTORNO  -------------------------
*
 rigcont  = (bloq uz llbase) et (bloq ur lllate);
*
*--------------  MOVIMIENTO IMPUESTO  -----------------------------
*
 rigsupe = bloq uz llsupe;
 valor = -6.06D0;
 movyy   = depi rigsupe valor;
 evol1   = evol manu t (prog 0. 1.) level (prog 0. 1.);
 evfut   = char dimp evol1 movyy;
*
*---------------  RESOLUCION  -------------------------------------
*
 maxiter = 50;
 t0 = table;
 t0.MODELE              = mod1;
 t0.CARACTERISTIQUES    = mat1;
 t0.BLOCAGES_MECANIQUES = rigcont et rigsupe;
 t0.CHARGEMENT          = evfut;
 t0.ACCELERATION        = maxiter;
 t0.MAXITERATION        = maxiter;
 t0.PRECISION           = 1.E-10;
 t0.CONVERGENCE_FORCEE  = faux;
 t0.K_TANGENT           = vrai;
 t0.NITER_KTANGENT      = 1;
 t0.PRECISINTER         = 1.E-9;
 t0.NITERINTER_MAX      = 100;
 t0.FEFP_FORMULATION    = vrai;
 t0.UPDATE_LAGRANGIAN   = vrai;
 t0.TEMPS_CALCULES      = prog 0. pas 0.0001 0.001
                                  pas 0.0005 0.01
                                  pas 0.005  0.1
                                  pas 0.010  0.2;
 t0.TEMPS_SAUVES        = t0.TEMPS_CALCULES;
 t0.'PROCESSEURS'='MONO_PROCESSEUR';
 pasapas t0;
*
*---------------  POSTPROCESO  ------------------------------------
*
 imax   = (dime t0.TEMPS_CALCULES) - 1;
 fuer   = prog 0.;
 i      = 1;
 geo0   = form;
 repeter blocdefi imax;
   aux  = reac rigsupe t0.deplacements.i;
   aux  = resu aux;
   pbas = extr aux MAIL ;
   pbas = POIN 1 pbas ;
   aux  = extr aux fz pbas;
   fuer = fuer et (prog aux);
   i=i+1;
 fin blocdefi;
 des = valor * t0.TEMPS_CALCULES;
 law = evol manu t des f(t) fuer;
 err = (aux + 2.1063258E+04)/2.1063258E+04 ;
 list err;
 SI ((ABS err) < 0.5e-5) ;
   ERRE 0 ;
 SINON ;
   ERRE 5 ;
 FINSI ;
 FIN;












