* fichier :  elas17.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
GRAPH='N';
SAUT PAGE ;
*************************************************************************
*    ANALYSE  D'UN TREILLIS AVEC LE CHARGEMENT THERMIQUE
*************************************************************************
*
* DESCRIPTION DU PROBLEME
* -----------------------
*
* UN TREILLIS ARTICULE EST CHARGE PAR UNE FORCE A SON EXTREMITE LIBRE
* ET SOUMIS A UN ECHAUFFEMENT SUR UN DE SES ELEMENTS.IL S'AGIT DE
* CALCULER LES DEPLACEMENTS DES NOEUDS ET LES FORCES DANS LES BARRES.
*
* REFERENCE :
* ----------
* J. S. PRZEMIENIECKI,THEORY OF MATRIX STRUCTURAL ANALYSIS(NEW YORK:
* MCGRAW-HILL BOOK COMPANY,1968),P. 155
************************************************************************
OPTION  ECHO 0 ;
OPTIO DIME 3 ELEM SEG2 ;
*
*  MAILLAGE
*
P1 = 20. 20. 0. ; P2 = 20. 0. 0. ;
P3 = 0. 20. 0.  ; P4 = 0. 0. 0. ;
L1 = D 1 P3 P1 ; L2 = D 1 P4 P1 ;
L3 = D 1 P1 P2 ; L4 = D 1 P3 P2 ;
L5 = D 1 P4 P2 ; L6 = D 1 P3 P4 ;
LA1 = L1   ET L5 ET L6 ;
LA2 = L2 ET L4 ;LTOT = LA1 ET LA2 ET L3 ;
*
*  MODELE
*
 MOD1 = MODEL LA1 MECANIQUE ELASTIQUE BARR ;
 MOD2 = MODEL  LA2 MECANIQUE ELASTIQUE BARR ;
 MOD3 = MODEL  L3 MECANIQUE ELASTIQUE BARR ;
 MODT = MOD1 ET MOD2  ET MOD3 ;
*
*  PROPRIETES DE MATERIAUX
*
 MAT1 = MATER MOD1 YOUN 1.E7 NU 0.3 ALPHA 1.E-6  ;
 MAT2 = MATER MOD2 YOUN 1.E7 NU 0.3 ALPHA 1.E-6  ;
 MAT3 = MATER MOD3 YOUN 1.E7 NU 0.3 ALPHA 1.E-6  ;
 CAR1 = CARAC MOD1 SECT 1.  ;
 CAR2 = CARAC MOD2 SECT 0.7071068  ;
 CAR3 = CARAC MOD3 SECT 1.  ;
 MAT = MAT1 ET MAT2 ET MAT3 ; CAR = CAR1 ET CAR2 ET CAR3 ;
*
* CONDITIONS AUX LIMITES
*
 CL1 = BLOQ L6 'DEPL'  ;
*
*  MATRICE DE RIGIDITE
*
 RIG1 = RIGI MODT (MAT ET CAR) ;
* 1)CHARGEMENT MECANIQUE
*
*  CHARGEMENT MECANIQUE
*
  F1 = FORCE (0. 1000. 0.) P1 ;
*
*  RESOLUTION
*
  DEPM = RESO (RIG1 ET CL1) F1 ;
  SIGM = SIGM MODT (MAT ET CAR) DEPM ;
*2) CHARGEMENT THERMIQUE
*
*  CHAMP DE TEMPERATURE
*
 CTHER1 = MANU 'CHML' L3   'T' 100. TYPE 'TEMPERATURES' ;
 CTHER2 = MANU 'CHML' LA1  'T' 0. TYPE 'TEMPERATURES' ;
 CTHER3 = MANU 'CHML' LA2  'T' 0. TYPE 'TEMPERATURES' ;
 CTHER = CTHER1 ET CTHER2 ET CTHER3 ;
 SIGTH = THETA MODT (MAT ET CAR) CTHER ;
 FTH   = BSIGM MODT SIGTH CAR ;
 DEPTH = RESO (RIG1 ET CL1)  FTH   ;
 SIGTH1 = SIGMA MODT DEPTH (MAT ET CAR) ;
 SIGTH2 = SIGTH1 - SIGTH ;
*
*  MECANIQUE + THERMIQUE
*
 DEP = DEPM + DEPTH ;
 SIG = SIGM + SIGTH2 ;
*
*   COMPARAISON AVEC LES RESULTATS ANALYTIQUES
*
U1 = EXTR DEP UX P1 ;U1TH = -1.272727E-3 ;
ERU1 =((U1 - U1TH) / (ABS U1TH))*100.  ;
V1 = EXTR DEP UY P1 ;V1TH = 6.363636E-3  ;
ERV1 =((V1 - V1TH) / (ABS V1TH))*100.  ;
U2 = EXTR DEP UX P2 ;U2TH = 7.272727E-4 ;
ERU2 =((U2 - U2TH) / (ABS U2TH))*100.  ;
V2 = EXTR DEP UY P2 ;V2TH = 3.636364E-3  ;
ERV2 =((V2 - V2TH) / (ABS V2TH))*100.  ;
*
MESS '       DEPLACEMENTS POINT P1       '   ;
MESS 'U1 THEORIE  'U1TH 'CASTEM  'U1'%ERREUR  'ERU1 ;
MESS 'V1 THEORIE  'V1TH 'CASTEM  'V1'%ERREUR  'ERV1 ;
MESS '       DEPLACEMENTS POINT P3       '   ;
MESS 'U2 THEORIE  'U2TH 'CASTEM  'U2'%ERREUR  'ERU2 ;
MESS 'V2 THEORIE  'V2TH 'CASTEM  'V2'%ERREUR  'ERV2 ;
*
EF1 = EXTR SIG 1 1  1 EFFX ; EF1TH = -636.36 ;
ERF1 = ((EF1 - EF1TH) / (ABS EF1TH)) * 100. ;
EF2 = EXTR SIG 2 1  1 EFFX ; EF2TH = 899.95 ;
ERF2 = ((EF2 - EF2TH) / (ABS EF2TH)) * 100. ;
EF3 = EXTR SIG 3 1  1 EFFX ; EF3TH = 363.64 ;
ERF3 = ((EF3 - EF3TH) / (ABS EF3TH)) * 100. ;
EF4 = EXTR SIG 2 2  1 EFFX ; EF4TH = -514.26 ;
ERF4 = ((EF4 - EF4TH) / (ABS EF4TH)) * 100. ;
EF5 = EXTR SIG 1 2  1 EFFX ; EF5TH = 363.64 ;
ERF5 = ((EF5 - EF5TH) / (ABS EF5TH)) * 100. ;
EF6 = EXTR SIG 1 3  1 EFFX ; EF6TH = 0.  ;
ERF6 = EF6 - EF6TH ;
*
MESS '      EFFORTS DANS LES BARRES   '    ;
MESS'EFF1 THEORIE  'EF1TH'CASTEM  'EF1'%ERREUR  'ERF1 ;
MESS'EFF2 THEORIE  'EF2TH'CASTEM  'EF2'%ERREUR  'ERF2 ;
MESS'EFF3 THEORIE  'EF3TH'CASTEM  'EF3'%ERREUR  'ERF3 ;
MESS'EFF3 THEORIE  'EF4TH'CASTEM  'EF4'%ERREUR  'ERF4 ;
MESS'EFF4 THEORIE  'EF5TH'CASTEM  'EF5'%ERREUR  'ERF5 ;
MESS'EFF5 THEORIE  'EF6TH'CASTEM  'EF6'%ERREUR  'ERF6 ;
*
*  DEFORMEE
*
 DEF1 = DEFORM DEP LTOT 100. ROUG ;
 DEF0 = DEFORM DEP LTOT 0. VERT ;
  SI (NEG GRAPH 'N');
 TRAC (0 0 10000)(DEF0 ET DEF1) ;
 FINSI  ;
*
SI ((MAXI  ABS
(PROG ERF1 ERF2 ERF3 ERF4 ERF5 ERF6 ERV1 ERV2 )) < 5);
ERRE 0;
SINON;
ERRE 5;
FINSI;
FIN ;




