*******************************************************************
*              raff01.dgibi
*******************************************************************
*
* Calcul en mecanique de la rupture avec un maillage raffine par
* l'opérateur RAFF.
*
* d'une plaque elastique en traction en 2D déformations planes
* avec fissure horizontale
* test avec des éléments QUA4 QUA6 TRI3 et TRI6
*
*    ^ ^ ^ ^ ^ ^ ^
*    | | | | | | |
*    -------------
*   |             |<
*   |             |
*   |             |
*   |             |
*   |             |
*   |             |
*   |             |
*   |             |<
*    ---------====
*    ^       ^
*      Dimentions du quart de plaque:
*        l1= 0.1 m
*        l2 = 0.2 m
*      Longueur de la demi-fissure :
*        a=0.03 m
*
*création : gg, le 15.03.2017
*
* Comparaison des facteurs d'intensite de contraintes simules
* avec ceux obtenu par un calcul sur un maillage convergé
* ( mailles de 6 E-4 m en pointe de fissure )

********************************************************************
*OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO TRAC PSC FTRAC tailleplaque ;

OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO TRAC X;
*---------------------------------------------------
* Option de trace
*graph = vrai;
graph = faux;

complet = vrai;
*complet = faux;

*----------------------------------------------------*
* Données
*----------------------------------------------------*

* Données géométriques
l1=0.1;
l2=0.2;
*abssice du front de fissure
X0=0.7;
* Données Materiaux
E1= 7.2E10;
nu1= 0.4;
* Donnée de chargement
pre1=-500;

*coefficient de dilatation deformée
XAMP = 1.E7 ;
*Densitée des maillages
DENS1 = 0.0100001 ;
DENS2 = (DENS1/8)*1.0001 ;
*Parametre RAFF Distance maillage fin
*DMIN = l1*0.15;
DMIN = (2**0.5)*dens1;


*Parametre RAFF Distance maillage large
*DMAX = DMIN+(l1*0.2) ;
DMAX = DMIN+ (2 * DENS1) ;


* Definition des points

P1 = 0. 0. ;
P2 = (l1*X0) (l2*0.) ;
P3 = l1 0. ;
P4 = l1 l2 ;
P5 = 0. l2 ;

* Valeur théorique de KI
* (obtenu avec un calcul overkill) DENS=0.005
FIC0SIF = 163.31;
FIC0GTH = 162.34;
*----------------------------------------------------*

*----------------------------------------------------*
ERRKI= TABL;
KI=TABL;
LSTERR=TABL;
LSTKI=TABL;
*Boucle sur les types d'éléments
ELE1= TABL;
ELE1.1='QUA4';
si complet;
 ELE1.2='QUA8';
 ELE1.3='TRI3';
 ELE1.4='TRI6';
finsi;

I=0;
REPETER ELEMENT1 (dime ELE1) ;

I = I+1 ;
OPTI ELEM (ELE1.I);
MESS ELE1.I;

ERRKI.I = TABL;
KI.I = TABL;

*----------------------------------------------------*
*Definition du maillage grossier
NL11A = ENTI 'PROCH' (l1*X0/DENS1);
NL11B = ENTI 'PROCH' (l1*(1-X0)/DENS1);
NL12 = ENTI 'PROCH' (l2/DENS1);
NL13 = ENTI 'PROCH' (l1/DENS1) ;
L11A = DROITE NL11A P1 P2 ;
L11B = DROITE NL11B P2 P3 ;
L12 = DROITE NL12 P3 P4 ;
L13 = DROITE NL13 P4 P5 ;
L14 = DROITE NL12 P5 P1 ;

SURF1 = DALL (L11A ET L11B) L12 L13 L14 ;

si (graph);
  TRAC SURF1 TITR 'Surf1' ;
finsi;

*Modele
MOD1 = MODE SURF1 MECANIQUE ELASTIQUE EPSI LINEAIRE ;

*Materiau
MAT1 = MATE MOD1 YOUN E1 NU nu1 ;

*Conditions aux limites
CL1 = BLOQUE UY L11A ;
CL2 = BLOQUE UX L12 ;
CLT1 = CL1 ET CL2 ;

*Chargement
F1 = PRES 'MASS' MOD1 pre1 . L13 ;
* F1 = DEPI CL3 0.02 ;

*Rigidite
RIG0 = RIGI MOD1 MAT1 ;
RIG1 = RIG0 et CLT1 ;

*Resolution
RES1 = RESO RIG1 F1 ;

si (graph);

*Deformee
  DEF0 = DEFO 0. SURF1 RES1 'GRIS';
  DEF1 = DEFO XAMP SURF1 RES1 'BLEU';

  TRAC (DEF0 et DEF1) TITR 'Deformee1';
*Deplacement Uy le complet de la lèvre de la fissure
  DEPF1 = EVOL 'ROUG' 'CHPO' RES1 UY L11B ;
  DESS DEPF1 TITR 'Deplacement Uy ';

*Contraintes

  SIG1 = SIGM MOD1 MAT1 RES1 ;
  TRAC 'TITR' 'Contrainte xx force' (EXCO SMXX SIG1) MOD1 ;

  TRAC 'TITR' 'Contrainte yy force' (EXCO SMYY SIG1) MOD1 ;

  TRAC 'TITR' 'Contrainte xy force' (EXCO SMXY SIG1) MOD1 ;


*Contrainte normale de traction le long de l'axe horizontal (lig+levre)
  SIGP1 = CHAN 'CHPO' SIG1 MOD1 ;
  SNT1 = EVOL 'ROUG' 'CHPO' SIGP1 SMYY (L11A ET L11B) ;
  DESS SNT1 TITR 'Contrainte normale';
finsi;

*Facteur d'intensité de contraintes G_THETA
FIC1 = TABLE ;
FIC1 . 'OBJECTIF' = MOT 'J' ;
FIC1 . 'COUCHE' = ENTI 'PROCH' 1 ;
FIC1 . 'LEVRE_SUPERIEURE' = L11B ;
FIC1 . 'FRONT_FISSURE' = P2 ;
FIC1 . 'SOLUTION_RESO' = RES1 ;
FIC1 . 'MODELE' = MOD1 ;
FIC1 . 'CARACTERISTIQUES' = MAT1 ;
FIC1 . 'CHARGEMENTS_MECANIQUES' = F1 ;
FIC1 . 'BLOCAGES_MECANIQUES' = CLT1 ;
G_THETA FIC1 ;

*En contraines planes :
* FIC1GTH= ((FIC . RESULTATS) * (E1))**0.5 ;
*En déformations planes :
FIC1GTH = ((FIC1 . RESULTATS) * (E1/(1-(nu1*nu1))))**0.5 ;
mess 'Facteur Intensité de Contraintes maillage régulier grossier:';
mess FIC1SIF;
mess FIC1GTH;

ERRKI.I.1 = ABS((FIC1GTH - FIC0GTH)/FIC0GTH);
KI.I.1 =FIC1GTH;
*----------------------------------------------------*

si (complet);
*----------------------------------------------------*
*Definition du maillage fin
  NL21A = ENTI 'PROCH' (l1*X0/DENS2);
  NL21B = ENTI 'PROCH' (l1*(1-X0)/DENS2);
  NL22 = ENTI 'PROCH' (l2/DENS2);
  NL23 = ENTI 'PROCH' (l1/DENS2) ;
  L21A = DROITE NL21A P1 P2 ;
  L21B = DROITE NL21B P2 P3 ;
  L22 = DROITE NL22 P3 P4 ;
  L23 = DROITE NL23 P4 P5 ;
  L24 = DROITE NL22 P5 P1 ;

  SURF2 = DALL (L21A ET L21B) L22 L23 L24 ;

  si (graph);
    TRAC SURF2 TITR 'Surf2';
  finsi;
*Modele
  MOD2 = MODE SURF2 MECANIQUE ELASTIQUE EPSI LINEAIRE ;

*Materiau
  MAT2 = MATE MOD2 YOUN E1 NU nu1 ;

*Conditions aux limites
  CL3 = BLOQUE UY L21A ;
  CL4 = BLOQUE UX L22 ;
  CLT2 = CL3 ET CL4 ;

*Chargement
  F2 = PRES 'MASS' MOD2 pre1 . L23 ;
* F2 = DEPI CL3 0.02 ;

*Rigidite
  RIG0 = RIGI MOD2 MAT2 ;
  RIG2 = RIG0 et CLT2 ;

*Resolution
  RES2 = RESO RIG2 F2 ;

  si (graph);
*Deformee
    DEF20 = DEFO 0. SURF2 RES2 'GRIS';
    DEF21 = DEFO XAMP SURF2 RES2 'BLEU';
    TRAC (DEF20 et DEF21) TITR 'Deformee2';

*Deplacement Uy le long de la lèvre de la fissure
    DEPF2 = EVOL 'ROUG' 'CHPO' RES2 UY L21B ;
    DESS DEPF2 TITR 'Deplacement Uy ';

*Contraintes
    SIG2 = SIGM MOD2 MAT2 RES2 ;
    TRAC 'TITR' 'Contrainte xx force' (EXCO SMXX SIG2) MOD2 ;

    TRAC 'TITR' 'Contrainte yy force' (EXCO SMYY SIG2) MOD2 ;

    TRAC 'TITR' 'Contrainte xy force' (EXCO SMXY SIG2) MOD2 ;


*Contrainte normale de traction le long de l'axe horizontal (lig+levre)
    SIGP2 = CHAN 'CHPO' SIG1 MOD1 ;
    SNT2 = EVOL 'ROUG' 'CHPO' SIGP2 SMYY (L21A ET L21B) ;
    DESS SNT2 TITR 'Contrainte normale';
  finsi;

*Facteur d'intensité de contraintes G_THETA
  FIC2 = TABLE ;
  FIC2 . 'OBJECTIF' = MOT 'J' ;
  FIC2 . 'COUCHE' = ENTI 'PROCH' 8 ;
  FIC2 . 'LEVRE_SUPERIEURE' = L21B ;
  FIC2 . 'FRONT_FISSURE' = P2 ;
  FIC2 . 'SOLUTION_RESO' = RES2 ;
  FIC2 . 'MODELE' = MOD2 ;
  FIC2 . 'CARACTERISTIQUES' = MAT2 ;
  FIC2 . 'CHARGEMENTS_MECANIQUES' = F2 ;
  FIC2 . 'BLOCAGES_MECANIQUES' = CLT2 ;
  G_THETA FIC2 ;

*En contraines planes :
*  FIC2GTH= ((FIC . RESULTATS) * (E1))**0.5 ;
*En déformations planes :
  FIC2GTH = ((FIC2 . RESULTATS) * (E1/(1-(nu1*nu1))))**0.5 ;
  mess 'Facteur Intensité de Contraintes maillage régulier raffiné :';
  mess FIC2SIF;
  mess FIC2GTH;
  ERRKI.I.2 = ABS((FIC2GTH - FIC0GTH)/FIC0GTH);
  KI.I.2 = FIC2GTH;
*----------------------------------------------------*
finsi;

*----------------------------------------------------*
*Definition du maillage avec RAFF

*Distance à la pointe de fissure
X Y = COOR SURF1 ;
DPF = (((X-(l1*X0))*(X-(l1*X0)))+(Y*Y))**0.5 ;

* Raff maillage
AFF = (((DENS1-DENS2)*(DPF-DMIN))/(DMAX-DMIN)) + DENS2 ;
DENS3 = BORN AFF 'SCAL' 'COMPRIS' (DENS1*1.001) (DENS2*1.001) ;
SURF3 = RAFF SURF1 DENS3 ;

si (graph);
  TRAC SURF3 TITR 'Surf3';
finsi;

*opti donn 5;

MAILL = SURF3 ;

*Modele
MOD3 = MODE MAILL MECANIQUE ELASTIQUE EPSI LINEAIRE ;

*Materiau
MAT3 = MATE MOD3 YOUN E1 NU nu1 ;

*Contours du maillage pour CL et chargement
CT = CONT MAILL ;
XC YC = COOR CT ;
PTCL0 = YC POIN 'MINI' ;
XC0 YC0 = COOR PTCL0 ;
PTCL1 = XC0 POIN 'INFERIEUR' (l1*X0 + 1E-10) ;
SEGCL1 = CT ELEM 'APPUYE' PTCL1 ;

PTCL2 = XC POIN 'SUPERIEUR' (l1- 1E-10) ;

PTCL3 = YC POIN 'MAXI' ;
SEGCL3 = CT ELEM 'APPUYE' PTCL3 ;

*Fissure
PTFISS = (DIFF PTCL0 PTCL1) ET P2 ;
CT = CONT MAILL ;
LFISS = CT ELEM 'APPUYE' PTFISS ;

*Conditions aux limites
CL5 = BLOQUE UY PTCL1 ;
CL6 = BLOQUE UX PTCL2 ;
CLT3 = CL5 ET CL6 ;

*Chargement
F3 = PRES 'MASS' MOD3 pre1 SEGCL3 ;

*Rigidite
RIG0 = RIGI MOD3 MAT3 ;
REL0 = RELA MOD3 ;


RIG3 = RIG0 et REL0 et CLT3 ;

list rig3;

* opti donn 5;
*Resolution
RES3 = RESO RIG3 F3 ;

si (graph);
*Deformee
  DEF30 = DEFO 0. SURF3 RES3 'GRIS';
  DEF31 = DEFO XAMP SURF3 RES3 'BLEU';
  TRAC (DEF30 et DEF31) TITR 'Deformee3';

*Deplacement Uy le long de la lèvre de la fissure
  DEPF3 = EVOL 'ROUG' 'CHPO' RES3 UY LFISS ;
  DESS DEPF3 TITR 'Deplacement Uy ';

*Contraintes
  SIG3 = SIGM MOD3 MAT3 RES3 ;
  TRAC 'TITR' 'Contrainte xx force' (EXCO SMXX SIG3) MOD3 ;

  TRAC 'TITR' 'Contrainte yy force' (EXCO SMYY SIG3) MOD3 ;

  TRAC 'TITR' 'Contrainte xy force' (EXCO SMXY SIG3) MOD3 ;


*Contrainte normale de traction le long de l'axe horizontal (lig+levre)
  SIGP3 = CHAN 'CHPO' SIG3 MOD3 ;
  SNT3 = EVOL 'ROUG' 'CHPO' SIGP3 SMYY SEGCL1 ;
  DESS SNT3 TITR 'Contrainte normale';
finsi;

*Facteur d'intensité de contraintes G_THETA
FIC3 = TABLE ;
FIC3 . 'OBJECTIF' = MOT 'J' ;
FIC3 . 'COUCHE' = ENTI 'PROCH' 8 ;
FIC3 . 'LEVRE_SUPERIEURE' = LFISS ;
FIC3 . 'FRONT_FISSURE' = P2 ;
FIC3 . 'SOLUTION_RESO' = RES3 ;
FIC3 . 'MODELE' = MOD3 ;
FIC3 . 'CARACTERISTIQUES' = MAT3 ;
FIC3 . 'CHARGEMENTS_MECANIQUES' = F3 ;
FIC3 . 'BLOCAGES_MECANIQUES' = CLT3 ;
G_THETA FIC3 ;

*En contraines planes :
* FIC2GTH= ((FIC . RESULTATS) * (E1))**0.5 ;

*En déformations planes :
FIC3GTH = ((FIC3 . RESULTATS) * (E1/(1-(nu1*nu1))))**0.5 ;
mess 'Facteur Intensité de Contraintes maillage régulier raffiné :';
mess FIC3SIF;
mess FIC3GTH;
ERRKI.I.3 = ABS((FIC3GTH - FIC0GTH)/FIC0GTH);
KI.I.3 = FIC3GTH;

si (complet);
  LSTERR.I = PROG ERRKI.I.1 ERRKI.I.2 ERRKI.I.3 ;
  LSTKI.I = PROG KI.I.1 KI.I.2 KI.I.3 ;
sinon;
  LSTERR.I = PROG ERRKI.I.1 ERRKI.I.3 ;
  LSTKI.I = PROG KI.I.1 KI.I.3 ;
finsi;

SI  (ERRKI.I.3 < (3.0E-2));
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
  Erre 0;
Sinon;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  Erre 5;
finsi;
*opti donne 5;

FIN ELEMENT1;

si (graph);
  EVOLERR1 = EVOL 'MANU' 'Erreur' LSTERR.1 'Erreur' LSTERR.2;

  EVOLERR2 = EVOL 'MANU' 'Erreur' LSTERR.3 'Erreur' LSTERR.4 ;

  @EXCEL1 EVOLERR1 'ERREURS_QUA' ;
  @EXCEL1 EVOLERR2 'ERREURS_TRI' ;
  EVOLKI1 = EVOL 'MANU' 'Erreur' LSTKI.1 'Erreur' LSTKI.2;

  EVOLKI2 = EVOL 'MANU' 'Erreur' LSTKI.3 'Erreur' LSTKI.4 ;

  @EXCEL1 EVOLKI1 'KI_QUA' ;
  @EXCEL1 EVOLKI2 'KI_TRI' ;
finsi;

FIN ;








