* fichier :  newmark1.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Plastique
************************************************************************
*
* Ce cas test verifie que le bilan energetique en dynamique est correct
*
* Verification en comportement et en choc
*
optio dime 2 elem qua4;dens 1;
optio mode plan cont;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;
*
*   test comportement
*
p1=(0 0); p2= 1 0;
l1 = manu seg2 p1 p2;
mo = model l1  MECANIQUE ELASTIQUE PLASTIQUE parfait barr;
ma = mater mo young 1 nu 0.0 rho 0 sigy 1 sect 1;
mas = masse 'DEPL' 1 (manu poi1 p2);
bl = bloq depl (p1 );
lis1=prog 0 pas 0.1   3;
fo=force (manu poi1 p2 ) (2.5 0);
li1 = prog 0  1000;
li2 = prog 1 1;
ev = evol manu t li1 f(t) li2;
cha1 = char meca fo ev;
tab1 = table;
tab1.'BLOCAGES_MECANIQUES'=bl;
tab1.'MODELE'=mo;
tab1.'CARACTERISTIQUES'=ma;
tab1.'CHARGEMENT'=cha1;
tab1.'DYNAMIQUE'=vrai;
tab1.'MASSE_CONSTANTE'=mas;
tab1.'TEMPS_CALCULES'=lis1;
tab1.'SOUS_INCREMENT'=100;
tab1.'MAXDEFOR'=1e-1;
pasapas tab1;
vxx = tab1.vitesses.30 extr p2 ux;
uxx = tab1.deplacements.30 extr p2 ux;
ecin = vxx*vxx / 2.;
eabs = 1.0*(uxx - 1) + 0.5;
eext = 2.5* uxx;
optio echo 0;
mess 'position            ' uxx   ;
mess 'vitesse             ' vxx   ;
mess 'energie cinetique   ' ecin ;
mess 'energie absorbee    ' eabs ;
mess 'travail             ' eext ;
bilan = eext - ecin - eabs;
mess 'bilan               ' bilan;
optio echo 1;
si ((ABS bilan) > 5E-3) ; erreur 5; finsi;


*
* test choc
*
ma = mater mo young 1 nu 0.0 rho 0 sigy 1000 sect 1;
blu = bloq 'MAXI' 'UX' p2;
lis2=prog 0 pas 0.1 1;
di = depi blu 0.5;
cha2 = cha1 et (char dimp di ev);
tab2 = table;
tab2.'BLOCAGES_MECANIQUES'=(bl et blu);
tab2.'MODELE'=mo;
tab2.'CARACTERISTIQUES'=ma;
tab2.'CHARGEMENT'=cha2;
tab2.'DYNAMIQUE'=vrai;
tab2.'MASSE_CONSTANTE'=mas;
tab2.'TEMPS_CALCULES'=lis2;
pasapas tab2;
vxx = tab2.vitesses.10 extr p2 'UX';
uxx = tab2.deplacements.10 extr p2 'UX';
ecin = vxx*vxx / 2.;
eabs = uxx*uxx/2.;
eext = 2.5* uxx;
optio echo 0;
mess 'position            ' uxx   ;
mess 'vitesse             ' vxx   ;
mess 'energie cinetique   ' ecin ;
mess 'energie absorbee    ' eabs ;
mess 'travail             ' eext ;
bilan = eext - ecin - eabs;
mess 'bilan               ' bilan;
optio echo 1;
si ((ABS bilan) > 5E-3) ; erreur 5; finsi;
fin;









