************************************************************************
************************************************************************
* Section : Langage Objets
************************************************************************
* Verification du comportement de l'operateur COUL                     *
* Changement de couleur des objets de type :                           *
* - maillage                                                           *
* - evolutions                                                         *
* - deformees                                                          *
* - vecteurs                                                           *
************************************************************************


** Indicateur de trace
ITRAC = FAUX ;


** Options generales
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' 'QUA8' ;


** Maillage
p0 = 0. 0. ;
p1 = 0.2 0. ;
p2 = 0. 0.2 ;
p3 = 1. 0. ;
p4 = 1. 1. ;
p5 = 0. 1. ;
lqc = CERC 10 p1 p0 p2 ;
ldroi = DROI 5 p3 p4 ;
lhaut = DROI 5 p4 p5 ;
s1 = REGL 'DINI' 0.01 'DFIN' 0.1 lqc (ldroi ET lhaut) ;
lgauc = s1 COTE 2 ;
lbas = s1 COTE 4 ;
cs1 = CONT s1 ;


** Modele et materiau
mo = MODE s1 'MECANIQUE' ;
ma = MATE mo 'YOUN' 210.E9 'NU' 0.3 ;
*
MOP = 'MODE' lhaut 'CHARGEMENT' 'PRESSION' ;
MAP = 'PRES' MOP 'PRES' -1.E2 ;
*
mo = mo 'ET' MOP ;
ma = ma ;
ri0 = RIGI mo ma ;


** Conditions aux limites
bl1 = BLOQ lbas 'UY' ;
bl2 = BLOQ lgauc 'UX' ;
MOP = 'MODE' lhaut 'CHARGEMENT' 'PRESSION' ;
MAP = 'PRES' MOP 'PRES' -1.E2 ;
f1  = 'BSIG' MOP MAP ;


** Resolution
u1 = RESO (ri0 ET bl1 ET bl2) f1 ;


** Test du changment de couleur sur les MAILLAGEs
SI ITRAC ;
  TRAC s1 'TITR' 'Maillage initial' ;
FINSI ;
s2 = s1 COUL ;
SI ITRAC ;
  TRAC s2 'TITR' 'Maillage apres changement par defaut' ;
FINSI ;
s2 = s1 COUL 'ORAN' ;
SI ITRAC ;
  TRAC s2 'TITR' 'Maillage apres changement MOT' ;
FINSI ;


** Test du changment de couleur sur les EVOLUTIOns
ev1 = EVOL 'VERT' 'CHPO' u1 'UX' ldroi ;
ev2 = EVOL 'ROUG' 'CHPO' u1 'UY' lhaut ;
ev3 = ev2 ET ev1 ;
SI ITRAC ;
  DESS ev3 'TITR' 'Evolutions initiales' ;
FINSI ;
ev4 = ev3 COUL ;
SI ITRAC ;
  DESS ev4 'TITR' 'Evolutions apres changement par defaut' ;
FINSI ;
ev4 = ev3 COUL 'ORAN' ;
SI ITRAC ;
  DESS ev4 'TITR' 'Evolutions apres changement MOT' ;
FINSI ;
lm1 = MOTS 'ORAN' 'ORAN' 'CYAN' 'CYAN' ;
ev4 = ev3 COUL lm1 ;
SI ITRAC ;
  DESS ev4 'TITR' 'Evolutions apres changement LISTMOTS' ;
FINSI ;


** Test du changment de couleur sur les DEFORMEes
def1 = DEFO cs1 u1 0. 'VERT' ;
def2 = DEFO s1 u1    'ROUG' ;
def3 = def1 ET def2 ;
SI ITRAC ;
  TRAC def3 'TITR' 'Deformees initiales' ;
FINSI ;
def4 = def3 COUL ;
SI ITRAC ;
  TRAC def4 'TITR' 'Evolutions apres changement par defaut' ;
FINSI ;
def4 = def3 COUL 'ORAN' ;
SI ITRAC ;
  TRAC def4 'TITR' 'Evolutions apres changement MOT' ;
FINSI ;
lm1 = MOTS 'ORAN' 'CYAN' ;
def4 = def3 COUL lm1 ;
SI ITRAC ;
  TRAC def4 'TITR' 'Evolutions apres changement LISTMOTS' ;
FINSI ;


** Test du changment de couleur sur les VECTEURs
r1 = REAC u1 bl1 ;
r2 = REAC u1 bl2 ;
v1 = VECT f1 'FORC' 'JAUN' ;
v2 = VECT r1 'FORC' 'BLEU' ;
v3 = VECT r2 'FORC' 'ROUG' ;
v4 = v1 ET v2 ET v3 ;
SI ITRAC ;
  TRAC v4 s1 'TITR' 'Vecteurs initiaux' ;
FINSI ;
v5 = v4 COUL ;
SI ITRAC ;
  TRAC v5 s1 'TITR' 'Vecteurs apres changement par defaut' ;
FINSI ;
v5 = v4 COUL 'ORAN' ;
SI ITRAC ;
  TRAC v5 s1 'TITR' 'Vecteurs apres changement MOT' ;
FINSI ;
lm1 = MOTS 'ORAN' 'CYAN' 'ROSE' ;
v5 = v4 COUL lm1 ;
SI ITRAC ;
  TRAC v5 s1 'TITR' 'Vecteurs apres changement LISTMOTS' ;
FINSI ;

FIN ;









