* fichier :  flua1t.dgibi
************************************************************************
* Section : Mecanique Fluage
************************************************************************
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
*------------------------------------------------------------------;
*                                                                  ;
TITRE 'CREEP DATA FOR SS 304 LN (RCC-MR) USING 3-D FORMULATION'    ;
****************************************************************
*                                                              *
* TEST DE FLUAGE SUIVANT LE MODELE RCC-MR  POUR ACIERS 304-SS  *
*    POUR UN CUBE SOUMIS A UNE PRESSION SUIVANT UNE FACE.      *       *
*                                                              *
*    COMPARAISON AVEC LA FORMULE ANALYTIQUE DE FLUAGE          *
*                                                              *
*    TEST ELABORE PAR Mr MILLARD.                              *
*    TEST MODIFIE POUR EVOLUTION LE 7/07/92                    *
*                                                              *
*------------------------------------------------------------------;
OPTION ECHO 0                                                      ;
GRAPH = 'N';
OPTION DIME 3 ELEM CUB8                                            ;
DENS 1.                                                            ;
OPTI TRAC BENS;
*__________________________________________________________________;
*                                                                  ;
* SKETCH OF GEOMETRY                                               ;
*                                                                  ;
*__________________________________________________________________;
*                                                                  ;
*                                                                  ;
*          .____.____.                                             ;
*         /.        /|                                             ;
*        / . PZ    / |                                             ;
*     PY/__._.____/...                                             ;
*       | .       | /                                              ;
*       |.        |/                                               ;
*     P0.____.____/                                                ;
*           LOX    PX                                              ;
*                                                                  ;
*                                                                  ;
*                                                                  ;
*__________________________________________________________________;
* GEOMETRY DETAIL                                                  ;
*__________________________________________________________________;
P0   = 0 0 0                                                       ;
PX   = 1 0 0                                                       ;
PY   = 0 1 0                                                       ;
PZ   = 0 0 1                                                       ;
L0X  = P0 DROI 1 PX                                                ;
FAC1 = L0X  TRAN 1 PY                                              ;
VOLM = FAC1 VOLU 1 TRAN PZ                                         ;
OEIL = 20 -30 10                                                   ;
FAC4 = FACE VOLM 2                                                 ;

SI (NEG GRAPH 'N');
TRACE OEIL VOLM                                                   ;
FINSI;
*__________________________________________________________________;
* BOUNDARY CONDITIONS                                              ;
*__________________________________________________________________;
FAC2 = VOLM POIN PLAN P0 PX PZ (0.1)                               ;
FAC3 = VOLM POIN PLAN P0 PY PZ (0.1)                               ;
CL1  = BLOQ UZ FAC1                                                ;
CL2  = BLOQ UY FAC2                                                ;
CL3  = BLOQ UX FAC3                                                ;
CLY  = CL1 ET CL2 ET CL3                                           ;
*__________________________________________________________________;
* MATERIAL DATA                                                    ;
*__________________________________________________________________;
MO  = MODE VOLM 'MECANIQUE' 'ELASTIQUE' 'FLUAGE' 'RCCMR_304'       ;

MAT = MATE MO YOUN 2.00E5 NU 0.3 RHO 7.8E3 ALPH 20.0E-06 SMAX 50.0
               BF1  0.222E-6   BF2  0.036797 BF3 6.0
               RF1  0.4051E-4  RF2 0.031829  RF3 3.5
               AF1  7.544324   SF1 0.2266E-2 AF2 -0.257143E-2
               AF3  0.62202E-4 SF2 41.34                           ;

*__________________________________________________________________;
*  EXTERNAL LOADING                                                ;
*                                                                  ;
*------------------------------------------------------------------;
LOAD  = PRES MASS MO  -200.0 FAC4                                  ;
*                                                                  ;
*__________________________________________________________________;
* STIFFNESS COMPUTATION                                            ;
*__________________________________________________________________;
  RI1 = RIGI  MO MAT                                               ;
  RI2 = RI1 ET CLY                                                 ;
  DD  = RESOL RI2  LOAD                                            ;
  SS  = SIGMA MAT MO DD                                            ;
  SXX = EXTR SS SMZZ 1 1 1                                         ;
*__________________________________________________________________;
*  INITIALISATION DE LA TABLE                                      ;
*__________________________________________________________________;
  LI1 = PROG 0.0   100.0                                           ;
  LI2 = PROG 1.0   1.0                                             ;
  EV  = EVOL MANU T LI1 F(T) LI2                                   ;
  CHA = CHAR 'MECA' LOAD EV                                        ;
si complet;
  LIS = PROG 0.01 PAS 0.01 0.1 0.2 PAS 0.1 1.0 2.0 PAS 1.0 10.0    ;
sinon;
  LIS = PROG  0.01 PAS 0.01 0.1 ;
finsi;
  TAB =  TABLE                                                     ;
  TAB . 'CONTRAINTES' = TABLE                                      ;
  TAB . 'DEPLACEMENTS' = TABLE                                     ;
  TAB . 'CONTRAINTES' . 0 = SS                                     ;
TMASAU=table;
tab . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
  TAB . 'DEPLACEMENTS' . 0 = DD                                    ;
  TAB.'BLOCAGES_MECANIQUES' =  CLY                                 ;
  TAB.'MODELE' = MO                                                ;
  TAB.'CARACTERISTIQUES' = MAT                                     ;
  TAB.'TEMPS_CALCULES' = LIS                                       ;
  TAB.'CHARGEMENT' = CHA                                           ;

  PASAPAS TAB                                                      ;
*__________________________________________________________________;
*   BOUCLE DE CALCUL ANALYTIQUE ET D'EXTRACTION
*   DES RESULTATS
*__________________________________________________________________;
TAB2 = TAB.CONTRAINTES                                             ;
TAB3 = TAB.DEFORMATIONS_INELASTIQUES                               ;

NPAS = 'DIME' LIS                                                  ;
I = 0 ;                                                            ;
LTT  = PROG 0.000                                                  ;
LSXX = PROG SXX                                                    ;
LEXX = PROG 000.0                                                  ;
ldefeq = prog 0.;
S = 200.0;
LEF= prog 0.00;

'REPETER' AFFICHE NPAS                                             ;


I = I + 1                                                          ;
T = 'EXTRAIRE' LIS I                                               ;
SIGM = TAB.CONTRAINTES.I                                           ;                                              ;
*IST SIGM                                                          ;
EPSM = TAB.DEFORMATIONS_INELASTIQUES.I                             ;
LTT = LTT ET (PROG T )                                             ;
SXX  = EXTRAIRE SIGM SMZZ 1 1 1                                    ;
S = SXX;

EXX  = EXTRAIRE EPSM EIZZ 1 1 1                                    ;


LSXX = LSXX ET (PROG SXX )                                         ;
LEXX = LEXX ET (PROG EXX )                                         ;
BF1 = .222E-6; BF2 = .036797; BF3 = 6.0;
RF1 = .4051E-4; RF2 = .031829; RF3 = 3.5;
AF1 = 7.544324; AF2 = -.257143E-2; AF3 = .62202E-4;
SF1 = .2266E-2; SF2 = 41.34;

******* CALCUL ANALYTIQUE ************
HB = BF2*S/BF3;
HR = RF2*S/RF3;


B = BF1*((((EXP(HB))-(EXP(HB*(-1.))))/2)**BF3);

R = RF1*((((EXP(HR))-(EXP(HR*(-1.))))/2)**RF3);

A1 = AF1*B/R;

GS=SF1/RF1*R;

SI ( S > SF2 );
  A2 = AF2+(AF3*S);
SINON ;
  A2=0.;
FINSI;
EF = (A1*(1-(EXP(-1.*R*T)))) + (A2*(1-(EXP(-1.*gS*T)))) +(B*t);

  DEFEQ = EXTRAIRE TAB.VARIABLES_INTERNES.I EPSE 1 1 1;

  ldefeq = ldefeq et ( prog defeq);

LEF = LEF ET (PROG EF);

'FIN' AFFICHE                                                      ;

I = 1;
LER = PROG 0.;

REPETER BOUCLE ( dime LIS);

   i = i + 1;
   exxi = extraire lexx i;
   efi  = extraire lef i;
   eri = abs((exxi-efi)/efi*100);
   ler = ler et (prog eri);

FIN BOUCLE;
LIST LER;

*__________________________________________________________________;
EV1 = EVOL MANU  LTT  LSXX                                         ;
EV2 = EVOL MANU  LTT  LEXX                                         ;

SI ( NEG GRAPH 'N');

DESS EV1                                                           ;
DESS EV2                                                           ;
FINSI;
*__________________________________________________________________;

ermax = maxi ler;
LIST ERMAX;
SI  (ermax <EG 5.);
    ERRE 0;
SINON;
    ERRE 5;
FINSI


FIN;                                                               ;














