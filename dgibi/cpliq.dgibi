* fichier :  cpliq.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
*****************  CAS TEST : cpliq.dgibi   **************************
*
* Test de l'opérateur 'VARI' 'CPLIQ'(P,H)
* Les données sont un FLOTTANT, un LISTREEL ou un CHPO
*
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
'OPTI' 'ECHO' 0 ;
*
*------------------------------------------------------> FLOTTANT
*
X1 = 1.D5 ;
X2 = 2.093D5 ;
X3 = 'VARI' 'CPLIQ' X1 X2 ;
XSTO1 = X3 ;
*
X1 = 5.D5 ;
X2 = 2.097D5 ;
X3 = 'VARI' 'CPLIQ' X1 X2 ;
XSTO2 = X3 ;
*
X1 = 10.D5 ;
X2 = 2.101D5 ;
X3 = 'VARI' 'CPLIQ' x1 x2 ;
XSTO3 = X3 ;
*------------------------------------------------------> LISTREEL
Y1 = 'PROG' 1.d5 5.d5 10.d5 ;
Y2 = 'PROG' 2.093E5 2.097E5 2.101e5 ;
Y3 = 'VARI' 'CPLIQ' Y1 Y2 ;
YSTO1 = EXTR Y3 1 ;
YSTO2 = EXTR Y3 2 ;
YSTO3 = EXTR Y3 3 ;
*------------------------------------------------------> CHPO
P1   = 1.  2.093 ;
P2   = 5.  2.097 ;
P3   = 10. 2.101 ;
P1P3 = P1 'DROI' 1 P2 'DROI' 1 P3 ;
Z1   = 1.D5 * ('COOR' 1 P1P3) ;
Z2   = 1.D5 * ('COOR' 2 P1P3) ;
Z3   = 'VARI' 'CPLIQ' Z1 Z2 ;
ZSTO1 = 'EXTR' Z3 'SCAL' P1  ;
ZSTO2 = 'EXTR' Z3 'SCAL' P2  ;
ZSTO3 = 'EXTR' Z3 'SCAL' P3  ;
*-------------------------------------------> Controle
CTRL1 = XSTO1 + YSTO1 + ZSTO1 / 3.d0 - XSTO1 / XSTO1 ;
CTRL2 = XSTO2 + YSTO2 + ZSTO2 / 3.d0 - XSTO2 / XSTO2 ;
CTRL3 = XSTO3 + YSTO3 + ZSTO3 / 3.d0 - XSTO3 / XSTO3 ;
CTRL4 = CTRL1 + CTRL2 + CTRL3 ;
XREF1 = 4181. ;
XREF2 = 4182. ;
XREF3 = 4179. ;
CTRL5 = XSTO1 + YSTO1 + ZSTO1 / 3.d0 - XREF1 / XREF1 ;
CTRL6 = XSTO2 + YSTO2 + ZSTO2 / 3.d0 - XREF2 / XREF2 ;
CTRL7 = XSTO3 + YSTO3 + ZSTO3 / 3.d0 - XREF3 / XREF3 ;
CTRL8 = CTRL5 + CTRL6 + CTRL7 ;
*-------------------------------------------> Affichage
'MESS' '     ' ;
'MESS' '     ' ;
'MESS' 'Point 1  VARI CPLIQ ----->' XSTO1 YSTO1 ZSTO1 XREF1 ;
'MESS' 'Point 2  VARI CPLIQ ----->' XSTO2 YSTO2 ZSTO2 XREF2 ;
'MESS' 'Point 3  VARI CPLIQ ----->' XSTO3 YSTO3 ZSTO3 XREF3 ;
'MESS' '     ' ;
'MESS' 'Comparaison calculs rel.----->' CTRL4 ;
'MESS' 'Erreur rel. VDI         ----->' CTRL8 ;
'MESS' '     ' ;
'MESS' '     ' ;
'MESS' '     ' ;
*-------------------------------------------> Compte-rendu et sortie
EPS4  = 1.E-14 ;
EPS8  = 1.E-2  ;
CTRL4 = ABS CTRL4 ;
CTRL8 = ABS CTRL8 ;
LOG4 = CTRL4 > EPS4 ;
LOG8 = CTRL8 > EPS8 ;
L0   = LOG4 'OU' LOG8 ;
'SI' L0        ;
   'ERREUR' 5  ;
'SINON'        ;
   'ERREUR' 0  ;
'FINSI'        ;
*
'FIN' ;





