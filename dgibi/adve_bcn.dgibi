* fichier :  adve_bcn.dgibi
************************************************************************
* Section : Thermique Advection
************************************************************************
*                                                                      *
* On resout : v.gradT = 4,                                             *
*                                                                      *
* Avec : vx=1, vy=1                                                    *
* on a : dT/dx + dT/dy = 4                                             *
*                                                                      *
* De + : T(1,0)=0, T(0,1)=0                                            *
* Sur 1 element QUA4 de geometrie carree et de cote 1.                 *
*                                                                      *
* La solution analytique est : T(x,y) = 2(x+y-1)                       *
* Soit : T(0,0)=T(P1)=-2 et T(1,1)=T(P3)=2                             *
*                                                                      *
***************************************************
** TESTING FILE FOR THE OPERATOR ADVE BUILDING A  *
** NON-SYMMETRIC STIFFNESS MATRIX                 *
***************************************************

OPTI DIME 2 ELEM qua4 MODE PLAN CONT;
p1 = 0. 0.;
p2 = 1. 0.;
p3 = 1. 1.;
p4 = 0. 1.;
LI1 = droite 1 p1 p2;
LI2 = droite 1 p2 p3;
LI3 = droite 1 p3 p4;
LI4 = droite 1 p1 p4;

SU =  DALLER LI1 LI2 LI3 LI4;
MOD1= MODE SU THERMIQUE ISOTROPE advection;

* La densite de puissance thermique :
vec1 = 'SOUR' MOD1 SU 4. ;

* Le vecteur vitesse :
jxx  = MANU 'CHPO' SU 1 'UX' 1. ;
JJxx = CHAN 'CHAM' Jxx MOD1 'RIGIDITE' 'CARACTERISTIQUES' ;
jyy  = MANU 'CHPO' SU 1 'UY' 1. ;
JJyy = CHAN 'CHAM' Jyy MOD1 'RIGIDITE' 'CARACTERISTIQUES' ;

jj = mate MOD1 'K' 1. 'RHO' 1. 'C' 1. 'UX' jjxx 'UY' jjyy;
matno1 = ADVE mod1 JJ;
list matno1;

*Matrice élémentaire non-symétrique
*-0.33333E+00  0.83333E-01  0.16667E+00  0.83333E-01 ;
*-0.25000E+00  0.00000E+00  0.25000E+00  0.00000E+00 ;
*-0.16667E+00 -0.83333E-01  0.33333E+00 -0.83333E-01 ;
*-0.25000E+00  0.00000E+00  0.25000E+00  0.00000E+00 ;

*A linear system is solved with a non-symmetric matrix

* Les temperatures imposees :                                          *
BL2 = BLOQ 'T' P2;
BL4 = BLOQ 'T' P4;
SOL7 = RESOU (matno1 et (bl2 et bl4)) vec1;

* Verification :
TP1  = -2. ;
TP3  = +2. ;
ERR1 = ABS((EXTRAIRE SOL7 'T' P1) - TP1) ;
ERR2 = ABS((EXTRAIRE SOL7 'T' P3) - TP3) ;
SI ((ERR1+ERR2) > 1.E-10);
  ERRE 5 ;
* MESS '###### TEST NOT PASSED ######';
SINON;
  ERRE 0 ;
* MESS '###### TEST PASSED ######';
FINSI;

*****************************************
matno1 = ADVE mod1 JJ 'SYMM';
list matno1;

*Matrice élémentaire symétrique
*-0.33333E+00 -0.83333E-01 0.00000E+00 -0.83333E-01
*-0.83333E-01  0.00000E+00 0.83333E-01  0.00000E+00
* 0.00000E+00  0.83333E-01 0.33333E+00  0.83333E-01
*-0.83333E-01  0.00000E+00 0.83333E-01  0.00000E+00

FIN;


