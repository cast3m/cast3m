* fichier :  rupt14-weib.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*----------------------------------------------------------------
*----------------------------------------------------------------
*                                                               *
*       Test du critère de Weibull pour un cylindre             *
*       en traction modelisé en axisymétrique et en 3D          *
*                                                               *
*----------------------------------------------------------------
*----------------------------------------------------------------
*                                                               *
opti echo 1     ; monop=faux;                                   ;
*               paramètres pour calculer sigma w                *
m = 22. ; v0 = 0.000125 ; sigu = 2530. ; ic = 1. ; n = 2.       ;
*                                                               *
*****************************************************************
*                                                               *
*                       MODELE AXISYMETRIQUE                    *
*                                                               *
*****************************************************************
*
*****************************************************************
*               Maillage                                        *
*****************************************************************
opti elem qua8 dime 2 mode axis                                 ;
*                                                               *
*               densites utilisees                              *
l1 = 0.25 ;d1 = 50e-3 ; d2 = 120e-3 ; d11 = d1/10.              ;
*                                                               *
*               coordonnees des points                          *
po = 0. 0. ; pb = l1 0. ; pc = 0. d1 ; pd = 0. l1               ;
pa = d1 0.                                                      ;
*                                                               *
*               definition des lignes                           *
ligoa = droi po pa 1                                            ;
ligab = droi pa pb  'DINI' 0.08 'DFIN' 0.12                     ;
ligob = ligoa et ligab                                          ;
surf1 = ligob tran 1 pc                                         ;
*
lig1 = cote 3 surf1                                             ;
*                                                               *
*               création de la surface                          *
surf3 = lig1 tran 'DINI' 0.08 'DFIN' 0.12 (0. (l1-d1))          ;
surf2 = surf1 et surf3                                          ;
*                                                               *
*               extraction des lignes                           *
lib = cote 1 surf2                                              ;
lic = cote 3 surf2                                              ;
*                                                               *
elim surf2 1e-3                                                 ;
*                                                               *
*****************************************************************
*               création du modéle                              *
*****************************************************************
mod1 = mode surf2 mecanique elastique plastique                 ;
*                                                               *
*****************************************************************
*               définition du matériau                          *
*****************************************************************
*                                                               *
*               courbe de traction                              *
peps = prog 0. 0.0026 0.0027 0.0030 0.0035 0.0045 0.0065 0.0085  0.0105 0.0125 0.0176 0.0227 0.0329                       ;
psig = prog 0. 503. 503. 503. 503. 503. 503. 503. 503. 516. 538.  557. 594.                                                ;
tracevt = evol manu 'eps' peps 'sig' psig                       ;
*                                                               *
mat1 = mate mod1 youn (503./0.0026) nu 0.3 tract tracevt        ;
*                                                               *
*****************************************************************
*               rigidité                                        *
*****************************************************************
cdl0 = bloq uz lib                                              ;
cdl2 = bloq uz lic                                              ;
dep1 = depi cdl2 4e-3                                           ;
forcevt = evol manu abs (prog 0. 1.) ord (prog 0. 1.)           ;
fo1 = char 'DIMP' dep1 forcevt                                  ;
*                                                               *
*****************************************************************
*               definition de la table d'entree                 *
*****************************************************************
tab1 = table                                                    ;
tab1.caracteristiques = mat1                                    ;
tab1.modele = mod1                                              ;
tab1.blocages_mecaniques = cdl0 et cdl2                         ;
tab1.chargement = fo1                                           ;
tab1.temps_calcules = (prog 0. pas 0.5 1.)                      ;
*                                                               *
*****************************************************************
*               procedure de calcul                             *
*****************************************************************
TMASAU=table;
tab1 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
tab1.'NB_EL_PARA'= 2;
pasapas tab1                                                    ;
*                                                               *
*****************************************************************
*               Calcul de la contrainte de Weibull axi          *
*****************************************************************
*                                                               *
*               zone du critere                                 *
surfcrit = elem surf2 1                                         ;
zone1 = redu mod1 surfcrit;
*                                                               *
enttab1 = table                                                 ;
enttab1.'XMULT' = 2.                                            ;
enttab1.'OBJMO' = zone1                                         ;
enttab1.'WEIBULL' = vrai                                        ;
enttab1.'TNONL' = tab1                                          ;
enttab1.'M' = m ; enttab1.'V0' = v0 ; enttab1.'SIGU' = sigu     ;
enttab1.'IC' = ic ; enttab1.'N' = n                             ;
enttab1.'TEMPER' = 0                                            ;
*                                                               *
sortab1 = critloc enttab1;
*                                                               *
sigwaxi = (sortab1.'SIGW').1.                                   ;
*                                                               *
*****************************************************************
*                                                               *
*                       MODELE 3D                               *
*                                                               *
*****************************************************************
*
*****************************************************************
*               Maillage                                        *
*****************************************************************
opti dime 3 elem cu20 mode trid                                 ;
*                                                               *
*               variables                                       *
a1 = 2**(-0.5)                                                  ;
l1 = 50e-3 ; l2 = 250e-3 ; oeil = 2 -1000 300                   ;
*                                                               *
*               coordonnees des points                          *
pb1 = 0. 0. 0. ; pba = l1 0. 0. ; pbc = 0. l1 0.                ;
pbb = l1 l1 0. ; pb2 = l2 0. 0. ; pb3 = (l2*a1) (l2*a1) 0.      ;
pb4 = 0. l2 0.; ph1 = 0. 0. l2                                  ;
*                                                               *
*               densites utilisees                              *
d1 = 80e-3 ; d2 = 120e-3                                        ;
*                                                               *
*               definition des lignes                           *
ligb1a = droit 1 pb1 pba                                        ;
ligbab = droit 1 pba pbb                                        ;
ligbbc = droit 1 pbb pbc                                        ;
ligbc1 = droit 1 pbc pb1                                        ;
ligba2 = droit pba pb2 dini d1 dfin d2                          ;
ligb3b = droit 2 pb3 pbb                                        ;
ligbc4 = droit pbc pb4 dini d1 dfin d2                          ;
arcb23 = cerc 1 pb2 pb1 pb3                                     ;
arcb43 = cerc 1 pb4 pb1 pb3                                     ;
*                                                               *
*               definition de la surface du bas                 *
surfb1 = dall ligb1a ligbab ligbbc ligbc1 plan                  ;
surfb2 = dall ligb3b ligbbc ligbc4 arcb43 plan                  ;
surfb3 = dall ligba2 arcb23 ligb3b ligbab plan                  ;
surfbas = surfb1 et surfb2 et surfb3                            ;
*                                                               *
*               creation du volume                              *
volu1 = surfbas volu dini l1 dfin d2 tran ph1                   ;
surfhaut = face 2 volu1                                         ;
surflat = face 3 volu1                                          ;
*                                                               *
*               extraction des surfaces laterales               *
*surface oxz                                                    *
poxz = poin surflat plan pb1 (2. 0. 2.) (0. 0. 2.) (1e-3)       ;
surfl1 = elem surflat appu strict poxz                          ;
*surface poyz                                                   *
poyz = poin surflat plan pb1 pb4 ph1 (1e-3)                     ;
surfl2 = elem surflat appu strict poyz                          ;
*                                                               *
elim volu1 (1e-3)                                               ;
elim (surfbas et surfhaut et surfl1 et surfl2) (1e-3)           ;
*                                                               *
*****************************************************************
*               conditions de symetrie                          *
*****************************************************************
condi1 = symt depl pb1 ph1 pb2 volu1 (1e-3)                     ;
condi2 = symt depl pb1 ph1 pb4 volu1 (1e-3)                     ;
condi3 = symt depl pb1 pb2 pb4 volu1 (1e-3)                     ;
condit = condi1 et condi2 et condi3                             ;
*                                                               *
*****************************************************************
*               creation du modele                              *
*****************************************************************
mod2 = MODE volu1 mecanique elastique plastique                 ;
*                                                               *
*****************************************************************
*               definition du materiau                          *
*****************************************************************
*                                                               *
mate2 = MATE mod2 young (503./0.0026) nu 0.3 tract tracevt      ;
*                                                               *
*****************************************************************
*               rigidite                                        *
*****************************************************************
blo1 = bloq uz surfbas                                          ;
rig1 = condit et blo1                                           ;
rig2 = bloq uz surfhaut                                         ;
rigt = rig1 et rig2                                             ;
dep2 = depi rig2 (4e-3)                                         ;
forcevt = evol manu abs (prog 0. 1.) ord (prog 0. 1.)           ;
fo2 = char 'DIMP' dep2 forcevt                                  ;
*                                                               *
*****************************************************************
*               definition de la table d'entree                 *
*****************************************************************
tab2 = table                                                    ;
tab2.'CARACTERISTIQUES' = mate2                                 ;
tab2.'MODELE' = mod2                                            ;
tab2.'BLOCAGES_MECANIQUES' = rigt                               ;
tab2.'CHARGEMENT' = fo2                                         ;
tab2.'TEMPS_CALCULES' = (prog 0. pas 0.5 1.)                    ;
*                                                               *
*****************************************************************
*               procedure de calcul                             ;
*****************************************************************
TMASAU=table;
tab2 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;

pasapas tab2                                                    ;
*                                                               *
*****************************************************************
*               Calcul de la contrainte de Weibull 3D           *
*****************************************************************
*                                                               *
*               zone du critere                                 *
volucrit = volu1 elem contenant pb1                             ;
zone2 = redu mod2 volucrit;
*                                                               *
enttab2 = table                                                 ;
enttab2.'XMULT' = 8.                                            ;
enttab2.'OBJMO' = zone2                                         ;
enttab2.'WEIBULL' = vrai                                        ;
enttab2.'TNONL' = tab2                                          ;
enttab2.'M' = m ; enttab2.'V0' = v0 ; enttab2.'SIGU' = sigu     ;
enttab2.'IC' = ic ; enttab2.'N' = n                             ;
enttab2.'TEMPER' = 0                                            ;
*                                                               *
sortab2 = critloc enttab2                                       ;
*                                                               *
sigw3d = (sortab2.'SIGW').1.                                    ;
*                                                               *
*****************************************************************
*               Test d'erreur                                   *
*****************************************************************
ecart = ( abs(sigwaxi-sigw3d) ) / sigwaxi                       ;
mess 'ecart relatif : ' (ecart*100.) '%'                        ;
si (ecart < 0.1 )                                               ;
        erre 0                                                  ;
sinon                                                           ;
        erre 5                                                  ;
finsi                                                           ;
*                                                               *
fin                                                             ;
*----------------------------------------------------------------















