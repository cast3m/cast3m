* fichier : fibre1.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
************************************************************************
************************************************************************
*                 CAS TEST fibre1.dgibi                                *
*             Exemple d'utilisation du modèle à fibre                  *
*                                                                      *
*              Poutre avec un déplacement imposé                       *
*                            L=1800 mm                                 *
*                                                                      *
*                      D. Combescure - Décembre 2006                   *
*                      Laboratoire DYN - CEA Saclay                    *
************************************************************************
************************************************************************
*
*-----------------------------------------------------------------------
*    Géométrie : 200mm x 100mmx L=1500m +2*150.
*    Beton: fcj = 25,1 MPa, ftj = 3,08 MPa, Eij = 23GPa
*    Aciers :
*             1 lit inférieur 2HA12 + 1 lit supérieur avec 2HA6
*             cadres n = 1, fi6e200
*-----------------------------------------------------------------------
*FLAGVISU = VRAI;
*FLAGLONG = VRAI;
*
FLAGVISU = FAUX;
FLAGLONG = FAUX;
*
 OPTI DIME 2 ELEM QUA4;

*
 phi6  = 0.006;
 phi8  = 0.008;
 phi10 = 0.010;
 phi12 = 0.012;
 phi13 = 0.013;
 phi19 = 0.019;

 ceq6  = 3.14159*phi6 /4.0;
 ceq8  = 3.14159*phi8 /4.0;
 ceq10  = 3.14159*phi10 /4.0;
 ceq12 = 3.14159*phi12/4.0;
 ceq13 = 3.14159*phi13/4.0;
 ceq19 = 3.14159*phi19/4.0;

 sec6 = 3.14159*((phi6**2)/4.);
 sec8 = 3.14159*((phi8**2)/4.);
 sec10 = 3.14159*((phi10**2)/4.);
 sec12 = 3.14159*((phi12**2)/4.);
 sec13 = 3.14159*((phi13**2)/4.);
 sec19 = 3.14159*((phi19**2)/4.);
*
fyacier = 700.E6;
valtraf = 5.;
*
* Dimensions de la poutre et ferraillage
*
 Happui = 1.50;
 Htot = Happui +(2.*0.15);
 phiacier = phi12;
 secacier = sec12;
 hgthc = 0.20;
 wdthc = 0.10;
 epenro = 0.020;
*
*-------------------------------------------------------------------------
*    Maillage fibre acier - supérieur
*-------------------------------------------------------------------------
 CA1  = (((-0.5)*wdthc) + epenro) ((-0.5*hgthc) + epenro) ;
 CA2  = ((0.5*wdthc) - epenro) ((-0.5*hgthc) + epenro) ;
 SAINF = CA1 ET CA2;

 CA3  = (((-0.5)*wdthc) + epenro) ((0.5*hgthc) - epenro) ;
 CA4  = ((0.5*wdthc) - epenro) ((0.5*hgthc) - epenro) ;
 SASUP = CA3 ET CA4;

 SACIER = (SAINF COUL ROSE) et (SASUP COUL ROUGE);


*-----------------------------------------------------------------------
*    Description du coffrage poteau et des sections BA correspondantes.
*-----------------------------------------------------------------------


 pbc1 = (-0.5*wdthc) (-0.5*hgthc);
 pbc2 = (-0.5*wdthc) ( 0.5*hgthc);
 vbc3 =    wdthc           0.000;

 SBET  = DROI 8 pbc1 pbc2 TRAN 1 vbc3;


*-----------------------------------------------------------------------
*    Modèles sur maillage sections
*-----------------------------------------------------------------------

 OPTI DIME 3 elem qua4;

*-------------------------- Matériau  Béton ----------------------------

 EBET  = 23.0E9 ;
 NUBET = 0.25   ;
 FCBET = 25.1E6 ;
 FTBET = 3.08E6 ;
 RBET = 2500.;

 modBC = MODE sBET MECANIQUE ELASTIQUE PLASTIQUE BETON_BAEL QUAS;
 matBC = MATE modBC 'YOUN' EBET  'NU' NUBET 'RHO' 2500. 'FC' FCBET;


 carBC = CARA modBC ALPY .66 ALPZ .66;

*-------------------------- Matériau  Acier ----------------------------

*  Acier sans prise en compte de l'adhérence
*  Béton: confiné, adhérence: bonne

paci = ((2.*sec6) + (2.*sec12))/(hgthc*wdthc);
*
* Modèles, matériaux et caractéristiques

MESHFIB = SBET et SAINF et SASUP;

*
* Acier PARFAIT_UNI
*
 modAINF = MODE SAINF MECANIQUE ELASTIQUE PLASTIQUE PARFAIT_UNI POJS;

 matAINF = MATE modAINF YOUN 2.0E11 NU 0.3 RHO 7100
          'SECT' sec12 'SIGY' fyacier H (0.01*2.0E11);

 carAINF = CARA modAINF ALPY .66 ALPZ .66;
*
 modASUP = MODE SASUP MECANIQUE ELASTIQUE PLASTIQUE PARFAIT_UNI POJS;

 matASUP = MATE modASUP YOUN 2.0E11 NU 0.3 RHO 7100
          'SECT' sec6 'SIGY' fyacier H (0.01*2.0E11);

 carASUP = CARA modASUP ALPY .66 ALPZ .66;
*
*
 modstot = modBC ET modAINF et modASUP;


 matstot = matBC ET carBC ET matAINF ET carAINF
        ET matASUP ET carASUP;



*-----------------------------------------------------------------------
*    Géométrie de la poutre en 3D.
*-----------------------------------------------------------------------

 OPTI ELEM SEG2;

 oeil = 0. -10000. 0.;
SI FLAGLONG;
 n1 = 3;
 n2 = 15;
SINON;
 n1 = 1;
 n2 = 3;
FINSI;
 p0 = 0. 0. 0.;
 p1d = (0.5*Happui) 0. 0.;
 p2d = (0.5*Htot) 0. 0.;
 p1g = (-0.5*Happui) 0. 0.;
 p2g = (-0.5*Htot) 0. 0.;

 Lpoutre = (d n1 p2g p1g) et (d n2 p1g p0)
         et (d n2 p0 p1d) et (d n1 p1d p2d);


*-----------------------------------------------------------------------
*    Adaptation des modèles et caractéristiques des matériaux béton et
*    acier sur maillage frame: modèles a fibres d'élément de Timoshenko
*-----------------------------------------------------------------------

modpout = MODE Lpoutre MECANIQUE ELASTIQUE SECTION
                PLASTIQUE SECTION TIMO;
matpout = MATE modpout MODS modsTOT MATS matsTOT VECT (0 1 0);


*  Image 3D de la poutre

 TITR 'Maillage 3D: Partie courante du poteau' ;
 tabtrac = table;
 tabtrac.'MATS' = exco matpout MATS;
 pot3D = POUT2MAS modpout matpout gauss tabtrac;
*
SI (EGA FLAGVISU VRAI);
 opti sort 'Representation3D.inp';
 sort avs pot3D (tabtrac.'MATS_3D');
 opti trac open;
 TRAC (700 -1000 700) (Lpoutre et pot3D);
FINSI;
*
*-----------------------------------------------------------------------
*    Conditions aux limites
*-----------------------------------------------------------------------

con1 = BLOQ UY RX LPOUTRE;
con2 = BLOQ UZ (p1g et p1d);
con3 = BLOQ UX p0;

*-----------------------------------------------------------------------
*    Construction de la matrice de rigidité : rigpot
*-----------------------------------------------------------------------

 rigpout = RIGI modpout matpout ;
 rigtot = rigpout et con1 et con2 et con3;

*-----------------------------------------------------------------------
*    Construction de la matrice de masse et verification de la masse
*-----------------------------------------------------------------------

 Maspout  = MASS modpout matpout ;
 ch1 = MANU CHPO lpoutre 1 1.0 UZ ;
 mt1 = ch1 * maspout ;
 mt  = RESU mt1 ;
* mt contient la masse totale

*-----------------------------------------------------------------------
*    Calcul des fréquences propres du système.
*-----------------------------------------------------------------------
*besoin de def lineaire pour le calcul modal
 OPTI EPSI LINEAIRE;
 freqsol = VIBR 'PROCHE' (prog 1.) (lect 2)
             rigtot maspout ;

 freq1 = freqsol.MODES.1 .FREQUENCE;
 dptm1 = freqsol.MODES.1 .DEFORMEE_MODALE;

 freq2 = freqsol.MODES.2 .FREQUENCE;
 dptm2 = freqsol.MODES.2 .DEFORMEE_MODALE;

 df0 = DEFO lpoutre dptm1 0.0 JAUN ;
 df1 = DEFO lpoutre dptm1 0.5 BLEU ;
 df2 = DEFO lpoutre dptm2 0.5 BLEU ;
 tabtrac = table;
 tabtrac.DEPLACEMENTS = TABLE;
 tabtrac.DEPLACEMENTS . 1 = dptm1;
 tabtrac.DEPLACEMENTS . 2 = dptm2;

 mesh3D = pout2mas modpout matpout gauss tabtrac;

 df3D1 = defo mesh3D
         (tabtrac.DEPLACEMENTS_3D . 1) 0.5;
 df3D2 = defo mesh3D
         (tabtrac.DEPLACEMENTS_3D . 2) 0.5;
*
SI (EGA FLAGVISU VRAI);
 TITR (chain 'Mode 1 - Frequence' freq1 'Hz');
 TRAC oeil (df0 ET df1 et df3D1) ;
 TITR (chain 'Mode 2 - Frequence' freq2 'Hz');
 TRAC oeil (df0 ET df2 et dF3D2) ;
FINSI;
*

**********************************************
*
* Chargement statique à déplacement imposé
*
*********************************************
* et on repasse en def quadratique pour le calcul pasapas...
 OPTI EPSI QUADRATIQUE;
*   Déplacement imposé vers le bas
* Pour L=1.80m
*
 dmax =  -0.010  ;
*
 dp   =  -0.0002   ;
 ld6 = PROG 0. PAS dp  dmax;
 nt6 = dime ld6 ;
 MESS 'Nombre de pas de calcul = ' nt6 ;
 tm6 = nt6 - 1.0 ;
 lt6 = prog 0.0 pas 1.0 tm6;
 titr 'Déplacement en tête, nombre de pas =' nt6 ;
 evpzm = EVOL MANU 'Temps' lt6 'Dépl(mm)' ld6 ;

*  Déplacement imposé

 upzm  = BLOQ UZ p0;
 dpzm  = DEPI upzm 1.0 ;
 chpzm = CHAR DIMP dpzm evpzm ;

*-----------------------------------------------------------------------
*    Calcul statique - chargement monotone
*-----------------------------------------------------------------------
SI (EGA FLAGLONG VRAI);
 tcalc = PROG 0. pas 1.0 (ENTI tm6)  ;
SINON;
 tcalc = PROG 0. pas 1.0 2.0  ;
FINSI;
 tsauv = tcalc ;
*
 TAB1 = TABLE;
 TAB1 . BLOCAGES_MECANIQUES = con1 et con2 et con3 et
                              upzm;
 TAB1 . MODELE              = modpout;
 TAB1 . CARACTERISTIQUES    = matpout;
 TAB1 . CHARGEMENT          = chpzm ;
 TAB1 . TEMPS_CALCULES      = tcalc  ;
 TAB1 . TEMPS_SAUVES        = tsauv  ;
 TAB1 . PRECISION           = 1.E-6  ;
 TAB1. MAXDEFOR             = 1.E-5;
 TAB1 . MOVA                = RIEN   ;
 TAB1 . MAXITERATION        = 150    ;

PASAPAS TAB1;


N_REP = (DIME (TAB1.'TEMPS')) - 1 ;
T_REP = TAB1.'TEMPS'.N_REP ;
LIST N_REP;
LIST T_REP;


*-----------------------------------------------------------------------
*    Post-Traitement
*-----------------------------------------------------------------------

dep0 = TAB1.'DEPLACEMENTS';
tim0 = tab1. TEMPS;
sig0 = TAB1.'CONTRAINTES';
var0 = TAB1.'VARIABLES_INTERNES';
ind0 = INDE dep0;
time = PROG (tim0 . 0);

uzp0  = PROG 0.;
fzp0  = PROG 0.;

ep1 = prog 0.;
si1 = prog 0.;
nn = (('DIME' ind0) - 1);

j = 1;
REPE lab1 (('DIME' ind0) - 1);
  j = j + 1;
  indj  = ind0.j;
  depj  = dep0.indj;
  sigj = sig0.indj;
  epsj = epsi depj modpout matpout;
  vecj  = bsigma sigj modpout matpout;
  time = time et (prog tim0. &lab1);
  uzp0 = uzp0 ET (PROG (EXTR depj 'UZ' p0));
  fzp0 = fzp0 ET (PROG (EXTR vecj 'FZ' p0));
FIN lab1;
*
evtiuz = evol manu 'TEMPS(s)'  time
           'Depl (mm)' ((1000.)*uzp0);
evtifz = evol manu 'TEMPS(s)'  time
           'Force (kN)' ((0.001)*fzp0);
*
SI (EGA FLAGVISU VRAI);
 TITR 'Deplacement p0' ;
 dess evtiuz;
 TITRE 'Force';
 dess evtifz;
FINSI;
*
tabtrac = table;
tabtrac. 'DEPLACEMENTS' = table;
tabtrac. 'VAIS' = table;
tabtrac. 'VONS' = table;
nlab2 = entier ((('DIME' ind0))) - 1;
depj  = dep0.nlab2;
varj  =  var0.nlab2;
tabtrac. 'DEPLACEMENTS' . 1 =depj;
tabtrac. 'VAIS' . 1 = exco varj 'VAIS';
tabtrac. 'VONS' . 1 = exco varj 'VONS';
deft = defo Lpoutre depj 100;
mesh3D = pout2mas modpout matpout gauss tabtrac;
*
*
* Analyse des contraintes et déformations dans les fibres
*
SI (EGA FLAGVISU VRAI);
 nn = DIME tabtrac.deplacements_3D;
 opti trac open;
 trac (defo mesh3D (tabtrac.deplacements_3D. nn));
 trac mesh3D
  (exco (tabtrac.VONS_3D. nn) smxx);
 list (maxi (exco (tabtrac.VONS_3D. nn) smxx));
 list (mini (exco (tabtrac.VONS_3D. nn) smxx));
*
* Exportation vers PARAVIEW via AVS
*
 opti sort 'visualisationSMXX3D.inp';
 sort avs (mesh3D coul vert)
  ((tabtrac.deplacements_3D. nn) et (tabtrac.VAIS_3D. nn) et
   (tabtrac.VONS_3D. nn));;
FINSI;
*
FIN;








