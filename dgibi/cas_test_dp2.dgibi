* fichier : cas_test_dp2.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*** essai de traction simple
*** Cas test

* Type & Elements de representation *
opti dime 3 elem cub8 ;

* Caracteristiques geometriques *
la=1.;
mla=1. ;
crit = 0.00002 ;

* Defintion des noeuds *
P1 = 0. 0.   0.  ;
P2 = 0. la   0.   ;

* Definition des contours *
L1 = droit 1 P1 P2 ;

* Generation de contours *
coteg = L1 TRAN (0. 0. la) DINI mla DFIN mla;

* Creation du volume *
geom = VOLU coteg  TRAN (la 0. 0.)  DINI la DFIN la ;

* Defintion des surfaces supplementaires *
coted=FACE geom  2 ;

* Epurer le volume *
elim crit geom ;
elim crit (geom et coteg);
elim crit (geom et coted);

* Affichage *
coted = coted coul rouge ;
coteg = coteg coul bleu ;

* constantes materiaux

yg = 31.25e9 ;
nub = 0.2 ;
gf = 250 ;
ltr = 3.5e6 ;
lcs =35e6 ;
lbi = 1.16 * lcs ;
sigy = 20.e6 ;
epm = -2.e-3;
epu = -6.e-3 ;

mod1 = MODE geom MECANIQUE ELASTIQUE PLASTIQUE DRUCKER_PRAGER_2 ;

jaco1 = jaco mod1 ;
jaco2 = chan 'RIGIDITE' mod1 jaco1 ;
lcar = jaco2**(1./3.) ;

mat1 = MATE mod1 'YOUN' yg 'NU' nub  'GF' gf
'LTR' ltr 'LCS' lcs 'LBI' lbi 'SIGY' sigy 'EPM' epm
'EPU' epu 'LCAR' lcar ;

*** Conditions aux limites

cl1 = BLOQ P1 UZ UY;
cl2 = BLOQ L1 UZ ;
cl3 = BLOQ COTEG UX ;
cldepl = BLOQ COTED UX ;

cltot = cl1 et cl2 et cl3 et cldepl ;

*** chargement

fo1 = DEPI cldepl 1. ;
prog1 = PROG 0. 1. ;
prog2 = PROG 0. 7.5e-4 ;

step1 = 1./20. ;
list1 = PROG 0. PAS step1 1 ;

evol1 = EVOL MANU prog1 prog2 ;


char1 = CHARGEMENT fo1 evol1 DIMP ;

tab1 = TABLE ;
tab1 . PLASTIQUE = vrai ;
tab1 . MODELE = mod1 ;
tab1 . CARACTERISTIQUES=mat1 ;
tab1 . CHARGEMENT = char1 ;
tab1 . BLOCAGES_MECANIQUES = cltot ;
tab1 . TEMPS_CALCULES = list1 ;
tab1 . TEMPS_SAUVES = list1 ;

PASAPAS tab1 ;

ind = inde (tab1.contraintes);
nbind1 = dime ind;
nbind1 = nbind1 - 1 ;
************************************************************

*definition des fonctions *

sigxx1=prog;
dex1=prog;
dev = prog ;

i = 1;

repeter bou1 nbind1;

  cont1 = (tab1).contraintes.i;

  sigxx1=sigxx1 et (prog(extr cont1 SMXX 1 1 1));

  dep1 = (tab1) . deplacements . i ;

  eps0 = EPSI dep1 mod1;
  depx1 = (EXTR eps0 EPXX 1 1 1);
  depy1 = (EXTR eps0 EPYY 1 1 1);
  depz1 = (EXTR eps0 EPZZ 1 1 1) ;

*  opti donn 5 ;
************************************************************
* deformations suivant les 3 composantes *

  devv = (depx1 + depy1 + depz1) ;
  dev = dev et (prog devv) ;
  dex1 = dex1 et (prog depx1);
 i = i+1;

fin bou1;

sixx = evol manu 'depl_xx' dex1 'sigxx' sigxx1;
sivv = evol manu 'defo_vol' dev 'sigxx' sigxx1 ;

*dess sivv;
*dess sixx;
fin ;






