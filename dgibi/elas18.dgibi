* fichier :  elas18.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
saut page;
********************************************************************
* Etude d'une poutre encastree a une extremite et chargee a l'autre
*    on néglige l'énergie associée à l'effort tranchant
*    La flexion se fait dans le plan xoy
*
*  encastrement                       extremite chargee
*
*     |-------------------------------------*
*                                           ^
*     p1                                    p2
********************************************************************
* Notations:
*
* h =hauteur
* b = largeur
* L =longueur totale
* I = b * h**3 / 12
* P =charge appliquee en P2
* E =module d'Young
* nu=Poisson
********************************************************************
* Objectif du test:
* on cherche à verifier la solution theorique en flexion pure
* donnant la flèche en bout
********************************************************************
*
* 1) option de calcul
opti dime 3 elem seg2;
*
* 2) quelques donnees et calcul de quelques constantes
*     pour la solution theorique
*
h=1.; E=1.e10; nu=.3; L=10.; b = 0.5 ;
P = 50000. ;
h3 = h ** 3 ;
L3 = l ** 3 ;
inerz = ( b * h3 ) /12. ;
inery = ( (b ** 3 ) * h ) /12. ;
*
*
* 3) maillage
*
nel=5;
p1 = 0 0 0; p2 = L 0 0;
d1= p1 d nel p2;
*
* 4) modele et materiaux
*    et  caracteristiques geometriques
*
modti=MODE d1 mecanique elastique pout;
mater=MATE modti 'YOUN' E 'NU' nu;
carac= 'CARA' modti 'SECT' ( b * h )  'INRY' inery 'INRZ' inerz
                    'TORS' (inery + inerz)   VECT ( 0 1 0 );
mc=mater et carac;
*
* 5) chargement
*
chrti = force 'FY' P  p2 ;
*
* 6) bloquage
*
bloqtot=bloq depl rota p1;
*
*
* 7) calcul de la rigidite
*
  rigti=rigi modti mc;
  rigtti=rigti et bloqtot ;
*
* 8) solution
*
  depti=reso rigtti chrti;
*
* 9) calcul de la solution analytique
*
  theorie=  ( P * L3 ) / ( ( 3. * E ) * inerz ) ;
  calcul= extr depti 'UY' p2 ;
  erreur=abs ((calcul - theorie) / theorie);
*
* 10) comparaison et message
*
  mess 'calcul,theorie,erreur:  ' calcul theorie erreur;
  mess '***********************';
  si (erreur > 5.d-4);
       erre 5;
  sinon;
       erre 0;
  finsi;
fin;







