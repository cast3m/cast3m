* fichier :  guionnet_tra.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*       Test Guionnet_tra.dgibi: Jeux de données      *
*       ----------------------------------------      *
*                                                     *
**************************************************
*                                                *
*  TEST DE VALIDATION D'UNE LOI DE COMPORTEMENT  *
*  --------------------------------------------  *
*              DE MATERIAU                       *
*              -----------                       *
*                                                *
*  LOI DE COMPORTEMENT VISCOPLASTIQUE DE :       *
*            GUIONNET                            *
*                                                *
*  COMPARAISON DE CALCULS SUR DES ELEMENTS:      *
*     - COQUE EPAISSE ( MFR=5 )                  *
*     - MASSIF 3D     ( MFR=1 )                  *
*                                                *
*  STRUCTURE : UNE PLAQUE CARREE                 *
*     - COTE : 1. M                              *
*     - EPAISSEUR : 0.01  M                      *
*                                                *
*  CHARGEMENT : DEPLACEMENTS IMPOSES             *
*               ESSAI DE TRACTION                *
*                                                *
**************************************************
*opti echo 0 dime 3 elem cub8 ;
opti dime 3 elem cub8 ;
*
*  Maillage
*
l1 = 1. ;
l2 = 1. ;
ep0 = .01 ;
p1 = 0. 0. 0. ;
p2 = ep0 0. 0. ;
p3 = ep0 l2 0. ;
p4 = 0. l2 0. ;
p10 = 0. 0. l1 ;
p20 = ep0 0. l1 ;
p30 = ep0 l2 l1 ;
p40 = 0. l2 l1 ;
li1 = d p1 1 p2 ;
li2 = d p2 1 p3 ;
li3 = d p3 1 p4 ;
li4 = d p4 1 p1 ;
li10 = d p10 1 p20 ;
li20 = d p20 1 p30 ;
li30 = d p30 1 p40 ;
li40 = d p40 1 p10 ;
sur1 = 'DALL' li1 li2 li3 li4 ;
sur10 = 'DALL' li10 li20 li30 li40 ;
mail1 = sur1 'VOLU' 1 sur10 ;
*
*  Modele
*
youn0 = 2.1E11 ;
nu0 = .3 ;
mod0 = 'MODE' mail1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'  'VISCOPLASTIQUE' 'GUIONNET' ;
mat0 = MATE mod0 'YOUN' 192500.  'NU  ' 0.3   'N   ' 24      'K   ' 200.    'CK  ' 0.       'R0  ' 25.      'CD  ' 0.     'M   ' 0.8         'A   ' 24000.    'A1  ' 3.3E5  'C   ' 180.   'C1  ' 40. 'G   ' 0.     'R   ' 0.      'NN  ' 0.005    'C2  ' 40.    'Q   ' 1.466    'G1  ' 0. 'R1  ' 0.       'BETA' 1.1 'C0  ' 180.     'P1M0' 0.00333 ;
*   *  Conditions aux limites  *
cl1 = 'BLOQ' sur1 'UZ' ;
cl2 = 'BLOQ' ( li1 'ET' li10 ) 'UY' ;
cl3 = 'BLOQ' ( li2 'ET' li20 ) 'UX' ;
cl4 = 'BLOQ' sur10 'UZ' ;
cl0 = cl1 'ET' cl2 'ET' cl3 'ET' cl4 ;
*
*  Chargement
*
depmax = .1 ;
dep0 = 'DEPI' cl4 depmax ;
ev0 = 'EVOL' 'MANU' temps ( 'PROG' 0. 1000. ) y ( 'PROG' 0. 1. ) ;
cha0 = 'CHAR' 'MECA' dep0 ev0 ;
*
dt0 = 10. ;
tfin0 = 100. ;
pro0 = 'PROG' 0. 'PAS' dt0 tfin0 ;
*
*  Appel à PASAPAS
*
ta1 = 'TABLE' ;
ta1 .'MODELE' = mod0 ;
ta1 .'CARACTERISTIQUES' = mat0 ;
ta1 .'CHARGEMENT' = cha0 ;
ta1 .'BLOCAGES_MECANIQUES' = cl0 ;
ta1 .'TEMPS_CALCULES' = pro0 ;
*
PASAPAS ta1 ;
*
*  Post traitement
*
dim0 = 'DIME' ta1 .'TEMPS' ;
*
*  Résultats calculés
*
reac0 = ta1 .'REACTIONS'.( dim0 - 1 ) ;
depl0 = ta1 .'DEPLACEMENTS'.( dim0 - 1 ) ;
temp0 = ta1 .'TEMPS'.( dim0 - 1 ) ;
sigm0 = ta1 .'CONTRAINTES'.(dim0 - 1 ) ;
eps0 = 'EPSI' mod0 depl0 ;
*
defo0 = 'DEFO' mail1 depl0 0. ;
defo1 = 'DEFO' mail1 depl0 10. 'ROUG' ;
*titr ' deformee finale' ;
*trace ( defo0 'ET' defo1 ) ;
*
sigm0 = 'CHAN' 'CHPO' sigm0 mod0 ;
*titr ' Contraintes suivant X' ;
*trace ( 'EXCO' sigm0 'SMXX' ) mail1 ;
*titr ' Contraintes suivant Y' ;
*trace ( 'EXCO' sigm0 'SMYY' ) mail1 ;
*titr ' Contraintes suivant Z' ;
*trace ( 'EXCO' sigm0 'SMZZ' ) mail1 ;
*titr ' Contraintes d cisaillement suivant YZ' ;
*trace ( 'EXCO' sigm0 'SMYZ' ) mail1 ;
reac0 = 'REDU' reac0 sur10 ;
reac0 = 'EXCO' reac0 'FZ' 'SCAL' ;
chp1 = 'MANU' 'CHPO' sur10 1 'SCAL' 1. ;
reac1 = 'XTY' reac0 chp1 ( 'MOTS' 'SCAL' )  ( 'MOTS' 'SCAL' ) ;
*
*opti echo 0 dime 3 elem qua8 ;
opti dime 3 elem qua8 ;
*
*  Maillage
*
p1 = 0. 0. 0. ;
p4 = 0. l2 0. ;
p10 = 0. 0. l1 ;
p40 = 0. l2 l1 ;
li1 = d p1 1 p4 ;
li2 = d p4 1 p40 ;
li3 = d p40 1 p10 ;
li4 = d p10 1 p1 ;
mail1 = 'DALL' li1 li2 li3 li4 ;
*titr ' Maillage' ;
*trace mail1 ;
*
*  Modele
*
mod0 = 'MODE' mail1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'  'VISCOPLASTIQUE' 'GUIONNET' 'COQ8' ;
mat0 = MATE mod0 'YOUN' 192500.  'NU  ' 0.3      'N   ' 24    'K   ' 200. 'CK  ' 0. 'A   ' 24000.     'R0  ' 25.   'CD  ' 0.     'M   ' 0.8  'A1  ' 3.3E5 'C   ' 180.   'C1  ' 40. 'G   ' 0. 'R   ' 0.      'NN  ' 0.005   'C2  ' 40. 'Q   ' 1.466  'G1  ' 0.    'R1  ' 0. 'BETA' 1.1 'C0  ' 180.   'P1M0' 0.00333 ;
car0 = 'CARA' mod0 'EPAI' ep0 ;
*
*  Conditions aux limites
*
cl1 = 'BLOQ' li1 'UZ' ;
cl2 = 'BLOQ' li4 'UY' ;
cl3 = 'BLOQ' mail1 'UX' ;
cl4 = 'BLOQ' li3 'UZ' ;
cl0 = cl1 'ET' cl2 'ET' cl3 'ET' cl4 ;
*
*  Chargement
*
dep0 = 'DEPI' cl4 depmax ;
ev0 = 'EVOL' 'MANU' temps ( 'PROG' 0. 1000. ) y ( 'PROG' 0. 1. ) ;
cha0 = 'CHAR' 'DIMP' dep0 ev0 ;
*
*  Appel à PASAPAS
*
ta1 = 'TABLE' ;
ta1 .'MODELE' = mod0 ;
ta1 .'CARACTERISTIQUES' = mat0 'ET' car0 ;
ta1 .'CHARGEMENT' = cha0 ;
ta1 .'BLOCAGES_MECANIQUES' = cl0 ;
ta1 .'TEMPS_CALCULES' = pro0 ;
*
PASAPAS ta1 ;
*
*  Post traitement
*
dim0 = 'DIME' ta1 .'TEMPS' ;
*
*  Résultats calculés
*
reac0 = ta1 .'REACTIONS'.( dim0 - 1 ) ;
depl0 = ta1 .'DEPLACEMENTS'.( dim0 - 1 ) ;
temp0 = ta1 .'TEMPS'.( dim0 - 1 ) ;
sigm0 = ta1 .'CONTRAINTES'.( dim0 - 1 ) ;
eps0 = 'EPSI' mod0 depl0 car0 ;
*
defo0 = 'DEFO' mail1 depl0 0. ;
defo1 = 'DEFO' mail1 depl0 10. 'ROUG' ;
*titr ' deformee finale' ;
*trace ( defo0 'ET' defo1 ) ;
*
sigm0 = 'CHAN' 'CHPO' sigm0 mod0 ;
*titr ' Contraintes suivant Y' ;
*trace ( 'EXCO' sigm0 'SMSS' ) mail1 ;
*titr ' Contraintes suivant Z' ;
*trace ( 'EXCO' sigm0 'SMTT' ) mail1 ;
*titr ' Contraintes d cisaillement suivant YZ' ;
*trace ( 'EXCO' sigm0 'SMST' ) mail1 ;
reac0 = 'REDU' reac0 li3 ;
reac0 = 'EXCO' reac0 'FZ' 'SCAL' ;
chp1 = 'MANU' 'CHPO' li3 1 'SCAL' 1. ;
reac2 = 'XTY' reac0 chp1 ( 'MOTS' 'SCAL' )  ( 'MOTS' 'SCAL' ) ;
*
'MESS' ' Réaction au sommet de la structure , calcul massif        ='  reac1 ;
'MESS' ' Réaction au sommet de la structure , calcul coque épaisse =' reac2 ;
'SI' ( reac1 '>' 0. ) ;
   err1 = ( 'ABS' ( reac1 -reac2 ) ) / reac1 ;
   err1 = err1 * 100. ;
'SINON' ;
   err1 = 0. ;
'FINSI' ;
'MESS' ' Erreur obtenue en % =' err1 ;
*
'SI' ( err1 '>' 0.03 ) ;
   'ERRE' 5 ;
'SINON' ;
   'ERRE' 0 ;
'FINSI' ;
*
'FIN' ;










