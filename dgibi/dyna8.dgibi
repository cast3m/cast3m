* fichier :  dyna8.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*           Test Dyna8.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test dyna8.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*************************************************************************
*                            DYNA8
*
*           CALCUL DES FREQUENCES PROPRES D'UNE POUTRE-CONSOLE
*                    (COMPARAISON AVEC TIMOSHENKO)
*
* On recherche les frequences propres d'une poutre encastree a une
* extremite (surface ST) et libre a l'autre extremite.
*
* Ce test est similaire au test DYNA6, mais au lieu d'utiliser des
* elements POUT on utilise ici des elements massifs CU20.
*
* Les resultats sont compares aux resultats obtenus par methode analyti-
* que et cites dans l'ouvrage de Timoshenko.
*
*************************************************************************
TITR 'FREQUENCES PROPRES D UNE POUTRE-CONSOLE' ;
OPTI DIME 3 ELEM CU20 ;
OPTI EPSI LINEAIRE;
TEMPS;
*
*----------------------- GEOMETRIE --------------------------------------
*
P0  = 0  0 0 ;
P10 = 10 0 0 ;
PY  = 0  1 0 ;
PZ  = 0  0 2 ;
LYZ = P0  DROI 1 PY ;
ST  = LYZ TRAN 1 PZ ;
TRA = ST  VOLU 10 TRAN P10 ;
*
OEIL1 = 15 15 15 ;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' OEIL1 TRA ;
FINSI;
*
*----------------------- MATERIAU ---------------------------------------
*
MM = MODE TRA MECANIQUE CU20 ;

MA = MATE MM YOUN 2.1E11 NU 0.3 RHO 7800 ;
*
*---------------- CONDITIONS AUX LIMITES, RIGIDITE ET MASSE -------------
*
INC = BLOQ DEPL ROTA ST ;
K   = RIGI MM MA ;
KT  = K ET INC ;
MT  = MASS MM MA ;
*
*---------------- CALCUL DES FREQUENCES PAR INTERVALLE ------------------
*
* Cette methode de recherche en trois intervalles est plus rapide que
* la recherche en une seule fois sur l'intervalle 0-150.
*
AUTO0 = VIBR INTERVALLE 0. 50. BASSE 2 KT MT ;
AUTO1 = VIBR INTERVALLE 50. 100. BASSE 2 KT MT ;
AUTO2 = VIBR INTERVALLE 100. 150. BASSE 2 KT MT ;
*
*----------------- TRACE ET SORTIES -------------------------------------

*
F1 = AUTO0 . 'MODES' . 1 . 'FREQUENCE';
F2 = AUTO0 . 'MODES' . 2 . 'FREQUENCE';
F3 = AUTO1 . 'MODES' . 1 . 'FREQUENCE';
F4 = AUTO1 . 'MODES' . 2 . 'FREQUENCE';
F5 = AUTO2 . 'MODES' . 1 . 'FREQUENCE';
F6 = AUTO2 . 'MODES' . 2 . 'FREQUENCE';
*
SAUT PAGE ; SAUT 2 LIGNE ;
*
MESS ' MODE 1     FREQUENCE THEORIQUE 8.391      CALCULEE ' F1 ;
MESS ' MODE 2     FREQUENCE THEORIQUE 16.78      CALCULEE ' F2 ;
MESS ' MODE 3     FREQUENCE THEORIQUE 52.52      CALCULEE ' F3 ;
MESS ' MODE 4     FREQUENCE THEORIQUE 59.75      CALCULEE ' F4 ;
MESS ' MODE 5     FREQUENCE THEORIQUE 105.0      CALCULEE ' F5 ;
MESS ' MODE 6     FREQUENCE THEORIQUE 129.6      CALCULEE ' F6 ;
*
*------------- CODE DE FONCTIONNEMENT ---------------------------------*
ERR=TABLE;
ERR.1=100*(ABS(8.391 -F1)/8.391);
ERR.2=100*(ABS(16.78 -F2)/16.78);
ERR.3=100*(ABS(52.52 -F3)/52.52);
ERR.4=100*(ABS(59.75 -F4)/59.75);
ERR.5=100*(ABS(105.0 -F5)/105.0);
ERR.6=100*(ABS(129.6 -F6)/129.6);
I=0;
REPETER BOUC 6;
  I=I+1;
  SI (ERR.I < 1);
    ERRE 0;
  SINON;
    ERRE 5;
  FINSI;
FIN;

TEMPS;

FIN;






