*****************************************************
************************************************************************
* Section : Fluides Convection
************************************************************************
* fichier :  tubturb.dgibi                          *
** modifie le 15/06/2014 passage EQPR -> EQEX       *
*****************************************************
COMPLET = FAUX ;
GRAPH = 'N' ;

KPRESS='CENTRE';
DISCR='LINE';
BETA=1.;

SI ( COMPLET ) ;
along = 3 ; n = 24 ; nlong = 6 ;
SINON ;
along = 3 ; n = 24 ; nlong = 15 ;
FINSI ;

******************************************************************
* TESTE DE STABILITÉ DU MODELE K-E ET DES FONCTIONS DE PAROI EN  *
* CONVECTION FORCE - ÉCOULEMENT EN CONDUITE LISSE CYLINDRIQUE    *
* LES PROFILS RADIAUX DES VARIABLES V,K,E OBTENUS À 3 DIAMÈTRES  *
* DE L'ENTRÉE SONT COMPARÉS AU PROFIL OBTENU LORS DE L'ÉTABLISSE *
* DE LA FICHE DE VALIDATION CORRESPONDANTE                       *
******************************************************************

'OPTI' 'DIME' 2 'ELEM' 'QUA4'  'MODE' 'AXIS'  ;
'OPTI' 'ISOV' 'SURF'  ;

** dimension
Rh = 0.05 ; DH = 2. * RH ; LH = along*DH ;

** maillage
A = 0.0 0. ; B = RH  0.  ; C = RH  LH  ; D = 0.  LH  ;
bas = a DROIT n b ;
paroi = b DROIT (2*n*along/nlong) c ;
haut = c droit n d ;
axe = d droit  (2*n*along/nlong) a ;
mgeo = dalle bas paroi haut axe plan ;
mgeo = orien mgeo ;
cgeo = contour mgeo ;

*-----------------------------------------------------------------
**** donnée hydraulique de l écoulement
NU = 6.53e-4 / 992.2D0 ;
CNU = 0.09 ;
AKER = 0.41 ;
REY = 100000. ;
UI = REY*NU/DH ;

*-----------------------------------------------------------------
**** calcul de la perte de charge

LAM = LOG REY / (LOG 10.) * 1.8 - 1.64 ** (-2.) ;
mess 'Lambda = ' LAM ;
REPE BL1 5 ;
  lam = lam ** 0.5 * REY log / (log 10) * 2. - 0.8 ** (-2.) ;
  mess 'Lambda = ' LAM ;
FIN bl1 ;

DPSRDZ = LAM*0.5/DH*UI*UI ;

*-----------------------------------------------------------------
**** calcul de la vitesse de glissement à partir des pertes de charge
TO0     = DPSRDZ * Rh * 0.5 ;
TO0_IMP = (-1.*to0) 0. ;
UETH    = TO0 ** 0.5 ;

*-----------------------------------------------------------------
**** restriction du domaine à la zone logarithmique (Y+>40)
R = MGEO COOR 1 ;
Y = RH - R ;
YCL= RH * 0.05 ;
DEPLA MGEO AFFI (RH - YCL/RH) A B ;
R = MGEO COOR 1 ;
Y = RH - R ;

*-----------------------------------------------------------------
**** construction des objets modeles
Mmgeo = chan mgeo QUAF ;
Mbas  = chan bas  QUAF ;
ELIM Mmgeo Mbas 0.0001 ;
$DOMT = 'MODE' Mmgeo 'NAVIER_STOKES' DISCR ;
$DBAS = 'MODE' Mbas  'NAVIER_STOKES' DISCR ;
mgeo=doma $DOMT 'MAILLAGE';
bas=doma $DBAS 'MAILLAGE';
DOMA $DOMT 'IMPR' ;
VTBAS = PI * RH * RH ;
Mhaut  = chan haut  QUAF ;
nbp = nbel paroi ;
paroi = elem paroi (lect 2 pas 1 nbp) ;
Mparoi = chan paroi QUAF ;
ELIM (Mmgeo et Mbas et Mhaut et Mparoi) 0.0001 ;
$DHAU = 'MODE' Mhaut 'NAVIER_STOKES' DISCR ;
$DPAR = 'MODE' Mparoi 'NAVIER_STOKES' DISCR ;
haut= doma $DHAU 'MAILLAGE';
paroi= doma $DPAR 'MAILLAGE';
YPLUS = Y * UETH / NU ;

*-----------------------------------------------------------------
**** estimation du profil de vitesse (hypothèse de couche limite log.)
VZ =  YPLUS LOG / AKER + 5.5 * UETH ;
VZ = NOMC VZ 'UY' ;
VZ1 = KCHT $DOMT VECT SOMMET VZ ;
DZ = DBIT $DBAS (VZ1 + (VZ1 mini * PI * YCL* RH)) ;
UZ = DZ / VTBAS ;
**** correction du profil de vitesse pour le respect du REYNOLDS
MESS 'FACTEUR DE CORRECTION DES VITESSES ' (UI/UZ) ;
VZ = KCHT $DOMT VECT SOMMET (VZ1/UZ*UI) ;

*-----------------------------------------------------------------
**** estimation de la viscosité turbulente
NUT = AKER * Y * UETH * (R / RH) + (Y/RH*nu) ;
NUTET = NUT/UETH/RH*0.75+(Y/RH*0.015) ;
NI = kcht $domt scal sommet (NUTET*UETH*RH) ;

*-----------------------------------------------------------------
**** calcul de la longueur de mélange
R2 = R/RH ** 2 ;
LP = R2 * 0.06 + 0.08 * R2 * (-1.) + 0.14 ;
LP = LP * RH ;

*-----------------------------------------------------------------
**** estimation de l'énergie cinétique turbulente à partir
**** de la longueur de mélange
KI = NI / LP ** 2. / (CNU**0.5) ;

*-----------------------------------------------------------------
**** estimation de la dissipation visqueuse
EI = CNU * KI * KI / NI ;

*-----------------------------------------------------------------
**** conditions en entrée
VEN = exco (REDU VZ BAS) 'UY'   '2UN'  ;
KEN = exco (REDU KI BAS) 'SCAL' 'KN'   ;
EEN = exco (REDU EI BAS) 'SCAL' 'EN'   ;

MT  = table ;
MT.1 = 'MARQ TRIA REGU TITR SORTIE' ;
MT.2 = 'TIRR TITR ENTREE' ;
HAUT  = INVE HAUT ;
ZC = MGEO COOR 2 ; ZC = (MAXI ZC) - ZC ;

RV  = EQEX $DOMT 'ITMA' 160 'ALFA' 0.9
'OPTI' 'SUPG'
 'ZONE' $DOMT 'OPER' 'FILTREKE' UI Dh      'INCO' 'KN' 'EN'
 'ZONE' $DOMT 'OPER' 'NSKE'   NU   'NUT' 'INCO' 'UN' 'KN' 'EN'
 'ZONE' $DPAR 'OPER' 'FPU' NU 'UET' YCL 'INCO' 'UN' 'KN' 'EN' ;

RV = EQEX RV 'OPTI' 'CENTREE'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'UN' 'DELTAT' INCO 'UN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'KN' 'DELTAT' INCO 'KN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'EN' 'DELTAT' INCO 'EN'
;

RV = EQEX RV
   'CLIM'  'UN' 'VIMP'  BAS  VEN
   'CLIM'  'KN' 'TIMP'  BAS  KEN
   'CLIM'  'EN' 'TIMP'  BAS  EEN
   'CLIM'  'UN' 'UIMP'  CGEO 0.   ;

RVP = EQEX 'OPTI' 'EF' KPRESS
 'ZONE' $DOMT OPER  KBBT  -1. beta  INCO 'UN' 'PRES'
;

    rvp.'METHINV'.TYPINV=1 ;
    rvp.'METHINV'.IMPINV=0 ;
    rvp.'METHINV'.NITMAX=300;
    rvp.'METHINV'.PRECOND=3 ;
    rvp.'METHINV'.RESID  =1.e-8 ;
    rvp.'METHINV' . 'FCPRECT'=100 ;
    rvp.'METHINV' . 'FCPRECI'=100 ;

  RV.'PROJ'     =RVP ;

RV.'INCO' = table 'INCO' ;
RV.INCO.'UN' =  VZ ;
RV.INCO.'PRES' = KCHT $DOMT 'SCAL' KPRESS 0.;
RV.INCO.'NUT' = NOEL $DOMT NI ;
RV.INCO.'KN' =  KCHT $DOMT SCAL SOMMET KI ;
RV.INCO.'EN' =  KCHT $DOMT SCAL SOMMET EI ;
RV.'INCO'.'UET' = KCHT $DPAR SCAL CENTRE UETH ;

*-----------------------------------------------EXEC
  EXEC RV ;
*-----------------------------------------------EXEC

SI ( COMPLET ) ;
** On recherche le régime permanent :


repe bl2 15 ;

K = RV.'INCO'.'KN' ;
E = RV.'INCO'.'EN' ;
N =  RV.'INCO'.'NUT' ;
U =  kops (RV.'INCO'.'UN') '*' (-1.)  ;
UT = RV.'INCO'.'UET' ;

RV  = EQEX $DOMT 'ITMA' 400 'ALFA' 0.8
'OPTI' 'SUPG'
 'ZONE' $DOMT 'OPER' 'FILTREKE' UI Dh      'INCO' 'KN' 'EN'
 'ZONE' $DOMT 'OPER' 'NSKE'  NU  'NUT' 'INCO' 'UN' 'KN' 'EN'
 'ZONE' $DPAR 'OPER' 'FPU' NU 'UET' YCL 'INCO' 'UN' 'KN' 'EN' ;

RV = EQEX RV 'OPTI' 'CENTREE'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'UN' 'DELTAT' INCO 'UN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'KN' 'DELTAT' INCO 'KN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'EN' 'DELTAT' INCO 'EN'
;

RVP = EQEX 'OPTI' 'EF' KPRESS
 'ZONE' $DOMT OPER  KBBT  -1. beta  INCO 'UN' 'PRES'
;

    rvp.'METHINV'.TYPINV=1 ;
    rvp.'METHINV'.IMPINV=0 ;
    rvp.'METHINV'.NITMAX=300;
    rvp.'METHINV'.PRECOND=3 ;
    rvp.'METHINV'.RESID  =1.e-8 ;
    rvp.'METHINV' . 'FCPRECT'=100 ;
    rvp.'METHINV' . 'FCPRECI'=100 ;

  RV.'PROJ'     =RVP ;

RV.'INCO' = table 'INCO' ;
RV.INCO.'UN' =  U ;
RV.INCO.'PRES' = KCHT $DOMT 'SCAL' KPRESS 0.;
RV.INCO.'NUT' = N ;
RV.INCO.'KN' =  K ;
RV.INCO.'EN' =  E ;
RV.'INCO'.'UET' = UT ;

VEN        = exco (redu HAUT U) 'UY' 'SCAL' ;
KEN        =  redu HAUT K ;
EEN        =  redu HAUT E ;

  RV.CLIM = 0 ;
  RV = EQEX RV
   'CLIM'  'UN' 'VIMP'  HAUT VEN
   'CLIM'  'KN' 'TIMP'  HAUT KEN
   'CLIM'  'EN' 'TIMP'  HAUT EEN
   'CLIM'  'UN' 'UIMP'  CGEO 0.   ;

*-----------------------------------------------EXEC aller
  EXEC RV ;
*-----------------------------------------------EXEC aller

K2 =  copi RV.'INCO'.'KN' ;
K = RV.'INCO'.'KN' ;
E = RV.'INCO'.'EN' ;
N =  RV.'INCO'.'NUT' ;
U =  kops (RV.'INCO'.'UN') '*' (-1.)  ;
UT = RV.'INCO'.'UET' ;

RV  = EQEX $DOMT 'ITMA' 400 'ALFA' 0.8
'OPTI' 'SUPG'
 'ZONE' $DOMT 'OPER' 'FILTREKE' UI Dh    'INCO' 'KN' 'EN'
 'ZONE' $DOMT 'OPER' 'NSKE'   NU   'NUT' 'INCO' 'UN' 'KN' 'EN'
 'ZONE' $DPAR 'OPER' 'FPU' NU 'UET' YCL 'INCO' 'UN' 'KN' 'EN' ;

RV = EQEX RV 'OPTI' 'CENTREE'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'UN' 'DELTAT' INCO 'UN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'KN' 'DELTAT' INCO 'KN'
 'ZONE' $DOMT 'OPER' 'DFDT' 1. 'EN' 'DELTAT' INCO 'EN'
;

RVP = EQEX 'OPTI' 'EF' KPRESS
 'ZONE' $DOMT OPER  KBBT  -1. beta  INCO 'UN' 'PRES'
;

    rvp.'METHINV'.TYPINV=1 ;
    rvp.'METHINV'.IMPINV=0 ;
    rvp.'METHINV'.NITMAX=300;
    rvp.'METHINV'.PRECOND=3 ;
    rvp.'METHINV'.RESID  =1.e-8 ;
    rvp.'METHINV' . 'FCPRECT'=100 ;
    rvp.'METHINV' . 'FCPRECI'=100 ;

  RV.'PROJ'     =RVP ;

RV.'INCO' = table 'INCO' ;
RV.INCO.'UN' =  U ;
RV.INCO.'PRES' = KCHT $DOMT 'SCAL' KPRESS 0.;
RV.INCO.'NUT' = N ;
RV.INCO.'KN' =  K ;
RV.INCO.'EN' =  E ;
RV.'INCO'.'UET' = UT ;

VEN        = exco (redu BAS U) 'UY' 'SCAL' ;
KEN        =  redu BAS K ;
EEN        =  redu BAS E ;

  RV.CLIM = 0 ;
  RV = EQEX RV
   'CLIM'  'UN' 'VIMP'  BAS VEN
   'CLIM'  'KN' 'TIMP'  BAS KEN
   'CLIM'  'EN' 'TIMP'  BAS EEN
   'CLIM'  'UN' 'UIMP'  CGEO 0.   ;

*-----------------------------------------------EXEC retour
  EXEC RV ;
*-----------------------------------------------EXEC retour

erm = k - k2 / (k maxi) abs maxi ;
mess 'maxi (k - k2 / (k maxi)) : ' erm ;
si ( erm < 2.e-5) ;
  quitter bl2 ;
finsi ;
menage ;
FIN bl2 ;

*********************************************
*                                           *
*      POSTRAITEMENT DES RÉSULTATS          *
*                                           *
*********************************************

SI ( 'EGA' graph 'O') ;

EVV = EVOL CHPO  (RV.INCO.UN/UETH) 'UY' (DOMA $DBAS MAILLAGE)  ;
EVV0 = EVOL VERT CHPO (VZ/UETH) 'UY' (DOMA $DBAS MAILLAGE) ;
RAY = EXTR evv 'ABSC' ; RAY = RAY / RH ;
YY = EXTR evv 'ORDO' ; YY0 = EXTR evv0 'ORDO' ;
EVV  = EVOL MANU  RAY  YY 'UY' ;
EVV0 = EVOL MANU  RAY  YY0 'UY' ;

RV.INCO.'NNT' = ELNO $DOMT RV.INCO.'NUT' ;
EVN = EVOL CHPO  (RV.INCO.'NNT'/(UETH*RH)) 'SCAL' (DOMA $DBAS MAILLAGE);
EVN0 = EVOL VERT CHPO  NUTET 'SCAL' (DOMA $DBAS MAILLAGE)  ;
YY = EXTR evN 'ORDO' ; YY0 = EXTR evN0 'ORDO' ;
EVN  = EVOL MANU  RAY  YY 'SCAL' ;
EVN0 = EVOL MANU  RAY  YY0 'SCAL' ;

EVK = EVOL CHPO (RV.INCO.'KN'/(UETH*UETH)) 'SCAL' (DOMA $DBAS MAILLAGE);
EVK0 = EVOL VERT CHPO  (KI/UETH/UETH) 'SCAL' (DOMA $DBAS MAILLAGE)  ;
YY = EXTR evK 'ORDO' ; YY0 = EXTR evK0 'ORDO' ;
EVK  = EVOL MANU  RAY  YY 'SCAL' ;
EVK0 = EVOL MANU  RAY  YY0 'SCAL' ;

EVE = EVOL CHPO (RV.INCO.'EN'/(UETH*UETH*UETH/RH))
 'SCAL'  (DOMA $DBAS MAILLAGE)  ;
EVE0 = EVOL VERT CHPO (EI/(UETH*UETH*UETH/RH))
'SCAL' (DOMA $DBAS MAILLAGE)  ;
YY = EXTR evE 'ORDO' ; YY0 = EXTR evE0 'ORDO' ;
EVE  = EVOL MANU  RAY  YY  'SCAL' ;
EVE0 = EVOL MANU  RAY  YY0 'SCAL' ;

RV.INCO.'LN'  = KOPS (0.09**(-0.25)) '*'
(KOPS RV.INCO.'NNT' '/' (KOPS RV.INCO.'KN' '**' 0.5)) ;
EVL = EVOL CHPO ((RV.INCO.'LN')/RH) 'SCAL'  (DOMA $DBAS MAILLAGE)  ;
EVL0 = EVOL VERT CHPO (LP/RH) 'SCAL' (DOMA $DBAS MAILLAGE)  ;
YY = EXTR evL 'ORDO' ; YY0 = EXTR evL0 'ORDO' ;
EVL  = EVOL MANU  RAY  YY  'SCAL' ;
EVL0 = EVOL MANU  RAY  YY0 'SCAL' ;

nn = nbel dbas.maillage ;
EVT0 =  EVOL MANU  RAY  RAY 'SCAL' ;
nn = nbel dPAR.maillage ;
X = (dbas.maillage COOR 1) MAXI / RH ;
TOC = SOMT RV.INCO.'UET' / nn / UETH ** 2 ;
EVT =  EVOL MANU  (PROG X)  (PROG TOC)  'SCAL' ;

MT = TABLE ;
MT.1 = 'MARQ CROI REGU TIRR' ;
MT.2 = 'MARQ CARR  ' ;

DESS MT (EVT0  ET EVT)
titr 'PROFIL RADIAL DU CISAILLEMENT ADIMENSIONNELLE'
TITX 'RAYON/RH' TITY  'TO/UETH/UETH'

DESS (EVN ET EVN0)
titr 'PROFIL RADIAL DE LA VISCOSITE ADIMENSIONNELLE'
TITX 'RAYON/RH' TITY  'NUT/UETH/RH';

DESS (EVE ET EVE0)
titr 'PROFIL RADIAL DU TAUX DE DISS. ADIMENSIONNEL'
TITX 'RAYON/RH' TITY  'E/UETH*RH';

DESS (EVV ET EVV0)
titr 'PROFIL RADIAL DE LA VITESSE ADIMENSIONNELLE'
TITX 'RAYON/RH' TITY  'V/UETH';

DESS (EVK ET EVK0) titr
'PROFIL RADIAL DE L ENERGIE TURBULENTE ADIMENSIONNELLE'
TITX 'RAYON/RH' TITY  'K/UETH/UETH';

DESS (EVL ET EVL0) titr
'PROFIL RADIAL DE LA LONGUEUR DE MELANGE ADIMENSIONNELLE'
TITX 'RAYON/RH' TITY  'L/RH';

FINSI ;

SINON ;
EVV = EVOL CHPO  (RV.INCO.UN/UETH) 'UY' (DOMA $DBAS MAILLAGE)  ;
v = EXTR evv 'ORDO' ;
list v ;
v0 = prog
26.642 26.535 26.423 26.306 26.184 26.056 25.921 25.780 25.630
25.472 25.303 25.123 24.930 24.721 24.495 24.248 23.976 23.672
23.330 22.936 22.474 21.914 21.204 20.231 18.682 ;
VDIF0 = V - V0 / V0 abs maxi ; list VDIF0 ;
SI ( VDIF0  > 5.E-5 ) ;
            ERREUR 5 ;
FINSI ;

EVK = EVOL CHPO (RV.INCO.'KN'/(UETH*UETH)) 'SCAL' (DOMA $DBAS MAILLAGE);
K = EXTR evK 'ORDO' ;
list k ;
K0 = PROG
3.98961E-02 .11874 .22847 .36068 .50826 .66518 .82637 .98756 1.1453
1.2966 1.4394 1.5718 1.6926 1.8010 1.8965 1.9789 2.0483  2.1050
2.1495 2.1822 2.2041 2.2158 2.2181 2.2120 2.1982 ;
KDIF0 = K - K0 / K0 ABS MAXI ; list KDIF0 ;
SI (KDIF0  > 5.E-5 ) ;
            ERREUR 5 ;
FINSI ;

EVE = EVOL CHPO (RV.INCO.'EN'/(UETH*UETH*UETH/RH))
 'SCAL' (DOMA $DBAS MAILLAGE)  ;
E = EXTR eve 'ORDO' ;
list e ;
E0 = PROG
9.35296E-03 4.80687E-02 .12863 .25632 .43158 .65176 .91234 1.2079
1.5329 1.8824 2.2521 2.6394 3.0434 3.4653 3.9095 4.3841 4.9025
5.4863 6.1700 7.0111 8.1107 9.6651 12.110 16.648 28.290 ;
EDIF0 = E - E0 / E0 ABS MAXI ; list EDIF0 ;
SI ( EDIF0  > 5.E-5 ) ;
            ERREUR 5 ;
FINSI ;

FINSI ;

FIN;














