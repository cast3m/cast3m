* FICHIER : DYNA_NL4.DGIBI
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*******************************************************

*******************************************************
*                                                     *
*    CALCUL DU REBOND  D'UNE BARRE                    *
*    AVEC PASAPAS ET RELA                             *
*                                                     *
*    D. COMBESCURE AOUT 2006                          *
*******************************************************
*GRAPH = VRAI;
*COMPLET = VRAI;
GRAPH = FAUX;
COMPLET = FAUX;
*
OPTION  ECHO 1 ;
OPTIO DIME 2 ELEM QUA8 MODE PLAN CONT;
*
*  MAILLAGE
*
 NL = 10;
 H1 = 1.D-1;
 L1 = 0.5;
 P1 = (0.) (0.5*H1);
 P2 = (0.) ((-0.5)*H1);
 VH1 = L1 0.;
 P3 = L1 (0.5*H1);
 P4 = L1 ((-0.5)*H1);
 P3B = L1 (0.5 * H1);
 P4B = L1 ((-0.5)*H1);
 P5 = ((-1.)*L1) (0.5 * H1);
 P6 = ((-1.)*L1) ((-0.5)*H1);
 LIG1 = D 1 P1 P2;
 LIG2 = D 1 P3 P4;
 LIG2B = D 1 P3B P4B;
 LIG3 = D 1 P5 P6;
 S1 =  (LIG1 TRAN NL VH1) ET (LIG1 TRAN NL ((-1.)*VH1));

* S1 =  (LIG1 TRAN NL VH1);
 LIGH = (D NL P5 P1) ET (D NL P1 P3);
 ELIM 0.000001 (LIG1 ET LIG2 ET S1 ET LIG3 ET LIGH);
*
*  MODELE
*
 MOD1 = MODELE S1 MECANIQUE ELASTIQUE  ;
*
*  PROPRIETES DE MATERIAUX
*
 RO = 7800.;
 MAT1 = MATER MOD1 YOUN 200.E6 NU 0.0 RHO RO ;
*
*  CONDITIONS AUX LIMITES
*
 CL1 = (BLOQ S1 UY) ;
 CL2 = RELA 'MAXI' UX LIG2 - 'UX' LIG2B;
 CL3 = BLOQ DEPL LIG2B;
 DEPIM2 =DEPI CL2 0.01;
*
*  MATRICE DE RIGIDITE
*
 RIG1 = RIGI MOD1 MAT1 ;
*
*  MATRICE DE MASSE
*
 MAS1 = MASSE MOD1 MAT1 ;
*
* CHARGEMENT
*
DDT = 2.0D-5;
LI1 = (PROG 0. DDT (2.*DDT) 10.);
LI2 = (PROG 1. 1.  1. 1.);
EV = EVOL MANU 'T' LI1 'F(T)' LI2 ;
*DESS EV YBORD 0. 2. XBORD 0. 1.0;
F1 = MAS1*(MANU CHPO S1 1 UX (9.81));
F2 = MAS1*(MANU CHPO S1 1 UX (9.81));
CHA1 = CHAR  (F1 ET DEPIM2) EV ;
CHA2 = CHAR  (F2 ET DEPIM2)  EV ;
*CHA1 = CHAR  (DEPIM2) EV ;
*CHA2 = CHAR  (DEPIM2)  EV ;
*
*  PREPARATION DE LA TABLE DE DONNEES POUR PASAPAS
*

SI (EGA COMPLET VRAI) ;
 TIME = PROG  0. PAS 1.E-3 50.D-2  ;
SINON ;
 TIME = PROG  0. PAS 1.E-3 1.D-2  ;
FINSI ;




*
TAB1 = TABLE;
TAB1 . 'MODELE' = MOD1;
TAB1 . 'CARACTERISTIQUES' = MAT1;
TAB1 . 'BLOCAGES_MECANIQUES' = CL1 ET CL2 ET CL3;
TAB1 . 'CHARGEMENT' = CHA1;
*TAB1 . 'AMORTISSEMENT' = 0.01*(RIGI MOD1 MAT1);
TAB1 . 'TEMPS_CALCULES' = TIME;
TAB1 . 'DYNAMIQUE' = VRAI;
TAB1 . 'CONVERGENCE_FORCEE' = FAUX;
PASAPAS TAB1;
*
*  EXTRAIRE  LES OBJETS D'EVOLUTION DE DEPLACEMENTS
*
LT    = VIDE 'LISTREEL' ;
LUX1  = VIDE 'LISTREEL' ;
LUX2  = VIDE 'LISTREEL' ;
LUX3  = VIDE 'LISTREEL' ;
LVX1  = VIDE 'LISTREEL' ;
LVX2  = VIDE 'LISTREEL' ;
LVX3  = VIDE 'LISTREEL' ;
LEPOT = VIDE 'LISTREEL' ;
LENEC = VIDE 'LISTREEL' ;
LEELA = VIDE 'LISTREEL' ;
LEREA = VIDE 'LISTREEL' ;
I = 1 ;
M1 = P5 ;M2 = P1 ; M3 = P3 ;
TEMPS =  INDEX ( TAB1.DEPLACEMENTS);
NBB = (DIME TEMPS) - 2;

REPETER  BB1 NBB ;
  I    = I + 1 ;
  LT   = LT ET (PROG (TAB1.'TEMPS'. (TEMPS. I))) ;
  DEP1 = TAB1 . 'DEPLACEMENTS' . (TEMPS. I) ;
  EPS1 = EPSI DEP1 MOD1 MAT1;
  VIT1 = TAB1 . 'VITESSES' .     (TEMPS. I) ;
  CON1 = TAB1 . 'CONTRAINTES' .  (TEMPS. I) ;
  REA1 = REAC DEP1 CL3;
  ENE1 = ENER MOD1 EPS1 CON1;
  LUX1 = LUX1 ET( PROG (EXTR DEP1 UX  P1) ) ;
  LUX2 = LUX2 ET( PROG (EXTR DEP1 UX  P2) ) ;
  LUX3 = LUX3 ET( PROG (EXTR DEP1 UX  P3) ) ;
  LVX1 = LVX1 ET( PROG (EXTR VIT1 UX  P1) ) ;
  LVX2 = LVX2 ET( PROG (EXTR VIT1 UX  P2) ) ;
  LVX3 = LVX3 ET( PROG (EXTR VIT1 UX  P3) ) ;

*  LEELA =  LEELA ET (PROG  (INTG ENE1 MOD1));
  LEELA =  LEELA ET
     (PROG ((0.5*('XTY' DEP1 (RIG1*DEP1)
           ('MOTS' UX UY)
           ('MOTS' FX FY))) ));
  LENEC = LENEC ET
     (PROG ((0.5*(XTY VIT1 (MAS1*VIT1)
           (MOTS UX UY) (MOTS FX FY))) ));
  LEPOT = LEPOT ET
      (PROG (XTY DEP1  (MAS1*(MANU CHPO S1 1 UX (9.81)))
                  (MOTS UX UY) (MOTS FX FY)));
  LEREA =  LEREA ET (PROG (EXTR (RESU REA1) P1 FX));
FIN BB1 ;

EVU1    =  EVOL       MANU  'TIME' LT 'UX ' ((-1.)*LUX1) ;
EVU2    =  EVOL ROUGE MANU  'TIME' LT 'UX ' ((-1.)*LUX2);
EVU3    =  EVOL BLEU  MANU  'TIME' LT 'UX ' ((-1.)*LUX3) ;
EVREA   =  EVOL BLEU  MANU  'TIME' LT 'UX ' (LEREA) ;

EVEC    =  EVOL BLEU  MANU  'TIME' LT 'EC' LENEC ;
EVPOT   =  EVOL ROUGE MANU  'TIME' LT 'EP' LEPOT ;
EVEL    =  EVOL       MANU  'TIME' LT 'EP' LEELA ;
EVBILAN =  EVOL       MANU  'TIME' LT (((-1.)*LEPOT) +  LEELA + LENEC);

EVV1    =  EVOL       MANU  'TIME' LT 'UX ' ((-1.)*LVX1) ;
EVV2    =  EVOL ROUGE MANU  'TIME' LT 'UX ' ((-1.)*LVX2) ;
EVV3    =  EVOL BLEU  MANU  'TIME' LT 'UX ' ((-1.)*LVX3) ;

*
* ---  TRACER LES COURBES
*
SI GRAPH ;
 TA = TABLE;
 TA.1 = 'MARQ CARR';
 DESS (EVU1 ET EVU2 ET EVU3) TITRE 'DEPLACEMENT';
 DESS (EVV1 ET EVV2 ET EVV3) TITRE 'VITESSE';
 DESS (EVU3) XBORD 0. 0.20 TITRE 'DEPLACEMENT';
 DESS (EVV3) XBORD 0. 0.20 TITRE 'VITESSE';
 TA = TABLE;
 TA.1 = 'TIRR';
 TA.2= 'TIRC';
 DESS (EVPOT ET EVEC ET EVEL ET EVBILAN) TA XBORD 0. 0.20
      YBORD 0. 90. TITRE 'ENERGIE';
FINSI ;

*
TEST = (MAXI (EXTR EVBILAN ORDO) ABS)
      / (MAXI (EXTR EVPOT ORDO) ABS);
*
SI (TEST > 1.D-6) ;
 ERR 4;
FINSI ;

FIN;




