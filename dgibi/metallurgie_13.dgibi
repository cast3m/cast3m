* fichier :  metallurgie_13.dgibi
************************************************************************
* Section : Metallurgie
************************************************************************

*__________________________________________________________________*
*   TEST METALLURGIE_13                                            *
*                                                                  *
*   CALCUL DE l'ERREUR ANALYTIQUE - CAST3M POUR LEBLOND            *
*   Le calcul est fait pour differents pas de temps                *
*                                                                  *
*                                                                  *
*   Le modele metallurgique est code en ESOPE (Cast3m)             *
*__________________________________________________________________*
*                                                                  *
*   Vitesse de refroidissement : 10 C/s                            *
*      - Transformation (type Leblond) de l'autenite               *
*        en bainite seulement                                      *
*__________________________________________________________________*
*                                                                  *
*  On trace la courbe de l'erreur en fonction du pas de temps,     *
*  sur une echelle log-log                                         *
*__________________________________________________________________*
*
'OPTI' 'TRAC' PSC 'EPTR' 10 ;
IDESS = VRAI ;

OPTI DIME 2 ELEM 'TRI3';

P1 = 0. 0. ;
P2 = 1. 0. ;
P3 = 0. 1. ;

MAILT   = MANU 'TRI3' P1 P2 P3;

LISTPHA  = MOTS 'AUST' 'BAIN' ;
LISTREAC = MOTS 'AUST' ;
LISTPROD = MOTS 'BAIN' ;
LISTTYPE = MOTS 'LEBL' ;
NOMCONS = '16MND5';

MODCP1 = MODE MAILT 'METALLURGIE' 'PHASES' LISTPHA
                                  'REACTIFS' LISTREAC
                                  'PRODUITS' LISTPROD
                                  'TYPE' LISTTYPE
                                  'CONS' NOMCONS ;

TINI = 600.  ;
TFIN = 405.   ;


PEQ1 = EVOL 'MANU' 'T'   (PROG 375 380 405 600 )
                   'PEQ' (PROG 0.  1.  1.  0.  );

F1  = 'EVOL' 'MANU' 'TPOI' (PROG -100 -80 -60 -50 -40 -30 -25 -20 -18 -15 -12 -10 -9 -5 -1 -0.05 0 1.D-6)
                    'F' (PROG 0.005 1.573 2.857 3.417 3.982 4.583 4.833 5.26 5.472 6.033 7.675 11.4 18.45 17.1 0.328 0.00238 0.00238 0) ;

TAU1 = 20. ;

*Declaration de la vitesse de refroidissement : en Degres/s
DT_vit = -10. ;

*Calcul de la duree du refroidissement :
DUREE = -1. * (TINI - TFIN) / DT_vit ;

*Declaration du Temps initial :
TEMPINI = 1. ;
*Calcul du Temps final :
TEMPFIN = TEMPINI + DUREE ;
*Calcul du pas de temps en fonction de la vitesse de refroidissement :
NBPAST = 8.D0 ;
LDTini = 'LOG' (-10000.D0 / DT_vit) ;
LDTfin = 'LOG' (-0.001D0 / DT_vit) ;
PASLOG = (LDTfin - LDTini) / (NBPAST - 1);
LISTLOG ='PROG' LDTini 'PAS' PASLOG LDTfin;
LISTPAS='EXP' LISTLOG;


ERR_TAB = 'PROG' ;
PAS_TAB = 'PROG' ;
EVO_ERR = 'TABL' ;
EVO_ANA = 'TABL' ;
EVO_CAL = 'TABL' ;

*Boucle sur les differents taille de pas de temps :

MATCPa = 'MATE' MODCP1 'TAU1' TAU1 ;
REPE BOU_PAS (DIME LISTPAS) ;

  PAS = &BOU_PAS ;

  DTPS = 'EXTR' LISTPAS PAS ;

  LTCAL   ='PROG' TEMPINI 'PAS' DTPS TEMPFIN ;
  T       = TINI + (DT_vit * (LTCAL - TEMPINI));
  DTEMP   =('EXTR' T 2) - ('EXTR' T 1);

  ALPHA = 1. ;
* Initialisation de CHE1 : proportions de phases initiales :
  CTYP = 'CHAI' 'VARIABLES_INTERNES' ;
  CHE1 ='MANU' 'CHML' MODCP1 'AUST' ALPHA
                             'MART' 0.
                             'BAIN' 0.
                             'FERR' 0.
                             'MB  ' 0.
               'TYPE' CTYP 'NOEUD' ;

  PROPi  ='PROG' ALPHA ;

  F1L    ='IPOL' DT_vit F1 ;
  MATCPb = MATCPa 'ET' ('MATE' MODCP1 'F1' F1L) ;

  CHETPS0 ='MANU' 'CHML' MAILT 'TEMP' TEMPINI ;
  CHETPS1 = CHETPS0 '+' DTPS ;

  CHT0    ='MANU' 'CHML' MAILT 'T' TINI ;
  CHT1    = CHT0 + DTEMP ;


  nbpas = (DIME LTCAL) - 1;
  REPE CTEMP nbpas;
*   On calcule la temperature debut de pas et la temperature fin de pas :
    T0      = EXTR T  &CTEMP             ;
    T1      = EXTR T (&CTEMP + 1)        ;

*   On determine les differents parametres materiaux :
    PEQ11L = 'IPOL' T0 PEQ1 ;
    PEQ12L = 'IPOL' T1 PEQ1 ;

*   On definit le Materiaux debut de pas et le materiaux fin de pas :
    MATCP1 = MATCPb 'ET' ('MATE' MODCP1 'PEQ1' PEQ11L) ;
    MATCP2 = MATCPb 'ET' ('MATE' MODCP1 'PEQ1' PEQ12L) ;

*   Pour destruction
    CHDET0  = CHETPS0 ;
    CHDET1  = CHETPS1 ;
    CHDET2  = CHT0    ;
    CHDET3  = CHT1    ;
    LRDET0  = PROPi   ;
    CHEDET  = CHE1    ;

    CHE1   ='COMP'  MODCP1 (CHE1 'ET' CHETPS0 'ET' CHT0 'ET' MATCP1 ) ( CHETPS1 'ET' CHT1 'ET' MATCP2 ) ;

*   Extraction pour tracer :
    PiEvol ='EXTR' CHE1 'AUST' 1 1 1;
    PROPi  = PROPi 'ET' PiEvol ;

    CHETPS0 = CHETPS0 '+' DTPS  ;
    CHETPS1 = CHETPS0 '+' DTPS  ;

    CHT0    = CHT0    '+' DTEMP ;
    CHT1    = CHT0    '+' DTEMP ;

*   Destruction
    DETR CHDET0;
    DETR CHDET1;
    DETR CHDET2;
    DETR CHDET3;
    DETR LRDET0;
    DETR CHEDET;

  FIN CTEMP;

* Evolution calculee :
  EVO   = 'EVOL' 'NOIR' 'MANU' 'TEMP' LTCAL 'PROP' PROPi ;
  EVO_CAL . PAS = EVO ;

* Evolution analytique :
  PANALY ='EXP' ( -1 * F1L * (LTCAL - TEMPINI) / TAU1 ) ;
  PANALY = (F1L * (TEMPFIN - LTCAL)) + (TAU1 * (1 - PANALY )) ;
  PANALY = PANALY / ( F1L * (TEMPFIN - TEMPINI) ) ;
  EVOANA ='EVOL' 'VERT' 'MANU' 'TEMP' LTCAL 'AUST' PANALY;
  EVO_ANA . PAS = EVOANA ;

* Erreur absolue entre analytique et calculee :
  ERRABS  = 'ABS' (PANALY '-' PROPi) ;
  EVOERR = 'EVOL' 'ROUG' 'MANU' 'TEMP' LTCAL 'ERREUR' ERRABS ;
  EVO_ERR . PAS = EVOERR ;

  NORMERR = 'MAXI' ERRABS ;
  ERR_TAB = ERR_TAB 'ET' NORMERR ;
  PAS_TAB = PAS_TAB 'ET' DTPS ;

FIN BOU_PAS;



*Post-traitement :
'SI' IDESS ;

  REPE BOU_PAS (DIME LISTPAS) ;
    PAS = &BOU_PAS ;
    EVOERR = EVO_ERR . PAS ;
    DTPS = 'EXTR' LISTPAS PAS ;
    TITRE = 'CHAI' 'Erreur pour le pas de temps : ' DTPS ;
    DESS EVOERR 'GRIL' 'TIRC' 'GRIS'
                'TITR' TITRE ;

    EVO = EVO_CAL . PAS ;
    EVOANA = EVO_ANA . PAS ;
    TITRE = 'CHAI' 'LEBLOND - calcul pour le pas de temps : ' DTPS ;
    TL = TABL ;
    TL . 2 = 'TIRR' ;
    TL . 'TITRE' = TABL ;
    TL . 'TITRE' . 1 = 'Evolution calculee' ;
    TL . 'TITRE' . 2 = 'Evolution analytique' ;

    'SI' ( DTPS '>' 1.D0 ) ;
      TL . 1 = 'MARQ ROND NOLI' ;
    'SINO' ;
      TL . 1 = 'TIRR' ;
    'FINS' ;

    DESS (EVO 'ET' EVOANA) 'GRIL' 'TIRC' 'GRIS' 'TITR' TITRE 'LEGE' TL ;
  FIN BOU_PAS;

  TDESS     = 'TABL';
  TDESS . 1 = 'CHAI' ('MARQ ROND');
  ERRPAS    = 'EVOL' 'ROUG' 'MANU' 'PAS' PAS_TAB 'ERREUR' ERR_TAB;
  TITRE     = 'CHAI' 'Evolution de l erreur en fonction du pas de temps' ;
  DESS ERRPAS  'LOGX' 'LOGY'
               'GRIL' 'TIRC' 'GRIS'
               'TITR' TITRE
               'LEGE' TDESS;

'FINSI' ;

*Emission d'une erreur GIBIANE si l'erreur analytique - cast3m est trop grande :
REPE BOU_ERR (DIME LISTPAS) ;

  PAS = &BOU_ERR ;
  DTPS = 'EXTR' LISTPAS PAS ;
  NORMERR = 'EXTR' ERR_TAB PAS ;
  Tol1  = 1.D-2 * (DTPS ** 2) ; COMM 'Valeur fixee en 2018 par Thibault LINDECKER';

  MESS (CHAI '      Erreur : ' NORMERR ' | Tolerence : ' Tol1);
  SI (NORMERR >EG Tol1);
   MESS 'Erreur dans le calcul de la proportion d AUSTENITE';
   ERRE 5;
  FINS;

FIN BOU_ERR;

'TEMP' 'IMPR';

fin;




