* fichier :  lire_med_02.dgibi
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************

* Presentation : Ce cas-test permet de :
*                SORTIR des MAILLAGES, CHPOINT, MCHAML et TABLE PASAPAS
*                       au format MED
*                LIRE   les fichiers MED generes
*                VERIFIE et VALIDE les echanges au format MED
*                  - Les valeurs lues sont identiques aux valeurs
*                    attendues (Champs de coordonnees facile a comparer)
*
* Ameliorations a prevoir :
*                - Sortir et lire les MCHAML en d'autres points supports
*                - Sortir et lire les CONSTITUANTS dans les MCHAML
*                - Sortir et lire les NATURES      dans les CHPOINT
*
* Creation : 17/02/2017
* Createur : C. BERTHINIER
*
* Modifications :
*  CB215821 01/02/2018 : Sortie, Relecture & Validation des MAILLAGES
*                      : Sortie, Relecture & Validation des CHPOINTS
*                      : Sortie, Relecture & Validation des MCHAML
*                             - Aux GRAVITE
*                             - Aux NOEUDS
*  CB215821 13/03/2019 : Sortie, Relecture & Validation des TABLES PASAPAS
************************************************************************

'OPTI' 'TRAC' 'PSC';

************************************************************************
* PROCEDURE POUR VERIFIER UN MAILLAGE
************************************************************************
'DEBP' VERMAI M_CAS*'MAILLAGE' M_MED*'MAILLAGE' NOM_MAI*'MOT';
*  TRACE des MAILLAGES
   CHAI1 ='CHAI' 'MAILLAGE' ' ' NOM_MAI ' ' 'CAST3M';
   CHAI2 ='CHAI' 'MAILLAGE' ' ' NOM_MAI ' ' 'LU MED';
  'TRAC' 'QUAL' M_CAS 'TITR' CHAI1;
  'TRAC' 'QUAL' M_MED 'TITR' CHAI2;

*  Nombre d''elements
   NE_CAS ='NBEL' M_CAS;
   NE_MED ='NBEL' M_MED;
  'ELIM' M_MED M_CAS 1.D-6;
   NE_DIF ='NBEL' ('DIFF' M_CAS M_MED);

*  Nombre de noeuds
   NN_CAS ='NBNO' M_CAS;
   NN_MED ='NBNO' M_MED;

*  Verification Nombre d''elements
  'SI' (NE_CAS 'NEG' NE_MED);
    'OPTI' 'ECHO' 0;
    'MESS' '############################################################';
    'MESS' '#        LE MAILLAGES MED  RELUS N EST PAS CONFORME        #';
    'MESS' '#                  ERREUR NOMBRE ELEMENTS                  #';
    'MESS' '############################################################';
    'OPTI' 'ECHO' 1;
    'ERRE' 5;
  'FINS';
*  Verification Nombre de Noeuds
  'SI' (NN_CAS 'NEG' NN_MED);
    'OPTI' 'ECHO' 0;
    'MESS' '############################################################';
    'MESS' '#        LE MAILLAGES MED  RELUS N EST PAS CONFORME        #';
    'MESS' '#                  ERREUR NOMBRE DE NOEUDS                 #';
    'MESS' '############################################################';
    'OPTI' 'ECHO' 1;
    'ERRE' 5;
  'FINS';
*  Verification coordonnees identiques
  'SI' (NE_DIF 'NEG' 0);
    'OPTI' 'ECHO' 0;
    'MESS'  CHAI2;
    'MESS' '############################################################';
    'MESS' '#        LE MAILLAGES MED  RELUS N EST PAS CONFORME        #';
    'MESS' '#                   ERREUR CONNECTIVITE                    #';
    'MESS' '############################################################';
    'OPTI' 'ECHO' 1;
    'ERRE' 5;
  'FINS';
'FINP';
************************************************************************
* PROCEDURE POUR VERIFIER UN CHPOINT
************************************************************************
'DEBP' VERCHP CHP_CAS*'CHPOINT' CHP_MED*'CHPOINT' NOM_CHP*'MOT';
* A FAIRE !!!
'FINP';
************************************************************************
* PROCEDURE POUR VERIFIER UN MCHAML
************************************************************************
'DEBP' VERCHM CHM_CAS*'MCHAML' CHM_MED*'MCHAML' LIEU*'MOT' NOM_CHM*'MOT' LENT1*'LISTENTI' LMOT1*'LISTMOTS' XTEMPS*'FLOTTANT';
   MOD_CAS ='MODE' ('EXTR' CHM_CAS 'MAILLAGE') 'MECANIQUE' 'CONS' 'MED';
   MOD_MED ='MODE' ('EXTR' CHM_MED 'MAILLAGE') 'MECANIQUE' 'CONS' 'MED';
   CHM_CAS ='CHAN' 'CONS' CHM_CAS 'MED';

   CHAI1 ='CHAI' 'MCHAML' ' ' LIEU ' ' NOM_CHM ' ' 'CAST3M';
   CHAI2 ='CHAI' 'MCHAML' ' ' LIEU ' ' NOM_CHM ' ' 'LU MED';
  'TRAC' MOD_CAS CHM_CAS 'TITR' CHAI1;
  'TRAC' MOD_MED CHM_MED 'TITR' CHAI2;

*  Verification : Le champ lu est un champ de coordonnees, on le recree
   CHM_TMP='MANU' 'CHML' MOD_MED 'SCAL' 1.0 'TYPE' 'SCALAIRE' LIEU ;
   CHM_VER='VIDE' 'MCHAML';
  'REPE' SURI (DIME LMOT1);
     ID  ='EXTR' LENT1 &SURI;
     IMO ='EXTR' LMOT1 &SURI;
     CHM_VER=('NOMC' (('COOR' ID CHM_TMP) * (1.D0 + XTEMPS)) IMO) 'ET' CHM_VER ;
  'FIN' SURI;
   XDIFF  ='MAXI' 'ABS' (CHM_MED - CHM_VER) ;

  'SI' (XDIFF 'NEG' 0.D0);
    'OPTI' 'ECHO' 0;
    'MESS'  CHAI2;
    'MESS' '############################################################';
    'MESS' '#          LE MCHAML MED RELUS N EST PAS CONFORME          #';
    'MESS' '#                    ERREUR  DE VALEURS                    #';
    'MESS' '############################################################';
    'ERRE' 5;
  'FINS';
'FINP';

***********************************************************************
* Lecture d'un MAILLAGE 'NASTRAN' avec plusieurs types d'elements
***********************************************************************
 TAB2  ='LIRE' 'NAS' '../divers/nastran_long.nas' ;
 TAB21 ='INDE' (TAB2.'MAILLAGES');

***********************************************************************
* Fabrication des MAILLAGES avant d'exporter au format MED            *
***********************************************************************
 MAILTOT ='VIDE' 'MAILLAGE';
'REPE' SURI ('DIME' TAB21);
   MAILTOT = MAILTOT 'ET' (TAB2.'MAILLAGES' . (TAB21 . &SURI));
'FIN' SURI;

* Le MAILLAGE MAILTOT contient 11 types d'elements :
*  - 29  element(S) de type SEG2
*  - 93  element(S) de type TRI3
*  - 164 element(S) de type QUA4
*  - 271 element(S) de type TET4
*  - 36  element(S) de type PRI6
*  - 84  element(S) de type CUB8
*
*  - 29  element(S) de type TRI6
*  - 27  element(S) de type QUA8
*  - 271 element(S) de type TE10
*  - 24  element(S) de type PR15
*  - 12  element(S) de type CU20

 P1     = 'NOEU' 1   ;
 P2     = 'NOEU' 10  ;
 P3     = 'NOEU' 100 ;

 MSEG2  ='ELEM' MAILTOT 'SEG2';
 MTRI3  ='ELEM' MAILTOT 'TRI3';
 MQUA4  ='ELEM' MAILTOT 'QUA4';
 MTET4  ='ELEM' MAILTOT 'TET4';
 MPRI6  ='ELEM' MAILTOT 'PRI6';
 MCUB8  ='ELEM' MAILTOT 'CUB8';
 MTRI6  ='ELEM' MAILTOT 'TRI6';
 MQUA8  ='ELEM' MAILTOT 'QUA8';
 MTE10  ='ELEM' MAILTOT 'TE10';
 MPR15  ='ELEM' MAILTOT 'PR15';
 MCU20  ='ELEM' MAILTOT 'CU20';

***********************************************************************
* Sortie MED des MAILLAGES (SIMPLES & COMPLEXES)
***********************************************************************
'OPTI' 'SORT' 'MED_MAILLAGE_simple.med';
'SORT' 'MED' MTRI3 ;

'OPTI' 'SORT' 'MED_MAILLAGE_complexe.med';
'SORT' 'MED' MAILTOT ;

***********************************************************************
* Sortie MED des CHPOINTS
***********************************************************************
 CHPX1 ='CHAN' 'COMP' ('COOR' 1 MTRI3  ) 'UX';
 CHPY1 ='CHAN' 'COMP' ('COOR' 2 MAILTOT) 'UY';

 CHPTOT= CHPX1 'ET' CHPY1 ;
'OPTI' 'SORT' 'MED_CHPOIN_simple.med';
'SORT' 'MED' CHPX1;

'OPTI' 'SORT' 'MED_CHPOIN_complexe.med';
'SORT' 'MED' CHPTOT;

'OPTI' 'SORT' 'MED_CHPOIN_melange.med';
'SORT' 'MED' CHPX1 CHPY1 CHPTOT;

***********************************************************************
* Sortie MED des MCHAML
***********************************************************************
* MCHAML au GRAVITE (1 SOUS-ZONE et 11 SOUS-ZONES)
 MOD1a ='MODE' MTRI3   'MECANIQUE' ;
 MOD1b ='MODE' MAILTOT 'MECANIQUE' ;
 CHA1a ='MANU' 'CHML' MOD1a 'SCAL' 1.0 'GRAVITE';
 CHA1b ='MANU' 'CHML' MOD1b 'SCAL' 1.0 'GRAVITE';

 CHAX1 ='NOMC' ('COOR' 1 CHA1a) 'UX';
 CHAY1 ='NOMC' ('COOR' 2 CHA1b) 'UY';

'OPTI' 'SORT' 'MED_MCHAML_GRAVITE_SZ1.med';
'SORT' 'MED' CHAX1 ;

'OPTI' 'SORT' 'MED_MCHAML_GRAVITE_SZ11.med';
'SORT' 'MED' CHAY1 ;

* MCHAML au NOEUD (1 SOUS-ZONE et 11 SOUS-ZONES)
 CHA2a ='MANU' 'CHML' MOD1a 'SCAL' 1.0 'NOEUD';
 CHA2b ='MANU' 'CHML' MOD1b 'SCAL' 1.0 'NOEUD';
 CHAZ1 ='NOMC' ('COOR' 3 CHA2a) 'UZ';
 CHAZ2 ='NOMC' ('COOR' 3 CHA2b) 'UZ';

'OPTI' 'SORT' 'MED_MCHAML_NOEUD_SZ1.med';
'SORT' 'MED' CHAZ1 ;

'OPTI' 'SORT' 'MED_MCHAML_NOEUD_SZ11.med';
'SORT' 'MED' CHAZ2;

* MCHAML melange (NOEUD & GRAVITE, 1 SOUS-ZONE et 11 SOUS-ZONES)
'OPTI' 'SORT' 'MED_MCHAML_melange.med';
'SORT' 'MED' CHAX1 CHAY1 CHAZ1 CHAZ2;



***********************************************************************
* Sortie MED d'une TABLE de SOUS-TYPE PASAPAS
***********************************************************************
COO1 COO2 COO3 ='COOR' MAILTOT;
COO1           ='NOMC' COO1 'CX';
COO2           ='NOMC' COO2 'CY';
COO3           ='NOMC' COO3 'CZ';
COOCHP         = COO1 'ET' COO2 'ET' COO3 ;

COO1 COO2 COO3 ='COOR' CHA1b ;
COO1           ='NOMC' COO1 'MX';
COO2           ='NOMC' COO2 'MY';
COO3           ='NOMC' COO3 'MZ';
COOCHMG        = COO1 'ET' COO2 'ET' COO3 ;

COO1 COO2 COO3 ='COOR' CHA2b ;
COO1           ='NOMC' COO1 'NX';
COO2           ='NOMC' COO2 'NY';
COO3           ='NOMC' COO3 'NZ';
COOCHMN        = COO1 'ET' COO2 'ET' COO3 ;

LTPS1 ='PROG' 0.D0 'PAS' 0.5D0 1.5D0 ;
NBTPS ='DIME' LTPS1;

TPASAP ='TABL';
TPASAP.'SOUSTYPE'='MOT' 'PASAPAS' ;
TPASAP.'MODELE'  = MOD1b ;

TPASAP.'TEMPS'               ='TABL' ;
TPASAP.'COORDONNEES_CHPOINT1'='TABL' ;'COMM' 'Un seul   MSOUPO    dans le CHPOINT';
TPASAP.'MCHAML_GRAVI1'       ='TABL' ;'COMM' 'Un seul constituant dans le MCHAML aux GRAVITE';
TPASAP.'MCHAML_NOEUD1'       ='TABL' ;'COMM' 'Un seul constituant dans le MCHAML aux NOEUDS' ;
'REPE' SURI NBTPS;
   II     =&SURI;
   Xtps   ='EXTR' LTPS1 II ;

   TPASAP.'TEMPS'               . (II - 1) = Xtps ;
   TPASAP.'COORDONNEES_CHPOINT1'. (II - 1) =(COOCHP  + 1.D0) * ( 1.D0 + Xtps) ;
   TPASAP.'MCHAML_GRAVI1'       . (II - 1) =(COOCHMG + 1.D0) * ( 1.D0 + Xtps) ;
   TPASAP.'MCHAML_NOEUD1'       . (II - 1) =(COOCHMN + 1.D0) * ( 1.D0 + Xtps) ;
'FIN' SURI;

* TABLE de SOUSTYPE 'PASAPAS' avec CHPOINT, MCHAML (GRAVITE & NOEUDS)
'OPTI' 'SORT' 'MED_PASAPAS.med';
'SORT' 'MED' TPASAP ;


*#####################################################################*
* Verification relecture des MAILLAGES
*#####################################################################*
 TAB1 ='LIRE' 'MED' 'MED_MAILLAGE_simple.med'  ;
 TAB2 ='LIRE' 'MED' 'MED_MAILLAGE_complexe.med';

*#####################################################################*
* Validation relecture des MAILLAGES
*#####################################################################*
'LIST' TAB1;
 MTRI3a = TAB1.'MTRI3' ;
 VERMAI MTRI3 MTRI3a 'MTRI3';

'LIST' TAB2;
 MAILTOTa= TAB2.'MAILTOT';
 MSEG2a  = TAB2.'MSEG2'  ;
 MCU20a  = TAB2.'MCU20'  ;
 MPR15a  = TAB2.'MPR15'  ;
 MTE10a  = TAB2.'MTE10'  ;
 MQUA8a  = TAB2.'MQUA8'  ;
 MTRI6a  = TAB2.'MTRI6'  ;
 MCUB8a  = TAB2.'MCUB8'  ;
 MPRI6a  = TAB2.'MPRI6'  ;
 MTET4a  = TAB2.'MTET4'  ;
 MQUA4a  = TAB2.'MQUA4'  ;
 MTRI3a  = TAB2.'MTRI3'  ;

 VERMAI MAILTOT MAILTOTa 'MAILTOT';
 VERMAI MSEG2   MSEG2a   'MSEG2'  ;
 VERMAI MCU20   MCU20a   'MCU20'  ;
 VERMAI MPR15   MPR15a   'MPR15'  ;
 VERMAI MTE10   MTE10a   'MTE10'  ;
 VERMAI MQUA8   MQUA8a   'MQUA8'  ;
 VERMAI MTRI6   MTRI6a   'MTRI6'  ;
 VERMAI MCUB8   MCUB8a   'MCUB8'  ;
 VERMAI MPRI6   MPRI6a   'MPRI6'  ;
 VERMAI MTET4   MTET4a   'MTET4'  ;
 VERMAI MQUA4   MQUA4a   'MQUA4'  ;
 VERMAI MTRI3   MTRI3a   'MTRI3'  ;

*#####################################################################*
* Verification relecture des CHPOINT
*#####################################################################*
 TAB3 ='LIRE' 'MED' 'MED_CHPOIN_simple.med'  ;
 TAB4 ='LIRE' 'MED' 'MED_CHPOIN_complexe.med';
 TAB5 ='LIRE' 'MED' 'MED_CHPOIN_melange.med' ;

*#####################################################################*
* Validation relecture des CHPOINT
*#####################################################################*
* CONTROLE VISUEL POUR LE MOMENT, COMPARER LES VALEURS !!! A FAIRE !!!
'LIST' TAB3;
 CHPX2 = TAB3.'CHPX1';
'TRAC' CHPX1 MTRI3        'TITR' 'CHPOINT_SIMPLE CAST3M';
'TRAC' CHPX2 TAB3.'MTRI3' 'TITR' 'CHPOINT_SIMPLE LU MED';

*Verification : Le champ lu est un champ de coordonnees, on le recree
 MMED  ='EXTR' CHPX2 'MAILLAGE' ;
 MORI  ='EXTR' CHPX1 'MAILLAGE' ;
 NBMED ='NBEL' MMED;
 NBORI ='NBEL' MORI;
'SI' ('NEG' NBMED NBORI);
  'OPTI' 'ECHO' 0;
  'MESS' '############################################################';
  'MESS' '#     LE CHPOINT MED CHPX1 RELUS EST NON CONFORMES AU      #';
  'MESS' '#       CHPOINT EXPORTE : NOMBRE ELEMENTS INCORRECT        #';
   CHA1 ='CHAI' '#      :' NBORI ':' NBMED ':';
  'MESS' CHA1 ;
  'MESS' '############################################################';
  'OPTI' 'ECHO' 1;
  'ERRE' 5;
'FINS';

 CHPX2v ='CHAN' 'COMP' ('COOR' 1 MMED  ) 'UX';
 ValX2  ='MAXI' 'ABS' (CHPX2 - CHPX2v) ;
'SI' ('NEG' ValX2 0.D0);
  'OPTI' 'ECHO' 0;
  'MESS' '############################################################';
  'MESS' '#     LE CHPOINT MED CHPX1 RELUS EST NON CONFORMES AU      #';
  'MESS' '#       CHPOINT EXPORTE : VALEURS INCORRECTES              #';
  'MESS' '############################################################';
  'OPTI' 'ECHO' 1;
  'ERRE' 5;
'FINS';

'LIST' TAB4;
 CHPTO2 = TAB4.'CHPTOT_1' 'ET' TAB4.'CHPTOT_2';
 CHPX2  ='CHAN' 'COMP' ('COOR' 1 TAB4.'MTRI3'  ) 'UX';
 CHPY2  ='CHAN' 'COMP' ('COOR' 2 TAB4.'MAILTOT') 'UY';
'TRAC' CHPX1 MTRI3          'TITR' 'CHPOINT_COMPLEXE CAST3M, UX';
'TRAC' CHPX2 TAB4.'MTRI3'   'TITR' 'CHPOINT_COMPLEXE LU MED, UX';
'TRAC' CHPY1 MAILTOT        'TITR' 'CHPOINT_COMPLEXE CAST3M, UY';
'TRAC' CHPY2 TAB4.'MAILTOT' 'TITR' 'CHPOINT_COMPLEXE LU MED, UY';

*Verification : Le champ lu est un champ de coordonnees, on le recree
 CHPX2v ='CHAN' 'COMP' ('COOR' 1 ('EXTR' CHPX2 'MAILLAGE')) 'UX';
 CHPY2v ='CHAN' 'COMP' ('COOR' 2 ('EXTR' CHPY2 'MAILLAGE')) 'UY';
 ValX2  ='MAXI' 'ABS' (CHPX2 - CHPX2v);
 ValY2  ='MAXI' 'ABS' (CHPY2 - CHPY2v);

'SI' ('NEG' ValX2 0.D0);
  'OPTI' 'ECHO' 0;
  'MESS' '############################################################';
  'MESS' '#     LE CHPOINT MED CHPX1 RELUS EST NON CONFORMES AU      #';
  'MESS' '#       CHPOINT EXPORTE : VALEURS INCORRECTES              #';
  'MESS' '############################################################';
  'OPTI' 'ECHO' 1;
  'ERRE' 5;
'FINS';
'SI' ('NEG' ValY2 0.D0);
  'OPTI' 'ECHO' 0;
  'MESS' '############################################################';
  'MESS' '#     LE CHPOINT MED CHPY1 RELUS EST NON CONFORMES AU      #';
  'MESS' '#       CHPOINT EXPORTE : VALEURS INCORRECTES              #';
  'MESS' '############################################################';
  'OPTI' 'ECHO' 1;
  'ERRE' 5;
 FINS ;

'LIST' TAB5;
 CHPX2  = TAB5.'CHPX1';
 CHPY2  = TAB5.'CHPY1';
 CHPTOX ='EXCO' (TAB5.'CHPTOT_1' 'ET' TAB5.'CHPTOT_2') 'UX' 'UX';
 CHPTOY ='EXCO' (TAB5.'CHPTOT_1' 'ET' TAB5.'CHPTOT_2') 'UY' 'UY';
'TRAC' CHPX1  MTRI3          'TITR' 'MED CHPOIN MELANGE : CHPX1  CAST3M'    ;
'TRAC' CHPX2  TAB5.'MTRI3'   'TITR' 'MED CHPOIN MELANGE : CHPX1  LU MED'    ;
'TRAC' CHPY1  MAILTOT        'TITR' 'MED CHPOIN MELANGE : CHPY1  CAST3M'    ;
'TRAC' CHPY2  TAB5.'MAILTOT' 'TITR' 'MED CHPOIN MELANGE : CHPY1  LU MED'    ;
'TRAC' CHPX1  MTRI3          'TITR' 'MED CHPOIN MELANGE : CHPTOT CAST3M, UX';
'TRAC' CHPTOX TAB5.'MTRI3'   'TITR' 'MED CHPOIN MELANGE : CHPTOT LU MED, UX';
'TRAC' CHPY1  MAILTOT        'TITR' 'MED CHPOIN MELANGE : CHPTOT CAST3M, UY';
'TRAC' CHPTOY TAB5.'MAILTOT' 'TITR' 'MED CHPOIN MELANGE : CHPTOT LU MED, UY';

*#####################################################################*
* Verification relecture des MCHAML
*#####################################################################*
TAB6 ='LIRE' 'MED' 'MED_MCHAML_GRAVITE_SZ1.med' ;
TAB7 ='LIRE' 'MED' 'MED_MCHAML_GRAVITE_SZ11.med';
TAB8 ='LIRE' 'MED' 'MED_MCHAML_NOEUD_SZ1.med'   ;
TAB9 ='LIRE' 'MED' 'MED_MCHAML_NOEUD_SZ11.med'  ;
TAB10='LIRE' 'MED' 'MED_MCHAML_melange.med'     ;

*#####################################################################*
* Validation relecture des MCHAML
*#####################################################################*
'LIST' TAB6;
 CHAX1a = TAB6.'CHAX1';
 VERCHM CHAX1 CHAX1a 'GRAVITE' 'CHAX1' (LECT 1) (MOTS 'UX') 0.D0;

'LIST' TAB7;
 CHAY1a = TAB7.'CHAY1';
 VERCHM CHAY1 CHAY1a 'GRAVITE' 'CHAY1' (LECT 2) (MOTS 'UY') 0.D0;

'LIST' TAB8;
 CHAZ1a = TAB8.'CHAZ1';
 VERCHM CHAZ1 CHAZ1a 'NOEUD'   'CHAZ1' (LECT 3) (MOTS 'UZ') 0.D0;

'LIST' TAB9;
 CHAZ2a = TAB9.'CHAZ2';
 VERCHM CHAZ2 CHAZ2a 'NOEUD'   'CHAZ2' (LECT 3) (MOTS 'UZ') 0.D0;

'LIST' TAB10;
 CHAX1a = TAB10.'CHAX1';
 CHAY1a = TAB10.'CHAY1';
 CHAZ1a = TAB10.'CHAZ1';
 CHAZ2a = TAB10.'CHAZ2';

 VERCHM CHAX1 CHAX1a 'GRAVITE' 'CHAX1' (LECT 1) (MOTS 'UX') 0.D0;
 VERCHM CHAY1 CHAY1a 'GRAVITE' 'CHAY1' (LECT 2) (MOTS 'UY') 0.D0;
 VERCHM CHAZ1 CHAZ1a 'NOEUD'   'CHAZ1' (LECT 3) (MOTS 'UZ') 0.D0;
 VERCHM CHAZ2 CHAZ2a 'NOEUD'   'CHAZ2' (LECT 3) (MOTS 'UZ') 0.D0;

*#####################################################################*
* Verification relecture des TABLES de SOUSTYPE 'PASAPAS'
*#####################################################################*
 TAB11='LIRE' 'MED' 'MED_PASAPAS.med';
'LIST' TAB11;

*#####################################################################*
* Validation relecture des TABLES de SOUSTYPE 'PASAPAS'
*#####################################################################*

FIN;


