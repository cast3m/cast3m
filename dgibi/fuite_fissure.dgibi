* fichier :  fuite_fissure.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*   Test fuite_fissure.dgibi: Jeux de données         *
*    ----------------------------------------         *
*                                                     *
*******************************************************
*****   CALCUL DU DEBIT DE FUITE D'UN             *****
*****   MELANGE D'AIR SEC                         *****
*****   A TRAVERS UNE FISSURE TRAVERSANTE         *****
*****   POUR UNE DIFFERENCE DE PRESSION IMPOSEE   *****
*****   écoulement monodimensionnel, diphasique   *****
*****   homogène, en régime permanent             *****
*******************************************************
*****   AUTEUR   : HELENE SIMON                   *****
*****   DATE     : 26/10/2005                     *****
*******************************************************
opti echo 1 dime 2 elem qua4 ;
*une seule ligne : la fissure est parallele au cote l12
l = 1./(2.**0.5) ;
p1=0. 0. ; p2= l   l ;
l12 = d p1 p2 dini 0.01 dfin 0.01  ;
*
ouv = 'PROG' (nbno l12)*200e-6 ;
ep = manu 'CHPO' (chan 'POI1' l12) 1 'OUV' ouv natu 'DIFFUS';
tp = manu chpo l12  1 'T' 120. natu 'DIFFUS' ;
*modele
MO1 = 'MODELISER' l12 FISSURE MASS PARF POISEU_COLEBROOK;
*champ de prop materielles
MAT1 = 'MATERIAU' MO1 'RUGO' 40e-6  ;
*conditions aux limites
TAB_CL = table ;
TAB_CL.'PRESSION_TOTALE_AMONT' = 5e5 ;
TAB_CL.'PRESSION_VAPEUR_AMONT' = 0. ;
TAB_CL.'TEMPERATURE_AMONT' = 170. ;
TAB_CL.'PRESSION_TOTALE_AVAL' = 1e5 ;
TAB_CL.'TEMPERATURE_PAROI' = tp ;
TAB_CL.'OUVERTURE' = ep ;
*
res = fiss MO1 MAT1 TAB_CL ;
*
Qn = 'EXTRAIRE' res 'Q' P2 ;
Qref = 1.74459E-02 ;
'MESSAGE' 'debit calcule = ' Qn 'debit reference = ' Qref;
ere1=(Qn-Qref)/Qref ;
ere='ABS' ere1 ;
SI (ere  <EG 1.d-5 ) ;
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
'FIN' ;






