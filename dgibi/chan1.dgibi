* fichier : chan1.dgibi
************************************************************************
* Section : Langage Objets
* Section : Maillage Autres
************************************************************************
'OPTION' 'ECHO' 0 ;
************************************************************************
* NOM         : CHAN1
* DESCRIPTION : Teste le changement des QUAFs en TRI3 ou QUA4
*               ou TET4 ou CUB8 ou PYR5
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : gounand@semt2.smts.cea.fr
**********************************************************************
* VERSION    : v1, 09/01/2007, version initiale
* HISTORIQUE : v1, 09/01/2007, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
* Prière de PRENDRE LE TEMPS de compléter les commentaires
* en cas de modification de ce sous-programme afin de faciliter
* la maintenance !
************************************************************************
interact = FAUX ;
graph    = FAUX ;
'MESSAGE' ('CHAINE' 'Execution de chan1.dgibi') ;
*
* On vérifie que le changement de l'enveloppe du maillage et
* l'enveloppe du changement du maillage sont identiques
*
'DEBPROC' testmail ;
'ARGUMENT' mail*'MAILLAGE' ;
'ARGUMENT' typres*'MOT' ;
*
email = 'ENVELOPPE' mail ;
_mail = 'CHANGER' mail 'QUAF' ;
_email = 'CHANGER' email 'QUAF' ;
'ELIMINATION' ('ET' _mail _email) 1.D-6 ;
*
mail2  = 'CHANGER' _mail typres ;
email2 = 'CHANGER' _email typres ;
email3 = 'ENVELOPPE' mail2 ;
'SI' graph ;
   tit = 'CHAINE' 'Maillage original' ;
   'TRACER' 'FACE' 'ECLA' 0.7 mail 'TITR' tit ;
   tit = 'CHAINE' 'Maillage decoupe en ' typres ;
   'TRACER' 'FACE' 'ECLA' 0.7 mail2 'TITR' tit ;
   tit = 'CHAINE' 'Enveloppe du maillage decoupe en ' typres ;
   'TRACER' 'FACE' 'ECLA' 0.7 email3 'TITR' tit ;
'FINSI' ;
mvid = 'DIFF' email2 email3 ;
*'MESSAGE' 'Mail' ; 'LISTE' mail ;
*'MESSAGE' 'Email ' ; 'LISTE' email ;
*'MESSAGE' 'Email 2' ; 'LISTE' email2 ;
*'MESSAGE' 'Email 3' ; 'LISTE' email3 ;
*'MESSAGE' 'Mvid' ; 'LISTE' mvid ;
nlmv = 'NBEL' mvid ;
*'LISTE' nlmv ;
verm mail2 ;
ok = 'EGA' nlmv 0 ;
'RESPRO' ok ;
'FINPROC' ;
*
'OPTION' 'DIME' 3 'ELEM' 'CUB8'  ;
'SI' ('NON' interact) ;
  'OPTION' 'TRAC' 'PS' ;
'SINON' ;
*  'OPTION' 'TRAC' 'X' ;
  'OPTION' 'TRAC' 'OPEN' ;
'FINSI' ;
*
ltypl = 'MOTS' 'TET4' 'CUB8' 'PYR5' ;
dlt   = 'DIME' ltypl ;
ok = VRAI ;
'REPETER' iilt dlt ;
   ilt = &iilt ;
   mtypl = 'EXTRAIRE' ltypl ilt ;
* Maillage de base : un cube et un prisme
   p1 = 0. 0. 0. ; p2 = 1. 0. 0. ; p3 = 1. 1. 0. ; p4 = 0. 1. 0. ;
   p5 = 2. 2. 0. ; p6 = 0. 0. 1. ;
   m1 = 'MANUEL' 'QUA4' p1 p2 p3 p4 ; m2 = 'MANUEL' 'TRI3' p2 p5 p3 ;
   mt1 = 'VOLUME' 'TRAN' ('ET' m1 m2) 1 p6 ;
   tes = TESTMAIL mt1 mtypl ;
   'SI' ('NON' tes)  ;
      tt = 'CHAINE' '!!!! Test pas bon pour CHANGER mt1 ' mtypl ;
      'SI' graph ;
         tit = 'CHAINE' 'Mt1' ;
         'TRACER' 'FACE' 'ECLA' 0.7 mt1 'TITR' tit ;
      'FINSI' ;
   'FINSI' ;
* Maillage de base transformé en TET4
   mt2 = 'CHANGER' ('CHANGER' mt1 'QUAF') 'TET4' ;
   tes = TESTMAIL mt2 mtypl ;
   'SI' ('NON' tes)  ;
      tt = 'CHAINE' '!!!! Test pas bon pour CHANGER mt2 ' mtypl ;
      'SI' graph ;
         tit = 'CHAINE' 'Mt2' ;
         'TRACER' 'FACE' 'ECLA' 0.7 mt2 'TITR' tit ;
      'FINSI' ;
   'FINSI' ;
* Maillage de base transformé en PYR5
   'SI' ('NEG' mtypl 'CUB8') ;
      mt3 = 'CHANGER' ('CHANGER' mt1 'QUAF') 'PYR5' ;
      tes = TESTMAIL mt3 mtypl ;
      'SI' ('NON' tes)  ;
         tt = 'CHAINE' '!!!! Test pas bon pour CHANGER mt3 ' mtypl ;
         'SI' graph ;
            tit = 'CHAINE' 'Mt3' ;
            'TRACER' 'FACE' 'ECLA' 0.7 mt3 'TITR' tit ;
         'FINSI' ;
      'FINSI' ;
   'FINSI' ;
'FIN' iilt ;
*
'SI' ('NON' ok) ;
   'MESSAGE' ('CHAINE' 'Il y a eu des erreurs') ;
   'ERREUR' 5 ;
'SINON' ;
   'MESSAGE' ('CHAINE' 'Tout sest bien passe !') ;
'FINSI' ;
*
'MESSAGE' ('CHAINE' 'Cest tout pour aujourdhui') ;
*
'SI' interact ;
   'OPTION' 'ECHO' 1 ;
   'OPTION' 'DONN' 5 ;
'FINSI' ;
*
* End of dgibi file CHAN1
*
'FIN' ;





