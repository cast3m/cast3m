* fichier : ordo_1.dgibi

************************************************************************
* Section : Langage Objets
************************************************************************

* CAS-TEST DE VERIFICATION DU FONCTIONNEMENT DE L'OPERATEUR ORDO ET
* DE TOUTES SES OPTIONS (HORS 'COUT' => VOIR ORDO_2.DGIBI)
*
*
* GRAINE POUR LE GENERATEUR ALEATOIRE
SEED1 = 13 ;
*
* LONGUEUR DES LISTES GENEREES
NN1 = 7 ;
*
* CHEMIN D'ACCES AU FICHIER DE LISTING
FICHMD5 = 'ordo_1.txt' ;
*
* VALIDER LE BON FONCTIONNEMENT DU CAS-TEST
* "MANUELLEMENT" (= EN RELISANT LE LISTING)
* PUIS REPORTER CI-DESSOUS LA SOMME MD5 ATTENDUE
MD5LINUX = 'bd8d6531904bfa13eb1f5549ae191607' ;
MD5WINDO = 'd2e3fc06af052cc9bdd28342f7acd062' ;


*********************************************************************
*                                                                   *
*             G E N E R A T I O N   D E S   L I S T E S             *
*                                                                   *
*********************************************************************


* LISTMOTS
* ========
LMOT1 = MOTS 'DEfa' 'BLEU' 'RoUg' 'ROse' 'VeRT' 'tURQ' 'JaUN' 'BLAn'
             'NoIR' 'VIOl' 'ORAn' 'AzUR' 'OCEA' 'cYAN' 'OLIV' 'GRiS'
             'DEFA' 'BLEU' 'ROUG' 'ROSE' 'VERT' 'TURQ' 'JAUN' 'BLAN'
             'NOIR' 'VIOL' 'ORAN' 'AZUR' 'OCEA' 'CYAN' 'OLIV' 'GRIS'
             'defa' 'bleu' 'roug' 'rose' 'vert' 'turq' 'jaun' 'blan'
             'NoIR' 'ViOL' 'ORaN' 'AZUr' 'OCEa' 'CYAN' 'OLIv' 'GriS'
             'dEFA' 'blEU' 'rouG' 'ROse' 'vERt' 'tUrq' 'JaUN' 'BlaN'
             'NOiR' 'vIOl' 'OraN' 'AZuR' 'OcEa' 'cYAN' 'OLiv' 'GrIS' ;
NBCOUL = DIME LMOT1 ;
LPOS1 = ENTI (BRUI 'BLAN' 'UNIF' (0.5*NBCOUL) (0.5*NBCOUL) NN1 SEED1) ;
LPOS1 = BORN LPOS1 'COMPRIS' 1 NBCOUL ;
LMOT1 = EXTR LMOT1 LPOS1 ;

* LISTREEL
* ========
LREE1 = BRUI 'BLAN' 'UNIF' 0. 5. NN1 SEED1 ;

* LISTENTI
* ========
LENT1 = ENTI (BRUI 'BLAN' 'UNIF' 0. NN1 NN1 SEED1) ;




*********************************************************************
*                                                                   *
*   P R O C E D U R E S   P O U R   V E R I F I E R   L E   J E U   *
*                       D E   D O N N E E S                         *
*                                                                   *
*********************************************************************

OPTI 'SORT' FICHMD5 ;
*
DEBP TESTER LST ;
   NB = DIME LST ;
   CHA = ' ' ;
   REPE K NB ;
      OBJ = EXTR LST &K ;
      TYP = TYPE OBJ ;
      SI (EGA TYP 'FLOTTANT') ;
*         FORMAT FORTRAN AYANT LE MEME COMPORTEMENT SUR TOUTES LES
*         MACHINES DE TEST
          CHA = CHAI CHA ' ' 'FORMAT' '(F0.3)' OBJ ;
      SINON ;
          CHA = CHAI CHA ' ' OBJ ;
      FINS ;
   FIN K ;
   SORT 'CHAI' CHA ;
   LIST LST ;
FINP ;
*
DEBP VALIDER ;
*  ON FERME LE FICHIER DE LISTING
   OPTI 'SORT' FICHMD5 ;
*
*  ON CALCULE LE HASH MD5
*  (SANS PLANTAGE SI LA COMMANDE md5sum N'EXISTE PAS)
   OPTI 'ERRE' 'CONTINUER' ;
   TAB1 = EXTE (CHAI 'md5sum' ' ' FICHMD5) ;
   list tab1;
   OPTI 'ERRE' 'NORMAL' ;
*  VALIDATION DU HASH MD5
   SI (EXIS TAB1) ;
      MD5 = MOT TAB1. 1 ;
      RESP ((EGA MD5 MD5LINUX) OU (EGA MD5 MD5WINDO)) ;
   SINON ;
      RESP VRAI ;
   FINS ;
FINP ;



******************************
*                            *
*   L  I  S  T  R  E  E  L   *
*                            *
******************************



* OPTIONS SIMPLES
* ===============

LIST LREE1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1) ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1 'ABSO') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1 'UNIQ') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1 'UNIQ' 0.5) ;
SAUT 2 'LIGNES' ;



* OPTIONS COUPLEES
* ================

LIST LREE1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LREE1 'UNIQ' 0.5 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'ABSO' LREE1 'UNIQ' 0.5 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'ABSO' 'DECR' LREE1) ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'ABSO' LREE1 'UNIQ') ;
SAUT 2 'LIGNES' ;



* TRI MULTIPLE
* ============

LIST LREE1 ; LIST LENT1 ; LIST LMOT1 ;
SAUT 2 'LIGNES' ;

A B = ORDO LREE1 LENT1 'ABSO' 'DECR' ;
TESTER A ; TESTER B ;
SAUT 2 'LIGNES' ;

A B C = ORDO LREE1 LENT1 LMOT1 'CROI' ;
TESTER A ; TESTER B ; TESTER C ;
SAUT 2 'LIGNES' ;






******************************
*                            *
*   L  I  S  T  E  N  T  I   *
*                            *
******************************



* OPTIONS SIMPLES
* ===============

LIST LENT1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LENT1) ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LENT1 'ABSO') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LENT1 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'UNIQ' LENT1) ;
SAUT 2 'LIGNES' ;


* OPTIONS COUPLEES
* ================

LIST LENT1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'UNIQ' LENT1 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'ABSO' LENT1 'UNIQ' 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'ABSO' 'DECR' LENT1) ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LENT1 'ABSO' 'UNIQ') ;
SAUT 2 'LIGNES' ;



* TRI MULTIPLE
* ============

LIST LENT1 ; LIST LREE1 ; LIST LMOT1 ;
SAUT 2 'LIGNES' ;

A B = ORDO LENT1 'ABSO' LREE1 'DECR' ;
TESTER A ; TESTER B ;
SAUT 2 'LIGNES' ;

A B C = ORDO LENT1 LREE1 LMOT1 'CROI' ;
TESTER A ; TESTER B ; TESTER C ;
SAUT 2 'LIGNES' ;







******************************
*                            *
*   L  I  S  T  M  O  T  S   *
*                            *
******************************



* OPTIONS SIMPLES
* ===============

LIST LMOT1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1) ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'NOCA') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'UNIQ') ;
SAUT 2 'LIGNES' ;


* OPTIONS COUPLEES
* ================

LIST LMOT1 ;
SAUT 2 'LIGNES' ;

TESTER (ORDO 'UNIQ' LMOT1 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'NOCA' 'UNIQ' 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'NOCA' 'DECR') ;
SAUT 2 'LIGNES' ;

TESTER (ORDO LMOT1 'NOCA' 'UNIQ') ;
SAUT 2 'LIGNES' ;



* TRI MULTIPLE
* ============

LIST LMOT1 ; LIST LREE1 ; LIST LENT1 ;
SAUT 2 'LIGNES' ;

A B = ORDO LMOT1 LREE1 'NOCA' 'DECR' ;
TESTER A ; TESTER B ;
SAUT 2 'LIGNES' ;

A B C = ORDO LMOT1 LREE1 'CROI' LENT1 ;
TESTER A ; TESTER B ; TESTER C ;
SAUT 2 'LIGNES' ;





*****************
*               *
*   T A B L E   *
*               *
*****************


* AVEC UNE SEULE LISTE
* ====================

TAB1 = TABL ;
TAB1 . 56 = LENT1 ;
TESTER (TAB1 . 56) ;
SAUT 2 'LIGNES' ;

TAB2 = ORDO TAB1 56 ;
TESTER (TAB2 . 56) ;
SAUT 2 'LIGNES' ;

TAB2 = ORDO  TAB1 56 'ABSO' ;
TESTER (TAB2 . 56) ;
SAUT 2 'LIGNES' ;

TAB2 = ORDO 'DECR' TAB1 56 'UNIQ' ;
TESTER (TAB2 . 56) ;
SAUT 2 'LIGNES' ;

TAB2 = ORDO 'ABSO' 'UNIQ' TAB1 56 ;
TESTER (TAB2 . 56) ;
SAUT 2 'LIGNES' ;

TAB2 = ORDO 'ABSO' 'UNIQ' TAB1 56 'DECR' ;
TESTER (TAB2 . 56) ;
SAUT 2 'LIGNES' ;


* AVEC PLUSIEURS LISTES
* =====================

TAB10 = TABL ;
TAB10 . 56     = LENT1 ;
TAB10 . 3.14   = LREE1 ;
TAB10 . 'TOTO' = LMOT1 ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;

TAB20 = ORDO TAB10 3.14 ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;

TAB20 = ORDO 'DECR' TAB10 56 ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;

TAB20 = ORDO 'DECR' 'ABSO' TAB10 3.14 ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;

TAB20 = ORDO 'NOCA' 'DECR' TAB10 'TOTO' ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;

TAB20 = ORDO 'NOCA' TAB10 'TOTO' ;
TESTER (TAB10 . 56) ; TESTER (TAB10 . 3.14) ; TESTER (TAB10 . 'TOTO') ;
SAUT 2 'LIGNES' ;





***************************
*                         *
*   V A L I D A T I O N   *
*                         *
***************************
SI (NON (VALIDER)) ;
    ERRE 5 ;
FINS ;


FIN ;












