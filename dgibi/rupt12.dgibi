* fichier :  rupt12.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
OPTION ECHO 1 ;
 complet = faux;
* complet = vrai;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
***************************************************
**************************************************
*                                                I
*    VALIDATION DE LA PROCEDURE                  I
*        G_THETA EN DYNAMIQUE.                   I
*                                                I
*     PLAQUE EN TRACTION PURE AVEC               I
*          CHARGEMENT 'f(t)'                     I
*                                                I
*----------------------------------------------- I
* GEOMETRIE :                                    I
*   Longueur de la plaque   :  40  mm            I
*   Largeur de la plaque    :  104  mm           I
*   Longueur de la fissure  :  24 mm             I
*                                                I
* MATERIAU :                                     I
*   Module d'Young          : 7.561 daN mm^(-2)  I
*   Coefficient de poisson  : 0,286              I
*   Masse volumique         : 2.45E-6 kg/mm^(-3) I
*                                                I
**************************************************
OPTI echo 1 ;
OPTION DIME 2 ELEM TRI6 MODE PLAN DEFORMATION ;
graph='N';
*---------------------------------------*
* CONSTRUCTION DU MAILLAGE (RAYONANT)   *
*---------------------------------------*
 A =  0. 0.    ;  B = 52. 0.   ;
 C = 52. 40.   ;  D = 0. 40.   ;
 P1 = 0. 20.   ;  P2 = 0. 20.  ;
 P3 = 12.  20. ;  P5 = 52. 20. ;
 P4 = 6.  20. ;
*
SS COUR1 l1 l2 l3 l4 LVD LVG = @rayo P3 P4 6 ;
  P4 = L1 POINT INITIAL ;
  P6 = L4 POIN 'INITIAL' ;
  P7 = L3 POIN 'INITIAL' ;
  P7 = L2 POIN 'INITIAL' ;
*1ER CONTOUR
  AB    = DROI  A  B  'DINI' 5. 'DFINI' 5. ;
  BP5   = DROI  B  P5 'DINI' 5. 'DFINI' 2. ;
  P5P7  = DROI  P5 P7 'DINI' 2. 'DFINI' 1. ;
  P4P2  = DROI  P4 P2 'DINI' 2. 'DFINI' 1. ;
  P2A   = DROI  P2 A  'DINI' 1. 'DFINI' 5. ;
*2EM CONTOUR
  P1D   = DROI  P1 D  'DINI' 1. 'DFINI' 5. ;
  DC    = DROI  D  C  'DINI' 5. 'DFINI' 5. ;
  CP5   = DROI  C  P5 'DINI' 5. 'DFINI' 2. ;
  P6P1  = DROI  P6 P1 'DINI' 1. 'DFINI' 1. ;
*-----------------------------------------------
  CT2 = P4P2 ET P2A ET AB ET BP5 ET P5P7 ET L2 ET L1 ;
  CT3 = P6P1 ET  P1D ET DC ET CP5 ET P5P7 ET L3 ET L4 ;
  MAIL1 = SURF CT2 PLANE ;
  MAIL2 = SURF CT3 PLANE ;
  MAIL = MAIL1 ET MAIL2 ET SS ;
SI ( ega graph 'O');
  TRAC MAIL ;
finsi;
*      LEVRES SUP ET INF DE LA FISSURE
   LSUP =  LVD ;
   LINF =  LVG ;
*---------------------------------------*
* CONSTRUCTION DU MODEL
*---------------------------------------*
     YOU1 = 7.561E7;
     NU1  = 0.286 ;
     RH1  = 2.45E-6;
MO=MODE MAIL MECANIQUE ELASTIQUE ISOTROPE;
MAT = MATE MO YOUN YOU1 NU NU1 RHO RH1;
*---------------------------------------*
* RIGIDITE
*---------------------------------------*
     RIG1 = RIGIDITE MO MAT;
*---------------------------------------*
* CONDITIONS AUX LIMITES ;
*---------------------------------------*
  cl1 = bloque UX P1D ;
  cl2 = bloque UX P2A ;
  cl4 = bloque UX CP5 ;
  cl5 = bloque UX BP5 ;
  CL3 = BLOQUE UY  P5 ;
  CLT = CL1 ET CL2 ET CL3 ET CL4 ET CL5 ;
*---------------------------------------*
*                                       *
*    Procedure PASAPAS DYNAMIQUE        *
*                                       *
*---------------------------------------*
* Matrices de rigidite et de masse
RIGT = RIG1 ET CLT;
MAST = MASS MO MAT ;
*---------------------------------------*
*     Instant de calcul
*
  TI = 0. ;
si complet;
 TF = 20E-6; NP1 = 40 ;
sinon;
tf = ( 20e-6 / 40 ) * 6 ; np1= 6;
 finsi;
  DT  = (TF - TI ) / NP1 ;
*---------------------------------------*
  TITRE ' CHARGEMENT : IMPACT DE 0.4E3 ';
*---------------------------------------*
   F0 =  0.4E6  ;
L_TEMPS  = PROG 0.0 'PAS' DT 'NPAS' (NP1+1);
L_TEMP2  = PROG 0.0 'PAS' DT 'NPAS' NP1 ;
F_TEMPS  = PROG  ( NP1 + 2 )*F0 ;
*
EVO_FOR = EVOL MANU 'temps' L_TEMPS 'f(t)' F_TEMPS ;
 CH1_FORC = PRES 'MASS' MO -1. AB ;
 CH3_FORC = PRES 'MASS' MO -1. DC ;
 CHP_FORC = CH1_FORC ET CH3_FORC ;
 CHARG_T  = CHARG 'MECA' CHP_FORC  EVO_FOR;
*
  FR_COUP = 0.25 /DT  ;
  TAB_DYN = TABLE ;
  TAB_DYN.DYNAMIQUE = VRAI;
  TAB_DYN.MODELE = MO;
  TAB_DYN.CARACTERISTIQUES = MAT;
  TAB_DYN.BLOCAGES_MECANIQUES = CLT;
  TAB_DYN.CHARGEMENT = CHARG_T;
  TAB_DYN.TEMPS_CALCULES = L_TEMP2;
  PASAPAS TAB_DYN;

**************** tracés  *******************
SI ( ega graph 'O');
  trac (vect CHP_FORC 'FORC' 'BLEU') MAIL;
  dess EVO_FOR;
  isosig2 = prog 0. PAS 0.25E6 5E6;
  ntemp2 = dime L_TEMP2;
  itemp2 = -1;
  repe BDEFO ntemp2;
    itemp2 = itemp2 + 1;
    temp2 = extr L_TEMP2 (itemp2 + 1);
    u2t = TAB_DYN . DEPLACEMENTS . itemp2;
    sig2t = EXCO 'SMYY' (TAB_DYN . CONTRAINTES . itemp2);
    trac sig2t MO (DEFO u2t MAIL 10) isosig2
        'TITR' (chai 'u(t=' temp2 's') 'NCLK';
    xpause = 0.;
    repe bpause 1000;
       xpause = xpause + 1.;
    fin bpause;
  fin BDEFO;
finsi;

*opti donn 5 ;
*=======================================*
*
* CALCUL DE G PAR LA METHODE G_THETA
*
*=======================================*
   SUPTAB = TABL ;
   SUPTAB.'OBJECTIF' = MOT 'J_DYNA';
   SUPTAB.'LEVRE_SUPERIEURE' = LSUP;
   SUPTAB.'LEVRE_INFERIEURE' = LINF;
   SUPTAB.'FRONT_FISSURE' = P3 ;
   SUPTAB.'SOLUTION_PASAPAS' = TAB_DYN;
   SUPTAB.'COUCHE' = 3;
*----------------------------------*
* Appel DE la procedure G_THETA
*----------------------------------*
   G_THETA SUPTAB;
*------------------------------------*
* Evolution de G en fonction du temps.
* -----------------------------------*
SI ( ega graph 'O');
   DESS SUPTAB . 'EVOLUTION_RESULTATS' ;
finsi;

*------------------------------*
*     Calcul de K
*------------------------------*
ab1 = EXTR (SUPTAB.EVOLUTION_RESULTATS) ABSC;
ORD1 = EXTR (SUPTAB.EVOLUTION_RESULTATS) ORDO;
ORD2 = ((ORD1 * YOU1) /(1 - (0.286**2)))**0.5 ;
K1 = EVOL MANU 'TEMPS' AB1 'K1(t)' ORD2;
SI ( ega graph 'O');
  DESS K1 ;
finsi;


**************************************
****  SOLUTION ANALYTIQUE ************
**************************************
*anciennes valeurs de reference (formulation Attigui)
*li2 = prog 82275 2701582 4420771  5525946 ;
*nouvelles valeurs de reference (formulation B Prabel)
KREF = prog 74056. 3.12957E+06  4.34363E+06  5.64263E+06;

***RECUPERATION DU RESULTAT ET COMPARAISONS***
ERR1MAX = prog;
si complet; nfois = 4;
sinon; nfois = 1;
finsi;
I = 1;
REPETER BOU1 nfois;
  I = I + 5;
  J = &BOU1;
  K1REF = extr KREF J ;
  K1CAL = extr ord2 I;
  ERR1 = abs ((K1CAL - K1REF)/K1REF);
  MESS (CHAI 'I=' I ' K1REF=' KEXP ' K1CAL='K_CAL
             '  Erreur relative =' ERR1);
  ERR1MAX = ERR1MAX et  ERR1;
FIN BOU1;
*LIST ERR1MAX;

ERR1 = maxi ERR1MAX;
*SI (ERR1 <  5.E-2);
SI (ERR1 <  1.E-2);
      ERRE 0;
SINO;
      ERRE 5;
FINSI;
FIN;


















