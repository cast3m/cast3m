* fichier :  plexus1.dgibi
************************************************************************
* Section : Langage Objets
* Section : Maillage Autres
* Section : Mecanique Elastique
************************************************************************
* CAS TEST DU 92/01/16    PROVENANCE : PLA2

*******************************************
*  Test plexus1.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

*******************************************
*                PLEXUS1                  *
*                                         *
*       CAS TEST DE L INTERFACE           *
*        CASTEM 2000 - PLEXUS             *
*                                         *
* Cet exemple permet de tester la         *
*  relecture de la bande CAST3M cree par  *
*  PLEXUS                                 *
*******************************************
OPTI REST FORMAT
      '../divers/plexus1.couplage' ;

TITRE 'TEST NTERFACE CAST3M - PLEXUS';

*-Restitution de la bande cree par PLEXUS
TEMPS ;
REST FORMAT ;



SI (NEG GRAPH 'N');
   TITR 'MAILLAGE TOUT DANS PLEXUS1.DGIBI';
   TRAC 'QUAL' TOUT  ;
FINSI;

*    TESTS DES CHPO CREES PAR PLEXUS
*    ===============================
ND5  = NOEU 5  ;
ND11 = NOEU 11 ;
*
* creation d un chpo de deplacements et
* de vitesses ( 2 et 3eme sauvegarde)

DEPl2 = TABPLEX . 2 . DEPL ;
VITE3 = TABPLEX . 3 . VITE ;

SI (NEG GRAPH 'N');
   TITR 'CHAMP DE DEPLACEMENT au pas 2' ;
   TRAC DEPl2 TOUT  ;
   TITR 'CHAMP DE VITESSE au pas 3' ;
   TRAC VITE3 TOUT  ;
FINSI;

* extraction du deplacement suivant r pour
* le noeud no 5 et de la vitesse suivant z
* du noeud 11

FD5R  = EXTR DEPL2 ND5  UR ;
FV11Z = EXTR VITE3 ND11 UZ ;

*    TESTS DES MCHAML CREES PAR PLEXUS
*    =================================

CONT3 = TABPLEX . 3 . CONT ;
EPST2 = TABPLEX . 2 . EPTS ;

* on extrait du champ de contrainte et du
* champ des defo. totales la composante S_2
*(resp. D_1) pour laquelle on change le nom
* de la composante.

CHN22 = EXCO S_2 CONT3 N22 ;
CHD11 = EXCO D_1 EPST2 D11 ;

* on cree un objet de type MODE , un objet
* de type MATE associes a la 1 ere zone du
* maillage ZONE_1

MODL1=MODE ZONE_1 MECANIQUE ELASTIQUE COQ2;
CAR1 =MATE MODL1 'YOUN' 200.E3 'NU' 0.
                 'EPAI' 2.    'RHO' 7.8E-9;

* on reduit le nouveau chamelem sur la
* premiere zone du maillage ZONE_1

CHN22R=REDU CHN22 ZONE_1;

* on extrait la composante N22 du 1er
* sous-paquet du 1er element pour le 1er
* point de gauss (dans notre cas le 1er
* noeud : mchaml au noeuds en sortie de
* PLEXUS)

N22_1 = EXTR CHN22R N22 1 1 1 ;
TEMPS ;
VAL1 = 1.5492E-5 ;
VAL2 = 335.39 ;
VAL3 = 2.2031 ;
RESI1 = 100*(ABS((FD5R  - VAL1 )/ VAL1 ));
RESI2 = 100*(ABS((FV11Z - VAL2 )/ VAL2 ));
RESI3 = 100*(ABS((N22_1 - VAL3 )/ VAL3 ));

MESS 'deplacement theorique :' VAL1 'mm';
MESS 'deplacement calculee  :' FD5R 'mm';
MESS '    Soit un ecart de : ' RESI1 '%';
SAUTER 1 LIGNES ;

MESS 'vitesse theorique:' VAL2    'mm/s';
MESS 'vitesse calculee :' FV11Z    'm/s';
MESS '    Soit un ecart de : ' RESI2 '%';
SAUTER 1 LIGNES ;

MESS 'contrainte theorique:' VAL3  'MPa';
MESS 'contrainte calculee :' N22_1 'MPa';
MESS '    Soit un ecart de : ' RESI3 '%';
SAUTER 1 LIGNES ;


*           CODE FONCTIONNEMENT
*           -------------------

SI((RESI1 <EG 1E-2) ET (RESI2 <EG 1E-2) ET
   (RESI3 <EG 1E-2));
  ERRE  0;
SINO;
  ERRE  5;
FINSI;
FIN;




