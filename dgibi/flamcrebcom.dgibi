* fichier :  flamcrebcom.dgibi
************************************************************************
* Section : Chimie Combustion
************************************************************************
**********************************************************************
**** OPERATEUR FLAM                                               ****
**** Critere CREBCOM                                              ****
****                                                              ****
**** A. BECCANTINI DEN/DM2S/SFME/LTMF        JUIN 2001            ****
**********************************************************************

 'OPTION'  'DIME' 2 ;
 'OPTION'  'ELEM' QUA4 ;
 'OPTION'  'TRAC' 'X' ;
 'OPTION'  'ECHO'  0  ;


 GRAPH = FAUX ;


 LIG1 = (0.0 0.0) 'DROIT' 10 (0.0 1.0) ;
 DOM1 = 'TRANSLATION' LIG1 10 (1.0 0.0) ;
 DOM2 = 'TRANSLATION' LIG1 10 (-1.0 0.0) ;
 DOMTOT = DOM1 'ET' DOM2 ;

 $DOMTOT = 'MODE' DOMTOT 'EULER'  ;
 $DOM1   = 'MODE' DOM1   'EULER'  ;
 $DOM2   = 'MODE' DOM2   'EULER'  ;

 TDOMTOT = 'DOMA' $DOMTOT 'VF'  ;
 TDOM1   = 'DOMA' $DOM1   'VF'  ;
 TDOM2   = 'DOMA' $DOM2   'VF'  ;

 MDOMTOT = TDOMTOT . 'QUAF';
 MDOM1   = TDOM1   . 'QUAF';
 MDOM2   = TDOM2   . 'QUAF';
'ELIM' (MDOMTOT 'ET' MDOM1 'ET' MDOM2) 1D-5;

 EPSILON = 1.0D-2 ;
 CSIMAX  = 9.0D-1 ;


*
**** CSIMAX: maximum value of CSI
*
*    CSIN = CSIMAX if x   <  0
*           0.0   x  =>  0
*
*


 CSIN = ('MANUEL' 'CHPO' ('DOMA' $DOM2 'CENTRE') 1 'H2O2'
         (1.0001 '*' CSIMAX) 'NATURE' 'DISCRET')
        'ET'
        ('MANUEL' 'CHPO' ('DOMA' $DOM1 'CENTRE') 1 'H2O2' 0.0
         'NATURE' 'DISCRET') ;


 MOD1  =  'MODELISER'  ('DOMA' $DOMTOT 'MAILLAGE') 'THERMIQUE' ;


 'SI' GRAPH ;
    CHM_CSI  =  'KCHA' $DOMTOT 'CHAM' CSIN ;
    'TRAC' CHM_CSI  MOD1 'TITR' ('CHAINE' 'csi');
 'FINSI' ;


 CHP1 = 'FLAM' 'CREBCOM' $DOMTOT EPSILON CSIMAX CSIN ;


*
**** CHP1 =  1.0 in x = 0.05
*            0   ailleurs
*

 'SI' GRAPH ;
    CHM_C1   =  'KCHA' $DOMTOT 'CHAM' CHP1 ;
    'TRAC' CHM_C1   MOD1 'TITR' ('CHAINE' 'C1');
 'FINSI' ;

 GEO1 = ('COORDONNEE' 1 ('DOMA' $DOMTOT 'CENTRE')) 'POIN' 'COMPRIS'
   0.049  0.051 ;
 CHPTEST = 'MANUEL' 'CHPO' GEO1 1 'SCAL' 1.0 ;

 ERRO = 'MAXIMUM' (CHP1 '-' CHPTEST) 'ABS' ;

 'SI' (ERRO > 1.0D-6) ;
     'ERREUR' 5 ;
 'FINSI' ;

 'FIN' ;





*




