* fichier : fabbadd1.dgibi
* Section : Thermique Conduction 

OPTI ECHO 0 ;
OPTI DIME 3 ;
OPTI ELEM 'CUB8' ;

GRAPH = FAUX ; COMM 'Affichage graphiques' ; 
XINFO = FAUX ; COMM 'Affichage infos' ;

SI GRAPH ;
OPTI TRAC 'X' ;
SINO ;
OPTI TRAC 'PSC' ;
FINS;

************************************************************************
* Debut du jeu de donnees fabbrication additive SLM 
*
*  OPTIONS GENERALES
*  - 3D
*
* THERMIQUE
*  - Source de chaleur volumique (Goldak) dépendant du temps (CHARTHER)
*    avec trajectoire personnalisee (ligne polygonale)
*  - Conduction dans la matière
*  - Convection les bords
*  - Proprietes matériaux qui dépendent de T
*  - Trois matériaux : poudre, piece et substrat
*  - Le "métal poudre" et le "métal piece" fondent à T > Tliq
*  - Les elements sont ajoutés couche par couche
*  - Changement modèle "poudre" > modele "piece" 
*    dès qu'ils ont fondus une 1ère fois (PERSO2)
*
* Auteur : H. POMMIER harry.pommier@cea.fr
* Historique version :
* V0 version initiale 20/06/2018, Cast3M 2018
************************************************************************

****PROCEDURES

'DEBP' DUREE  X*'LISTREELS' Y*'LISTREELS' VIT*'FLOTTANT' ;
****************************************************************
* PROCEDURE DUREE : Calcul la duree du lasage d'une couche en 
*                   fonction de la vitesse et de la trajectoire
* Entrees  
*X      liste de coordonnees X en [m]
*Y      liste de coordonnees Y en [m]
*VIT    vitesse du procede en [m/s]
*
* Sortie 
*TPSLASA temps total de lasage pour parcourir 
*        la trajectoire a la vitesse VIT
*****************************************************************

TPSLASA = 0. ;
REPE BOUC0 ((DIME X) - 1);
I = &BOUC0 ;

X0 = EXTR X I ;
X1 = EXTR X (I+1) ;
Y0 = EXTR Y I ;
Y1 = EXTR Y (I+1) ;

LLOC = '**'('+'('**'(X1 - X0) 2) ('**' (Y1 - Y0) 2)) 0.5 ;
TPSLASA = '+' TPSLASA ('/' LLOC VIT) ;
FIN BOUC0 ;

FINP TPSLASA ;

*******************************************
* PROCEDURE CHARTHER

DEBP charther FAPASAP*'TABLE' tt*'FLOTTANT' ;
TAA = TABLE ; ETAB = PRECED.'WTABLE' ;

SI XINFO ;
MESS 'Debut procedure CHARTHER' ;
FINS ;

ncou = FAPASAP.'MES_DATA'.'COUCHE' ;
tpsfin = FAPASAP.'MES_DATA'.'TPSFIN' ;
tpslasa = FA.'PROCEDE'.'TEMPS_LASAGE' ;
ttloc = '-' tt ('*' ('-' ncou 1) tpsfin) ;
mesmod = FAPASAP.'MODELE' ;
modloc = EXTR mesmod 'CONS' 'COND' ;

**CHARGEMENT
SI (ttloc '<EG' tpslasa) ; 

*Parametres
progx = FA.'PROCEDE'.'TRAJECTOIRE'.'PROGX' ;
progy = FA.'PROCEDE'.'TRAJECTOIRE'.'PROGY' ;
vit = FA.'PROCEDE'.'VITESSE' ;
tpsintc = FA.'PROCEDE'.'TEMPS_INTERCOUCHE' ;
epcou = FA.'PROCEDE'.'EPAISSEUR_COU' ;

*Position source
posloc vecloc pavan = POSOU
ttloc progx progy vit ncou epcou ;
theta = ACOS (COOR 1 vecloc) ;
SI ((COOR 2 vecloc) '<' 0.) ;
theta = '*' -1. theta ; 
FINS;

SI XINFO ;
MESS 'Couche : ' ncou '/' NBCOU ' Avancement lasage de la couche :' pavan '%' ;
vx vy vz = COOR vecloc ;
MESS 'Angle source :' theta 'Direction (x,y) :' '('vx','vy')' ;
dx dy dz = COOR posloc ;
MESS 'Position source (x,y,z) :' '('dx','dy','dz')' ;
FINS ;

*Para source
Q = FA.'PROCEDE'.'SOURCE'.'Q' ; 
A = FA.'PROCEDE'.'SOURCE'.'A' ;
B = FA.'PROCEDE'.'SOURCE'.'B' ;
Cf = FA.'PROCEDE'.'SOURCE'.'Cf' ;
Cr = FA.'PROCEDE'.'SOURCE'.'Cr' ;
eta = FA.'PROCEDE'.'SOURCE'.'ETA' ;
Qeff = '*' Q eta ;
psour = PROG Qeff A B Cf Cr ;
pvol = SOUGOLDH modloc psour posloc theta ttloc tpslasa ;
charso = SOUR modloc pvol ;

*Correction puissance
SI (ttloc '<EG' tpslasa);
Qeff = maxi (resu charso) ;
SI XINFO ;
MESS (CHAINE 'Puissance source initiale' Qeff ' W') ;
FINS ;
para1 = EXTR Psour 1 ;
para1 = '*' para1 ('/' para1 Qeff) ;
para2 = EXTR Psour 2 ;
para3 = EXTR Psour 3 ;
para4 = EXTR Psour 4 ;
para5 = EXTR Psour 5 ;
Psour = PROG para1 para2 para3 para4 para5 ;
pvol = SOUGOLDH modloc psour posloc theta ttloc tpslasa ;
charso = SOUR modloc pvol ;
Qcor = maxi (resu charso) ;
SI XINFO ;
MESS (CHAINE 'Puissance source corrigee' Qcor ' W') ;
FINS ;
SINO ;
charso = '*' 0. charso ;
FINS ;

SINO ;
prefroi = '/' ('-' ttloc tpslasa) FA.'PROCEDE'.'TEMPS_INTERCOUCHE' ;
prefroi = '*' prefroi 100. ; COMM 'Avancement temps intercouche' ;
MESS 'Couche : ' ncou '/' NBCOU ' Temporisation intercouche :' prefroi '%' ;
pvol = MANU 'CHPO' (EXTR modloc 'MAILLAGE') 1 'SCAL' 0. 'NATU' 'DIFFUS' ;
charso = SOUR modloc pvol ;
FINS ;

TAA.'ADDI_SECOND' = charso ;

SI XINFO ;
MESS 'Fin procedure CHARTHER' ;
MESS 'Calcul thermique...' ;
FINS ;
FINP TAA ;

************************************************************************
* PROCEDURE POSOU : Calcul la position de la source et le vecteur directeur
* en fonction du temps, de la trajection (progx, progy), de la vitesse,
* du numero de la couche actuelle et de l epaisseur d une couche
************************************************************************

DEBP POSOU ;

ARGU TT*'FLOTTANT' ;
ARGU PROGX*'LISTREEL' ;
ARGU PROGY*'LISTREEL' ;
ARGU VIT*'FLOTTANT' ;
ARGU NCOU*'ENTIER' ;
ARGU EPCOU*'FLOTTANT' ;


TABDT = TABLE ; COMM 'Table des duree des segments' ;
TABDTCUM = TABLE ; COMM 'Table des duree cumulees des segments' ;
TABDL = TABLE ; COMM 'Table des longueurs des segments' ;
TABDLCUM = TABLE ; COMM 'Table des longueurs cumulees des segments' ;
TABDTCUM. 0 = 0. ;
TABDLCUM. 0 = 0. ;
flag = VRAI ;

NBOUC0 = '-' (DIME PROGX) 1 ;
REPE BOUC0 NBOUC0 ;
I = &BOUC0 ;
X0 = EXTR PROGX I ;
X1 = EXTR PROGX (I+1) ;
Y0 = EXTR PROGY I ;
Y1 = EXTR PROGY (I+1) ;
TABDL.I = '**'('+'('**'(X1 - X0) 2) ('**' (Y1 - Y0) 2)) 0.5 ;
TABDLCUM.I = TABDL. I + TABDLCUM.(I-1) ;
TABDT.I = ('/' TABDL.I VIT) ;
TABDTCUM.I = TABDT. I + TABDTCUM.(I-1) ;
SI ((TT '<EG' TABDTCUM.I) ET flag) ;
K = I ; COMM 'Le laser se trouve sur le segment K du trajet' ;
X0 = EXTR PROGX K ;
X1 = EXTR PROGX (K+1) ;
Y0 = EXTR PROGY K ;
Y1 = EXTR PROGY (K+1) ;
tt2 = '-' tt TABDTCUM.(K-1) ;
ratio = '/' tt2 TABDT.K ;
vecseg = (X1 - X0) (Y1 - Y0) 0. ;
vec0 = '/' ((X1 - X0) (Y1 - Y0) 0.) TABDL.K ;
x= '+' X0 (COOR ('*' vecseg ratio) 1) ;
y= '+' Y0 (COOR ('*' vecseg ratio) 2) ;
z = '*' ncou epcou ;
pos0 = x y z ;
flag = FAUX ;
FINS ;
FIN BOUC0 ;

LLASA = TABDLCUM.NBOUC0 ; COMM 'Longueur totale du lasage' ;
TPSLASA = TABDTCUM.NBOUC0 ; COMM 'Duree totale du lasage' ;

*Pourcentage du trajet total realise
SI (tt '<' TPSLASA) ; 
pavan = '*' ('/' tt TPSLASA) 100 ; 
SINO ;
pavan = 100. ;
FINS ;

FINP pos0 vec0 pavan ;

*****************************************************
* PROCEDURE SOUGOLDH 

*Auteur : H. POMMIER harry.pommier@cea.fr
*Historique version :
*Modification pour PASAPAS H. POMMIER 06-2018
*Description : source equivalente Goldak adaptee pour PASAPAS 
*Entrées :
*mo_sou Modele
*p_sour Liste des parametres de la source
*pos_sou Position de la source (x,y,z)
*theta angle de rotation autour de axe Z
*ttloc ttloc du calcul EF
*tpslasa ttloc de lasage
*(x : direction soudage, z : normale piece)
*Sortie :
*CHPO_SOM Champs Points de puissance volumique

DEBP SOUGOLDH mo_sou*'MMODEL' p_sour*'LISTREEL' pos_sou*'POINT'
theta*'FLOTTANT' ttloc*'FLOTTANT' tpslasa*'FLOTTANT';

xglo yglo zglo = COOR (EXTR mo_sou 'MAILLAGE') ;
*** repere local(xs,ys,zs)
xsou ysou zsou = COOR pos_sou ;
xxloc = xglo - xsou ;
yyloc = yglo - ysou ;
zzloc = zglo - zsou ;

*** Rotation de theta1 autour de l'axe des Y
*** puis rotation de theta2 autour de l'axe du nouvel axe des Z
cos0 = COS theta ;
sin0 = SIN theta ;

xloc = (xxloc*cos0) + (yyloc*sin0) ;
yloc = (yyloc*cos0) - (xxloc*sin0) ;
zloc = zzloc ;

Q =  EXTR p_sour 1 ;
a =  EXTR p_sour 2 ;
b =  EXTR p_sour 3 ;
Cf = EXTR p_sour 4 ;
Cr = EXTR p_sour 5 ;

*** * contribution de la source en y et z
cy = EXP ( (-3./(a**2.)) * (yloc * yloc) ) ;
cz = EXP ( (-3./(b**2.)) * (zloc * zloc) ) ;

*** * on calcule la source en avant du front
fil_av = xloc MASQ egsupe (-1.e-10) ;
fil_ar = fil_av MASQ inferieur (1.e-6) ;
Rf = 2. / (1. + ( Cr / Cf ) ) ;
Rr = 2. - Rf ;
cx_av = EXP ( ( -3. / ( Cf ** 2. ) ) * (xloc * xloc) ) ;
*cx_av = '*' cx_av (Rf / Cf) ;
cx_av = fil_av * cx_av ;
cx_ar = EXP ( ( -3. / ( Cr ** 2. ) ) * (xloc * xloc) ) ;
*cx_ar = '*' cx_ar ('/' Rr Cr) ;
cx_ar = fil_ar * cx_ar ;
cx = cx_av + cx_ar ;
Pi1 = (3.14159265358979**1.5) ;
qmax = (6 * (3.**0.5) * Rf * Q) / (Pi1 * Cf * a * b) ;
*qmax = (6 * (3.**0.5) * Q) / (Pi1 * a * b) ;
fil_z = zloc MASQ 'EGINFE'  ( 0. ) ;
cz = cz * fil_z ;
CHPO_SOM = qmax * cx * cy * cz  ;

SI (ttloc '>' tpslasa);
CHPO_SOM = '*' CHPO_SOM  0. ;
FINS ;

FINP CHPO_SOM ;


************************************************************************
* PROCEDURE PERSO2 : Apres la THERMIQUE dans TRANSNON
* MAJ le MODELE et le MATERIAU THERMIQUE : POUDRE -> PIECE
* ATTENTION : La temperature a jour est dans l'indice WTAB.'THER_COURANT'
* harry.pommier@cea.fr 29/06/2018
************************************************************************
DEBP PERSO2 TAB1*'TABLE' ;

SI XINFO ;
MESS 'Debut procedure PERSO2' ;
FINS ;

NCOU = TAB1.'MES_DATA'.'COUCHE' ; 

tfusion = TAB1.'MES_DATA'.'T_LIQUIDUS' ;
WTAB = TAB1.'WTABLE' ;

NB_PAS = (DIME TAB1.'MES_MODEL') ;
NB_PAS2 = (DIME TAB1.'TEMPS') ;

MODTHER = WTAB.'MO_TOTAL' ;
MODCOND= EXTR MODTHER 'COMP' 'CONDUCTION' ;
MAILTHER = EXTR MODTHER 'MAILLAGE' ;
MAILCOND = EXTR MODCOND 'MAILLAGE' ;
MATTOT  = TAB1.'CARACTERISTIQUES' ;

*Recuperation des elements liquides dans la poudre
CHATEMP = CHAN 'CHAM' WTAB.'THER_COURANT' MODCOND 'TEMPERATURES' ;
ELEMSUP = ELEM CHATEMP 'SUPE' tfusion 'STRICTEMENT' ;

  
**********
* Transformation du MODELE POUDRE en PIECE
**********

MAILFON = TAB1.'MAILLAGE_FONDU'.(NB_PAS - 1) ;
MAILPOUI = DIFF MAILFON FA.'MAILLAGE'.'POUDRE_INI' ;
ELNEW = INTE ELEMSUP MAILPOUI ;


*MAJ modele
SI ((NBEL ELNEW) '>EG' 1) ;
TAB1.'MAILLAGE_FONDU'.NB_PAS = 
TAB1.'MAILLAGE_FONDU'.(NB_PAS-1) ET ELNEW ;
TAB1.'MAILLAGE_FONDU2'.NB_PAS2 = TAB1.'MAILLAGE_FONDU'.NB_PAS ;
MAILCOUC = FA.'MAILLAGE'.'COUCHE_CUM'.NCOU ;
MAILPOUI = DIFF TAB1.'MAILLAGE_FONDU'.NB_PAS MAILCOUC ;

MAILPOU = MAILPOUI ET 
(DIFF FA.'MAILLAGE'.'COUCHE_CUM'.NCOU
 FA.'MAILLAGE'.'POUDRE_INI') ;
FA.'MAILLAGE'.'POUDRE' = MAILPOU ;

MODTHEPO = REDU FA.'MODELE'.'POUDRE_INI' MAILPOUI ;
FA.'MODELE'.'POUDRE' = MODTHEPO ;
MODTHEPI = MODE MAILFON 'THERMIQUE' 'CONDUCTION' 'CONS' 'COND' ;
FA.'MODELE'.'PIECE' = MODTHEPI ;
MODTHESU =      FA.'MODELE'.'SUBSTRAT' ;
FA.'MODELE'.'TOTAL' = 
MODTHEPO ET MODTHEPI ET MODTHESU ET MODCONV ;

MATHEPO = REDU FA.'MATERIAUX'.'POUDRE_INI' MODTHEPO ;
EVK = FA.'MATERIAUX'.'PARA_PIECE'.'EVK' ;
EVRHO = FA.'MATERIAUX'.'PARA_PIECE'.'EVRHO' ;
EVCP = FA.'MATERIAUX'.'PARA_PIECE'.'EVCP' ;
MATHEPI = MATE MODTHEPI 'K' EVK 'RHO' EVRHO 'C' EVCP ;
MATHESU = FA.'MATERIAUX'.'SUBSTRAT' ;
FA.'MATERIAUX'.'POUDRE' = MATHEPO ;
FA.'MATERIAUX'.'PIECE' = MATHEPI ;
FA.'MATERIAUX'.'TOTAL' = 
MATHEPO ET MATHEPI ET MATHESU ET MATCONV ;

*Mise a jour de WTAB pour le PAS suivant
WTAB.'MOD_MEC' = FA.'MODELE'.'TOTAL' ;
WTAB.'MAT_MEC' = FA.'MATERIAUX'.'TOTAL' ;
WTAB.'MO_TOT'  = FA.'MODELE'.'TOTAL' ;
WTAB.'MA_TOT'  = FA.'MATERIAUX'.'TOTAL' ;

*Indice obligatoire a renseigner car PAS_ETAT travaille sur MO_TOTAL
WTAB.'MO_TOTAL' = FA.'MODELE'.'TOTAL' ;
WTAB.'MA_TOTAL' = FA.'MATERIAUX'.'TOTAL' ;

* Enregistrement des modeles
TAB1.'MES_MODEL'.NB_PAS = WTAB.'MO_TOTAL' ;
TAB1.'MES_MODEL2'.NB_PAS2 = TAB1.'MES_MODEL'.NB_PAS ;
TAB1.'MODELE' = WTAB.'MO_TOTAL' ;
TAB1.'CARACTERISTIQUES' = FA.'MATERIAUX'.'TOTAL' ;
SINO ;

*Recuperation depuis le PAS precedent
TAB1.'MES_MODEL'.NB_PAS = TAB1.'MES_MODEL'.(NB_PAS-1) ;
TAB1.'MAILLAGE_FONDU'.NB_PAS = TAB1.'MAILLAGE_FONDU'.(NB_PAS-1) ;
TAB1.'MES_MODEL2'.NB_PAS2 = TAB1.'MES_MODEL'.NB_PAS ;
TAB1.'MAILLAGE_FONDU2'.NB_PAS2 = TAB1.'MAILLAGE_FONDU'.NB_PAS ;
FINS;

SI XINFO ;
MESS 'Fin procedure PERSO2' ;
FINS ;

FINP TAB1;

***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
****MAILLAGE

*Maillage murs (carre) et plateau (substrat), fabrication addictive, procede SLM
*Auteur : H. POMMIER harry.pommier@cea.fr
*Historique version :
*V0 version initiale 18/06/2018, Cast3M 2018



POEIL1 = 0. 0. 10. ;
POEIL2 = 10. -10. 10. ;

**********
*PARAMETRES (on travaille en [m])
**********
*Dimensions
r0 = 1.e-3 ; COMM 'Rayon du plateau substrat en [m]' ;
a0 = 1.e-3 ; COMM 'Cote du carre en [m]' ;
a1 = '/' a0 2. ; COMM 'Demi cote du carre en [m]' ;
efin = 100.e-6 ; COMM 'Largeur de la zone de maillage fin (cordon de lasage) [m]' ;
egros = 50.e-6 ; COMM 'Largeur de la zone de transition (de part et d autre du lasage) [m]' ;
h0 = 0.12e-3 ; COMM 'Hauteur de la piece' ;
ep = 100.e-6 ; COMM 'Epaisseur du plateau' ;

*Taille elements
mfin = 50.e-6 ; COMM 'Taille elements fins, cordon de lasage' ;
mmoy = 100.e-6 ; COMM 'Taille elements transitions, voisinage cordon de lasage' ;
mgros = 500.e-6 ; COMM 'Taille elements grossiers, loin du cordon de lasage' ;
epcou = 50.e-6 ; COMM 'Epaisseur d une couche' ;

**********
*POINTS
**********
PO = 0. 0. 0. ; COMM 'Origine au centre du plateau' ;
sqrt22 = ('/' ('**' 2. 0.5) 2) ; COMM 'Racine de 2 sur 2' ;
sqrt22i = ('*' sqrt22 -1.) ; COMM 'Moins racine de 2 sur 2' ;
PD1 = ('*' r0 sqrt22) ('*' r0 sqrt22i) 0. ;
PD2 = ('*' r0 sqrt22) ('*' r0 sqrt22) 0. ;
PD3 = ('*' r0 sqrt22i) ('*' r0 sqrt22) 0. ;
PD4 = ('*' r0 sqrt22i) ('*' r0 sqrt22i) 0. ;
*Carre 1
aloc = '-' a1 ('+' ('/' efin 2.) egros) ; COMM 'demi largeur du carre courant' ;
aloco = '*' aloc -1. ; COMM 'oppose de aloc' ;
PC11 = aloc aloco 0. ;
PC12 = aloc aloc 0. ;
PC13 = aloco aloc 0. ; 
PC14 = aloco aloco 0. ;
*Carre 2
aloc = '-' a1 ('/' efin 2.) ; COMM 'demi largeur du carre courant' ;
aloco = '*' aloc -1. ; COMM 'oppose de aloc' ;
PC21 = aloc aloco 0. ;
PC22 = aloc aloc 0. ;
PC23 = aloco aloc 0. ; 
PC24 = aloco aloco 0. ;
*Carre 3
aloc = '+' a1 ('/' efin 2.) ; COMM 'demi largeur du carre courant' ;
aloco = '*' aloc -1. ; COMM 'oppose de aloc' ;
PC31 = aloc aloco 0. ;
PC32 = aloc aloc 0. ;
PC33 = aloco aloc 0. ; 
PC34 = aloco aloco 0. ;
*Carre 4
aloc = '+' a1 ('+' ('/' efin 2.) egros) ; COMM 'demi largeur du carre courant' ;
aloco = '*' aloc -1. ; COMM 'oppose de aloc' ;
PC41 = aloc aloco 0. ;
PC42 = aloc aloc 0. ;
PC43 = aloco aloc 0. ; 
PC44 = aloco aloco 0. ;



**********
*DROITES
**********
*Carre 1
D11 = DROI PC11 PC12 'DINI' mgros 'DFIN' mgros ;
D12 = DROI PC12 PC13 'DINI' mgros 'DFIN' mgros ;
D13 = DROI PC13 PC14 'DINI' mgros 'DFIN' mgros ;
D14 = DROI PC14 PC11 'DINI' mgros 'DFIN' mgros ;
*Carre 2
D21 = DROI PC21 PC22 'DINI' mfin 'DFIN' mfin ;
D22 = DROI PC22 PC23 'DINI' mfin 'DFIN' mfin ;
D23 = DROI PC23 PC24 'DINI' mfin 'DFIN' mfin ;
D24 = DROI PC24 PC21 'DINI' mfin 'DFIN' mfin ;
*Carre 3
D31 = DROI PC31 PC32 'DINI' mfin 'DFIN' mfin ;
D32 = DROI PC32 PC33 'DINI' mfin 'DFIN' mfin ;
D33 = DROI PC33 PC34 'DINI' mfin 'DFIN' mfin ;
D34 = DROI PC34 PC31 'DINI' mfin 'DFIN' mfin ;
*Carre 4
D41 = DROI PC41 PC42 'DINI' mgros 'DFIN' mgros ;
D42 = DROI PC42 PC43 'DINI' mgros 'DFIN' mgros ;
D43 = DROI PC43 PC44 'DINI' mgros 'DFIN' mgros ;
D44 = DROI PC44 PC41 'DINI' mgros 'DFIN' mgros ;
*Cercle exterieur
C12 = CERC 'DINI' mgros 'DFIN' mgros 'CENTR' PD1 PO PD2 ;
C23 = CERC 'DINI' mgros 'DFIN' mgros 'CENTR' PD2 PO PD3 ;
C34 = CERC 'DINI' mgros 'DFIN' mgros 'CENTR' PD3 PO PD4 ;
C41 = CERC 'DINI' mgros 'DFIN' mgros 'CENTR' PD4 PO PD1 ;

*Diagonales
D01 = DROI PO PC11 'DINI' mgros 'DFIN' mmoy ;
D02 = DROI PO PC12 'DINI' mgros 'DFIN' mmoy ;
D03 = DROI PO PC13 'DINI' mgros 'DFIN' mmoy ;
D04 = DROI PO PC14 'DINI' mgros 'DFIN' mmoy ;
*Connexion carre 1 carre 2
D121 = DROI PC11 PC21 'DINI' mmoy 'DFIN' mfin ;
D122 = DROI PC12 PC22 'DINI' mmoy 'DFIN' mfin ;
D123 = DROI PC13 PC23 'DINI' mmoy 'DFIN' mfin ;
D124 = DROI PC14 PC24 'DINI' mmoy 'DFIN' mfin ;
*Connexion carre 2 carre 3
D231 = DROI PC21 PC31 'DINI' mfin 'DFIN' mfin ;
D232 = DROI PC22 PC32 'DINI' mfin 'DFIN' mfin ;
D233 = DROI PC23 PC33 'DINI' mfin 'DFIN' mfin ;
D234 = DROI PC24 PC34 'DINI' mfin 'DFIN' mfin ;
*Connexion carre 3 carre 4
D341 = DROI PC31 PC41 'DINI' mfin 'DFIN' mmoy ;
D342 = DROI PC32 PC42 'DINI' mfin 'DFIN' mmoy ;
D343 = DROI PC33 PC43 'DINI' mfin 'DFIN' mmoy ;
D344 = DROI PC34 PC44 'DINI' mfin 'DFIN' mmoy ;
*Connexion carre 4 cercle exterieur
D401 = DROI PC41 PD1 'DINI' mmoy 'DFIN' mgros ;
D402 = DROI PC42 PD2 'DINI' mmoy 'DFIN' mgros ;
D403 = DROI PC43 PD3 'DINI' mmoy 'DFIN' mgros ;
D404 = DROI PC44 PD4 'DINI' mmoy 'DFIN' mgros ;


**********
*SURFACES
**********
*Interieur carre 1
S1 = SURF (D01 ET D11 ET D02) 'PLANE' ;
S2 = SURF (D02 ET D12 ET D03) 'PLANE' ;
S3 = SURF (D03 ET D13 ET D04) 'PLANE' ;
S4 = SURF (D04 ET D14 ET D01) 'PLANE' ;
*Entre carre 1 et carre 2
S5 = SURF (D11 ET D121 ET D21 ET D122) 'PLANE' ;
S6 = SURF (D12 ET D122 ET D22 ET D123) 'PLANE' ; 
S7 = SURF (D13 ET D123 ET D23 ET D124) 'PLANE' ;
S8 = SURF (D14 ET D124 ET D24 ET D121) 'PLANE' ;
*Entre carre 2 et carre 3
S9 = SURF (D21 ET D231 ET D31 ET D232) 'PLANE' ;
S10 = SURF (D22 ET D232 ET D32 ET D233) 'PLANE' ; 
S11 = SURF (D23 ET D233 ET D33 ET D234) 'PLANE' ;
S12 = SURF (D24 ET D234 ET D34 ET D231) 'PLANE' ;
*Entre carre 3 et carre 4
S13 = SURF (D31 ET D341 ET D41 ET D342) 'PLANE' ;
S14 = SURF (D32 ET D342 ET D42 ET D343) 'PLANE' ; 
S15 = SURF (D33 ET D343 ET D43 ET D344) 'PLANE' ;
S16 = SURF (D34 ET D344 ET D44 ET D341) 'PLANE' ;
*Entre carre 3 et cercle exterieur
S17 = SURF (D41 ET D401 ET C12 ET D402) 'PLANE' ;
S18 = SURF (D42 ET D402 ET C23 ET D403) 'PLANE' ;
S19 = SURF (D43 ET D403 ET C34 ET D404) 'PLANE' ;
S20 = SURF (D44 ET D404 ET C41 ET D401) 'PLANE' ;

*Assemblage des zones
SLASA = (S9 ET S10 ET S11 ET S12) COUL 'ROUG' ;
STRAN = (S5 ET S6 ET S7 ET S8 ET 
S13 ET S14 ET S15 ET S16) COUL 'VERT' ;
SLOIN = (S1 ET S2 ET S3 ET S4 ET
S17 ET S18 ET S19 ET S20) COUL 'BLEU' ;
STOT = SLASA ET STRAN ET SLOIN ;

SI XINFO ;
MESS 'Nombre elements par couche :' (NBEL STOT) ;
FINS;

**********
*VOLUMES
**********
*Maillage du plateau
MAILPLAT = (STOT VOLU 'DINI' mfin 'DFIN' mgros 
'TRAN' (0. 0. ('*' ep -1.))) COUL 'VERT' ;

*Maillage couche par couche
nbcou = '/' h0 epcou ; COMM 'Nombre de couche' ;
nbcou = ENTI 'TRONCATURE' nbcou ;
SI XINFO ;
MESS 'Nombre de couches : ' nbcou ;
FINS ;

*Premiere couche
COULASA = (SLASA VOLU 1 
'TRAN' (0. 0. epcou)) COUL 'ROUG' ;
COUTRAN = (STRAN VOLU 1 
'TRAN' (0. 0. epcou)) COUL 'ORAN' ;
COULOIN = (SLOIN VOLU 1 
'TRAN' (0. 0. epcou)) COUL 'JAUN' ;


TABM = TABLE ;
TABM.'plateau' = MAILPLAT ;
TABM.'lasage' = TABLE ;
TABM.'transition' = TABLE ;
TABM.'loin' = TABLE ;
TABM.'couche' = TABLE ;
TABM.'lasage'. 1 = COULASA ;
TABM.'transition'. 1 = COUTRAN ;
TABM.'loin'. 1 = COULOIN ;
MAILLASA = TABM.'lasage'. 1 ;
MAILTRAN = TABM.'transition'. 1 ;
MAILLOIN = TABM.'loin'. 1 ;
REPE BOUC0 ('-' nbcou 1) ;
I = ('+' &BOUC0 1) ;
TABM.'lasage'.I = COULASA 'PLUS' (0. 0. ('*' (I-1) epcou)) ;
MAILLASA = MAILLASA ET TABM.'lasage'.I ;
TABM.'transition'.I = COUTRAN 'PLUS' (0. 0. ('*' (I-1) epcou)) ;
MAILTRAN = MAILTRAN ET TABM.'transition'.I ;
TABM.'loin'.I = COULOIN 'PLUS' (0. 0. ('*' (I-1) epcou)) ;
MAILLOIN = MAILLOIN ET TABM.'loin'.I ;
TABM.'couche'.I = 
(TABM.'lasage'.I) ET (TABM.'transition'.I) ET (TABM.'loin'.I) ;
FIN BOUC0 ;


MAILTOT = MAILLASA ET MAILTRAN ET MAILLOIN ET MAILPLAT ;
ELIM MAILTOT 1.e-7 ;

SI XINFO ;
NB = NBEL MAILTOT ;
MESS 'Nombre elements :' NB ;
FINS ;

**********
*GRAPH 
**********
SI GRAPH ;
TRAC POEIL1 STOT ;
TRAC 'FACE' 'CACHE' POEIL2 (MAILPLAT ET MAILLASA) ;
TRAC 'FACE' 'CACHE' POEIL2 MAILTOT ;
FINS ;

**********
*TRAJECTOIRE LASAGE
**********
PROGX = PROG (-1. * a1) a1  ;
PROGY = PROG (-1. * a1) (-1. * a1) ;


**********
*SAUV
**********
TABM.'EPAISSEUR_COU' = epcou ;
*OPTI SAUV 'maillage_murs_FA_SLM.sauv' ;
*SAUV TABM MAILTOT MAILLASA MAILTRAN MAILLOIN PROGX PROGY ;

***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************

*Calcul thermique murs (carre) et plateau (substrat), fabrication addictive, procede SLM
*Modele thermique conduction 3D
*Trois matériaux : 
* poudre
* piece
* substrat
*Procede SLM
*Le métal poudre et le métal continu fondent à T>Tliq
*Les elements sont ajoutés couche par couche


*****Restitution maillage
*OPTI REST 'maillage_murs_FA_SLM.sauv' ;
*REST ;
NBCOU = DIME TABM.'lasage' ; COMM 'Nombre de couches' ;


**********
*PARAMETRES 
**********
T_INI = 20. ; COMM 'Temperature initiale' ;
T_CONV = T_INI ;
T_LIQ = 1400. ; COMM 'Temperature liquidus' ;

*Construction de la TABLE Fabrication Additive pour stocker l etude
FA = TABLE ;
****MAILLAGE
FA.'MAILLAGE' = TABLE ; 
FA.'MAILLAGE'.'COUCHE' = TABLE ; COMM 'Couche unitaire' ;
FA.'MAILLAGE'.'COUCHE_CUM' = TABLE ; COMM 'Couches cumulees' ;
FA.'MAILLAGE'.'TOTAL_CUM' = TABLE ; COMM 'Couche + substrat' ;
FA.'MAILLAGE'.'POUDRE' = VIDE 'MAILLAGE' ;
FA.'MAILLAGE'.'SUBSTRAT' = VIDE 'MAILLAGE' ;
FA.'MAILLAGE'.'TOTAL' = VIDE 'MAILLAGE' ;

****MATERIAUX
FA.'CONSTITUANT' = TABLE ;
FA.'CONSTITUANT'.'SUBSTRAT' = MOT '316L' ;
FA.'CONSTITUANT'.'PIECE' = MOT '316L' ;
FA.'CONSTITUANT'.'POROSITE' = 0.2 ;
****PROCEDE
FA.'PROCEDE' = TABLE ;
FA.'PROCEDE'.'VITESSE' = 300.e-3 ; COMM 'Vitesse en [m/s]' ;
FA.'PROCEDE'.'TEMPS_INTERCOUCHE' = 0.5 ; COMM 'en [s]' ;
FA.'PROCEDE'.'EPAISSEUR_COU' = TABM.'EPAISSEUR_COU' ;
*Sources possibles : 'GOLDAK' ,
FA.'PROCEDE'.'SOURCE' = TABLE ;
FA.'PROCEDE'.'SOURCE'.'TYPE' = MOT 'GOLDAK' ;

SI ('EGA' (FA.'PROCEDE'.'SOURCE'.'TYPE') (MOT 'GOLDAK')) ;
FA.'PROCEDE'.'SOURCE'.'ETA' = 1. ; COMM 'Rendement' ;
FA.'PROCEDE'.'SOURCE'.'Q' = 50. ; 
FA.'PROCEDE'.'SOURCE'.'A' = 100.e-6 ;
FA.'PROCEDE'.'SOURCE'.'B' = 100.e-6 ;
FA.'PROCEDE'.'SOURCE'.'Cf' = 100.e-6 ;
FA.'PROCEDE'.'SOURCE'.'Cr' = 100.e-6 ;
FINS ;

*Trajectoire
FA.'PROCEDE'.'TRAJECTOIRE' = TABLE ;
FA.'PROCEDE'.'TRAJECTOIRE'.'PROGX' = PROGX ;
FA.'PROCEDE'.'TRAJECTOIRE'.'PROGY' = PROGY ;

****TEMPS CALCULES
TPSLASA = DUREE
FA.'PROCEDE'.'TRAJECTOIRE'.'PROGX'
FA.'PROCEDE'.'TRAJECTOIRE'.'PROGY'
(FA.'PROCEDE'.'VITESSE') ;
FA.'PROCEDE'.'TEMPS_LASAGE' = TPSLASA ;
TPSPAS1 = 1.e-4 ; COMM 'Pas de calcul pendant le lasage en [s]' ;
TPSPAS2 = 2.e-1 ; COMM 'Pas de calcul pendant le refroidissement en [s]' ;
TPSFIN = '+' TPSLASA FA.'PROCEDE'.'TEMPS_INTERCOUCHE' ;
*Temps calcules
LTCAL  = PROG 
TPSPAS1 'PAS' TPSPAS1 TPSLASA 'PAS' TPSPAS2 TPSFIN ;
*Temps sauves
TPSPAS11 = 1.e-3 ;COMM 'Pas de sauvegarde pendant le lasage en [s]' ;
TPSPAS22 = 2.e-1 ;COMM 'Pas de sauvegarde pendant le refroidissement en [s]' ;
LTSAUV  = PROG 
TPSPAS11 'PAS' TPSPAS11 TPSLASA 'PAS' TPSPAS22 TPSFIN ;

FA.'TEMPS_CALCULES' = TABLE ;
FA.'TEMPS_SAUVES' = TABLE ;
REPE BOUCT NBCOU ;
K = &BOUCT ;
FA.'TEMPS_CALCULES'.K = ('*' TPSFIN (K-1)) '+' LTCAL ;
FA.'TEMPS_SAUVES'.K = ('*' TPSFIN (K-1)) '+' LTSAUV ;
FIN BOUCT ;

**********
*IMPORTATION MAILLAGE
**********
*Substrat
FA.'MAILLAGE'.'SUBSTRAT' = TABM.'plateau' ;

*Couches de poudre
FA.'MAILLAGE'.'COUCHE_CUM'. 0 = VIDE 'MAILLAGE' ;
FA.'MAILLAGE'.'TOTAL_CUM'. 0 = VIDE 'MAILLAGE' ;
REPE BLOC1 NBCOU ;
I = &BLOC1 ;
list I ;
FA.'MAILLAGE'.'COUCHE'.I = 
(TABM.'lasage'.I ET TABM.'transition'.I ET TABM.'loin'.I) ;
FA.'MAILLAGE'.'COUCHE_CUM'.I = FA.'MAILLAGE'.'COUCHE_CUM'.(I-1)
 ET FA.'MAILLAGE'.'COUCHE'.I ;
 
FA.'MAILLAGE'.'TOTAL_CUM'.I = FA.'MAILLAGE'.'COUCHE_CUM'.I
 ET FA.'MAILLAGE'.'SUBSTRAT' ;
FIN BLOC1 ;
FA.'MAILLAGE'.'POUDRE' = FA.'MAILLAGE'.'COUCHE_CUM'.NBCOU ;
FA.'MAILLAGE'.'POUDRE_INI' = FA.'MAILLAGE'.'POUDRE' ;
*Maillage total
FA.'MAILLAGE'.'TOTAL' = FA.'MAILLAGE'.'SUBSTRAT' ET FA.'MAILLAGE'.'POUDRE' ;

SI GRAPH ;
TRAC 'FACE' 'CACHE' FA.'MAILLAGE'.'TOTAL' ;
FINS ;


**********
*MODELES
**********
FA.'MODELE' = TABLE ;
FA.'MODELE'.'SUBSTRAT' = VIDE 'MMODEL' ;
FA.'MODELE'.'POUDRE' = VIDE 'MMODEL' ;
FA.'MODELE'.'PIECE' = VIDE 'MMODEL' ;
FA.'MODELE'.'CONVECTION' = VIDE 'MMODEL' ;
FA.'MODELE'.'MAILLAGE_CONV' = VIDE 'MAILLAGE' ;
FA.'MODELE'.'TOTAL' = VIDE 'MMODEL' ;

*Substrat
MAILTHSU = FA.'MAILLAGE'.'SUBSTRAT' ;
MODTHESU = MODE MAILTHSU 'THERMIQUE' 'CONDUCTION' 'CONS' 'COND' ;
FA.'MODELE'.'SUBSTRAT' = MODTHESU ;

*Poudre
MAILTHPO = FA.'MAILLAGE'.'POUDRE' ;
MODTHEPO = MODE MAILTHPO 'THERMIQUE' 'CONDUCTION' 'CONS' 'COND' ;
FA.'MODELE'.'POUDRE' = MODTHEPO ;
FA.'MODELE'.'POUDRE_INI' = MODTHEPO ;
*Piece
MAILTHPI = VIDE 'MAILLAGE' ;
MODTHEPI = MODE MAILTHPI 'THERMIQUE' 'CONDUCTION' 'CONS' 'COND' ;
FA.'MODELE'.'PIECE' = MODTHEPI ;

*Total
FA.'MODELE'.'TOTAL' = 
MODTHESU ET MODTHEPO ET MODTHEPI ;


**********
*MATERIAUX
**********
FA.'MATERIAUX' = TABLE ;
FA.'MATERIAUX'.'PARA_PIECE' = TABLE ;
FA.'MATERIAUX'.'SUBSTRAT' = VIDE 'MCHAML' ;
FA.'MATERIAUX'.'POUDRE' = VIDE 'MCHAML' ;
FA.'MATERIAUX'.'PIECE' = VIDE 'MCHAML' ;
FA.'MATERIAUX'.'TOTAL' = VIDE 'MCHAML' ;



*Lecture des MATERIAUX ('316L','16MND5')

NOMCONS = FA.'CONSTITUANT'.'SUBSTRAT' ;

*Lecture des parametres dans la bibliotheque

*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Rho.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 200 400 600 800 1000 1200 1400 1500 3000 ;
TTMP. 2 = PROG 8.0e3 7.93e3 7.84e3 7.75e3 7.65e3 7.55e3
7.45e3 7.35e3 7.30e3 7.30e3 ;                                      
EVRHO   = EVOL MANU 'T' (TTMP. 1) 'RHO' (TTMP. 2) ;                  

*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_k.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 100 200 300 400 500 600 700 800 900 1000 
1200 1400 3000 ;
TTMP. 2 = PROG 0.0147e3 0.0158e3 0.0172e3 0.0186e3 0.02e3 
0.0211e3 0.0222e3 0.0232e3 0.0241e3 0.0248e3 0.0255e3 
0.0269e3 0.0283e3 0.0283e3 ;
EVK     = EVOL MANU 'T' (TTMP. 1) 'K' (TTMP. 2) ;

*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Cp.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 100 200 300 400 600 800 1000 1200 1500 3000 ;
TTMP. 2 = PROG 450 490 525 545 560 580 625 660 670 690 690 ;
EVCP    = EVOL MANU 'T' (TTMP. 1) 'C' (TTMP. 2) ;

SI GRAPH ;
DESS EVRHO TITR 'Masse Volumique' ;
DESS EVK TITR 'Conductivite Thermique' ;
DESS EVCP TITR 'Chaleur Specifique' ;
FINS ;

MATHESU = MATE MODTHESU 'K' EVK 'RHO' EVRHO 'C' EVCP ;
FA.'MATERIAUX'.'SUBSTRAT' = MATHESU ; 

*Sauvegarde pour utilisation dans PERSO2
FA.'MATERIAUX'.'PARA_PIECE'.'EVK' = EVK ;
FA.'MATERIAUX'.'PARA_PIECE'.'EVRHO' = EVRHO ;
FA.'MATERIAUX'.'PARA_PIECE'.'EVCP' = EVCP ;

SI ('NEG' FA.'CONSTITUANT'.'SUBSTRAT' FA.'CONSTITUANT'.'PIECE') ;
*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Rho.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 200 400 600 800 1000 1200 1400 1500 3000 ;
TTMP. 2 = PROG 8.0e3 7.93e3 7.84e3 7.75e3 7.65e3 7.55e3
7.45e3 7.35e3 7.30e3 7.30e3 ;                                      
EVRHO   = EVOL MANU 'T' (TTMP. 1) 'RHO' (TTMP. 2) ;                  

*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_k.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 100 200 300 400 500 600 700 800 900 1000 
1200 1400 3000 ;
TTMP. 2 = PROG 0.0147e3 0.0158e3 0.0172e3 0.0186e3 0.02e3 
0.0211e3 0.0222e3 0.0232e3 0.0241e3 0.0248e3 0.0255e3 
0.0269e3 0.0283e3 0.0283e3 ;
EVK     = EVOL MANU 'T' (TTMP. 1) 'K' (TTMP. 2) ;

*NOMDATA = CHAI 'Materiaux/' NOMCONS '/' NOMCONS'_Cp.csv' ;
TTMP = TABL ;
TTMP. 1 = PROG 20 100 200 300 400 600 800 1000 1200 1500 3000 ;
TTMP. 2 = PROG 450 490 525 545 560 580 625 660 670 690 690 ;
EVCP    = EVOL MANU 'T' (TTMP. 1) 'C' (TTMP. 2) ;

SI GRAPH ;
DESS EVRHO TITR 'Masse Volumique' ;
DESS EVK TITR 'Conductivite Thermique' ;
DESS EVCP TITR 'Chaleur Specifique' ;
FINS ;
FINS ;

MATHEPI = MATE MODTHEPI 'K' EVK 'RHO' EVRHO 'C' EVCP ;
FA.'MATERIAUX'.'PIECE' = MATHEPI ;
FA.'MATERIAUX'.'PIECE_INI' = MATHEPI ;
*Sauvegarde pour utilisation dans PERSO2
FA.'MATERIAUX'.'PARA_PIECE'.'EVK' = EVK ;
FA.'MATERIAUX'.'PARA_PIECE'.'EVRHO' = EVRHO ;
FA.'MATERIAUX'.'PARA_PIECE'.'EVCP' = EVCP ;

*Les proprietes de la poudre sont proportionnelle a sa porosite 'porop'
porop = FA.'CONSTITUANT'.'POROSITE' ;
EVK = '*' ('-' 1. porop) EVK ;
EVRHO = '*' ('-' 1. porop) EVRHO ;
EVCP = '*' ('-' 1. porop) EVCP ;
MATHEPO = MATE MODTHEPO 'K' EVK 'RHO' EVRHO 'C' EVCP ;
FA.'MATERIAUX'.'POUDRE' = MATHEPO ;
FA.'MATERIAUX'.'POUDRE_INI' = MATHEPO ;

FA.'MATERIAUX'.'TOTAL' = MATHESU ET MATHEPO ET MATHEPI ;

**********
*PASAPAS - Calcul thermique transitoire
**********

I = 1 ; COMM 'Indice de la couche en cours' ;

*REDU du calcul thermique sur la couche actuelle
MODCOU = REDU FA.'MODELE'.'POUDRE' FA.'MAILLAGE'.'COUCHE_CUM'. 1 ;
MODCOND  = MODTHESU ET MODCOU ET MODTHEPI ;
MATCOU = REDU FA.'MATERIAUX'.'POUDRE' FA.'MAILLAGE'.'COUCHE_CUM'. 1 ;
MATCOND  = MATHESU ET MATCOU ET MATHEPI ;
MAILCONV = ENVE (EXTR (MODTHESU ET MODCOU) 'MAILLAGE') ;

*Convection
MODCONV = MODE MAILCONV 'THERMIQUE' 'CONVECTION' 'CONS' 'CONV' ; 
MATCONV = MATE MODCONV 'H' 10. ;
CONV1 = CONV MODCONV MATCONV 'T' T_CONV ;
EVO1 = EVOL 'MANU' (PROG 0. 1.e10) (PROG 1. 1.) ;
CHACONV = CHAR 'Q' CONV1 EVO1 ;
CHARTOT = CHACONV ;

*Ajout convection au modele
MODTOT  = MODCOND ET MODCONV ;
MATTOT  = MATCOND ET MATCONV ;


*Initialisation
MAILTHER= EXTR MODCOND 'MAILLAGE' ;
CHPTINI = MANU 'CHPO' MAILTHER 1 'T' T_INI 'NATU' 'DIFFUS' ;
MAILFON = VIDE 'MAILLAGE' ;

*Declaration de la table PASAPAS
FAPASAP                                 = TABLE ;
FAPASAP.'MODELE'                        = MODTOT ;
FAPASAP.'CARACTERISTIQUES'              = MATTOT ;
FAPASAP.'CHARGEMENT'                    = CHARTOT ;
FAPASAP.'TEMPS_CALCULES'                = LTCAL ;
*FAPASAP.'TEMPS_SAUVES'                  = LTSAUV ;

FAPASAP.'TEMPS'                         = TABLE ;
FAPASAP.'TEMPERATURES'                  = TABLE ;
FAPASAP.'MES_MODEL'                     = TABLE ;
FAPASAP.'MAILLAGE_FONDU'                = TABLE ;
FAPASAP.'MES_MODEL2'                    = TABLE ;
FAPASAP.'MAILLAGE_FONDU2'               = TABLE ;

FAPASAP.'TEMPS'. 0                      = 0. ;
FAPASAP.'TEMPERATURES'. 0               = CHPTINI ;
FAPASAP.'MES_MODEL'. 0                  = MODTOT ;
FAPASAP.'MES_MODEL2'. 0                 = MODTOT ;
FAPASAP.'MAILLAGE_FONDU'. 0             = MAILFON ;
FAPASAP.'MAILLAGE_FONDU2'. 0            = MAILFON ;
*MES_MODEL2 et MAILLAGE_FONDU2 sont enregistres en synchronisation avec 'TEMPS'

FAPASAP.'PROCEDURE_PERSO2'              = VRAI ;
FAPASAP.'PROCEDURE_CHARTHER'            = VRAI ;

FAPASAP.'PRECISION'                         = 1.e-1 ; COMM 'Pour rapidite' ;

FAPASAP.'PROCESSEURS'                   = 'MOT' 'AUTOMATIQUE' ;

FAPASAP.'MES_DATA'                                              = TABLE ;
FAPASAP.'MES_DATA'.'COUCHE'                             = 1 ;
FAPASAP.'MES_DATA'.'TPSFIN'                     = TPSFIN ;
FAPASAP.'MES_DATA'.'T_LIQUIDUS'                 = T_LIQ ;

*Boucle sur les couches (un PASAPAS par couche)
REPE BOUCPAS NBCOU ;
I = &BOUCPAS ;

SI XINFO ;
MESS 'DEBUT CALCUL LA COUCHE :' I ;
FINS ;


PASAPAS FAPASAP ;


SI XINFO ;
MESS 'FIN CALCUL LA COUCHE :' I ;
FINS ;

MENA 'OBLI' ;
*nomfic = CHAI 'RESU/COUCHE_' I '.sauv' ;
*OPTI SAUV nomfic ;
*SAUV FAPASAP TABM FA ;
*Pour gagner de la place sur le disque (commande linux)
*SI (I '>' 2) ;
*comclean = CHAI 'rm RESU/COUCHE_' (I-2) '.sauv*' ;
*EXTE comclean ;
*FINS ;

SI (I '<' NBCOU) ;

*On ajoute le MODELE et le MATERIAUX de la couche suivante
NB_PAS = DIME FAPASAP.'TEMPS' ;
MODCOUI = REDU (EXTR FA.'MODELE'.'POUDRE_INI' 'CONS' 'COND') 
FA.'MAILLAGE'.'COUCHE'.(I+1) ;

MODCOND = EXTR FAPASAP.'MODELE' 'CONS' 'COND' ;
MODCOND2 = MODCOND ET MODCOUI ;
MAILCONV = ENVE (EXTR MODCOND2 'MAILLAGE') ;
MODCONV = MODE MAILCONV 'THERMIQUE' 'CONVECTION' 'CONS' 'CONV' ;
MODTOT = MODCOND2 ET MODCONV ;

*On ajoute le materiau
MATLAST = REDU FA.'MATERIAUX'.'TOTAL' MODCOND ;
MATCOND = MATLAST ET (REDU FA.'MATERIAUX'.'POUDRE_INI' MODCOUI) ;
MATCONV = MATE MODCONV 'H' 10. ;
MATTOT = MATCOND ET MATCONV ;


*Chargement convection
CONV1 = CONV MODCONV MATCONV 'T' T_CONV ;
EVO1 = EVOL 'MANU' (PROG 0. 1.e10) (PROG 1. 1.) ;
CHACONV = CHAR 'Q' CONV1 EVO1 ;

*Actualisation chargement
CHARTOT = CHACONV ;

*Ajout de la nouvelle couche à temperature T_INI
CHPTLAST = FAPASAP.'TEMPERATURES'.(NB_PAS-1) ;
MAILNEW = FA.'MAILLAGE'.'COUCHE'.(I+1) ;
CHPTNEW  = MANU 'CHPO' MAILNEW 1 'T' T_INI 'NATU' 'DIFFUS' ;

MAIL1 = EXTR CHPTLAST 'MAILLAGE' ;
MAIL2 = EXTR CHPTNEW 'MAILLAGE' ;
MAILSEP = INTE MAIL1 MAIL2 ;

MAIL11 = DIFF MAIL1 MAILSEP ;
MAIL22 = DIFF MAIL2 MAILSEP ;

*Il faut faire une hypothese sur la temperature sur la surface frontiere la couche precedente et la nouvelle
*On fait hypothese que cette couche est a la temperature nouvelle couche
CHPTLAST = REDU CHPTLAST MAIL11 ;
CHPTINI = CHPTLAST ET CHPTNEW ;
CHPTINI = REDU CHPTINI (EXTR MODTOT 'MAILLAGE') ;

*On actualise la table PASAPAS
FAPASAP.'MODELE'                        = MODTOT ;
FAPASAP.'CARACTERISTIQUES'              = MATTOT ;
FAPASAP.'CHARGEMENT'                    = CHARTOT ;
FAPASAP.'TEMPS_CALCULES'                = FA.'TEMPS_CALCULES'.(I+1)  ;
*FAPASAP.'TEMPS_SAUVES'                  = FA.'TEMPS_SAUVES'.(I+1) ;
FAPASAP.'TEMPERATURES'.(NB_PAS-1)       = CHPTINI ;
FAPASAP.'MES_MODEL'.(NB_PAS-1)          = MODTOT  ;
FAPASAP.'MES_DATA'.'COUCHE'                         = (I+1) ;

FINS ;
FIN BOUCPAS ;

SI GRAPH ;
N = DIME FAPASAP.'MAILLAGE_FONDU' ;
MAILF = FAPASAP.'MAILLAGE_FONDU'.(N-1) ;
TRAC FACE CACHE (MAILPLAT ET MAILF) ;
FINS ;

FIN ;












