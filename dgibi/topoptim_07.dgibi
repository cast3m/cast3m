* fichier topoptim_07.dgibi
************************************************************************
************************************************************************

************************************************************************
** Topology optimization of of a simple 2D structure subjected to a
** mechanical loading, with a circular periodicity restriction.
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V1.0 2017/04/18 Original version compatible with TOPOPTIM V2.1
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'MODE' 'PLAN' 'CONT' 'ELEM' QUA4 ;

* Mesh
nelr0 = 20 ;
nelt0 = 60 ;
p0 = 0.0 0.0 ;
p1 = 20.0 0.0 ;
p2 = 60.0 0.0 ;
lgn1 = LIGN nelt0 p0 p1 360.0 'ROTA' ;
lgn2 = LIGN nelt0 p0 p2 360.0 'ROTA' ;
msh0 = lgn1 REGL nelr0 lgn2 ;
ELIM msh0 1.0e-9 ;

* Model and material
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' ;
mat0 = MATE mod0 'YOUN' 210.0e9 'NU' 0.3 ;

* Boundary conditions and loading
bc0 = BLOQ 'DEPL' lgn1 ;
load0 = FORC (0.0 -1.0) p2 ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ;
mdl0.'BLOCAGES_MECANIQUES' = bc0 ;
mdl0.'CHARGEMENT'          = load0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_LINEAIRE' = mdl0 ;
tab0.'FRACTION_VOLUME'     = 0.5 ;
tab0.'RESTRICTIONS'        = TABL ;
tab0.'RESTRICTIONS'.(1)            = TABL ;
tab0.'RESTRICTIONS'.(1).'TYPE'     = MOT 'PERIODICITE_CIRCULAIRE' ;
tab0.'RESTRICTIONS'.(1).'PERIODES' = 3 ;
tab0.'RESTRICTIONS'.(1).'POIN1'    = p0 ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Plot to screen
topo0 = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
SI graph0 ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;


