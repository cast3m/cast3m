* fichier : dynacontact.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************

graph=FAUX; opti trac PSC EPTR 5;
complet = faux;
opti dime 2 elem qua4 mode plan defo;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;


**** maillage, modele et matrices *****

p1= 0 0.000015 ; p2 = 1 0.000015 ;
co= p1 droi 2 p2;l1= co;
p3 = 1 1 ; co = co droi 2 p3;
p4 = 4 1 ; co = co droi 6 p4;
p5 = 4 0.002 ; co = co droi 2 p5;
p6 = 5 0.002; l2= p5 droi 2 p6;co = co et l2;
p7= 5 1.1; co = co droi 2 p7;
p8= 0 1.1 ; co = co droi 10 p8 droi 2 p1;
su = surf co;
si graph; trac su 'TITR' 'maillage su'; finsi;

mo = model su mecanique elastique ;
ma = mo mate youn 2e11 nu 0.3 rho 7800;
ri = rigi mo ma;
m = mass mo ma;

vi= manu chpo su 1 'UY' -1.;
si graph; trac vi su 'TITR' 'maillage su VY(t=0)'; finsi;

si complet ;tfin= 5.E-3;sinon; tfin=1.e-3;finsi;
bl1 = bloqu mini 'UY' l1 ;
bl2= bloqu mini 'UY' l2;
fbl1= depi bl1 -0.000015;
fbl2= depi bl2 -0.002;
evt = evol manu 'temps' ( prog 0. 1.) 'coef' (prog 1. 1.);
cha1= char 'DIMP' (fbl1 et fbl2) evt;
LIS1 = PROG  0. PAS 1.E-5  tfin  ;


**** appel a dynamic (schema de Newmark acceleration moyenne) *****

TAB2 = TABLE ;
TAB2.'DEPL' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2.'VITE' = vi;
* TAB2.'ACCE' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2.'CHAR' = CHA1 ;
TAB2.'RIGI' = RI et bl1 et bl2 ;
TAB2.'MASS' = M ;
flnew= faux ;
si (flnew);
  TAB2. 'TEMPS_CALCULES' = LIS1;
  TAB2.'PAS_SAUVES' = mot 'TOUS';
sinon;
  TAB2.'FREQ' = 25000.;
  TAB2.'INST' = LIS1 ;
finsi;
TAB2.'SORT'=su;
* TAB2.'VITEUNIL'= FAUX;

TAB1 = DYNAMIC TAB2 ;


**** appel a dynamic (schema HHT) *****

TAB2H  = TABLE ;
TAB2H.'DEPL' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2H.'VITE' = vi;
* TAB2H.'ACCE' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2H.'CHAR' = CHA1 ;
TAB2H.'RIGI' = RI et bl1 et bl2 ;
TAB2H.'MASS' = M ;
flnew= faux ;
si (flnew);
  TAB2H. 'TEMPS_CALCULES' = LIS1;
  TAB2H.'PAS_SAUVES' = mot 'TOUS';
sinon;
  TAB2H.'FREQ' = 25000.;
  TAB2H.'INST' = LIS1 ;
finsi;
TAB2H.'SORT'=su;
* TAB2H.'VITEUNIL'= FAUX;
TAB2H . 'ALPHA_F' = 0.3;

TAB1H = DYNAMIC TAB2H ;


**** appel a dynamic (schema alpha-generalise) *****

TAB2A  = TABLE ;
TAB2A.'DEPL' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2A.'VITE' = vi;
* TAB2A.'ACCE' = MANU CHPO SU 2 UX 0. UY 0.  ;
TAB2A.'CHAR' = CHA1 ;
TAB2A.'RIGI' = RI et bl1 et bl2 ;
TAB2A.'MASS' = M ;
flnew= faux ;
si (flnew);
  TAB2A. 'TEMPS_CALCULES' = LIS1;
  TAB2A.'PAS_SAUVES' = mot 'TOUS';
sinon;
  TAB2A.'FREQ' = 25000.;
  TAB2A.'INST' = LIS1 ;
finsi;
TAB2A.'SORT'=su;
* TAB2A.'VITEUNIL'= FAUX;
* TAB2A . 'RHO_INF' = 0.5;
* TAB2A . 'RHO_INF' = 0.2;
* TAB2A . 'RHO_INF' = 0.1;
TAB2A . 'RHO_INF' = 0.;

TAB1A = DYNAMIC TAB2A ;


**** appel a pasapas *****

tt= table;
tt.'MODELE'= mo;
tt.'DYNAMIQUE'= VRAI;
tt.'CARACTERISTIQUES' = ma;
tt.'VITESSES' = table;
tt.'VITESSES' . 0 = vi;
tt.chargement =cha1;
tt.blocages_mecaniques = (bl1 et bl2);
tt.TEMPS_calcules= prog 1.e-5 pas 1.e-5 tfin;
tt.'IMPR'=vrai;
pasapas tt;


******** procedur de depouillement **************

debproc depo coev*flottant nb*entier ampdef*flottant;

na = ((dime tt.deplacements ) - 1) / nb;

repeter toto na;

* pas de temps traité
  titi  = &toto - 1;
  tit   = chai  ' pas numero ' (titi  * nb );
  titre tit;
* resultats de dynamic
  dyde  = tab1. (titi * nb + 1).'DEPL';
  dyvi  = tab1. (titi * nb + 1).'VITE';
  dyviy = exco dyvi 'UY';
  dydef = defo su dyde  ampdef rouge  dyviy ;
* resultats de pasapas
  pasvi = tt.deplacements.( titi * nb );
  vv    = vect tt.reactions . (titi * nb )  coev 'FX' 'FY' vert;
  padef = defo su pasvi vv ampdef;
* trace comparatif
  trac (dydef et padef);

fin toto;

finp  depo;


si ( graph ) ;
  depo 1.e-8 5 100.;
finsi;


**** comparaison du deplacement en P1 *****
na = (dime tt.deplacements ) - 1;

uydyna = prog; tdyna = prog ;
uyalph = prog;
uyhht  = prog;
uypap  = prog; tpap  = prog ;
i = 0;
repe toto na; i = i + 1;
* resultats de dynamic
  tdyna  = tdyna  et TAB1 . i . 'TEMP';
  uydyna = uydyna et (extr TAB1  . i . depl 'UY' p1);
  uyhht  = uyhht  et (extr TAB1H . i . depl 'UY' p1);
  uyalph = uyalph et (extr TAB1A . i . depl 'UY' p1);
* resultats de pasapas
  tpap   = tpap   et tt . temps . i;
  uypap  = uypap  et (extr tt.deplacements . i 'UY' p1);
fin toto;

evdyna = EVOL 'BLEU' 'MANU' 'LEGE' 'DYNAMIC' 't' tdyna 'UY' uydyna;
evhht  = EVOL 'ORAN' 'MANU' 'LEGE' 'HHT'     't' tdyna 'UY' uyhht;
evalph = EVOL 'ROUG' 'MANU' 'LEGE' '\a-gen.' 't' tdyna 'UY' uyalph;
evpap  = EVOL 'VERT' 'MANU' 'LEGE' 'PASAPAS' 't' tpap  'UY' uypap;
tdess = tabl;
tdess . 2 = 'MARQ S TRIU';
tdess . 3 = 'MARQ PLUS';
tdess . 4 = 'TIRR';
si graph;
dess (evdyna et evhht et evalph et evpap)
  'YBOR' -0.05E-3 0.5E-3 'YGRA' 0.05E-3 'LEGE' 'NO' tdess;
finsi;

**** comparaison des derniers champs de deplacementrs *****
pasvi=tt.deplacements. na;
dyde=tab1. ( na + 1 ). 'DEPL';
difde= pasvi - dyde;
cri= maxi pasvi;
crit = maxi difde / cri;
list crit;
si ( crit > 1.e-5);
 erreur 5;
finsi;


fin ;








