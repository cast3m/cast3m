* fichier :  rupt13.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
OPTION echo 1                                                   ;
GRAPH = 'N'                                                     ;
SAUT PAGE                                                       ;
*                                                               ;
** LE TAUX G DANS L'EPAISSEUR DE COQUE : NOUVELLE TECHNIQUE
** ICOQU = 1 ELEMENTS 'DKT'
** ICOQU = 2 ELEMENTS 'DST'
** ICOQU = 3 ELEMENTS 'COQ6'
** ICOQU = 4 ELEMENTS 'COQ4'
** ICOQU = 5 ELEMENTS 'COQ8'

** ICHAR = 1 FLEXION PURE
** ICHAR = 2 FLEXION + MENBRANE
** ICHAR = 3 FORCE SUR LA FISSURE
** ICHAR = 4 CHARGEMENTS MECANIQUES
** ICHAR = 5 CHARGEMENTS 3 + 4

** IMATE = 1 ELASTICITE
** IMATE = 2 ELASTO PLASTICITE

** IELEM = NUMEO DE L ELEMENT PROCHE DE LA FISSURE
*****************************************
*  SAISSIE DES PARAMETRES DE MAILLAGE   *
*****************************************
*
* CHOIX DES CHAMPS SINGULIERS

IELEM = 53;
*
* TYPE DE CHARGEMENT

ICHAR = 1;
*
* TYPE DE MATERIAU

IMATE = 1;
*
* EPAISSEUR TOTALE DE L'EPROUVETTE

EPAI_T = .7 * 40;
*
* TYPE D'ELEMENTS DE COQUE

ICOQU = 4;
*
* NOMBRE DE MULTICOUCHES

NB_MUL = 21;
*
* EPAISSEUR DE LA COUCHE POUR DETERMINER LE RAPPORT D'ENERGIE

EPAI_C = EPAI_T * 1.E-6;
*
* ---------------- A/W :

RAP1 = 0.25 ;
*
* ---------------- H/W :

RAP2 = 4. ;
*
* ---------------- W :

W = 70. ;
*
* LARGEUR DE BANDE DEPOSEE (POUR UN METAL BIPHASE)

LDEP = 10. ;
*
*EPROUVETTE SENB OU CCP ? (VAL = 0  OU  VAL = 1) ;

VAL = 1 ;
*
* TAILLE DE MAILLE EN POINTE DE FISSURE

TM = .2 ;
*
* NOMBRE DE DECOUGE SUR 45 DEGRES ;

N = 4 ;
***************************************
******* ELEMENTS ET FORMULATIONS ******
***************************************

SI (EGA ICOQU 1);
  MOT_E = TEXT 'TRI3'; REP = 0 ; MOT_S = TEXT 'SEG2';
  MOT_F1 = TEXT 'DKT';  MOT_F2 = TEXT '        ';
FINSI;
SI (EGA ICOQU 2);
  MOT_E = TEXT 'TRI3'; REP = 0 ; MOT_S = TEXT 'SEG2';
  MOT_F1 = TEXT 'DST';  MOT_F2 = TEXT '        ';
FINSI;
SI (EGA ICOQU 3);
  MOT_E = TEXT 'TRI6'; REP = 0 ; MOT_S = TEXT 'SEG3';
  MOT_F1 = TEXT 'COQ6';  MOT_F2 = TEXT '        ';
FINSI;
SI (EGA ICOQU 4);
  MOT_E = TEXT 'QUA4'; REP = 1 ; MOT_S = TEXT 'SEG2';
  MOT_F1 = TEXT 'COQ4';  MOT_F2 = TEXT 'DST';
FINSI;
SI (EGA ICOQU 5);
  MOT_E = TEXT 'QUA8'; REP = 1 ; MOT_S = TEXT 'SEG3';
  MOT_F1 = TEXT 'COQ8';  MOT_F2 = TEXT 'COQ6';
FINSI;

***************************************
******* PARAMETRES INTERMEDIAIRES *****
***************************************

LCRITW = (W * (1. - RAP1)) ;
LCRITA = W * RAP1 ;
*DEMI-COTE DU CARRE
C = MINI (PROG (W / 5.) LDEP LCRITA LCRITW) ;

*RAYON DU DISQUE
R = C / 2. ;

*DENSITE EN PERIPHERIE DE DISQUE
*TE = R / 10. ;
TE = 1.5 * ((PI * R) / (4. * N)) ;

*DENSITE AU PREMIER ARC
SI (EGA REP 1) ;
 TA = TM * 2. ;
SINON ;
 TA = TM ;
FINSI ;

*MOMENT D'INERTI
IX = W * (EPAI_T ** 3) / 12;

*******************************************
*  MAILLAGE                               *
*******************************************

*----------------------------------------------------*
*** PARTIE CIRCULAIRE AUTOUR DE LA POINTE DE FISSURE *
*----------------------------------------------------*

OPTI ECHO 1 DIME 2 ELEM MOT_E ;
P0 = 0. 0. ;
P1 = (RAP1 * W) 0. ;
DENS TA ;
SI (REP EGA 0) ;
 P2 = P1 PLUS (TM 0.) ;
SINON ;
 P2 = P1 PLUS ((TM * N * 2.) 0.) ;
 SI ((TM * N * 2.) > R) ;
  NC =  R / 2. ;
  OPTI ECHO 0 ;
  MESS 'LE PRODUIT (TM * N) DOIT ETRE INFERIEUR A' NC ;
  OPTI ECHO 1 ;
 FINSI ;
FINSI ;
DENS TE ;
P10 = P1 PLUS (R 0.) ;

LIGAM0 = P2 D P10 ;

SU21 = LIGAM0 ROTA N 45. P1 ;
L21 = SU21 COTE 4 ;
C21 =  SU21 COTE 2 ;
SU22 = (SU21 COTE 3) ROTA N 45. P1 ;
L22 = SU22 COTE 2 ;
C22 =  SU22 COTE 4 ;
SU23 = (SU22 COTE 3) ROTA N 45. P1 ;
L23 = SU23 COTE 4 ;
C23 =  SU23 COTE 2 ;
SU24 = (SU23 COTE 3) ROTA N 45. P1 ;
L24 = SU24 COTE 2 ;
C24 =  SU24 COTE 4 ;

RAC = L21 ET L22 ET L23 ET L24 ;
SU2 = SU21 ET SU22 ET SU23 ET SU24 ;

SI (EGA REP 0) ;
 SU0 = COUT P1 RAC ;
 SU0L = CHAN SU0 LIGNE ;
 P01 = (P1 PLUS P2) / 2. ;
 LIG0 =  SU0L ELEM APPUYE STRICTEMENT (P1 ET (SU0 POIN PROC P01)
   ET P2)    ;
 LIGAM0 =  LIGAM0 ET LIG0 ;
 L1 = LIGAM0 ;
SINON ;
 DENS TM ;
 QAD = P1 PLUS ((TM * N) 0.) ;
 QAG = P1 MOIN ((TM * N) 0.) ;
 BASD = P1 D N QAD ;
 BASG = P1 D N QAG ;
 BAS = ORDO (BASG ET BASD) ;
 PAVE = BAS TRANS N (0. (TM * N)) ;
 COPAV1 = PAVE COTE 2 ;
 COPAV2 = PAVE COTE 3 ;
 COPAV3 = PAVE COTE 4 ;
 COPAV = COPAV1 ET COPAV2 ET COPAV3 ;
 DEC = (ENTI (N / 1.2)) * (-1) ;
 JONCD = QAD D  P2 ;
 JONCG = QAG D  (SU24 POIN PROC QAG) ;
 SUJON = DALL COPAV JONCD RAC JONCG PLAN ;
 SU0 = SUJON ET PAVE ;
 LIG0 = BASD ET JONCD ;
 LIGAM0 =  LIGAM0 ET LIG0 ;
 L1 = LIGAM0 ;
FINSI ;

*---------------------------------------------*
*** CARRE EXTERIEUR AUTOUR DU FOND DE FISSURE *
*---------------------------------------------*

DENS (2. * TE) ;
PDB = P1 PLUS (C 0.) ;
PDH = P1 PLUS (C C) ;
PMH = P1 PLUS (0. C) ;
PGH = P1 PLUS (((-1) * C) C) ;
PGB = P1 PLUS (((-1) * C) 0.) ;

CARD = PDB D N PDH ;
CARHD = PDH D N PMH ;
CARHG = PMH D N PGH ;
CARG = PGH D N PGB ;

DECC = ((-1) * (ENTI ((C - R) / (1. * TE)))) - 1 ;

DIAD = (C21 POIN INITIAL) D DECC PDB ;
DIADH = (C22 POIN FINAL) D DECC PDH ;
DIAM = (C23 POIN INITIAL) D DECC PMH ;
DIAGH = (C24 POIN FINAL) D DECC PGH ;
DIAG = (C24 POIN INITIAL) D DECC PGB ;

SU3 = DALL C21 DIADH CARD DIAD PLAN;
SU4 = (DALL C22 DIAM CARHD DIADH PLAN) ET
   (DALL C23 DIAGH CARHG DIAM PLAN) ;
SU5 = DALL C24 DIAG CARG DIAGH PLAN ;

CARRE = SU0 ET SU2 ET SU3 ET SU4 ET SU5 ;
LIGAM1 = LIGAM0 ET DIAD ;

*-------------------------*
*** PARTIE METAL DEPOSE   *
*-------------------------*

CLCRITW =  (C < (LCRITW + 0.01)) ET (C > (LCRITW - 0.01)) ;
CLCRITA =  (C < (LCRITA + 0.01)) ET (C > (LCRITA - 0.01)) ;
CLDEP =    (C < (LDEP + 0.01)) ET (C >  (LDEP - 0.01)) ;

SI ((NON CLCRITW ) ET (NON CLCRITA)) ;
* MESS 'CAS GENERAL' ;

 VTL = 0. (LDEP - C) ;
 VTG = (((-1) * (LCRITA - C)) 0.) ;
 VTD =  (LCRITW - C) 0. ;
 NFL = ENTI ((LDEP - C) / (MESU (CARHD ELEM 1) LONG)) ;
 NFG = ENTI ((LCRITA - C) / (MESU (CARG ELEM 1) LONG)) ;
 NFD = ENTI ((LCRITW - C) / (MESU (CARD ELEM 1) LONG)) ;
 SI (NFG EGA 0) ;
  NFG = 1 ;
 SU7 = CARG TRAN NFG VTG ;
 SINON ;
  DENSI = (MESU (CARG ELEM 1) LONG) ;
  DENSF = DENSI * (1 + (NFG / 20.)) ;
  SU7 = CARG TRAN  'DINI' DENSI 'DFIN' DENSF VTG ;
 FINSI ;
 SI (NFD EGA 0) ;
  NFD = 1 ;
  SU6 = CARD TRAN NFD VTD ;
 SINON ;
  DENSI = (MESU (CARD ELEM 1) LONG) ;
  DENSF = DENSI * (1 + (NFD / 20.)) ;
  SU6 = CARD TRAN  'DINI' DENSI 'DFIN' DENSF VTD ;
 FINSI ;
 L8 = (SU7 COTE 4) ET (SU6 COTE 2) ET CARHD ET CARHG ;
 L8 = ORDO L8 ;
 SI (CLDEP) ;
*  MESS 'CAS CRITIQUE LDEP' ;
  MDEP = CARRE ET SU6 ET SU7 ;
  L8B = INVE L8 ;
  LIGAM = LIGAM1 ET (SU6 COTE 4) ;
  LSYM1 = (SU7 COTE 3) ;
 SINON ;
  SI (NFL EGA 0) ;
   NFL = 1 ;
   SU8 = L8 TRAN NFL VTL ;
  SINON ;
   DENSI = (MESU (CARHD ELEM 1) LONG) ;
   DENSF = DENSI * (1 + (NFL / 10.)) ;
   SU8 = L8 TRAN  'DINI' DENSI 'DFIN' DENSF VTL ;
  FINSI ;
  MDEP = CARRE ET SU6 ET SU7 ET SU8 ;
  L8B = SU8 COTE 3 ;
  LIGAM = LIGAM1 ET (SU6 COTE 4) ;
  LSYM1 =  (SU7 COTE 3) ET (SU8 COTE 4) ;
 FINSI ;

FINSI ;

SI (CLCRITW) ;
* MESS 'CAS CRITIQUE W' ;
 VTL = 0. (LDEP - C) ;
 VTT = (((-1) * (LCRITA - C)) 0.) ;
 NFL = ENTI ((LDEP - C) / (MESU (CARHD ELEM 1) LONG)) ;
 NFT = ENTI ((LCRITA - C) / (MESU (CARG ELEM 1) LONG)) ;
 SI (NFT EGA 0) ;
  NFT = 1 ;
  SU7 = CARG TRAN NFT VTT ;
 SINON ;
  DENSI = (MESU (CARG ELEM 1) LONG) ;
  DENSF = DENSI * (1 + (NFT / 20.)) ;
  SU7 = CARG TRAN  'DINI' DENSI 'DFIN' DENSF VTT ;
 FINSI ;
 L8 = (SU7 COTE 4) ET CARHD ET CARHG ;
 L8 = ORDO L8 ;
 SI (CLDEP) ;
*  MESS 'CAS CRITIQUE LDEP ET LCRITW' ;
  MDEP = CARRE ET SU7 ;
  L8B = INVE L8 ;
  LIGAM = LIGAM1 ;
  LSYM1 = (SU7 COTE 3) ;
 SINON ;
  SI (NFL EGA 0) ;
   NFL = 1 ;
   SU8 = L8 TRAN NFL VTL ;
  SINON ;
   DENSI = (MESU (CARHD ELEM 1) LONG) ;
   DENSF = DENSI * (1 + (NFL / 10.)) ;
   SU8 = L8 TRAN  'DINI' DENSI 'DFIN' DENSF VTL ;
  FINSI ;
  MDEP = CARRE ET SU7 ET SU8 ;
  L8B = SU8 COTE 3 ;
  LIGAM = LIGAM1 ;
  LSYM1 =  (SU7 COTE 3) ET (SU8 COTE 4) ;
 FINSI ;
FINSI ;

SI (CLCRITA) ;
* MESS 'CAS CRITIQUE A' ;
 VTL = 0. (LDEP - C) ;
 VTT = (LCRITW - C) 0. ;
 NFL = ENTI ((LDEP - C) / (MESU (CARHD ELEM 1) LONG)) ;
 NFT = ENTI ((LCRITW - C) / (MESU (CARD ELEM 1) LONG)) ;
 SI (NFT EGA 0) ;
  NFT = 1 ;
  SU6 = CARD TRAN NFT VTT ;
 SINON ;
  DENSI = (MESU (CARD ELEM 1) LONG) ;
  DENSF = DENSI * (1 + (NFT / 20.)) ;
  SU6 = CARD TRAN  'DINI' DENSI 'DFIN' DENSF VTT ;
 FINSI ;
 L8 = (SU6 COTE 2) ET CARHD ET CARHG ;
 L8 = ORDO L8 ;
 SI (CLDEP) ;
*  MESS 'CAS CRITIQUE LDEP ET LCRITA' ;
  MDEP = CARRE ET SU6 ;
  L8B = L8 ;
  LIGAM = LIGAM1  ET (SU6 COTE 4) ; ;
  LSYM1 = CARG ;
 SINON ;
  SI (NFL EGA 0) ;
   NFL = 1 ;
   SU8 = L8 TRAN NFL VTL ;
  SINON ;
   DENSI = (MESU (CARHD ELEM 1) LONG) ;
   DENSF = DENSI * (1 + (NFL / 10.)) ;
   SU8 = L8 TRAN  'DINI' DENSI 'DFIN' DENSF VTL ;
  FINSI ;
  MDEP = CARRE ET SU6 ET SU8 ;
  L8B = SU8 COTE 3 ;
  L8B = INVE L8B ;
  LIGAM = LIGAM1 ET (SU6 COTE 4) ;
  LSYM1 = (SU8 COTE 2) ET CARG ;
 FINSI ;
FINSI ;

*-------------------------*
*** PARTIE METAL DE BASE  *
*-------------------------*

L8B = ORDO L8B ;
PPI = L8B POIN INITIAL ;PF = L8B POIN FINAL ;
LU = (MESU L8B LONG) / (NBEL L8B) * 1.2 ;
FRON1 = (PPI PLUS (0. LU)) D (ENTI ((NBEL L8B) / 1.51))
   (PF PLUS (0. LU)) ;
LUB = ((MESU FRON1 LONG) / (NBEL FRON1)) * 1.2 ;
PIB = FRON1 POIN INITIAL ;PFB = FRON1 POIN FINAL ;
FRON2 =  (PIB PLUS (0. LUB)) D (ENTI ((NBEL FRON1) / 1.51))
   (PFB PLUS (0. LUB)) ;

JOIN1 = COUT L8B FRON1 ;
JOIN2 = COUT FRON1 FRON2 ;
JOIN = JOIN1 ET JOIN2 ;
JOINL = CHAN JOIN LIGNE ;
JOINP = JOINL POIN DROIT P0 (P0 PLUS (0. 10.)) (TM / 10.) ;
LSYM2 = JOINL ELEM APPUYE STRICTEMENT JOINP ;

LREST = ((W * RAP2) / 2.) - (LDEP + LU + LUB) ;
VT = 0. LREST ;
LE = MESU (FRON2 ELEM 1) LONG ;
CORP = FRON2 TRAN (ENTI (LREST / LE)) VT ;
LSYM3 = CORP COTE 2 ;
PINIF = LIGAM POIN INITIAL;

*---------------------------*
*PARTICULARITE POUR UN CCP  *
*---------------------------*

SI (VAL EGA 1) ;
 MBAS = CORP ET JOIN ;
*PLAN VERTICALE DE SYMETRIE
 LSYM = LSYM1 ET LSYM2 ET LSYM3 ;
 LSYM = LSYM COUL VERT ;
*FACE DE CHARGEMENT
 SCHAR = CORP COTE 3 ;
 SCHAR = SCHAR COUL ROUG ;
*LIGAMENT
 LIGAM = LIGAM COUL BLEU ;
*MAILLAGE COMPLET
 CCP = MBAS ET MDEP ;
 TITR 'EPROUVETTE CCP' ;
* TRAC (CCP ET LIGAM ET LSYM ET SCHAR) ;
FINSI ;

*----------------------------*
*PARTICULARITE POUR UN SENB  *
*----------------------------*

SI (VAL EGA 0) ;
 LRESID = CORP COTE 3 ;
 RESID = LRESID TRAN 1 (0. LE) ;
 MBAS = CORP ET JOIN ET RESID ;
*APPUI
 PAP = P0 PLUS (0. ((RAP2 / 2.) * W)) ;
 PAP = MBAS POIN PROC PAP ;
 PAP = MANU PAP POI1 VERT ;
*POINT D'APPLICATION DE LA FORCE
 PCHAR = P0 PLUS (W 0.) ;
 PCHAR = MDEP POIN PROC PCHAR ;
 PCHAR = MANU PCHAR POI1 ROUG ;
*LIGAMENT
 LIGAM = LIGAM COUL BLEU ;
*MAILLAGE COMPLET
 SENB = MBAS ET MDEP ;
 TITR 'EPROUVETTE SENB' ;
 TRAC (SENB ET LIGAM ET PAP ET PCHAR) ;
FINSI ;
*
PFISIN = LSYM POIN INIT;
CT1 = CONT CCP;
PSUP = (CT1 DIFF (LIGAM ET LSYM)) POIN DROI PFISIN P1;
CT2 = CHAN MOT_S CT1;
LIFIS1 = CT2 ELEM APPU STRI PSUP;
LLBB = ORDO (LIGAM ET LIFIS1); LLBB = INVE LLBB;
CCP = ORIE CCP ;
*
* PREMIERE COUCHE D'ELEMENT AUTOUR DE LA FISSURE
*
ELEFIS = CCP ELEM MOT_E IELEM;
*
*  FIN DE LA DÉFINITION DE LA GÉOMÉTRIE
*
OPTI DIME 3 MODE TRID;
*
*  COURBE DE TRACTION
*
YOUN1 = 2.E05;
NU1 = 0.3;
ALPH1 = 1.E-6;
YIEL1 = 4.E02;
ALPH1 = 0.5;
NEXPO1 = 7;
SIG0 = 1.E02;
EPS0 = SIG0 / YOUN1;
LSIGM = PROG 0. SIG0;
LEPSI = PROG 0. EPS0;
NPOIN = 50;
EPSTOT = 5.;
SS1 = (EPSTOT*YOUN1/YIEL1/ALPH1)**(1./NEXPO1);
SS1 = YIEL1 * SS1 / NPOIN;
REPETER BC1 NPOIN;
  SIG1 = SS1*&BC1;
  SI (< SIG1 SIG0); ITER BC1; FINSI;
  EPS1 = (((SIG1/YIEL1)**(NEXPO1 - 1))*ALPH1) + 1.;
  EPS1 = EPS1 * SIG1 / YOUN1;
  LSIGM = LSIGM ET (PROG SIG1);
  LEPSI = LEPSI ET (PROG EPS1);
FIN BC1;
TRAC1 = EVOL MANU 'EPSI' LEPSI 'SIGM' LSIGM;
SI (EGA IMATE 2); DESS TRAC1; FINSI;
*
*  EPAISSEUR ET EXCENTREMENT
*
EPAI_I = (EPAI_T - (EPAI_C*NB_MUL))/(NB_MUL - 1);
TBEPAI = TABLE; TBEXCE = TABLE;
REPETER BC_MUL1 ((2*NB_MUL) - 1);
  III = (&BC_MUL1 - NB_MUL) / 2.;
  TBEXCE.&BC_MUL1 = (EPAI_I + EPAI_C)*III;
  SI (EGA 0. ((&BC_MUL1/2.) - (&BC_MUL1/2)) 1.E-10);
    TBEPAI.&BC_MUL1 = EPAI_I;
  SINON;
    TBEPAI.&BC_MUL1 = EPAI_C;
  FINSI;
FIN BC_MUL1;
*
*  DFINITION DES MODLES ET DU MATRIAU
*
SI (EGA IMATE 1);
  MPL1 = TEXT ' '; MTR1 = TEXT ' '; MTR2 = TEXT ' ';
FINSI;
SI (EGA IMATE 2);
  MPL1 = TEXT 'PLASTIQUE'; MTR1 = TEXT 'TRAC'; MTR2 = TRAC1;
  EVO1 = EVOL MANU 'Temps' (PROG 0 1.E10) 'F(t)' (PROG 0 1.E10);
FINSI;
REPETER BC_MUL2 ((2*NB_MUL) - 1);
  SI (EGA &BC_MUL2 1);
    MOD_MUL = MODE ELEFIS MECANIQUE ELASTIQUE
              MPL1 MOT_F1 MOT_F2 CONS (CHAI &BC_MUL2);
    MAT_MUL = MATE MOD_MUL YOUN YOUN1 NU NU1 ALPH ALPH1 MTR1
              MTR2 EPAI TBEPAI.&BC_MUL2 EXCE TBEXCE.&BC_MUL2;
  SINON;
    MOD_MU1 = MODE ELEFIS MECANIQUE ELASTIQUE
              MPL1 MOT_F1 MOT_F2 CONS (CHAI &BC_MUL2);
    MAT_MU1 = MATE MOD_MU1 YOUN YOUN1 NU NU1 ALPH ALPH1 MTR1
              MTR2 EPAI TBEPAI.&BC_MUL2 EXCE TBEXCE.&BC_MUL2;
    MOD_MUL = MOD_MUL ET MOD_MU1;
    MAT_MUL = MAT_MUL ET MAT_MU1;
  FINSI;
FIN BC_MUL2;
MOD_R = MODE (CCP DIFF ELEFIS) MECANIQUE ELASTIQUE MPL1 MOT_F1 MOT_F2;
MAT_R = MATE MOD_R YOUN YOUN1 NU NU1 ALPH ALPH1 MTR1 MTR2 EPAI EPAI_T;
MO1 = MOD_MUL ET MOD_R;
MA0 = MAT_MUL ET MAT_R;
RIG0 = RIGI MO1 MA0;
*
*  DéFINITION DES CONDITIONS AUX LIMITES
*
BL1 = BLOQ UY RX RZ LIGAM;
BL2 = BLOQ UX RY RZ LSYM;
BL3 = BLOQ UZ SCHAR;
BLT = BL1 ET BL2 ET BL3;
*
*  CHARGEMENT : MOMENT DE FLEXION
*
SIGMAX = 10. ;
MOMTOT = SIGMAX * (W * (EPAI_T ** 2) / 6.);
ROTATI = (MOMTOT / (YOUN1 * IX)) * (2 * W);
PINICH = SCHAR POIN INITIAL;
PFINCH = SCHAR POIN FINAL;
SCHAR = SCHAR CHAN POI1;
PMILCH = SCHAR DIFF (PINICH ET PFINCH);
MOEXT = MOMTOT / ((NBNO SCHAR) - 1);
MOMIL = MOEXT * (NBNO PMILCH);
FO1 = (MOME MX MOEXT (PINICH ET PFINCH)) ET (MOME MX MOMIL PMILCH);
FYTOT = SIGMAX * W * EPAI_T;
FYEXT = FYTOT / ((NBNO SCHAR) - 1);
FYMIL = FYEXT * (NBNO PMILCH);
FO2 = (FORC FY FYEXT (PINICH ET PFINCH)) ET (FORC FY FYMIL PMILCH);
LFOR1 = LIFIS1 ELEM APPU LARG P1;
LFOR1 = LIFIS1 ELEM APPU LARG LFOR1;
LFOR1 = LIFIS1 ELEM APPU LARG LFOR1;
FO3 = FORC (0 1000. 800.) (LIFIS1 DIFF LFOR1);
EV1 = EVOL MANU 'X1' (PROG 0 70) (PROG -0.0005 0);
TEMP1 = VARI EV1 (COOR 1 CCP) 'T';
TEMP2 = VARI EV1 (COOR 1 CCP) 'TINF';
TEMP3 = VARI EV1 (COOR 1 CCP) 'TSUP';
TT = TEMP1 ET TEMP2 ET TEMP3;
SIGT1 = THET TT MO1 MA0;
FO4 = BSIG SIGT1 MO1 MA0 ;
SI (EGA ICHAR 1); FOEXT = FO1; FINSI;
SI (EGA ICHAR 2); FOEXT = FO1 ET FO2; FINSI;
SI (EGA ICHAR 3); FOEXT = FO3; FINSI;
SI (EGA ICHAR 4); FOEXT = FO4; FINSI;
SI (EGA ICHAR 5); FOEXT = FO4 ET FO3; FINSI;
*
*  RéSOLUTION
*
SI (EGA IMATE 1);
  DEP1 = RESO (RIG0 ET BLT) FOEXT;
FINSI;
SI (EGA IMATE 2);
  SI (EGA ICHAR 4); FOEXT = FOEXT * 0.; FINSI;
  TBTEMP = TABLE;
  TBTEMP.0 = TT * 0.; TBTEMP.1 = TT * 2.; TBTEMP.2 = TT * 5.;
  TBTEMP.3 = TT * 8.; TBTEMP.4 = TT * 12.;TBTEMP.5 = TT * 20.;
  TBTIME = TABLE;
  TBTIME.0 = 0.; TBTIME.1 = 0.5; TBTIME.2 = 1.3;
  TBTIME.3 = 3.6; TBTIME.4 = 4.7; TBTIME.2 = 8.;
  CHA1 = (CHAR 'MECA' EVO1 FOEXT) ET (CHAR 'T   ' TBTIME TBTEMP);;
  LIST1 = PROG 1. 3. 5;
  TABTOUT = TABLE;
  TABTOUT.BLOCAGES_MECANIQUES = BLT;
  TABTOUT.CARACTERISTIQUES = MA0;
  TABTOUT.MODELE = MO1;
  TABTOUT.CHARGEMENT = CHA1;
  TABTOUT.TEMPS_CALCULES = LIST1;
  TABTOUT.MAXITERATION = 99;
  PASAPAS TABTOUT;
FINSI;
******
****** SOLUTION NUMERIQUE DE G (PROCEDURE G_THETA)
******
SUPTAB = TABLE;
SUPTAB.'OBJECTIF' = MOT 'J';
SUPTAB.'LEVRE_SUPERIEURE' = LIFIS1;
SUPTAB.'FRONT_FISSURE' = P1;
SUPTAB.'ELEMENT_MULTICOUCHE' = ELEFIS ;
*
SI (EGA IMATE 1);
  SUPTAB.'SOLUTION_RESO' = DEP1;
  SUPTAB.'CARACTERISTIQUES' = MA0;
  SUPTAB.'MODELE' = MO1;
  SUPTAB.'CHARGEMENTS_MECANIQUES' = FOEXT;
  SI ((EGA ICHAR 4) OU (EGA ICHAR 5));
    SUPTAB.'TEMPERATURES' = (TEMP1 ET TEMP2 ET TEMP3);
    SI (EGA ICHAR 4);
      SUPTAB.'CHARGEMENTS_MECANIQUES' = FOEXT *0.;
    FINSI;
    SI (EGA ICHAR 5);
      SUPTAB.'CHARGEMENTS_MECANIQUES' = FO3;
    FINSI;
  FINSI;
FINSI;
SI (EGA IMATE 2);
  SUPTAB.'SOLUTION_PASAPAS' = TABTOUT;
FINSI;
NBCOUCH = 1; PC = PROG; PG = PROG;
******
REPETER SUO1 NBCOUCH;
   SUPTAB.'COUCHE' = 6 ;
   G_THETA SUPTAB;
FIN SUO1;
G_COQUE = EXTR SUPTAB.EPAISSEUR_RESULTATS ORDO;
COQUE05 = EXTR G_COQUE 21;
COQUE025 = EXTR G_COQUE 16;
mess '  ' ;
mess '  ' ;
mess '  ' ;
mess ' Integral J par elements coques en peau (z=h/2) :' COQUE05 ;
mess ' Solution SIF par elements massifs en peau : 1.77858E-02 ' ;
mess '  ' ;
mess ' Integral J par elements coques a z=h/4 :' COQUE025 ;
mess ' Solution SIF par elements massifs a z=h/4 : 4.38290E-03 ' ;
*CODE FONCTIONNEMENT ;
ERR1 = ABS ((COQUE05 - 1.77858E-02)/1.77858E-02);
ERR2 = ABS ((COQUE025 - 4.38290E-03)/4.38290E-03);
mess '  ' ;
SI ((ERR1 < 6.5E-2) et (ERR2 < 9.7E-2)) ;
    ERRE  0 ;
SINO ;
    ERRE  5 ;
FINSI ;
fin;















