*
*
* Viscosité dynamique de l'eau
* Valeurs de référence : tables VDI
* Comparaisons de différentes formules et tests de 'VARI' 'MULIQ'
*
*
'OPTI' 'DIME' 2 'ELEM' 'QUA4' 'TRAC' 'PSC' 'EPTR' 10 'DENS' 1. ;
graph = faux ;
*
*- Calcul de la viscosité dynamique de l'eau (formule Cathare)
*
'DEBP' muliqk TW0*'LISTREEL' ;
*
UN  = TW0 / TW0 ;
*TW1 = 273.15 * UN + TW0 ;
*
a0 = 570.58058 * UN ;
b0 = -140.D0 * UN ;
L1 = 2.414D-5 * ('EXP' (a0 / (b0 + TW0))) ;
*
'FINP' L1 ;
*
*- Calcul de la viscosité dynamique de l'eau (formule VDI)
*
'DEBP' muliqv TW0*'LISTREEL' PW0*'FLOTTANT' ;
*
UN  = TW0 / TW0 ;
*TW1 = 273.15 * UN + TW0 ;
PW1 = PW0 * UN ;
*
a1 = 241.4 ;
a2 = 0.3828209486 ;
a3 = 0.2162830218 ;
a4 = 0.1498693949 ;
a5 = 0.4711880117 ;
*
PC0   = 221.2D5 * UN ;
TC0   = (374.15 + 273.15) * UN ;
BETA  = PW1 / PC0 ;
TETA  = TW0 / TC0 ;
PS1   = 'VARI' 'PSATT' TW0 ;
BETAS = PS1 / PC0 ;
*
L1 = (a4 * (BETA - BETAS) * (TETA - (a5 * UN)) + UN) * a1 * 1.D-7 ;
L2 = ('EXP' ( ((a2*UN) / (teta - (a3*un))) * ('LOG' 10.) ) ) * L1 ;
*
'FINP' L2 ;
*
*
*----------------------------------------------
*- Température et pression pour comparaison
*----------------------------------------------
*
*
TW1   = ('PROG' 20. 40. 60. 80. 90.) + 273.15 ;
PW1   = 1.D5 ;
LREF1 = 1.D-6 * ('PROG' 1000. 651. 463. 351. 311.) ;
L10 = MULIQK TW1 ;
L11 = MULIQV TW1 PW1 ;
*
TW2   = ('PROG' 10. 30. 50. 100. 130. 150. 170.) + 273.15 ;
PW2   = 10.D5 ;
LREF2 = 1.D-6 * ('PROG' 1300. 797. 544. 279. 211. 181. 159.) ;
L20 = MULIQK TW2 ;
L21 = MULIQV TW2 PW2 ;
*
TW3   = ('PROG' 10. 50. 100. 150. 200. 220.) + 273.15 ;
PW3   = 25.D5 ;
LREF3 = 1.D-6 * ('PROG' 1300. 544. 279. 182. 134. 122.) ;
L30 = MULIQK TW3 ;
L31 = MULIQV TW3 PW3 ;
*
* Test de VARI avec T sous forme de FLOTTANT
'DEBP' testf t*'LISTREEL' ;
   nb     = 'DIME' t ;
   listmu = 'PROG' ;
   'REPE' blo1 nb ;
      valt   = 'EXTR' t &blo1 ;
      valmu  = 'VARI' 'MULIQ' valt ;
      listmu = listmu 'ET' ('PROG' valmu) ;
   'FIN' blo1 ;
'FINP' listmu ;
test_f1 = testf tw1 ;
test_f2 = testf tw2 ;
test_f3 = testf tw3 ;
*
* Test de VARI avec T sous forme de LISTREEL
test_l1 = vari 'MULIQ' tw1 ;
test_l2 = vari 'MULIQ' tw2 ;
test_l3 = vari 'MULIQ' tw3 ;
*
* Test de VARI avec T sous forme de CHPO
'DEBP' testc t*'CHPOINT' mail1*'MAILLAGE' ;
   test1  = 'VARI' 'MULIQ' t ;
   listmu = 'EXTR' test1 'VALE' mail1 ;
'FINP' listmu ;
P1 = 1. 0. ; P2 = 2. 0. ;
GEO1 = P1 'DROI' 4 P2 ; PT1 = 'CHAN' 'POI1' GEO1 ;
GEO2 = P1 'DROI' 6 P2 ; PT2 = 'CHAN' 'POI1' GEO2 ;
GEO3 = P1 'DROI' 5 P2 ; PT3 = 'CHAN' 'POI1' GEO3 ;
T1 = 'MANU' 'CHPO' PT1 'SCAL' TW1 ;
T2 = 'MANU' 'CHPO' PT2 'SCAL' TW2 ;
T3 = 'MANU' 'CHPO' PT3 'SCAL' TW3 ;
test_c1 = testc t1 pt1 ;
test_c2 = testc t2 pt2 ;
test_c3 = testc t3 pt3 ;
*
* Comparaisons mise en oeuvre Esope/Gibi pour Cathare
d10 = 'EVOL'        'MANU' tw1 (l10 - test_L1 / l10 * 100.) ;
d20 = 'EVOL' 'VERT' 'MANU' tw2 (l20 - test_l2 / l20 * 100.) ;
d30 = 'EVOL' 'ROUG' 'MANU' tw3 (l30 - test_l3 / l30 * 100.) ;
*
*-------------------------------------
*- Sorties Graphiques
*-------------------------------------
*
'SI' graph ;
*
ev10   = 'EVOL'        'MANU' tw1 l10 ;
ev20   = 'EVOL' 'VERT' 'MANU' tw2 l20 ;
ev30   = 'EVOL' 'ROUG' 'MANU' tw3 l30 ;
ev11   = 'EVOL'        'MANU' tw1 l11 ;
ev21   = 'EVOL' 'VERT' 'MANU' tw2 l21 ;
ev31   = 'EVOL' 'ROUG' 'MANU' tw3 l31 ;
evr1   = 'EVOL'        'MANU' tw1 lref1 ;
evr2   = 'EVOL' 'VERT' 'MANU' tw2 lref2 ;
evr3   = 'EVOL' 'ROUG' 'MANU' tw3 lref3 ;
rela10 = 'EVOL'        'MANU' tw1 (l10 - lref1 / lref1 * 100.) ;
rela20 = 'EVOL' 'VERT' 'MANU' tw2 (l20 - lref2 / lref2 * 100.) ;
rela30 = 'EVOL' 'ROUG' 'MANU' tw3 (l30 - lref3 / lref3 * 100.) ;
rela11 = 'EVOL'        'MANU' tw1 (l11 - lref1 / lref1 * 100.) ;
rela21 = 'EVOL' 'VERT' 'MANU' tw2 (l21 - lref2 / lref2 * 100.) ;
rela31 = 'EVOL' 'ROUG' 'MANU' tw3 (l31 - lref3 / lref3 * 100.) ;
*
TAB1 = TABLE ;
TAB1 . 'TITRE' = TABLE ;
TAB1 .  1 = ' MARQ PLEIN ROND NOLI' ;
TAB1 .  2 = ' MARQ PLEIN CARR NOLI' ;
TAB1 .  3 = ' MARQ PLEIN LOSA NOLI' ;
TAB1 .  4 = ' MARQ ROND NOLI' ;
TAB1 .  5 = ' MARQ CARR NOLI' ;
TAB1 .  6 = ' MARQ LOSA NOLI' ;
*
TIT1 = 'Dynamic viscosity of water (Cathare)' ;
TIT2 = ' C' ;
TIT3 = 'kg/m/s' ;
TAB1 . 'TITRE' .  1 = 'MOT' ' 1 bar' ;
TAB1 . 'TITRE' .  2 = 'MOT' '10 bar' ;
TAB1 . 'TITRE' .  3 = 'MOT' '25 bar' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'ref  1' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'ref 10' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'ref 25' ;
'DESS' (ev10 'ET' ev20 'ET' ev30 'ET' evr1 'ET' evr2 'ET' evr3)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Dynamic viscosity of water (VDI)' ;
TIT2 = ' C' ;
TIT3 = 'kg/m/s' ;
TAB1 . 'TITRE' .  1 = 'MOT' ' 1 bar' ;
TAB1 . 'TITRE' .  2 = 'MOT' '10 bar' ;
TAB1 . 'TITRE' .  3 = 'MOT' '25 bar' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'ref  1' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'ref 10' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'ref 25' ;
'DESS' (ev11 'ET' ev21 'ET' ev31 'ET' evr1 'ET' evr2 'ET' evr3)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Dynamic viscosity of water. Relative error of Cathare (x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Cat.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Cat. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Cat. (25 bar)' ;
'DESS' (rela10 'ET' rela20 'ET' rela30)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Dynamic viscosity of water. Relative error of VDI (x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'VDI.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'VDI. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'VDI. (25 bar)' ;
'DESS' (rela11 'ET' rela21 'ET' rela31)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 = 'Dynamic viscosity of water. Relative error (x100).' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Cat.  (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Cat. (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Cat. (25 bar)' ;
TAB1 . 'TITRE' .  4 = 'MOT' 'VDI.  (1 bar)' ;
TAB1 . 'TITRE' .  5 = 'MOT' 'VDI. (10 bar)' ;
TAB1 . 'TITRE' .  6 = 'MOT' 'VDI. (25 bar)' ;
'DESS' (rela10 'ET' rela20 'ET' rela30 'ET'
        rela11 'ET' rela21 'ET' rela31)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
*
TIT1 =
'Esope/Gibi relative error for Cathare formula (x100)' ;
TIT2 = ' C' ;
TIT3 = ' %' ;
TAB1 . 'TITRE' .  1 = 'MOT' 'Esope/Gibi (1 bar)' ;
TAB1 . 'TITRE' .  2 = 'MOT' 'Esope/Gibi (10 bar)' ;
TAB1 . 'TITRE' .  3 = 'MOT' 'Esope/Gibi (25 bar)' ;
'DESS' (d10 'ET' d20 'ET' d30)
'MIMA' 'TITR' TIT1 'TITX' TIT2 'TITY' TIT3 'LEGE' TAB1 ;
'FINSI' ;
*
*----------------------------------------------------------------------
*- Tests de bon fonctionnement
*- (Comparaisons des différents types et de la mise en oeuvre en gibi)
*----------------------------------------------------------------------
*
diff1 = 'MAXI' (test_f1 - test_l1 / l10) 'ABS' ;
diff2 = 'MAXI' (test_f2 - test_l2 / l20) 'ABS' ;
diff3 = 'MAXI' (test_f3 - test_l3 / l30) 'ABS' ;
diff4 = 'MAXI' (test_f1 - test_c1 / l10) 'ABS' ;
diff5 = 'MAXI' (test_f2 - test_c2 / l20) 'ABS' ;
diff6 = 'MAXI' (test_f3 - test_c3 / l30) 'ABS' ;
diff7 = 'MAXI' (test_f1 - l10 / l10)     'ABS' ;
diff8 = 'MAXI' (test_f2 - l20 / l20)     'ABS' ;
diff9 = 'MAXI' (test_f3 - l30 / l30)     'ABS' ;
didi  = diff1 + diff2 + diff3 + diff4 + diff5
      + diff6 + diff7 + diff8 + diff9 ;
'MESS' 'Sommes du max des différences relatives ' didi ;
'SI' (didi > 1.D-14) ;
   'ERRE' 5 ;
'FINSI' ;
*
'FIN' ;



