* fichier :  ktest_io1.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************
**********************************************************
*
* But : Tester le fonctionnement de la sauvegarde
* ---
*
* Jeu de donnees qui cree tous les objets possibles et
* qui les sauve ensuite de 4 manières differentes :
*     - en FORMate et en non-formate (binaire);
*     - en enumerant tous les objets a sauver par leur
*       noms et en utilisant l'operateur SAUV sans
*       arguments (il cherche alors a sauver tous les
*       objets qui se trovent dans la memoire).
*
**********************************************************

   opti dime 2 echo 0 ;

*** sg 2014/11 : nouveaux noms d'inconnues

   'OPTI' 'INCO' 'PP' 'PQ' ;
   'OPTI' 'INCO' ('MOTS' 'QQQQ') ('MOTS' 'QQQQ') ;

*** No  0 : Points ...

   dens 1. ;
   p1 =  0 0 ;
   p2 =  2 0 ;
   p3 = .5 1.5 ;

*** No  1 : Meleme ...

   opti elem seg2 ;
   li1 = p1 d 2 p2 ;
   li2 = p2 d 2 p3 ;
   li3 = p3 d 2 p1 ;
   co1 = li1 et li2 et li3 ;

   opti elem tri3 ;
   su1 = co1 surf plan ;

*** No  4 : Affecte - n'existe plus

*** No  5 : Chamelem - n'existe plus

*** No 21 : Modele - n'existe plus

*** No 38 : Mmodel ...

   mo0 = 'VIDE' 'MMODEL' ;

   mo1 = mode su1 mecanique elastique tri3 ;

*** No 39 : Mchaml ...

   ma1 = mate mo1 youn 2.1e+11 nu 0.3 rho 7850. alph 1.e-5 ;

*** No  3 : Rigidite ...

   ri1 = rigi mo1 ma1 ;
   cl1 = bloq depl li1 ;
   cltot = cl1 ;
   ritot = ri1 et cltot ;
   mas1 = mass mo1 ma1 ;

*** No  2 : Chpoint ...

   fo1 = forc FX 1.e+4 p3 ;
   de1 = reso ritot fo1 ;

*** No  8 : Solution ...

   OPTION epsilon lineaire;
   sol1 = vibr PROCHE (prog 0.) ritot mas1 'SOLU';
*  rem bp : objet SOLUTION = objet amene a disparaitre...

*** No 10 : Table ...

   tab1 = vibr PROCHE (prog 0.) ritot mas1 ;

*** No  9 : Structure ...

   st1 = stru ritot mas1 ;

*** No  7 : Elemstru ...

   elst1 = elst st1 p2 ;

*** No  6 : Bloqstru ...

   blst1 = clst st1 cltot ;

*** No 11 : Maffec - n'existe plus ?????

*** No 12 : Msostu - implicite

*** No 13 : Imatri - implicite

*** No 14 : Mjonct - implicite

*** No 15 : Attache ...

   att1 = jonc elst1 UX (prog 1.) ;

*** No 16 : Mmatri - implicite

*** No 17 : Deforme ...

   def1 = defo li1 de1 1. ;

*** No 18 : Listreel ...

   lr1 = prog 0. 1. ;

*** No 19 : Listentier ...

   le1 = lect 1 ;

*** No 22 : Evolution ...

   ev1 = evol manu 't' lr1 'f(t)' lr1 ;

*** No 20 : Chargement ...

   cha1 = char fo1 ev1 ;

*** No 23 : Superelement ...

   sup1 = supe RIGIDITE ri1 (p1 et p2) ;

*** No 24 : Logique ...

   boo1 = VRAI ;

*** No 25 : Flottant - implicite

   flot1 = pi ;

*** No 26 : Entier - implicite

   ent1 = 732 ;

*** No 27 : Mot - implicite

   mot1 = 'KAKA' ;

*** No 28 : Texte ...

   text1 = text 'Essai de construction d un morceau de texte' ;

*** No 29 : Listmots ...

   lm1 = MOTS UX UY  ;

*** No 30 : Vecteur ...

   ve1 = vect fo1 FX FY 1. ROUG ;

*** No 31 : Vectdoub - n'existe plus ?????

*** No 32 : Points - implicite

*** No 33 : Configuration ...

   conf2 = form de1 ;

*** No 34 : Listchpo ...

   lch1 = suit CHPOINT fo1 de1 ;

*** No 35 : Basemoda ...

   bas1 = BASE st1 att1 sol1 ;

*** No 36 : Procedur - implicite

*** No 40 : Minte - implicite

*** No 41 : Nuage ...

   nu1 = nuag COMP TEMPERATURE pi COMP TRAC ev1 ;

*** No 42 : Matrik - comment le creer (EQEX->EQPR->KMAC) ?????

   kritot = 'KOPS' 'RIMA' ritot ;
   icpri = 'MOTS' 'UX' 'UY' 'LX' ;
   icdua = 'MOTS' 'FX' 'FY' 'FLX' ;
   kritot = 'CHANGER' 'INCO' kritot icpri icpri icdua icpri ;
   kfo1   = 'NOMC' icdua fo1 icpri ;
   kmas1  = 'KOPS' 'RIMA' mas1  ;
   kde1   = 'KRES' kritot kfo1 ;

*** No 37 : Bloc ...

  repeter bloc1 1 ;

*** On testera aussi le fonctionnement de IMPPIL ...

*   opti impi 6 ;

*** Sauvegarde de tous les objets nommes ...

    opti sauv      'testsauv_noms_B.sortgibi' ;
    sauv      p1 p2 li1 mo0 mo1 ma1 ca1 ritot mas1 fo1 de1 sol1 tab1 st1
              elst1 blst1 att1 def1 lr1 le1 ev1 cha1 sup1 boo1 flot1
              ent1 mot1 text1 lm1 ve1 conf2 lch1 bas1 nu1
              kritot kmas1 kde1 bloc1 ;

    opti sauv FORM 'testsauv_noms_F.sortgibi' ;
    sauv FORM p1 p2 li1 mo0 mo1 ma1 ca1 ritot mas1 fo1 de1 sol1 tab1 st1
              elst1 blst1 att1 def1 lr1 le1 ev1 cha1 sup1 boo1 flot1
              ent1 mot1 text1 lm1 ve1 conf2 lch1 bas1 nu1
              kritot kmas1 kde1 bloc1 ;

*** Sauvegarde de tous les objets dans la memoire ...

    opti sauv      'testsauv_glob_B.sortgibi' ;
    sauv      ;

    opti sauv FORM 'testsauv_glob_F.sortgibi' ;
    sauv FORM ;

  fin bloc1 ;

fin ;













