* fichier :  ther51.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

SAUT PAGE ;
*-------------------------------------------------------------------*
* TEST THER51                                                       *
*                                                                   *
*    TEST DES CL DE TEMPERATURE POUR L'ELEMENT COQ2                 *
*    ------------------------------------------------------------   *
*                                                                   *
*               TEMPERATURE IMPOSEE + CONVECTION                    *
*                                   + FLUX                          *
*                                   + SOURCE                        *
*                                                                   *
* Ce test permet de v{rifier le bon fonctionnement des divers       *
* op{rateurs thermiques de CASTEM2000 dans le cas des {l{ments      *
* coq2 .                                                            *
*                                                                   *
* Un cylindre modelis{ par des {L{ments coq2 est soumis @ une       *
*  temp{rature impos{e @ une de ses extr{mit{s et successivement    *
* @ une condition de convection forc{e, @ une condition de flux     *
* impos{ et @ une condition de source volumique impos{e.            *
*                                                                   *
* Les r{sultats sont compares a ceux obtenus                        *
* par un calcul {quivalent utilisant les {l{ments massifs.  En      *
* particulier on compare la temperature des points situ{s a la face *
* exterieure du cylindre .                                          *
* Test elabore par Mr Dowlatyari 03/91,                             *
* Test modifie le 13/05/92                                          *
*-------------------------------------------------------------------*
opti echo 1;
graph = 'N';
opti trac bens;
SAUT PAGE ;
**********************************************************************  -
*************** 1/CALCUL EN ELEMENTS MASSIFS  ************************
**********************************************************************


OPTION DIME 2 ELEM QUA4 MODE AXISYMETRIQUE ;
TEMPS ;
*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
A1 = 19.75  0. ;  A2 = 20.25 0.;
B1 = 19.75  2. ;  B2 = 20.25 2.;
C1 = 19.75  4. ;  C2 = 20.25 4.;
D1 = 19.75  6. ;  D2 = 20.25 6.;
*
*-------------  CREATION DES SEGMENTS ET RECTANGLES --------------------
*
L1 = A2 D 1 B2;
L2 = B2 D 1 C2;
L3 = C2 D 1 D2;
*
LTOT = L1 ET L2 ET L3 ;
LINF = A1 D 1 A2;
RECSUP = MANU QUA4 C1 C2 D2 D1;
RECMI  = MANU QUA4 B1 B2 C2 C1;
RECINF = MANU QUA4 A1 A2 B2 B1;
*
LTOT = RECSUP ET RECMI ET RECINF;
si (NEG GRAPH 'N');
  TRAC LTOT;
finsi;
L_COT= L3 ;
* ATTENTION : il faut r{orienter les {l{ments (OPERATEUR ORIENT)
* si on n'est pas sºr que tous les {l{ments sont orient{s d'une
* fa\on coh{rante
*
*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE LTOT THERMIQUE ISOTROPE ;
MOD2 = MODE L_COT thermique  CONVECTION  ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE LINF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE LINF  'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE LINF  'T   ' ;
EE3 = DEPI BB3 10. ;
BB=BB1 ET BB2 ET BB3 ;
EE=EE1 ET EE2 ET EE3 ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'T' 200. ;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200. L_COT ;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1 1000. RECSUP;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET BB ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
*---------------- POST-TRAITEMENT:
*---------------  EXTRACTION DES VALEURS CARACTERISTIQUES ------------
*-------------ET TRACE DES COURBES COMPARATIVES------------------------
*


*LISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
*T1A1 = EXTR CHTER1 T A1 ;T1B1 =EXTR CHTER1 T B1 ;
*T1C1 = EXTR CHTER1 T C1 ;T1D1 =EXTR CHTER1 T D1 ;
*T2A1 = EXTR CHTER2 T A1 ;T2B1 =EXTR CHTER2 T B1 ;
*T2C1 = EXTR CHTER2 T C1 ;T2D1 =EXTR CHTER2 T D1 ;
*T3A1 = EXTR CHTER3 T A1 ;T3B1 =EXTR CHTER3 T B1 ;
*T3C1 = EXTR CHTER3 T C1 ;T3D1 =EXTR CHTER3 T D1 ;

T1A2 = EXTR CHTER1 T A2 ;T1B2 =EXTR CHTER1 T B2 ;
T1C2 = EXTR CHTER1 T C2 ;T1D2 =EXTR CHTER1 T D2 ;
T2A2 = EXTR CHTER2 T A2 ;T2B2 =EXTR CHTER2 T B2 ;
T2C2 = EXTR CHTER2 T C2 ;T2D2 =EXTR CHTER2 T D2 ;
T3A2 = EXTR CHTER3 T A2 ;T3B2 =EXTR CHTER3 T B2 ;
T3C2 = EXTR CHTER3 T C2 ;T3D2 =EXTR CHTER3 T D2 ;

*T1AM = ( T1A2 + T1A1)/2;T1BM = ( T1B2 + T1B1)/2;
*T1CM = ( T1C2 + T1C1)/2;T1DM = ( T1D2 + T1D1)/2;
*T2AM = ( T2A2 + T2A1)/2;T2BM = ( T2B2 + T2B1)/2;
*T2CM = ( T2C2 + T2C1)/2;T2DM = ( T2D2 + T2D1)/2;
*T3AM = ( T3A2 + T3A1)/2;T3BM = ( T3B2 + T3B1)/2;
*T3CM = ( T3C2 + T3C1)/2;T3DM = ( T3D2 + T3D1)/2;






TCON = PROG T1A2 T1B2 T1C2 T1D2;
*LIST TCON;
TFLUX = PROG T2A2 T2B2 T2C2 T2D2;
*LIST TFLUX;
TSOU = PROG T3A2 T3B2 T3C2 T3D2;
*LIST TSOU;

*********************************************************************
***************** 2/ CALCUL EN COQUES  ******************************
*********************************************************************


*
TITRE 'C.L. DE TEMPERATURE - ELEMENTS  COQ2' ;
OPTION DIME 2 ELEM SEG2 MODE AXISYMETRIQUE ;
TEMPS ;
*
*-------------  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS --
*
A1 = 20.  0. ;
B1 = 20.  2. ;
C1 = 20.  4. ;
D1 = 20.  6. ;
*
*-------------  CREATION DES SEGMENTS ---------------------------------
*
L1= MANU SEG2 A1  B1 ;
L2 = MANU SEG2 B1 C1;
L3 = MANU SEG2 C1  D1 ;
*
LTOT = L1 ET L2 ET L3 ;

*
*
P_INF = A1 ;
*
L_COT= L3 ;
* ATTENTION : il faut r{orienter les {l{ments (OPERATEUR ORIENT)
* si on n'est pas sºr que tous les {l{ments sont orient{s d'une
* fa\on coh{rante
*
*--------------- DONNEES DU PROBLEME DE THERMIQUE -----------------------
*
*--------------  MODELISATION -------------------------------------------
*
MOD1 = MODE LTOT THERMIQUE ISOTROPE COQ2 ;
MOD2 = MODE L_COT thermique CONVECTION 'SUPERIEURE' COQ2 ;
*
*--------------- DONNEES DES CARACTERISTIQUES DU MATERIAU ---------------
*
MAT1 = MATE MOD1 'K' 100. 'EPAI' 0.5 ;
MAT2 = MATE MOD2 'H' 15. ;
*
*--------------- CREATION DES MATRICES DE CONDUCTIVITE ------------------
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;
*
*--------------- TEMPERATURES IMPOSEES: BLOQUE + DEPI -------------------
*
BB1 = BLOQUE P_INF 'TINF' ;
EE1 = DEPI BB1 10. ;
BB2 = BLOQUE P_INF  'TSUP' ;
EE2 = DEPI BB2 10. ;
BB3 = BLOQUE P_INF  'T   ' ;
EE3 = DEPI BB3 10. ;
BB=BB1 ET BB2 ET BB3 ;
EE=EE1 ET EE2 ET EE3 ;
*
*--------------- FLUX EQUIVALENTS A LA CONDITION DE CONVECTION ----------
*
CNV1 = CONVECTION MOD2 MAT2 'TSUP' 200. ;
*
*--------------- FLUX IMPOSE --------------------------------------------
*
FLU1 = FLUX MOD1 200. L_COT 'SUPE' ;
*
*--------------- SOURCE DE CHALEUR --------------------------------------
*
S1 = SOURCE MOD1 MAT1 1000. L3   ;
*
*---------------- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -----------------
*
CCC1 = CND1 ET CND2 ET BB ;CCC2 = CND1 ET BB ;
*
*---------------- ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS -------------
*
FFF1 = EE ET CNV1 ;FFF2 = EE ET FLU1 ;FFF3 = EE ET S1 ;
*
*---------------- 1ERE RESOLUTION : CONVECTION --------------------------
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---------------- 2EME RESOLUTION : FLUX --------------------------------
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---------------- 3EME RESOLUTION: SOURCE -------------------------------
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
*---------------- POST-TRAITEMENT:
*---------------  EXTRACTION DES VALEURS CARACTERISTIQUES ------------
*-------------ET TRACE DES COURBES COMPARATIVES------------------------
*
*
*ISTE CHTER1 ;
*LISTE CHTER2 ;
*LISTE CHTER3 ;
T1A = EXTR CHTER1 T A1 ;T1B=EXTR CHTER1 T B1 ;
T1C = EXTR CHTER1 T C1 ;T1D=EXTR CHTER1 T D1 ;
T2A = EXTR CHTER2 T A1 ;T2B=EXTR CHTER2 T B1 ;
T2C = EXTR CHTER2 T C1 ;T2D=EXTR CHTER2 T D1 ;
T3A = EXTR CHTER3 T A1 ;T3B=EXTR CHTER3 T B1 ;
T3C = EXTR CHTER3 T C1 ;T3D=EXTR CHTER3 T D1 ;


******************************************************************
************** 3/ COMPARAISON ENTRE LES DEUX CALCULS *************
******************************************************************



DIST = PROG 0. 2. 4. 6. ;

si (NEG GRAPH 'N');
TEMPC1= PROG T1A T1B T1C T1D ;
EV1R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TCON ;
EV1C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC1 ;
TAB = 'TABLE' ;
TAB.1 = 'MARQ PLUS' ;
TAB.2 = 'MARQ ETOI' ;
TITRE 'ELEMENT COQ2 ;TEMPERATURE + CONVECTION ;+ : MASSIF,* : COQUE' ;
DESSIN TAB (EV1R ET EV1C )  ;

TEMPC2= PROG T2A T2B T2C T2D ;
EV2R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TFLUX;
EV2C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC2 ;
TITRE 'ELEMENT COQ2 ;TEMPERATURE + FLUX IMPOSE ;+ : MASSIF,* : COQUE';
DESSIN TAB (EV2R ET EV2C )  ;

TEMPC3= PROG T3A T3B T3C T3D ;
EV3R = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TSOU;
EV3C = EVOL MANU 'DISTANCE' DIST 'TEMPERATURE' TEMPC3 ;
TITRE 'ELEMENT COQ2 ;TEMPERATURE + SOURCE ;+  MASSIF,* : COQUE ';
DESSIN TAB (EV3R ET EV3C )  ;
finsi;
******************************************************************
***************** 4/ TEST DE FONCTIONNEMENT **********************
******************************************************************

ERCA= ABS (( T1A-T1A2)/T1A);
ERCB= ABS (( T1B-T1B2)/T1B);
ERCC= ABS (( T1C-T1C2)/T1C);
ERCD= ABS (( T1D-T1D2)/T1D);
ERFA= ABS (( T2A-T2A2)/T2A);
ERFB= ABS (( T2B-T2B2)/T2B);
ERFC= ABS (( T2C-T2C2)/T2C);
ERFD= ABS (( T2D-T2D2)/T2D);
ERSA= ABS (( T3A-T3A2)/T3A);
ERSB= ABS (( T3B-T3B2)/T3B);
ERSC= ABS (( T3C-T3C2)/T3C);
ERSD= ABS (( T3D-T3D2)/T3D);

ERTOT = PROG ERCA ERCB ERCC ERCD ERFA ERFB ERFC  ERFD ERSA ERSB
ERSC ERSD;
ERMAX = MAXI ERTOT;
temps;
SI ( ERMAX <EG 0.05 );
     ERRE 0;
SINON;
     ERRE 5;
FINSI;

FIN;















