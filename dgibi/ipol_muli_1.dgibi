OPTI 'DIME' 3 'ELEM' 'CUB8' 'ECHO' 0 ;
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************

************************************************************************
*                 Test de l'operateur IPOL option GRILL                *
*     interpolation multi-lineaire d'une fonction de n parametres      *
*                   definie sur une grile de points                    *
*                                                                      *
* - test avec fonction de 1, 2, 3 et 4 variables                       *
* - test de points ordinaires et singuliers (sur et hors de la grille) *
* - test avec des CHPOINTs                                             *
************************************************************************


** Tolerance pour les tests sur les valeurs interpolees
tol1 = 1.E-14 ;
MESS ; MESS ; MESS ;


** TEST en dimension 1 (interpolation lineaire)
** --------------------------------------------

** Grille de valeurs de la fonction
lyref = PROG -1.2  0.  0.2  0.6  1.  1.5  2.8 ;
lfref = PROG  0.  1. 2.  3.  1. -1.  1.5 ;

** Interpolation (sur un CHPOINT puis un MCHAML)
MESS 'Interpolation lineaire' ;
MESS ;
p1 = 0. -2. 0. ;
p2 = 0.  3.5  0. ;
mail1 = DROI 200 p1 p2 ;
chy = COOR 2 mail1 ;
chp1 = NOMC 'TOTO' chy ;
nu1 = NUAG 'COMP' 'TOTO' lyref
           'COMP' 'FONC' lfref ;
chp2 = IPOL chp1 nu1 'GRILL' ;
mo = MODE mail1 'MECANIQUE' ;
che1 = CHAN 'CHAM' mo chp1  ;
che2 = IPOL che1 nu1 'GRILL' ;

** Test des valeurs interpolees sur le CHPOINT
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
mailp1 = CHAN 'POI1' mail1 ;
REPE b1 (NBEL mailp1) ;
  pt1 = mailp1 POIN &b1 ;
  fc = EXTR chp2 'FONC' pt1 ;
  y1 = EXTR chp1 'TOTO' pt1 ;
  ft = IPOL y1 lyref lfref ;
  SI (EGA ft 0.) ;
    xdiv = 1. ;
  SINON ;
    xdiv = ft ;
  FINSI ;
  err1 = ABS ((fc - ft) ) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
MESS ;

** Test des valeurs interpolees sur le MCHAML
MESS ' Element| Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        | support|   theorique    |   interpolee   |       ' ;
MESS '--------|--------|----------------|----------------|-------' ;
REPE b1 (NBEL mail1) ;
  REPE b2 2 ;
    fc = EXTR che2 'FONC' 1 &b1 &b2 ;
    y1 = EXTR che1 'TOTO' 1 &b1 &b2 ;
    ft = IPOL y1 lyref lfref ;
    SI (EGA ft 0.) ;
      xdiv = 1. ;
    SINON ;
      xdiv = ft ;
    FINSI ;
    err1 = ABS ((fc - ft) / xdiv) ;
    MESS &b1 '|' &b2 '|' ft '|' fc '|' err1 ;
    SI (err1 > tol1) ;
      MESS ; MESS ;
      MESS 'ECHEC DU CAS TEST !' ;
      ERREUR 4 ;
    FINSI ;
  FIN b2 ;
FIN b1 ;

MESS ; MESS ; MESS ;




** TEST en dimension 2 (interpolation bi-lineaire)
** -----------------------------------------------

** Grille de valeurs de la fonction
lxref = PROG -5. -1.  2.  6. ;
lyref = PROG -3.  1.5 4. ;
lfref = PROG 0. 3. 6. 9.   1. 4. 7. 10.   2. 5. 8. 11. ;

** Interpolation (sur un CHPOINT)
MESS 'Interpolation bi-lineaire' ;
MESS ;
p1 = -4. 1. 0. ;
p2 = 1. 3. 0. ;
p3 = 4.5 -2.5 0. ;
p4 = -90. 0. 0. ;
p5 = -90. -1000. 0. ;
p6 = 0. 4. 0. ;
p7 = 2. 1.5 0. ;
p8 = 6. 4. 0. ;
p9 = -5. -3. 0. ;
mail1 = p1 ET p2 ET p3 ET p4 ET p5 ET p6 ET p7 ET p8 ET p9 ;
chx chy chz = COOR mail1 ;
chp1 = (NOMC 'CAVA' chy) ET (NOMC 'SALU' chx) ;
nu1 = NUAG 'COMP' 'SALU' lxref
           'COMP' 'CAVA' lyref
           'COMP' 'BIEN' lfref ;
chp2 = IPOL chp1 nu1 'GRILL' ;

** Test des valeurs interpolees sur le CHPOINT
tp = TABL ;
tf = TABL ;
tp . 1 = p1 ;
tf . 1 = ((0.*1.5) + (1.*12.) + (3.*0.5) + (4.*4.)) / 18. ;
tp . 2 = p2 ;
tf . 2 = ((4.*1.) + (5.*1.5) + (7.*2.) + (8.*3.)) / 7.5 ;
tp . 3 = p3 ;
tf . 3 = ((6.*6.) + (7.*0.75) + (9.*10.) + (10.*1.25)) / 18. ;
tp . 4 = p4 ;
tf . 4 = ((0.*1.5) + (1.*3.)) / 4.5 ;
tp . 5 = p5 ;
tf . 5 = 0. ;
tp . 6 = p6 ;
tf . 6 = ((5.*2.) + (8.*1.)) / 3. ;
tp . 7 = p7 ;
tf . 7 = 7. ;
tp . 8 = p8 ;
tf . 8 = 11. ;
tp . 9 = p9 ;
tf . 9 = 0. ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp2 'BIEN' pt ;
  SI (EGA ft 0.) ;
    xdiv = 1. ;
  SINON ;
    xdiv = ft ;
  FINSI ;
  err1 = ABS ((fc - ft) / xdiv) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;

MESS ; MESS ; MESS ;




** TEST en dimension 3 (interpolation tri-lineaire)
** ------------------------------------------------

** Grille de valeurs de la fonction
lxref = PROG -5. -1.  2.  6. ;
lyref = PROG -3.  1.5 4. ;
lzref = PROG 0. 10. ;
lfref = PROG 0. 3.  6. 9.     1. 4.  7. 10.    2. 5.  8. 11.
           -11. 6. -3. 0.   -10. 7. -4.  1.   -9. 8. -5.  2. ;


** Interpolation (sur un CHPOINT)
MESS 'Interpolation tri-lineaire' ;
MESS ;
p1  = -4. 1. 0. ;
p2  = 1. 3. 0. ;
p3  = 4.5 -2.5 0. ;
p4  = -90. 0. 0. ;
p5  = -90. -1000. 0. ;
p6  = 0. 4. 0. ;
p7  = 2. 1.5 0. ;
p8  = 6. 4. 0. ;
p9  = -5. -3. 0. ;
p10 = -6. -4. -2. ;
p11 = 2. 1.5 10. ;
p12 = 0.5 -0.75 10. ;
p13 = 1. 3. 7. ;
mail1 = p1 ET p2 ET p3 ET p4 ET p5 ET p6 ET p7 ET p8 ET p9 ET p10 ET
        p11 ET p12 ET p13 ;
chx chy chz = COOR mail1 ;
chp1 = (NOMC 'GROS' chy) ET (NOMC 'CAVA' chz) ET (NOMC 'WESH' chx) ;
nu1 = NUAG 'COMP' 'WESH' lxref
           'COMP' 'GROS' lyref
           'COMP' 'CAVA' lzref
           'COMP' 'COOL' lfref ;
chp2 = IPOL chp1 nu1 'GRILL' ;

** Test des valeurs interpolees sur le CHPOINT
tp = TABL ;
tf = TABL ;
tp . 1 = p1 ;
tf . 1 = ((0.*1.5) + (1.*12.) + (3.*0.5) + (4.*4.)) / 18. ;
tp . 2 = p2 ;
tf . 2 = ((4.*1.) + (5.*1.5) + (7.*2.) + (8.*3.)) / 7.5 ;
tp . 3 = p3 ;
tf . 3 = ((6.*6.) + (7.*0.75) + (9.*10.) + (10.*1.25)) / 18. ;
tp . 4 = p4 ;
tf . 4 = ((0.*1.5) + (1.*3.)) / 4.5 ;
tp . 5 = p5 ;
tf . 5 = 0. ;
tp . 6 = p6 ;
tf . 6 = ((5.*2.) + (8.*1.)) / 3. ;
tp . 7 = p7 ;
tf . 7 = 7. ;
tp . 8 = p8 ;
tf . 8 = 11. ;
tp . 9 = p9 ;
tf . 9 = 0. ;
tp . 10 = p10 ;
tf . 10 = 0. ;
tp . 11 = p11 ;
tf . 11 = -4. ;
tp . 12 = p12 ;
tf . 12 = (6. + 7. - 3. - 4.) / 4. ;
tp . 13 = p13 ;
tf . 13 = ((4. * (1. * 1.  * 3.)) + ( 7. * (2. * 1.  * 3.)) +
           (5. * (1. * 1.5 * 3.)) + ( 8. * (2. * 1.5 * 3.)) +
           (7. * (1. * 1.  * 7.)) + (-4. * (2. * 1.  * 7.)) +
           (8. * (1. * 1.5 * 7.)) + (-5. * (2. * 1.5 * 7.)))
          / (3. * 2.5 * 10.) ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp2 'COOL' pt ;
  SI (EGA ft 0.) ;
    xdiv = 1. ;
  SINON ;
    xdiv = ft ;
  FINSI ;
  err1 = ABS ((fc - ft) / xdiv) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;

MESS ; MESS ; MESS ;







** TEST en dimension 4 (interpolation quadr-lineaire)
** --------------------------------------------------

** Grille de valeurs de la fonction
ltref = PROG 0. 100. ;
lzref = PROG 0. 10. ;
lyref = PROG 0. 2.5 ;
lxref = PROG 0. 3. ;
lfref = PROG 10.   90.      50. -200.
            -20. -113.     -60.    0.
             30.    8.5     70.    3.
            -40.    3.14   -80.   28. ;

** Interpolation (sur un CHPOINT)
MESS 'Interpolation quadri-lineaire' ;
MESS ;
p1 = 0. 0. 0. ;
p2 = 1. 0. 0. ;
p3 = 2. 0. 0. ;
mail1 = p1 ET p2 ET p3 ;
chp1 = MANU 'CHPO' mail1 4 'Y' (PROG  1.5  0.   2.)
                           'Z' (PROG  7.   0.   2.)
                           'X' (PROG  2.   0.   3.)
                           'T' (PROG 71.7  8.  42.) ;
nu1 = NUAG 'COMP' 'T'    ltref
           'COMP' 'Z'    lzref
           'COMP' 'Y'    lyref
           'COMP' 'X'    lxref
           'COMP' 'SCAL' lfref ;
chp2 = IPOL chp1 nu1 'GRILL' ;

** Test des valeurs interpolees sur le CHPOINT
tp = TABL ;
tf = TABL ;
tp . 1 = p1 ;
tempo1 = (( 10. * (1. * 1.  * 3.)) + ( 30. * (2. * 1.  * 3.)) +
          (-20. * (1. * 1.5 * 3.)) + (-40. * (2. * 1.5 * 3.)) +
          ( 50. * (1. * 1.  * 7.)) + ( 70. * (2. * 1.  * 7.)) +
          (-60. * (1. * 1.5 * 7.)) + (-80. * (2. * 1.5 * 7.)))
          / (3. * 2.5 * 10.) ;
tempo2 = (( 90.  * (1. * 1.  * 3.)) + ( 8.5 * (2. * 1.  * 3.)) +
          (-113. * (1. * 1.5 * 3.)) + (3.14 * (2. * 1.5 * 3.)) +
          (-200. * (1. * 1.  * 7.)) + (  3. * (2. * 1.  * 7.)) +
          (   0. * (1. * 1.5 * 7.)) + ( 28. * (2. * 1.5 * 7.)))
          / (3. * 2.5 * 10.) ;
tf . 1 = ((28.3 * tempo1) + (71.7 * tempo2)) / 100. ;
tp . 2 = p2 ;
tf . 2 = ((10. * 92.) + (90. * 8.)) / 100. ;
tp . 3 = p3 ;
tempo1 = ((30. * (0.5 * 8.)) + (-40. * (2. * 8.)) +
          (70. * (0.5 * 2.)) + (-80. * (2. * 2.))) / (2.5 * 10.) ;
tempo2 = ((8.5 * (0.5 * 8.)) + (3.14 * (2. * 8.)) +
          (3.  * (0.5 * 2.)) + (28.  * (2. * 2.))) / (2.5 * 10.) ;
tf . 3 = ((58. * tempo1) + (42. * tempo2)) / 100. ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp2 'SCAL' pt ;
  SI (EGA ft 0.) ;
    xdiv = 1. ;
  SINON ;
    xdiv = ft ;
  FINSI ;
  err1 = ABS ((fc - ft) / xdiv) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;

MESS ; MESS ; MESS ;




MESS 'SUCCES DU CAS TEST !' ;


FIN ;





