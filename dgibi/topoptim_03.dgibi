* fichier topoptim_03.dgibi
************************************************************************
************************************************************************

************************************************************************
** Topology optimization of a simple 2D structure subjected to a
** thermal loading.
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V2.0 2017/04/18 Updated to make it compatible with TOPOPTIM V2.0
** V1.0 2014/04/28 Original version compatible with TOPOPTIM V1.0
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'ELEM' QUA4 ;

* Mesh
p0 = 0.0 0.0 ;
p1 = 0.0 40.0 ;
p2 = 40.0 0.0 ;
lgn0 = DROI 40 p0 p1 ;
msh0 = TRAN lgn0 40 p2 ;
p2 = msh0 POIN 'PROC' p2 ;
lgn1 = lgn0 PLUS p2 ;
ELIM (CONT msh0) lgn1 0.01 ;
lgn2 = DROI 9 (0.0 18.0) (0.0 22.0) ;
ELIM lgn0 lgn2 0.01 ;

* Model and material
mod0 = MODE msh0 'THERMIQUE' 'CONDUCTION' ;
mat0 = MATE mod0 'K' 50.0 ;

* Boundary conditions and loading
bc0 = BLOQ 'T' lgn2 ;
load0 = SOUR mod0 0.01 msh0 ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ;
mdl0.'BLOCAGES_THERMIQUES' = bc0 ;
mdl0.'CHARGEMENT'          = load0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_LINEAIRE' = mdl0 ;
tab0.'RAPPORT_RAIDEURS_THERMIQUES' = 26.0e-3 / 50.0 ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Plot to screen
topo0 = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
SI graph0 ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;



