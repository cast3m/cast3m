* fichier :  gdef1.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
********************************************************************
*                                                                  *
*  TEST GDEF1                                                      *
*                                                                  *
*  CISAILLEMENT PUR EN GRANDES DEFORMATIONS ELASTIQUES             *
*                                                                  *
*      On compare avec les valeurs obtenues a la solution          *
*      analytique                                                  *
*                                                                  *
********************************************************************
*
OPTI ECHO 0;
TITRE   ' TEST GDEF1  ' ;
GRAPH = 'N';
OPTI DIME 2 ELEM QUA4 MODE PLAN CONT;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon JAUMANN;

*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*
P1 = 0 0 ;
P2 = 1. 0. ;
P3 = 1. 1. ;
P4 = 0. 1. ;
L1 = P1 D 1 P2 ;
L2 = P2 D 1 P3;
L3 = P3 D 1 P4 ;
L4 = P4 D 1 P1 ;
SU = DALLER L1 L2 L3 L4 ;
SI (NEG GRAPH 'N');
   TRAC SU    ;
FINSI;
*
*==================================================================*
*           modele - materiau - caracteristiques en Pa             *
*                      conditions aux limites                      *
*==================================================================*
*
MO = MODE SU MECANIQUE ELASTIQUE  ;
YU = 1000. ;   XNU = 0.3 ;
DEUG = YU / ( 1. + XNU ) ;
MA = MATE MO YOUN YU     NU XNU ;
BL1 = BLOQ  DEPL P1 ;
BL2 = BLOQ  DEPL P2 ;
BL3 = BLOQ UX L3 ;
BL4 = BLOQ UY L3 ;
FF = DEPI BL3 1. ;
*
*==================================================================*
*         Initialisation de la table pour appel a PASAPAS          *
*==================================================================*
*
TAB1 = TABLE;
tab1.grands_deplacements=vrai;

TAB1 . BLOCAGES_MECANIQUES= BL1 ET BL2 ET BL3 ET BL4 ;
TAB1 . MODELE = MO;
TAB1 . CARACTERISTIQUES = MA ;
EV1 = EVOL MANU X (PROG 0  5 10 15 20 )
 Y  (PROG 0  5 10 5 0   );
TAB1 . CHARGEMENT = CHAR MECA FF EV1 ;
TAB1 . TEMPS_CALCULES = PROG 0.2   PAS 0.2 8;
TMASAU=table;
tab1 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
*TMASAU .'DEFLO'=VRAI;
PASAPAS TAB1 ;

*dep=tab1.deplacements; co = tab1.contraintes ;
*list co.1;
*list co.2;
*fin;

*
*==================================================================*
*                   affichage des resultats                        *
*==================================================================*
*
MESS ' RESULTATS TEST GDEF1 ';
MESS ' -------------------- ';
SAUT 2 LIGN;
MESS '   On cherche les déformations';
MESS '   Elles sont comparees a des valeurs de reference ';
SAUT 2 LIGN;
*
*----------- r{sultats --------------------------------------------------
*
opti echo 1;
AA = TAB1 . DEPLACEMENTS ;
BB = TAB1 . CONTRAINTES  ;
CC = TAB1 . DEFORMATIONS ;
N = DIME AA ;
SP11= PROG ;
SP12= PROG ;
SP22= PROG ;
EP11= PROG ;
EP12= PROG ;
EP22= PROG ;
EPTH11= PROG ;
EPTH12= PROG ;
EPTH22= PROG ;
EP = PROG ;
EP = TAB1 . TEMPS_CALCULES ;
REPE BOU (N - 1 ) ;
CSX = CHAN CHPO  MO ( BB . &BOU ) ;
ESX = CHAN CHPO  MO ( CC . 0 ) ;
S11 = EXTR CSX SMXX P1 ;
SP11 = SP11 ET ( PROG S11 ) ;
S22 = EXTR CSX SMYY P1 ;
SP22 = SP22 ET ( PROG S22 ) ;
S12 = EXTR CSX SMXY P1 ;
SP12 = SP12 ET ( PROG S12 ) ;
E11 = EXTR ESX EPXX P1 ;
EP11 = EP11 ET ( PROG E11 ) ;
E22 = EXTR ESX EPYY P1 ;
EP22 = EP22 ET ( PROG E22 ) ;
E12 = EXTR ESX GAXY P1 ;
EP12 = EP12 ET ( PROG (E12 * 0.5) ) ;
DX = ( EXTR ( AA . &BOU ) UX P3) * 57.29577951 ;
CDX = 0.5 * ( 1. - ( COS  DX ) ) ;
SDX =  0.5 * (SIN DX) ;
EPTH11= EPTH11 ET ( PROG CDX ) ;
EPTH12= EPTH12 ET ( PROG  SDX ) ;
EPTH22= EPTH22 ET ( PROG ( -1. * CDX ) ) ;
*EX = LOG ( 1. + DX)   ;
*EP = EP ET ( PROG EX ) ;
FIN BOU ;
SP11 = SP11 / DEUG ;
SP12 = SP12 / DEUG ;
SP22 = SP22 / DEUG ;
EV11 = EVOL ROSE  MANU 'DEPLA' EP 'SIG11' SP11 ;
EV12 = EVOL ROSE  MANU 'DEPLA' EP 'SIG12' SP12 ;
EV22 = EVOL ROSE  MANU 'DEPLA' EP 'SIG22' SP22 ;
FV11 = EVOL ROSE  MANU 'DEPLA' EP 'EPS11' EP11 ;
FV12 = EVOL ROSE  MANU 'DEPLA' EP 'EPS12' EP12 ;
FV22 = EVOL ROSE  MANU 'DEPLA' EP 'EPS22' EP22 ;
FVTH11 = EVOL TURQ  MANU 'DEPLA' EP 'EPS11' EPTH11 ;
FVTH12 = EVOL TURQ  MANU 'DEPLA' EP 'EPS12' EPTH12 ;
FVTH22 = EVOL TURQ  MANU 'DEPLA' EP 'EPS22' EPTH22 ;
SI (NEG GRAPH 'N');
* DESS  ( FVTH11 ET FV11) ;
* DESS  ( FVTH12 ET FV12) ;
* DESS  ( FVTH22 ET FV22) ;
 DESS  ( EV11 ET FVTH11 ) ;
 DESS  ( EV12 ET FVTH12 ) ;
 DESS  ( EV22 ET FVTH22 ) ;
FINSI;
*
*   COMPARAISON SOL ANALYTIQUE
*   ecarts
*
ER11 =(MAXI ABS ( sp11 - EPTH11 ))/(MAXI ABS EPTH11);
ER12 =(MAXI ABS ( sp12 - EPTH12 ))/(MAXI ABS EPTH12);
ER22 =(MAXI ABS ( sp22 - EPTH22 ))/(MAXI ABS EPTH22);
*
SAUT 1 LIGN;
MESS ' Ecart relatif en déformations eps11      :' ER11 ;
SAUT 1 LIGN;
MESS ' Ecart relatif en déformations eps12      :' ER12 ;
SAUT 1 LIGN;
MESS ' Ecart relatif en déformations eps22      :' ER22 ;
*
*
*==================================================================*
*                  code fonctionnement                             *
*==================================================================*
*
* L'ecart maximum entre valeurs de reference et calculee doit etre
* inferieur a 5.d-2
*
SAUT 2 LIGNE;
SI (ER11 <EG 0.05 ) ;
  SI (ER12 <EG 0.05 ) ;
    SI (ER22 <EG 0.05 ) ;
        ERRE 0;
    SINON;
        ERRE 5;
    FINSI;
  SINON;
    ERRE 5;
  FINSI;
SINON;
  ERRE 5;
FINSI;
*
SAUT 1 LIGN;
*
FIN;












