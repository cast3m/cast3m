* fichier :  dyna6.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*           Test Dyna6.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test dyna6.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************************
*                          DYNA6
*
*             CAS TEST OPERATEUR VIBRATION
*             MODES DE FLEXION D'UNE POUTRE
*                   ENCASTREE-LIBRE
*
* Cet exemple permet de tester les trois options
* de l'opérateur VIBR :
*
*    - par intervalle (INTERVALLE) ou on sépare les
*      modes par dichotomie sur un intervalle donné
*    - par proximité (PROCHE) ou on recherche les
*      modes proches de valeurs données
*    - par la méthode de Lanczos (SIMULTANE) ou on
*      projette sur un sous-espace
*    - par la méthode d Arnoldi (IRAM) ou on
*      projette sur un sous-espace
*
*
* Le problème consiste à trouver les fréquences pour
* le premier et le deuxième mode, d'une poutre
* encastrée à une extrémité et libre à l'autre.
*
* Les valeurs obtenues par les trois méthodes sont
* comparées aux valeurs théoriques.
*
*******************************************************
TITR 'FLEXION D UNE POUTRE ENCASTREE-LIBRE';
OPTI DIME 3 ELEM SEG2 MODE TRID IMPI 0;
OPTI EPSI LINEAIRE;
TEMPS ;
DENSITE 0.1;

*---------------------- MAILLAGE ----------------------
*----------- Q2 EST UN NOEUD POUR LE MODE NUMERO 2 ----
Q1 = 0. 0. 0.;Q2 = (2.*0.783) 0. 0. ;Q3 = 2. 0. 0. ;
LIG= Q1 D Q2 D Q3 ;
OEIL1 = 5 5 5 ;

SI(NEG GRAPH 'N');
    TRAC OEIL1 LIG ;
FINSI;

STAN= MODE LIG MECANIQUE POUT ;

*------------ MATERIAU ET CARATERISTIQUES -------------
MATPL1=MATE STAN YOUNG 2.E11 NU 0.3 RHO 7800;
CARPL1=CARA STAN SECT 0.01 INRY 6.25E-6 INRZ 1.E-3 TORS 1.E-2;
MATPL1=MATPL1 ET CARPL1;

*------------------ RIGIDITE ET MASSE -----------------
RIGPL1 =RIGI STAN MATPL1;
MASPLA1=MASS STAN MATPL1 ;

*------------------ ON BLOQUE Q1 ----------------------
BLOQ1 =BLOQ DEPL ROTA Q1 ;
RIGPLA1=RIGPL1 ET BLOQ1 ;

*______________________________________________________
*           PREMIERE OPTION  (INTERVALLE)
*           ON SEPARE LES MODES PAR DICHOTOMIE
*           PUIS ON LANCE LES ITERATIONS INVERSES
*______________________________________________________
MODPLA1=VIBR INTE 10. 120. RIGPLA1 MASPLA1 ;

*______________________________________________________
*           DEUXIEME OPTION  (PROCHE)
*           ON LANCE LES ITERATIONS INVERSES
*           A PARTIR DES FREQUENCES CONTENUES
*           DANS LE LISTREEL
*______________________________________________________
PR=PROG 17. 110.;
MODPLA2=VIBR PROC PR RIGPLA1 MASPLA1 ;

*______________________________________________________
*           TROISIEME OPTION  (SIMULTANE)
*           ON UTILISE LA METHODE DE LANCZOS
*           (PROJECTION SUR UN SOUS-ESPACE)
*______________________________________________________
MODPLA3=VIBR SIMU 1. 2 RIGPLA1 MASPLA1 ;
*______________________________________________________
*           4EME OPTION  (IRAM)
*______________________________________________________
MODPLA4=VIBR IRAM 1. 2 RIGPLA1 MASPLA1 ;
*
*---------------------- Resultats ---------------------
*
OPTI ECHO 0;
SAUTER PAGE;
F1= MODPLA1 . 'MODES' . 1 . 'FREQUENCE';
G1= MODPLA1 . 'MODES' . 2 . 'FREQUENCE' ;
F2= MODPLA2 . 'MODES' . 1 . 'FREQUENCE' ;
G2= MODPLA2 . 'MODES' . 2 . 'FREQUENCE' ;
F3= MODPLA3 . 'MODES' . 1 . 'FREQUENCE' ;
G3= MODPLA3 . 'MODES' . 2 . 'FREQUENCE' ;
F4= MODPLA4 . 'MODES' . 1 . 'FREQUENCE' ;
G4= MODPLA4 . 'MODES' . 2 . 'FREQUENCE' ;
*
MESS '*';
MESS '********    ON VERIFIE LES FREQUENCES';
MESS '*';
MESS '         ANALYTIQUE   *   INTERVALLE    *     PROCHE *    SIMULTANE * IRAM ';
MESS '         -------------*-----------------*----------- -----*-------------';
MESS ' MODE 1    17.71   * ' F1 ' * ' F2 ' * ' F3 ' * ' F4;
MESS ' MODE 2   110.98   * ' G1 ' * ' G2 ' * ' G3 ' * ' G4;
MESS ' ';
MESS ' ';
MESS '*';
MESS '********    ON VERIFIE LA DEFORMEE DU DEUXIEME MODE';
MESS '*';
MESS ' ';
X1=EXTR (MODPLA1 . 'MODES' . 2 . 'DEFORMEE_MODALE') UZ Q2;
X2=EXTR (MODPLA2 . 'MODES' . 2 . 'DEFORMEE_MODALE') UZ Q2;
X3=EXTR (MODPLA3 . 'MODES' . 2 . 'DEFORMEE_MODALE') UZ Q2;
X4=EXTR (MODPLA4 . 'MODES' . 2 . 'DEFORMEE_MODALE') UZ Q2;
MESS '        ANALYTIQUE   *   INTERVALLE    *     PROCHE *    SIMULTANE * IRAM';
MESS '        -------------*-----------------*----------- -----*-------------';
MESS 'MODE 2     0.00      *  ' X1 ' * ' X2 ' * ' X3 ' *' X4;
TEMPS ;
*           CODE FONCTIONNEMENT
FREF=17.71;
GREF=110.98;
* le point Q2 est proche du "noeud" du 2eme mode
XREF= 0.;
F = prog F1 F2 F3 F4;
G = prog G1 G2 G3 G4;
X = prog X1 X2 X3 X4;
RES_F = 100 * (MAXI 'ABS' ((F-FREF)/FREF));
RES_G = 100 * (MAXI 'ABS' ((G-GREF)/GREF));
RES_X = 100 * (MAXI 'ABS' X);

SI((RES_F <EG 1.) ET (RES_G <EG 1.) ET (RES_X <EG 1.));
  ERRE 0;
SINO;
  ERRE 5;
FINSI;

FIN;








