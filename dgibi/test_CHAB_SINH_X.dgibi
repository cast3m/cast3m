* fichier : test_CHAB_SINH_X.dgibiq
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
**--------------------------------------
**Nouvelle loi de comportement mécanique
**--------------------------------------


**Données
**-------
*longueur de la plaque
Lo=100.;
*largeur de la demi plaque
La=20.;

**Points du mailage
**-----------------
OPTI 'DIME' 2 'ELEM' QUA4 MODE AXIS;

A1=0. 0.;
A2=la 0.;
A3=la Lo;
A4=0. Lo;
L1=DROITE 1 A4 A1;
L2=DROITE 1 A1 A2;
L3=DROITE 1 A2 A3;
L4=DROITE 1 A3 A4;
cube=DALL L1 L2 L3 L4;
*TRAC cube;

**Définition modèle et matériau
**-----------------------------
*modèle mécanique
MODM=MODEL cube MECANIQUE ELASTIQUE VISCOPLASTIQUE 'CHAB_SINH_X';
MATM=MATER MODM 'YOUN' 180014.4716 'NU' 0.3 'SIGY' 359.8729
'K' 2.2 'N' 0.6 'B' 23.1521 'RI' 40.3074 'epso' 6.E-8 'C1' 16535.60
'gam1' 165.5549 'C2' 784.2466;

**Blocages mécaniques
**-------------------
CLM1=BLOQ 'UZ' L2;
CLM2=BLOQ 'UZ' L4;

CL_M=CLM1 ET CLM2;

**Déplacements imposés
**--------------------
def1=2.;
def2=4.;
dep1=(def1/100) * Lo;
dep2=(def2/100) * Lo;
V=0.0001;
T_def1=(def1/100)/V;
T_def2=(def2/100)/V;

*-------------------------
*boucles à la première def
*-------------------------
EVT=PROG 0.;
EVDEF=PROG 0;
EVT=EVT ET (PROG (T_def1 / 2.));
EVDEF=EVDEF ET (PROG dep1);
REPE bou 8;
  ite=&bou;
  EVT=EVT ET (PROG ((EXTR EVT (DIME EVT)) + T_def1));
  EVDEF=EVDEF ET (PROG ((-1) ** (ite) * dep1));
FIN bou;
REPE bou 6;
  ite=&bou;
  EVT=EVT ET (PROG ((EXTR EVT (DIME EVT)) + T_def2));
  EVDEF=EVDEF ET (PROG ((-1) ** (ite) * dep2));
FIN bou;

DI1=DEPI CLM2 1.;
EV2=EVOL MANU 'temps' EVT 'deplacement' EVDEF;
CHAM1=CHAR 'DIMP' DI1 EV2;


**Mise en place de la table PASAPAS
**---------------------------------
tm=table;
tm.MES_SAUVEGARDES=TABLE;
tm.MES_SAUVEGARDES.'DEFTO'=VRAI;
tm.MES_SAUVEGARDES.'DEFIN'=VRAI;
tm.precision=1.E-3;
tm.modele=MODM;
tm.caracteristiques=MATM;
tm.blocages_mecaniques=CL_M;
tm.chargement=CHAM1;
tm.temps_calcules=prog 0. pas (T_def1/10.) (EXTR EVT (DIME EVT));

PASAPAS tm;

**Analyse des champs résultants
**-----------------------------
IDIM=(DIME (tm.temps)) - 1;
*déformée
DEPF=tm.deplacements.IDIM;
DEFI=DEFO DEPF cube 0. VERT;
DEFF=DEFO DEPF cube 1. ROUGE;
*TRAC (DEFI ET DEFF) TITRE 'déformée';
*Contraintes de V.Mises
CO=tm.contraintes.IDIM;
COP2=VMIS (MODM) CO;
*TRAC DEFF COP2 (MODM) TITRE 'Contrainte de Von Mises';
*tracé des isovaleurs des déplacements
ZZ1=EXCO DEPF 'UZ';
OPTI ISOV 'SURFACE';
*TRAC cube ZZ1 TITRE 'déplacement Uz';
XX1=EXCO DEPF 'UR';
*TRAC cube XX1 TITRE 'déplacement Ur';
*tracé de la contrainte SMZZ
TCON=EXCO tm.contraintes.IDIM 'SMZZ';
*TRAC TCON (MODM) TITRE 'contrainte suivant zz';
*écrouissage isotrope
EI=EXCO tm.variables_internes.IDIM 'r';
*TRAC cube MODM EI TITRE 'écrouissage isotrope';
*déformation plastique cumulée
DPC=EXCO tm.variables_internes.IDIM 'EPSE';
*TRAC cube MODM DPC TITRE 'déformation plastique cumulée';


**caractéristiques en fonction du temps
**-------------------------------------
*initilisation
DEPP5=PROG 0.;
CONPC=PROG 0.;
DEFTPC=PROG 0.;
SEC=PROG 0.;
RPC=PROG 0.;
PPC=PROG 0.;
DEI=PROG 0.;
X1=PROG 0.;
X2=PROG 0.;
*boucle
REPETER bou IDIM;
ite=&bou;
TE=tm.temps.ite;
DEP=tm.deplacements.ite;
CON=tm.contraintes.ite;
DEFT=tm.deformations.ite;
VARI2=tm.variables_internes.ite;
DEFI=tm.deformations_inelastiques.ite;
*echelle de temps
SEC=SEC ET (PROG TE);
*déplacement du bout de l'éprouvette
DEPP5n=EXTR DEP 'UZ' A3;
DEPP5=DEPP5 ET (PROG DEPP5n);
*contrainte au point de contrôle
CONPCn=EXTR CON 'SMZZ' 1 1 1;
CONPC=CONPC ET (PROG CONPCn);
*déformation totale au point de contrôle
DEFTPCn=EXTR DEFT 'EPZZ' 1 1 1;
DEFTPC=DEFTPC ET (PROG DEFTPCn);
*écrouissage au point de contrôle
RPCn=(EXTR VARI2 'r' 1 1 1) * 40.3074 * 23.1521;
RPC=RPC ET (PROG RPCn);
*p au point de contrôle
PPCn=EXTR VARI2 'EPSE' 1 1 1;
PPC=PPC ET (PROG PPCn);
*deformation inelastique suivant Uz
DEIn=EXTR DEFI 'EIZZ' 1 1 1;
DEI=DEI ET (PROG DEIn);
*écrouissage cinématique non linéaire suivant z:
X1n=(EXTR VARI2 'A1ZZ' 1 1 1) * 16535.60;
X1=X1 ET (PROG X1n);
*écrouissage cinématique linéaire suivant z:
X2n=(EXTR VARI2 'A2ZZ' 1 1 1) * 784.2466;
X2=X2 ET (PROG X2n);
FIN bou;

**graphiques
**----------
*deplacement(t)
EVDEPB=EVOL 'MANU' 'temps' SEC 'déplacement' DEPP5;
*DESS EVDEPB TITRE 'deplacement du bout de la poutre (t)';
*contrainte suivant z au point de contrôle (t)
EVCONPC=EVOL 'MANU' 'temps' SEC 'contrainte suivant z' CONPC;
*DESS EVCONPC TITRE 'contrainte suivant z au point de contrôle';
*tracé de la déformation totale au point de contrôle (t)
EVDEFTPC=EVOL 'MANU' 'temps' SEC 'déformation totale' DEFTPC;
*DESS EVDEFTPC TITRE 'déformation totale au point de contrôle';
*tracé de la contrainte en fonction de la déformation
EVCD=EVOL 'MANU' 'epsilon' DEFTPC 'contrainte' CONPC;
*DESS EVCD TITRE 'contrainte/deformation totale';
*tracé de l'écrouissage en fonction de p
EVRD=EVOL 'MANU' 'p' PPC 'écrouissage' RPC;
*DESS EVRD TITRE 'ecrouissage/p';
*tracé de p en fonction de la déformation
EVPD=EVOL 'MANU' 'epsilon' DEFTPC 'p' PPC;
*DESS EVPD TITRE 'p/deformation totale';
*tracé de la deformation inelastique
EVDEFI=EVOL 'MANU' 'epsilon' DEFTPC 'deformation inelastique' DEI;
*DESS EVDEFI TITRE 'deformation inelastique/deformation totale';
*tracé de X1
EVX1=EVOL 'MANU' 'p' PPC 'X1' X1;
*DESS EVX1 TITRE 'X1/p';
*tracé de X2
EVX2=EVOL 'MANU' 'p' PPC 'X2' X2;
*DESS EVX2 TITRE 'X2/p';

**sauvegarde des résultats
**------------------------
@EXCEL1 EVCD 'et-contrainte.xls';
@EXCEL1 EVRD 'p-R.xls';
@EXCEL1 EVX1 'p-X1.xls';
@EXCEL1 EVX2 'p-X2.xls';

FIN;



