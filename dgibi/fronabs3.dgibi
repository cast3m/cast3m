* fichier :  fronabs3.dgibi
************************************************************************
* Section : Mecanique Interaction Fluide Structure
************************************************************************
*****************************************
*
* Test des frontières absorbantes
*
*  Barre en cisaillement
*
*  D. COMBESCURE 30/09/2005
*
*****************************************
*GRAPH = 'O';
GRAPH = 'N';
*
opti dime 3 elem cu20;
*
coef = 2;
*
Ebet = 30000.D6;
nubet = 0.25;
Rhobet = 2300.;
*
Esol = 2000.D6;
nusol = 0.45;
Rhosol = 2000.;
*
H1= 1.;
Lx1= 40.;
Lx2= 20.;
Ly= 10.;
Lz = 10.;
*
nx1sol = 10*coef;
nx2sol = 5*coef;
nysol = 1;
nzsol = 1;
*
pfor = 0. 0. 0.;
*
p1 = 0. ((-0.5)*Ly) ((-0.5)*Lz);
p2 = 0. (0.5*Ly) ((-0.5)*Ly);
p3 = 0. ((-0.5)*Ly) (0.5*Lz);
p4 = 0. (0.5*Ly) (0.5*Ly);
vhor1 = Lx1  0. 0.;
vhor2 = Lx2  0. 0.;
vver = 0. 0. Lz;
*
p1b = p1 plus vhor1;
p2b = p2 plus vhor1;
p3b = p3 plus vhor1;
p4b = p4 plus vhor1;
*
p1c = p1b plus vhor2;
p2c = p2b plus vhor2;
p3c = p3b plus vhor2;
p4c = p4b plus vhor2;
*
su1 = tran (d nysol p1 p2) nzsol vver;
su2 = su1 plus vhor1;
su3 = su2 plus vhor2;
volbet = su1 volu tran nx1sol vhor1;
volsol = su2 volu tran nx2sol vhor2;
*
blxz = (BLOQ UX (volbet et volsol))
    et (BLOQ UZ (volbet et volsol));
blyz = (BLOQ UY (volbet et volsol))
    et (BLOQ UZ (volbet et volsol));
*
elim 0.001 (volbet et volsol et su1 et su2 et su3
           et p1 et p2 et p3 et p4 et p1b et p2b et p3b et p4b
           et p1c et p2c et p3c et p4c);
oeil = 0. 0. 10000.;
*
modbet = MODE volbet mecanique elastique;
matbet = MATE modbet YOUN Ebet NU nubet RHO rhobet;
*
modsol = MODE volsol mecanique elastique;
matsol = MATE modsol YOUN Ebet NU nubet RHO rhobet;
*
meshtot = volbet et volsol;
*
* Matrice de raideur et de masse
*
*
Mas1 = MASS (modbet et modsol) (matbet et matsol);
Krig = RIGI (modbet et modsol) (matbet et matsol);
bet = 2.*0.0025/(2.*pi*200.);
Cdam1 = bet*Krig;
*
Camor = AMOR modsol su3 matsol;
bl0 = bloq depl su3;
*
meshtot = extr (modbet et modsol) mail;
*
* Définition du chargement
*
Fmax = 600.D6;
Tchar = 0.01;
*
bl0x = bloq UX su1;
bl0y = bloq UY su1;
for1x = depi bl0x 1.;
for1y = depi bl0y 1.;
*
*
*trac meshtot (vecteur for1x FX FY FZ (1.D-8));
*
tduree = 0.005;
prtime1 = prog 0. pas 0.0001 tduree;
prtime2 = prog (tduree+0.001) pas 0.001 3.;
evchar = evol manu  (prtime1 et prtime2)
 ((sin ((360./tduree)*prtime1)) et (prog (dime prtime2)*0.));
cha1y = char for1y evchar;
cha1x = char for1x evchar;
*
* Calcul temporel
*
dt = 0.001;
*
tab2 = table;
tab2.'DEPL' = manu chpo meshtot UX 0. UY 0. UZ 0.;
tab2.'VITE' = manu chpo meshtot UX 0. UY 0. UZ 0.;
tab2.'CHAR' = cha1y;
tab2.'MASS' = Mas1;
tab2.'RIGI' = Krig et blxz et bl0y;
tab2.'AMOR' = Camor;
tab2.'FREQ' = (1./dt);
tab2.'INST' = prog 0. pas (dt/4.) 0.10;
tab1 = dynamic tab2;
*
prtime = prog;
prux1 = prog;
prux2 = prog;
prux3 = prog;
pruy1 = prog;
pruy2 = prog;
pruy3 = prog;
pruz1 = prog;
pruz2 = prog;
pruz3 = prog;
ntime = dime tab1;
repeter lab2 (ntime - 1);
  nlab2 = &lab2 + 1;
  depj = (tab1.nlab2).'DEPL';
  si (&lab2 ega 1);
    deft = defo meshtot depj 1.;
  sinon;
    deft = deft et (defo meshtot depj 1.);
  finsi;
  prtime = prtime et (prog ((tab1.nlab2).'TEMP'));
  pruz1 = pruz1 et (prog (extr depj p1 UZ));
  pruz2 = pruz2 et (prog (extr depj p1b UZ));
  pruz3 = pruz3 et (prog (extr depj p1c UZ));
  prux1 = prux1 et (prog (extr depj p1 UX));
  prux2 = prux2 et (prog (extr depj p1b UX));
  prux3 = prux3 et (prog (extr depj p1c UX));
  pruy1 = pruy1 et (prog (extr depj p1 UY));
  pruy2 = pruy2 et (prog (extr depj p1b UY));
  pruy3 = pruy3 et (prog (extr depj p1c UY));
fin lab2;
*
evuz1 = (evol manu prtime pruz1);
evuz2 = (evol manu prtime pruz2);
evuz3 = (evol manu prtime pruz3);
evux1 = (evol manu prtime prux1);
evux2 = (evol manu prtime prux2);
evux3 = (evol manu prtime prux3);
evuy1 = (evol manu prtime pruy1);
evuy2 = (evol manu prtime pruy2);
evuy3 = (evol manu prtime pruy3);
si (ega graph 'O');
  dess (evuy1 et evuy2 et evuy3);
  trac oeil deft anime cach;
finsi;
*
* Cas sans frontière absorbante
*
tab2 = table;
tab2.'DEPL' = manu chpo meshtot UX 0. UY 0. UZ 0.;
tab2.'VITE' = manu chpo meshtot UX 0. UY 0. UZ 0.;
tab2.'CHAR' = cha1y;
tab2.'MASS' = Mas1;
tab2.'RIGI' = Krig et blxz et bl0y;
*tab2.'AMOR' = Camor;
tab2.'FREQ' = (1./dt);
tab2.'INST' = prog 0. pas (dt/4.) 0.10;
tab1 = dynamic tab2;
*
prtime = prog;
pruy1b = prog;
pruy2b = prog;
pruy3b = prog;
ntime = dime tab1;
repeter lab3 (ntime - 1);
  nlab2 = &lab3 + 1;
  depj = (tab1.nlab2).'DEPL';
  si (&lab2 ega 1);
    deft = defo meshtot depj 1.;
  sinon;
    deft = deft et (defo meshtot depj 1.);
  finsi;
  prtime = prtime et (prog ((tab1.nlab2).'TEMP'));
  pruy1b = pruy1b et (prog (extr depj p1 UY));
  pruy2b = pruy2b et (prog (extr depj p1b UY));
  pruy3b = pruy3b et (prog (extr depj p1c UY));
fin lab3;
evuy1b = (evol manu prtime pruy1b);
evuy2b = (evol manu prtime pruy2b);
evuy3b = (evol manu prtime pruy3b);
ta = table;
ta. 2 = 'TIRR';
si (ega graph 'O');
  dess (evuy1b et evuy2b et evuy3b);
  dess evchar xbord 0. 0.10;
  dess (evuy2 et evuy2b) ta;
  depj = (tab1. 70).'DEPL';
  deftj = defo meshtot depj 4.;
  trac oeil deftj;
finsi;
*
fin;
*
*






