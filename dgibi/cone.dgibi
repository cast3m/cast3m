* fichier :  cone.dgibi
************************************************************************
* Section : Fluides Transport
************************************************************************
*=---------------------------------------------------------------------
*= Transport d'un cone par un champ de vitesse à rotationnel constant.
*= Comparaison de schéma en temps EF implicites
*=---------------------------------------------------------------------
*
*------------------
* Options générales
*------------------
*
GRAPH = 'O'                   ;
ERR1=5.e-5     ;
COMPLET = FAUX ;
SI ( COMPLET ) ;
  DIAM=2. ;
SINON        ;
  DIAM=0.5 ;
FINSI ;

DISCR='LINE';

'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
'OPTI' 'ISOV' 'SULI'          ;
'OPTI' 'ECHO' 1 'TRACER' 'PSC' ;
*
*=========
* MAILLAGE
*=========
*
*
*- Création des points supports des DROITES
*
L     = 2.D0 ; LS2   = L / 2.D0        ;
H     = L    ; HS2   = H / 2.D0        ;
X0    = -1.D0 * LS2     ; X1    = X0 + L          ;
Y0    = -1.D0 * HS2     ; Y1    = Y0 + H          ;
INUMX = 20              ;
INUMY = INUMX           ;
XNUMY = 'FLOT' INUMX    ;
INUX1 = INUMX - 1       ;
INUY1 = INUMY - 1       ;
X01   = X0 + X1 * 0.5D0 ;
Y01   = Y0 + Y1 * 0.5D0 ;
DX    = X1 - X0 / INUMX ;
DY    = Y1 - Y0 / INUMY ;
DX1   = DX / 2.D0       ;
DY1   = DY / 2.D0       ;
*
B1 = X0 Y0  ;
B3 = X1 Y0  ;
H1 = X0 Y1  ;
H3 = X1 Y1  ;
*
*
*- Création des DROITES frontieres
*
DRBAS = B1 'DROI' INUMX B3 ;
DRDRO = B3 'DROI' INUMY H3 ;
DRHAU = H3 'DROI' INUMX H1 ;
DRGAU = H1 'DROI' INUMY B1 ;
DRCOT = DRBAS 'ET' DRDRO 'ET' DRGAU 'ET' DRHAU ;
PELIM = DX1 / (5.D0 * INUMX) ;
*
*- Creation maillage GEOMETRIQUE
*
PTOT1 = 'DALL' DRBAS DRDRO DRHAU DRGAU ;
*
*- Creation maillage HYBRIDE y compris sous-objets (cond. limites)
*
PTOTC = CHAN PTOT1 QUAF ;
MDRCOT = CHAN DRCOT QUAF ;
ELIM (PTOTC et MDRCOT) 1.E-5 ;

HYTOT = 'MODE' PTOTC 'NAVIER_STOKES' DISCR;
$DRCOT= 'MODE' MDRCOT 'NAVIER_STOKES' DISCR ;

DRCOT=doma $drcot 'MAILLAGE' ;

XVOL= DOMA HYTOT 'VOLUME'  ;
DOMA HYTOT 'IMPR' ;
ptot1=DOMA HYTOT  'MAILLAGE';

*
*
*- Coordonnées des points par classe de points
*
XS YS = 'COOR' ( DOMA HYTOT  'MAILLAGE') ;
*
*  XCEN : Abscisse du sommet du cone
*  YCEN : Ordonnée du sommet du cone
*  RCEN : Rayon du cone
*
XCEN = 0.D0       ;
YCEN =-0.5D0      ;
RCEN = 3.0D0 * DX ;

*================
* INITIALISATIONS
*================
*
*                                            ------------------------
*                                            = Conditions initiales =
*                                            ------------------------
* h(x,y) = f(r)
* avec f(r) = max( h(1.-r/rcen) , 0.)
* On se sert des égalités suivantes :
*      m = min(f,g) = (f+g - abs(f-g)) / 2
*      M = max(f,g) = (f+g + abs(f-g)) / 2
*
HAUT = 1.D0 ;
*
*- Charge aux sommets
*
CRS1  = (XS-XCEN)*(XS-XCEN) + ((YS-YCEN)*(YS-YCEN)) ** 0.5D0 ;
CRS2  = 1.D0 - (CRS1 / RCEN) * HAUT                          ;
CRS3  = 'ABS' CRS2                                           ;
CRS4  = CRS2 + CRS3 / 2.D0                                   ;
SI ( COMPLET ) ;
daxh=drbas plus (0. 0.5)  ;
daxv=drgau plus (1. 0.)  ;
elim (daxh et daxv et ptot1) 1.e-5 ;
SINON ;
daxh=drbas plus (0. 1.)  ;
daxv=drgau plus (0.5 0.)  ;
elim (daxh et daxv et ptot1) 1.e-5 ;
FINSI ;
*
*- Vitesses aux sommets
*
SPEEX = 'NOMC' 'UX' YS 'NATU' 'DISCRET' ;
SPEEY = 'NOMC' 'UY' XS 'NATU' 'DISCRET' ;
SPEEY = (-1.D0) * SPEEY                 ;

TN    = KCHT HYTOT  SCAL SOMMET  CRS4        ;
evolth=evol 'CHPO' TN  daxh ;
evoltv=evol 'CHPO' TN  daxv ;
s2c= SOMT ((NOEL HYTOT TN)*XVOL) ;
mess ' Solution exacte projetée ' s2c ;


DEBPROC TEST ;
ARGU OPDFDT*MOT  OPKONV*MOT BETA*FLOTTANT CFL*FLOTTANT
     DIAM*FLOTTANT GRAPH*MOT TIT*MOT;
DT=CFL*0.1 ;
NBIT=enti (3.146 * DIAM / DT) + 1 ;

 coef = 1.e-20 ;

 RV =  'EQEX' HYTOT ITMA NBIT 'FIDT' 1 'DTI' 0.
 'OPTI' 'EF' 'SEMI' BETA  OPKONV 'MATCONS' 'MTCV'
 'ZONE' HYTOT 'OPER' 'KONV' 1.  'UN' COEF DT 'INCO' 'TN'
 'CLIM' 'TN' 'TIMP' DRCOT  0.  ;

 Si ( EGA OPDFDT 'BDF2');
 RV =  'EQEX' RV
 'OPTI' 'EF' 'CENTREE' BDF2
 'ZONE' HYTOT 'OPER' 'DFDT' 1. 'TNM' 'TNMM' DT 'UN' coef 'INCO' 'TN'
 ;
 Sinon ;
 RV =  'EQEX' RV
 'OPTI' 'EF'     OPDFDT
 'ZONE' HYTOT 'OPER' 'DFDT' 1. 'TNM' DT 'UN' coef 'INCO' 'TN'
 ;
 FINSI ;

 RV.'INCO'         = TABLE 'INCO'                        ;
 RV.'INCO'.'TN'    = KCHT HYTOT  SCAL SOMMET  0.          ;
 RV.'INCO'.'TNM'    = KCHT HYTOT  SCAL SOMMET  CRS4       ;
 RV.'INCO'.'TNMM'    = KCHT HYTOT  SCAL SOMMET  CRS4      ;
 RV.'INCO'.'T0'    = KCHT HYTOT  SCAL SOMMET  0.          ;
 RV.'INCO'.'UN'    = KCHT HYTOT  VECT SOMMET  SPEEX  SPEEY ;

u = vect RV.'INCO'.'UN' 1. ux uy ;
Si(EGA GRAPH 'O') ;
 trac u ptot1 'NCLK' ;
 trac RV.'INCO'.'TNM'  ptot1 'NCLK';
Finsi ;

rv.'OMEGA'= 1. ;
rv.'NITER'=1 ;

EXEC rv ;

maxc=maxi (RV.'INCO'.'TN') ; minc=mini (RV.'INCO'.'TN');
mess 'MAXC=' maxc 'MINC=' minc ;
TN=(RV.'INCO'.'TN') ;
s2c= SOMT ((NOEL HYTOT TN)*XVOL) ;
mess ' S2C= ' s2c ;

SI  ('EGA' graph 'O' );
   P1 = 'PROG' -5. -5. 2. ;
   MONTAGNE (RV.'INCO'.'TN') ptot1 2. P1 'TITRE' tit 'CACHE' ;
   'TRACER' RV.'INCO'.'TN'  ptot1 ('PROG' -0.1 'PAS' 0.1 0.9) drcot ;
FINSI ;
FINPROC RV ;

* SCHEMA CRANK-NICHOLSON GENERALISE GALERKIN (CNG)

CFL=0.3 ;
BETA=1.  ;
OPDFDT='CENTREE' ;
OPKONV='CNG' ;
TIT= CHAI 'CNG (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI(NON COMPLET ) ;
lrr=prog
  0.0000       3.40214E-04 -3.38706E-03  2.51870E-03  2.61931E-04
 -5.08234E-03 -5.46411E-03  4.64921E-02  0.28964      0.68311
  0.91024      0.71943      0.31742      3.46789E-02 -3.85647E-02
 -1.60213E-03  6.77212E-04 -5.38969E-03  8.96169E-04  1.29709E-02
   0.0000;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur CNG ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

* SCHEMA Tenseur Visqueux (TVISQ)

CFL=0.2 ;
BETA=0.  ;
OPDFDT='CENTREE' ;
OPKONV='TVISQUEU' ;
TIT= CHAI 'TVISQUEU (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI(NON COMPLET ) ;
lrr=prog
   0.0000     -5.85075E-06 -9.97378E-05  2.74751E-04  8.36836E-04
 -4.17140E-03 -1.10214E-02  4.50260E-02  0.27635      0.65080
  0.88107      0.73478      0.35486      5.62308E-02 -2.97219E-02
 -2.25054E-03  6.37495E-03 -2.45424E-03 -1.62176E-03  4.25752E-03
   0.0000 ;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur Tenseur Visqueux ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

* SCHEMA CRANK-NICHOLSON GALERKIN (CN)

CFL=1. ;
BETA=0.5 ;
OPDFDT='CENTREE' ;
OPKONV='CENTREE' ;
TIT= CHAI 'CN  (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI ( NON COMPLET ) ;
lrr=prog
   0.0000      1.61279E-03 -2.14574E-03  1.38828E-03 -1.93813E-04
 -1.21903E-03  8.08998E-03  5.65941E-02  0.23412      0.56658
  0.86213      0.80941      0.43175      8.00014E-02 -6.17969E-02
 -3.78373E-02  6.84644E-03  3.67903E-04  3.34448E-03  2.55760E-03
   0.0000 ;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur CN ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

* SCHEMA CRANK-NICHOLSON PETROV-GALERKIN (CN-PG)

CFL=1. ;
BETA=0.5 ;
OPDFDT='SUPG' ;
OPKONV='SUPG' ;
TIT= CHAI 'CN-PG  (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI (NON COMPLET ) ;
lrr=prog
   0.0000     -2.62438E-06 -2.06304E-06  7.81568E-05 -1.32996E-04
 -2.06769E-03  2.65700E-03  6.36668E-02  0.26105      0.57709
  0.80791      0.74328      0.42283      9.99338E-02 -3.63482E-02
 -2.27419E-02  4.81322E-03  1.92001E-03 -1.65550E-03  1.04995E-03
   0.0000;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur CN-PG ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

* SCHEMA EULER IMPLICITE PETROV-GALERKIN (IMPL-PG)

CFL=1. ;
BETA=1. ;
OPDFDT='SUPG' ;
OPKONV='SUPG' ;
TIT= CHAI 'IMPL-PG  (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI ( NON COMPLET ) ;
lrr=prog
   0.0000      2.02489E-05 -7.02367E-04 -1.20282E-03  4.45689E-03
  2.94926E-02  8.93340E-02  0.19028      0.31829      0.43680
  0.49888      0.47086      0.35698      0.20568      8.09276E-02
  1.65337E-02  3.46557E-04  3.42702E-04 -4.78618E-04  2.29204E-04
   0.0000;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur IMPL-PG ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

* SCHEMA BDF2

CFL=1.  ;
BETA=0.5 ;
OPDFDT='BDF2' ;
OPKONV='SUPG' ;
TIT= CHAI 'BDF2  (1/4 de TOUR)' ;
rv = TEST OPDFDT OPKONV BETA  CFL DIAM GRAPH TIT;
evolh=evol 'CHPO' (RV.'INCO'.'TN')  daxh ;
evolv=evol 'CHPO' (RV.'INCO'.'TN')  daxv ;
evolth=evolth et evolh ;
evoltv=evoltv et evolv ;

SI(NON COMPLET ) ;
lrr=prog
   0.0000     -1.18298E-07 -4.71604E-06  2.44781E-05  2.25936E-05
 -8.55947E-04  5.70537E-04  3.48491E-02  0.17230      0.44369
  0.72821      0.81692      0.61789      0.25389     -3.02306E-02
 -8.97148E-02 -2.20597E-02  9.88837E-03 -7.72484E-04 -3.78951E-05
   0.0000;
  lr=extr evolv 'ORDO';
  list lr ;
  ER=SOMM( abs (lr - lrr) ) ;
  mess ' Erreur sur BDF2 ' er ;
  si ( er > 1.e-4 ) ; erreur 5 ; finsi ;
FINSI ;

si ('EGA' graph 'O' );
titre 'Coupe ox à y=1/2 ' ;
TAB1=TABLE;
TAB1.'TITRE'=TABLE ;
TAB1.1='TIRR MARQ CROI REGU';
TAB1.'TITRE' . 1 = mot 'T initial ' ;
TAB1.2='MARQ ETOI REGU';
TAB1.'TITRE' . 2 = mot 'CNG  CFL 0.9';
TAB1.3='MARQ ETOI REGU';
TAB1.'TITRE' . 3 = mot 'TVISQ CFL 0.5';
TAB1.4='MARQ CROI REGU ';
TAB1.'TITRE' . 4 = mot 'CN  CFL 1. ';
TAB1.5='MARQ LOSA REGU ';
TAB1.'TITRE' . 5 = mot 'CNPG CFL 1.' ;
TAB1.6='MARQ CARR REGU ';
TAB1.'TITRE' . 6 = mot 'IMPLPG CFL 1.' ;
TAB1.7='MARQ PLUS REGU ';
TAB1.'TITRE' . 7 = mot 'BDF2 CFL 1.' ;
DESS EVOLTH 'TITX' 'Ox'    LEGE  TAB1 ;
titre 'Coupe oy à x=0   ' ;
DESS EVOLTV 'TITX' 'Oy'    LEGE  TAB1 ;
FINSI ;

 FIN ;
















