* fichier :  t_char.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************
GRAPH='N' ;
SAUT PAGE ;
*-----------------------------------------
* TEST CHAR, TIRE, LIST, EXTR, EXIS
* extension CHARGEMENT LIE / LIBRE, STAT/ROTA,TRAN,TRAJ
*-----------------------------------------
opti dime 2 elem qua4 ;
*
p0 = 1. 0. ; e_x = p0 ; e_y = 0. 1. ; p_ori = 0. 0. ;
p1 = p0 plus e_x ;
s1 = (p0 d 1 p1) trans 1 e_y ;
lc1 = c (inve (s1 cote 4)) p0 p_ori 4 ;
cont = (d 6 p_ori p0)  et lc1 ;
su1 = surf cont plan ;
st1 = s1 et su1 ;
chp1 = manu chpo 1 s1  fy -1. ;
pe1 = 10. 1. ; pe2 = 9. 2. ; pe3 = 8. 3. ;
*-------------------------------------
* test CHAR
ev2 = evol manu (prog 0. 2.) (prog 1. 2.) ;
ev3 = evol manu (prog 0. 1.) (prog 1. 2.) ;
cha1 = char meca ev2 chp1 ;
chp2 = tire cha1 1. ;
list chp2 ;
* translation a vitesse croissante
cha2 = char ev2 chp1 trans (2.*e_y) ev3 ;
list (cha1 et cha2) ;
*chp3 = tire cha2 1.5 ;
chp4 = tire cha2 0.5 ;
*list chp3 ;
si (neg graph 'N') ;
  trac (s1 et (extr chp1 mail) et (extr chp4 mail)) ;
finsi ;
*
* rotation de vitesse 45 degre/s
ev4 = evol manu (prog 0. 2.) (prog 45.  45.) ;
cha5 = char meca ev2 chp1 rota p1 ev4 ;
chp5 = tire cha5 1. ;
chp6 = tire cha5 2. ;
si (neg graph 'N') ;
  trac (s1 et (extr chp5 mail) et (extr chp6 mail)) ;
finsi ;
v5 = vecteur chp5 fx fy 0.2 vert ;
v1 = vecteur chp1 fx fy 0.2 rouge ;
si (neg graph 'N') ;
  trac (v1 et v5) s1 ;
finsi ;
*
* trajectoire
chtra1 = manu chpo 1 (pe1 et pe2 et pe3) temp (prog 0. 1. 2.) ;
cha6 = char  ev2 chp1 libre traj chtra1 ;
chp7 = tire cha6 1. ;
chp8 = tire cha6 2. ;
si (neg graph 'N') ;
  trac (s1 et (extr chp7 mail) et (extr chp8 mail)) ;
finsi ;
v7 = vecteur chp7 fx fy 0.2 vert ;
v1 = vecteur chp1 fx fy 0.2 rouge ;
si (neg graph 'N') ;
  trac (v1 et v7) s1 ;
finsi ;
*
cha_t = cha2 et cha6 ;
si (exis cha_t libr) ;
  list (extr cha_t libr) ;
finsi ;
*
fx0 =  1.06066E+00 ;
fy0 = -1.06066E+00 ;
fx1  = extr chp5 fx (point 1 (extr chp5 mail)) ;
fy1  = extr chp5 fy (point 1 (extr chp5 mail)) ;
resix = abs((fx1 - fx0)/fx0) ;
resiy = abs((fy1 - fy0)/fy0) ;
si ((resix <eg 5.e-3) et (resiy <eg 5.e-3)) ;
  erre 0 ;
sinon ;
  erre 5 ;
finsi ;
*
fin ;




