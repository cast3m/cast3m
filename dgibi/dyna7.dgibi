* fichier :  dyna7.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*           Test Dyna7.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test dyna7.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************************
*                          DYNA7
*               CAS TEST OPERATEUR VIBRATION
*
*
* Cet exemple permet de tester les trois options de
* l'opérateur VIBR
*
* On calcule les modes de ballotement d'une lame mince
* de fluide dans un espace annulaire en mode de
* Fourier 1 et 3.
*
* On suppose la cavité indéformable. L'effet de la
* gravité est pris en compte.
*
*
*******************************************************
TITR 'MODES DE BALLOTEMENT D UNE LAME MINCE DE FLUIDE';
OPTI DIME 2 ELEM QUA4;
OPTI EPSI LINEAIRE;
TEMPS ;

*--------------------------- MAILLAGE -----------------
*----------------- ON NE MAILLE QUE LE FLUIDE ---------
*
A1=0.995 0.;A2=1.005 0.;A3=1.005 2.;A4=0.995 2.;
A5=1.005 1.2;A6=0.995 1.2;
L1=D 1 A1 A2;
L2=A2 D 24 A5 D 16 A3;
L3=D 1 A3 A4;
L4=A4 D 16 A6 D 24 A1;
FLUID=DALL L1 L2 L3 L4;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' FLUID ;
FINSI;
*
OPTI ELEM SEG2;
SURFA=D 1 A3 A4;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' SURFA ;
FINSI;
*
MAIL=FLUID ET SURFA;
*
SI (NEG GRAPH 'N');
  TRAC 'QUAL' MAIL;
FINSI;
*
*---------- DEFINITION DE QUELQUES CONSTANTES ---------
*
ROREF=1.;CEREF=1.;LCARA=1.;
RO0=1000.;CS=1200.;GRA=9.81;
*
*-- MODELE ET OBJET AFFECTE FLUIDE ET SURFACE LIBRE ---
*-------- POUR L'HARMONIQUE DE FOURIER 1 --------------
*
OPTI MODE FOUR 1;
MODLIQ1=MODE FLUID LIQUIDE LQU4;
MODLIQ2=MODE SURFA LIQUIDE LSU2;
MODLIQ=MODLIQ1 ET MODLIQ2;
*
*--------------- MATERIAU LIQUIDE ---------------------
*
MAT=MATE MODLIQ RHO RO0 RORF ROREF CSON CS CREF CEREF LCAR LCARA G GRA;
*
*--------------- RIGIDITE ET MASSE --------------------
*
RAI=RIGI MODLIQ MAT;
MAS=MASS MODLIQ MAT;
*
*______________________________________________________
* ON CALCULE LE PREMIER MODE AVEC L'OPTION INTERVALLE
* DE VIBR. ON RENORMALISE LE CHAMP DE DEPLACEMENT
* (ET DE PRESSION) DE MANIERE A CE QUE LA PRESSION SOIT
* EGALE A 1 A L'ALTITUDE 0 ET ON EXTRAIT LES PRESSIONS
* AUX POINTS A2, A5 ET A3
*______________________________________________________
*
MOD1=VIBR INTE 0. 1. RAI MAS;
F1 =MOD1 . MODES . 1 . FREQUENCE;
PRE=MOD1 . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
X1=EXTR PP P A2;Y1=EXTR PP P A5;Z1=EXTR PP P A3;
*
*______________________________________________________
* ON CALCULE LE PREMIER MODE AVEC L'OPTION PROCHE DE VIBR
*______________________________________________________
*
MOD2=VIBR PROC (PROG 0.5) RAI MAS;
F2= MOD2 . MODES . 1 . FREQUENCE;
PRE= MOD2 . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
X2=EXTR PP P A2;Y2=EXTR PP P A5;Z2=EXTR PP P A3;
*
*______________________________________________________
* ON CALCULE LE PREMIER MODE AVEC L'OPTION SIMULTANE DE VIBR
*______________________________________________________
*
MOD3=VIBR SIMU 0.5 1 RAI MAS;
F3= MOD3 . MODES . 1 . FREQUENCE;
PRE= MOD3 . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
X3=EXTR PP P A2;Y3=EXTR PP P A5;Z3=EXTR PP P A3;
*______________________________________________________
* ON CALCULE LE PREMIER MODE AVEC L'OPTION IRAM DE VIBR
*______________________________________________________
*
MOD4=VIBR SIMU 0.5 1 RAI MAS;
F4= MOD4 . MODES . 1 . FREQUENCE;
PRE= MOD4 . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
X4=EXTR PP P A2;Y4=EXTR PP P A5;Z4=EXTR PP P A3;
*
*______________________________________________________
* ON RECOMMENCE LES OPERATIONS CETTE FOIS
* AVEC L'HARMONIQUE 3
*______________________________________________________
*
OPTI MODE FOUR 3;
MODLIQ1=MODE FLUID LIQUIDE LQU4;
MODLIQ2=MODE SURFA LIQUIDE LSU2;
*
MODLIQ=MODLIQ1 Et MODLIQ2;
*
MAT=MATE MODLIQ RHO RO0 RORF ROREF CSON CS CREF CEREF LCAR LCARA G GRA;
*
RAI=RIGI MODLIQ MAT;
MAS=MASS MODLIQ MAT;
*
MOD1=VIBR INTE 0. 1. RAI MAS;
*------------------------------------------------------
G1= MOD1 . MODES . 1 . FREQUENCE;
PRE= MOD1  . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
T1=EXTR PP P A2;U1=EXTR PP P A5;V1=EXTR PP P A3;
*------------------------------------------------------
MOD2=VIBR PROC (PROG 0.9) RAI MAS;
*
G2=MOD2 . MODES . 1 . FREQUENCE;
PRE= MOD2  . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
T2=EXTR PP P A2;U2=EXTR PP P A5;V2=EXTR PP P A3;
*------------------------------------------------------
MOD3=VIBR SIMU 0.9 1 RAI MAS;
G3=MOD3 . MODES . 1 . FREQUENCE;
PRE= MOD3  . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
T3=EXTR PP P A2;U3=EXTR PP P A5;V3=EXTR PP P A3;
*------------------------------------------------------
MOD4=VIBR SIMU 0.9 1 RAI MAS;
G4=MOD4 . MODES . 1 . FREQUENCE;
PRE= MOD4  . MODES . 1 . DEFORMEE_MODALE;
PBAS=EXTR PRE P A2;
PP=PRE/PBAS;
T4=EXTR PP P A2;U4=EXTR PP P A5;V4=EXTR PP P A3;
*------------------------------------------------------
SAUT PAGE; SAUT 2 LIGNE ;
MESS '*';
MESS '********    ON VERIFIE LES FREQUENCES';
MESS '*';
MESS ' ';
MESS '         ANALYTIQUE   *   INTERVALLE    *     PROCHE *    SIMULTANE * IRAM';
MESS '         -------------*-----------------*----------- -----*-------------';
MESS ' HARM 1    0.489      *   ' F1 ' * ' F2 ' * ' F3 ' * ' F4;
MESS ' HARM 3    0.863      *   ' G1 ' * ' G2 ' * ' G3 ' * ' G4;
MESS ' ';
MESS '*';
MESS '********   ON VERIFIE LE CHAMP DE PRESSION AUX POINTS A1,A5 ET A3';
MESS '*';
MESS ' ';
MESS '        ANALYTIQUE   *   INTERVALLE    *     PROCHE *    SIMULTANE * IRAM';
MESS '        -------------*-----------------*----------- -----*-------------';
MESS 'HARM 1     1.00      *  ' X1 ' * ' X2 ' * ' X3' * ' X4 ;
MESS 'HARM 1     1.81      *  ' Y1 ' * ' Y2 ' * ' Y3' * ' Y4 ;
MESS 'HARM 1     3.76      *  ' Z1 ' * ' Z2 ' * ' Z3' * ' Z4 ;
MESS 'HARM 3     1.00      *  ' T1 ' * ' T2 ' * ' T3' * ' T4 ;
MESS 'HARM 3    18.31      *  ' U1 ' * ' U2 ' * ' U3' * ' U4 ;
MESS 'HARM 3   201.72      *  ' V1 ' * ' V2 ' * ' V3' * ' V4 ;
TEMPS ;

*------------- code de bon fonctionnement ------------*
SAUT 1 PAGE;
ERR   = TABL;
ERR.1 = 100 * (ABS(0.489 -F1)/0.489);
ERR.2 = 100 * (ABS(0.489 -F2)/0.489);
ERR.3 = 100 * (ABS(0.489 -F3)/0.489);
ERR.4 = 100 * (ABS(0.863 -G1)/0.863);
ERR.5 = 100 * (ABS(0.863 -G2)/0.863);
ERR.6 = 100 * (ABS(0.863 -G3)/0.863);
ERR.7 = 100 * (ABS(1-X1)/1);
ERR.8 = 100 * (ABS(1-X2)/1);
ERR.9 = 100 * (ABS(1-X3)/1);
ERR.10= 100 * (ABS(1.81 -Y1)/1.81);
ERR.11= 100 * (ABS(1.81 -Y2)/1.81);
ERR.12= 100 * (ABS(1.81 -Y3)/1.81);
ERR.13= 100 * (ABS(3.76 -Z1)/3.76);
ERR.14= 100 * (ABS(3.76 -Z2)/3.76);
ERR.15= 100 * (ABS(3.76 -Z3)/3.76);
ERR.16= 100 * (ABS(1-T1));
ERR.17= 100 * (ABS(1-T2));
ERR.18= 100 * (ABS(1-T3));
ERR.19= 100 * (ABS(18.31 -U1)/18.31);
ERR.20= 100 * (ABS(18.31 -U2)/18.31);
ERR.21= 100 * (ABS(18.31 -U3)/18.31);
ERR.22= 100 * (ABS(201.72-V1)/201.72);
ERR.23= 100 * (ABS(201.72-V2)/201.72);
ERR.24= 100 * (ABS(201.72-V3)/201.72);
* AJOUT IRAM
ERR.25 = 100 * (ABS(0.489 -F4)/0.489);
ERR.26 = 100 * (ABS(0.863 -G4)/0.863);
ERR.27 = 100 * (ABS(1     -X4)/1);
ERR.28 = 100 * (ABS(1.81  -Y4)/1.81);
ERR.29 = 100 * (ABS(3.76  -Z4)/3.76);
ERR.30 = 100 * (ABS(1     -T4));
ERR.31 = 100 * (ABS(18.31 -U4)/18.31);
ERR.32 = 100 * (ABS(201.72-V4)/201.72);

I=0;
REPETER BOUC1 32;
I=I+1;
  SI (ERR.I <EG 1);
   SAUT 1 LIGN;
   ERRE 0;
  SINON;
   MESS 'erreur valeur ' I ' du tableau :' ERR.I;
   ERRE 5;
  FINSI;
FIN BOUC1;

FIN;








