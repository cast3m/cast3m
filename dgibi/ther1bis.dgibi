* fichier :  ther1bis.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************

*******************************************
*  Test ther1bis.dgibi: jeux de données   *
*  ------------------------------------   *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*-----------------------------------------*
*                THER1BIS                 *
*                                         *
*    TEST D'UN PROBLEME DE DIFFUSION      *
*      AVEC UNE TEMPERATURE IMPOSEE       *
*         ET UNE SOURCE REPARTIE          *
*-----------------------------------------*
*                                         *
*    TEMPERATURE IMPOSEE + SOURCE         *
*                                         *
* Ce test permet de vérifier le bon       *
* fonctionnement des divers               *
* opérateurs thermiques de CAST3M         *
*                                         *
* Une plaque rectangulaire constituée     *
* d'éléments QUA4 est soumise à           *
* une température imposée à une de ses    *
* extrémités et une condition de source   *
* volumique imposée sur une partie        *
*                                         *
* Les résultats sont comparés à la        *
* solution analytique du problème         *
*                                         *
*-----------------------------------------*

*----- OPTIONS GENERALES DE CALCUL -------*
OPTI DIME 2 ELEM QUA4 ;

TEMPS ;

*- CREATION DE LA GEOMETRIE:       *
*   POINTS SUPPORTS DES ELEMENTS - *

A1 = 0. 0. ;A2 = 3. 0. ;
B1 = 0. 4. ;B2 = 3. 4. ;
C1 = 0. 6. ;C2 = 3. 6. ;

*- CREATION DES LIGNES -*
L1 = D 50 A1 A2;
L2 = D 50 B1 B2;
L3 = D 50 C1 C2;

*- CREATION DES SURFACES -*
S1 = (REGL 100 L1 L2) COUL BLEU;
S2 = (REGL 50  L2 L3) COUL ROUG;

STOT = S1 ET S2;

SI(NEG GRAPH 'N');
    TRACE 'QUAL' STOT ;
FINSI;

*--- DONNEES DU PROBLEME DE THERMIQUE ----*

*--------------  MODELISATION ------------*

MOD1 = MODE STOT  THERMIQUE ISOTROPE ;

*DONNEES DES CARACTERISTIQUES DU MATERIAU

KCOND = 100. ;
MAT1  = MATE MOD1 'K' KCOND ;

*- CREATION DES MATRICES DE CONDUCTIVITE -*

CND1 = CONDUCTIVITE MOD1 MAT1 ;

*- TEMPERATURES IMPOSEES: BLOQUE + DEPI --*

BB1 = BLOQUE L1 'T' ;
T0  = 10. ;
EE1 = DEPI BB1 T0. ;

*----------- SOURCE DE CHALEUR -----------*
VALQ = 1000. ;
S1 = SOURCE MOD1 VALQ S2 ;

*- ASSEMBLAGE DES TERMES DE CONDUCTIVITE -*
CCC = CND1 ET BB1 ;

*-ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS
FFF = EE1 ET S1 ;

*------------- RESOLUTION ----------------*
CHTER = RESO CCC FFF ;

*--- POST-TRAITEMENT: TRACE DES CHAMPS ---*
*               RESULTATS                 *
*- ET CALCUL DES VALEURS CARACTERISTIQUES *

TITR 'TEMPERATURE' ;
SI(NEG GRAPH 'N');
     TRACER STOT CHTER ;
FINSI;

T = EXTR CHTER T C2 ;

TEMPS ;

*   CODE DE FONCTIONNEMENT

* Calcul de la température en C2
TREF  = T0 - ( (VALQ / (2. * KCOND)) *
              (
               ((COOR 2 C2)**2) -
               (2*(COOR 2 C2)*(COOR 2 C2))+
               ((COOR 2 B2)**2)
              )
             );

RESI1=100. * (ABS((T-TREF)/TREF));

* TEST SOURCE
MESS 'Temperature theorique :' TREF '°C';
MESS 'Temperature calculee  :' T    '°C';
MESS '    Soit un ecart de : ' RESI1 '%';
SAUTER 1 LIGNES ;


RESITOT = PROG RESI1 ;
SI((MAXI RESITOT) <EG 1.);
  ERRE  0;
SINO;
  ERRE  5;
FINSI;
FIN;




