* fichier : INTG_test.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************


************************************************************************
* Ce Cas-Test permet de tester l'operateur INTG dans differentes
* configurations d'options
************************************************************************

OPTI DIME 3 ELEM 'CUB8';
XZPREC = (VALE 'PREC') * 100.D0;

************************************************************************
*  MAILLAGE d'un CUBE de cote L
************************************************************************
L  = 2.D0 ;
NBE1 = 10 ;

P1 = 0. 0. 0. ;
P2 = L  0. 0. ;

L1 = DROI NBE1 P1 P2;
L2 = L1 PLUS (0. L  0.);

S1 = L1 REGL NBE1 L2;
S2 = S1 PLUS (0. 0. L);

V1 = S1 VOLU NBE1 S2;

*TRAC CACH V1;


************************************************************************
*  MODELE et MCHAML
************************************************************************
MOD1 = MODE V1 'MECANIQUE' 'ELASTIQUE';
MCH1 = MANU 'CHML' MOD1 'VOL1' 1.D0   ; COMM 'MCHAML CONSTANT';

MOD2 = REDU MOD1 (VIDE 'MAILLAGE');


************************************************************************
*  INTEGRALES
************************************************************************
INTG11 = INTG MOD1 MCH1;

TEST = ABS (INTG11 - (L ** 3)) ;
MESS TEST XZPREC;
SI (TEST > XZPREC);
  MESS 'ERREUR 1 DANS INTG';
  ERRE 5;
FINS;


INTG12 = INTG MOD1 MCH1 'ELEM';
MIN12  = MINI INTG12;
MAX12  = MAXI INTG12;
DELTA  = MAX12 - MIN12;
TEST   = MAX12 - (INTG11 / (NBE1 **3));
MESS MIN12 MAX12 DELTA TEST;
SI ((DELTA > XZPREC) OU (TEST > XZPREC));
  MESS 'ERREUR 2 DANS INTG';
  ERRE 5;
FINS;


INTG21 = INTG MOD2 MCH1;
MESS INTG21;
SI (INTG21 > 0.D0);
  MESS 'ERREUR 3 DANS INTG';
  ERRE 5;
FINS;


INTG22 = INTG MOD2 MCH1 'ELEM';
MAIL22 = EXTR INTG22 'MAIL';
TEST   = NBEL MAIL22;
SI (TEST NEG 0);
  MESS 'ERREUR 4 DANS INTG';
  ERRE 5;
FINS;

FIN;





