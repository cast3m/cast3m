* fichier :  soravs.dgibi
************************************************************************
* Section : Entree-Sortie Entree-Sortie
* Section : Mecanique Endommagement
************************************************************************
*********************************************************
*
*  Test du fonctionnement de la directive SORT 'AVS' et
*  de l'opérateur LIRE 'AVS'. On effectue les deux
*  opérations et on vérifie que le CHPOINT lu est égal
*  à celui qui a été écrit.
*
*  Auteur : M. Bulik
*
*  Date : Septembre '95
*
*********************************************************
*** options générales ...

    opti dime 3 echo 1 elem seg2 mode trid ;

*** noeuds ...

    dens 0.1 ;

    p1 = 0 0 0 ;
    p2 = 1 0 0 ;

*** lignes ...

    li1 = p1 d 1 p2 ;
    li2 = li1 plus (0.1 1. 0.) ;

*** surfaces ...

    opti elem qua4 ;

    su1 = li1 regl 1 li2 ;

*** volumes ...

    opti elem cub8 ;

    vo1 = su1 volu tran 2 (-0.1 0.1 1.) ;

*** Modèle ...

    mo1 = mode vo1 mecanique elastique ;

*** Matériau ...

    ma1 = mate mo1 YOUN 2.e+11 NU 0.3 ;

*** Rigidité et CL ...

    ri1 = rigi mo1 ma1 ;
    cl1 = bloq depl su1 ;
    ritot = ri1 et cl1 ;

*** Chargement ...

    fo1 = forc fx 10000 (vo1 poin proc (1 1 1)) ;

*** Résolution ...

    de1 = reso ritot fo1 ;

*** Les contraintes ...

    si1 = sigm mo1 ma1 de1 ;
    si1g = chan GRAVITE si1 mo1 ;

*** la sortie AVS ...

    opti sort 'test-soravs.inp' ;

    valtemp = 1.23456789 ;
    sort 'AVS' de1 si1g 'SUIT' 'TEMP' valtemp ;

    de2 = 2 * de1 ;
    si2g = 2 * si1g ;
    sort 'AVS' de2 si2g 'SUIT' ;

*** La lecture AVS ...

    opti sort 'toto' ;
    opti lect 'test-soravs.inp' ;

    tblect1 = lire AVS ;

    tblect2 = lire AVS ;

*** La comparaison ...

    list (nbno vo1) ;
    elim (vo1 et (tblect1 . LEMAILLA) et (tblect2 . LEMAILLA)) ;

    de1redu = redu de1 vo1 ;
    chpodiff = de1redu - (tblect1 . LECHPOIN) ;
    list (abs (maxi chpodiff)) ;
    si ((abs (maxi chpodiff)) > 1.e-14) ;
       erreur 5 ;
    finsi ;

*    chamdiff = si1g + (-1 * (tblect . LEMCHAML)) ;
*    list (abs (maxi chamdiff)) ;
*    si ((abs (maxi chamdiff)) > 1.e-10) ;
*       erreur 5 ;
*    finsi ;

    valdiff = abs (valtemp - (tblect1 . 'time'));
    list valdiff ;
    si(valdiff > 1.e-7) ;
       erreur 5 ;
    finsi ;

    de2redu = redu de2 vo1 ;
    chpodiff = de2redu - (tblect2 . LECHPOIN) ;
    list (abs (maxi chpodiff)) ;
    si ((abs (maxi chpodiff)) > 1.e-14) ;
       erreur 5 ;
    finsi ;

*** Bye ...

    fin ;








