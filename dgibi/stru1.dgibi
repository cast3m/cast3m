* fichier :  stru1.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST
SAUT PAGE;

*********************************************************************
*                                                                   *
*   TEST STRU1                                                      *
*                                                                   *
*   PLAQUE RAIDIE SUR APPUIS SIMPLES, SOUS CHARGE UNIFORMEMENT      *
*     REPARTIE                                                      *
*                                                                   *
*     La plaque rectangulaire de 2 metre de long, de 1 metre de     *
*   large et 0.06 metre d'epaisseur est raidie a l'aide de 6        *
*   raidisseurs de 0.01 metre d'epaisseur.                          *
*                                                                   *
*     La plaque superieure (uniquement) repose simplement sur les   *
*   quatre cotes et est uniformement chargee.                       *
*                                                                   *
*     Pour des raisons de symetrie on ne maille que le quart de la  *
*   structure.                                                      *
*                                                                   *
*     On se propose de chercher :                                   *
*       - d'une part le deplacement vertical du milieu de la plaque,*
*       - d'autre part le deplacement vertical maximal.             *
*                                                                   *
*   Ref. : JL. BATOZ,J.CHATELAIN,JP. JAMEUX                         *
*         ETUDE D UNE PLAQUE RAIDIE                                 *
*         BULLETIN DU CLUB S.A.P - VOLUME VIII, N1                 *
*                                                                   *
*********************************************************************

GRAPH = 'N';

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*
OPTION DIME 3;
OPTION ELEM QUA4;

*
*  QUELQUES DONNEES
*
OEIL = 10000 -10000 10000;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                             GEOMETRIE                              *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*            PLAQUE SUPERIEURE
*
PA = 0 .5 0; PB =1. .5 0; PD = 1. 0 0; PC = 0 0 0;
PF = .6667 0 0; PE = .3333 0 0;
*
DAB = PA DROIT 18 PB; DBD = PB DROIT 10 PD;
DDC = PD DROIT 18 PC; DCA = PC DROIT 10 PA;
SU1 = DALLER DAB DBD DDC DCA PLAN;
*
*            RAIDISSEUR CENTRAL
*
PA2 = 0 .5 -.1; PC2 = 0 0 -.1;
D1 = PA DROIT 10 PC;
D3 = PC2 DROIT 10 PA2;
D2 = PC DROIT 2 PC2;
D4 = PA2 DROIT 2 PA;
RA1 = DALL D1 D2 D3 D4 PLAN;
*
* SEGMENTS DE DROITES POUR CONDITIONS DE SYMETRIE
D5 = D2 PLUS (.33333 0. 0.);
D6 = D2 PLUS (.66667 0. 0.);
*
*            RAIDISSEURS 2 ET 3
*
RA2 = RA1 PLUS (.33333 0 0) ;
RA3 = RA1 PLUS (.66667 0 0) ;
*
SURF = SU1 ET RA1 ET RA2 ET RA3;
*
ELIM SURF .0001;
ELIM (SURF ET D1 ET D2 ET D3 ET D4 ET D5 ET D6) .001;
*
SI (NEG GRAPH 'N');
  TITR ' TEST STRU1 : MAILLAGE';
  TRAC CACH OEIL QUAL SURF;
FINSI;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*      MODELE                                                   *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MODL1 = MODE RA1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODL2 = MODE RA2 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODL3 = MODE RA3 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODL4 = MODE SU1 MECANIQUE ELASTIQUE ISOTROPE COQ4;
MODL5 = MODL1 ET MODL2 ET MODL3 ET MODL4;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*     MATERIAUX - CARACTERISTIQUES                              *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MATR1 = MATE MODL1 YOUN 2.E11 NU 0.3 EPAI 0.005 EXCEN 0.0025;
MATR2 = MATE MODL2 YOUN 2.E11 NU 0.3 EPAI 0.01  EXCEN 0.0;
MATR3 = MATE MODL3 YOUN 2.E11 NU 0.3 EPAI 0.01  EXCEN 0.0;
MATR4 = MATE MODL4 YOUN 2.E11 NU 0.3 EPAI 0.006  EXCEN 0.0;
MATR5 = MATR1 ET MATR2 ET MATR3 ET MATR4;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*             CONDITIONS AUX LIMITES                                *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = BLOQUE DAB UZ;
CL2 = BLOQUE DAB RY;
CL3 = BLOQUE DBD UZ;
CL4 = BLOQUE DBD RX;
CL5 = SYMT DEPL ROTA (0 0 0) (0 1 0) (0 0 1) SURF 0.0001;
CL6 = SYMT DEPL ROTA (0 0 0) (1 0 0) (0 0 1) SURF 0.0001;
CL = CL1 ET CL2 ET CL3 ET CL4 ET CL5 ET CL6;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                 CHARGEMENT
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MOP = 'MODE' SU1 'CHARGEMENT' 'PRESSION' 'COQ4' ;
MAP = 'PRES' MOP 'PRES' 5E4 ;
CHARG = 'BSIG' MOP MAP ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*           RESOLUTION - CALCUL ELASTIQUE
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
RIG = RIGI MODL5 MATR5;
RIG = RIG ET CL;
*
DEP = RESO RIG CHARG;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
* RESULTATS
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
NOSURF = NBNO SURF ;
ELSURF = NBEL SURF ;
*
SI (NEG GRAPH 'N');
  AMPL1 = 1.E-5;
  REA1 = REAC DEP RIG;
  VE = VECT REA1 AMPL1 FX FY FZ BLEU ;
  VF = VECT CHARG (AMPL1 * 20) FX FY FZ ROUGE;
  VV = VE ET VF;
  DEF0 = DEFO SURF DEP 0.;
  DEF1 = DEFO SURF DEP 25. VV VERT;
  TITR ' STRU1 : DEFORMEE, CHARGEMENT, REACTIONS' ;
  TRACE OEIL (DEF0 ET DEF1);

  TITR 'STRU1 : Deplacements UZ';
  TRAC CACH SURF (EXCO DEP UZ);

  DEF1 = DEFO SURF DEP 25. ROUGE;
  OEIL = 0.25 -1.E6 0.;
  TITR ' STRU1 : DEFORMEE';
  TRACE OEIL (DEF0 ET DEF1);
FINSI;
*
UZC = EXTR DEP UZ PC ;
UZC1 = -0.475E-3;
ERG1 = ABS ((UZC - UZC1) / UZC1) * 100 ;
*
DZ = EXCO DEP UZ;
ZMIN = MAXI (ABS (DZ)); ZMIN = -1. * ZMIN;
ZMIN1 = -0.126E-2;
ERG2 = ABS ((ZMIN - ZMIN1) / ZMIN1) * 100 ;
*
SAUT PAGE ;
SAUT 2 LIGN;
*
MESS ' RESULTATS ';
MESS ' --------- ';
*
SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE AU POINT PC : ' UZC1 'METRES';
MESS ' DEPLACEMENT CALCULE AU POINT PC   : ' UZC  'METRES' ;
MESS '     SOIT UN ECART DE              : ' ERG1 '%' ;
*
SAUT 2 LIGN ;
MESS ' DEPLACEMENT THEORIQUE MAXIMUM (ABS) : ' ZMIN1 'METRES';
MESS ' DEPLACEMENT CALCULE MAXIMUM (ABS)   : ' ZMIN 'METRES';
MESS '     SOIT UN ECART DE                : ' ERG2 '%' ;
*
*SAUT 1 LIGNE;
*MESS ' ABSICE DU POINT OU SE TROUVE LE MAXIMUM : ' ;
*MESS '         - ABSICE THEORIQUE : ' XX1 'METRES';
*MESS '         - ABSICE CALCULEE  : ' XX  'METRES';
*MESS '     SOIT UN ECART DE       : '  ERG3  '%' ;
*
SAUT 1 LIGNE;
MESS ' Nombre de noeuds  : ' NOSURF ;
MESS ' Nombre d elements : ' ELSURF ;
MESS '  Soit ' (NOSURF * 6) 'd.d.l.';
SAUT 1 LIGNE;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*            CODE FONCTIONNEMENT                                     *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
ERGMAX = MAXI (PROG ERG1 ERG2);
*
* Il y a erreur si un des pourcentages ERG* est suprieur  5%
*
SI (ERGMAX <EG 5.);
    ERRE  0 ;
SINO;
    ERRE  5 ;
FINSI;
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;










