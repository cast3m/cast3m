* fichier :  relaxendo.dgibi
************************************************************************
* Section : Mecanique Viscoendommagement
* Section : Mecanique Viscoplastique
************************************************************************
complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
OPTI ECHO 0 DIME 2 ELEM QUA8 MODE AXIS;
*
*-----------------------------------------------------------------
*  MAILLAGE AXISYMETRIQUE EPROUVETTE CYLINDRIQUE
*  MATERIAU VISCO-PLASTIQUE ENDOMMAGEABLE AVEC MATERIAU DEPENDANT
*                           =============
*  DE LA TEMPERATURE (MODULE D'YOUNG ET COEFFICIENT DE DILATATION
*                     THERMIQUE)
*-----------------------------------------------------------------
   P1 = 0 0;   P2 = 3E-3 0;   P3 = 3E-3 30E-3;   P4 = 0 30E-3;
*
   L1 = P1 P2 DROIT 1  ;
   L2 = P2 P3 DROIT 1 ;
   L3 = P3 P4 DROIT 1  ;
   L4 = P4 P1 DROIT 1 ;
*
*  mesh
*
   EPROU = L1 L2 L3 L4 DALLER PLAN ;
*
* boundary conditions
*
   CL1 = BLOQ L1 UZ ;
   CL4 = BLOQ L4 UR ;
*
   CL3 = BLOQ L3 UZ ;
   FO3 = DEPI CL3 1E-4 ;
   CL = CL1 ET CL4 ET CL3 ;
*
* MODE defines the behavior of the material and the finite element
*                  formulation
*
   MO = MODE EPROU MECANIQUE ELASTIQUE VISCOPLASTIQUE VISCODOMMAGE;
*
   PROGKSI = PROG 0. 10000. ;
   PROGK1 = PROG 15. 15. ;
   CTRAC1 = EVOL MANU KSI PROGKSI K PROGK1  ;
*
    evalph = EVOL MANU 'T' (PROG 1000 1025) 'ALPH' (PROG 1E-4 1E-3) ;
  EVY = EVOL MANU 'T' (PROG 1000 1025) 'YOUN' (PROG 300000 150000);
    MATVAR = MATE MO YOUN EVY NU 0. rho 7800. ALPH evalph
                  N 10.8 M 9.8 KK 1450. ALP1 0. BLP1 0.
                  R 5.2 A 2511.35
                  EVOL CTRAC1 SMAX 50. ;
   MAT = MATE MO YOUN 150000. NU 0. rho 7800. alph 1.E-3
                  N 10.8 M 9.8 KK 1450. ALP1 0. BLP1 0.
                  R 5.2 A 2511.35
                  EVOL CTRAC1 SMAX 50. ;
*
*       definition des cartes de temperature
*
   TEMP0 = MANU CHPO EPROU 1 'T' 1025.   ;
   TEMP1 = MANU CHPO EPROU 1 'T' 1000.   ;
*
   RIG = (RIGI MO MAT) ET CL ;
*
   DEPI = RESO RIG FO3 ;
   SIGI = SIGMA DEPI MAT MO ;
*
   t0 = 0. ;  t1 = 8.E5  ; tf = 1E9 ;
   LISTT = PROG t0 tf ;
   LISTF = PROG 2*1.  ;
*
   EV = EVOL MANU 'TEMPS' LISTT 'FORCE' LISTF;
   CHA1 = CHAR DIMP FO3 EV ;
*
   TAB = TABLE ;
   TEMP = TABLE  ; TEMPER = TABLE;
   TEMP . 0 = 0.; TEMP . 1 = 8.E5;
   TEMPER . 0 = TEMP0 ; TEMPER . 1 = TEMP1 ;
   CHA2 = CHAR T TEMP TEMPER;
   CHA = CHA1 ET CHA2;
   TAB.'DEPLACEMENTS' = TABLE;
   TAB.'CONTRAINTES' = TABLE;
   TAB.'DEPLACEMENTS' . 0 = DEPI   ;
   TAB.'CONTRAINTES' . 0  = SIGI   ;
si complet;
   LIS = prog 1E-3 pas 1E-3
        1E-2 PAS 1E-2 1E-1 .2 pas 0.2 1 2 PAS 2 10 20
        PAS 20 100 200 pas 200 1000
        2000 pas 2000 10000 PAS 1000 12000 ;
sinon;
  LIS =  prog 1E-3 pas 1E-3
        6.E-3 ;
finsi;

TAB.'BLOCAGES_MECANIQUES' = CL;
TAB.'MODELE' = MO;
TAB.'CHARGEMENT' = CHA;
TAB.'TEMPS_CALCULES' = LIS;
TAB.'CARACTERISTIQUES' = MATVAR;
TAB.'TALPHA_REFERENCE' = 20. ;
tab.maxisouspas = 200;

PASAPAS TAB ;
*
*
*----------------------------------------------------------------
*  CONTROLE DES RESULTATS AVEC DE LA SOLUTION DE REFERENCE
*               OBTENUE PAR ALGORITHME
*----------------------------------------------------------------
*
si complet;
 REF_D = PROG 7.86E-3 3.33E-2 1.254E-1 ;
 REF_P = PROG 4.15E-3 8.66E-3 1.55E-2  ;
 REF_S = PROG 231.0 242.2 230.1        ;
TA = 2000.;
TB = 6000.;
TC = 12000.;
sinon;
TA= 3.e-3;
TB = 5.e-3;
TC = 6.e-3;
REF_D = PROG  4.90537E-07  7.65936E-07  8.95267E-07  ;
REF_P = PROG  2.75079E-04  3.29655E-04  3.50683E-04  ;
REF_S = PROG   458.74       450.55       447.40 ;
finsi;
*
D1 = EXTR (PECHE TAB VARIABLES_INTERNES TA)  'VHWD' 1 1 1;
P1 = EXTR (PECHE TAB VARIABLES_INTERNES TA)  'EPSE' 1 1 1;
S1 = EXTR (PECHE TAB CONTRAINTES TA)         'SMZZ' 1 1 1;
D2 = EXTR (PECHE TAB VARIABLES_INTERNES  TB)  'VHWD' 1 1 1;
P2 = EXTR (PECHE TAB VARIABLES_INTERNES  TB)  'EPSE' 1 1 1;
S2 = EXTR (PECHE TAB CONTRAINTES  TB)         'SMZZ' 1 1 1;
D3 = EXTR (PECHE TAB VARIABLES_INTERNES TC) 'VHWD' 1 1 1;
P3 = EXTR (PECHE TAB VARIABLES_INTERNES TC) 'EPSE' 1 1 1;
S3 = EXTR (PECHE TAB CONTRAINTES TC)        'SMZZ' 1 1 1;
 D = PROG D1 D2 D3 ;
 P = PROG P1 P2 P3 ;
 S = PROG S1 S2 S3 ;


*
errd = MAXI (ABS ((REF_D - D) / REF_D)) ;
errp = MAXI (ABS ((REF_P - P) / REF_P)) ;
errs = MAXI (ABS ((REF_S - S) / REF_S)) ;
*
err = MAXI (prog errd errp errs) ;
*
temps;
SI ( ERR <EG 0.05 );
   ERRE 0;
SINON;
   ERRE 5;
FINSI;
*
FIN;














