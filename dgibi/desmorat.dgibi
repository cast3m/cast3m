* fichier : desmorat.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************

OPTI dime 3 elem CUB8  echo 0;
*-------------------définition de la géométrie--------------------
P1 = 0. 0. 0.;
P2 = 0. 0.1 0.;
P3 = 0.1 0.1 0.;
P4 = 0.1 0. 0.;
V1 = 0. 0. 0.1;
P5 = 0.1 0. 0.1;
Li1 = D 1 P1 P2 ;
Li2 = D 1 P2 P3;
Li3 = D 1 P3 P4;
Li4 = D 1 P4 P1;
Ltot = Li1 ET Li2 ET Li3 ET li4;
Surf1 = SURF Ltot PLANE;
Vovo = VOLU Surf1 1  TRANS V1;
Surf2 = FACE 2 Vovo;
Surf3 = Vovo POIN PLAN P1 P4 P5 0.001;
Surf4 = Vovo POIN PLAN P3 P4 P5 0.001;
ELIM 0.01 Vovo ;
*-----------MODELE------------------
    Mod1= MODE Vovo MECANIQUE ELASTIQUE ENDOMMAGEMENT DESMORAT CUB8;
    kappa0 = 5E-05 ;
    Mat1 = MATE Mod1 RHO 2400 YOUN 42.E09 NU 0.2 'K0' kappa0 'A' 5E+3
    'a' 2.93E-4
    'etaC' 0. 'etaT' 3. 'Dc' 0.99;


    list mod1;
*-------------CONDITIONS LIMITES-------------
CL1 = BLOQ Li1 Ux;
CL2 = BLOQ P2 Uy;
CL3 = BLOQ Surf1 Uz ;
Cldep = BLOQ Surf2 Uz;
*-------------ASSEMBLAGE CONDITIONS LIMITES
CLtot = CL1 ET CL2 ET CL3  ET  CLdep;
*CLtot = CL3  ET  CLdep;
*--------------CHARGEMENT---------------
FO1 = DEPI Cldep -1;
LI1 = PROG 0. 1e-2;
LI2 = PROG 0  1e-3 ;
EV = EVOL MANU LI1 LI2 ;
CHA1 =  CHAR 'DIMP' FO1 EV;
LIS1 = PROG 0. PAS 1e-5 0.3e-2 ;
TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CLtot;
TAB1.'MODELE' = Mod1;
TAB1.'CHARGEMENT' = CHA1;
TAB1.'CARACTERISTIQUES' = Mat1;
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'MOVA' = 'D11';
TAB1.DYNAMIQUE=faux ;
*--------------RESOLUTION--------------
PASAPAS TAB1;

n= dime (tab1.deplacements) - 1;
var = tab1.variables_internes.n ;
tra1= extr var 'D11 ' 1 1 1 ;
tra2= extr var 'D22 ' 1 1 1 ;
tra3= extr var 'D33 ' 1 1 1 ;

D11 = 0.90687;
D22 = 0.90589;
D33 = 4.18284E-22;

err1 = abs ((TRA1 - D11)/D11 * 100) ;
err2 = abs ((TRA2 - D22)/D22 * 100) ;
*err3 = abs ((TRA3 - D33)/D33 * 100) ;
err3 = (abs ( d33 - tra3)) * 1.e9;

mess ' err1 ' err1  ' err2 ' err2 ' err3 ' err3;
si ((err1 > .06) ou (err2 > .06) ou (err3 > .1));
 erre 5;
sinon ;
 erre 0 ;
finsi ;

fin;





