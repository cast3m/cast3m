* fichier : thgdep1.dgibi
IG1      = FAUX ;
*----------------------------------------------------------------------*
* Cas-test de calcul thermomecanique en grands deplacements, avec      *
* convergence thermique-mÃ©canique.                                     *
* On teste egalement la reprise en fournissant une liste des temps     *
* calcules qui debute au temps suivant.                                *
*----------------------------------------------------------------------*
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;
*                                                                      *
O1       = 0. 0. ;
X1       = 1. 0. ;
Y1       = 0. 1. ;
*                                                                      *
L1       = O1 'DROI' 5 X1 ;
S1       = L1 'TRAN' 3 Y1 ;
L2       = S1 'COTE' 2 ;
L3       = S1 'COTE' 3 ;
L4       = S1 'COTE' 4 ;
'SI' IG1 ;
  'TRAC' 'QUAL' S1 ;
'FINS' ;
*                                                                      *
MODT1    = 'MODE' S1 'THERMIQUE' ;
MODM1    = 'MODE' S1 'MECANIQUE' ;
*                                                                      *
MATT1    = 'MATE' MODT1 'K' 100. 'RHO' 0. 'C' 1. ;
MATM1    = 'MATE' MODM1 'YOUN' 2.E9 'NU' 0.25 'ALPH' 1.E-6 ;
*                                                                      *
MOD1     = MODT1 'ET' MODM1 ;
MAT1     = MATT1 'ET' MATM1 ;
*                                                                      *
CLM1     = ('BLOQ' L1 'UY') 'ET' ('BLOQ' L4 'UX') ;
CLT1     = 'BLOQ' L1 'T' ;
*
F1       = 'PRES' 'MASS' MODM1 L3 (-1.E8) ;
Q1       = 'FLUX' MODT1 L3 300. ;
QREF1    = 'FLUX' MODT1 L3 1. ;
*                                                                      *
LTPS1    = 'PROG' 0. 1.0 2. ;
LAMP1    = 'PROG' 0. 0.5 1. ;
EV1      = 'EVOL' 'MANU' 'TEMP' LTPS1 'AMPL' LAMP1 ;
*                                                                      *
CGM1     = 'CHAR' 'MECA' F1 EV1 ;
CGT1     = 'CHAR' 'Q'    Q1 EV1 ;
*                                                                      *
TAB1                         = 'TABL' ;
TAB1 . 'MODELE'              = MOD1 ;
TAB1 . 'CARACTERISTIQUES'    = MAT1 ;
TAB1 . 'BLOCAGES_MECANIQUES' = CLM1 ;
TAB1 . 'BLOCAGES_THERMIQUES' = CLT1 ;
TAB1 . 'CHARGEMENT'          = CGM1 'ET' CGT1 ;
TAB1 . 'TEMPS_CALCULES'      = 'PROG' 0. 1. ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'RELAXATION_THETA'    = 1. ;
TAB1 . 'CONVERGENCE_MEC_THE'    = VRAI ;
*                                                                      *
PASAPAS TAB1 ;
*                                                                      *
CHT1     = TAB1 . 'TEMPERATURES' . 1 ;
DEP1     = TAB1 . 'DEPLACEMENTS' . 1 ;
*                                                                      *
UXM1    = 'MINI' ('EXCO' DEP1 'UX') ;
UYM1    = 'MAXI' ('EXCO' DEP1 'UY') ;
TM1     = 'MAXI' ('ENLE' CHT1 'LX') ;
FLU1    = ('MAXI' ('TIRE' CGT1 'Q' 1.)) / ('MAXI' QREF1) ;
TREF1   = FLU1 / ('EXTR' MATT1 'K' 1 1 1) * (1. + UYM1) / (1. + UXM1) ;
ERR1    = 'ABS' (TM1 - TREF1) ;
*                                                                      *
TAB1 . 'TEMPS_CALCULES'      = 'PROG' 2. ;
*                                                                      *
PASAPAS TAB1 ;
*                                                                      *
IFIN1    = ('DIME' TAB1 . 'TEMPERATURES') - 1 ;
CHT2     = TAB1 . 'TEMPERATURES' . IFIN1 ;
DEP2     = TAB1 . 'DEPLACEMENTS' . IFIN1 ;
*                                                                      *
UXM2    = 'MINI' ('EXCO' DEP2 'UX') ;
UYM2    = 'MAXI' ('EXCO' DEP2 'UY') ;
TM2     = 'MAXI' ('ENLE' CHT2 'LX') ;
FLU2    = ('MAXI' ('TIRE' CGT1 'Q' 2.)) / ('MAXI' QREF1) ;
TREF2   = FLU2 / ('EXTR' MATT1 'K' 1 1 1) * (1. + UYM2) / (1. + UXM2) ;
ERR2    = 'ABS' (TM2 - TREF2) ;
*                                                                      *
'SI' ((ERR1 > 1.E-4) 'OU' (ERR2 > 1.E-4)) ;
  'ERRE' 5 ;
'SINO' ;
  'OPTI' 'ECHO' 0 ;
  'SAUT' 1 'LIGN' ;
  'MESS' ' ****** Erreur max. = ' ('MAXI' ('PROG' err1 err2)) ;
  'MESS' ' ' ;
  'MESS' ('CHAI' '    -'
'----------------------------- TEST REUSSI ----------------------------'
  '-') ;
'FINS' ;
'FIN' ;
'OPTI' 'ECHO' 1 ;



