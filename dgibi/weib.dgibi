* fichier :  weib.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
opti echo 0;
opti epsilon lineaire;
*--------------------------------------------------------------------
*
*  TRAVE CARICATA PER FLESSIONE A QUATTRO PUNTI ; DETERMINAZIONE
*  DELLA PROBABILITA' DI ROTTURA SECONDO LA STATISTICA DI WEIBULL:
*
*--------------------------------------------------------------------
*
************** CARATTERISTICHE GEOMETRICHE DELLA TRAVE ****************
*
TITRE  'TRAVE CARICATA PER FLESSIONE A QUATTRO PUNTI';
OPTION  DIME 2 ELEM QUA8;
L1= 4.5e1   ; comm 'lunghezza trave' ;
D1= 4.0e1   ; comm 'distanza tra gli appoggi' ;
D2= 2.0e1   ; comm 'distanza tra i carichi' ;
H = 3.0     ; comm 'altezza trave' ;
S = 4.0     ; comm 'spessore trave' ;
*
******* CARICO PER UNITA' DI SPESSORE ED AGENTE SU META' TRAVE ********
*
PP1= -1.05E+2 ;
*
********************* CARATTERISTICHE DEL MATERIALE *******************
*
YO1= 3.0e+5  ;
NU1=  .23    ;
*
****** VALORI DEL MODULO DI WEIBULL E DELLA SIGMA-ZERO ****************
****** DETERMINATI A PARTIRE DA UN SET DI PROVE SPERIMENTALI **********
*
* dati sperimentali
 lis1 = prog
 427.9555 852.2001 800.0194 445.7708 740.6716 827.7161 827.3882 590.6464
 750.3228 746.3323 446.1818 875.4909 630.5025 604.9180 648.6437 832.6529
 648.8641 785.7595 754.6536 697.0067 904.4279 807.3849 717.4614 690.0081
 600.4105 742.2141 893.2710 795.1147 697.7165 798.2668 ;
*
* determinazione dei parametri statistici
MESS '========================================';
MESS '==       WEIBULL STATISTICS           ==';
MESS '========================================';
 EW1 SU1 = weip lis1 40. 20. (3. * 4.) ;
MESS '========================================';
MESS ' Weibull modulus : ' EW1 ;
MESS ' Sigma zero      : ' SU1 ;
MESS '========================================';
*
******* VALORI DELLA TENSIONE DI RIFER. E DEL FATTORE D'INTEGRAZIONE **
SMR1= 0.      ; comm 'nulla per materiali ceramici' ;
V0  = 2.0 * S ; comm '1 condizione di simmetria + spessore' ;
***********************************************************************
A0= 0.  0.;
A1= ((L1-D1)/2.) 0. ;
A2= (L1/2.)      0. ;
A3= ((L1-D2)/2.) 0. ;
p3= ((L1-D2)/2.) h ;
V1=  0.          H  ;
P2= (L1/2.)      H  ;
AT1= D 5 A0 A1 D 40 A2;
S1=AT1 TRAN 6 V1 ;
CT2= COTE 2 S1   ;
P3= S1 POINT PROC P3 ;
TASS S1;
*
****************************** FORMULAZIONE ***************************
*
OPTION MODE PLAN CONT ;
OBJ= MODE  S1 MECANIQUE ELASTIQUE QUA8;
*
******************************** MATERIALE ****************************
*
MAT1=MATE  OBJ  YOUN YO1  NU NU1  ;
*
**************************** RIGIDEZZA E VINCOLI **********************
*
ENC1= BLOQUE UY A1 ;
ENC2= SYMT DEPL A2 P2  S1 0.0001;
RIG1= RIGI MAT1 OBJ;
RIGT =RIG1 ET ENC1 ET ENC2;
*
********************************* CARICHI *****************************
*
FP3= FORC FY PP1 P3 ;
*
******************************* RISOLUZIONE ***************************
*
DE1 =RESOU RIGT FP3 ;
*
***************************** CAMPO DI TENSIONI ***********************
*
SE1 = SIGMA DE1 OBJ MAT1 ;
*
*********** STATEMENT DI RICHIAMO DELLA PROCEDURA 'WEIBULL' **********
*
PROT1 CHEL2 = WEIBULL SE1 OBJ V0 SMR1 SU1 EW1;
*
PROS = 1. - PROT1;
MESS '========================================';
MESS 'PROBAB. DI SOPRAVVIVENZA:' PROS  ;
MESS 'PROBAB. DI ROTTURA      :' PROT1 ;
MESS '========================================';
MESS ' ';
*
pr2  = pros * 100. ;
ERR1 = ABS (100 * ( 60.20 - pr2 ) / 60.20 ) ;
*
MESS '========================================';
MESS '==       SURVIVAL PROBABILITY         ==';
MESS '========================================';
MESS ' SOLUTION ANALYTIQUE     60.20           %  ';
MESS ' SOLUTION CALCULEE     'pr2 '  %  ';
MESS ' ERREUR DE             'ERR1 '  %  ';
MESS '========================================';
*
SI (ERR1 < 5 );
  ERRE 0;
SINON;
  ERRE 5;
FINSI;
*
FIN;






