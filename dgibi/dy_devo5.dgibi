* fichier :  dy_devo5.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
*         Test Dy_devo5.dgibi: Jeux de donnï¿½es        *
*         ------------------------------------        *
*                                                     *
GRAPH='N';
SAUT PAGE ;
d_pasa = 'O' ;
*******************************************************
*  REPONSE TRANSITOIRE D'UNE POUTRE -METHODE MODALE
*******************************************************
*
* DESCRIPTION DU PROBLEME
* -----------------------
*
* UNE POUTRE EST MONTEE SUR DES APPUIS ELASTIQUES .
* SUPPOSANT UNE CHARGE REPARTIE TRIANGULAIRE AVCE UNE
* INTENSITE VARIABLE DANS LE TEMPS, ON CHERCHE A
* CALCULER LA FLECHE MAXIMALE EN DIFFERENTS INSTANTS.
*
* REFERENCE :
* ----------
*
* WALTER C. HURY AND MOSHE F. RUBINSTEIN,DYNAMICS OF
* STRUCTURES, (ENGLEWOOD CLIFFS: PRENTICE-HALL,
* INC.,1964),P.294
*******************************************************
OPTION  ECHO 1 ;
OPTION DIME 3 ELEM SEG2 ;
OPTI EPSI LINEAIRE;
*
*  MAILLAGE
*
 P1 = 0. 0. 0. ; P2 = 50. 0. 0. ;
 L1 = D 4 P1 P2  ;
*
* MODELE
*
 MOD1 = MODELE L1 MECANIQUE ELASTIQUE POUT ;
*
*  CARACTERISTIQUES MATERIELLES ET GEOMETRIQUES
*
 MAT1 = MATER MOD1 YOUN 1.E6 NU 0.3  RHO 0.1 ;
 CAR1 = CARAC MOD1 SECT 1 INRY 10. INRZ 10. TORS 1.
  VECT (0. 1. 0.)  ;
*
*   CONDITIONS AUX LIMITES : APPUI ELASTIQUE
*          ET CONDITIONS DE SYMETRIE
*
  K = 974.1 / 2  ;
 CL1 = APPUI UY K  P1  ;
  CL10 = bloq uy p1 ;
 CL2 = BLOQ L1 RX RY UX UZ ;
 CL3 = BLOQ P2 RZ    ;
 CLTOT = CL1  ET CL2 ET CL3 ;
  CLTOT0 = CL10  ET CL2 ET CL3 ;

*  MATRICE DE  RIGIDITE
*
 RIG1 = RIGIDITE MOD1 (MAT1 ET CAR1 ) ;
*
*  MATRICE DE MASSE
*
 MAS1 = MASSE MOD1 (MAT1 ET CAR1 ) ;
*
*  CALCUL DES MODES
*
 TAB1 = VIBR  'INTERVALLE' 0. 5. (RIG1 ET CLTOT) MAS1     ;
TAB1.MODES = NNOR TAB1.MODES AVEC (MOTS UY ) ;
TAB2 = TAB1.MODES ;
NMOD = DIME TAB2  - 2 ;
IB = 0 ;
 REPETE BOUC NMOD ;
  IB = IB + 1   ;
  F1 = (TAB2.IB).'FREQUENCE'  ;
  OMEG1 = (F1 * 2.) * PI ;
  SI (IB 'EGA' 1 ) ; OMEG1TH = 7.155 ;
  SINON ; OMEG1TH = 28.23   ; FINSI ;
 ERR1 = ((OMEG1 - OMEG1TH) / (ABS OMEG1TH)) * 100. ;
MESS
'MODE 'IB 'THEORIE 'OMEG1TH'CASTEM 'OMEG1 '%ERREUR '
 ERR1 ;
  DEP1 = (TAB2.IB).DEFORMEE_MODALE ;
  DEF1 = DEFORM DEP1 L1 1. ROUG ;
  DEF0 = DEFORM DEP1 L1 0.  ;
  SI (NEG GRAPH 'N');
    TRAC (0 0 10000)(DEF0 ET DEF1)  ;
  FINSI;

    ma1 = manu poi1 tab2 . ib . point_repere ;
    mo1 = mode ma1 mecanique elastique modal ;
    ca1 = mate mo1 freq tab2 . ib . frequence
 mass tab2 . ib . masse_generalisee defo tab2 . ib . deformee_modale;
*  ride ((rig1 et cltot) * tab2 . ib . deformee_modale) ;
  si (&bouc ega 1) ;
    ma_moda = tab2. ib . 'POINT_REPERE' ;
    mo_moda = mo1 ; ca_moda = ca1 ;
  sinon ;
    ma_moda = ma_moda et tab2. ib . 'POINT_REPERE' ;
    mo_moda = mo_moda et mo1 ; ca_moda = ca_moda et ca1 ;
  finsi ;
 FIN BOUC  ;
*

*
TABM = TAB1  ;
*
* --  PREPARATION DES DONNEES POUR L'OPERATEUR DYNE
*
*
* CHARGEMENT
*
*
*  FORCE REPARTIE
*
 FC1 = COOR 1 L1 ;
 FC2 = (FC1 / -50.) * 2. ;
 FC3 = NOMC FC2 UY ;
 MODB = MODEL L1 MECANIQUE ELASTIQUE BARR ;
 MATB  = MATER MODB SECT 1. RHO 1. ;
 MASB  = MASSE MODB MATB ;
 FC4 = MASB * FC3  ;
 FC5 = EXCO FC4 FY FY ;
LI1 = PROG 0.  1.  1.025  3.0 ;
LI2 = PROG 0.  1.    0.  0. ;
EV = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR 'FORC' FC5  EV ;
TABCHAR = TABLE 'CHARGEMENT' ;
*
*   PROJECTION SUR LA BASE MODALE
*
CHA2 = PJBA CHA1 TAB1 ;
TABCHAR.'BASE_A' = CHA2  ;
cha3 = pjba cha1 mo_moda ca_moda ;
*
*  TABLE DE SORTIE
*
 TABSOR = TABLE 'SORTIE' ;
 TABVAR = TABLE 'VARIABLE' ;
 TABSOR.'VARIABLE' = TABVAR ;
 TABVAR.'DEPLACEMENT' = VRAI ;
 TABVAR.'VITESSE' = FAUX   ;
 TABVAR.'ACCELERATION' = FAUX ;
*
* APPEL A DYNE DE_VOGELAERE
*
n2n = 20 ;
tabdyn = table 'PASAPAS' ;
tabdyn . modele = mo_moda ;
tabdyn . caracteristiques = ca_moda ;
tabdyn . chargement = cha3 ;
* tabdyn . modele = mod1 ;
* tabdyn . caracteristiques = mat1 et car1 ;
* tabdyn . chargement = cha1 ;
* tabdyn . blocages_mecaniques = cl2 et cl3 ;
* tabdyn . rigidite_constante = cl1 ;

si ('EGA' d_pasa 'O') ;
 tabdyn . temps_calcules = prog 0 pas 0.025 (4*n2n*0.025);
   tabdyn.dynamique = vrai ;
 tabdyn . convergence_forcee = faux ;
 pasapas tabdyn ;
sinon ;
 tabdyn . nombre_pas = 4*n2n ;
 tabdyn . pas_de_temps = 0.025 ;
 tabdyn . pas_de_sortie = 1 ;
 dyne de_vogelaere tabdyn ;
finsi ;

*
TABRES =
DYNE DE_VOGELAERE TABM TABCHAR TABSOR 80 0.025 1 ;
*
 ou1 = prog 0. ; ou11 = prog 0. ;
*d_pasa = 'O';
repeter b_post n2n ;
  DEPU1 = RECO TABRES TABM
         (0. + (&b_post* 0.1)) 'DEPL'  ;
  chdepu1 = peche tabdyn deplacements
          (0. + (&b_post* 0.1)) ;
  si ('EGA' d_pasa 'O')  ; depu11 = reco mo_moda ca_moda chdepu1 ;
    sinon ; depu11 = chdepu1 ; finsi ;
  FLECHu1 = EXTR DEPu1 P2 UY  ;
  FLECHu11 = EXTR DEPU11 P2 UY  ;
   ou1 = ou1 et (prog flechu1) ;
   ou11 = ou11 et (prog flechu11) ;
fin b_post ;
 ab1 = prog 0. pas 0.1  2. ;
 e1 = evol vert manu ab1 ou1  ;
 e2 = evol rouge manu ab1 ou11  ;
  SI (NEG GRAPH 'N');
 dess (e1 et e2 et ev) ;
  FINSI ;
*
*  RECOMPOSITION POUR LES TEMPS 1.45 ET 1.9
*
DEP1 = RECO TABRES TABM 1.45 'DEPL'  ;
DEP2 = RECO TABRES TABM 1.9 'DEPL'  ;
*
chdep1 = peche tabdyn deplacements 1.45 ;
si ('EGA' d_pasa 'O')  ; dep11 = reco mo_moda ca_moda chdep1 ;
           sinon ; dep11 = chdep1 ; finsi ;
chdep2 = peche tabdyn deplacements 1.9 ;
si ('EGA' d_pasa 'O')  ; dep22 = reco mo_moda ca_moda chdep2 ;
          sinon ; dep22 = chdep2 ; finsi ;

*
*  COMPARAISON AVEC DES RESULTATS ANALYTIQUES
*
 FLECH1 = EXTR DEP1 P2 UY  ;  FLECH11 = EXTR DEP11 P2 UY  ;

 FLECH1TH = 0.23382   ;
 ERR1 = (( FLECH1 - FLECH1TH) / (ABS FLECH1TH)) * 100.;
 ERR11 = (( FLECH11 - FLECH1TH) / (ABS FLECH1TH)) * 100.;
MESS '    FLECHE MAXIMALE  T = 1.45       '  ;
MESS'THEORIE   'FLECH1TH'CASTEM  'FLECH1'%ERREUR 'ERR1;
MESS'THEORIE   'FLECH1TH'CASTEM  'FLECH11'%ERREUR 'ERR11;
*
 FLECH2 = EXTR DEP2 P2 UY  ; FLECH22 = EXTR DEP22 P2 UY  ;
FLECH2TH = -0.24283   ;
 ERR2 = (( FLECH2 - FLECH2TH) / (ABS FLECH2TH)) * 100.;
 ERR22 = (( FLECH22 - FLECH2TH) / (ABS FLECH2TH)) * 100.;
MESS '    FELECHE MAXIMALE  T = 1.9      '  ;
MESS'THEORIE   'FLECH2TH'CASTEM  'FLECH2'%ERREUR 'ERR2;
MESS'THEORIE   'FLECH2TH'CASTEM  'FLECH22'%ERREUR 'ERR22;
*
SI ((MAXI ABS (PROG ERR1 ERR2 ERR11 ERR22)) < 5);
ERRE 0;
SINON;
ERRE 5;
FINSI;
FIN ;

















