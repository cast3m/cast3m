* fichier :  gdep1.dgibi
************************************************************************
* Section : Mecanique Flambage
************************************************************************
complet = faux;
optio epsi 'LINEAIRE';
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
GRAPH='N';
SAUT PAGE ;
****************************************************************
*  GRAND ROTATION D' UNE POUTRE
*****************************************************************
*
*  DESCRIPTION DU PROBLEME
*  -----------------------
* IL S'AGIT DE TROUVER LA POSITION POST FLAMBAGE D 'UN POTEAU
* CHARGE AXIALEMENT A UNE EXTREMITE ET ENCASTRE A L'AUTRE
* EXTREMITE .
*
* REFERENCE :
* ----------
* STEPHEN P. TIMOSHENKO AND JAMES M. GERE,THEORY OF ELASTIC STABILITY
* ,(NEW YORK :MCGRAW-HILL BOOK COMPANY,1961),P.48
******************************************************************
OPTION  ECHO 1 ;
OPTIO DIME 3 ELEM SEG2 ;
*
*-----------------------------------------------------------------
* le pilotage est fait sur l'amplitude du deplacement du point P2
* auquel on attribut le signe du deplacement transversal (UY)
'DEBPROC' AUTOPILO DELT*'CHPOINT' DELA*'CHPOINT' ZMOD*'MMODEL'
 ZMAT*'MCHAML' TTT*TABLE;
   Ux = ( EXTR DELT P2 'UX' ) ;
   Uy = ( EXTR DELT P2 'UY' ) ;
   U = (ux*ux + (uy*uy))**0.5 ;
   NORM1 = U * uy / (abs uy) ;
FINPROC   NORM1;
*---------------------------------------------------------------
* MAILLAGE
*
P1 = 0. 0. 0.  ; P2 = 12. 0. 0. ;
L1 = D 12 P1 P2  ;
*
*   MODELE
*
 MOD1 = MODEL L1 MECANIQUE ELASTIQUE  epsilon lineaire POUT;
*
*   CARACTERISTIQUES MATERIELLES ET GEOMETRIQUES
*
 MAT1 = MATER MOD1 YOUN 2.E7 NU 0.0  ;
 CAR1 = CARA MOD1 'SECT' 0.15 'INRY' 2.8125E-4 'INRZ' 2.8125E-4
 'TORS' 0.00001 ;
*
* MATRICE DE RIGIDITE
*
 RIG1 = RIGI MOD1 (MAT1 ET CAR1) ;
*
*  FORCES ET CONDITIONS AUX LIMITES
*
 CL1 = BLOQ P1 'DEPL' 'ROTA' ;
 F1  = FORCE (-1. 0. 0. ) P2 ;
*
*  RESOLUTION
*
 DEP1 = RESO (RIG1 ET CL1) F1 ;
 SIG1 = SIGM MOD1 (MAT1 ET CAR1) DEP1 ;
*
*   MATRICE DE RAIDEUR GEOMETRIQUE
*
 KS1 = KSIGM MOD1 SIG1 (CAR1) 'FLAM' ;
*
* CALCUL DE MODES DE FLAMBAGE
*
 TAB1 = TABLE ;
 TAB1.OBJM = MOD1 ;
 TAB1.LAM1 = 0. ;
 TAB1.LAM2 = 1000. ;
 TAB1.NMOD = 1 ;
 TAB1.CLIM = (CL1  ) ;
 TAB1.RIGI = RIG1 ;
 TAB1.KSIG = KS1 ;
 TAB2 = FLAMBAGE TAB1 ;
 LAMB1 = (TAB2.1).LAMB ;
*
*  CALCUL NON-LINEAIRE  GEOMETRIQUE
*
 F11 = F1 *  LAMB1 ;
 F3 = FORCE   (0. 1. 0.) P2  ;
*
*---- PREPARATION DE LA TABLE POUR NONLIN
*
*  OBJET CHARGEMENT
*
LI1 = PROG 0. 2. ;
LI2 = PROG 0. 2. ;
EV = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR  'MECA' ( F11 ET F3)  EV ;
*
LIS1A = prog 0. 0.65;
TAB3 = TABLE;
TAB3.'GRANDS_DEPLACEMENTS' = VRAI ;
TAB3.'AUTOMATIQUE' = VRAI ;
TAB3.'AUTOCRIT' = 0.05  ;
TAB3.'MAXITERATION'= 100 ;
TAB3.'AUTOPAS' =1000 ;
TAB3.'BLOCAGES_MECANIQUES' = CL1;
TAB3.'MODELE' = MOD1;
TAB3.'CARACTERISTIQUES' = (MAT1 ET CAR1);
TAB3.'CHARGEMENT' = CHA1;
TAB3.'TEMPS_CALCULES' = LIS1A;
TAB3.'K_SIGMA'=FAUX;
TAB3.'REAC_GRANDS'=10.E-2 ;
PASAPAS TAB3 ;
*
TAB3.'AUTOCRIT' = 0.1  ;
TAB3.'AUTOPAS' =1000 ;
LIS1B = prog 0.65 0.9;
TAB3.'TEMPS_CALCULES' = LIS1B;
PASAPAS TAB3 ;
*
si complet;
LIS1C = prog 0.9 1.;
TAB3.'AUTOCRIT' = 0.2  ;
TAB3.'TEMPS_CALCULES' = LIS1C;
PASAPAS TAB3 ;
*
LIS1D = prog 1. 1.152;
TAB3.'AUTOCRIT' = 0.5  ;
TAB3.'TEMPS_CALCULES' = LIS1D;
PASAPAS TAB3 ;
finsi;
**************************************************************
*   COMPARAISON AVEC LES RESULTATS ANALYTIQUES
*
* 1) CHARGEMENT = 1.152 * PCR
 DEP1 = PECHE TAB3 'DEPLACEMENTS'  ;
 PP1 = POINT L1 12   ;
 XX1 = (EXTR DEP1 PP1  UX) +  (COOR 1 PP1) ;
 YY1 = (EXTR DEP1 PP1  UY) +  (COOR 2 PP1) ;
 XX2 = (EXTR DEP1 P2  UX) +  (COOR 1 P2) ;
 YY2 = (EXTR DEP1 P2  UY) +  (COOR 2 P2) ;
 TN = ((YY2 - YY1)/(XX2 - XX1)) ;
 ALPHA = ATG TN ;
si complet;
ALPHATH = 60.  ;
sinon;
ALPHATH= 6.5710;
finsi;
 ER1 =((ABS (ALPHA - ALPHATH )) / (ABS ALPHATH )) * 100. ;
MESS    'ANGLE DE ROTATION  P = 1.152  * PCR     ' ;
MESS 'THEORIE ' ALPHATH 'CASTEM ' ALPHA '%ERREUR ' ER1 ;
*
SI (ER1 < 6);
ERRE 0;
SINON;
ERRE 5;
FINSI;
FIN ;


















