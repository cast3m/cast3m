************************************************************************
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Flexion d'une poutre en grands deplacements avec chargemet suiveur   *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement sur la configuration deformee avec la  *
*   procedure CHARMECA (ou bien PERSO1)                                *
* - la resolution avec PASAPAS en hypothese grands deplacements        *
* - le post traitement du deplacement de la poutre                     *
*                                                                      *
************************************************************************


** Options generales
OPTI 'DIME' 3 'ELEM' 'SEG2' 'ECHO' 0 ;
ITRAC = FAUX ;
OEIL1 = 0. (-1.E6) 0. ;
XPAS = 0.05 ;


** Parametres geometriques
LONG = 1. ;
SEC = 1.E-2 ;
INR1 = 1.E-4 ;
TOR1 = 2.E-4 ;


** Maillage
P1 = 0. 0. 0. ;
P2 = 0. 0. LONG ;
L1 = DROI 25 P1 P2 ;


** Modele et materiau (elastique)
MO1 = MODE L1 'MECANIQUE' 'ELASTIQUE' 'POUT' ;
MA1 = MATE MO1 'YOUN' 50.E6 'NU' 0.3 'SECT' SEC 'INRY' INR1 'INRZ' INR1 'TORS' TOR1 ;


** Blocages mecaniques (encastrement de la base)
BLO1 = BLOQ 'DEPL' 'ROTA' P1 ;


** Chargement en effort impose
F1 = FORC (1. 0. 0.) P2 ;
EV1 = EVOL 'MANU' 'Temps' (PROG 0. 1.) 'Coef' (PROG 0. 2.E4) ;
CHAR1 = CHAR 'MECA' F1 EV1 ;


** Procedure CHARMECA pour appliquer un effort perpendiculaire a la
*  poutre au cours de son deplacement
DEBP CHARMECA T1*'TABLE' TPS1*'FLOTTANT' ;
* donnees utiles
  TAM = T1 . 'AMOI' ;
  PF = TAM . 'POINT' ;
  EV1 = TAM . 'EVOL' ;
* angle de rotation de la section droite
  U1 = T1 . 'ESTIMATION' . 'DEPLACEMENTS' ;
  RYS = EXTR U1 'RY' PF ;
****  une autre possibilite : mettre a jour la force sur la config. fin de pas
***  DRYS = 0. ;
***  SI (EGA (TYPE ZDEPT) 'CHPOINT') ;
***    DRYS = EXTR ZDEPT 'RY' PF ;
***  FINSI ;
***  RYS = (EXTR (T1 . 'CONTINUATION' . 'DEPLACEMENTS') 'RY' PF) + DRYS ;
  RYSD = RYS * 180. / PI ;
* direction de l'effort
  DIR1 = (COS RYSD) 0. (-1. * (SIN RYSD)) ;
* creation du second membre
  VAL1 = IPOL TPS1 EV1 ;
  F1 = FORC (VAL1 * DIR1) PF ;
  T2 = TABLE ;
  T2 . 'ADDI_SECOND' = F1 ;
FINP T2 ;


** Il est aussi possible de modeliser la force suiveuse avec PERSO1
*  (mais les modifications des CL sont pises en compte au pas de temps suivant,
*   les resultats auront donc un pas de temps de retard !)
DEBP PERSO1 T1*'TABLE' ;
* donnees utiles
  TAM = T1 . 'AMOI' ;
  PF = TAM . 'POINT' ;
  EV1 = TAM . 'EVOL' ;
* angle de rotation de la section droite
  U1 = T1 . 'ESTIMATION' . 'DEPLACEMENTS' ;
  RYS = EXTR U1 'RY' PF ;
  RYSD = RYS * 180. / PI ;
* direction de l'effort
  DIR1 = (COS RYSD) 0. (-1. * (SIN RYSD)) ;
* creation du second membre (pour le pas de temps suivant)
  TPS1 = (T1 . 'ESTIMATION' . 'TEMPS') + XPAS ;
  VAL1 = IPOL TPS1 EV1 ;
  F1 = FORC (VAL1 * DIR1) PF ;
  CHAR2 = CHAR 'MECA' F1 (EVOL 'MANU' (PROG 0. 1.) (PROG 1. 1.)) ;
  T1 . 'WTABLE' . 'CHARGEMENT' = CHAR2 ;
FINP ;


** Resolution
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MO1 ;
TAB1 . 'CARACTERISTIQUES'    = MA1 ;
TAB1 . 'BLOCAGES_MECANIQUES' = BLO1 ;
*TAB1 . 'CHARGEMENT'          = CHAR1 ;
TAB1 . 'TEMPS_CALCULES'      = PROG XPAS 'PAS' XPAS 1. ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'PREDICTEUR'          = 'HPP' ;
TAB1 . 'PROCEDURE_CHARMECA'  = VRAI ;
***TAB1 . 'PROCEDURE_PERSO1'    = VRAI ;
TAB1 . 'AMOI'                = TABL ;
TAB1 . 'AMOI' . 'EVOL'       = EV1 ;
TAB1 . 'AMOI' . 'POINT'      = P2 ;
PASAPAS TAB1 ;


** Post-traitement
TT = TAB1 . 'TEMPS' ;
TU1 = TAB1 . 'DEPLACEMENTS' ;
TR1 = TAB1 . 'REACTIONS' ;
DEF0 = DEFO L1 (TU1 . 0) 0. ;
CONF0 = FORM ;
* trace des deformees successives
REPE B1 (DIME TT) ;
  I1 = &B1 - 1 ;
  TPS1 = TT . I1 ;
  U1 = TU1 . I1 ;
  R1 = TR1 . I1 ;
  V1 = VECT R1 5.E-5 'FORC' 'ROUG' ;
  DEF1 = DEFO L1 U1 1. V1 'BLEU' ;
  SI ITRAC ;
    TRAC OEIL1 (DEF0 ET DEF1) 'TITR' (CHAI 'Deformee au temps ' TPS1) ;
  FINSI ;
FIN B1 ;
* evolution des deplacements du sommet au cours du temps
TLEG = TABL ;
TLEG . 'TITRE' = TABL ;
TLEG . 'TITRE' . 1 = 'Ux' ;
TLEG . 'TITRE' . 2 = 'Uz' ;
EVUX1 = EVOL 'ROUG' 'TEMP' TAB1 'DEPLACEMENTS' 'UX' P2 ;
EVUZ1 = EVOL 'BLEU' 'TEMP' TAB1 'DEPLACEMENTS' 'UZ' P2 ;
SI ITRAC ;
  DESS (EVUX1 ET EVUZ1) 'TITR' 'Deplacements vs Temps' 'LEGE' TLEG ;
FINSI ;

FIN ;

 

 

 



