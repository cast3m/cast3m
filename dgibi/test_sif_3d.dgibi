* fichier :  test_sif_3d.dgibi
************************************************************************
* Section : Mecanique Rupture
************************************************************************


************************************************************************
* Verification & Validation de la procedure SIF
************************************************************************
* Geometrie : Piece et fissure
* -----------
* 3D, milieu infini (ici cylindre de rayon l1 et hauteur 2*l1) avec
* fissure en forme de disque de rayon l2 (penny-shaped crack)
* soumise a une contrainte sig, dans la direction orthogonale a la fissure
*
* Validation : Test sur le calcul du KI
* -----------
* Valeur theorique KI = 2 * sig * (l2 / pi)**0.5
*
************************************************************************

* Options generales
OPTI 'DIME' 3 'ELEM' 'CU20' ;

* Parametres
l1  = 10.   ;
l2  = 1.    ;
sig = 42.E6 ;

* Maillage
* --densites (loin et pres de la fissure)
den1 = l1 / 4. ;
den2 = l2 / 10. ;
* --fissure levre superieure
OPTI 'DENS' den2 ;
p1  = 0. 0. 0. ;
p2  = l2 0. 0. ;
l12 = DROI p1 p2 ;
s1  = l12 ROTA 90 p1 (p1 PLUS (0. 0. 1.)) ;
lff = s1 COTE 2 ;
ELIM s1 1.E-10 ;
s1s = REGE s1 ;
s1i = s1s PLUS (0. 0. 0.) ;
ELIM s1i lff 1.E-10 ;
* --section non fisuree
OPTI 'DENS' den1 ;
p3  = l1 0. 0. ;
p4  = 0. l1 0. ;
l34 = CERC (NBEL lff) p3 p1 p4 ;
s2  = REGL lff l34 ;
* --volumes superieurs et inferieurs
v1   = VOLU 'TRAN' (s1s ET s2) 'DINI' den2 'DFIN' den2 (0. 0. (0.05 * l1 ));
v1   = VOLU 'TRAN' v1          'DINI' den2 'DFIN' den1 (0. 0. (0.95 * l1 ));
v2   = VOLU 'TRAN' (s1i ET s2) 'DINI' den2 'DFIN' den2 (0. 0. (-0.05 * l1));
v2   = VOLU 'TRAN' v2          'DINI' den2 'DFIN' den1 (0. 0. (-0.95 * l1));
vtot = v1 ET v2 ;

* Modele et materiau
mo = MODE vtot 'MECANIQUE' ;
ma = MATE mo   'YOUN' 200.E9 'NU' 0.3 ;

* Blocages
x y z = COOR vtot ;
px0   = x POIN 'MINI' ;
py0   = y POIN 'MINI' ;
bl1   = BLOQ 'UX' px0 ;
bl2   = BLOQ 'UY' py0 ;
bl3   = BLOQ 'UZ' l34 ;

* Chargement
shaut = v1 FACE 2 ;
sbas  = v2 FACE 2 ;
f1    = PRES 'MASS' mo shaut (-1. * sig) ;
f2    = PRES 'MASS' mo sbas  (-1. * sig) ;
f     = f1 ET f2 ;

* Resolution
r0  = RIGI mo ma ;
rig = r0 ET bl1 ET bl2 ET bl3 ;
u   = RESO rig f ;

* Solution de reference
k1ref = 2. * sig * ((l2 / pi) ** 0.5) ;

* Solution par SIF
t1 = TABL ;
t1 . 'MAILLAGE' = vtot ;
t1 . 'FRTFISS'  = lff  ;
t1 . 'LEVRE_1'  = s1s  ;
t1 . 'MODMIXTE' = VRAI ;
t1 . 'LEVRE_2'  = s1i  ;
SIF ma u t1 ;
k1sif = t1 . 'K1' . p2 ;
errsif = 100. * (k1sif - k1ref) / k1ref ;

* Affichage
OPTI 'ECHO' 0 ;
SAUT 3 'LIGNE' ;
MESS 'BILAN, CALCUL DE KI' ;
MESS ;
MESS 'Solution de reference ' k1ref  ;
MESS 'Calcul avec SIF       ' k1sif  ;
MESS 'Ecart relatif (%)     ' errsif ;

* Test d'erreur
SI ((ABS errsif) > 1.D0) ;
  ERRE 'ERREUR DANS LE CALCUL DU KI'  ;
SINON ;
  MESS 'CAS TEST PASSE AVEC SUCCES !' ;
FINSI ;
OPTI 'ECHO' 1 ;

FIN ;


