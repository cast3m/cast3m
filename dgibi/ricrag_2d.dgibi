*******************************************************
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*  Cas test de l'implantation numérique du modele
*  RICRAG 2D LOCAL/NON LOCAL
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
* Benjamin.Richard@lmt.ens-cachan.fr
*******************************************************
* Les cas de charges sont entrés :
* - 1 : Traction monotone
* - 2 : Compression monotone
* - 3 : Traction cyclique
* - 4 : Compression cyclique
* - 5 : Traction/compression cyclique
*******************************************************
* Choix du cas de charge
ncas  = 1;

* Test du fichier compatible avec le non local
* nloc0 = 0;   Cas local
* nloc0 = 1;   Cas non local
graph= mot 'N';
nloc0 = 0;
*******************************************************

*
*-------------- Options de calcul ---------------------
*
OPTION DIME 2 ELEM QUA4 MODE PLAN CONT;

*
*-------------- Definition de la geometrie ------------
*
P1 = 0. 0.;
P2 = 1. 0.;
P3 = 1. 1.;
P4 = 0. 1.;

L1 = P1 DROIT 1 P2 ;
L2 = P2 DROIT 1 P3 ;
L3 = P3 DROIT 1 P4 ;
L4 = P4 DROIT 1 P1 ;

LTOT = L1 ET L2 ET L3 ET L4 ;

S1   = SURF LTOT PLANE;

*
*------- Définition des conditions aux limites --------
*----------- et des déplacements imposés --------------
*
CL  = BLOQ P1 UX UY;
CLL = BLOQ P2 UY;
CL1 = BLOQ L3 UY;

D1  = DEPI CL1 1;

*
*----- Définition du modèle ---------------------------
*
SI ( EGA nloc0 1 ) ;
MOD1   = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
         ENDOMMAGEMENT RICRAG
      'NON_LOCAL' 'MOYE' 'V_MOYENNE' (MOTS 'EPTI') ;
SINON ;
MOD1   = MODE S1 MECANIQUE ELASTIQUE ISOTROPE
         ENDOMMAGEMENT RICRAG ;
FINSI ;


*
*----- Paramètres matériaux ---------------------------
*

* Module d'Young
youngn = 36000E+6;

* Coefficient de Poisson
nun    = 0.2;

* Résistance en traction
ftn    = 3.6e6;

* Fragilité en traction
aldin  = 1.0e-2;

* Fragilité en compression
alinn  = 5.0e-4;

* Module d'écrouissage 1
gam1n  = 7.0e9;

* Moduke d'écrouissage 2
a1n    = 7.0e-7;

mat1   =MATE mod1 YOUN youngn  NU   nun
                  FT   ftn   ALIN alinn
                  GAM1 gam1n   A1   a1n
                  ALDI aldin;

*
*----- Définition des cas de charge -------------------
*

SI (EGA ncas 1) ;
LI1  = PROG 0. 1.;
LI2  = PROG 0. 4.0e-4;
LIS1 = PROG 0. PAS 0.02 1.;
FINSI;

SI (EGA ncas 2) ;
LI1  = PROG 0.  1.;
LI2  = PROG 0. -8.0e-3;
LIS1 = PROG 0. PAS 0.02 1.;
FINSI;

SI (EGA ncas 3) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 1.5e-4 9.5e-6 2.0E-4 3.0E-5 2.5E-4;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 4) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. -3.0e-3 -1.5e-4 -5.0E-3 -3.5E-4 -8.0E-3;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 5) ;
LI1  = PROG 0. 1. 2. 3. 4. 5. 6.;
LI2  = PROG 0. 1.3e-4 -3.0e-3 -1.5E-4 -5.0E-3 -3.5E-4 -8.0E-3;
LIS1 = PROG 0. PAS 0.02 6;
FINSI;

EV = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;

*
*----------- Calcul par l'operateur PASAPAS ------------
*
SI (EGA nloc0 1);
LC = 0.000001;
CO1 = CONNEC mod1 LC NORMAL;
MAT1 = MAT1 ET ( MATE MOD1 'LCAR' LC) ;
FINSI;

TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CL ET CLL ET CL1;
TAB1.'MODELE' = MOD1;
TAB1.'MOVA' = 'D   ';
TAB1.'CHARGEMENT' = CHA1;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'TEMPS_CALCULES' = LIS1;

SI (EGA nloc0 1);
TAB1.CONN = CO1;
FINSI;

PASAPAS TAB1 ;

*
*----------- Courbe effort-deplacement -----------------
*
ev2=@global tab1 CL1 EV fy;
si ( ega graph 'O');

DESS EV2;
finsi;
  ee = extr ev2 ordo 1;
  aa = extr ee ( dime ee);
  list aa;
  err =  abs (aa - 2.37395E+06) /2.37395E+06 ;
  message ' erreur relative ' err;
  si (err > 1.e-3);
    erreur (5);
     finsi;
fin;














