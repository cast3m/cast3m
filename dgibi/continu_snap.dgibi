
************************************************************************
*
*  continu_snap.dgibi
*  = cas test basé sur snap.dgibi (de la base cast3m),
*    mais avec une comparaison avec la procedure de CONTINUation
*
*  Mots-clé :
*  flambage, grand deplacements, snap-through
*
*  Objectif :
*  Il s'agit de trouver la position post flambage d'une barre articulée
*  Par symétrie, une seule barre est maillee et calculee
*
*
*         ||
*         ||
*         \/ F
*         /\
*        /  \
*     | /    \ |
*     |/      \|
*     |        |
*
************************************************************************
*
*
***************************************************
***                                             ***
***                    OPTIONS                  ***
***                                             ***
***************************************************

OPTI 'TRAC' 'PSC' 'EPTR' 5 'POTR' 'HELVETICA_16';
OPTI DIME 2 ELEM SEG2 MODE PLAN CONT ; DENS 1. ;
*
SAUT PAGE ;
GRAPH = FAUX;
* GRAPH = VRAI ;

***************************************************
***                                             ***
***             DONNEES DU PROBLEME             ***
***                                             ***
***************************************************

*------------- geometrie   ligne ST formee d' 1  SEG2 ------------------
*              PAR SYMETRIE, ON N'ETUDIE QUE LA MOITIE
*
Lx = 10.; Uini = 1.;
P1 = 0.    Uini   ; P2 = Lx 0.  ;
ST = P1 D 1  P2 ;
*
*------------ calcul mecanique .  ------------------
*
 MO = MODE ST  MECANIQUE  BARR     ;
*
*   MATERIAU ET CARACTERISTIQUES
*
Ey1 = 2.1E11; Sec1 = 0.05;
MA1 = MATE MO YOUN Ey1  NU 0.3 ;
CAR1 = CARA MO SECT Sec1 ;
MACA= MA1 ET CAR1;
*
*----------- calcul de la rigidite -------------------------------------
*
RI1 =    RIGI MACA  MO ;
*
*----------- definition des conditions aux limites ---------------------
*
CL1 = BLOQ UX P1 ;
CL2 = BLOQ UX P2 ;
CL3 = BLOQ UY P2 ;
CL4 = BLOQ UY p1;
CL = CL1 ET CL2 ET CL3;
*
*----------- definition du chargement ----------------------------------
*
xF11 = -12.5e5;
FP11 = FORCE (  0 xF11 ) p1;
* LIX1 = PROG 0. 40. ; LIY1 = PROG 0. 40. ;
*bp : pour la continuation on aura peut etre a extrapoler un peu...
LIX1 = PROG -10. 0. 40. ; LIY1 = PROG -10. 0. 40. ;
EV1 = EVOL MANU 'T' LIX1 'F(T)' LIY1 ;
CHA1 = CHAR MECA FP11   EV1 ;



***************************************************
***                                             ***
***       CALCUL NON-LINEAIRE GEOMETRIQUE       ***
***                                             ***
***************************************************

*----------- resolution par la procedure CONTINU -----------------------

TRES1 = tabl;
TRES1 . 1 = tabl;
TRES1 . 1 . 'POINT_MESURE' = P1;
TRES1 . 1 . 'COMPOSANTE'   = mot 'UY';
TRES1 . 1 . 'COULEUR'      = mot 'ROSE';
TRES1 . 1 . 'TITRE'        = mot 'UY continuation';
TAB1 = tabl ;
TAB1 . 'MODELE' = MO;
TAB1 . 'CARACTERISTIQUES' = MACA;
TAB1 . 'CHARGEMENT' = CHA1;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'BLOCAGES_MECANIQUES' = CL;
TAB1 . 'PRECISION' = 1.E-6;
LIS1T = prog 0. PAS 0.1 2.;
TAB1 . 'TEMPS_CALCULES' = LIS1T;
TAB1 . 'MAXIPAS' = 500;
TAB1 . 'ACCELERATION' = 4;
* calcul d'une norme de deplacement
y1 = coor ST 2;
u1max = (XTX y1)**0.5;
u1max = 0.05 * u1max;
TAB1 . 'MAXI_DEPLACEMENT' = u1max;
TAB1 . 'RESULTATS' = TRES1;

opti debug 1 ;
CONTINU TAB1;

evtot = TAB1 . 'RESULTATS' . 'RESULTATS_EVOL';
toto = tabl ; toto . 1 = mot 'MARQ S PLUS';
si GRAPH;
  dess evtot toto;
finsi;

uyp = -1.*(extr evtot 'ORDO'); nyp = dime uyp;
fyp = -1.*xF11 * (extr evtot 'ABSC');
evcon = evol 'ROUG' manu 'UY' uyp 'FY' fyp;


* tracé

Tdess1 = tabl ;
Tdess1 . 'TITRE' = tabl ;
Tdess1 . 'TITRE' . 1 = mot 'continu' ;
Tdess1 . 'TITRE' . 2 = mot 'pasapas' ;
Tdess1 . 'TITRE' . 3 = mot '[Crisfield]' ;
Tdess1 . 1 = mot 'MARQ CROI REGU' ;
Tdess1 . 2 = mot 'TIRR MARQ ROND REGU' ;
Tdess1 . 3 = mot 'TIRC' ;

si GRAPH;
DESS evcon
'TITX' 'UY' 'POSX' 'CENT'
'TITY' 'FY' 'POSY' 'CENT' 'TITR' 'continu_snap (continu)';
finsi;


*----------- resolution par la procedure PASAPAS -----------------------
*            CALCUL EN GRANDS DEPLACEMENTS
*
TAB2 = TABLE ;
TAB2.'GRANDS_DEPLACEMENTS'=VRAI;
TAB2.'AUTOMATIQUE' = VRAI;
*
* ---------- pilotage suivant le deplacement du point P1
*
'DEBPROC' AUTOPILO DELT*'CHPOINT' DELA*'CHPOINT' ZMOD*'MMODEL'
          ZMAT*'MCHAML' TTT*TABLE ;
      NORM1 =     (extraire  delt p1 'UY' ) *-1 ;
*      mess ' norm1 '  norm1;
FINPROC   NORM1;
*--------------------------------------------------------
*
temp zero;
TAB2.'AUTOPAS' = 200;
TAB2.'AUTOCRIT' = 0.075;
TAB2.'AUTORESU' = 1;
TAB2.'BLOCAGES_MECANIQUES' = CL;
TAB2.'MODELE' = MO;
TAB2.'CARACTERISTIQUES' = MACA;
TAB2.'CHARGEMENT' = CHA1;
LIS11 =   PROG 0. 2. ;
TAB2.'TEMPS_CALCULES' = LIS11;
tab2.'REAC_GRANDS'=200.;
PASAPAS TAB2 ;
temp ;


*----------- resultats -------------------------------------------------
*        courbe de snap through : montee descente montee
PGX = PROG 0.;
PGY = PROG 0.;
NDIM = (DIME ( TAB2 . 'DEPLACEMENTS' )) - 1 ;

REPETER TBOU2 NDIM ;

  LEDEP = TAB2 . 'DEPLACEMENTS' .  (&TBOU2);
  REA1 = REAC CL3 LEDEP ;
  V    = EXTR LEDEP UY P1 ;
  PGX  = PGX ET ( -1. *  V ) ;
  VV   = EXTR REA1  FY P2 ;
  PGY  = PGY ET ( PROG VV ) ;

FIN TBOU2 ;
*
evopas = EVOL BLEU MANU  'UY' PGX 'FY' PGY ;
si GRAPH;
DESS evopas toto;
DESS evopas
'TITX' 'UY' 'POSX' 'CENT'
'TITY' 'FY' 'POSY' 'CENT' 'TITR' 'continu_snap (pasapas)';
finsi;

* tracé comparatif
si GRAPH;
DESS (evcon et evopas) Tdess1 LEGE SO
'TITX' 'UY' 'POSX' 'CENT'
'TITY' 'FY' 'POSY' 'CENT' 'TITR' 'continu_snap';
finsi;

*
* opti donn 5 trac x;
***************************************************
***                                             ***
***            TEST DE NON REGRESSION           ***
***                                             ***
***************************************************

*** RESULTATS ANALYTIQUES  ***

* ref : [Crisfield, 1996 NL FEA solids and structures, p5]

* Uanal = prog 0. PAS 0.05 2.2;
* on compare avec continu
Uanal1 = uyp;
Uadim1 = (-1.*Uanal1) / Uini;
Fanal1 = (2.*Uadim1) + (3.*(Uadim1**2)) + (Uadim1**3);
Fanal1 = (-0.5 * Ey1 * Sec1 * (Uini**3) / (Lx**3)) * Fanal1;
evanal1= evol MANU 'U' Uanal1 'F' Fanal1;

* on compare avec pasapas
Uanal2 = PGX;
Uadim2 = (-1.*Uanal2) / Uini;
Fanal2 = (2.*Uadim2) + (3.*(Uadim2**2)) + (Uadim2**3);
Fanal2 = (-0.5 * Ey1 * Sec1 * (Uini**3) / (Lx**3)) * Fanal2;
evanal2= evol MANU 'U' Uanal2 'F' Fanal2;

*tracé
si GRAPH;
DESS (evcon et evopas et evanal1) Tdess1 LEGE SO
'TITX' 'UY' 'POSX' 'CENT'
'TITY' 'FY' 'POSY' 'CENT' 'TITR' 'continu_snap';
finsi;

*** calcul des max et des min ***
* (max sur la 1ere moitié du calcul)
lmoit1 = lect 1 pas 1 (nyp/2);
fmax1 = maxi (extr fyp lmoit1);  fmin1 = mini fyp;
gmax1 = maxi (extr Fanal1 lmoit1);  gmin1 = mini Fanal1;

lmoit2 = lect 1 pas 1 (NDIM/2);
fmax2 = maxi (extr PGY lmoit2);  fmin2 = mini PGY;
gmax2 = maxi (extr Fanal2 lmoit2);  gmin2 = mini Fanal2;


*** table TNR ***

TABTNR = TABL ;

TABTNR.'ANA' = TABL ;
TABTNR.'ANA'. 1 = CHAI 'CONTINU' ;
TABTNR.'ANA'. 2 = CHAI 'CONTINU' ;
TABTNR.'ANA'. 3 = CHAI 'PASAPAS' ;
TABTNR.'ANA'. 4 = CHAI 'PASAPAS' ;

TABTNR.'DESC' = TABL ;
TABTNR.'DESC'.  1 = CHAI 'max(F)' ;
TABTNR.'DESC'.  2 = CHAI 'min(F)' ;
TABTNR.'DESC'.  3 = CHAI 'max(F)' ;
TABTNR.'DESC'.  4 = CHAI 'min(F)' ;

TABTNR.'NOMS' = TABL ;
TABTNR.'NOMS' . 1  = 'CHAI' 'fmax1  ' ;
TABTNR.'NOMS' . 2  = 'CHAI' 'fmin1  ' ;
TABTNR.'NOMS' . 3  = 'CHAI' 'fmax2  ' ;
TABTNR.'NOMS' . 4  = 'CHAI' 'fmin2  ' ;

TABTNR.'CAL' = PROG fmax1 fmin1 fmax2 fmin2;

TABTNR.'REF' = PROG gmax1 gmin1 gmax2 gmin2;

TABTNR.'TOL' = PROG 4*1.E-2;

OPTI ECHO 0 ;
error = 0 ;
FORMA1 = '(1PE12.5)' ;
FORMA2 = '(1PE10.0)' ;
MESS '###_DEBUT_DES_TESTS_DE_NON-REGRESSION_###' ;
MESS (CHAI 'ANALYSE : VARIABLE : DESCRIPTION : VALEUR REF.  :'
           ' VALEUR CALC. : ERR. RELAT.  :  TOLERANCE   :  ETAT') ;
REPE BOU1 (DIME TABTNR.'NOMS') ;
  ana = CHAI TABTNR.'ANA'. &BOU1 ;
  nom = CHAI TABTNR. 'NOMS' . &BOU1 ;
  des =  CHAI TABTNR. 'DESC' . &BOU1 ;
  ref = EXTR TABTNR.'REF' &BOU1 ;
  cal = EXTR TABTNR.'CAL' &BOU1 ;
  tol = EXTR TABTNR.'TOL' &BOU1 ;
  err = ABS (cal - ref) ;
  SI ((ABS ref) '>EG' 1.E-10) ;
    err = err / (ABS (ref)) ;
  FINSI ;
  SI (err '>EG' tol) ;
    error = error + 1 ;
    etat  = CHAI ERREUR ;
  SINON ;
    etat  = CHAI 'OK' ;
  FINSI ;
  MESS (CHAI               ana ' : '
                           nom ' : '
                           des ' : '
             FORMAT FORMA1 ref ' : '
             FORMAT FORMA1 cal ' : '
             FORMAT FORMA1 err ' : '
             FORMAT FORMA2 tol ' : '
                           etat) ;
FIN BOU1 ;
*
******************
* Emission de la banniere de fin de TEST
******************
*
* opti DONN 5 ECHO 1 'TRAC' X;
MESS '###_FIN_DES_TESTS_DE_NON-REGRESSION_###' ;
TEMPS ;
SI (error '>' 0) ;
  MESS 'Il y a ' error ' erreur(s) !' ;
  ERRE 5 ;
SINON ;
  MESS 'Aucun probleme detecte !' ;
FINSI ;
*

FIN ;








