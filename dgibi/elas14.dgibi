* fichier :  elas14.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS LES GRAPHIQUES SONT AFFICHES
*

opti trac bens;


*-------------------------------------------------------------------*
*  TEST ELAS14                                                      *
*                                                                   *
*     CONSOLE SOUS CHARGE  CONCENTREE                               *
*    -----------------------------------                            *
*                                                                   *
*                                                                   *
*                                                                   *
* IL S'AGIT D'UNE CONSOLE SOUS CHARGE CONCENTREE A L'EXTREMITE      *
* libre .On etudie la variation de la fleche sous la charge en      *
* fonction de l'elancement L/H (L etant la longueur de la console   *
* et H son epaisseur ).On compare les resultats  donnees par        *
* l'element coque DST a ceux de l'element coque DKT  en tracant la  *
* variation des fleches sous la charge donnees par les deux elements*
*(normalisees par la fleche theorique) en fonction de l'elancement .*
* On constate que les resultats sont semblables que pour des        *
* elancements eleves .Dans la branche ou l'element DKT n'est plus   *
* satisfaisant l'element DST est en parfait accord avec la reponse  *
* theorique .                                                       *
* Reference :I.CHAHROUR ,these de doctorat soutenue a INSA LYON     *
* septembre 1991                                                    *
*                                                                   *
*-------------------------------------------------------------------*
graph = 'N';
SAUT PAGE ;
TITRE 'FLECHE SOUS LA CHARGE EN FONCTION DE L ELANCEMENT ';
*________________________________________________________________________
*  GEOMETRIE
*________________________________________________________________________
OPTI DIME 3 ELEM TRI3 ECHO 1  DENS 0.5 ;
B=  1. ;  L=10. ;
F = 500.*B ;
NB =4/2;  NB2=1;
A1 = (L*0.75) 0. 0. ;A2=(L*0.75) B  0.0;
B1 =  L   0. 0. ; B2 = L  B 0.0 ;
C1 =  (0.875*L)  (B/2) 0.0 ;
TR1 = MANU TRI3 A1 B1  C1 ;
TR2 = MANU TRI3 B1 B2  C1 ;
TR3 = MANU TRI3 B2 A2  C1 ;
TR4 = MANU TRI3 A2 A1  C1 ;
S1=TR1 ET TR2 ET TR3 ET TR4 ;
DEPL=L/4 ;
S2=MOINS S1 (DEPL 0. 0. );
S3= S2 MOINS (DEPL 0. 0. );
S4= S3 MOINS (DEPL 0. 0. );
ST = S1 ET S2 ET S3 ET S4 ;
ELIM ST 0.1 ;
E=0.0 0.0 0.0 ;D=0.0 1. 0.0  ;
DD1=POIN ST PROC  D;
EE1=POINT ST PROC E  ;
LB=DROIT  1 DD1 EE1 ;
OEIL = 0 0 1000 ;
SI (NEG GRAPH 'N') ;
  TRACE OEIL ST  ;
FINSI ;
*________________________________________________________________________
*  INITIALISATION
*________________________________________________________________________
ITER=0 ;
XABS=PROG ;
FONC1=PROG;
FONC2=PROG;
FONC3=PROG;
LSHM=100. ;LLSH=0. ;
NB=10;DLLSH=(LOG (LSHM))/NB ;
RP=0.0 ;
*________________________________________________________________________
*  BOUCLE SUR LE NOMBRE DE CALCUL
*________________________________________________________________________
REPETE  BLOC1 NB  ;
LLSH =ITER*DLLSH ;LSH=EXP LLSH ;EP=L/LSH;
ITER=ITER+1;
*________________________________________________________________________
*  MATERIAUX
*________________________________________________________________________
E=1.2E6 ;
*
MOD1=MODE ST MECANIQUE ELASTIQUE ISOTROPE DST  ;
MAT1=MATE MOD1 YOUN E NU 0. EPAI EP  RHO 1. ;
MOD2=MODE ST MECANIQUE ELASTIQUE ISOTROPE DKT  ;
MAT2=MATE MOD2 YOUN E NU 0. EPAI EP   ;
*________________________________________________________________________
*  CONDITIONS AUX LIMITES ET RIGIDITE
*________________________________________________________________________
RI1=RIGI MOD1 MAT1 ;
RI2=RIGI MOD2 MAT2 ;
CL1=BLOQ UX UY UZ  RY RX RZ LB ;
RIT1 = RI1 ET  CL1 ;
RIT2 = RI2 ET  CL1 ;
*________________________________________________________________________
*  FORCES IMPOSEES
*________________________________________________________________________
FO1 = FORC FZ  F  B1  ;
FO2 = FORC FZ  F  B2  ;
*________________________________________________________________________
*  CALCUL ET SORTIE
*________________________________________________________________________
*
DEP1 = RESO RIT1 (FO1 ET FO2) ;
DEP2 = RESO RIT2 (FO1 ET FO2) ;
FLEC1 = EXTR DEP1 UZ B1;
FLEC2 = EXTR DEP2 UZ B1;
*__________________________________________________________
*   CALCUL DE LA FLECHE THEORIQUE
*__________________________________________________________
H=EP ;
I=B*(H**3); I=I/12  ;FLET1=2*F*(L**3) ;EI=3*E*I;FLET1=FLET1/EI;
TAU=(2.*F)/(B*H);G=E/2.;GAMA=TAU/G ;FLET2=GAMA*L*1.2 ;
FLET=FLET1+FLET2 ;
*__________________________________________________________
*   FORMATION DES LISTES REELS CONTENANT LES VALEURS DE L/H
*   ET LES VALEURS DE W/Wth
*__________________________________________________________
*
XABS=INSER XABS  ITER LLSH ;
FONC1=INSER FONC1 ITER (FLEC1/FLET);
FONC2=INSER FONC2 ITER (FLEC2/FLET) ;
RP=ABS(1.-(FLEC1/FLET)) +RP;
FIN BLOC1 ;
TEMPS ;
*__________________________________________________________
*   FIN DU BOUCLE ,TRACE DES COURBES W/WTH=F(Ln(L/H))
*__________________________________________________________
Si ( NEG GRAPH 'N');
 EV1=EVOL ROUGE MANU 'Ln L/H' XABS 'W/Wth' FONC1 ;
 EV2=EVOL BLEU MANU 'Ln L/H' XABS 'W/Wth' FONC2 ;
 SAUT PAGE ;
 DESSIN (EV1 ET EV2);
finsi;

RPP=RP/NB ;

*__________________________________________________________
*            CODE FONCTIONNEMENT
*__________________________________________________________
*
SI (RPP <EG 2E-2);
    ERRE  0 ;
SINO;
    ERRE  5 ;
FINSI;
FIN ;







