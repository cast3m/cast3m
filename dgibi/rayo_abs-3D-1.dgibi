* fichier :  rayo_abs-3D-1.dgibi
************************************************************************
* Section : Thermique Diffusion
* Section : Thermique Rayonnement
* Section : Thermique Convection
************************************************************************

complet = faux;
*
*************************************************
*  pour calcul complet mettre complet à : vrai;
*************************************************
***********************************************************************
*                                                                     *
*  Rayonnement thermique en milieu absorbant dans une cavité sphérique*
*  (pas de couplage avec d'autres modes de transfert d'énergie)       *
*                                                                     *
*  Comparaison à un calcul analytique                                 *
*  Ref: Siegel&Howell Ed.3 p609-615                                   *
*                                                                     *
*  On evalue la puissance perdue par un gaz absorbant de température  *
*  uniforme 2273K et de coefficient d'absorption 100./m contenu dans  *
*  cavité de rayon 0.01m à la température uniforme de 1273K.          *
*                                                                     *
*  Calcul 3D                                                          *
*  Remarques: le calcul des facteurs de forme n'utilise pas l'option  *
*             convexe 'CONV'                                          *
*             avec la densité choisie, la sphère comprend 88 éléments *
*             pour avoir un meilleur calcul mettre d = 0.003          *
***********************************************************************
*
 option echo 0 dime 3 elem tri3 ;
*option dime 3 elem tri6 ;
 graph = faux ;
*
*       P3
*       |
*       O   P2    sphère de centre O
*       |
*       P1
*
*-------------------------------
*   Maillage
*-------------------------------
*
R1 = 1.E-2 ;
O  =  0. 0. 0. ;
O1 =  0.  1.E-6  0. ;
O2 =  0. -1.E-6  0. ;
p1 =  0.0  0. ( -1. * R1 )  ;
p2 =  R1  0.0 0. ;
p3 = 0. 0. R1 ;

* densite uniforme
si complet;
d = 0.002 ;
sinon;
d = 0.008;
finsi;

p1p2 = cerc p1 O p2 DINI d DFIN d;
p2p3 = cerc p2 O p3 DINI d DFIN d;
lign1    = p1p2 et p2p3 ;
lign2 = inve(SYME lign1 'DROIT' p1 p3) ;
lign_int=lign1 et lign2;
elim 0.000001 lign_int ;
oeil = 10. 10. 10. ;

sphe1 = surf lign_int 'SPHERIQUE' O2;
sphe2 = surf lign_int 'SPHERIQUE' O1;
sphe_ext = sphe1 et (inve sphe2) ;
elim 0.000001 sphe_ext ;
si graph ;
trac oeil sphe_ext cache ;
finsi ;

cavite =  sphe_ext ;
tout = cavite;

*-------------------------------
*  Propriétés physiques
*-------------------------------

e_wall = 0.5 ;
abso0 = -100. ;
T_wall = 1273.  ;
T_gas = 2273. ;

*-------------------------------
*  Modèle de rayonnement
*-------------------------------

mrt = mode sphe_ext thermique rayonnement 'CAVITE' ;
e   = mate mrt 'EMIS' e_wall CABS abso0 'TABS' T_gas;

* opti 'IMPI' 1 ;

*-------------------------------
*  Facteurs de forme et matrice de rayonnement
*-------------------------------

 fft = ffor mrt e;
 chamr = raye mrt  fft    e ;

*-------------------------------
* gaz absorbant : calcul du terme R*Tg4
*-------------------------------

tg = manu chpo tout 1 'T'  T_gas natu 'DIFFUS';
tg_cavi   = redu tg cavite ;
tge_cavi   =  chan 'CHAM' tg_cavi   mrt 'GRAVITE' ;
crg= rayn mrt  chamr tge_cavi    ;
fg =  crg*tg_cavi ;

*-------------------------------
* paroi : calcul du terme R*Tw4
*-------------------------------

tp = manu chpo tout 1 'T' T_wall natu 'DIFFUS';
t_cavi   = redu tp cavite ;
te_cavi   =  chan 'CHAM' t_cavi   mrt 'GRAVITE' ;
cr = rayn mrt  chamr te_cavi    ;
fw =  cr *t_cavi ;

*-------------------------------
* puissance perdue par les frontieres:
*-------------------------------

puis_n =  fw - fg ;
t1 = manu chpo tout 1 'T' 1.0    natu 'DIFFUS' ;
lm1 = MOTS 'Q' ;
lm2 = MOTS 'T' ;
puis_t = xty puis_n t1 lm1 lm2 ;

*-------------------------------
* puissance théorique perdue par les frontieres:
*-------------------------------

abso0 = -1 * abso0 ;
b0 = 5.670E-8 ;
EMG1 = b0 * ( T_gas ** 4. ) ;
EMS1 = b0 * ( T_wall ** 4. ) ;
AIRE1 = 4. * 3.1416 * R1 * R1 ;
tau1 = 2. / ( ( 2. * abso0 * R1 ) ** 2. ) ;
tau0 = ( 2. * abso0 * R1 ) + 1. ;
tau0 = tau0 * ( 'EXP' ( -2. * abso0 * R1 ) ) ;
tau1 = ( 1. - tau0 ) * tau1 ;
abso1 = 1. - tau1 ;
denom0 = ( 1. / e_wall ) + ( 1. / abso1 ) - 1. ;
res0 = -1. * AIRE1 * ( EMG1 - EMS1 ) / denom0 ;

*-------------------------------
* puissance obtenue numériquement à la creation du test (06/96)
*-------------------------------
si complet;
res1 = -699.76 ;
erma = 10.;
sinon;
res1 = -510.37 ;
erma = 30.;
finsi;

*-------------------------------
*  Erreurs
*-------------------------------

* par rapport a la solution analytique
si ( ( 'ABS' res0 ) '>' 1.E-5 ) ;
  err0 = ( 'ABS' ( res0 - puis_t ) ) / ( 'ABS' res0 ) ;
  err0 = err0 * 100. ;
sinon ;
  err0 = 0. ;
finsi ;

* par rapport à la solution numérique de référence
err1 = ( 'ABS' ( res1 - puis_t ) ) / ( 'ABS' res1 ) ;
err1 = err1 * 100. ;
'MESS' ' Puissance absorbée calculée  = ' puis_t ;
'MESS' ' Puissance absorbée théorique = ' res0 ;
'MESS' ' Erreur en pourcentage        = ' err0 ' % ' ;

*-------------------------------
* calcul du flux rayonné au moyen de l'évaluation de la
* température de rayonnement (option 2 de RAYE)
*-------------------------------

       trad = raye mrt  fft e  te_cavi 1e-7  ;
*      mess 'trad ' (mini trad) (maxi trad);

* calcul du coefficient d'echange

       hrad = HRCAV  mrt e te_cavi trad ;
*      mess 'hrad ' (mini hrad) (maxi hrad);

       trad_n1= chan 'CHPO' mrt trad       ;
       trad_n = nomc trad_n1 'T' 'NATU' 'DIFFUS';

* pour la condition de convection
       cr = cond mrt hrad;
       f = conv mrt hrad trad_n;

*      flux rayonne

       fray  =  (cr * tp)- f;
       fray_tot = maxi (resu fray);

*      mess ' flux par methode 2 ' fray_tot ;

*-------------------------------
*  Erreur methode 2
*-------------------------------

si ( ( 'ABS' res0 ) '>' 1.E-5 ) ;
  err2 = ( 'ABS' ( res1 - fray_tot) ) / ( 'ABS' res1 ) ;
  err2 = err2 * 100. ;
sinon ;
  err2 = 0. ;
finsi ;
'MESS' ' Puissance absorbée calculée methode 2  = ' fray_tot ;
'MESS' ' Puissance absorbee théorique = ' res0 ;
'MESS' ' Erreur methode 2 en pourcentage = ' err2 ' % ' ;

si (err0 '>' erma) ;
 'ERRE' 5 ;
finsi ;
si (( err1 '<' 1. ) et ( err2 '<' 1. ));
 'ERRE' 0 ;
sinon ;
 'ERRE' 5 ;
finsi ;

fin ;




