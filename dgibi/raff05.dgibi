************************************************************
** raff05.dgibi
************************************************************
*                                                          *
*  Calcul elastique 2D avec un changement de niveau de     *
*  raffinement dans une zonne  X-FEM                       *
*  Test  : Raff , MODLI, FPMASS, RIGI1, RIGIX, RIGIXR,     *
*  RUSRUR,  RIGSUX, TRIELE                                 *
*                                                          *
*    ^ ^ ^ ^ ^ ^ ^                                         *
*    | | | | | | |                                         *
*    -------------  CL de Dirichlet                        *
*   |             |                                        *
*   |             |                                        *
*   |             |                                        *
*   |             |                                        *
*   |   -=====-   | Fissure horizontale                    *
*   |             |                                        *
*   |             |                                        *
*   |             |                                        *
*   |             |                                        *
*  > -------------  CL de Dirichlet                        *
*    ^ ^ ^ ^ ^ ^ ^                                         *
*                                                          *
* creation gg le                                           *
************************************************************
************************************************************
TITRE 'raff05.dgibi' ;

OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO ;

OPTI 'POTR' 'HELVETICA_16';
OPTI TRAC X ;

*graph = vrai;
graph = faux ;

* dimention du caree
l=10.;
*paramètres de raffinement


dens1min=(l/4.0);
*d1min=dens1min**1.5;
dens2min=(l/8.0);
*d2min=dens2min**1.5;
dens3min=(l/64.0);

densmax=(l/2)*1.000001 ;
densma4= 8*densmax;
dmin=0.00001;
*dmin = (3*l/8);
dmax=(l/2)*0.99999 ;
dmax2=(l)*0.99999 ;

* Points
p1 = 0. (-1.0*l) ;
p2 = l (-1.0*l). ;
p3 = l l ;
p4 = 0. l ;
p5 = (-1.0*l) (-1.0*l);
p6 = (-1.0*l) l;


* Lignes
l12 = p1 DROI 2 p2;
l23 = p2 DROI 4 p3;
l34 = p3 DROI 2 p4;
l41 = p4 DROI 4 p1;
l14 = p1 DROI 2 p4;
l45 = p4 DROI 2 p5;
l51 = p5 DROI 2 p1;

* Surface
s0 = DALL l12 l23 l34 l41;
t0 = surf (l14 et l45 et l51);
st0= s0 et t0 ;


* Premier raffinement

x0 y0 = coor s0 ;

DF0= abs y0;

aff2 = ((((densmax) - dens2min)*(DF0 - dmax))/(dmax - dmin)) + densmax ;
dens2 = born aff2 'SCAL' 'COMPRIS'
(dens2min*1.000001) (densmax*1.000001);

aff2 = ((((densmax) - (dens3min/64))*(DF0 - (dmax)))/
((dmax) - (2*dmin))) + densmax ;
dens2 = born aff2 'SCAL' 'COMPRIS'
(dens2min*1.000001) (densmax*1.000001);

s02 = raff s0 dens2;



*----------- rafinement d'un maillage isssue de 2 it de raff ---------*
x2 y2 = coor s02 ;

DF2= (((y2**2)+(((X2 - (l*2/7))/8)**2))**0.5);


aff23 = (((densmax - dens3min)*(DF2 - dmax))/(dmax - dmin)) + densmax ;
dens23 = born aff23 'SCAL' 'COMPRIS'
(dens3min*1.000001) (densmax*1.000001);
s23 = raff s02 dens23;
*s23 =s02;

*--------- Propriété materiau                                      ---*


E1=2.09086E+11;
nu1=0.3;


*Courbe de traction matériau éprouvette
EPS1 = PROG 0. 2.50007E-3 5.39261E-3 1.40558E-2 2.28078E-2 3.17654E-2
4.1161E-2 5.14294E-2 6.33414E-2 7.82031E-2 9.81451E-2 0.12653 0.1685
0.23177 0.32756 0.47195 0.68754 1.0056 1.4685 2.1336 3.0765 4.3966
6.223 8.7212 12.102 16.633 22.647 30.56 40.883 54.247 71.419 93.330
121.10 ;

SG1 = PROG 0. 522.73 532.73 562.73 592.73 622.73 652.73 682.73 712.73
742.73 772.73 802.73 832.73 862.73 892.73 922.73 952.73 982.73 1012.7
1042.7 1072.7 1102.7 1132.7 1162.7 1192.7 1222.7 1252.7 1282.7 1312.7
1342.7 1372.7 1402.7 1432.7 ;
SG1=SG1*1.0E+6;

EVTRAC = EVOL 'MANU' EPS1 SG1 ;


*-----------geometrie fissure -----------------------------------------*

pf1 = (l*2/7)  (l*3/64);
pf2 = (l*5/7) (l*3/64);
pmilieu = (l/2) (l*3/64);

lfiss =  pf1 DROI 10 pf2;


*----------modelisation xfem ------------------------------------------*
x23 y23 = coor s23 ;
PSI1 PHI1 = PSIPHI s23 LFISS 'DEUX' pf1 pf2;

*PTX = POIN (abs(y23)) 'INFERIEUR' (L/4) ;
PTX = POIN (abs(y23)) 'INFERIEUR' (L/8) ;


SURFX = S23 ELEM 'APPUYE' 'LARGEMENT' PTX ;
SURFFEM = S23 DIFF SURFX ;


*MODX = MODE SURFX MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE XQ4R ;
MODX = MODE SURFX MECANIQUE ELASTIQUE EPSI LINEAIRE XQ4R ;


*list modx;
MATX = MATE MODX 'YOUN' E1 'NU' nu1 ;
*MNLX = MATE MODX YOUN E1 NU nu1 TRAC EVTRAC ;

*MODFEM = MODE SURFFEM MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE ;
MODFEM = MODE SURFFEM MECANIQUE ELASTIQUE  EPSI LINEAIRE ;


MATFEM = MATE MODFEM 'YOUN' E1 'NU' nu1 ;
*MNLFEM = MATE MODFEM YOUN E1 NU nu1 TRAC EVTRAC ;

*CHPENR REL1 = TRIE (MODX)  PSI1 PHI1 'DESE' ;
CHPENR = TRIE (MODX)  PSI1 PHI1 ;

* constructionsion des blocages des ddl X-fem non actifs dans
* les éléments de transition.
* Ainsi que des relations de conformité dues au raffinement.
REL1 = RELA (MODX et MODFEM);
*---------Rigidite ---------------------------------------------*

RigX = RIGI(MODX et MODFEM) (MATX et MATFEM);
*RNLX = RIGI(MODX et MODFEM) (MNLX et MNLFEM);

*---------condition aux limite et chargement    ----------------*


lb23 = s23 POIN 'DROI' p1 p2 0.001;
cl23 = BLOQ lb23 uy;
clx = BLOQ p1 ux;

lp23 = (cont s23) elem 'APPUYE' 'STRICTEMENT' l34;
presX = PRES 'MASS' (modX et modfem) -2.0E10 lp23 ;

clp = bloq lp23 uy  ;
f1 = depi clp 1. ;
LI1 = PROG 0. 1. ;
LI2 = PROG 0. 1. ;

CHPO1 = MANU  'CHPO' lp23 'FY'  -2.0E11 ;
EV1 = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR 'DIMP' F1 EV1 ;

*---------- resolution d'un pb linéaire sur le maillage X-FEM----------*

resX = reso (rigX et cl23 et clx et CLP et REL1) F1;
SIGX = SIGM (MODX et MODFEM ) (MATX et MATFEM) resx;
defX  = defo 1.0 resX s23 ;
FOR1 = BSIG (MODX et MODFEM) SIGX;
EPS1 = EPSI  (MODX et MODFEM) resX ;

resXR = XFEM 'RECO' resx (MODX et MODFEM) ;
defXR  = defo 1.0 resXr s23 ;
DPSUP DPINF = XFEM 'FISS' lfiss resX (MODX et MODFEM) ;
DEFfiss = (DEFO 1.  DPSUP lfiss'VERT') et
 (DEFO 1.   DPINF lfiss 'ROUG')  ;

si (graph);
    trac (s23 et (coul bleu surfX) et  (coul roug Lfiss))
    (vect presx 'FORC' 'ROUG') TITR ' Maillage raffine et enrichi' ;
    trac (DEFfiss et defxR)  TITR 'Deformee';
    trac resx (surfx et lfiss) TITR 'Champ de deplacement XFEM';
    trac sigx (modx) TITR 'Champ de contraintes ';
    trac FOR1 S23 TITR 'Forces Nodales';
    trac EPS1 (MODX et MODFEM) TITR 'DEFORMATIONS';
    trac resxR (surfx et lfiss) TITR 'Champ de deplacement FEM' ;
finsi ;

*---------- resolution d'un pb non linéaire sur le maillage X-FEM------*

*Résolution PASAPAS
TABPAP = TABLE ;
TABPAP . 'TEMPS_CALCULES' = PROG 0. pas 0.1 1. ;
TABPAP . 'CARACTERISTIQUES' = (MATX et MATFEM) ;
TABPAP . 'CHARGEMENT' = CHA1 ;
TABPAP . 'BLOCAGES_MECANIQUES' = cl23 et clx  ET CLP et REL1;
TABPAP . 'MODELE' = (MODX et MODFEM) ;
TABPAP . 'LAGRANGIEN' = mot 'REACTUALISE' ;
PASAPAS TABPAP ;

Udiff = resx - (tabpap.deplacements.10);
diffy = exco Udiff 'UY';
diffy2 =  chan 'CHAM' (diffy **2) s23;
si (graph);
   trac Udiff (surfx et lfiss) TITR 'différence entre reso et pasapas';
finsi ;

* geometrie pour le post traitement
phaut = (L/2) L;
pbas = (L/2) 0;

* symetrie

uy1 = exco resxR 'UY';
S23sym uysym1  = s23 uy1 SYME 'DROIT' phaut pbas;
* passage a un chamelem aux noeuds
uysym2 = chan 'CHAM' uysym1 S23sym;
* projection
uysym3 = PROI uysym2 s23;

* passage de uproj en chpoin
uy3= chan 'CHAM' uy1 s23;

diffy =  (uysym3-uy1)**2;
diffy1 = chan 'CHAM' diffy s23;

unor = CHAN 'CHAM' ((uy1)**2) s23;


ptsym = poin y23 compris 1.0 1.5;
ssym = elem s23 appu larg ptsym ;
modsym = redu (MODX et MODFEM) ssym;
erry1 = (INTG (modsym) diffy1) / (INTG (modsym) unor);
list erry1;

si (graph);
 trac (abs (uysym3 - uy1)) ssym TITR 'Defaut de symetrie de la composante Uy';
finsi ;

SI  (erry1 < (3.0E-5));
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
  Erre 0;
Sinon ;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  Erre 5;
finsi ;

erry2 = (INTG (MODX) diffy2) / (INTG (MODX) unor);
list erry2;
SI  (erry2 < (1.0E-15)) ;
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
  Erre 0 ;
Sinon ;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  Erre 5;
finsi;
*FIN;












