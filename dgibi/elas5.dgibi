* fichier :  elas5.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************

*******************************************************
*           Test elas5.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 91/06/13     PROVENANCE : TEST

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*___________________________________________________________________
*   TEST ELAS5                                                     *
*                                                                  *
*   CYLINDRE - SPHERE AXISYMETRIQUE                                *
*                                                                  *
*   cas-test NAFEMS : test numero LE7                              *
*                                                                  *
*      La structure est constituée d'un cylindre surmonté d'une    *
*   demi-sphère. Elle est soumise a une pression interne de 1MPa.   *
*                                                                  *
*      Le problème présentant une symétrie axiale (géometrie et    *
*   chargement), on se place en mode axisymétrique.                *
*                                                                  *
*      On se propose de calculer la contrainte axiale Smyy sur la  *
*   surface extérieure au point D. La valeur trouvée est ensuite   *
*   comparée à une valeur théorique trouvée analytiquement.        *
*                                                                  *
*___________________________________________________________________
OPTI DIME 2;
OPTI MODE AXIS;
OPTI ELEM SEG2;
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
* Dimension en metres
*
* Points
*
RO2 = 0.;    ZO2 = 1.5;   PO2 = RO2 ZO2;
*
RA = 0.;     ZA = 2.5;    PA = RA ZA;
RB = 0.9814; ZB = 1.6920; PB = RB ZB;
RC = 1.;     ZC = 1.5;    PC = RC ZC;
RD = 1.;     ZD = 1.4034; PD = RD ZD;
RE = 1.;     ZE = 1.1136; PE = RE ZE;
RF = 1.;     ZF = 0.;     PF = RF ZF;
*
* Droites, cercles et lignes
*
* coefficient de finesse du maillage
K = 2;
*
N1 = 6 * K;
AB = PA CERC N1 PO2 PB;
*
N2 = 4 * K;
BC = PB CERC N2 PO2 PC;
*
N3 = 2 * K;
CD = PC DROI N3 PD;
*
N4 = 3 * K;
DE = PD DROI N4 PE;
*
N5 = 4 * 2;
EF = PE DROI N5 PF;
*
* Maillage
*
SUR1 = AB ET BC ET CD ET DE ET EF;
*
ELIM SUR1 0.001;
*
SI (NEG GRAPH 'N');
   TITR 'ELAS5 : MAILLAGE';
   TRAC QUAL SUR1;
FINSI;
*
ELSUR1 = NBEL SUR1;
NOSUR1 = NBNO SUR1;
*
*==================================================================*
*      modele - affecte - materiau - caracteristique               *
*           rigidite - conditions aux limites                      *
*==================================================================*
MODL1 = MODE SUR1  MECANIQUE ELASTIQUE ISOTROPE COQ2;
MATR1 = MATE MODL1 YOUN 210E9 NU 0.3 EPAI 0.025;
*
MOP = 'MODE' SUR1 'CHARGEMENT' 'PRESSION' COQ2 ;
MAP = 'PRES' MOP 'PRES' 1.E6 ;
*
MODLT = MODL1 'ET' MOP ;
MATRT = MATR1 ;
*
RIG1  = RIGI MODLT MATRT;
*
* Conditions aux limites
*    - point pa : pas de deplacement radial et pas de rotation,
*    - point pf : pas de deplacement vertical.
*
CL1  = BLOQ ROTA PA;
CL2  = BLOQ UR PA;
CL3  = BLOQ UZ PF;
*
RIG1 = RIG1 ET CL1 ET CL2 ET CL3;
*
*==================================================================*
*                      Chargement                                  *
*==================================================================*
*
*   Pression interne uniforme de 1e6Pa.
*
cha1 = bsig mop map ;
*
*==================================================================*
*            resolution : champs de deplacements                   *
*==================================================================*
*
DEP1 = RESO RIG1 CHA1;
*
* Trace facultatif de la deformee et des reactions
*
SI (NEG GRAPH 'N');
  AMPVEC = .5E-6;
  REA1 = REAC DEP1 RIG1;
  VECT1 = VECT CHA1 (AMPVEC * 1) FR FZ ROUGE;
  VECT2 = VECT REA1  AMPVEC      FR FZ BLEU ;
  DEF0 = DEFO 0. DEP1 SUR1;
  DEF1 = DEFO 100. DEP1 SUR1 (VECT1 ET VECT2) VERT;
  TITR 'ELAS5 : DEFORMEE, CHARGEMENT, REACTIONS';
  TRAC SUR1 (DEF0 ET DEF1);
FINSI;
*
*==================================================================*
*              extraction d'une contrainte                         *
*==================================================================*
*
*   On cherche la contrainte axiale sigyy sur la surface exterieure
* au point D.
*   Cette valeur est ensuite comparee a la valeur theorique obtenue
* analytiquement.
*
* Calcul du champs de contraintes generalisees.
*
CHAM1 = SIGM MODL1 MATR1 DEP1;
*
* Calcul du champs de contraintes au sens des milieux continus.
*   Pour cela on utilise l'operateur CALP .
*
CHAM2 = CALP CHAM1 MATRT MODLT SUPE;
*
* Interpolation des contraintes au noeuds
*
CHAM3 = CHAN NOEUD MODL1 CHAM2;
CHPO3 = CHAN CHPO CHAM3 MODL1;
*
*    Extraction de la contrainte smyy au point D : Pour cela on extrait
* la contrainte smxx du repere local des elements qui correspond a la
* contrainte smyy dans le repere global.
*
SMYYD = EXTR CHPO3 SMXX PD;
SMYYD = SMYYD / 1.E6;
*
SMYYD1 = 25.86;
*
* Calcul de l'ecart avec la valeur de reference
*
ERGYYD = 100 * (ABS ((SMYYD1 - SMYYD) / SMYYD1));
*
*==================================================================*
*                   affichage des resultats                        *
*==================================================================*
*
MESS ' Resultats ELAS5';
MESS ' ---------------';
SAUT 2 LIGN;
*
MESS '     La valeur cherchee est la contrainte axiale au point D, ';
MESS ' sur la surface exterieure. Elle est comparee a la valeur    ';
MESS ' theorique obtenue analytiquement.';
SAUT 1 LIGN;
MESS ' Contrainte axiale theorique en D :' SMYYD1 'MPa';
SAUT 1 LIGN;
MESS ' Contrainte axiale calculee en D  :' SMYYD 'MPa';
SAUT 1 LIGN;
MESS '          Soit un ecart de :' ERGYYD '%';
SAUT 2 LIGN;
*
SI (K EGA 1);
  MESS ' Maillage grossier';
SINON;
  MESS ' Maillage fin';
FINSI;
*
MESS '    Nombre d elements :' ELSUR1;
MESS '    Nombre de noeuds  :' NOSUR1;
MESS '       soit ' (NOSUR1 * 3) 'd.d.l.';
*
*==================================================================*
*                  code fonctionnement                             *
*==================================================================*
*
*     L'ecart maximum entre valeur theorique et calculee doit etre
* inferieure a 1%.
*
SAUT 2 LIGNE;
SI (ERGYYD <EG 1.);
    ERRE 0;
SINON;
    ERRE 5;
FINSI;
*
*==================================================================*
*                 Temps de calcul et fin                           *
*==================================================================*
*
SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;
*
FIN;








