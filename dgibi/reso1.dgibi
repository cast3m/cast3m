*
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
* fichier : reso1.dgibi
*
'OPTI' 'ECHO' 0 'VERI' 0 ;
*
'SAUTER' 2 'LIGNE' ;
'MESSAGE' ' Execution de reso1.dgibi' ;
'SAUTER' 2 'LIGNE' ;
*
* On résoud un petit système linéaire 2x2 avec une relation
* Ceci a pour but de tester la résolution, y compris lorsque
* les noms de primales et duales sont égales (noms d'inconnues non
* standard, cad non référencés dans le bdata.eso)
*
'OPTION' 'DIME' 1 'ISOV' 'SULI' 'ELEM' 'SEG2' ;
p0 = 'POIN' 0. ;
nprin = 'MOTS' 'UX' 'UY' ; nduan = 'MOTS' 'FX' 'FY' ;
npria = 'MOTS' 'VX' 'VY' ; nduaa = 'MOTS' 'VX' 'VY' ;
*
rigux = 'MANUEL' 'RIGI' p0 ('MOTS' 'UX') ('PROG' 1.) ;
riguy = 'MANUEL' 'RIGI' p0 ('MOTS' 'UY') ('PROG' 1.) ;
rel   = rela 0.5 'UX' p0 '-' 0.5 'UY' p0 ;
smb   = 'MANUEL' 'CHPO' p0 1 'FX' 1. ;
*
rtot = rigux 'ET' riguy 'ET' rel ;
ftot = smb ;
* Chgt de nom des composantes
rtot2 = 'CHANGER' rtot 'INCO' nprin npria nduan nduaa ;
ftot2 = 'EXCO' nduan ftot nduaa 'NOID' ;
*
sol = 'RESOUD' rtot ftot ;
res = '-' ftot ('*' rtot sol) ;
mresa = 'MAXIMUM' res 'ABS' ;
'MESSAGE' ('CHAINE' 'mresa=' mresa) ;
*
'SI' ('>' mresa 1.D-10) ;
   cherr = 'Norm residu mresa > 1.D-10 lors de la resolution' ;
   'ERREUR' cherr ;
'FINSI' ;
sol2 = 'RESOUD' rtot2 ftot2 ;
res2 = '-' ftot2 ('*' rtot2 sol2) ;
mresa2 = 'MAXIMUM' res2 'ABS' ;
'MESSAGE' ('CHAINE' 'mresa2=' mresa2) ;
*
'SI' ('>' mresa2 1.D-10) ;
   cherr = 'Norm residu mresa2 > 1.D-10 lors de la resolution' ;
   'ERREUR' cherr ;
'FINSI' ;
*
'FIN' ;











