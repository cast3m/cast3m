* fichier :  poudre6.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
******************************************************************
*                                                                *
*                       Cylindre de poudre                       *
*                                                                *
*   Programme test numero 6 de la loi poudre_A - le 14/03/1997   *
*                                                                *
*                        Laurent Sanchez                         *
*                                                                *
******************************************************************
*   loi a 3 parametres log A,Vitesse de deplacement imposee
*   Un cylindre de poudre seul en TA6V est densifie.
*   Simulation d'un essai en matrice fermee
*   VITESSE DE DEPLACEMENT IMPOSEE EGALE A 10-3MM/S - RHO0 = 0.7
*   La densite initiale de la poudre est uniforme.
*   Calcul avec Gdes Rotations, Gdes deformations, Pression
*   Suiveuse, log A & les vrais coefficients thermiques, New E
*   ATTENTION : PRG fonctionnant avec k2001
*               utilation de POUDRE_A : loi a 3 parametres
*   Le maillage est regulier en Q8.
*
OPTION DIME 2 ELEM QUA8 MODE AXIS ;
*
*
******************************************************************
*                                                                *
*    Maillage                                                    *
*                                                                *
******************************************************************
*
*
*    Parametres du maillage en mm
*
rayon_1 = 2.0 ;
haute_1 = 2.0 ;
*
*
*    Definition des coordonnees des points
*
xa00 = 0.0 ;
ya00 = 0.0 ;
*
xa01 = rayon_1 ;
ya01 = ya00 ;
*
xa02 = xa01 ;
ya02 = ya01 + haute_1 ;
*
xa03 = 0.0 ;
ya03 = ya02 ;
*
*
*    Definition des points
*
a00 = xa00 ya00 ;
a01 = xa01 ya01 ;
a02 = xa02 ya02 ;
a03 = xa03 ya03 ;
*
*
*    Definition du nombre d'elements
*
nba00a01 = 1 ;
nba01a02 = 1 ;
nba02a03 = 1 ;
nba03a00 = 1 ;
*
*
*    Definition des lignes
*
la00a01 = DROI nba00a01 a00 a01 ;
la01a02 = DROI nba01a02 a01 a02 ;
la02a03 = DROI nba02a03 a02 a03 ;
la03a00 = DROI nba03a00 a03 a00 ;
*
*
*    Definition des surfaces
*
elmat1 = SURF ( la00a01 ET la01a02 ET la02a03 ET la03a00 )
 PLAN COUL bleu ;
*
piece_1 = elmat1 ;
tot     = piece_1 ;
*
*
******************************************************************
*                                                                *
*    Contour exterieur et symetrie                               *
*                                                                *
******************************************************************
*
*
l_ext1 = la01a02 ;
l_ext2 = la02a03 ;
l_ext  = l_ext1 ET l_ext2 ;
*
l_symZ = la00a01 ;
l_symR = la03a00 ;
*
*TRAC l_ext ;
*TRAC l_sym ;
TITR 'Maillage regulier en Q8 d un quart de piece: tot' ;
*TRAC tot ;
*
*
*****************************************************************
*                                                               *
*    Conditions de l'essai                                      *
*                                                               *
*****************************************************************
*
*
T0tempe = 800. ;
T1tempe = 800. ;
T2tempe = 800. ;
T3tempe = 800. ;
*
T0temps = 0. ;
T1temps = 600. ;
T2temps = T1temps + 600. ;
T3temps = T2temps + 600. ;
*
*
*****************************************************************
*                                                               *
*    Champ-point de temperature                                 *
*                                                               *
*****************************************************************
*
*
T0CHTEM  = MANU 'CHPO' tot 1 'T' T0tempe ;
*
clth0001 = BLOQ 'T' l_ext ;
p_bloth1 = DEPI clth0001 1. ;
p_temps1 = PROG T0temps T1temps T2temps T3temps ;
p_theta1 = PROG T0tempe T1tempe T2tempe T3tempe ;
ev_ther1 = EVOL MANU TEMPS p_temps1 'T' p_theta1 ;
*DESS ev_ther1 TITR 'Evolution du chargement en temperature (C)' ;
ch_ther1 = CHAR 'TIMP' p_bloth1 ev_ther1 ;
*
*
*****************************************************************
*                                                               *
*    Fichier Materiau Mecanique - Poudre TA6V                   *
*                                                               *
*****************************************************************
*
*
p1model = MODE piece_1 MECANIQUE ELASTIQUE VISCOPLASTIQUE
 POUDRE_A CONS DEUX ;
*
V1YOUN  = 76.0e+03 ;
V1NU    = 0.33 ;
V1ALPHA = 1.06e-5 ;
*
V1logA  = -9.28 ;
V1N     = 3.04 ;
V1QSRT  = 0. ;
*
V1RHOR  = 0.7 ;
V1F0    = 0.1627 ;
V1F1    = 0. ;
V1F2    = 0.8743 ;
V1F3    = 0. ;
V1F4    = 2. ;
V1F5    = 0.66 ;
V1C0    = 1.9325 ;
V1C1    = 0. ;
V1C2    = 0.6013 ;
V1C3    = 0. ;
V1C4    = 0. ;
V1C5    = 1000. ;
*
*
p1mater = MATER p1model YOUN V1YOUN NU V1NU ALPHA V1ALPHA
 A V1logA N V1N QSRT V1QSRT F0 V1F0 F1 V1F1 F2 V1F2 F3 V1F3
 F4 V1F4 F5 V1F5 C0 V1C0 C1 V1C1 C2 V1C2 C3 V1C3 C4 V1C4
 C5 V1C5 RHOR V1RHOR ;
*
*
*****************************************************************
*                                                               *
*    Fichier Materiau Thermique - Poudre TA6V                   *
*                                                               *
*****************************************************************
*
*
p2model = MODE piece_1 THERMIQUE ISOTROPE CONS UN ;
*
V2C     = 845. ;
V2K     = 18.0e-3 ;
V2RHO   = 4.0e-6 ;
*
*
p2mater = MATER p2model 'C' V2C 'K' V2K 'RHO' V2RHO ;
*
*
*****************************************************************
*                                                               *
*    Chargement                                                 *
*                                                               *
*****************************************************************
*
*
p0bloca  = BLOQ UZ l_symZ ;
p1bloca  = BLOQ UR ( l_symR ET l_ext1 ) ;
p2bloca  = BLOQ UZ l_ext2 ;
*p_bloca  = p0bloca ET p1bloca ;
p_bloca  = p0bloca ET p1bloca ET p2bloca ;
*
*T0pres   =  0.1 ;
*T1pres   = 15.  ;
*T2pres   = 15.  ;
*T3pres   =  0.1 ;
*
li_temps = PROG T0temps T1temps T2temps T3temps ;
*
*li_pres  = PROG T0pres  T1pres  T2pres  T3pres ;
*ev_pres  = EVOL MANU TEMPS li_temps PRES li_pres ;
*DESS ev_pres TITR 'Evolution du chargement en pression (MPa)' ;
*p_force1 = MANU 'CHPO' l_ext 1 'P' 1.0 ;
*p_charg1 = CHAR 'PSUI' p_force1 ev_pres ;
*
T0depla  =  0.0 ;
T1depla  = -0.6 ;
T2depla  = -0.6 ;
T3depla  =  0.6 ;
*
li_depl1 = PROG T0depla T1depla T2depla T3depla ;
ev_depl1 = EVOL MANU 'TEMPS' li_temps 'DEPLACEMENT' li_depl1 ;
ch_depl1 = DEPIMP p2bloca 1.0 ;
p_charg1 = CHAR DIMP ch_depl1 ev_depl1 ;
*
*
*****************************************************************
*                                                               *
*    Calcul                                                     *
*                                                               *
*****************************************************************
*
*
p_model = p1model ET p2model ;
p_mater = p1mater ET p2mater ;
p_charg  = p_charg1 ET ch_ther1 ;
*
*
TAB1                         = TABLE ;
TAB1 . 'TEMPERATURES'        = TABLE ;
TAB1 . 'TEMPERATURES' . 0    = T0CHTEM ;
TAB1 . 'CHARGEMENT'          = p_charg ;
TAB1 . 'MODELE'              = p_model ;
TAB1 . 'CARACTERISTIQUES'    = p_mater ;
TAB1 . 'BLOCAGES_MECANIQUES' = p_bloca ;
TAB1 . 'BLOCAGES_THERMIQUES' = clth0001 ;
*
TAB1 . 'TEMPS_CALCULES'      = PROG 0. 50. PAS 50. 300. ;
*
TAB1 . 'TEMPS_SAUVES'        = PROG 0. 300. ;
*
TAB1 . 'MAXITERATION'        = 169 ;
TAB1 . 'MAXISOUSPAS'         = 2000 ;
TAB1 . 'MOVA'                = 'MOT' 'VHOR' ;
TAB1 . 'PRECISION'           = 5.0E-4 ;
TAB1 . 'TALPHA_REFERENCE'    = 25. ;
TAB1 . 'PROCEDURE_THERMIQUE' = 'MOT' 'LINEAIRE' ;
TAB1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1 . 'CELSIUS'             = VRAI ;
TAB1.'LAGRANGIEN' = 'MOT' 'REACTUALISE';
*
PASAPAS TAB1 ;
*
*
*****************************************************************
*                                                               *
*    Test de validation                                         *
*                                                               *
*****************************************************************
*
*
ind1  = INDEX (TAB1 . 'VARIABLES_INTERNES') ;
nb1   = DIME ind1 ;
*
*
*    Densite finale numerique
*
vm1   = TAB1 . 'VARIABLES_INTERNES' .(ind1.nb1) ;
vm2   = EXCO VHOR vm1 ;
vm3   = REDU vm2 tot ;
rho1  = MAXI vm3 ;
*
*
*    Densite finale analytique
*
rho2  = 0.85 ;
*
*
*    Contrainte radiale finale numerique
*
ind1  = INDEX (TAB1 . 'CONTRAINTES' ) ;
nb1   = DIME ind1 ;
*
*
*    Deplacement numerique du point C
*
ind2  = INDEX (TAB1 . 'DEPLACEMENTS' ) ;
nb2   = DIME ind2 ;
uz1   = EXTR TAB1 .'DEPLACEMENTS'.(ind2.nb2) UZ a02 ;
*
*
*    Deplacement analytique du point C
*
uz2   = haute_1 * (V1RHOR - rho2) ;
*
*
*    Erreurs sur le deplacement et la densite :
*
erho1 = ABS ((rho1 - rho2 ) / rho2);
euz1  = ABS ((uz1 - uz2) / uz2) ;
*
*
*    Ecriture des erreurs a l'ecran :
*
OPTI ECHO 0 ;
*
SAUTER 3 LIGNES ;
*
MESS 'Calcul en matrice fermee :' ;
MESS 'Erreur sur le deplacement vertical :  '  euz1 ;
MESS 'Erreur sur la densite finale       :  '  erho1 ;
SAUTER 3 LIGNES ;
*
*
SI ( (euz1 + erho1 ) >EG 0.05 ) ;
   MESS '--------- RESULTATS INCORRECTS -------------' ;
   SAUTER 3 LIGNES ;
   ERREUR 5;
SINON ;
   MESS '---------  RESULTATS CORRECTS  -------------' ;
   SAUTER 3 LIGNES ;
FINSI ;
*
FIN ;









