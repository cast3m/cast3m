* fichier :  thpl3.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
* CAS TEST DU 92/03/20     PROVENANCE : TC1
* CAS TEST DU 92/03/19     PROVENANCE : PHIL
opti echo 0;
MESS ' ***************************************************************';
MESS ' * COMMISSION V.P.C.S.  : THERMO-PLASTICITE                    *';
MESS ' *                                                             *';
MESS ' * CYLINDRE SOUMIS A UNE REPARTITION LOGARITHMIQUE DE          *';
MESS ' * TEMPERATURE, CALCUL EN AXISYMETRIE.                         *';
MESS ' *                                                             *';
MESS ' * FONCTIONS TESTEES : ELASTO-PLASTICITE PARFAITE SOUS         *';
MESS ' *                     CHARGEMENT THERMIQUE.                   *';
MESS ' *                                                             *';
MESS ' ***************************************************************';
saut 1 lign;
MESS ' LES RESULTATS DE REFERENCE SONT LA MOYENNE DES CALCULS';
MESS ' EFFECTUES PAR FRAMASOFT ET LE CETIM';

OPTION TRACE BENS;
OPTION DIME 2 ;
OPTION MODE AXIS;
OPTION ELEM QUA8;

GRAPH='N';
*
*++++++++++++++++++++++++++++++++*
*    GEOMETRIE DU CYLINDRE       *
*++++++++++++++++++++++++++++++++*
*
DENS 1. ; RA = 1. ; ZA = 0.  ; PA = RA ZA;
DENS 1.2; RB = 2. ; ZB = 0.  ; PB = RB ZB;
dens 1.1; pm = 1.497 0;


DENS 1. ; RC = 1. ; ZC = 0.1 ; PC = RC ZC;
DENS 1.2; RD = 2. ; ZD = 0.1 ; PD = RD ZD;
DENS 1.1; pz = 1.497 0.1;
*
DAm = PA DROIT -7 pm;
DmB = pm droit -3  PB;
DAB = DAm et DmB;

DBD = PB DROIT -1 PD;
DDz = PD DROIT -3 Pz;
DCA = PC DROIT -1 PA;
DzC = Pz DROIT -7 PC;
DDC = DDz et DzC;

*
SUR1 = DALL DAB DBD DDC DCA PLAN ;


ELIM SUR1 0.0001;
*
NOSUR1 = NBNO SUR1;
ELSUR1 = NBEL SUR1;
*
SI (NEG GRAPH 'N');
  TITR ' SSNA04 : MAILLAGE PAR QUA8 (NOEUDS)';
  TRACE NOEUD SUR1;
  TITR ' SSNA04 : MAILLAGE PAR QUA8 (ELEMENTS)';
  TRACE ELEM SUR1;
FINSI;

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                  CONDITIONS AUX LIMITES                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CL1 = BLOQUE DAB UZ ;
CL2 = BLOQUE DDC UZ ;
CL = CL1 ET CL2 ;
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*         MODELES - MATERIAUX - CARACTERISTIQUES
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
MO = MODE SUR1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'PARFAIT' QUA8;

MAT = MATE MO YOUN 200000 NU 0.3 ALPHA 12E-6 SIGY 300;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*        CHAMPS RELATIFS A LA PROCEDURE PASAPAS                  *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
*==============================================================*
*       CHAMPS DE TEMPERATURE REPARTIE SUR SUR1                *
*==============================================================*
*
* 10 PAS DE CHARGEMENT ENTRE 0C ET 200C
*
T0 = 0.; T1 = 1. ; T2 = 2.; T3 = 3. ;
T4 = 4.; T5 = 5. ; T6 = 6.; T7 = 7. ;
T8 = 8.; T9 = 9. ; T10 = 10.;
*
* CREATION DU CHPOINT DE TEMPERATURE POUR TE = 1C (TEMP. EXTE.)
*
CHPSUR = COOR 1 SUR1;
*
CONST1 = 1. / (LOG 2.);
CHP2 = (LOG CHPSUR) * CONST1;

* TRANSFORMATION DU SOUS-TYPE "SCAL" EN SOUS-TYPE "T" POUR CHP2
*    UTILISATION DE L'OPERATEUR VARI
*
LIS10 = PROG 0. 200.;
LIS20 = PROG 0. 200.;
EVOL2 = EVOL MANU SCAL LIS10 T LIS20;
*
CHP1 = VARI CHP2 EVOL2 T;

*
TEMP = TABLE;TEMPER = TABLE;
TEMP . 0 = T0;TEMP . 1 = T1;
TEMP . 2 = T2;TEMP . 3 = T3;
TEMP . 4 = T4;TEMP . 5 = T5;
TEMP . 6 = T6;TEMP . 7 = T8;
TEMP . 8 = T8;TEMP . 9 = T9;
TEMP . 10 = T10;

TEMPER . 0 = MANU CHPO SUR1 1 T 0.;
TEMPER . 1 = (CHP1 * 20.);
TEMPER . 2 = (CHP1 * 40.);
TEMPER . 3 = (CHP1 * 60.);
TEMPER . 4 = (CHP1 * 80.);
TEMPER . 5 = (CHP1 * 100.);
TEMPER . 6 = (CHP1 * 120.);
TEMPER . 7 = (CHP1 * 140.);
TEMPER . 8 = (CHP1 * 160.);
TEMPER . 9 = (CHP1 * 180.);
TEMPER . 10 = (CHP1 * 200.);

CHA2 = CHAR 'T' TEMP TEMPER;

*
*================================================================
*   LISTE DE VALEURS DE TEMPS OU L'ON VEUT DES RESULTATS : LIS1
*================================================================
*
LIS1 = PROG T0 T1 T2 T3 T4 T5 T6 T7 T8 T9 T10;
*
*================================================================
*   CREATION D'UN CHAMPS DE FORCE NUL : THERMO-ELASTICITE       *
*================================================================
*
LIS2 = PROG T0 T10;
LFORCE = PROG 0. 0. ;
FOR1 = FORCE FZ 1. PA;
CHPO1 = RESU FOR1;
EVOL1 = EVOL MANU TEMPS LIS2 FORCE LFORCE;
CHA1 = CHAR 'MECA' CHPO1 EVOL1;

*================================================================
*           REMPLISSAGE DE LA TABLE POUR PASAPAS                 *
*================================================================
*
TAB1 = TABLE;
TAB1.'MODELE' = MO ;
TAB1.'CARACTERISTIQUES' = MAT;
TAB1.'CHARGEMENT' = (CHA1 ET CHA2);
TAB1.'TEMPS_CALCULES' = LIS1;
TAB1.'TEMPS_SAUVES' = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL;

*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               PROCEDURE PASAPAS                               *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
PASAPAS TAB1;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*               EXTRACTION DES RESULTATS                       *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*

NN = 10;
CHEL1 = TAB1.CONTRAINTES.NN;



CHPO1 = CHAN CHPO MO CHEL1;

CHEL2 = VMIS MO CHEL1;
*
SI (NEG GRAPH 'N');
   TITR ' TRACE DES CONTRAINTES DE VON-MISES';
   TRAC CHEL2 MO  SUR1;
   LLIS2 = PROG 0. 290. 295. 300. 305. 310. 600.;
   TITR 'LIEU OU IL Y A PLASTIFICATION';
   TRAC LLIS2 CHEL2 MO SUR1;
FINSI;
*
SMRRA = EXTR CHPO1 SMRR PA; SMRRA1 = 0.1894;
ERGRRA = ABS (100 * (SMRRA - SMRRA1) / SMRRA1) ;
SMRRB = EXTR CHPO1 SMRR PB; SMRRB1 = 0.152 ;
ERGRRB = ABS (100 * (SMRRB - SMRRB1) / SMRRB1) ;
SMRRM = extr CHPO1 SMRR Pm; SMRRM1 = 47.09;
ERGRRM = ABS (100 * (SMRRM - SMRRM1) / SMRRM1) ;


SMZZA = EXTR CHPO1 SMZZ PA; SMZZA1 = 114.2 ;
ERGZZA = ABS (100 * (SMZZA - SMZZA1) / SMZZA1) ;
SMZZB = EXTR CHPO1 SMZZ PB; SMZZB1 = -345.9;
ERGZZB = ABS (100 * (SMZZB - SMZZB1) / SMZZB1) ;
 SMZZm = EXTR CHPO1 SMZZ Pm; SMZZm1 = -285.1 ;
ERGZZm = ABS (100 * (SMZZm - SMZZm1) / SMZZm1);

SMTTA = EXTR CHPO1 SMTT PA; SMTTA1 = 340.8 ;
ERGTTA = ABS (100 * (SMTTA - SMTTA1) / SMTTA1) ;
SMTTB = EXTR CHPO1 SMTT PB; SMTTB1 = -191. ;
ERGTTB = ABS (100 * (SMTTB - SMTTB1) / SMTTB1) ;
SMTTm = EXTR CHPO1 SMTT Pm; SMTTm1 = -51.52;
ERGTTm = ABS (100 * (SMTTm - SMTTm1) / SMTTm1) ;
opti echo 0;

SAUT 1 LIGN;
MESS ' RESULTATS : TEST SSNA04/90 ';
MESS ' -------------------------- ';
MESS '    rA = 1; rM = 1.497; rB =2;';
SAUT 1 LIGN;

MESS ' Resultats sur les contraintes interpolees (MPa)';
MESS ' -----------------------------------------------';
SAUT 1 LIGN;
*
MESS ' CONT.  POINT    REFERENCE        CALCULEE         ECART%';
MESS ' SMRR     A       ' SMRRA1 SMRRA  'NON SIGNIFICATIF';
MESS ' SMRR     B       ' SMRRB1 SMRRB  'NON SIGNIFICATIF';
MESS ' SMRR     M       ' SMRRM1 SMRRM  ERGRRM  ;
SAUT 1 LIGN;

MESS ' SMTT     A       ' SMTTA1 SMTTA  ERGTTA  ;
MESS ' SMTT     B       ' SMTTB1 SMTTB  ERGTTB  ;
MESS ' SMTT     M       ' SMTTm1 SMTTm  ERGTTm  ;
SAUT 1 LIGN;
*
MESS ' SMZZ     A       ' SMZZA1 SMZZA  ERGZZA  ;
MESS ' SMZZ     B       ' SMZZB1 SMZZB  ERGZZB  ;
MESS ' SMZZ     M       ' SMZZm1 SMZZm  ERGZZm  ;
 SAUT 1 LIGN;

SAUT 1 LIGN;
*
DEP1 = TAB1.DEPLACEMENTS.NN;
*
SI (NEG GRAPH 'N');
  TITR ' DEFORMEE : CALCUL AXISYMETRIQUE (SSNA04)';
  DEF0 = DEFO SUR1 DEP1 0. ;
  DEF1 = DEFO SUR1 DEP1 20. ROUGE ;
  TRAC (DEF0 ET DEF1) ;
FINSI;
*
URA = EXTR DEP1 UR PA;
URB = EXTR DEP1 UR PB;
URM = EXTR DEP1 UR Pm;
*
URA1 =  1.808E-03;
URB1 =  3.859E-03;
*
ERGURA = ABS (100 * (URA - URA1) / URA1) ;
ERGURB = ABS (100 * (URB - URB1) / URB1) ;
*
MESS ' Resultats sur les deplacements radiaux ';
MESS ' -------------------------------------- ';
SAUT 1 LIGN;
*
MESS ' Deplacement radial theorique pour r = 1 mm :' URA1 'mm';
MESS ' Deplacement radial calcul   pour r = 1 mm :' URA 'mm';
MESS '                Soit une erreur relative de :' ERGURA '%';
SAUT 1 LIGN;

MESS ' Deplacement radial calcul pour r = 1.497 mm : ' URM 'mm';
SAUT 1 LIGN;
*
MESS ' Deplacement radial thorique pour r = 2 mm :' URB1 'mm';
MESS ' Deplacement radial calcul   pour r = 2 mm :' URB 'mm';
MESS '                Soit une erreur relative de :' ERGURB '%';
*
SAUT 1 LIGN;
*
MESS ' Nombre de noeuds  : ' NOSUR1;
MESS ' Nombre d elements : ' ELSUR1;
SAUT 1 LIGN ;
*
TEMPS;
*
SAUT 1 LIGN ;
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*              CODE FONCTIONNEMENT                             *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
* Maximum des erreurs relatives
*
ERGMAX = MAXI (PROG ERGURA ERGURB ERGTTA ERGTTB ERGZZA ERGZZB
         ERGRRM ERGZZM ERGTTM);
* Il y a erreur si un des pourcentage erg*** est superieur a 5%
*
SI (ERGMAX <EG 5.);
     ERRE  0 ;
SINON;
     ERRE  5 ;
FINSI;
*
FIN;
*






