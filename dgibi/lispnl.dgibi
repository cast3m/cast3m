* fichier :  lispnl.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTION ECHO 0;
GRAPH = 'N';
SAUT PAGE;
*
*   PLAQUE FISSUREE SOLLICITEE EN TRACTION PURE
*        PLASTICITE PARFAITE
*
opti dime 3 elem qua4;
a =  25.;b = 51.5;c=b - a;h=525 / 2.;r = a / b;
E = 207000.;Eprim=E  / (1. - (0.3 ** 2));vsigf=471.;
p1 =   0.001  0.  0. ;p2 =   0.001 50.  0. ;
p3 =   0.  0. 0.;p4 =   0. 50. 0.;
d1 = p1 d 1 p2;d2 = p3 d 1 p4 ;
cad   = d1  trans 5 ( h 0. 0.) ;
lchar = cad cote 3 ;pchar = lchar poin init;
cag   = d2  trans 5 ((h * -1) 0. 0.) ;
lbloc = cag cote 3 ;
tole  = cad et cag ;
*
* Approximation de la courbe de traction parfaite
* une loi de Ramberg-Osgood avec alpha=1 et n=20
*  Comparaison des courbes
n=20;sig0=410.;
SI ( NEG GRAPH 'N' );
Psig=prog 0. pas 10. sig0 pas 1. 510.;
Peps=prog (dime Psig) * 0.;
evcal=evol manu (prog 0. (vsigf / E) 0.3) (prog 0. vsigf vsigf);
i=0;repeter bbb (dime Psig);i=i + 1;
sig=extr Psig i;
eps=(sig0 / E)  * ((sig / sig0) + ((sig / sig0) ** n));
remp Peps i eps;
fin bbb;
evshih=evol manu 'eps' Peps 'sig (MPa)' Psig;
t=tabl;
t.1='MARQ CARR ';
t.2='MARQ ETOI ';
t.'TITRE'=tabl;
t.'TITRE'. 1 = mot 'PARF';
t.'TITRE'. 2 = mot 'N=20';
titr 'Courbes de traction plastique parfaite et ecrouissage n=20';
dess (evcal et evshih) t logo;
FINSI;
*
*
mod1 = mode tole MECANIQUE ELASTIQUE PLASTIQUE PARFAIT coq4;
mat1 = mate mod1 youn E nu 0.3 epai b sigy sig0;
fis =    racc ( d1) ( d2) 0.001 ;
mod2 = mode fis MECANIQUE ELASTIQUE PLASTIQUE LINESPRING LISP;
mat2 = mate mod2  youn E  nu 0.3  epai b  fiss a
       vx 0. vy 0. vz 1. SIGF vsigf;
mo = mod1 et mod2 ;
ma = mat1 et mat2 ;
total = tole et fis ;
*
blo1 = bloq lbloc UX UZ RX RY RZ ;
blo2 =bloq P1 UY;
blo4 = bloq lchar UX;
charx = depi blo4 1.;
blo = blo1 et blo2 et blo4;
rig = (RIGI mo ma) et blo ;
*
EVT=EVOL MANU ABS (PROG 0. 12.) ORD (PROG 0. 1.2);
PGT=PROG 1. PAS 1 8. pas 0.2 9.8;
FDT=CHAR charx EVT 'MECA';
*
TENTR = TABLE;
TENTR.CHARGEMENT  = FDT;
TENTR.BLOCAGES_MECANIQUES=blo;
TENTR.MODELE=mo;
TENTR.CARACTERISTIQUES=ma;
TENTR.TEMPS_CALCULES=PGT;
PASAPAS TENTR;
*
tdep=TENTR.'DEPLACEMENTS';
tsig=TENTR.'CONTRAINTES';
tvar=TENTR.'VARIABLES_INTERNES';
*
F=1.12 - (0.23 * r) + (10.6 * (r ** 2)) - (21.7 * (r ** 3))
+ (30.4 * (r ** 4));coef=F * ((pi * a) ** 0.5);
H1=0.0391;
nu1=((1. + ((a / c) ** 2)) ** 0.5) - (a / c);
P0=(2. / (3. ** 0.5)) * 1.26 * nu1 * c * sig0;
*
NPAS=dime PGT;MPAS=NPAS + 1;
PU=PROG MPAS * 0.;
PJEcal=PROG MPAS * 0.;PJEshih=PROG MPAS * 0.;
PJPcal=PROG MPAS * 0.;PJPshih=PROG MPAS * 0.;
PJcal=PROG MPAS * 0.;PJshih=PROG MPAS * 0.;
*
repeter blo NPAS;i=&blo;
 VU=extr (tdep.i) 'UX' pchar;
 sig=tsig.i;var=tvar.i;
 RSIG=REDU SIG fis;
 P=extr RSIG 'NZZ' 1 1 1;
 RVAR=REDU VAR fis;
 VK1cal=EXTR RSIG KI 1 1 1;  VJEcal=(VK1cal ** 2) / Eprim;
 VJPcal=EXTR RVAR VHJP 1 1 1;
  VJcal=VJEcal + VJPcal;
 VK1shih=coef * (P / b);VJEshih=(VK1shih ** 2) / Eprim;
 VJPshih=(sig0 ** 2) / E * c * a / b * h1 * ((P / P0) ** n);
  VJshih=VJEshih + VJPshih;
 REMP PU (i + 1) VU;
 REMP PJEcal (i + 1) VJEcal;REMP PJEshih (i + 1) VJEshih;
 REMP PJPcal (i + 1) VJPcal;REMP PJPshih (i + 1) VJPshih;
 REMP PJcal (i + 1) VJcal;REMP PJshih (i + 1) VJshih;
SI (i ega NPAS);
ERRelas=abs ((VJEcal - VJEshih) / VJEshih);
ERRplas=abs ((VJPcal - VJPshih) / VJPshih);
SI (ERRelas <  3.5E-2);ERRE 0;SINO;ERRE 5;FINSI;
SI (ERRplas <  8.E-2);ERRE 0;SINO;ERRE 5;FINSI;
FINSI;
fin blo;
*
SI ( NEG GRAPH 'N' );
EVEcal=EVOL VERT MANU 'U (mm)' PU 'J (KJ/M^2)' PJEcal;
EVEshih=EVOL ROUG MANU 'U (mm)' PU 'J (KJ/M^2)' PJEshih;
EVPcal=EVOL VERT MANU 'U (mm)' PU 'J (KJ/M^2)' PJPcal;
EVPshih=EVOL ROUG MANU 'U (mm)' PU 'J (KJ/M^2)' PJPshih;
EVcal=EVOL VERT MANU 'U (mm)' PU 'J (KJ/M^2)' PJcal;
EVshih=EVOL ROUG MANU 'U (mm)' PU 'J (KJ/M^2)' PJshih;
dess (evEcal et evEshih) t logo;
dess (evPcal et evPshih) t logo;
dess (evcal et evshih) t logo;
FINSI;
FIN;







