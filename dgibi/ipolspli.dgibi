* fichier : ipolspli.dgibi
************************************************************************
* Section : Mathematiques Fonctions
************************************************************************
*
'OPTI' 'ECHO' 0 ;
************************************************************************
* NOM         : IPOLSPLI
* DESCRIPTION : Compare les différentes interpolations d'une fonction
*               Linéaire et Spline.
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : gounand@semt2.smts.cea.fr
**********************************************************************
* VERSION    : v1, 07/09/2012, version initiale
* HISTORIQUE : v1, 07/09/2012, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
*
graph   = FAUX ;
interact= FAUX ;
'OPTION' 'DIME' 1 'ELEM' 'SEG2' ;
'SI' ('NON' interact) ;
  'OPTION' 'TRAC' 'PS' ;
'SINON' ;
  'OPTION' 'TRAC' 'X' ;
'FINSI' ;
*
lx = 'PROG' 0. 0.5 1. 1.5 2. ;
ly = 'PROG' 0. 1. 0.  1.  0. ;
*
lxf = 'PROG' -0.3 'PAS' 0.01 2.3 ;
ly1 = 'IPOL' lxf lx ly ;
ly2 = 'IPOL' lxf lx ly 'SPLINE' ;
ly3 = 'IPOL' lxf lx ly 'SPLINE' 'DGAU' 2. ;
ly4 = 'IPOL' lxf lx ly 'SPLINE' 'DDRO' -2. ;
ly5 = 'IPOL' lxf lx ly 'SPLINE' 'DGAU' 2. 'DDRO' -2. ;
*
ev1 = 'EVOL' jaun 'MANU' lxf ly1 ;
ev2 = 'EVOL' roug 'MANU' lxf ly2 ;
ev3 = 'EVOL' vert 'MANU' lxf ly3 ;
ev4 = 'EVOL' bleu 'MANU' lxf ly4 ;
ev5 = 'EVOL' turq 'MANU' lxf ly4 ;
*
evt = ev1 'ET' ev2 'ET' ev3 'ET' ev4 'ET' ev5 ;
tt = 'TABLE' ;
tt . 'TITRE' = 'TABLE' ;
ttt = tt. 'TITRE' ;
tt . 1 = 'CHAINE' 'MARQ ROND REGU' ; ttt . 1 = 'Lineaire' ;
tt . 2 = 'CHAINE' 'MARQ CROI REGU' ; ttt . 2 = 'Spline nat' ;
tt . 3 = 'CHAINE' 'MARQ BARR REGU' ; ttt . 3 = 'Spline dxg' ;
tt . 4 = 'CHAINE' 'MARQ CARR REGU' ; ttt . 4 = 'Spline dxd' ;
tt . 5 = 'CHAINE' 'MARQ LOSA REGU' ; ttt . 5 = 'Spline dxgd' ;
'DESSIN' evt 'LEGE' tt 'TITR'
'Comparaison interpolations : lineaire et spline cubique+cond. limites';
*
dx1 = 'ABS' ('-' ly2 ly1) ;
dx2 = 'ABS' ('-' ly3 ly1) ;
dx3 = 'ABS' ('-' ly4 ly1) ;
dx4 = 'ABS' ('-' ly5 ly1) ;
*
lmin = 'PROG' ('MINI' dx1)  ('MINI' dx2)  ('MINI' dx3)  ('MINI' dx4) ;
lmax = 'PROG' ('MAXI' dx1)  ('MAXI' dx2)  ('MAXI' dx3)  ('MAXI' dx4) ;
*
crit1 = '>' ('MAXI' lmin) 1.D-10 ;
*'LISTE' lmax ;
lmaxr = 'PROG' 0.28 0.28 0.28 0.19 ;
crit2 = '<' ('MAXIMUM' ('-' lmaxr lmax)) 0. ;
lok = 'NON' ('OU' crit1 crit2) ;
*
* Fin du jeu de donnees
*
'SAUTER' 2 'LIGNE' ;
'SI' lok ;
   'MESSAGE' 'Tout sest bien passe' ;
'SINON' ;
   'MESSAGE' 'Il y a eu des erreurs' ;
'FINSI' ;
'SAUTER' 2 'LIGNE' ;
'SI' interact ;
   'OPTION' 'DONN' 5 'ECHO' 1 ;
'FINSI' ;
'SI' ('NON' lok) ;
   'ERREUR' 5 ;
'FINSI' ;
*
* End of dgibi file IPOLSPLI
*
'FIN' ;





