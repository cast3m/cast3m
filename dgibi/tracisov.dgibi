* fichier :  tracisov.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************
************************************************************************
* NOM         : TRACISOV
* DESCRIPTION : Le but de ce cas-test est de tester le trace
*               d'isovaleurs pour les diverses options et sorties.
*               Malheureusement, on ne peut tester le bon fonctionnement
*               qu'a l'oeil du specialiste car on ne sait pas faire de
*               test automatise.
*
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SFME/LTMF)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 21/10/2013, version initiale
* HISTORIQUE : v1, 21/10/2013, création
* HISTORIQUE : 20/04/2016, ajout du cas de la fiche 8919
* HISTORIQUE :
************************************************************************
'OPTI' 'ECHO' 0 ;
'SAUT' 'PAGE' ;
interact = faux ;
* s'il n'y a pas de graphique, le cas-test ne sert a rien...
*graph    = vrai ;
*
'OPTION' 'DIME' 2 ;
*
pA = 0. 0. ; pB = 1. 0. ; pC = 1. 1. ; pD = 0. 1. ;
mt = 'MANUEL' 'QUA4' pA pB pC pD ;
mt = 'CHANGER' mt 'TRI3' ;
mtq = 'CHANGER' mt 'QUADRATIQUE' ;
*
xmt = 'COORDONNEE' 1 mtq ;
*
* Petit bout de code non exécuté pour générer les tableaux
* ISOTAB et ISOTA0 de l'include CCCOUL
*
*niso = 10 ;
*'REPETER' n 11 ;
*   niso = &n '+' 1 ;
*   'MESSAGE' ('CHAINE' 'niso=' niso) ;
*   dn = '/' ('-' 12. 1.) ('FLOTTANT' &n) ;
*   'REPETER' i niso ;
*     j = 'ENTIER' ('+' 1.5 (('-' &i 1) '*' dn)) ;
*      'MESSAGE' ('CHAINE' '   i=' &i ' j=' j) ;
*   'FIN' i ;
*'FIN' n ;
*
'SI' interact ;
   lsort = 'MOTS' 'X' 'PS' 'PSC' 'OPEN' ;
'SINON' ;
   lsort = 'MOTS' 'PS' 'PSC' ;
*   lsort = 'MOTS' 'PSC' ;
'FINSI' ;
lisov = 'MOTS' 'LIGN' 'SURF' 'SULI' ;
*lisov = 'MOTS' 'SURF' 'SULI' ;
*
'REPETER' isort ('DIME' lsort) ;
   mosort = 'EXTRAIRE' lsort &isort ;
   'OPTI' 'TRAC' mosort ;
   'REPETER' iniso 18 ;
*   'REPETER' iniso 1 ;
      niso = &iniso '-' 1 ;
*      niso = &iniso '+' 1 ;
      'REPETER' iisov ('DIME' lisov) ;
         moisov = 'EXTRAIRE' lisov &iisov ;
         'OPTI' 'ISOV' moisov ;
         'REPETER' iquad 2 ;
*         'REPETER' iquad 1 ;
            'SI' ('EGA' &iquad 1) ;
               moquad = 'TRI3' ;
               mtt = mt ;
            'SINO' ;
               moquad = 'TRI6' ;
               mtt = mtq ;
            'FINSI' ;
            momo = 'CHAINE' mosort ' ' niso ' ' moisov ' ' moquad ;
            'MESSAGE' momo ;
            'SI' ('NEG' niso 0) ;
               'TRACER' xmt mtt niso 'TITR' momo ;
            'SINON' ;
               'TRACER' xmt mtt 'TITR' momo ;
            'FINSI' ;
         'FIN' iquad ;
      'FIN' iisov ;
   'FIN' iniso ;
'FIN' isort ;
*
* Cas-test qui donnait un resultat incorrect avant la fiche 8919
* le coté droit du cube apparaissait en noir au lieu de rouge
*
'OPTI' 'DIME' 3 'ELEM' 'CUB8' ;
pA = 0. 0. 0. ; pB = 1. 0. 0. ; pC = 1. 1. 0. ; pD = 0. 1. 0. ;
mt = 'MANUEL' 'QUA4' pA pB pC pD ;
mt = 'VOLUME' mt 'TRAN' 1 (0. 0. 1.) ;
xmt ymt zmt = 'COORDONNEE'  mt ;
ccmt  = '*' ('+' xmt (* 0.9 ymt)) 1.0 ;
'REPETER' isort ('DIME' lsort) ;
   mosort = 'EXTRAIRE' lsort &isort ;
   'OPTI' 'TRAC' mosort ;
   'REPETER' iisov ('DIME' lisov) ;
      moisov = 'EXTRAIRE' lisov &iisov ;
      'OPTI' 'ISOV' moisov ;
      momo = 'CHAINE' mosort ' ' moisov ;
      'MESSAGE' momo ;
      'TRACER' ccmt mt 1  'TITR' momo ;
   'FIN' iisov ;
'FIN' isort ;
*
'SI' interact ;
   'OPTION' 'DONN' 5 ;
'FINSI' ;
*
* End of dgibi file TRACISOV
*
'FIN' ;







