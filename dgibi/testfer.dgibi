* fichier :  testfer.dgibi
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************

*******************************************************
*         Test testfer.dgibi: jeux de données         *
*         -----------------------------------         *
*                                                     *
* Toit cylindrique soumis à son propre poids          *
*******************************************************

*OPTI ECHO 0;

*** affichage
GRAPH = 'N' ;

*** titre du maillage
NOM1 = 'TOIT';

*** données du problème ------------------------------------------------
*** caractéristiques du matériau
YOUN1 = 3e+10;
NU1   = 0.;
RHO1  = 2.0833e+4;

*** dimensions
LON1  = 3.;
EPAI1 = LON1 / 100.;

*** options de calcul --------------------------------------------------
OPTI DIME 3;

*** maillage -----------------------------------------------------------
OPTI ELEM SEG3;

*** coordonnées des points
P1   =  0.                  LON1               0.;
P2   =  (LON1 * (sin 40.)) (LON1 * (cos 40.))  0.;
CEN1 =  0.                  0.                 0.;

*** nb éléments
N1 = 20;

*** lignes
L1 = CERC N1 P1 CEN1 P2;

*** création de la surface
OPTI ELEM TRI6;

VEC1 = 0. 0. LON1;
S200 = L1 TRAN  N1  VEC1;

*** affichage
SI(NEG GRAPH 'N');
   TRAC FACE S200;

*'LISTE' (noeud 84) ;
*'OPTION' donn 5 ;
   FINSI;

*** modèle mécanique ---------------------------------------------------
MOD1 = MODE S200 MECANIQUE ELASTIQUE ISOTROPE COQ6;

*** matériau et caractéristiques ---------------------------------------
MAT1 = MATER MOD1 YOUNG YOUN1 NU NU1 RHO RHO1;
CAR1 = CARA MOD1 EPAI EPAI1;
MAT1 = MAT1 ET CAR1;

*** construction de la matrice de rigidité -----------------------------
RIG1 = RIGI MOD1 MAT1;

*** définition des conditions aux limites ------------------------------
*** identification des côtés pour application des conditions
*** aux limites
LC10 LC20 LC30 LC40 = COTE S200;

SI(NEG GRAPH 'N');
   TRAC QUALI (LC10 ET LC20 ET LC30 ET LC40);
FINSI;

BLOQ30 = BLOQ UX UY RZ LC30;
BLOQ40 = BLOQ UX RY RZ LC40;
BLOQ10 = BLOQ UZ RX RY LC10;

RIG1   = RIG1 ET BLOQ10 ET BLOQ40 ET BLOQ30;

*** poids propre -------------------------------------------------------
MAS1 = MASS MOD1 MAT1;
CHA1 = MANU CHPO S200 1 UY (-1.*10.);
CHA1 = MAS1 * CHA1;

SI(NEG GRAPH 'N');
   VEC1 = VECT CHA1 .01 FX FY FZ ROUGE;
   TRAC VEC1 S200;
FINSI;

*** résolution ---------------------------------------------------------
TEMPS ZERO;
RES1 = RESO RIG1 CHA1;
TEMPS;
SAUT 1 LIGNE;

*** post-traitement ----------------------------------------------------

SI(NEG GRAPH 'N');
****** maillage initial et déformée
   DEF0 = DEFO S200 RES1 0.;
   DEF1 = DEFO S200 RES1 VERT;
   TITR 'TOIT : DEFORMEE';
   TRAC S200 (DEF0 ET DEF1);
FINSI;

*** déplacement aux points P1 et P2
MESS 'Deplacement Uy au point P1  :' (EXTR RES1 UY P1);
MESS 'Deplacement Uy au point P2  :' (EXTR RES1 UY P2);

*** CHPO pour tester SORTIR option FERVIEW -----------------------------

baba = 'NOMC' 'XX' ('COORDONNEE' 1 S200) ;
bobo = 'NOMC' 'YY' ('COORDONNEE' 2 S200) ;
boba = 'NOMC' 'ZZ' ('COORDONNEE' 3 S200) ;
bubu = baba 'ET' bobo 'ET' boba ;
byby =(1. '-' baba) 'ET' (1. '-' bobo) 'ET' (1. '-' boba) ;

'OPTION' SORT 'testfer_MAILLAGE.asc' ;
'SORTIR'  'FER' S200  ;

'OPTION' SORT 'testfer_CHPOINT.asc' ;
'SORTIR' 'FER' S200  bubu  RES1 'TITR' 'Chpoints test' 'TEMP' 1. ;

'SORTIR' 'FER'  bubu RES1 S200 'TITR' 'Chpoints test' 'TEMP' 1. ;
'SORTIR' 'FER' S200  byby RES1 'SUIT' 'TEMP' 2.  ;
'SORTIR' 'FER' S200 byby  RES1 'SUIT' 2 'TEMP' 2. ;


FIN;






