* fichier :  ccar2.dgibi
************************************************************************
* Section : Fluides Transitoire
************************************************************************
**
**  --- 10 OCTOBRE 1997 ---
**
**   TEST CAVITE CARRE
**
**   teste NS(SUPG) DUDW en IMPL
**   Formulation MACRO P0 et P1

GRAPH = 'N'                   ;
COMPLET = FAUX ;

SI ( COMPLET ) ;
ds1=0.02 ; ds2=0.1 ;
*ds1=0.02 ; ds2=0.2 ;
SINON        ;
err1=5.e-5 ;
ds1=0.04 ; ds2=0.3 ;
FINSI ;



 DEBPROC TEST KPRESS*MOT TYPK*MOT MACRO*MOT GRAPH*MOT ;
 option dime 2 elem TYPK ;

 p1= 0 0 ; p12=0.5 0. ; p2= 1 0 ;

 ab=p1 d dini ds1 dfin ds2 p12 d dini ds2 dfin ds1 p2 ;
 mt= ab trans dini ds1 dfin ds2 (0 0.5) trans dini ds2
 dfin ds1 (0 0.5) ;
 bc=cote 2 mt ; cd=cote 3 mt ; da=cote 4 mt ;
 AA=bc moin (0.5 0.) ;
 BB=ab plus (0. 0.5) ;
 elim (AA et BB et mt) 1.e-3 ;
*trace mt ;
 mt= chan mt quaf ;
 $mt=mode mt 'NAVIER_STOKES' MACRO ;
 doma $mt 'IMPR';

EPSS=1.e-8;
MU  =1. ;
RO= 400. ;
DT=1. ;

   mess ' avt eqex ' ;
   RV= eqex $mt 'OMEGA' 0.8 'NITER' 5
  'OPTI' 'EF' 'IMPL' 'SUPG' 'NODIV' KPRESS
  ZONE  $mt       OPER    DUDW EPSS       INCO 'UN'
  ZONE  $mt       OPER    NS 1. 'UN'  (MU/RO)    INCO 'UN'
  CLIM
  UN UIMP CD   1. UN VIMP CD 0.  UN UIMP DA   0. UN VIMP DA 0.
  UN UIMP AB   0. UN VIMP AB 0.  UN UIMP BC   0. UN VIMP BC 0. ;

rv.inco= table inco ;
rv.inco.un =  kcht $mt  vect sommet (1.e-5 1.e-5) ;
rv.inco.pres =  kcht $mt  scal KPRESS 1.e-5  ;

 exec rv ;

 AA = chan AA quaf ;
 BB = chan BB quaf ;
 $AA=mode AA 'NAVIER_STOKES' QUAF;
 $BB=mode BB 'NAVIER_STOKES' QUAF;

srti=doma $AA 'MAILLAGE'        ;
srth=doma $BB 'MAILLAGE'        ;
evolV = EVOL 'CHPO' (rv.'INCO'.'UN') UX (srti  ) ;
evolH = EVOL 'CHPO' (rv.'INCO'.'UN') UY (srth  ) ;
evx=extr evolV 'ORDO' ;
 list evx ;
evy=extr evolV 'ABSC' ;
evolV= evol 'MANU' 'Vitesse' evx 'Hauteur' evy ;
rv.'EVOLV'=evolV ;
rv.'EVOLH'=evolH ;

si ('EGA' graph 'O' );
TAB1=TABLE;
TAB1.'TITRE'=TABLE ;
TAB1 . 1 ='MARQ      REGU '     ;
TAB1.'TITRE' . 1 = mot 'Composante_UX '     ;
DESS evolV 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;

TAB1.'TITRE' . 1 = mot 'Composante_UY '     ;
DESS evolH 'TITX' 'R (m)' 'TITY' 'V (m/s)' LEGE  TAB1 ;
 c1=vect (rv.inco.'UN') 0.3 ux uy jaune ;
 trace c1 mt ;

finsi ;

FINPROC RV ;

 RV= test 'CENTREP0' 'QUA8' 'MACRO' GRAPH ;
 evr= (rv.'EVOLV');
 evv= evr;
 evh= (rv.'EVOLH');
SI ( NON COMPLET ) ;

lrr=prog
 3.20000E-09 -1.95670E-02 -3.46624E-02 -5.44712E-02 -7.11346E-02
 -9.14791E-02 -9.77563E-02 -9.34327E-02 -6.91673E-02 -2.08744E-02
1.09297E-03  1.96608E-02  8.27457E-02   .18111       .42771
.67715       .99968 ;

 lr=extr evr 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP0 QUA8 MACRO' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;

 RV= test 'CENTREP1' 'QUA8' 'MACRO' GRAPH ;
 evr= (rv.'EVOLV');
 evv= evv et evr;
 evh= evh et (rv.'EVOLH');
FINSI ;

SI ( NON COMPLET ) ;
lrr=prog
  3.20000E-09 -2.22194E-02 -4.22438E-02 -7.27231E-02  -.10111
  -.14861      -.19646      -.18980     -8.80570E-02 -1.02128E-02
  7.89333E-02   .13149       .19107       .27450       .49017
 .72000       .99968 ;
 lr=extr evr 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 QUA8 MACRO' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP0' 'TRI6' 'MACRO' GRAPH ;
 evr= (rv.'EVOLV');
 evv= evv et evr;
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
 3.20000E-09 -1.69001E-02 -3.03568E-02 -5.15530E-02 -6.80733E-02
 -9.49562E-02  -.11348      -.10127     -8.38768E-02 -2.73698E-02
 -4.41043E-04  3.39423E-02  8.03398E-02   .17378       .41718
 .67718       .99968;

 lr=extr evr 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP0 TRI6 MACRO' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;

 RV= test 'CENTREP1' 'TRI6' 'MACRO' GRAPH ;
 evr= (rv.'EVOLV');
 evv= evv et evr;
 evh= evh et (rv.'EVOLH');
SI ( NON COMPLET ) ;
lrr=prog
 3.20000E-09 -3.20068E-02 -5.41819E-02 -9.38147E-02  -.11829
 -.16305      -.18968      -.15340     -8.29263E-02  1.76002E-03
  .10284       .10603       .16072       .24019       .45912
  .68188       .99968 ;
 lr=extr evr 'ABSC' ;
 ER=SOMM( abs (lr - lrr) ) ;
 mess ' Ecart sur CENTREP1 TRI6 MACRO' er ;
* si ( er > err1 ) ; erreur 5 ; finsi ;
FINSI ;


si ('EGA' graph 'O' );
titre 'Coupe oy à x=1/2 ' ;
TAB1=TABLE;
TAB1.'TITRE'= table ;
TAB1 . 1 ='TIRR MARQ CROI REGU ';
TAB1.'TITRE' . 1 ='P0 iso-Q2';
TAB1 . 2 ='TIRM MARQ PLUS REGU ';
TAB1.'TITRE' . 2 ='P1 iso-Q2';
TAB1 . 3 ='TIRR MARQ LOSA REGU ';
TAB1.'TITRE' . 3 ='P0 iso-P2';
TAB1 . 4 ='TIRM MARQ CARR REGU ';
TAB1.'TITRE' . 4 ='P1 iso-P2';
DESS EVV 'TITX' 'Oy'    LEGE  TAB1 ;
titre 'Coupe ox à y=1/2 ' ;
DESS EVH 'TITX' 'Ox'    LEGE  TAB1 ;
FINSI ;


FIN ;





















