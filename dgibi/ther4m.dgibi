* fichier :  ther4.dgibi
************************************************************************
* Section : Thermique Convection
************************************************************************
GRAPH='N';
SAUT PAGE ;
*-------------------------------------------------------------------*
*                                                                   *
*   TEST DES CL DE TEMPERATURE POUR PY13 CU20 TE10 PR15 TRIDIM:     *
*   -----------------------------------------------------------     *
*                                                                   *
*               TEMPERATURE IMPOSEE + CONVECTION                    *
*                                   + FLUX                          *
*                                   + SOURCE                        *
*                                                                   *
* Cet exemple permet de tester les conditions aux limites de temp{- *
* rature pour des {lements massifs 3D quadratiques.                 *
*                                                                   *
* Un parall{l{pip}de rectangle constitu{ d'{l{ments PY13, CU20, TE10*
* et PR15 est soumis @ une temp{rature impos{e @ sa face sup{rieure *
* et successivement @ une convection forc{e, @ un flux impos{ et @  *
* une source volumique de chaleur.                                  *
*                                                                   *
* Les r{sultats sont pr{sent{s sous forme d'isovaleurs qui doivent  *
* etre parall}les aux faces sup{rieure et inf{rieure.               *
*                                                                   *
*                      21 DECEMBRE 1988                             *
*-------------------------------------------------------------------*
*
*---  DISPOSITION DES NOEUDS DES ELEMENTS:
*
*            A4__AF4___F4___DF4___D4
*            /         /          /|
*          AB4       EF4       CD4 D34
*          /         /          /  |   IN:CENTRE DE
*        B4___BE4___E4___CE4___C4  D3  (AN BN EN FN AN+1 BN+1 EN+1 FN+1)
*         |  6PY13  |   1CU20  |  /
*        B34       E34       C34 CD3   JN:CENTRE DE
*         |         |          |/      (DN CN EN FN DN+1 CN+1 EN+1 FN+1)
*        B3___BE3___E3___CE3___C3
*
*            A3__AF3___F3___DF3___D3
*            /         /          /|
*          AB3       EF3  DE3  CD3 D23
*          /         /          /  |
*        B3___BE3___E3___CE3___C3  D2
*         |  1CU20  | 4PY13    |  /
*        B23       E23   +   C23 CD2
*         |         |   4TE10  |/
*        B2___BE2___E2___CE2___C2
*
*            A2__AF2___F2___DF2___D2
*            /         /          /|
*          AB2       EF2  DE2  CD2 D12
*          /         /          /  |
*        B2___BE2___E2___CE2___C2  D1
*         |  6PY13  |   2PR15  |  /
*        B12       E12       C12 CD1
*         |         |          |/
*        B1___BE1___E1___CE1___C1
*
*
*------------------------------------------------------------------------
*---  OPTIONS GENERALES DE CALCUL:
*
TITRE 'THER4 - C.L. TEMPERATURE - ELEMENTS QUADRATIQUES' ;
OPTION  ECHO 0 ;
OPTION DIME 3 ;
TEMPS ;
*
*---  CREATION DE LA GEOMETRIE: POINTS SUPPORTS DES ELEMENTS
*
  A1=0.  0.   0.;  B1=1.  0.   0.;  C1=1.  4.   0.;  D1=0. 4.   0.;
  A2=0.  0.   2.;  B2=1.  0.   2.;  C2=1.  4.   2.;  D2=0. 4.   2.;
  A3=0.  0.   4.;  B3=1.  0.   4.;  C3=1.  4.   4.;  D3=0. 4.   4.;
  A4=0.  0.   6.;  B4=1.  0.   6.;  C4=1.  4.   6.;  D4=0. 4.   6.;
  E1=1.  2.   0.;  F1=0.  2.   0.;  E2=1.  2.   2.;  F2=0. 2.   2.;
  E3=1.  2.   4.;  F3=0.  2.   4.;  E4=1.  2.   6.;  F4=0. 2.   6.;
  I1= .5 1.   1.;  J2= .5 3.   3.;  I3= .5 1.   5.;
 A12=0.  0.   1.; B12=1.  0.   1.; C12=1.  4.   1.; D12=0. 4.   1.;
 A23=0.  0.   3.; B23=1.  0.   3.; C23=1.  4.   3.; D23=0  4.   3.;
 A34=0.  0.   5.; B34=1.  0.   5.; C34=1.  4.   5.; D34=0. 4.   5.;
 E12=1.  2.   1.; F12=0.  2.   1.; E23=1.  2.   3.; F23=0. 2.   3.;
 E34=1.  2.   5.; F34=0.  2.   5.;
 AB1= .5 0.   0.; AB2= .5 0.   2.; AB3= .5 0.   4.; AB4= .5 0.   6.;
 BE1=1.  1.   0.; BE2=1.  1.   2.; BE3=1.  1.   4.; BE4=1.  1.   6.;
 CE1=1.  3.   0.; CE2=1.  3.   2.; CE3=1.  3.   4.; CE4=1.  3.   6.;
 CD1= .5 4.   0.; CD2= .5 4.   2.; CD3= .5 4.   4.; CD4= .5 4.   6.;
 DF1=0.  3.   0.; DF2=0.  3.   2.; DF3=0.  3.   4.; DF4=0.  3.   6.;
 AF1=0.  1.   0.; AF2=0.  1.   2.; AF3=0.  1.   4.; AF4=0.  1.   6.;
 EF1= .5 2.   0.; EF2= .5 2.   2.; EF3= .5 2.   4.; EF4= .5 2.   6.;
 MEC1= 1. 3. 1. ; MFD1= 0. 3. 1. ;
*
*---  MILIEUX DES SEGMENTS (XN IN) ET (XN JN)
*
AI1= .25  .5  .5; BI1= .75  .5  .5; EI1= .75 1.5  .5; FI1= .25 1.5  .5;
EJ2= .75 2.5 2.5; FJ2= .25 2.5 2.5; DJ2= .25 3.5 2.5; CJ2= .75 3.5 2.5;
AI3= .25  .5 4.5; BI3= .75  .5 4.5; EI3= .75 1.5 4.5; FI3= .25 1.5 4.5;
*
*---  MILIEUX DES SEGMENTS (XN+1 IN) ET (XN+1 JN)
*
A2I1=.25  .5 1.5; B2I1=.75  .5 1.5; E2I1=.75 1.5 1.5; F2I1=.25 1.5 1.5;
E3J2=.75 2.5 3.5; F3J2=.25 2.5 3.5; D3J2=.25 3.5 3.5; C3J2=.75 3.5 3.5;
A4I3=.25  .5 5.5; B4I3=.75  .5 5.5; E4I3=.75 1.5 5.5; F4I3=.25 1.5 5.5;
DE2 =.5  3.  2. ; DE3 =.5  3.  4. ;
*
*---  CREATION DES PYRAMIDES A 13 NOEUDS:
*
PP01 = MANU PY13
 A1   AB1  B1   BE1  E1   EF1  F1   AF1  AI1  BI1  EI1  FI1  I1 ;
PP02 = MANU PY13
 E1   EF1  F1   F12  F2   EF2  E2   E12  EI1  FI1  F2I1 E2I1 I1 ;
PP03 = MANU PY13
 E2   EF2  F2   AF2  A2   AB2  B2   BE2  E2I1 F2I1 A2I1 B2I1 I1 ;
PP04 = MANU PY13
 A2   AB2  B2   B12  B1   AB1  A1   A12  A2I1 B2I1 BI1  AI1  I1 ;
PP05 = MANU PY13
 B1   BE1  E1   E12  E2   BE2  B2   B12  BI1  EI1  E2I1 B2I1 I1 ;
PP06 = MANU PY13
 A1   AF1  F1   F12  F2   AF2  A2   A12  AI1  FI1  F2I1 A2I1 I1 ;
PP07 = MANU PY13
 E2   EF2  F2   F23  F3   EF3  E3   E23  EJ2  FJ2  F3J2 E3J2 J2 ;
PP08 = MANU PY13
 D3   CD3  C3   C23  C2   CD2  D2   D23  D3J2 C3J2 CJ2  DJ2  J2 ;
PP09 = MANU PY13
 C2   CE2  E2   E23  E3   CE3  C3   C23  CJ2  EJ2  E3J2 C3J2 J2 ;
PP10 = MANU PY13
 D2   DF2  F2   F23  F3   DF3  D3   D23  DJ2  FJ2  F3J2 D3J2 J2 ;
PP11 = MANU PY13
 A3   AB3  B3   BE3  E3   EF3  F3   AF3  AI3  BI3  EI3  FI3  I3 ;
PP12 = MANU PY13
 E3   EF3  F3   F34  F4   EF4  E4   E34  EI3  FI3  F4I3 E4I3 I3 ;
PP13 = MANU PY13
 E4   EF4  F4   AF4  A4   AB4  B4   BE4  E4I3 F4I3 A4I3 B4I3 I3 ;
PP14 = MANU PY13
 A4   AB4  B4   B34  B3   AB3  A3   A34  A4I3 B4I3 BI3  AI3  I3 ;
PP15 = MANU PY13
 B3   BE3  E3   E34  E4   BE4  B4   B34  BI3  EI3  E4I3 B4I3 I3 ;
PP16 = MANU PY13
 A3   AF3  F3   F34  F4   AF4  A4   A34  AI3  FI3  F4I3 A4I3 I3 ;
*
*---  VOLUME COMPOSE DES 16 PYRAMIDES A 13 NOEUDS:
*
VOL1 = PP01 ET PP02 ET PP03 ET PP04 ET PP05 ET PP06 ET PP07 ET PP08
    ET PP09 ET PP10 ET PP11 ET PP12 ET PP13 ET PP14 ET PP15 ET PP16 ;
*
*---  CREATION DES TETRAEDRES A 10 NOEUDS:
*
TT01 = MANU TE10
 D2   CD2  C2   CE2  E2   DE2  DJ2  CJ2  EJ2  J2 ;
TT02 = MANU TE10
 D2   DF2  F2   EF2  E2   DE2  DJ2  FJ2  EJ2  J2 ;
TT03 = MANU TE10
 D3   CD3  C3   CE3  E3   DE3  D3J2 C3J2 E3J2 J2 ;
TT04 = MANU TE10
 D3   DF3  F3   EF3  E3   DE3  D3J2 F3J2 E3J2 J2 ;
*
*---  VOLUME COMPOSE DES 4 TETRAEDRES A 10 NOEUDS:
*
VOL2 = TT01 ET TT02 ET TT03 ET TT04 ;
*
*---  CREATION DES PRISMES A 15 NOEUDS:
*
PM01 = MANU PR15 E2 E12 E1 MEC1 C2 CE2 EF2 EF1 CD2
                 F2 F12 F1 MFD1 D2 DF2 ;
PM02 = MANU PR15 E1 CE1 C1 C12 C2 MEC1 EF1 CD1 CD2
                 F1 DF1 D1 D12 D2 MFD1 ;
*
*---  VOLUME COMPOSE DES 2 PRISMES A 15 NOEUDS:
*
VOL4 = PM01 ET PM02 ;
*
*---  CREATION DES CUBES A 20 NOEUDS:
*
CB02 = MANU CU20
E2 BE2 B2 AB2 A2 AF2 F2 EF2 E23 B23 A23 F23 E3 BE3 B3 AB3
A3 AF3 F3 EF3 ;
CB03 = MANU CU20
E3 CE3 C3 CD3 D3 DF3 F3 EF3 E34 C34 D34 F34 E4 CE4 C4 CD4
D4 DF4 F4 EF4 ;
*
*--- VOLUME COMPOSE DES 2 CUBES A 20 NOEUDS:
*
VOL3 = CB02 ET CB03 ;
*
*--- REUNION DES VOLUMES ELEMENTAIRES:
*
VOLTOT = VOL1 ET VOL2 ET VOL3 ET VOL4 ;
*
VOLSOU = PP11 ET PP12 ET PP13 ET PP14 ET PP15 ET PP16 ET CB03 ;
*
*---  SURFACE INFERIEURE:
*
S_INF1 = MANU QUA8 A1 AB1 B1 BE1 E1 EF1 F1 AF1 ;
S_INF2 = MANU QUA8 C1 CD1 D1 DF1 F1 EF1 E1 CE1 ;
S_INF = S_INF1 ET S_INF2 ;
*
*---  SURFACE SUPERIEURE:
*
S_SUP1 = MANU QUA8 A4 AB4 B4 BE4 E4 EF4 F4 AF4 ;
S_SUP2 = MANU QUA8 C4 CD4 D4 DF4 F4 EF4 E4 CE4 ;
S_SUP = S_SUP1 ET S_SUP2 ;
OEIL1 = 15 12 10 ;
SI(NEG GRAPH 'N');
     TRAC OEIL1 VOLTOT ;
FINSI;
*
*---  DONNEES DU PROBLEME DE THERMIQUE:
*
*---  MODELISATION:
*
MOD1 = MODE VOLTOT THERMIQUE ISOTROPE ;
MOD2 = MODE S_SUP thermique  CONVECTION ;
*
*---  DONNEE DES CARACTERISTIQUES DU MATERIAU:
*
MAT1 = MATE MOD1 'K' 100. ;
MAT2 = MATE MOD2 'H' 15. ;
*
*---  CREATION DES MATRICES DE CONDUCTIVITE:
*
CND1 = CONDUCTIVITE MOD1 MAT1 ;
CND2 = CONDUCTIVITE MOD2 MAT2 ;
*
*---  TEMPERATURES IMPOSEES: BLOQUE + DEPI
*
BB1 = BLOQUE S_INF 'T' ;
EE1 = DEPI BB1 10. ;
*
*---  FLUX EQUIVALENTS A LA CONDITION DE CONVECTION:
*
CNV1 = CONVECTION MOD2 MAT2 'T' 200. ;
list cnv1;

*
*---  FLUX IMPOSE:
*
FLU1 = FLUX MOD1 20. S_SUP ;
*
*---  SOURCE VOLUMIQUE:
*
S1 = SOURCE MOD1 100. VOLSOU ;
*
*---  ASSEMBLAGE DES TERMES DE CONDUCTIVITE:
*
CCC1 = CND1 ET CND2 ET BB1 ;CCC2 = CND1 ET BB1 ;
*
*---  ASSEMBLAGE DES TERMES DE FLUX EQUIVALENTS:
*
FFF1 = EE1 ET CNV1 ;FFF2 = EE1 ET FLU1 ;FFF3 = EE1 ET S1 ;
*
*---  1ERE RESOLUTION: TEMPERATURE IMPOSEE + CONVECTION
*
CHTER1 = RESOUDRE CCC1 FFF1 ;
*
*---  2EME RESOLUTION: TEMPERATURE IMPOSEE + FLUX
*
CHTER2 = RESOUDRE CCC2 FFF2 ;
*
*---  3EME RESOLUTION: TEMPERATURE IMPOSEE + SOURCE
*
CHTER3 = RESOUDRE CCC2 FFF3 ;
*
* REMARQUE : LE CHAMP DE TEMPERATURE EST SENSIBLE A LA REPARTITION
* DES POINTS D'INTEGRATION DANS LA STRUCTURE.
*
*---  POST-TRAITEMENT: TRACE DU MAILLAGE ET DES CHAMPS RESULTATS
*
OEIL = 100. 100. 100. ;
TITRE 'ELEMENTS PY13 CU20 TE10 PR15 : MAILLAGE' ;
SI(NEG GRAPH 'N');
    TRACER OEIL VOLTOT ;
FINSI;
OEIL = 100. 0. 0. ;
TITRE 'ELEMENTS PY13 CU20 TE10 PR15 : CONVECTION' ;
SI(NEG GRAPH 'N');
    TRACER OEIL VOLTOT CHTER1 ;
FINSI;
TITRE 'ELEMENTS PY13 CU20 TE10 PR15 : FLUX' ;
SI(NEG GRAPH 'N');
    TRACER OEIL VOLTOT CHTER2 ;
FINSI;
TITRE 'ELEMENTS PY13 CU20 TE10 PR15 : SOURCE' ;
SI(NEG GRAPH 'N');
    TRACER OEIL VOLTOT CHTER3 ;
FINSI;
*
T1 = EXTR CHTER1 T C4 ;
T2 = EXTR CHTER2 T D4 ;
T3 = EXTR CHTER3 T D4 ;
SAUT PAGE ;
SAUT 2 LIGN ;
MESS ' TEMPERATURE MAXIMUM              DE REFERENCE        CALCULEE ';
MESS 'CONVECTION                             102.14           ' T1 ;
MESS 'FLUX                                   11.243           ' T2 ;
MESS 'SOURCE                                 20.432           ' T3 ;
TEMPS ;
*      CODE DE FONCTIONNEMENT SUR LA TEMP. MAX. EN SOURCE IMPOSEE
TREF=20.4;RESI=ABS((T3-TREF)/TREF);
SI(RESI <EG 5E-2);
  ERRE  0;
SINO;
  ERRE  5;
FINSI;
FIN;







