* fichier : ricjoi_2d.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*******************************************************
*  Cas test de l'implantation numérique du modele
*  RICJOI 2D LOCAL
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
* Benjamin.Richard@lmt.ens-cachan.fr
*******************************************************
* Les cas de charges sont entrés :
* - 1 : Essai cyclique mono cadrant
* - 2 : Essai cyclique bi cadrant (cisaillement)
* - 3 : Essai effet unilatteral
* - 4 : Essai frottement
* - 5 : Essai variations stiffness
* - 6 : Essai compression normale avec rouille monotone
*******************************************************
* Choix du cas de charge
ncas  = 1;
graph= MOT 'N';

*
*-------------- Options de calcul ---------------------
*
OPTION DIME 2 ELEM SEG2 MODE PLAN DEFO ;

*
*-------------- Definition de la geometrie ------------
*
A = 0.00 0.00 ;
B = 1.00 0.00;
C = 0.00 0.00 ;
D = 1.00 0.00 ;

AB =  A DROIT 1 B ;
CD =  C DROIT 1 D ;

OPTION ELEM RAC2 ;

JOINT = RACC 0.001 CD AB ;

*
*------- Définition des conditions aux limites --------
*----------- et des déplacements imposés --------------
*
CL1 = BLOQ AB UX ;
CL2 = BLOQ AB UY ;

SI (OU (EGA ncas 3) (EGA ncas 6));
CL3 = BLOQ CD UY ;
SINON;
CL3 = BLOQ CD UX ;
FINSI;

CLJOINT =  CL1 ET CL2 ET CL3 ;

*
*----- Définition du modèle ---------------------------
*
MODJOI = MODE JOINT MECANIQUE ELASTIQUE PLASTIQUE INTJOI JOI2 ;

*
*----- Paramètres matériaux ---------------------------
*

* Rigidite normale
knn = (70000E+6);

* Rigidite tangentielle
ksn = (knn/2.4);

* Coefficient de dilatation
alpnn = 1.0E-10;

* Fragilite
adn = 1.1E-5;

* Seuil initial en energie pour l'endommagement
y0n = 50;

* Coefficient de dissymétrie entre modes
alpan = 6;

* Coefficient d'écrouissage 1
gainn = 2.0E9;

* Coefficient d'écrouissage 2
aainn = 5.0E-7;

* Coefficient de Gurson modifié 1
q1con = 3.5;

* Coefficient de Gurson modifié 2
q2con = 0.9;

* Coefficient de Gurson modifié 3
q3con = 0.1;

* Contrainte de raidissement seuil
sycon = -1.0E6;

* Coefficient loi de contraction 1
ncoen = 2.0;

* Coefficient loi de contraction 2
kcoen = 1.0E+10;

* Degré de corrosion (en perte de section)
SI (EGA ncas 6);
tcn = 1.0E-10;
SINON;
tcn = 0.0;
FINSI;

* Indicateur de phase de calcul
gonfn = 0;

MAJOI = MATE MODJOI KS ksn KN knn  ALPN alpnn
         AD    adn Y0 y0n ALPA alpan
         GAIN  gainn AAIN aainn
         Q1CO  q1con Q2CO q2con Q3CO q3con
         SYCO sycon NCOE ncoen KCOE kcoen
         TC tcn GONF gonfn;

*
*----- Définition des cas de charge -------------------
*

*
* ----------            DEFINITION DU CHARGEMENT       ----------
*
*
* ----------   DEFINITION DE LA PROGRESSION DU CHARGEMENT ----------
*

* Essai cyclique mono cadrant
SI (EGA ncas 1);
  LI1  = PROG 0. 1.      2.     3.      ;
  LI2  = PROG 0. 5.0E-3 0.0E0   10.0E-3 ;
  LIS1 = PROG 0.0 PAS 0.01 3.0;
FINSI;

* Essai cyclique bi cadrant (cisaillement)
SI (EGA ncas 2);
  LI1  = PROG 0. 1. 2. 3. 4. 5. 6. 7. 8. 9;
  LI2  = PROG 0. -3.0E-3 5.0E-3 -7.0E-3 9.0E-3 -11.0E-3 13.0E-3
             -15.0E-3 17E-3 0.0;
  LIS1 = PROG 0.0 PAS 0.01 9.0;
FINSI;

* Essai effet unilatteral
SI (EGA ncas 3);
  LI1  = PROG 0. 1. 2. 3.;
  LI2  = PROG 0. 5.0E-3 -1.0E-3 10E-3;
  LIS1 = PROG 0. PAS 0.01 3.0;
FINSI;

* Essai frottement
SI (EGA ncas 4);
  LI1  = PROG 0. pas 1. 7.;
  LI2  = PROG 0. 5.0E-3 0.0E-3 6e-3 0.0e-3 8e-3 0.0e-3 10E-3;
  LIS1 = PROG 0. PAS 0.01 7.0;
FINSI;

* Essai variations stiffness
SI (EGA ncas 5);
  LI1  = PROG 0. pas 1. 1.;
  LI2  = PROG 0. 10.0E-3 ;
  LIS1 = PROG 0. PAS 0.01 1.0;
FINSI;

* Essai compression normale avec rouille monotone
SI (EGA ncas 6);
  LI1  = PROG 0. pas 1. 1.;
  LI2  = PROG 0. -1.0E-4 ;
  LIS1 = PROG 0. PAS 0.01 1;
FINSI;

  RIG1 = DEPI CL3 1.0 ;
  EV1  = EVOL MANU LI1 LI2;
  CAR1 = CHAR 'DIMP' RIG1 EV1 ;

*
*----------- Calcul par l'operateur PASAPAS ------------
*
   TAB                       = TABLE ;
   TAB.'MOVA'                = 'DINT';
   TAB.'BLOCAGES_MECANIQUES' = CLJOINT;
   TAB.'CARACTERISTIQUES'    = MAJOI;
   TAB.'MODELE'              = MODJOI;
   TAB.'TEMPS_CALCULES'      = LIS1;
   TAB.'CHARGEMENT'          = CAR1;

   PASAPAS TAB ;
*
* ---------- POST TRAITEMENT ----------
*

*
*----------- Courbe effort-deplacement -----------------
*
 dep0 = TAB.'DEPLACEMENTS';
 tim0 = tab. TEMPS;
 sig0 = TAB.'CONTRAINTES';
 var0 = TAB.'VARIABLES_INTERNES';
 ind0 = INDE dep0;
 time = PROG (tim0 . 0);

 uzp0  = PROG 0.;
 fzp0  = PROG 0.;

 ep1 = prog 0.;
 si1 = prog 0.;
 nn = (('DIME' ind0) - 1);

 j = 1;

 REPE lab1 (('DIME' ind0) - 1);
   j = j + 1;
   indj  = ind0.j;
   depj  = dep0.indj;
   sigj  = sig0.indj;
   epsj  = epsi depj modjoi majoi;
   vecj  = bsigma sigj modjoi majoi;
   time  = time et (prog tim0. &lab1);

   SI (EGA ncas 6);
   uzp0  = uzp0 ET (PROG (EXTR (exco depj uy) scal (NOEUD 3)));
   fzp0  = fzp0 ET (PROG (EXTR (exco vecj fy) scal (NOEUD 2)));
   FINSI;

   SI (EGA ncas 3);
   uzp0  = uzp0 ET (PROG (EXTR depj 'UY' C));
   fzp0  = fzp0 ET (PROG (EXTR vecj 'FY' A));
   FINSI;

   SI (OU (OU (OU (EGA ncas 1) (EGA ncas 2))
                  (EGA ncas 3)) (EGA ncas 4));
   uzp0  = uzp0 ET (PROG (EXTR depj 'UX' C));
   fzp0  = fzp0 ET (PROG (EXTR vecj 'FX' A));
   FINSI;

 FIN lab1;
  aa = extr fzp0 ( dime fzp0);

  err =  abs (aa +  9.32668E+06) / 9.32668E+06;
  message ' erreur relative ' err;
  si (err > 1.e-3);
    erreur (5);
     finsi;
 EV2 = EVOL MANU uzp0 (-1.0*fzp0);
 si ( ega graph 'O');
 DESS EV2;
 finsi;
 FIN ;









