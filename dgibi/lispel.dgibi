* fichier :  lispel.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTION ECHO 0;
GRAPH = 'N';
SAUT PAGE;
*_________________________________________________________
*
*  PLAQUE AVEC FISSURE SEMI-ELLIPTIQUE DEBOUCHANTE
*             a/c = a/t = 0.2
*
*  Comparaison des valeurs du facteur d'intensite de contrainte
*  avec la solution de Raju-Newman tirée de RCC-MR A16 (dmt94-043)
*
*  Dimensions plaque : largeur=longueur=160mm , epai 20mm
*  Dimensions fissure : a=4mm  c=20mm
*  Chargement : contrainte de membrane uniforme SIG=1MPa
*_________________________________________________________
*
* Materiau:
xYoung = 210000.;
*____________________________________________
*
*  MAILLAGE DE LA PLAQUE
*____________________________________________
*
OPTION DIME 3 ELEM qua4;

* Geometrie:
larg1 = 80.;long1 = 80.;
ww  = 20.;flength = 20.0; fdepth  =  4.0;

* Nombre d'elements:
*nb1 = 10;nb2 = 15;nb3 = 16;
nb = 15 ; nb1 = enti (nb / 2.) ;

dens (flength / (10. * nb1));
p6  = 0.    flength 0.;

dens (flength / nb1);
p0  = 0.    0.   0.;

dens (flength / (0.75 * nb1));
p4  = (flength / 2)  0.    0.;
p8  = (flength / 2) flength 0.;

dens (flength / (0.2 * nb1));
P5  = long1        0.    0.;
p7  = 0.           larg1   0.;
p9  = long1        larg1   0.;

d1 = DROI (-1 * nb1) p0 p4;
d2 = DROI (-1 * nb) p6 p0;
d3 = DROI (-1 * nb1) p8 p6;
d4 = DROI (-1 * nb) p4 p8;
d5 = DROI (-1 * nb) p4 p5;
d6 = DROI (-1 * nb) p8 p9;
d7 = DROI (-1 * nb) p6 p7;
d8 = DROI (-1 * nb) p5 p9;
d9 = DROI (-1 * nb1) p9 p7;

su1 = DALL d1 d2 d3 d4 PLAN;
su2 = DALL d5 d8 (INVE d6) (INVE d4) PLAN;
su3 = DALL d6 d9 (INVE d7) (INVE d3) PLAN;
su6 = su1 ET su2 ET su3;

d8a d2a d7a su8 = d8 d2 d7 su6 SYME
                PLAN (0. 0. 0.) (0. 1. 0.) (0. 0. 1.);
p06 = P6 SYME PLAN (0. 0. 0.) (0. 1. 0.) (0. 0. 1.);

su7 = su6 ET su8;

d7ap = chan poi1 d7a;ep06 = manu poi1 p06;
ELIM .0001 d7ap ep06;
ELIM .0001 su7 (d7ap diff ep06);

su9 = ORIENT su7 DIRECTION (0. 0. 10.);

*_____________________________________________________________________
*
*--- modele et caracteristiques du materiau, coque
mod1 =MODE su9 MECANIQUE ELASTIQUE coq4;
mat1 =MATE mod1 YOUN xYoung NU .3 EPAI ww;

*_____________________________________________________________________
*
*               MAILLAGE DE LA FISSURE
*_____________________________________________________________________
*
*--- modele et caracteristiques du materiau, fissure

d2    = INVE d2; d2a   = INVE d2a;

ii = 0;REPETER matFiss (NBEL d2);ii = ii+1;

  pp1a = d2  POIN ii;
  pp2a = d2  POIN (ii+1);
  pp1b = d2a POIN ii;
  pp2b = d2a POIN (ii+1);
  elemA = d2  ELEM COMP pp1a pp2a;
  elemB = d2a ELEM COMP pp1b pp2b;
  fiss0 = RACC 0.0001 (INVE elemA) elemB;
  xx1 = COOR 2 pp1a;
  xx2 = COOR 2 pp2a;
  yy1 = fdepth * ( (1. - ((xx1 / flength)**2.))**.5 );
  yy2 = fdepth * ( (1. - ((xx2 / flength)**2.))**.5 );
  aai = ( yy1 + yy2 ) / 2.;

  mod0 = MODE fiss0 MECANIQUE ELASTIQUE LISP;
  mat0 = MATE mod0 YOUN xYoung NU .3  EPAI ww FISS aai
         VX 0.  VY 0.  VZ -1.;
*
  SI (ii EGA 1);
     mod2  = mod0;
     mat2  = mat0;
     fiss1 = fiss0;
   SINON;
     mod2  = mod2 ET mod0;
     mat2  = mat2 ET mat0;
     fiss1 = fiss1 ET fiss0;
  FINSI;
*
FIN matFiss;
*

*  Assemblage des 2 maillages
surf1 = su9 ET fiss1;
SI ( NEG GRAPH 'N' );trac surf1 (0. 0. 1.E6); FINSI;

*_____________________________________________________________________
*
*--- conditions aux limites
*
enc1 = SYMT DEPL ROTA (0. 0. 0.) (1. 0. 0.) (0. 0. 1.) surf1 0.0001;
enc2 = BLOQ d8a UX UZ RX ;
encT = enc1 ET enc2;
*_____________________________________________________________________
*
mod = mod1 et mod2 ;
mat = mat1 et mat2 ;
*__________________________________________________________
*
*--- chargement : contrainte de membrane en traction
*
SIG = 1.;
effortN = larg1 * ww * SIG;
ff = ((nb + 1.)/ nb) * effortN;
f1 = ff 0. 0.;
f2 = (ff / (-2. * (nb + 1.))) 0. 0.;

char1 = (FORC f1 d8) et (FORC f2 P5) et (FORC f2 P9);

*__________________________________________________________
*
* --- calcul rigidite
*
rig0 = (RELA 1. UX p06 - 1. UX p6) ET (RELA 1. UY p06 - 1. UY p6)
        ET (RELA 1. UZ p06 - 1. UZ p6) ET (RELA 1. RX p06 - 1. RX p6)
        ET (RELA 1. RY p06 - 1. RY p6) ET (RELA 1. RZ p06 - 1. RZ p6);
*
rig1 = (RIGI mod1 mat1) ET rig0;
rig2 = (RIGI mod2 mat2);
*__________________________________________
*
* --- Resolution:
*
* Force:
rig = rig1 et rig2 et encT;
re1  = RESO rig char1;

*_____________________________________________________________________
*
* ---  FACTEUR D'INTENSITE DE CONTRAINTE
*  détermination du facteur F = K / (SIG * (pi * a / Q)^0.5)
*

* ---  valeurs calculées
sigf = SIGM mod2 mat2 re1 ;

pKcal = prog (nbel fiss1) * 0.;py1 = prog (nbel fiss1) * 0.;
i=0; repe bbb (nbel fiss1) ; i = i + 1;
  elemi = elem fiss1 i;
  sigi = redu sigf elemi;
  vki = extr sigi 'KI' 1 1 2;remp pKcal i vki;
  yi = (coor 2 (bary elemi));remp py1 i yi;
fin bbb;

* ---  valeurs de Raju-Newman
*

a = fdepth ; t = ww ; c = flength ;
b = 4 * c;ac = a/c; at = a/t;
Q  = (1.0) + (1.464*((ac)**1.65));
K0 = SIG * ((pi * a / Q) ** 0.5);
pFrn = prog 1.178 1.171 1.151 1.117 1.07 1.01
            0.938 0.851 0.749 0.64 0.587;
pKrn = pFrn * K0;
pphin2 = prog 1. pas -0.1 0.;
py2= (cos (pphin2 * 90.)) * c;

* ---  COMPARAISON
*  écart maxi en excluant les 3 elts près de la surface libre

nn=nbel fiss1; pecart = prog (nn - 3) * 0.;

i=0 ; repe bbb (nn - 3); i = i + 1;
vKcal = extr pKcal i;
vy1 = extr py1 i;
vKrn = ipol vy1 py2 pKrn;
vecart= (vKcal - vKrn) / vKrn; vecart = abs vecart;
remp pecart i vecart;
fin bbb;

ERR = maxi pecart;
SI (ERR <  3.4);ERRE 0;SINO;ERRE 5;FINSI;

SI ( NEG GRAPH 'N' );
t=tabl;
t.1='MARQ CARR ';
t.2='MARQ ETOI ';
t.'TITRE'=tabl;
t.'TITRE'. 1 = mot 'RAJU-NEWMANN';
t.'TITRE'. 2 = mot 'LINESPRING';
evcal = evol manu 'y (mm)' py1 'K (MPam^0.5)' pKcal;
evrn = evol manu 'y (mm)' py2 'K (MPam^0.5)' pKrn;
titr
'K = f (Y) - plaque en tract. - defaut semi-ellip. a/c=a/t=0.2';
dess (evrn et evcal) lege t logo xbor 0. 20. ybor 0. 5.;
FINSI;
fin;







