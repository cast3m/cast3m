* fichier :  mistral_D3r.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*
*     Test mistral_D3r.dgibi: Jeux de donnees         *
*     ---------------------------------------         *
*                                                     *
  opti echo 0 ;
  opti dime 3 mode tridim elem cu20 ;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;

*
*******************************************************
*                TEST DE VALIDATION                   *
*                ------------------                   *
*                                                     *
*  MODELE MISTRAL                                     *
*    DILATATION THERMIQUE, ELASTICITE ET CROISSANCE   *
*                                                     *
*  MAILLAGE :                                         *
*    SECTEUR DE TUBE CYLINDRIQUE                      *
*                                                     *
*  CHARGEMENT :                                       *
*    CONTRAINTE AXIALE,                               *
*    TEMPERATURE ET FLUX NEUTRONIQUE                  *
*******************************************************
*
* Geometrie
*
* ------------------------------------------------------
  RI = 49.5E-3 ; RE = 50.5E-3 ; H = 0.010 ;
  TETA = 30. ; NMAILT = 2 ;
* ------------------------------------------------------
   RM = (RI+RE)/2. ;
   P11 = COS(TETA) ;         P12 = SIN(TETA) ; P13 = 0. ;
   P21 = (-1.)*(SIN(TETA)) ; P22 = COS(TETA) ; P23 = 0. ;
   P31 = 0.  ;               P32 = 0. ;        P33 = 1. ;
*
* Maillage
*
   PIB = RI 0. 0. ;  PEB = RE 0. 0. ;
   PIH = RI 0. H  ;  PEH = RE 0. H ;
   LB = droi 2 PIB PEB ;
   LE = droi 1 PEB PEH ;
   LH = droi 2 PEH PIH ;
   LI = droi 1 PIH PIB ;
   PLR0Z = dall LB LE LH LI plan ;
*
   POMB = RM 0. 0. ; POMH = RM 0. H ;
   PMB = PLR0Z poin proc POMB ;
   PMH = PLR0Z poin proc POMH ;
*
*   trac PLR0Z ;
*
* ------------------
  CRITELIM = 1.E-5 ;
* ------------------
   PAXB = 0. 0. 0. ; PAXH = 0. 0. H ;
   EPROU = PLR0Z volu NMAILT rota TETA PAXB PAXH ;
*
   PLR1Z = face 2 EPROU ;
   VECT1 = P21 P22 P23 ;
   FACE3 = face 3 EPROU ;
   POMB1 = (RM*P11) (RM*P12) 0. ;
   PMB1 = EPROU poin proc POMB1 ;
   POMH1 = (RM*P11) (RM*P12) H ;
   PMH1 = EPROU poin proc POMH1 ;
   PLRT0 = EPROU poin plan PIB PEB PMB1 CRITELIM ;
   PLRTHH = EPROU poin plan PIH PEH PMH1 CRITELIM ;
   PLRTH = FACE3 elem appu stri PLRTHH ;
*
*   trac EPROU ;
*
* Modele et materiau
*
   MOD3 = mode EPROU mecanique elastique orthotrope
                               viscoplastique mistral ;
* ------------------------------------------------------------
  SENSIP1 = -2 ; SENSIP2 = -3 ; ICBASE = 0 ;
  fichier = '../divers/mimatD3r_par' ;
  PDILT    E1 E2 E3 NU12 NU23 NU13 MU12 MU23 MU13
  PNBRE PCOHI PECOU PEDIR PRVCE PECRX PDVDI PCROI PINCR
    = @mistpar fichier SENSIP1 SENSIP2 ;
* ------------------------------------------------------------
   MAT3 = mate MOD3 'YG1 ' E1    'YG2 ' E2    'YG3 ' E3
                    'NU12' NU12  'NU23' NU23  'NU13' NU13
                    'G12 ' MU12  'G23 ' MU23  'G13 ' MU13
                    'ALP1' 0.     'ALP2' 0.     'ALP3' 0.
 'DILT' PDILT 'NBRE' PNBRE 'COHI' PCOHI 'ACOU' PECOU 'EDIR' PEDIR
 'RVCE' PRVCE 'ECRX' PECRX 'DVDI' PDVDI 'CROI' PCROI 'INCR' PINCR
                    'SIP1' SENSIP1  'SIP2' SENSIP2  'IBAS' ICBASE
                    'RADIAL' PAXB PAXH ;
*
* Conditions aux limites
*
*   CLORT = bloq ortho PAXB PAXH EPROU ;
   CL0 = bloq uy PLR0Z ;
   CL1 = rela depl dire VECT1 PLR1Z ;
   CLB = bloq uz PLRT0 ;
   CLH = rela ense uz PLRTH ;
*
   CL = CL0 et CL1 et CLB et CLH ;
*
* Chargement
*
* -------------------------------------------------------
   T1 = 1.E8 ;
   TEMPSCAL = prog 0. 0.01 0.02 0.05 0.1 0.2
                      0.3  0.4  0.5  0.7 1. ;
   TT0 = 300. ; TT1 = 600. ;
   PHI0 = 1.E18 ;
   SZ0 = 0. ; SZ1 = 100.E6 ;
* -------------------------------------------------------
*
   TEMPS = prog 0. T1 ;
   TEMPSCAL = T1*TEMPSCAL ;
*
   TT = manu chpo EPROU 1 'T' 1. ;
   EVTT = evol manu TEMPS (prog TT0 TT1) ;
   CHTT = char 'T' TT EVTT ;
*
   PHI = manu chpo EPROU 1 'FI' 1. ;
   EVFI = evol manu TEMPS (prog PHI0 PHI0) ;
   CHFI = char 'FI' PHI EVFI ;
*
   PRESH = pres mass MOD3 (-1.) PLRTH ;
   EVSZ = evol manu TEMPS (prog SZ0 SZ1) ;
   CHSZ = char meca PRESH EVSZ ;
*
   CHA = CHTT et CHFI et CHSZ ;
*
* Valeurs initiales
*
   VINT0 = zero MOD3 'VARINTER' ;
*
* Calcul
*
   TAB = TABLE ;
   TAB.'VARIABLES_INTERNES' = TABLE ;
   TAB.'BLOCAGES_MECANIQUES' = CL ;
   TAB.'CARACTERISTIQUES' = MAT3 ;
   TAB.'MODELE' = MOD3 ;
   TAB.'CHARGEMENT' = CHA ;
   TAB.'VARIABLES_INTERNES' . 0 = VINT0 ;
*   TAB.'GRANDS_DEPLACEMENTS' = VRAI ;
   TAB.'TEMPS_CALCULES' = TEMPSCAL ;
   PASAPAS TAB ;
*
* Traitement des resultats
*
   TEM = TAB.'TEMPS' ;
   SIG = TAB.'CONTRAINTES' ;
   DEP = TAB.'DEPLACEMENTS' ;
   VI  = TAB.'VARIABLES_INTERNES' ;
   NT = dime TEMPSCAL-1 ;
* ---------------
   ERMAX = 0.005 ;
* ---------------
   T = TEM.NT ;
   SIGT = SIG.NT ;
   SIGT_PO = chang chpo MOD3 SIGT ;
   SIGXX = extr SIGT_PO SMXX PMH1 ;
   SIGYY = extr SIGT_PO SMYY PMH1 ;
   SIGZZ = extr SIGT_PO SMZZ PMH1 ;
   SIGXY = extr SIGT_PO SMXY PMH1 ;
   SIGXZ = extr SIGT_PO SMXZ PMH1 ;
   SIGYZ = extr SIGT_PO SMYZ PMH1 ;
   SIGRR = ((P11*P11)*SIGXX)+((P12*P12)*SIGYY)
          +(2.*(P11*P12)*SIGXY) ;
   SIGTT = ((P21*P21)*SIGXX)+((P22*P22)*SIGYY)
          +(2.*(P21*P22)*SIGXY) ;
   SIGRT = ((P11*P21)*SIGXX)+((P12*P22)*SIGYY)
          +(((P11*P22)+(P12*P21))*SIGXY) ;
   SIGRZ = ((P11*P33)*SIGXZ)+((P12*P33)*SIGYZ) ;
   SIGTZ = ((P21*P33)*SIGXZ)+((P22*P33)*SIGYZ) ;
*
   DEPT = DEP.NT ;
*
   UXT = extr DEPT UX PMH1 ;
   UYT = extr DEPT UY PMH1 ;
   URT = (UXT*(COS(TETA)))+(UYT*(SIN(TETA))) ;
   UTT = (UXT*(-1.)*(SIN(TETA)))+(UYT*(COS(TETA))) ;
   EPST = URT/RM ;
*
   EPSIT = epsi DEPT MOD3 ;
   EPSIT_PO = chang chpo MOD3 EPSIT ;
   EPSXX = extr EPSIT_PO EPXX PMH1 ;
   EPSYY = extr EPSIT_PO EPYY PMH1 ;
   EPSZZ = extr EPSIT_PO EPZZ PMH1 ;
   EPSXY = (extr EPSIT_PO GAXY PMH1)/2. ;
   EPSXZ = (extr EPSIT_PO GAXZ PMH1)/2. ;
   EPSYZ = (extr EPSIT_PO GAYZ PMH1)/2. ;
   EPSRR = ((P11*P11)*EPSXX)+((P12*P12)*EPSYY)
          +(2.*(P11*P12)*EPSXY) ;
   EPSTT = ((P21*P21)*EPSXX)+((P22*P22)*EPSYY)
          +(2.*(P21*P22)*EPSXY) ;
   EPSRT = ((P11*P21)*EPSXX)+((P12*P22)*EPSYY)
          +(((P11*P22)+(P12*P21))*EPSXY) ;
   EPSRZ = ((P11*P33)*EPSXZ)+((P12*P33)*EPSYZ) ;
   EPSTZ = ((P21*P33)*EPSXZ)+((P22*P33)*EPSYZ) ;
*
   VIT = VI.NT ;
   VIT_PO = chang chpo MOD3 VIT ;
   EPSTHRR = extr VIT_PO ETH1 PMH1 ;
   EPSTHTT = extr VIT_PO ETH2 PMH1 ;
   EPSTHZZ = extr VIT_PO ETH3 PMH1 ;
   EPSTHRZ = extr VIT_PO ETH4 PMH1 ;
   EPSTHRT = extr VIT_PO ETH5 PMH1 ;
   EPSTHTZ = extr VIT_PO ETH6 PMH1 ;
   EPSCRR = extr VIT_PO ECR1 PMH1 ;
   EPSCTT = extr VIT_PO ECR2 PMH1 ;
   EPSCZZ = extr VIT_PO ECR3 PMH1 ;
   EPSCRT = extr VIT_PO ECR4 PMH1 ;
   EPSCRZ = extr VIT_PO ECR5 PMH1 ;
   EPSCTZ = extr VIT_PO ECR6 PMH1 ;
*
   mess ;
   mess 'T :                          ' T ;
   mess ;
   mess 'SIGRR  SIGTT  SIGZZ :    ' SIGRR SIGTT SIGZZ ;
   mess 'SIGRT  SIGRZ  SIGTZ :    ' SIGRT SIGRZ SIGTZ ;
   mess ;
   mess 'UR EPST UT :             ' URT EPST UTT ;
   mess ;
   mess 'EPSRR  EPSTT  EPSZZ :    ' EPSRR EPSTT EPSZZ ;
   mess 'EPSRT  EPSRZ  EPSTZ :    ' EPSRT EPSRZ EPSTZ ;
   mess ;
   mess 'EPSTHRR EPSTHTT EPSTHZZ :' EPSTHRR EPSTHTT EPSTHZZ ;
   mess 'EPSTHRT EPSTHRZ EPSTHTZ :' EPSTHRT EPSTHRZ EPSTHTZ ;
   mess ;
   mess 'EPSCRR EPSCTT EPSCZZ :   ' EPSCRR EPSCTT EPSCZZ ;
   mess 'EPSCRT EPSCRZ EPSCTZ :   ' EPSCRT EPSCRZ EPSCTZ ;
   mess ;
*
*   SIGT_XX = exco SIGT 'SMXX' ;
*   trac SIGT_XX MOD3 ;
*   SIGT_YY = exco SIGT 'SMYY' ;
*   trac SIGT_YY MOD3 ;
*   SIGT_ZZ = exco SIGT 'SMZZ' ;
*   trac SIGT_ZZ MOD3 ;
*   EPST_XX = exco EPSIT 'EPXX' ;
*   trac EPST_XX MOD3 ;
*   EPST_YY = exco EPSIT 'EPYY' ;
*   trac EPST_YY MOD3 ;
*   EPST_ZZ = exco EPSIT 'EPZZ' ;
*   trac EPST_ZZ MOD3 ;
*
* ------------------------------------------------------------
   YOUNGZ = 1.E11 ;
   POISZR = 0.2 ;  POISZT = 0.4 ;
* ------------------------------------------------------------
   EPSTHRR0 = (2.E-5)*(TT1-TT0) ;
   EPSTHTT0 = (1.E-5)*(TT1-TT0) ;
   EPSTHZZ0 = (5.E-6)*(TT1-TT0) ;
   EPSEZZ0 = (SZ1-SZ0)/YOUNGZ ;
   EPSERR0 = (-1.)*POISZR*EPSEZZ0 ;
   EPSETT0 = (-1.)*POISZT*EPSEZZ0 ;
   FITC0 = 1.E18 ;
   T00 = (FITC0/PHI0)**0.5 ;
   T01 = (T1+(FITC0/PHI0))**0.5 ;
   EPSCRR0 = (-1.5E-15)*(PHI0**0.5)*(T01-T00) ;
   EPSCTT0 = (-0.5E-15)*(PHI0**0.5)*(T01-T00) ;
   EPSCZZ0 = (2.E-15)*(PHI0**0.5)*(T01-T00) ;
   EPSRR0 = EPSTHRR0+EPSERR0+EPSCRR0 ;
   EPSTT0 = EPSTHTT0+EPSETT0+EPSCTT0 ;
   EPSZZ0 = EPSTHZZ0+EPSEZZ0+EPSCZZ0 ;
*
   EREPSRR = (EPSRR/EPSRR0) - 1. ;
   EREPSTT = (EPSTT/EPSTT0) - 1. ;
   EREPSZZ = (EPSZZ/EPSZZ0) - 1. ;
   P_ER = prog EREPSRR EREPSTT EREPSZZ ;
   EREPSMA = maxi P_ER 'ABS' ;
*
   EREPSTRR = (EPSTHRR/EPSTHRR0) - 1. ;
   EREPSTTT = (EPSTHTT/EPSTHTT0) - 1. ;
   EREPSTZZ = (EPSTHZZ/EPSTHZZ0) - 1. ;
   P_ER = prog EREPSTRR EREPSTTT EREPSTZZ ;
   EREPSTMA = maxi P_ER 'ABS' ;
*
   EREPSCRR = (EPSCRR/EPSCRR0) - 1. ;
   EREPSCTT = (EPSCTT/EPSCTT0) - 1. ;
   EREPSCZZ = (EPSCZZ/EPSCZZ0) - 1. ;
   P_ER = prog EREPSCRR EREPSCTT EREPSCZZ ;
   EREPSCMA = maxi P_ER 'ABS' ;
*
   si (EREPSMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur rel.) sur EPSRR, EPSTT ou EPSZZ > ' ERMAX ;
    mess EREPSRR EREPSTT EREPSZZ ;
   finsi ;
   si (EREPSTMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur rel.) sur EPSTHRR, EPSTHTT ou EPSTHZZ > ' ERMAX ;
    mess EREPSTRR EREPSTTT EREPSTZZ ;
   finsi ;
   si (EREPSCMA > ERMAX) ;
    mess ;
    mess 'ABS(erreur rel.) sur EPSCRR, EPSCTT ou EPSCZZ > ' ERMAX ;
    mess EREPSCRR EREPSCTT EREPSCZZ ;
    mess ;
   finsi ;
   P_ER = prog EREPSMA EREPSTMA EREPSCMA ;
   ERMA = maxi P_ER ;
   si (ERMA > ERMAX) ;
    ERRE 5 ;
   finsi ;

   fin ;







