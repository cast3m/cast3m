* fichier :  ET_LISTMOTS.dgibi
************************************************************************
* Section : Langage Objets
************************************************************************

*******************************************************
*           Test ET_LISTMOTS.dgibi: Jeux de donn√©es   *
*           ---------------------------------         *
*******************************************************
*___________________________________________________________________
*   TEST ET_LISTMOTS                                               *
*                                                                  *
*   Permet de realiser la verification et validation du 'ET' dans  *
*   les cas suivants :                                             *
*     LISTMOTS 'ET' MOT                                            *
*     MOT      'ET' LISTMOTS                                       *
*     LISTMOTS 'ET' LISTMOTS                                       *
*___________________________________________________________________
*

************************************************************************
* Construction des LISTMOTS initiaux
************************************************************************
TOTO = 'MOTS' 'ABCD' 'EFJJ' 'DKF' ;
TITI = 'MOTS' 'AABB' 'EEFF' 'JKLM' 'JGFG';

************************************************************************
* LISTMOTS 'ET' MOT
************************************************************************
TATA = TOTO 'ET' 'IJKL';

* VALIDATION
BVALID = 'EGA' ('DIME' TATA) (('DIME' TOTO) + 1);
'REPE' SURI ('DIME' TOTO);
  III=&SURI;
  BVALID = BVALID 'ET' ('EGA' ('EXTR' TATA III)('EXTR' TOTO III));
'FIN' SURI;

BVALID = BVALID 'ET' ('EGA' ('EXTR' TATA (('DIME' TOTO) + 1)) 'IJKL');
LIST TATA;
'SI' (NON BVALID);
  MESS 'Erreur 1 dans le ET entre un LISTMOTS et un MOT';
  ERRE 5;
'FINS';


************************************************************************
* MOT 'ET' LISTMOTS
************************************************************************
TUTU = 'ET' 'MLKI' TOTO;
* VALIDATION
BVALID = 'EGA' ('DIME' TUTU) (('DIME' TOTO) + 1);
BVALID = BVALID 'ET' ('EGA' ('EXTR' TUTU 1) 'MLKI');
'REPE' SURI ('DIME' TOTO);
  III=&SURI;
  JJJ=III+1;
  BVALID = BVALID 'ET' ('EGA' ('EXTR' TUTU JJJ)('EXTR' TOTO III));
'FIN' SURI;
LIST TUTU;
'SI' (NON BVALID);
  MESS 'Erreur 2 dans le ET entre un MOT et un LISTMOTS';
  ERRE 5;
'FINS';


************************************************************************
* LISTMOTS 'ET' LISTMOTS
************************************************************************
TETE = TATA 'ET' TITI;

* VALIDATION
BVALID = 'EGA' ('DIME' TETE) (('DIME' TATA) + ('DIME' TITI) );
'REPE' SURI ('DIME' TATA);
  III=&SURI;
  BVALID = BVALID 'ET' ('EGA' ('EXTR' TETE III)('EXTR' TATA III));
'FIN' SURI;
 KKK=('DIME' TATA);
'REPE' SURJ ('DIME' TITI);
  JJJ=&SURJ;
  KKK=KKK+1;
  BVALID = BVALID 'ET' ('EGA' ('EXTR' TETE KKK)('EXTR' TITI JJJ));
'FIN' SURJ;
LIST TETE;
'SI' (NON BVALID);
  MESS 'Erreur 3 dans le ET entre un LISTMOTS et un LISTMOTS';
  ERRE 5;
'FINS';

FIN;



