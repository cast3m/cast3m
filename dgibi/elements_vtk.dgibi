OPTI 'DIME' 3 'ELEM' 'CUB8' ;
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************

GRAPH = FAUX ;


* ****************************************
* CREATION DES DIFFERENTS TYPES D'ELEMENTS
* ****************************************

P0   = 0. 0. 0. ;
PX   = 1. 0. 0. ;
PY   = 0. 1. 0. ;
PZ   = 0. 0. 1. ;
PXY  = 1. 1. 0. ;
PXZ  = 1. 0. 1. ;
PYZ  = 0. 1. 1. ;
PXYZ = 1. 1. 1. ;

* Elements lineaires
* ==================
_POI1 = P0 ;
_SEG2 = MANU 'SEG2' P0 PX ;
_TRI3 = MANU 'TRI3' P0 PX PY ;
_QUA4 = MANU 'QUA4' P0 PX PXY PY ;
_CUB8 = _QUA4 VOLU 'TRAN' 1 PZ ;
_TET4 = MANU 'TET4' P0 PX PY PZ ;
_PYR5 = MANU 'PYR5' (0.5*PX) (0.5*PY) (-0.5*PX) (-0.5*PY) PZ ;
_PRI6 = _TRI3 VOLU 'TRAN' 1 PZ ;

* Elements quadratiques
* =====================
_SEG3  = CHAN 'QUAD' _SEG2 ;
_TRI6  = CHAN 'QUAD' _TRI3 ;
_QUA8  = CHAN 'QUAD' _QUA4 ;
_CUB20 = CHAN 'QUAD' _CUB8 ;
_TET10 = CHAN 'QUAD' _TET4 ;
_PYR13 = CHAN 'QUAD' _PYR5 ;
_PRI15 = CHAN 'QUAD' _PRI6 ;

* Elements a bulles
* =================
_TRI7  = CHAN 'QUAF' _TRI3 ;
_QUA9  = CHAN 'QUAF' _QUA4 ;
_CUB27 = CHAN 'QUAF' _CUB8 ;

* Concatenation de tous les maillages
* ===================================
_SEG2  = _SEG2  PLUS (2. *PX) ;
_TRI3  = _TRI3  PLUS (4. *PX) ;
_QUA4  = _QUA4  PLUS (6. *PX) ;
_CUB8  = _CUB8  PLUS (8. *PX) ;
_TET4  = _TET4  PLUS (10.*PX) ;
_PYR5  = _PYR5  PLUS (12.*PX) ;
_PRI6  = _PRI6  PLUS (14.*PX) ;
_SEG3  = _SEG3  PLUS (2. *PX) PLUS (2.*PY) ;
_TRI6  = _TRI6  PLUS (4. *PX) PLUS (2.*PY) ;
_QUA8  = _QUA8  PLUS (6. *PX) PLUS (2.*PY) ;
_CUB20 = _CUB20 PLUS (8. *PX) PLUS (2.*PY) ;
_TET10 = _TET10 PLUS (10.*PX) PLUS (2.*PY) ;
_PYR13 = _PYR13 PLUS (12.*PX) PLUS (2.*PY) ;
_PRI15 = _PRI15 PLUS (14.*PX) PLUS (2.*PY) ;
_TRI7  = _TRI7  PLUS (4. *PX) PLUS (4.*PY) ;
_QUA9  = _QUA9  PLUS (6. *PX) PLUS (4.*PY) ;
_CUB27 = _CUB27 PLUS (8. *PX) PLUS (4.*PY) ;

MTOT = _POI1 ET
       _SEG2 ET _TRI3 ET _QUA4 ET _CUB8 ET _TET4 ET _PYR5 ET _PRI6 ET
       _SEG3 ET _TRI6 ET _QUA8 ET _CUB20 ET _TET10 ET _PYR13 ET
        _PRI15 ET _TRI7 ET _QUA9 ET _CUB27 ;

MTOT1 = CHAN 'POI1' MTOT ;


* ****************************
* ORDRE DES NOEUDS DANS CAST3M
* ****************************

SI GRAPH ;
    REPE BLOC1 (NBNO MTOT1) ;
        TRAC (MTOT ET (COUL 'ROUG' (MANU 'POI1' (MTOT1 POIN &BLOC1)))) ;
    FIN BLOC1 ;
FINS ;


* *************************
* EXPORTATION VERS PARAVIEW
* *************************

REPE BLOC1 (NBNO MTOT1) ;

    CHPO1 = MANU 'CHPO' (MTOT1 POIN &BLOC1) 1 'SCAL' 1. ;

*    OPTI 'SORT' '../divers/elements_vtk' ;
     OPTI 'SORT' 'elements_vtk' ;
     SORT 'VTK' MTOT CHPO1 'ID_NOEUD' 'TEMPS' (&BLOC1*0.001) ;

FIN BLOC1 ;


FIN ;







