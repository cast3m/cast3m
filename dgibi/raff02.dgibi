************************************************************
** raff02.dgibi
************************************************************
* TEST de la procedure de raffinement en cour de calcul    *
*       RAFF - PASAPAS - PROI en 3D avec plasticite        *
*                  un seul raffinements                    *
*   Calcul sur une CT comparaison avec l'essais 520RY-AT   *
*   realise au DM2S/SEMT/LISN                              *
*                                                          *
*création : gg, le 15.03.2017                              *
************************************************************


************************************************************
*                                                          *
*                       MAILLAGE CT25                      *
*       Metal de base 707 + cordon de soudure              *
*                                                          *
************************************************************
************************************************************

TITRE 'EPROUVETTE CT25 3D' ;
OPTI DIME 3 ELEM CUB8 ;
OPTI TRAC X ;
*---------------------------------------------------
*---------------------------------------------------
* Option de trace
*graph = vrai;
graph = faux;
*graph_ps = vrai;
graph_ps = faux;
*---------------------------------------------------

*---------------------------------------------------
*---------------------------------------------------
*     DONNEES
*     unites = m N Pa
*---------------------------------------------------
*---------------------------------------------------

dn = 1.0E-7;

*      Fissure
*---------------------------------------------------
* Taille de maille fissure ;

tm =5.0E-5;
* longueur prefissure

lpfis = 0.02482;


* CHOISIR longueur fissure maillage
xfis10 = lpfis;


*      Eprouvettete
*---------------------------------------------------
a=0.02;

le = 0.004;

ep = 0.025 ;

*paramètres de raffinement

dmin=0.0030;
densmax=0.008 ;
densmin=densmax/8;
*densmin=densmax/16.;
dmax=dmin + (2*densmax) ;

*---------------------------------------------------
*      Nb elements
*---------------------------------------------------

* epaisseur eprouvette
ne_ep = 3 ;
np_ep =ne_ep + 1;
* maillage fissure
nf1 = 59;

nf2 = 2;



* cercle goupille
* nc1 multiple de 4
nc1 = 32 ;
*
* nc2 multiple de 2
nc2 = 2 ;
ne1 = 2 ;
ne2 = 2 ;
ne3 = 4 ;


*---------------------------------------------------
*      Eprouvette
*---------------------------------------------------

* rayons
r2 = 0.005 ;

r3 = 0.00625 ;

* angles
a1 = 15. ;
a2 = 30. ;

* ordonnees
y1 = 0.0015 ;
y2 = le/2. ;
y3 = 0.006 ;
y5 = 0.0155 ;
y6 = 0.03 ;
*
yf1 = tm*3*3*2;
list yf1;

* abscisses
x1 = -0.0125 ;
x2 = 0.00458 ;
x3 = x2 + r2 ;
x5 = a;
x6 = 0.05;
*
xf1 = x6 - (nf1*3*3*tm);
list xf1;

* coordonnees supplementaires
y4 = y3 + ((-1.)*(x1*(tan(a1))))  ;
x4 = x5 + ((-1.)*(y1/(tan(a2))))  ;
x7 = x3 + (y3 - y2);




*---------------------------------------------------
*---------------------------------------------------
*     MAILLAGE
*
*---------------------------------------------------
*---------------------------------------------------

*---------------------------------------------------
*      Les points
*---------------------------------------------------

* centres des cercles
O1 = 0. y5 0.;
O2 = x2 0. 0.;

* contour eprouvette
p1 = x6 0. 0.;
p2 = x6 y6 0.;
p3 = x1 y6 0.;
p4 = x1 y4 0.;
p5 = 0. y3 0.;
p7 = 0. y2 0.;
p8 = x3 y1 0.;
p9 = x4 y1 0.;
p10 = x5 0. 0.;

* sous decoupe eprouvette
p11 = x7 y6 0.;
p12 = (p11 COOR 1) (p5 COOR 2) 0.;
p13 = (p11 COOR 1) (p9 COOR 2) 0.;
p14 = x4 y6 0.;
p15 = (p14 COOR 1) (p5 COOR 2) 0.;
p16 = (p1 COOR 1) (p9 COOR 2) 0.;
p17 = (p1 COOR 1) (p15 COOR 2) 0.;
p18 = (r3*((2**(0.5))/2)) (y5+(r3*((2**(0.5))/2))) 0.;
p19 = ((-1.)*(p18 COOR 1)) (p18 COOR 2) 0.;

p20 = xfis10 0. 0. ;
p21 = xfis10 y1 0. ;
p22 = xfis10 y3 0. ;
p23 = xfis10 y6 0. ;

*--------------------------------------------------
*      Les lignes
*---------------------------------------------------
* eprouvette (sans fissure) ----------
* se1
p9p13 = p9 DROI ne2 p13;
p15p12 = P15 DROI ne2 p12;
* se2
p14p11 = p14 DROI ne2 p11;
* se3
p13p12 = p13 DROI ne1 p12;
p12p5 = p12 DROI (1+(nc1/8)) p5;
p13p5 = p13p12 ET p12p5;
ELIM dn p13p5 ; p13p5 = REGE p13p5;
*p8p7 = CERC ((-1)*(ne1+1+(nc1/8))) p8 O2 p7 'DINI' 0.1 'DFIN' 2.;
p8p7 = CERC ((-1)*(ne1+1+(nc1/8))) p8 O2 p7 'DINI' 0.0001 'DFIN' 0.002;
p13p8 = p13 DROI nc2 p8;
p7p5 = p7 DROI (nc2) p5;
ELIM dn p7p5 ; p7p5 = REGE p7p5;
*se4
p18p19 = CERC (nc1/4) p18 O1 p19;
p11p3 = p11 DROI (nc1/4) p3;
c11 = CERC ((3*(nc1/4))/2) p19 O1 (O1 PLUS (0. ((-1)*r3) 0.));
c12 = CERC ((3*(nc1/4))/2) (O1 PLUS (0. ((-1)*r3) 0.)) O1 p18;
c1 = c11 ET c12;
ELIM dn c1; c1 = REGE c1;
p3p4 = p3 DROI (nc1/4) p4;
p4p5 = p4 DROI ((-1)+(nc1/8)) p5;
p5p12 = p5 DROI ((+1)+(nc1/8)) p12;
p12p11 = p12 DROI (nc1/4) p11;
c1bis = p3p4 ET p4p5 ET p5p12 ET p12p11;


*--------------------------------------------------
*     Les surfaces
*---------------------------------------------------

* eprouvette (sans fissure) *-----------------------
se1 = p9p13 REGL ne1 p15p12;
se2 = p15p12 REGL (nc1/4) p14p11;
se3 = DALL (INVE p13p5) p13p8 p8p7 p7p5;
se4a = p18p19 REGL 4 p11p3;
se4b = c1 REGL 4 c1bis;
se4 = se4a ET se4b;
ELIM dn se4; se4 = REGE se4;

* eprouvette avec fissure   *-----------------------

p15p22 = droi 3 p15 p22;
p14p23 = droi 3 p14 p23;
se5 = regl 8 p15p22 p14p23;
elim dn se2 se5;

p22p17 = droi 9 p22 p17;
p23p2 = droi 9 p23 p2;
se6 = regl 8 p22p17 p23p2;
elim dn se5 se6;

p9p21 = droi 3 p9 p21;
se7 = regl 1 p9p21 p15p22;
elim dn se1 se7;
elim dn se6 se7;

p21p16 = droi 9 p21 p16;
se8 = regl 1 p21p16 p22p17;
elim dn se6 se8;
elim dn se7 se8;

p10p20 = droi 3 p10 p20;
se9 = regl 1 p10p20 p9p21;
elim dn se9 se7;

p20p1 = droi 9 p20 p1;
se10 = regl 1 p20p1 p21p16;
elim dn se10 se9;
elim dn se10 se8;

si (graph) ;
   trac (se1 et se2 et se3 et se4 et se5 et se6 et se7 et se8 et se9 et se10);
finsi ;
* eprouvette totale *--------------------------------
seprou = se1 et se2 et se3 et se4 et se5 et se6 et se7 et se8 et se9 et se10;
ELIM dn seprou;
seprou = REGE seprou;


* goupille *-----------------------------------------
O1p18 = O1 DROI 1 p18;
sgoup= O1p18 ROTA (nc1/4) (90.) O1 (O1 PLUS (0. 0. 1.));
ELIM dn sgoup; sgoup = REGE sgoup;
* ELIM dn (vgoupet seprou);
*ELIM 0.001 (vgoupet seprou);
ELIM 0.000001 (sgoup et seprou);
*ELIM 0.001 seprou ;
ELIM 0.000001 seprou ;

*--------------------------------------------------
*     Les volumes
*---------------------------------------------------
veprou= seprou VOLU ne_ep 'TRAN' (0. 0. (ep/2.));
vcub8 = veprou;

vgoup = sgoup VOLU ne_ep 'TRAN' (0. 0. (ep/2.));

ELIM 0.000001 (vgoup et veprou );

*--------------------------------------------------
*     Boite pour la visualisation
*--------------------------------------------------
p24 = (1.2*xfis10) 0. 0. ;
p9p24 = droi 1 p9 p24;
sboit = p9p24 TRAN 1 (0. 0. (ep/2.));


*----------------------------------------------------
*     Geometries pour CL en 3D et fissure
*----------------------------------------------------
xeprou yeprou  zeprou = coor veprou ;
pmil = yeprou poin 'INFERIEUR' (1.E-4*tm);
lmil = (enve veprou) elem 'APPUYE' pmil ;
fronfis = DROI ne_ep p20  (xfis10 0. (ep/2.));
psym1 = xeprou poin 'SUPERIEUR' xfis10;
pfis1 = xeprou poin 'INFERIEUR' xfis10;
ssym1 = lmil elem 'APPUYE' 'LARGEMENT' psym1 ;
sfis10 = lmil elem 'APPUYE' 'LARGEMENT' pfis1 ;
surfz0= seprou;

xgoup ygoup zgoup  = coor vgoup;
lchar = ygoup poin 'INFERIEUR' (y5+(1.E-4*tm));

si (graph) ;

  trac (vcub8 et (vgoup coul bleu) et (ssym1 coul roug) et (sfis10 coul vert)) 'CACH';
  OPTI TRAC PSC ;
*  OPTI FTRA 'proj_images/mail_n.ps';
  trac (vcub8 et (vgoup coul bleu) et (ssym1 coul roug) et (sfis10 coul vert)) 'CACH';
  OPTI TRAC X;
finsi ;

*----------------------------------------------------
*     Modèle + Matériau
*----------------------------------------------------

*Modèles éprouvette
MODEPR = MODE VEPROU MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE ;
MODC8  = MODE VCUB8  MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE ;

*Caractéristique materiaux

E1=2.09086E+11;
nu1=0.3;

*Courbe de traction matériau éprouvette
EPS1 = PROG 0. 2.50007E-3 5.39261E-3 1.40558E-2 2.28078E-2 3.17654E-2 4.1161E-2 5.14294E-2 6.33414E-2 7.82031E-2 9.81451E-2 0.12653 0.1685 0.23177 0.32756 0.47195 0.68754 1.0056 1.4685 2.1336 3.0765 4.3966 6.223 8.7212 12.102 16.633 22.647 30.56 40.883 54.247 71.419 93.330 121.10 ;

SG1 = PROG 0. 522.73 532.73 562.73 592.73 622.73 652.73 682.73 712.73 742.73 772.73 802.73 832.73 862.73 892.73 922.73 952.73 982.73 1012.7 1042.7 1072.7 1102.7 1132.7 1162.7 1192.7 1222.7 1252.7 1282.7 1312.7 1342.7 1372.7 1402.7 1432.7 ;
SG1=SG1*1.0E+6;
EVTRAC = EVOL 'MANU' EPS1 SG1 ;

si (graph);
   dess EVTRAC 'TITR'  ' courbe traction eprouvette';
finsi ;

*Matériaux éprouvette
MATEPR = MATE MODEPR YOUN E1 NU nu1 TRAC EVTRAC ;


*Modèle goupille
MODGOUP = MODE VGOUP MECANIQUE ELASTIQUE EPSI LINEAIRE ;

*Matériau goupille
MATGOUP = MATE MODGOUP YOUN E1 NU nu1 ;


MODold=(MODEPR et MODGOUP);



*----------------------------------------------------
*     CL + Chargement
*----------------------------------------------------





cl1 = bloq ssym1 uy ;
cl2 = bloq lchar uy  ;
cl3 = bloq (surfz0 et sgoup) uz;
cl4 = bloq lchar ux ;
CL = CL1 ET CL2 et cl3 et CL4;
CLodl=CL;

uchar=0.0016;
f1 = depi cl2 uchar/2;


LI1 = PROG 0. 1. ;
LI2 = PROG 0. 1. ;
EV1 = EVOL MANU T LI1 F(T) LI2 ;

CHA1 = CHAR 'DIMP' F1 EV1 ;
list CHA1;
*----------------------------------------------------
*     Résolution de tout le problème
*     sur le maillage grossier
*----------------------------------------------------
f4 = depi cl2 uchar;
LI4 = PROG 0.0 1.0 ;
EV4 = EVOL MANU T LI1 F(T) LI4 ;
CHA4 = CHAR 'DIMP' F4 EV4 ;

TABPAP4=0.;
TABPAP4= TABL;
TABPAP4 . 'TEMPS_CALCULES' = PROG 0. pas 0.05 1. ;
TABPAP4 . 'CARACTERISTIQUES' = (MATEPR et matgoup) ;
TABPAP4 . 'CHARGEMENT' = CHA4 ;
TABPAP4 . 'BLOCAGES_MECANIQUES' = CL ;
TABPAP4 . 'MODELE' = (MODEPR et modgoup) ;
TABPAP4 . 'LAGRANGIEN' = mot 'REACTUALISE' ;
PASAPAS TABPAP4 ;

eglo4=PROG;
I=0;
repe bcl4 20 ;
   I=I+1;

   sig1 = TABPAP4 . 'CONTRAINTES' . i;
   eps1 = EPSI (MODEPR et modgoup) TABPAP4 . 'DEPLACEMENTS'. i;
   eloc1 = ENER (MODEPR et modgoup)  eps1 sig1;
   eglo1 = INTG eloc1 (MODEPR et modgoup) ;
   eglo4 = eglo4 et eglo1 ;



fin bcl4;

*DESS ev1;
*----------------------------------------------------
*     Premier pas de résolution résolution
*----------------------------------------------------
temps=TABL;

*Résolution PASAPAS
TABPAP = TABLE ;
TABPAP . 'TEMPS_CALCULES' = PROG 0. pas 0.1 1. ;
TABPAP . 'CARACTERISTIQUES' = (MATEPR et matgoup) ;
TABPAP . 'CHARGEMENT' = CHA1 ;
TABPAP . 'BLOCAGES_MECANIQUES' = CL ;
TABPAP . 'MODELE' = (MODEPR et modgoup) ;
TABPAP . 'LAGRANGIEN' = mot 'REACTUALISE' ;
PASAPAS TABPAP ;
TABRAF=TABL;
TABRAF.1=TABPAP;


ouv2 = PROG 0.0;
for2 = PROG 0.0;
eglo2= PROG 0.0;
KI2 = PROG 0.0;
Sig_m2 = PROG 0.0;
KIamorc = 1213.;

repe bcl1 10 ;

   ouv2 = ouv2 ET (2*(EXTR (TABPAP . 'DEPLACEMENTS' . &bcl1)  'UY' P7));

   for1=0  ;
   REPE bcl11 4;
      for1 = for1 + ((EXTR (TABPAP. 'REACTIONS' . &bcl1 )'FY' (POIN lchar &bcl11))*2);
   fin bcl11;
   for2 = for2 ET for1;
   sig1 = TABPAP . 'CONTRAINTES' . &bcl1;
   eps1 = EPSI (MODEPR et modgoup) TABPAP . 'DEPLACEMENTS'. &bcl1;
   eloc1 = ENER (MODEPR et modgoup)  eps1 sig1;
   eglo1 = INTG eloc1 (MODEPR et modgoup) ;
   eglo2 = eglo2 et eglo1 ;


fin bcl1;
*-----------------------------------------------------------------------
*                                       - raffiner le maillage
*                                       - receer le mod et le mat
*                                       - recreer les cl
*                                       - incrémenter le chargement
*                                       - projeter les variables issu
*                                       du dernier pas de pasapas
*                                       - réaliser la suite du calcul
*                                       pasapas
*-----------------------------------------------------------------------


*-----------------------------------------------------------------------
*      Raffinement autour de la pointe de fissure
*-----------------------------------------------------------------------

xeprou yeprou  zeprou= coor veprou ;

DF1= (((yeprou**2) + ((xfis10 -xeprou)**2.))**0.5);
*DF1= (((yeprou**2) + ((BORN  ((xfis10 - 0.0032)-xeprou)
*'SCAL' 'MINIMUM' 0.0)**2.))**0.5);

aff = (((densmax - densmin)*(DF1 - dmax))/(dmax - dmin)) + densmax ;
dens1 = born aff 'SCAL' 'COMPRIS' (densmin*1.000001) (densmax*1.000001);

si (graph) ;
    trac dens1 veprou ;
finsi ;

VOLD= (VEPROU et VGOUP);

veprou = raff veprou dens1 ;
*    LIST (NBEL SEPROU);


VOLURL = ELEM VEPROU SURE ;
VCUB8 = VEPROU DIFF VOLURL ;

ELIM 0.000001 (Vgoup et Veprou);
ELIM 0.000001 Veprou ;
ELIM 0.000001 VCUB8 ;
*----------------------------------------------------
*     Geometries pour CL en 3D et fissure
*----------------------------------------------------

xeprou yeprou  zeprou = coor veprou ;
pmil = yeprou poin 'INFERIEUR' (1.E-4*tm);
lmil = (enve veprou) elem 'APPUYE' pmil ;
fronfis = DROI ne_ep (xfis10 0. 0.)  (xfis10 0. (ep/2.));
psym1 = xeprou poin 'SUPERIEUR' xfis10;
pfis1 = xeprou poin 'INFERIEUR' xfis10;
ssym1 = lmil elem 'APPUYE' 'LARGEMENT' psym1 ;
sfis10 = lmil elem 'APPUYE' 'LARGEMENT' pfis1 ;
poiz0 = zeprou poin 'INFERIEUR' (1.E-4*tm);
surfz0 = (enve veprou) elem 'APPUYE' poiz0 ;

si (graph) ;

  trac (vcub8 et (vgoup coul bleu) et (ssym1 coul roug) et (sfis10 coul vert)) 'CACH';

finsi ;

*----------------------------------------------------
*     Modèle + Matériau
*----------------------------------------------------

MODOLD = MODEPR;
MODEPR = MODE VEPROU MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE ;
MODC8  = MODE VCUB8  MECANIQUE ELASTIQUE PLASTIQUE EPSI LINEAIRE ;
MATEPR = MATE MODEPR YOUN E1 NU nu1 TRAC EVTRAC ;
MATC8  = MATE MODC8  YOUN E1 NU nu1 TRAC EVTRAC ;
*----------------------------------------------------
*     CL + Chargement
*----------------------------------------------------

KEPR = RIGI MODEPR MATEPR ;

*----------------------------------------------------
* Matrice de Raideur (relation de conrformité géom)
*----------------------------------------------------

KRELA = RELA MODEPR ;

*----------------------------------------------------
*     CL + Chargement
*----------------------------------------------------

cl1 = bloq ssym1 uy ;

cl2 = bloq lchar uy  ;

cl3 = bloq (surfz0 et sgoup) uz;
cl4 = bloq lchar ux ;
CL = CL1 ET CL2 et cl3 et CL4;
CLodl=CL;

f1 = depi cl2 (uchar/2);
LI2 = PROG (1.0) (2.0) ;
EV1 = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR 'DIMP' F1 EV1 ;
list CHA1;


*-----------------------------------------------------------------------
*     Projection des champs issues de l'iteration précédente
*-----------------------------------------------------------------------

*    DEPLACEMENT
uold=tabpap.continuation.deplacements;
ugoup= redu uold vgoup;
* passage de u en chamelem
uproj1 = chan 'CHAM' (redu vold uold) vold;
* projection
uproj2 = PROI uproj1 VEPROU;
* passage de uproj en chpoin
uproj3 = chan 'CHPO' uproj2 MODEPR;

uproj= uproj3 et ugoup ;

si (graph) ;
    trac uold vold TITR 'u old';
    trac uproj VEPROU  TITR 'u proj';
finsi;

*CONTRAINTES

sigold=tabpap.continuation.contraintes;
siggoup= redu sigold vgoup;

* passage point de gausses -> noeuds du chaml
sigproj1 = CHAN  'NOEUD' MODOLD  (redu vold sigold)  ;
* projection
sigproj2= PROI modepr sigproj1 'STRESSES';
* passage noeuds -> point de gausses du chaml

*list sigproj2;
sigproj4 = CHAN 'TYPE' sigproj2 'CONTRAINTES';
sigproj = sigproj4 et siggoup ;



si (graph) ;
     trac sigold modold TITR 'sig old';
     trac sigproj modepr TITR 'sig proj';

finsi;

*VARIABLES_INTERNES

pold=tabpap.continuation.variables_internes;
pgoup= redu pold vgoup;

* passage point de gausses -> noeuds du chaml
pproj1 = CHAN  'NOEUD' MODOLD  (redu vold pold)  ;
* projection
pproj2= PROI modepr pproj1 'STRESSES';

* positivité de p
pproj3= born pproj2 'EPSE' 'MINIMUM' 0.0;

pproj= pgoup et pproj3;

si (graph) ;
    trac pold modold TITR 'p old';
    trac pproj modepr TITR 'p proj';
finsi;

* calcul de l'énergie de déformation

epsold = EPSI (MODOLD et modgoup)  uold;
elocold = ENER (MODOLD et modgoup) epsold sigold;
egloold = INTG elocold (MODOLD et modgoup) ;

epsproj = EPSI (modepr et modgoup )  uproj;
elocproj = ENER (modepr et modgoup ) epsproj sigproj;
egloproj = INTG elocproj (MODEPR et modgoup) ;

step1=(0.0 + 5.0E06)/22 ;
liii1= PROG (0.0) PAS step1 5.0E06;
step1=(0.0 + 3.50E08)/22 ;
liii1= PROG (0.0) PAS step1 3.50E08;

si (graph) ;
  trac liii1 elocold modold   TITR 'energie champ avant proj';
  trac liii1 elocproj modepr  TITR 'energie champprojete';

  trac elocold modold  TITR 'energie champ avant proj';
  trac elocproj modepr TITR 'energie champprojete';
finsi;

*Résolution PASAPAS
TABPAP=0.;
TABPAP= TABL;
TABPAP . 'TEMPS_CALCULES' = PROG 0. pas 0.1 1. ;
TABPAP . 'CARACTERISTIQUES' = (MATepr et matgoup) ;
TABPAP . 'CHARGEMENT' = CHA1 ;
TABPAP . 'BLOCAGES_MECANIQUES' = CL ET KRELA ;
TABPAP . 'MODELE' = (MODepr et modgoup) ;
TABPAP . 'LAGRANGIEN' = mot 'REACTUALISE' ;
TABPAP . 'DEPLACEMENTS' = TABL;
TABPAP . 'DEPLACEMENTS' . 0 = uproj;
TABPAP . 'CONTRAINTES' = TABL;
TABPAP . 'CONTRAINTES' . 0 = sigproj;
TABPAP . 'VARIABLES_INTERNES' = TABL;
TABPAP . 'VARIABLES_INTERNES' . 0 = pproj;
PASAPAS TABPAP ;
TABRAF.2=TABPAP;

*  opti donn 5;
J=0;
REPE bcl2 10 ;
    J=J+1;

    ouv1 =(2*(EXTR (TABPAP . 'DEPLACEMENTS' . j) 'UY' P7));


    ouv2 = ouv2 ET ouv1;
    K=0;
    for1=0;
    REPE bc21 np_ep;
      K=K+1;
      for1 = for1 + ((EXTR (TABPAP. 'REACTIONS' . j ) 'FY' (POIN lchar K))*2);
    fin bc21;
    for2 = for2 ET for1;
    sig1 = TABPAP . 'CONTRAINTES' . j;
    eps1 = EPSI (MODEPR et modgoup) TABPAP . 'DEPLACEMENTS'. j;
    eloc1 = ENER (MODEPR et modgoup)  eps1 sig1;
    eglo1 = INTG eloc1 (MODEPR et modgoup) ;
    eglo2 = eglo2 et eglo1 ;


fin bcl2;

*----------------------------------------------------------------------*
*          Resolution en partant directement du dernier maillage       *
*----------------------------------------------------------------------*

* chargement
f1 = depi cl2 uchar;
LI2 = PROG 0.0 1.0 ;
EV1 = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR 'DIMP' F1 EV1 ;

TABPAP3=0.;
TABPAP3= TABL;
TABPAP3 . 'TEMPS_CALCULES' = PROG 0. pas 0.05 1. ;
TABPAP3 . 'CARACTERISTIQUES' = (MATEPR et matgoup) ;
TABPAP3 . 'CHARGEMENT' = CHA1 ;
TABPAP3 . 'BLOCAGES_MECANIQUES' = CL ET KRELA ;
TABPAP3 . 'MODELE' = (MODEPR et modgoup) ;
TABPAP3 . 'LAGRANGIEN' = mot 'REACTUALISE' ;
PASAPAS TABPAP3 ;
*temps.(I+2)=DATE;

ouv3= prog;
for3= prog;
eglo3=prog;
ouv4= prog;
for4= prog;

J=0;
REPE bcl3 20 ;
    J=J+1;

    ouv1 =(2*(EXTR (TABPAP3 . 'DEPLACEMENTS' . j) 'UY' P7));
    ouv3 = ouv3 ET ouv1;

    ouv4 = ouv4 et (2*(EXTR (TABPAP4 . 'DEPLACEMENTS' . j) 'UY' P7));
    K=0;
    for1=0;
    REPE bcl31 np_ep;
       K=K+1;
       for1 = for1 + ((EXTR (TABPAP3. 'REACTIONS' . j )'FY' (POIN lchar K))*2);
    fin bcl31;
    for3 = for3 ET for1;
    K=0;
    for1=0;
    REPE bcl32 np_ep;
       K=K+1;
       for1 = for1 + ((EXTR (TABPAP4. 'REACTIONS' . j )'FY' (POIN lchar K))*2);
    fin bcl32;
    for4 = for4 ET for1;
    sig1 = TABPAP3 . 'CONTRAINTES' . j;
    eps1 = EPSI (MODEPR et modgoup) TABPAP3 . 'DEPLACEMENTS'. j;
    eloc1 = ENER (MODEPR et modgoup)  eps1 sig1;
    eglo1 = INTG eloc1 (MODEPR et modgoup) ;
    eglo3 = eglo3 et eglo1 ;

fin bcl3;

TAB1= TABL;
TAB1 . 'TITRE'= 'TABLE';
TAB1 . 'TITRE' . 1 = 'MOT' 'Raff en cour de calcul';
TAB1 . 'TITRE' . 2 = 'MOT' 'Maillage fin' ;
TAB1 . 'TITRE' . 3 = 'MOT' 'Maillage grossier' ;
TAB1 . 'TITRE' . 4 = 'MOT' 'Courbe experimentale' ;
*TAB1 . 1 = 'MOT' 'MARQ  PLUS ';
TAB1 . 2 = 'MOT' 'TIRR';
TAB1 . 3 = 'MOT' 'TIRR';

* Données expérimentales pour comparaison :
* (essais 5FTRA20RY-AT au LISN)



OUVEXP1 = PROG 0 2.41661E-03  5.30094E-03  7.63577E-03  1.00449E-02 1.27110E-02  1.56070E-02  1.86920E-02  2.16473E-02  2.45408E-02 2.77426E-02  3.07212E-02  3.34408E-02  3.67156E-02  4.03118E-02 4.37164E-02  4.62874E-02  5.01488E-02  5.31909E-02  5.61370E-02 5.99129E-02  6.34043E-02  6.65528E-02  6.99978E-02  7.32631E-02 7.70875E-02  8.08755E-02  8.46865E-02  8.85926E-02  9.18141E-02 9.52742E-02  9.88186E-02  0.10224 0.10657 0.10996 0.11396 0.11699 ;
OUVEXP2 =  PROG 0.12151 0.12498 0.12893 0.13283 0.13624 0.14064 0.14422 0.14830 0.15246 0.15596 0.16034 0.16425 0.16871 0.17227  0.17591 0.17957 0.18386 0.18799 0.19211 0.19610 0.20000 0.20378 0.20782 0.21256  0.21643 0.22014 0.22449 0.22856 0.23254 0.23670 0.24096 0.24470 0.24898 0.25300 0.25716 0.26109 0.26519 0.26925 0.27342 0.27816 0.28178 0.28576 0.28991 0.29410 0.29791 0.30235 0.30668 0.31095 0.31533 0.31948 0.32415 0.32819 0.33253;
OUVEXP3 = PROG  0.33713 0.34150 0.34597 0.35026 0.35450 0.35883 0.36311 0.36743 0.37209 0.37653 0.38110 0.38537 0.38967 0.39411 0.39873 0.40309 0.40792 0.41242 0.41704 0.42137 0.42601 0.43042 0.43556 0.44009 0.44495 0.44955 0.45372 0.45806 0.46273 0.46761 0.47230 0.47712 0.48179 0.48671 0.49134 0.49650 0.50160 0.50623 0.51082 0.51634 0.52069 ;
ouvexp = OUVEXP1 et OUVEXP2 ET OUVEXP3 ;
* convertion en m
ouvexp = 0.001*ouvexp;


forexp1 = PROG 456.08621 852.54  1164.9  1548.8  1858.2  2286.3  2581.0 2930.3  3287.3  3573.5  3998.0  4388.6  4773.6 5174.7  5624.6  5985.0 6383.5  6811.0  7200.5  7646.5  8078.4 8530.0  8972.0  9405.0  9875.8 10339.  10802.  11255.  11715.  12197.  12659. 13113.  13599.  14104. 14587.  15079.  15533.  15990.  16466.  16991. 17503.  17984.  18501.;
forexp2 = PROG 18979.  19474.  19970.  20461.  20989.  21498. 22031.  22501.  23011. 23529.  24052.  24538.  25023.  25511.  26017. 26507.  27024.  27557. 28015.  28540.  29037.  29512.  30038.  30523. 31008.  31491.  32003. 32556. 33043.  33557.  34057.  34541.  35032.  35524.  35980.  36502. 36983.  37509. 38014.  38534.  39051.  39536.  40020.  40547.  41007. 41510.  41980.  42478. 42946.  43439.  43923.;
forexp3 = PROG 44400.  44871.  45352.  45827.  46339.  46847.  47294. 47771.  48213. 48679.  49142.  49596.  50044.  50490.  50935.  51389.  51841.  52291. 52750.  53187.  53645.  54089.  54507.  54902.  55338.  55790.  56257. 56719.  57154.  57562.  57987.  58401.  58821.  59262.  59687.  60060. 60441.;
forexp= forexp1 et forexp2 et forexp3;


EVFOUV2 = EVOL 'MANU' ouv2 for2  ;
EVFOUV3 = EVOL 'ROUG' 'MANU' ouv3 for3 ;
EVFOUV4 = EVOL 'BLEU' 'MANU' ouv4 for4 ;
EVFOUVE = EVOL 'VERT' 'MANU' ouvexp forexp ;

EVEOUV2 = EVOL 'MANU' ouv2 eglo2  ;
EVEOUV3 = EVOL 'ROUG' 'MANU' ouv3 eglo3  ;
EVEOUV4 = EVOL 'BLEU' 'MANU' ouv4 eglo4 ;

res2 = TABRAF. 2 .'DEPLACEMENTS'. 10;
ressig2 = TABRAF. 2 .'CONTRAINTES'. 10;
resp2 = TABRAF. 2 .'VARIABLES_INTERNES'. 10;
res3 = TABPAP3.'DEPLACEMENTS'. 20;
ressig3 = TABPAP3 .'CONTRAINTES'. 20;
resp3 = TABPAP3.'VARIABLES_INTERNES'. 20;

*    deformees finales

def0 = DEFO 0.0 res2 veprou 'GRIS' ;
def2 = DEFO 1 res2 veprou ;
def3 = DEFO 1 res3 veprou 'ROUGE';


si (graph);


   DESS (EVFOUV2 et EVFOUV3 et EVFOUV4 et EVFOUVE) 'TITR' 'Courbe Force Ouverture' 'LEGE' TAB1;

   DESS (EVEOUV2 et EVEOUV3 et EVEOUV4) 'TITR' 'Courbe Energie totale Ouverture' 'LEGE' TAB1;
     TRAC (def0 et def2 et def3) 'CACH';
finsi ;

*    Champ différence
udiffx = (EXCO res2 'UX')  - (EXCO res3 'UX') ;
udiffy = (EXCO res2 'UY')  - (EXCO res3 'UY') ;
udiffz = (EXCO res2 'UZ')  - (EXCO res3 'UZ') ;

udiff = ( (udiffx**2)+(udiffy**2)+(udiffz**2))**0.5;
umax = maxi ((((EXCO res3 'UX')**2)+ ((EXCO res2 'UY')**2)+((EXCO res3 'UZ')**2))**0.5);

uerr = udiff /umax;

si (graph);
  trac  udiff veprou TITR 'Ecart sur le deplacement';
  trac  uerr veprou TITR 'Ecart relatif sur le deplacement';
finsi;

perr = (resp2 - resp3) / (maxi resp3);



eps2 = EPSI (MODEPR et modgoup) res2;
eloc2 = ENER (MODEPR et modgoup)  eps2 ressig2;
eps3 = EPSI (MODEPR et modgoup) res3;
eloc3 = ENER (MODEPR et modgoup)  eps3 ressig3;

elocerr = (ABS (eloc2 - eloc3)) / (maxi eloc3);

si (graph_ps);
    OPTI 'TRAC' 'PSC' 'EPTR' 7 ;
    OPTI 'POTR' 'HELVETICA_16';

*    OPTI FTRA 'proj_images/fouv.ps';
    DESS (EVFOUV2 et EVFOUV3 et EVFOUV4 et EVFOUVE) 'TITR' 'Courbe Force Ouverture' 'LEGE' TAB1;

*    OPTI FTRA 'proj_images/ufin.ps';
    trac  (exco res2 'UY') veprou TITR 'Deplacement final';

*    OPTI FTRA 'proj_images/ufinz.ps';
    trac  (exco res2 'UY') veprou TITR 'Deplacement final' boit sboit;

*    OPTI FTRA 'proj_images/sfin.ps';
    trac  ressig2 modepr TITR 'Contraintes finales';

*    OPTI FTRA 'proj_images/sfinz.ps';
    trac  ressig2 modepr TITR 'Contraintes finales' boit sboit;

*    OPTI FTRA 'proj_images/pfin.ps';
    trac  resp2 modepr TITR 'Deformation plastique finale';

*    OPTI FTRA 'proj_images/pfinz.ps';
    trac  resp2 modepr TITR 'Deformation plastique finale' boit sboit;

*    OPTI FTRA 'proj_images/eouv.ps';
    DESS (EVEOUV2 et EVEOUV3 et EVEOUV4) 'TITR' 'Courbe Energie totale Ouverture' 'LEGE' TAB1;

*    OPTI 'TRAC' 'PSC' 'EPTR' 1 ;
    OPTI FTRA 'proj_images/udiff.ps';
    trac  udiff veprou TITR 'Ecart sur le deplacement ';

*    OPTI FTRA 'proj_images/uerr.ps';
    trac  uerr veprou TITR 'Ecart relatif sur le deplacement';

*    OPTI FTRA 'proj_images/perr.ps';
    trac  perr modepr TITR 'Ecart relatif sur la deformation plastique';

*    OPTI FTRA 'proj_images/elocerr.ps';
    trac  elocerr modepr TITR 'Ecart relatif sur l energie de deformation locale';

*    OPTI FTRA 'proj_images/uold.ps';
    trac  (exco uold 'UY') vold TITR 'deplacement UY avent proj ';

*    OPTI FTRA 'proj_images/uproj.ps';
    trac  (exco uproj 'UY') VEPROU TITR 'deplacement UY apres proj';

*    OPTI FTRA 'proj_images/sigold.ps';
    trac  sigold modold TITR 'sigma avent proj';

*    OPTI FTRA 'proj_images/sigproj.ps';
    trac  sigproj modepr TITR 'sigma apres proj';

*    OPTI FTRA 'proj_images/pold.ps';
    trac  pold modold TITR 'variable interne avent proj';

*    OPTI FTRA 'proj_images/pproj.ps';
    trac  pproj modepr TITR 'variable interne apres proj';

    step1=(0.0 + 3.50E08)/22 ;
    liii1= PROG (0.0) PAS step1 3.50E08;

*    OPTI FTRA 'proj_images/eold1.ps';
    trac liii1 elocold (modold  ) TITR 'energie champ avant proj';

*    OPTI FTRA 'proj_images/eproj1.ps';
    trac liii1 elocproj (modepr  )TITR 'energie champ projete';

*    OPTI FTRA 'proj_images/eold2.ps';
    trac elocold (modold  ) TITR 'energie champ avant proj';

*    OPTI FTRA 'proj_images/eproj2.ps';
    trac elocproj (modepr  )TITR 'energie champ projete';

    OPTI 'TRAC' 'PSC' 'EPTR' 2 ;

*    OPTI FTRA 'proj_images/mail_raff.ps';
    trac (veprou et (vgoup coul bleu) et (ssym1 coul roug) et (sfis10 coul vert)) 'CACH';

*    OPTI FTRA 'proj_images/mail_raffz.ps';
    trac (veprou et (vgoup coul bleu) et (ssym1 coul roug) et (sfis10 coul vert)) 'CACH' boit sboit;

    OPTI TRAC X;

finsi;


*----------------------------------------------------------------------*
*          Ecart experience simulation sur le maillage raffiné         *
*----------------------------------------------------------------------*
OPTI SAUV 'RAFF02.SAUV';
SAUV;
nrjsim = extr (intg EVFOUV3 1 3) 1;
ouvf   = EXTR ouv3 3;

repe bcl4 (dime ouvexp);
  si ((EXTR ouvexp &bcl4) > ouvf) ;
    indf = &bcl4;
    quit bcl4;
  finsi;
fin bcl4;


nrjexp = extr (intg EVFOUVE 1 indf) 1;
errexp = (ABS (nrjsim - nrjexp))/nrjexp;

*----------------------------------------------------------------------*
*                  Erreur de projection                                *
*----------------------------------------------------------------------*

nrjdir = extr (intg EVFOUV3 11 20) 1 ;
nrjproj = extr (intg EVFOUV2 11 20) 1;
errproj = (ABS (nrjdir - nrjproj))/nrjdir;
list errexp;
list errproj;

SI  ((errproj < (1.0E-2)) et (errexp < (1.0E-1)));
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
  Erre 0;
Sinon;
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  Erre 5;
finsi;

FIN ;






