* fichier :  enc2dke.dgibi
*
*  Enceinte 2D Axisymetrique
*  Relachement d'un melange d'helium et de vapeur
*  modèle K-Epsilon formulation Semi explicite (EFM1)
'OPTI' 'DIME' 2 'ELEM' 'CU20' ;
 'OPTI' 'MODE' 'AXIS' ;

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

 'SI' COMPLET ;
  nbit=4000   ;
  DT0 = 8.e-3 ;
  n1=5 ;
  n2=10;
  n3=20;
 GRAPH  = VRAI                                          ;
 'SINON'      ;
  nbit=100    ;
  DT0 = 8.e-3 ;
  n1=5 ;
  n2=10;
  n3=20;
 'FINSI'      ;


p1=0 0 ;
p11=0.4 0 ;
p2=0.1 0. ;
p3=1 0 ;
p4=1 2;
p5=0 2 ;


breche = p1  'DROI' n1 p2 ;
bas= p2 'DROI' n2 p3 ;
paroid=p3 'DROI' n3 p4 ;
plaf=p4 'DROI' (n1 + n2) p5 ;
axe = p5 'DROI' n3 p1 ;
vtp = bas 'ET' paroid 'ET' plaf ;
mt= 'DALLER' (breche 'ET' bas) paroid plaf axe ;

 RXT = 'TABLE' ;

 RXT.'vtf'=mt ;
 RXT.'axe'   = axe ;
 RXT.'breche'=breche ;
 RXT.'diru1'=0. 1. ;
 RXT.'epsi' = 1.E-5 ;
 RXT.'pi'  = 0.5 0.5 ;

 RXT.'DT0'  = DT0     ;
 RXT.'DETMAT'= VRAI ;
 RXT.'RENU'  = 'RIEN' ;
 RXT.'DISCR'= 'LINE';
 RXT.'KPRE' = 'MSOMMET';
 RXT.'ALGO' = 'EFM1';
 RXT.'GRAPH'  = GRAPH ;
 RXT.'IMPR' = 0 ;

 RXT.'MODTURB' = 'KEPSILON' ;
 RXT.'FPAROI' = VRAI ;
 RXT.'YP'     = 0.05  ;

 RXT.'VAPEUR' = VRAI ;
 RXT.'HE'     = VRAI ;

 RXT.'TF0'    =       20.0                         ;
 RXT.'PT0'    =        1.0e5                       ;
 RXT.'Yvap0'  =        0.1                         ;
 RXT.'Yhe0'   =        0.0                         ;

 RXT.'scenario'          = table                              ;
 RXT.'scenario'.'t'      = prog   0.0 100.0                   ;
 RXT.'scenario'.'qair'   = prog   0.005 0.005                 ;
 RXT.'scenario'.'qeau'   = prog   0.0011  0.0011              ;
 RXT.'scenario'.'qhe'    = prog   0.0011  0.0011              ;
 RXT.'scenario'.'tinj'   = prog  20.0  20.0                   ;

 EXECRXT nbit RXT ;

un=RXT.'TIC'.'UN';
mess ' Max U ' (maxi un) ' Min U ' (mini un) ;
test = abs ((maxi un) - 1.02   );
mess ' Test =' test;
 Si (test '>' 0.1  ) ; erreur 5 ; Finsi ;

tf =RXT.'TIC'.'TF' ;
mess ' Max Tf' (maxi tf) ' Min Tf' (mini tf) ;
test = abs ((maxi tf) - 20.1   );
mess ' Test =' test;
 Si (test '>' 1.   ) ; erreur 5 ; Finsi ;



$vtf=RXT.'GEO'.'$vtf' ;
vtf=doma $vtf maillage;
kn= RXT.'TIC'.'KN';
en= RXT.'TIC'.'EN';
rho=RXT.'TIC'.'RHO' ;
rhe=RXT.'TIC'.'RHE';
un =RXT.'TIC'.'UN';

ixe=inve axe ;
evauz = evol chpo (exco un 'UY') ixe ;
evarh = evol chpo rho ixe ;
evatf = evol chpo tf  ixe ;
evahe = evol chpo rhe ixe ;


Si GRAPH ;

 dess evauz 'TITRE' 'Vitesse sur l axe';
 dess evarh 'TITRE' 'Densite melange sur l axe';
 dess evatf 'TITRE' 'Temperature sur l axe';
 dess evahe 'TITRE' 'Densite Helium sur l axe';

 ung= vect un 0.1 ux uy jaune;
 trace ung vtf;
 opti isov suli ;
 trace rho vtf 'TITRE' ' Rho' ;
 trace rhe vtf 'TITRE' ' Rhe' ;
 trace tf  vtf 'TITRE' ' Tf ' ;
 trace kn  vtf 'TITRE' ' KN ' ;
 trace en  vtf 'TITRE' ' EN ' ;
Finsi ;

 FIN ;




