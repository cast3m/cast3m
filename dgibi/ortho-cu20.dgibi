* fichier :  ortho-cu20.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*------------------------------------------------------------------
*
*     TEST ORTHOTROPIE : PLAQUE EN FLEXION - MAILLAGE VOLUMIQUE
*    Michel BULIK (inspiré d'un test d'Alain MOAL (octobre 1996))
*
*   Il s'agit d'une plaque carrée, encastrée sur les bords et
*   soumise à une pression uniforme. On modélise succésivement
*   les cas suivants :
*
*   1. 1er axe d'orthotropie = Ox
*      E_1 = 2.e+5
*      E_2 = 2.e+4
*
*   2. 1er axe d'orthotropie = Oy
*      E_1 = 2.e+4
*      E_2 = 2.e+5
*
*   Le troisième axe d'orthotropie est toujours parallèle à l'axe
*   Oz.
*
*   Le test porte sur l'égalité des flèches au centre de la plaque
*   dans les deux cas (physiquement équivalents).
*
*------------------------------------------------------------------

opti dime 3 mode trid elem cu20 ;
opti trac x ;
graph = faux ;

*---- maillage
p1 = -1.e3 -1.e3 0. ;
p2 =  1.e3 -1.e3 0. ;
p3 =  1.e3  1.e3 0. ;
p4 = -1.e3  1.e3 0. ;

n1 = 4 ;
n2 = 4 ;
n3 = 1 ;

l1 = p1 d n1 p2 ;
l2 = p2 d n2 p3 ;
l3 = p3 d n1 p4 ;
l4 = p4 d n2 p1 ;

s0 = daller l1 l2 l3 l4 plan ;
s1 = s0 moin (0. 0. 50.) ;

scl1 = l1 tran n3 (0. 0. -50.) ;
scl2 = l2 tran n3 (0. 0. -50.) ;
scl3 = l3 tran n3 (0. 0. -50.) ;
scl4 = l4 tran n3 (0. 0. -50.) ;
scl0 = scl1 et scl2 et scl3 et scl4 ;

v0 = s1 volu n3 s0 ;
elim 0.1 scl0 v0 ;

*---- calcul

*---- cas 1
youn1 =  200000. ;
youn2 =   20000. ;
youn3 =    2000. ;
youn0 = (youn1 + youn2 + youn3)/3.;
gcis0 = youn0 /(2.*(1.+0.3)) ;
valnu12 = 0.3 ; comm '=> valnu21 = 0.03 ' ;
valnu23 = 0.3 ; comm '=> valnu32 = 0.03 ' ;
valnu13 = 0.3 ; comm '=> valnu31 = 0.003' ;

mod0 = MODE v0 mecanique elastique orthotrope  ;
mat0 = MATE mod0 direction (1. 0. 0.) (0. 1. 0.) parallele
            YG1 youn1 YG2 youn2 YG3 youn3
            NU12 valnu12 NU23 valnu23 NU13 valnu13
            G12 gcis0
            G23 gcis0
            G13 gcis0
            RHO 8.e-6 ;
pres0 = pres mass mod0 0.5 s0 ;

rig0 = (rigi mod0 mat0) et (bloq depl scl0) ;

dep0 = reso rig0 pres0 ;

*---- depouillement
defo0 = defo v0 dep0 1. roug ;
depz0 = exco uz dep0 ;
fle1 = extr depz0 scal (v0 poin proc (0. 0. 0.)) ;
titre 'Deplacement en z et deformee (fleche 1 = 'fle1' mm)';
si graph ;
   trace cach (1.e8 1.e8 1.e8) depz0 defo0 ;
finsi ;

*---- cas 2
youn1 =  20000. ;
youn2 = 200000. ;
youn3 =   2000. ;
youn0 = (youn1 + youn2 + youn3)/3.;
gcis0 = youn0 /(2.*(1.+0.3)) ;
valnu12 = 0.03 ; comm '=> valnu21 = 0.3  ' ;
valnu23 = 0.3  ; comm '=> valnu32 = 0.003' ;
valnu13 = 0.3  ; comm '=> valnu31 = 0.03 ' ;

mat0 = MATE mod0 direction (0. 1. 0.) (1. 0. 0.) parallele
            YG1 youn1 YG2 youn2 YG3 youn3
            NU12 valnu12 NU23 valnu23 NU13 valnu13
            G12 gcis0
            G23 gcis0
            G13 gcis0
            RHO 8.e-6 ;

rig0 = (rigi mod0 mat0) et (bloq depl scl0) ;

dep0 = reso rig0 pres0 ;

*---- depouillement
defo0 = defo v0 dep0 1. roug ;
depz0 = exco uz dep0 ;
fle2 = extr depz0 scal (v0 poin proc (0. 0. 0.)) ;
titre 'Deplacement en z et deformee (fleche 2 = 'fle2' mm)';
si graph ;
   trace cach (1.e8 1.e8 1.e8) depz0 defo0 ;
finsi ;

*---------------- test si c'est ok
ladiff = abs(fle2 - fle1) ;
opti echo 0 ;
saut 1 lign ;
mess 'Différence entre les 2 résultats =' ladiff ;
si(ladiff > 1.e-10) ;
   erre 5 ;
finsi ;
fin ;






