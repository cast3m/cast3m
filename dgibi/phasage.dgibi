* fichier :  phasage.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
complet=vrai;
opti dime 3 elem cu20 mode trid;
opti epsilon lineaire;
am=-1; bm = -0.5; cm=-0.275;cp=0.275;bp=0.5;ap=1.;

*** nca2=10;nca=nca2*2;nd1=2; nd2=4;
si complet ;
nca2=5;nca=nca2*2;nd1=1; nd2=2;
sinon;
nca2=4;nca=nca2*2;nd1=1; nd2=2;
finsi;
pa = am am 20;pb=bm am 20; pc= cm am 20; pd = cp am 20;
pe= bp am 20; pf = ap am 20;
liba = pa droi nd1 pb droi nd1 pc droi nd2 pd droi nd1 pe droi nd1 pf;
su = liba tran nd1 ( 0 bp 0) trans nd1 ( 0 0.225 0.) trans nd2 (0 0.55 0)  tran nd1 ( 0 0.225 0) tran nd1 ( 0 BP 0);

*trac su;

vol1= su volu trans nca2 ( 0 0 -10);
 su2 = vol1 face 2;
 pceny = point su2 droite ( 0 0 10) ( 1 0 10) 0.51;
pcenxy = point pceny droite ( 0 0 10) ( 0 1 10) 0.51;

su21= elem su2 appu strict pcenxy coul rouge;

*trac ( su2 et su21);

vol2 = su21 volu trans nca2 (0 0 -10.);
su3 = vol2 face 2;

voltot = vol1 et vol2;

*trac voltot;

pc1d = cm cp 0.; pc1f = cm cp 20;
cab1 = pc1d droi nca pc1f chan seg2;
pc2d = cp cm 0.; pc2f = cp cm 20;
cab2 = pc2d droi nca pc2f chan seg2;
pc3d = cp cp 0.; pc3f = cp cp 20;
cab3 = pc3d droi nca pc3f chan seg2;
pc4d = cm cm 0.; pc4f = cm cm 20;
cab4 = pc4d droi nca pc4f chan seg2;
pc5d = 0 0 0.; pc5f = 0 0 20;
cab5 = pc5d droi nca pc5f chan seg2;

cable= cab1 et cab2 et cab3 et cab4 et cab5 coul bleu;
fer= cable plus (0 0 0 );
list ( nbno cable);

*elim voltot cable 0.01;


*trac ( vol2 et cable);


*
*  description des modeles
*
mobet1 = model vol1 mecanique elastique;
mobet2 = model vol2 mecanique elastique;
mobeton = mobet1 et mobet2;

mocab1= model cab1 mecanique elastique barr;
mocab2= model cab2 mecanique elastique barr;
mocab3= model cab3 mecanique elastique barr;
mocab4= model cab4 mecanique elastique barr;
mocab5= model cab5 mecanique elastique barr;

mofer = model fer mecanique elastique barr;
*
* description des materiaux et sections
*

mabet1= mate mobet1 young 40000e6 nu 0.2 rho 2500.;
mabet2= mate mobet2 young 40000e6 nu 0.2 rho 2500.;
mabeton = mabet1 et mabet2;

macab1= mate mocab1 young 193000e6 nu 0.3 rho 7859 sect 0.0025;
macab2= mate mocab2 young 193000e6 nu 0.3 rho 7859 sect 0.0025;
macab3= mate mocab3 young 193000e6 nu 0.3 rho 7859 sect 0.0025;
macab4= mate mocab4 young 193000e6 nu 0.3 rho 7859 sect 0.0025;
macab5= mate mocab5 young 193000e6 nu 0.3 rho 7859 sect 0.0025;

mafer = mate mofer young 100000e6 nu 0.3 rho 7859 sect 0.00001;
*
* condition aux limites de deplacement
*

pp= point voltot droite (0 0 0 )( 0 0 20) 0.01;
blxy = bloqu 'UX' 'UY' pp;
px= point su3 proche (0.5 0 0 );
bly = bloqu 'UY' px;
blz = bloqu 'UZ' su3;
claccr = rela accro  fer voltot 0.01;
cltot = blxy et bly et blz et claccr;

*
*  chargement du au poids
*

gamma = -9.81;
mmas = masse mobeton mabeton;
vz = manu chpo voltot 1 'UZ' gamma;
fpoi= mmas * vz;
evpoi = evol manu 'TEMPS' (prog 0. 10000.) 'COEF' (PROG 1. 1.);
charpoid= chargement fpoi evpoi;

*
* description des caracteristioques generales pour les pertes quasi instantanee
*

coefprec=table;

coefprec . 'FF' = 0.16 ;
coefprec . 'PHIF' = 0.0015;
coefprec . 'GANC' = 0.012;
coefprec . 'RMU0' = 0.45  ;
coefprec . 'FPRG' = 1940.e6 ;
coefprec . 'RH10' = 2.3;

*
* creation de la table des etapes pour le cas 2 il n'y a q'une etape
*
cas3etap=table;
cas2=table;
cas3etap . 1 = cas2 ;
cas2 . 'TPS' = 300.   ;

********************************** premier groupe

group1_1 = table;
cas2 . 1 = group1_1 ;

group1_1 . 'GEOMETRIE1' = manu poi1 pc2d;
group1_1 .  'MODELE' = mocab2;
group1_1 . 'MATERIAU' = macab2;
group1_1 . 'FORCE' = 3.75e6;
group1_1 . 'COEF_PREC' = coefprec;
group1_1 . 'TYPE_CAB' = '1EXT';

group1_2= table;
cas2 . 2 = group1_2 ;

group1_2 . 'GEOMETRIE1' = manu poi1 pc1d;
group1_2 .  'MODELE' = mocab1;
group1_2 . 'MATERIAU' = macab1;
group1_2 . 'FORCE' = 3.75e6;
group1_2 . 'COEF_PREC' = coefprec;
group1_2 . 'TYPE_CAB' = '1EXT';


***************************** deuxieme groupe
si complet;

cas3=table;
cas3etap . 2 = cas3;

cas3.'TPS'= 450.;
gr31=table;
cas3 . 1 = gr31;
gr31. 'GEOMETRIE1' = manu poi1 pc3d ;
gr31. 'MODELE' = mocab3;
gr31. 'MATERIAU' = macab3;
gr31. 'FORCE' = 3.75e6;
gr31. 'COEF_PREC' = coefprec;
gr31. 'TYPE_CAB' = '1EXT';

gr32=table;
cas3 . 2 = gr32;
gr32. 'GEOMETRIE1' = manu poi1 pc4d ;
gr32. 'MODELE' = mocab4;
gr32. 'MATERIAU' = macab4;
gr32. 'FORCE' = 3.75e6;
gr32. 'COEF_PREC' = coefprec;
gr32. 'TYPE_CAB' = '1EXT';

**************************** troisieme groupe

cas4=table;
cas3etap . 3 = cas4;
cas4.'TPS'= 600.;
gr41=table;
cas4 . 1 = gr41;
gr41. 'GEOMETRIE1' = manu poi1 pc5d ;
gr41. 'GEOMETRIE2' = manu poi1 pc5f ;
gr41. 'MODELE' = mocab5;
gr41. 'MATERIAU' = macab5;
gr41. 'FORCE' = 3.75e6;
gr41. 'COEF_PREC' = coefprec;
gr41. 'TYPE_CAB' = '2EXT';


finsi;

*
* appel a la procedur  tension
*

cas3etap = tension cas3etap;
*
* donnees relatives au levee
*
tlev = table;tlev1=table;tlev2=table;
tlev . 1 = tlev1;tlev . 2 = tlev2;
tlev1.'INSTANT' = 0.; tlev2.'INSTANT' = 150.;
tlev1.'MODELE' = mobet1;tlev2.'MODELE' = mobet2;
tlev1.'MATERIAU' = mabet1;tlev2.'MATERIAU' = mabet2;
tlev1.'COEF1'=70.;tlev2.'COEF1'=70.;
tlev1.'COEF2'= MANU CHML mobet1 'TAUX'  9.e-3 'STRESSES';
tlev2.'COEF2'= MANU CHML mobet2 'TAUX'  9.e-3 'STRESSES';
raysec1 = manu chml mobet1 'EPAI' 50. 'STRESSES';
raysec2 = manu chml mobet2 'EPAI' 100. 'STRESSES';
raysec = raysec1 et raysec2;
tlev1.'SECHAGE' = raysec1;tlev2.'SECHAGE' = raysec2;


*
* appel a phasage
*

tphas= table;

tphas.'FLUAGE' = 'BPEL99';
tphas.'RETRAIT' = 'BPEL99';
tphas.'PRECONTRAINTE'= cas3etap;
tphas.'LEVEES'= tlev;
tphas. 'TEMPS_FINAL' = 3000.;
tphas.'BLOCAGES' = cltot;
tphas.'DEPOU'= vrai;
tphas.'MOD_RESTE' = mofer;
tphas.'MAT_RESTE' = mafer;
*option debug ;
*option echo 2;




tphas = phasage TPHAS;


*opti sauv 'complet.res';

*sauv tphas;

*fin;

tsui = tphas.table_suite;
ttem= tsui.temps;
tdep= tsui.deplacements;
tcon= tsui.contraintes;
na = (dime ttem) - 1;
maxdepz= maxi( abs (exco tdep . na UZ));
maxcoz=maxi ( abs ( exco tcon . na SMZZ));
si  complet;
message ' calcul complet = vrai';
verdep=1.1017; vericont=1.86069e+07;
sinon ;message ' calcul complet = faux';
verdep= 1.0949        ; vericont= 8.37572e+06;
finsi;
mess ' deplacemment en Z maxi '  maxdepz ' comparé à ' verdep;
mess ' contrainte en Z maxi ' maxcoz ' comparée à ' vericont;


erdep= abs((maxdepz - verdep) / verdep );
ercont = abs ((maxcoz - vericont) / vericont);

si ( erdep  > 0.001) ;

   mess ' erreur deplacement ' erdep  ' comparé à 0.001';
   erreur  5;
finsi;
si ( ercont > 0.001 );

   mess ' errreur contrainte ' ercont ' comparé à 0.001';
   erreur  5;
finsi;

fin;























