* fichier : volu.dgibi
************************************************************************
* Section : Maillage Autres
************************************************************************
'OPTION' 'ECHO' 0 ;
************************************************************************
* NOM         : VOLU
* DESCRIPTION : Cas-test pour l'operateur VOLU.
*
*
*  Cas 1 : On cherche à mailler en tétraèdres un cube moins un cylindre.
*
*               Avec les paramètres nx = 3 ; rcyl = 0.88 ; ang = 5. ;
*               les éléments 118 et 120 ont les mêmes noeuds mais ont
*               des orientations opposées.
*               Bug corrige avec la fiche 9542
*
* Ameliorations possibles : mettre une batterie de cas problematiques au
* lieu d'un seul.
*
*
* LANGAGE     : GIBIANE-CAST3M
* AUTEUR      : Stéphane GOUNAND (CEA/DEN/DM2S/SEMT/LTA)
*               mél : stephane.gounand@cea.fr
**********************************************************************
* VERSION    : v1, 12/09/2017, version initiale
* HISTORIQUE : v1, 12/09/2017, création
* HISTORIQUE :
* HISTORIQUE :
************************************************************************
*
interact = faux ;
graph = faux ;
*
'OPTION' 'DIME' 3 'ELEM' 'TET4'  ;
'SI' (interact 'ET' graph) ;
   'OPTI' 'TRAC' open ;
'FINS' ;
*
* Paramètres : nx : nombre de maille
*              rcyl : rayon du cylindre interne
*              ang ; rotation du cylindre interne par rapport à son axe
*
nx = 3 ; rcyl = 0.88 ; ang = 5. ;
*
* Maillage de l'enveloppe
*
   nx2 = '*' nx 2 ;
   mrcyl = ('*' rcyl -1.) ;
   pA = rcyl 0. 0. ; pB = 0. rcyl 0. ; pC = mrcyl 0. 0. ;
   pD = 0. mrcyl 0. ;
   pA pB pC pD = 'TOURNER' pA pB pC pD (0. 0. 0.) (0. 0. 1.) ang ;
   pE = 1. -1. 0. ; pF = 1. 1. 0. ; pG = -1. 1. 0. ; pH = -1. -1. 0. ;
*
   ctc1 = 'CER3' nx2 pA pB pC ;   ctc2 = 'CER3' nx2 pC pD pA ;
   cerb = ctc1 'ET' ctc2 ;
   carb = 'DROIT' nx pE pF 'DROIT' nx pG 'DROIT' nx pH 'DROIT'
      nx pE ;
   bas = 'SURFACE' ('ET' carb ('INVERSE' cerb)) 'PLANE' ;
   hau cerh carh = 'PLUS' bas cerb carb (0. 0. 2.) ;
   suri = 'REGLER' nx cerb cerh ;
   sure = 'REGLER' nx carb carh ;
*
* On fait attention à l'orientation (normale rentrante pour env)
*
   env = bas 'ET' ('INVE' hau) 'ET' suri 'ET' ('INVE' sure) ;
   'SI' graph ;
      'TRAC' 'CACH' env 'TITR' 'Enveloppe' ;
*   vnor = @VISOR ('INVE' env) jaun ;
*   'TRAC' 'CACH' env vnor 'TITR' 'Enveloppe+normalesortante' ;
   'FINS' ;
*
* Maillage volumique
*
   'MESS' 'Appel VOLU' ;
   vol = 'VOLU' env ;
   'SAUT' 1 'LIGN' ;
   'MESS' 'Appel VOLU VERB' ;
   vol2 = 'VOLU' env 'VERB' ;
*
*  Tests divers (consistance...)
*
lok = vrai ;
*
* Le test avec 'DIFF' ne marche pas : les noeuds internes ont des numéros
* différents !
*
**  Identité de vol et vol2
**
*dvol = 'DIFF' vol2 vol ;
*'SI' ('NEG' ('NBEL' dvol) 0) ;
*   'MESS' '!!! maillages differents avec et sans le motcle VERB' ;
*   lok = lok 'ET' faux ;
*'FINS' ;
*  Identité de vol et vol2
*
dpvol = 'MOIN' vol2 vol ;
mdpvol = 'MAXI' dpvol 'ABS' ;
* Ce test semble strict mais on pense que l'on doit avoir égalité
* stricte des coordonnées (même programme exécutée sauf entrées-sorties)
* Si ca ne marche pas, on pourra mettre ('VALE' 'PREC') a la place.
'SI' ('>' mdpvol ('VALE' 'PETI')) ;
   'MESS' '!!! maillages differents avec et sans le motcle VERB' ;
   lok = lok 'ET' faux ;
'FINS' ;
*  éléments en double
vol3 = 'UNIQ' vol ;
dnod = '-' ('NBEL' vol) ('NBEL' vol3) ;
'SI' ('>' dnod 0) ;
   'MESS' '!!! elements en double dans vol' ;
   lok = lok 'ET' faux ;
'FINS' ;
*  enveloppe identique a l'original
denv = 'DIFF' ('ENVE' vol) env ;
'SI' ('NEG' ('NBEL' denv) 0) ;
   'MESS' '!!! enveloppe du maillage volu differente de loriginal' ;
   lok = lok 'ET' faux ;
'FINS' ;

*
* Test final
*
'SI' ('NON' lok) ;
   'ERREUR' 5 ;
'SINON' ;
   'SAUT' 1 'LIGN' ;
   'MESSAGE' ('CHAINE' 'Tout sest bien passe !') ;
'FINSI' ;
*
'SI' interact ;
   'OPTION' 'ECHO' 1 ;
   'OPTION' 'DONN' 5 ;
'FINSI' ;
*
* End of dgibi file VOLU
*
'FIN' ;





