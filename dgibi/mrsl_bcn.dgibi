* fichier :  mrsl_bcn.dgibi
* TRIAXIAL TEST WITH A NON-HOMOGENEOUS SAMPLE
* TEST: MRS-Lade model
*


fin;    probleme // :

*
*-------------  OPCIONES GENERALES  --------------------------------
*
GRAPH = 'N' ;
OPTION DIME 2 TRAC x ELEM qua4 MODE axis;
*
precomp = 600.D0;
valdesp = -0.05 *  1.5;
*
npoi    = 10;
npoi2   = 10;
*
*-------------  CREACION DE LA GEOMETRIA  -------------------------
*
 p1 = 0. 0.;
 p2 = 1. 0.;
 p3 = 1. 1.5;
 p4 = 0. 1.5;
 l12 = d npoi p1 p2;
 l23 = d npoi2 p2 p3;
 l34 = d npoi p3 p4;
 l41 = d npoi2 p4 p1;
 malla = daller l12 l23 l34 l41;
 mall1 = malla elem 1;
 da = 1.  / (flot npoi);
 db = 1.5 / (flot npoi2);
 aa = da 0.;
 bb = da db;
 cc = 0. db;
 la2 = d 1 aa p2;
 l4c = d 1 p4 cc;
 lcb = d 1 cc bb;
 lba = d 1 bb aa;
 contaux = la2 et l23 et l34 et l4c et lcb et lba;
 mall2 = malla inclus contaux 'BARY';
*
*--------------  MODELO  ------------------------------------------
*
 ROHOR  = 2.5D3;
 E_ELAS = 146.D3;
 NUHOR  = 0.2D0;
 PCC = 0.D0;
 PAA = 1.D0;
 QAA = 1.D0;
 EXM = 7.423D-2;
 EWW = 0.7D0;
 XK1 = 0.2D0;
 XK2 = 0.7256D0;
 ETB = 1.12D0;
 EXV = 1.15D0;
 XEP = 7.5D-1;
 XNN = -0.5D0;
 CCO = 4.D-3;
 EXL = 1.0867654D0;
 PCO = 5000.D0;
 EXR = 1.592D0;
 CCA = 0.000159D0;
 PHIs  = 0.D0;
 ALPas = 0.8D0;
 MOD1b = modl MALL2 mecanique elastique plastique mrs_lade;
 MAT1b = mater mod1b youn E_ELAS nu NUHOR rho ROHOR pc PCC pa PAA
                     qa QAA expm EXM e EWW k1 XK1 k2 XK2 etab ETB
                     expv EXV epsi XEP n XNN ccon CCO expl EXL
                     pcap PCO expr EXR ccap CCA phi PHIs alp ALPas;
*
 etb2 = etb*0.9D0;
 pco2 = pco*0.9D0;
 MOD1a = modl MALL1 mecanique elastique plastique mrs_lade;
 MAT1a = mater mod1a youn E_ELAS nu NUHOR rho ROHOR pc PCC pa PAA
                     qa QAA expm EXM e EWW k1 XK1 k2 XK2 etab ETB2
                     expv EXV epsi XEP n XNN ccon CCO expl EXL
                     pcap PCO2 expr EXR ccap CCA phi PHIs alp ALPas;
 modi = mod1a et mod1b;
 mati = mat1a et mat1b;
*
*--------------  PROBLEMA: PRECOMPRESION  -------------------------
*
 RIGconti = (bloq uz l12) et (bloq ur l41);
 valorp   = pres mass modi precomp (l23 et l34);
 evolu0   = EVOL MANU t (PROG 0. 1.) level (PROG 0. 1.);
 evfut0   = CHAR DIMP evolu0 valorp;
 maxiter  = 10;
 ti = TABLE ;
 ti.MODELE              = MODi;
 ti.CARACTERISTIQUES    = MATi;
 ti.BLOCAGES_MECANIQUES = RIGconti;
 ti.CHARGEMENT          = evfut0;
*ti.ACCELERATION        = maxiter;
*ti.MAXITERATION        = maxiter;
 ti.PRECISION           = 1.E-8;
*ti.CONVERGENCE_FORCEE  = faux;
*ti.K_TANGENT           = VRAI;
 ti.TEMPS_CALCULES      = PROG 0. 1.;
 ti.TEMPS_SAUVES        = PROG 0. 1.;
 PASAPAS ti ;
 desppre = ti.deplacements.1;
 form desppre;
*
*--------------  PROBLEMA: TRIAXIAL  ------------------------------
*
 mod1 = mod1a et mod1b;
 mat1 = mat1a et mat1b;
 RIGcont = (bloq uz l12) et (bloq ur l41);
*
*--------------  MOVIMIENTO IMPUESTO  -----------------------------
*
 rigsupe = (bloq uz l34);
 movyy   = (depi rigsupe valdesp);
 evolu2  = EVOL MANU t (PROG 0. 1.) level (PROG 0. 1.);
 evfut2  = CHAR DIMP evolu2 movyy;
 valorp  = pres mass mod1 precomp l23;
 evolu1  = EVOL MANU t (PROG 0. 1.) level (PROG 1. 1.);
 evfut1  = CHAR DIMP evolu1 valorp;
*
*--------------  RESOLUCION  --------------------------------------
*
 tiempos = prog 0. pas 0.04 0.48 pas 0.02 1.;
 maxiter = 15;
 t0 = TABLE ;
 t0.CONTRAINTES         = TABLE ;
 t0.CONTRAINTES.0       = ti.CONTRAINTES.1;
 t0.MODELE              = MOD1;
 t0.CARACTERISTIQUES    = MAT1;
 t0.BLOCAGES_MECANIQUES = RIGcont et rigsupe;
 t0.CHARGEMENT          = evfut2 et evfut1;
*t0.ACCELERATION        = maxiter;
*t0.MAXITERATION        = maxiter;
 t0.PRECISION           = 1.E-10;
*t0.CONVERGENCE_FORCEE  = faux;
 t0.K_TANGENT           = VRAI;
 t0.TEMPS_CALCULES      = tiempos;
 t0.TEMPS_SAUVES        = tiempos;
 PASAPAS t0 ;
*
*--------------  POSTPROCESO  -------------------------------------
*
 imax   = 38;
 fuer   = prog 0.;
 i      = 1;
 repeter blocdefi imax;
   aux  = reac rigsupe t0.deplacements.i;
   aux  = resu aux;
   pbas = extr aux MAIL ;
   pbas = POIN 1 pbas ;
   aux  = extr aux fz pbas;
   aux  = -1.D0 * aux;
   fuer = fuer et (prog aux);
   i=i+1;
 fin blocdefi;
 law = evol manu t (-1.0*(tiempos*valdesp)) f(t) fuer;
 SI (NEG GRAPH 'N') ;
   dessin law;
 FINSI ;
 err = (aux - 3.14205E+03)/3.14205E+03 ;
 list err;
 SI ((ABS err) < 0.5e-5) ;
   ERRE 0 ;
 SINON ;
   ERRE 5 ;
 FINSI ;
 FIN;




