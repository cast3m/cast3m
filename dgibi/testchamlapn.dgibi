* fichier : testchamlapn.dgibi
* Cet exemple montre et teste comment utiliser les CHAMELEMs avec
* l'opérateur LAPN (mais ce serait la même chose avec NS TSCA ou KONV)
* pour définir des propriétés physiques discontinues d'un matériau à un
* autre. C'est le cas d'une paroi métallique avec sa peinture.
*
* On maille la paroi métallique (wall), la peinture (peint) on crée un
* modèle 'NAVIER_STOKES' sur l'ensemble des deux mur = wall et peint;
* en PRECISANT le constituant (idem pour les deux).
* On déduit ensuite deux sous modèle $wall et $peint sur lesquels on
* définit respectivement les propriétés physiques souhaitées.
* On les assemble pour avoir un CHAMELEM unique (kk). Ensuite il EST
* NECESSAIRE de réduire ce coefficient sur le modèle $mur sur lequel
* porte l'opérateur LAPN.

DISCR = QUAF ;
opti dime 3 elem cub8 ;
p0 = 0. 0. 0. ;
p1 = 1. 0. 0. ;
l1= p0 d 3 p1;
co1 = l1 trans 2 (0. 1. 0.);
co2 = co1 plus (0. 0. 2.);
co2 = co2 coul verte;
co3 = co2 plus (0. 0. 0.1);
wall = co1 volu 2 co2 ;
peint = co2 volu 3 co3 ;
wall = wall coul rouge ;
peint= peint coul vert;
*trace (wall et peint) cache ;

Mwall = chan wall  QUAF ;
Mpeint= chan peint QUAF ;

$mur = model (Mwall et Mpeint) 'NAVIER_STOKES' DISCR 'CONS' toto;
*$mur = model (Mwall et Mpeint) mecanique elastique 'CONS' toto;

$wall  = redu $mur Mwall;
$peint = redu $mur Mpeint;


kwall = manu 'CHML' $wall 'SCAL'  46. ;
kpeint= manu 'CHML' $peint 'SCAL' 0.3 ;


kk = kwall et kpeint ;
list kk ;

ak= redu kk $mur ;
list ak ;

RV = EQEX  'OMEGA' 1.    'NITER' 1  ITMA 1   'FIDT' 1
  'OPTI' 'EF' 'IMPL' 'SUPG'
'ZONE' $mur 'OPER' 'LAPN' ak  'INCO' 'TN';

rv.inco=table 'INCO';
rv.inco.'TN'=kcht $mur scal sommet 1.;
a b =lapn rv.'1LAPN';

FIN;


