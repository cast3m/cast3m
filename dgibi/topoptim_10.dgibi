* fichier topoptim_10.dgibi
************************************************************************
************************************************************************

************************************************************************
** Non-linear topology optimization of a simple 2D structure in contact
** with a rigid sphere with prescribed displacement.
**
** Author:
** Guenhael Le Quilliec (LaMe - Polytech Tours)
**
** Version:
** V1.0 2017/04/18  Original version compatible with TOPOPTIM V2.1
************************************************************************

graph0 = FAUX ;

* General options
OPTI 'DIME' 2 'MODE' 'AXIS' 'ELEM' QUA4 ;

* Bulk mesh
p0 =  0.0      0.0 ;
p1 =  0.0    -30.0e-3 ;
p2 = 30.0e-3 -30.0e-3 ;
p3 = 30.0e-3   0.0 ;
lgn0 = DROI 30 p0 p1 ;
lgn1 = DROI 30 p1 p2 ;
lgn2 = DROI 30 p2 p3 ;
lgn3 = DROI 30 p3 p0 ;
msh0 = SURF (lgn0 ET lgn1 ET lgn2 ET lgn3) ;

* Indent mesh
p5 =  0.0    20.0e-3 ;
p6 = 20.0e-3 20.0e-3 ;
arc0 = CERC 30 p0 p5 p6 ;

* Model and material
mod0 = MODE msh0 'MECANIQUE' 'ELASTIQUE' ;
mat0 = MATE mod0 'YOUN' 0.1e9 'NU' 0.4 ;

* Time steps
nbstp0 = 20 ;
time0 = 2.0 ;
lst_tps0 = PROG 0.0 'PAS' (time0 / (FLOT nbstp0)) time0 ;

* Boundary conditions and loading
bc1 = (BLOQ 'UR' lgn0) ET (BLOQ 'UZ' lgn1) ET (BLOQ 'UR' arc0) ;
bc2 = BLOQ 'UZ' arc0 ;
evol0 = EVOL 'MANU' 'Temps' lst_tps0 'Coef'
              (PROG 0.0 'PAS' (1.0 / (FLOT nbstp0)) 1.0) ;
load0 = CHAR 'DIMP' (DEPI bc2 -2.0e-3) evol0 ;

* Contact
ct0 = IMPO 'MAIL' 5.0 arc0 lgn3 ;
modct0 = MODE ct0 'CONTACT' 'FROTTANT' 'COULOMB' ;
matct0 = MATE modct0 'MU' 0.5 ;

* Factor evolutions
pfct0 = EVOL 'MANU' 'CYCLES' (PROG 1  6) 'P' (PROG 1.0  3.0) ;
qfct0 = EVOL 'MANU' 'CYCLES' (PROG 2 32) 'Q' (PROG 1.0 10.0) ;

* Finite element model table
mdl0 = TABL ;
mdl0.'MODELE'              = mod0 ET modct0 ;
mdl0.'CARACTERISTIQUES'    = mat0 ET matct0 ;
mdl0.'BLOCAGES_MECANIQUES' = bc1 ET bc2 ;
mdl0.'CHARGEMENT'          = load0 ;
mdl0.'TEMPS_CALCULES'      = lst_tps0 ;

* Optimization table
tab0 = TABL ;
tab0.'RESOLUTION_PASAPAS'  = mdl0 ;
tab0.'FRACTION_VOLUME'     = 0.2 ;
tab0.'FACTEUR_P'           = pfct0 ;
tab0.'FACTEUR_Q'           = qfct0 ;
tab0.'SEUIL'               = 1.0e-2 ;
tab0.'MES_SAUVEGARDES'     = TABL ;
tab0.'MES_SAUVEGARDES'.'RESOLUTION' = VRAI ;
tab0.'TRAC'                = graph0 ;

* Optimization
TOPOPTIM tab0 ;

* Output data
topo0 = tab0.'TOPOLOGIE'.(tab0.'CYCLE') ;
topomsh0 = tab0.'MAILLAGE'.(tab0.'CYCLE') ;
res0 = tab0.'RESOLUTION'.(tab0.'CYCLE') ;

* Deforme the mesh
DEPL (msh0 ET arc0) 'PLUS' res0.'DEPLACEMENTS'.(1) ;

* Plot to screen
SI graph0 ;
    TRAC (topomsh0 ET (arc0 COUL 'BLEU')) ;
    TRAC (REDU topo0 topomsh0) (REDU mod0 topomsh0)
         (PROG 0.0 'PAS' (1.0 / 56.0) 1.0) ;
FINS ;

FIN ;


