* fichier : faceaface3.dgibi
************************************************************************
* Section : Thermique Transitoire
* Section : Thermique Rayonnement
* Section : Thermique Convection
************************************************************************

************************************************************************
*......../........./........./........./........./........./........./72
* test de calcul FACE a FACE avec PASAPAS
* Echange thermique entre deux faces proches (L/e>>10) ,
* en convection, en rayonnement
* et mixte rayonnement plus convection
*                                          Philippe YALA le 14/12/09
*
*    VERSION 3D de faceaface.dgibi
************************************************************************
OPTI DIME 3 ELEM CUB8 ;
************************************************************************
* CALCUL EN GEOMETRIE 3D
* deux cylindres coaxiaux avec convection externe
* 1 calcul avec CONV dans le jeu ;  H=Cd_Air/Ep
* 2 calcul avec RAYO dans le jeu
* 3 calcul complet avec CONV et RAYO
************************************************************************
*DONNEES GEOMETRIQUES
* rayon arbre
rarbre = 10.e-3 ;
* jeu type H7 g6 ; soit en micron : +15/0  -5/-14
jeumin = 0. + 5.e-6 ;
jeumax = 15.e-6 + 14.e-6 ;
 graph= mot 'N';
jeu = jeumax ;
* ou jeu = jeumin ;

* epaisseur alesage
epal = 5.e-3 ;
* hauteur
haut = 5.e-3 ;
************************************************************************
* MAILLAGE presque sans discretisation du jeu ( noeud double)
P0 = 0. 0. 0.;
P1 = 0. haut 0.;
laxe = P0 droit P1 DINI 0.002 DFIN 0.001 ;
arbre = (laxe trans (rarbre 0. 0.) DINI 0.002 DFIN 0.001 ) coul vert ;
lor1 lhaut lcot1 lbas = cote arbre ;
EPS = 1.e-5 ;
* Remarque ne fonctionne pas avec EPS=0. pour RAYO
*lcot2 = lcot1 plus ( EPS 0. 0.) ;
lcot2 = lcot1 plus ( EPS*10 0. 0.) ;
alesage = ( lcot2 trans ( epal 0. 0.) DINI 0.001 DFIN 0.002 )
coul rouge ;
lor2 lhaut2 lcotext lbas2 = cote alesage ;

arbre = VOLU arbre 60 ROTA -360 (0. 0. 0.) (0. -1. 0.) ;
alesage = VOLU alesage 60 ROTA -360 (0. 0. 0.) (0. -1. 0.) ;

ARBRE = ARBRE COUL VERT ;
ALESAGE = ALESAGE COUL ROUG;

elim arbre  0.000001 ;
elim  alesage 0.000001 ;

arbre = REGENERER ARBRE ;

tot  = arbre et alesage ;
si ( ega graph 'O');
  trac  CACHE TOT face;
FINSI ;
LCOTEXT = LCOTEXT ROTA 60 -360 (0. 0. 0.) (0. -1. 0.) ;
ELIM ( ALESAGE ET LCOTEXT) 0.000001 ;




************************************************************************
*DONNEES THERMIQUES
MESS ;
MESS 'DONNEES THERMIQUES';
TK = 273.15 ;MESS 'Zero °C' TK ;
EMA = 0.8  ;MESS 'Emissivité ARBRE' EMA ;
EMB = 0.9  ;MESS 'Emissivité ALESAGE cote interne' EMB ;
EMC = 1.0 ;MESS 'Emissivité ALESAGE cote externe ' EMC ;
TAMB = 30.0 ;MESS 'Température ambiance' TAMB '°C' ;
TINI = 500.0 ;MESS 'Température initiale' TINI '°C' ;
TAMB = TAMB + TK ; TINI = TINI +TK ;

************************************************************************
*MODELISISATION THERMIQUES
*conduction
MCD1  = MODE ARBRE THERMIQUE ISOTROPE ;
MCD2  = MODE ALESAGE THERMIQUE ISOTROPE ;
MAT1  = MATE MCD1  'K'  15.0 'C'  500.0  'RHO' 7800.0   ;
MAT2  = MATE MCD2  'K'  132. 'C'  945.0  'RHO' 2670.0  ;
************************************************************************
* convection extérieure
MOHEX = MODE lcotext 'THERMIQUE'  'CONVECTION' ;
MAHEX = MATE MOHEX 'H' EMC ;
TCHA = PROG 0.   1.e10  ;
PINC = PROG 1. 1.  ;
MEVOLTE = EVOL 'MANU' 'TEMP' TCHA 'AMP' PINC ;
CH_TE = 'MANU' 'CHPO' lcotext 1 'T' TAMB ;
CHAT = CHAR 'TECO' CH_TE MEVOLTE ;
************************************************************************
* initialisation en temperature
TEM0 = MANU CHPO TOT  1 T TINI ;
************************************************************************
* Temps calculés
PTPS1 = prog 0. 0.01 0.02 0.05  0.1 0.2  0.5 1. 2. 4. 6. pas 2. 100. ;
PTPS2 = prog  20. pas 5.  50. pas 10.  100. pas 20. 600. ;
PTPS = PTPS1   ;
*ptps= prog 0. 0.01 0.02 0.05 ;
* Temps sauvés
PTPSS = prog  0. pas 4. 100. ;
************************************************************************

* Convection bilatérale (face a face)
*-------------------------------
cont1 = enve arbre ;
elim (cont1 et arbre) eps ;
PT1 = point cont1 cyli ( 0 0 0 ) (0 -1. 0 ) (rarbre 0. 0.) 0.0001 ;
lcot1 = elem cont1 appu strict PT1 ;
elim (lcot1 et cont1 et arbre) 0.0001 ;
cont2 = enve alesage ;
elim (cont2 et alesage) eps ;
PT2 = point cont2 cyli ( 0 0 0 ) (0 -1. 0)
((rarbre+(EPS*10)) 0. 0.) 0.0001 ;
lcot2 = elem cont2 appu strict PT2 ;
elim (lcot2 et cont2 et alesage) 0.0001 ;
rac12 = LIAI 1.1e-4 lcot1 lcot2 ;

 mcv12 = MODE rac12 thermique CONVECTION ;
 h12 = 0.004 /jeu ;
 ma12 = MATE mcv12 'H' h12 ;


*****************ASSEMBLAGE DES MATÉRIAUX ET DES MODÈLES****************
*CALCUL source + convection face a face + rayo. ext.

MODU1 = MCD1 et MCD2 et MOHEX et mcv12 ;
MATU1 = MAT1 et MAT2 et MAHEX et ma12;

********************CRÉATION DE LA TABLE POUR PASAPAS*******************
TAPAS = TABLE ;
TAPAS.CELSIUS = FAUX ;
TAPAS.CRITERE = 1E-4 ;
TAPAS.MODELE = MODU1 ;
TAPAS.CARACTERISTIQUES = MATU1 ;
TAPAS.TEMPERATURES = TABLE ;
TAPAS.TEMPERATURES . 0 = TEM0 ;
TAPAS.CHARGEMENT = CHAT ;
TAPAS.PROCEDURE_THERMIQUE = DUPONT  ;
*TAPAS.PROCEDURE_THERMIQUE = NONLINEAIRE  ;
*TAPAS.RELAXATION_THETA = 0.6 ;
TAPAS.TEMPS_CALCULES = PTPS ;
TAPAS.TEMPS_SAUVES = PTPSS ;


************************************************************************
PASAPAS TAPAS ;

CTK1 = TAPAS.TEMPERATURES.((DIME PTPSS) - 1 )  ;
CTC1 = CTK1 - TK ;
titre  NOMCAL '-Isothermes   ' ;
*TRAC CTC1 TOT ;
* suivi de la temperature
TEMP1 = PROG ;TEMP2 = PROG ;
ITER = 0 ;
NBT1 = (DIME PTPSS) ;
REPETER BCL1  NBT1 ;
TP1 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) arbre   ;
T1 = MAXIMUM ( TP1 ) - TK ;
TEMP1 = INSERER TEMP1 &BCL1 T1 ;
TP2 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) alesage  ;
T2 = MAXIMUM ( TP2 ) - TK ;
TEMP2 = INSERER TEMP2 &BCL1 T2 ;
ITER = ITER + 1;
FIN BCL1 ;

TITRE ' EVOLUTION DES TEMPERATURES' ;
EV1 = EVOL VERT MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP1 ;
EV2 = EVOL VERT MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP2 ;
TABDES = TABLE; TABDES.'TITRE' = TABLE ;
TABDES.1 = 'MARQ TRIA' ;TABDES.'TITRE'. 1 = 'Arbre_Conv' ;
TABDES.2 = 'MARQ CROI' ;TABDES.'TITRE'. 2 = 'Alesage_Conv' ;
*DESS (EV1 ET EV2  ) TABDES 'MIMA' 'LEGE' ;
* opti donn 5;
************************************************************************
*  CALCUL 2 jeu en rayonnement pur
************************************************************************
* RAYONNEMENT bilatérale (face a face)
*-----------------------------------------------------------------------
geor = diff ( rac12 chan ligne ) (CHAN LIGN (lcot1 et lcot2)) ;
*list geor ;
*trac geor ;
mcv12r = MODE RAC12 THERMIQUE 'CONVECTION'  ;

 mora1 = mode lcot1 THERMIQUE 'RAYONNEMENT'
    fac_a_fac  lcot1 lcot2 geor  mcv12r   CONS 'FACAFAC1';
 mora2 = mode lcot2 THERMIQUE 'RAYONNEMENT'
    fac_a_fac  lcot1 lcot2 geor  mcv12r CONS 'FACAFAC1';

MORA12 = mora1 et mora2 ;



 mar1 = MATE mora1 'EMIS' EMA ;
 mar2 = MATE mora2 'EMIS' EMB ;
 MAR12  =  mar1 et  mar2 ;

*****************ASSEMBLAGE DES MATÉRIAUX ET DES MODÈLES***************

*CALCUL source + rayonnement face a face + rayo. ext.

MODU2 = MCD1 et MCD2 et MOHEX  et mora12;
MATU2 = MAT1 et MAT2 et MAHEX et MAR12 ;
******************* MODIFICATION DE LA TABLE POUR PASAPAS *************
TAPAS = TABLE ;
TAPAS.CELSIUS = FAUX ;
TAPAS.CRITERE = 1E-4 ;
TAPAS.MODELE = MODU2;
TAPAS.CARACTERISTIQUES = MATU2  ;
TAPAS.TEMPERATURES = TABLE ;
TAPAS.TEMPERATURES . 0 = TEM0 ;
TAPAS.CHARGEMENT = CHAT ;
TAPAS.PROCEDURE_THERMIQUE = DUPONT  ;
TAPAS.TEMPS_CALCULES = PTPS ;
TAPAS.TEMPS_SAUVES = PTPSS ;
***********************************************************************
PASAPAS TAPAS ;
*opti donn 5;
CTK1 = TAPAS.TEMPERATURES.((DIME PTPSS) - 1)  ;
CTC1 = CTK1 - TK ;
titre  NOMCAL '-Isothermes - '  ;
*TRAC CTC1 TOT ;
* suivi de la temperature
TEMP1 = PROG ;TEMP2 = PROG ;
ITER = 0 ;
NBT1 = (DIME PTPSS) ;
REPETER BCL1  NBT1 ;
TP1 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) arbre   ;
T1 = MAXIMUM ( TP1 ) - TK ;
TEMP1 = INSERER TEMP1 &BCL1 T1 ;
TP2 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) alesage  ;
T2 = MAXIMUM ( TP2 ) - TK ;
TEMP2 = INSERER TEMP2 &BCL1 T2 ;
ITER = ITER + 1;
FIN BCL1 ;

TITRE ' EVOLUTION DES TEMPERATURES' ;
EV3 = EVOL BLEU MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP1 ;
EV4 = EVOL BLEU MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP2 ;

TABDES.3 = 'MARQ TRIA' ;TABDES.'TITRE'. 3 = 'Arbre_Rayo' ;
TABDES.4 = 'MARQ CROI' ;TABDES.'TITRE'. 4 = 'Alesage_Rayo' ;
*DESS (EV1 ET EV2  et EV3 ET EV4 ) TABDES 'MIMA' 'LEGE' ;
*opti donn 5;
************************************************************************
*  CALCUL 3 jeu en convection et rayonnement
************************************************************************
*****************ASSEMBLAGE DES MATÉRIAUX ET DES MODÈLES***************
MODU3 = MODU1  et mora12;
MATU3 = MATU1 et MAR12 ;
******************* MODIFICATION DE LA TABLE POUR PASAPAS *************
TAPAS = TABLE ;
TAPAS.CELSIUS = FAUX ;
TAPAS.CRITERE = 1E-4 ;
TAPAS.MODELE = MODU3 ;
TAPAS.CARACTERISTIQUES = MATU3 ;
TAPAS.TEMPERATURES = TABLE ;
TAPAS.TEMPERATURES . 0 = TEM0 ;
TAPAS.CHARGEMENT = CHAT ;
TAPAS.PROCEDURE_THERMIQUE = DUPONT  ;
TAPAS.TEMPS_CALCULES = PTPS ;
TAPAS.TEMPS_SAUVES = PTPSS ;
TAPAS.RAYONNEMENT = TABLE ;
***********************************************************************
PASAPAS TAPAS ;

CTK1 = TAPAS.TEMPERATURES.((DIME PTPSS) - 1)  ;
CTC1 = CTK1 - TK ;
titre  NOMCAL '-Isothermes -  '  ;
*TRAC CTC1 TOT ;
* suivi de la temperature
TEMP1 = PROG ;TEMP2 = PROG ;
ITER = 0 ;
NBT1 = (DIME PTPSS) ;
REPETER BCL1  NBT1 ;
TP1 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) arbre   ;
T1 = MAXIMUM ( TP1 ) - TK ;
TEMP1 = INSERER TEMP1 &BCL1 T1 ;
TP2 = REDU (EXCO 'T' (TAPAS.'TEMPERATURES'.ITER ) 'T' ) alesage  ;
T2 = MAXIMUM ( TP2 ) - TK ;
TEMP2 = INSERER TEMP2 &BCL1 T2 ;
ITER = ITER + 1;
FIN BCL1 ;
TITRE ' EVOLUTION DES TEMPERATURES' ;
EV5 = EVOL ROUG MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP1 ;
EV6 = EVOL ROUG MANU 'TEMPS (s)' PTPSS 'T (C)' TEMP2 ;
TABDES.5 = 'MARQ TRIA' ;TABDES.'TITRE'. 5 = 'Arbre_Conv&Rayo' ;
TABDES.6 = 'MARQ CROI' ;TABDES.'TITRE'. 6 = 'Alesage_Conv&Rayo' ;
si ( ega graph 'O');
DESS (EV1 ET EV2  et EV3 ET EV4 et EV5 ET EV6) TABDES 'MIMA' 'LEGE' ;
finsi;
aa1 = extr ev1 ordo;aa1 = mini aa1 ;er1= abs(aa1 - 499.07);
aa2 = extr ev2 ordo;aa2 = mini aa2 ;er2= abs(aa2 - 496.77);
aa3= extr ev3 ordo; aa3 = mini aa3 ;er3= abs(aa3 - 499.38);
aa4= extr ev4 ordo; aa4 = mini aa4 ;er4= abs(aa4 - 496.37);
aa5= extr ev5 ordo; aa5 = mini aa5 ;er5= abs(aa5 - 498.83);
aa6= extr ev6 ordo; aa6 = mini aa6 ;er6= abs(aa6 - 497.08);


mess aa1 aa2 aa3  aa4  aa5    aa6 ;
mess er1 er2 er3 er4 er5 er6 ;
err= er1 + er2 + er3 + er4 + er5 + er6;
list err;
si ( err > 5.e-2) ; erreur 5; finsi;


************************************************************************
*                             FIN DU FICHIER                           *
************************************************************************
FIN ;













