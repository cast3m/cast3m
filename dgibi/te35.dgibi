* fichier :  te35.dgibi
************************************************************************
* Section : Mecanique Fluage
************************************************************************
 'OPTI' 'DIME' 2 'ELEM' 'QUA4' 'MODE' 'PLAN' 'CONT' ;
**
**
**
**       D4          S1         D2
**
**       P1          D1          P2
*
GRAPH = 'N' ;
*
*  OPTIONS DE CALCUL
* CHOIMEC = 1 OTTOSEN
* CHOIMEC = 2 CERAMIQUE
* CHOIMEC = 3 FLUAGE
CHOIMEC = 2;
* DANS LE CAS OU ON A LE MODELE CERAMIQUE ON PEUT INACTIVER OTTOSEN
* INACTO = VRAI
* OU INACTIVER LE FLUAGE
* INACTF = VRAI
'SI' ('EGA' CHOIMEC 2) ;
  INACTO = FAUX ;
  INACTF = FAUX ;
'FINS' ;
CHOICAS = 1;
* CHOICAS = 1
* ON CASSE EN DIMINUANT LA TEMPERATURE AU DELA DU TEMPS 5E-5
* On passe de 1500 à 1400
* CHOICAS = 2
* ON CASSE EN AUGMENTATION LA VITESSE DE DEFORMATION /2
* CHOICAS = 3 On reste à T = cte
* AU DELA DU TEMPS 5E-5
* facteur de multiplication des temps qui vont de 1 à 9 MULT1
MULT1 = 1.E-5 ;
*
X1 = 10. ;
X2 = 11. ;
P1 = X1 0. ;
P2 = X2 0. ;
D1 = 'DROI' 10 P1 P2 ;
S1 = 'TRAN' D1 10 (0. 5.) ;
'SI' ('NEG' GRAPH 'N') ;
  'TRAC' S1 ;
'FINS' ;
D2 = 'COTE' 2 S1 ;
D3 = 'COTE' 3 S1 ;
D4 = 'COTE' 4 S1 ;
PP = D2 'POIN' 'FINAL' ;
** THERMIQUE
* Température imposée à l'extérieur et l'intérieur de la
* meule
TCHOIX = 1500. ;
CHTER = 'MANU' 'CHPO' S1 1 'T' TCHOIX ;
'SI' ('EGA' CHOICAS 1 );
  TCHOIX2 = 1400. ;
  CHTER2 = 'MANU' 'CHPO' S1 1 'T' TCHOIX2 ;
'SINO' ;
  CHTER2 = CHTER ;
'FINS' ;
**********************************************
************** MODELE ET MATERIAUX MECANIQUE
**********************************************
*
YOYO = 5.E11 ;
NUNU = 0.3 ;
DILA = 9.E-26 ;
RORO = 9000. ;
* OTTOSEN
'SI' ('EGA' CHOIMEC 1) ;
  MODME = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'OTTOSEN' ;
  MATME = 'MATE' MODME 'YOUN' YOYO 'NU' NUNU
   'RHO' RORO 'ALPH' DILA;
  MAT2 = 'TAIL' MODME ;
'FINS' ;
'SI' ( 'OU' ('EGA' CHOIMEC 2) ('EGA' CHOIMEC 3)) ;
  SIMAX = 1.E6 ;
  QFLUGU = -5.3/8.61735E-5 ;
  NTFLUGU = 1. ;
  NFLUGU = 3.9 ;
  AFLUGU = ('EXP' 28.8)/(1.E6**3.9);
  CHTER3 = 0. ;
  'REPE' BOUOU 100 ;
    CHTER3 = CHTER3 + 20.;
    AVFLUK = AFLUGU * ('EXP' (QFLUGU * (CHTER3**-1.))) ;
    'SI' ('<' CHTER3 1400.) ;
      AVFLUK = 1.E-40 ;
    'FINS' ;
    'SI' ('EGA' &BOUOU 1) ;
      PROT  = 'PROG' CHTER3 ;
      PROAV = 'PROG' AVFLUK ;
    'SINO' ;
      PROT  = 'INSE' PROT  &BOUOU CHTER3 ;
      PROAV = 'INSE' PROAV &BOUOU AVFLUK ;
    'FINS' ;
  'FIN' BOUOU ;
  AVFLUKE = 'EVOL' 'MANU' 'T' PROT 'AF1' PROAV;
* CERAMIQUE
  'SI' ('EGA' CHOIMEC 2) ;
    MODME = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' 'FLUAGE' 'CERAMIQUE' ;
    'SI' ('OU' INACTO INACTF) ;
      'SI' INACTO ;
        MATME = 'MATE' MODME 'YOUN' YOYO 'NU' NUNU
                'RHO' RORO 'ALPH' DILA 'SMAX' SIMAX
                'AF1' AVFLUKE 'AF2' NFLUGU 'AF3' NTFLUGU
                'LTR' 1.E20 'GS' 1.E20 'TTRA'  0.  'ENDG' 100.01;
      'FINS' ;
      'SI' INACTF ;
        AVFLUK = 1.E-40 ;
        MATME = 'MATE' MODME 'YOUN' YOYO 'NU' NUNU
                'RHO' RORO 'ALPH' DILA 'SMAX' SIMAX
                'AF1' AVFLUKE 'AF2' NFLUGU 'AF3' NTFLUGU
                'TTRA'  0.  'ENDG' 100.01;
      'FINS' ;
    'SINO' ;
      MATME = 'MATE' MODME 'YOUN' YOYO 'NU' NUNU
              'RHO' RORO 'ALPH' DILA 'SMAX' SIMAX
              'AF1' AVFLUKE 'AF2' NFLUGU 'AF3' NTFLUGU
              'TTRA'  0.  'ENDG' 100.01;
    'FINS' ;
    MAT2 = 'TAIL' MODME ;
  'FINS' ;
  'SI' ('EGA' CHOIMEC 3) ;
    MODME = 'MODE' S1 'MECANIQUE' 'ELASTIQUE' 'FLUAGE' 'NORTON' ;
    MATME = 'MATE' MODME 'YOUN' YOYO 'NU'NUNU
            'RHO' RORO 'ALPH' DILA 'SMAX' SIMAX
            'AF1' AVFLUKE 'AF2' NFLUGU 'AF3' NTFLUGU ;
  'FINS' ;
'FINS' ;
***************************************************
******** BLOQUAGES MECA
*********************************************
* BLOCAGE DE D1
BLOCO = 'BLOQ' 'UY' D1 ;
* BLOCAGE DES POINTS DE D3
BLOCO3 = 'BLOQ' 'UY' D3 ;
BLOP = 'BLOQ' 'UX' 'UY' P1 ;
*
'SI' ('EGA' CHOICAS 2) ;
  FACTI = 10. ;
'SINO' ;
  FACTI = 1.;
'FINS' ;
DITER = DILA * 1500.*5. ;
'MESS' 'DITER = ' DITER ;
CHAMPI   = 'DEPI' BLOCO 0. ;
CHAMPI31 = 'DEPI' BLOCO3 DITER  ;
CHAMPI32 = 'DEPI' BLOCO3 (DITER + 0.5E-3) ;
CHAMPI33 = 'DEPI' BLOCO3 (DITER + 1.E-3) ;
CHAMPI34 = 'DEPI' BLOCO3 (DITER + 1.5E-3) ;
CHAMPI35 = 'DEPI' BLOCO3 (DITER + 2.E-3) ;
*
HILO = 2.E-3 + (0.5E-3*FACTI) ;
CHAMPI36 = 'DEPI' BLOCO3 (DITER + HILO) ;
HILO = HILO + (0.5E-3*FACTI) ;
CHAMPI37 = 'DEPI' BLOCO3 (DITER + HILO) ;
HILO = HILO + (0.5E-3*FACTI) ;
CHAMPI38 = 'DEPI' BLOCO3 (DITER + HILO) ;
HILO = HILO + (0.5E-3*FACTI) ;
CHAMPI39 = 'DEPI' BLOCO3 (DITER + HILO) ;
* Table des temps
TATEMPS = 'TABLE' ;
TATEMPS.0 = 0. ;
TATEMPS.1 = 1.*MULT1 ;
TATEMPS.2 = 2.*MULT1 ;
TATEMPS.3 = 3.*MULT1 ;
TATEMPS.4 = 4.*MULT1 ;
TATEMPS.5 = 5.*MULT1 ;
TATEMPS.6 = 6.*MULT1 ;
TATEMPS.7 = 7.*MULT1 ;
TATEMPS.8 = 8.*MULT1 ;
TATEMPS.9 = 9.*MULT1 ;
TATEMPS.10 = 10.*MULT1 ;
TATEMPS.11 = 11.*MULT1 ;
TATEMPS.12 = 12.*MULT1 ;
* Table d'évolution de la température
TABTER = 'TABLE' ;
TABTER.0 = 'MANU' 'CHPO' S1 1 'T' 0. ;
TABTER.1 = CHTER;
TABTER.2 = CHTER;
TABTER.3 = CHTER;
TABTER.4 = CHTER;
TABTER.5 = CHTER;
TABTER.6 = CHTER2;
TABTER.7 = CHTER2;
TABTER.8 = CHTER2;
TABTER.9 = CHTER2;
TABTER.9 = CHTER2;
TABTER.10 = CHTER2;
TABTER.11 = CHTER2;
TABTER.12 = CHTER2;
* Table du chargement mécanique
FORTOT = CHAMPI;
CHARMEC = 'TABLE' ;
CHARMEC.0  = FORTOT  ;
CHARMEC.1  = FORTOT 'ET' CHAMPI31;
CHARMEC.2  = FORTOT 'ET' CHAMPI32;
CHARMEC.3  = FORTOT 'ET' CHAMPI33;
CHARMEC.4  = FORTOT 'ET' CHAMPI34;
CHARMEC.5  = FORTOT 'ET' CHAMPI35;
CHARMEC.6  = FORTOT 'ET' CHAMPI36;
CHARMEC.7  = FORTOT 'ET' CHAMPI37;
CHARMEC.8  = FORTOT 'ET' CHAMPI38;
CHARMEC.9  = FORTOT 'ET' CHAMPI39;
CHARMEC.10 = FORTOT 'ET' CHAMPI33;
CHARMEC.11 = FORTOT 'ET' CHAMPI33;
CHARMEC.12 = FORTOT 'ET' CHAMPI33;
* Chargement temps force
CHARFOR = 'CHAR' 'MECA' TATEMPS CHARMEC ;
* Chargement temps température
CHARGT = 'CHAR' 'T' TATEMPS TABTER ;
TABA = 'TABLE' ;
TABA.'CHARGEMENT' = CHARGT 'ET' CHARFOR ;
LISTEMPS = 'PROG' 0. 1. 'PAS' 0.2 5. 'PAS' 0.01 5.2 'PAS' 0.2 9.;
LISTEMPS = LISTEMPS * MULT1 ;
TABA.'MODELE' = MODME ;
'SI' ('OU' ('EGA' CHOIMEC 1) ('EGA' CHOIMEC 2)) ;
  TABA.'CARACTERISTIQUES' = MATME 'ET' MAT2;
'SINO' ;
  TABA.'CARACTERISTIQUES' = MATME ;
'FINS' ;
TABA.'TEMPS_CALCULES' = LISTEMPS ;
TABA.'TEMPS_SAUVES' = LISTEMPS ;
TABA.'PRECISON' = 1.E-4 ;
TABA.'BLOCAGES_MECANIQUES' = BLOCO 'ET' BLOCO3 'ET' BLOP;
TABA.'MAXITERATION' = 500 ;
TABA.'PROCESSEURS' = 'AUTOMATIQUE';
PASAPAS TABA ;
I = 'DIME' TABA.'TEMPS_SAUVES';
*
'SI' ('OU' ('EGA' CHOIMEC 1) ('EGA' CHOIMEC 2)) ;
* On d'extrait les fissures
* on extrait les variables internes
  FIFISS = TABA.'VARIABLES_INTERNES'.(I-1) ;
  VECI = 'VECT' FIFISS MODME  .01 ;
*
  'SI' ('NEG' GRAPH 'N') ;
    'TRAC' S1 VECI ;
  'FINS' ;
'FINS' ;

MA = 'ELEM' 61 S1 ;
'REPE' BOILA I ;
* Contraintes
  SI1 =  'REDU' TABA.'CONTRAINTES'.(&BOILA-1)  MA ;
  SILO2 = 'EXTR' SI1 'SMXX' 1 1 2 ;
  SILO3 = 'EXTR' SI1 'SMYY' 1 1 2 ;
  SILO4 = 'EXTR' SI1 'SMZZ' 1 1 2 ;
  SILO5 = 'EXTR' SI1 'SMXY' 1 1 2 ;
* Déformations
  DE1 = TABA.'DEPLACEMENTS'.(&BOILA-1)  ;
  EEP = 'REDU' ('EPSI' MODME DE1 ) MA ;
* On retranche la déformation thermique
  TATA = 'TIRE' CHARGT (TABA.'TEMPS'.(&BOILA-1)) ;
  ETH = 'EPTH' MODME MATME TATA;
  EEPTH = 'REDU' ETH MA ;
  EEVOU = EEP - EEPTH ;
  EZDE2 = 'EXTR' EEVOU 'EPXX' 1 1 2 ;
  EZDE3 = 'EXTR' EEVOU 'EPYY' 1 1 2 ;
  EZDE4 = 'EXTR' EEVOU 'EPZZ' 1 1 2 ;
  EZDE5 = 'EXTR' EEVOU 'GAXY' 1 1 2 ;
  TIMER = TABA.'TEMPS'.(&BOILA-1) ;
  'MESS' 'EZDE2 ='EZDE2 ;
  'SI' ('EGA' &BOILA 1) ;
    LOOX2 = 'PROG' EZDE2 ;
    LOOX3 = 'PROG' EZDE3 ;
    LOOX4 = 'PROG' EZDE4 ;
    LOOX5 = 'PROG' EZDE5 ;
    LOOY2 = 'PROG' SILO2 ;
    LOOY3 = 'PROG' SILO3 ;
    LOOY4 = 'PROG' SILO4 ;
    LOOY5 = 'PROG' SILO5 ;
    LOOT  = 'PROG' TIMER ;
  'SINO' ;
    LOOX2 = 'INSE' LOOX2 &BOILA EZDE2 ;
    LOOX3 = 'INSE' LOOX3 &BOILA EZDE3 ;
    LOOX4 = 'INSE' LOOX4 &BOILA EZDE4 ;
    LOOX5 = 'INSE' LOOX5 &BOILA EZDE5 ;
    LOOY2 = 'INSE' LOOY2 &BOILA SILO2 ;
    LOOY3 = 'INSE' LOOY3 &BOILA SILO3 ;
    LOOY4 = 'INSE' LOOY4 &BOILA SILO4 ;
    LOOY5 = 'INSE' LOOY5 &BOILA SILO5 ;
    LOOT  = 'INSE' LOOT  &BOILA TIMER ;
  'FINS' ;
'FIN' BOILA ;
'SI' ('NEG' GRAPH 'N') ;
  TSIG   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'SIGMAXX' LOOY2 ;
  'DESS' TSIG ;
  TSIG   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'SIGMAYY' LOOY3 ;
  'DESS' TSIG ;
  TSIG   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'SIGMAZZ' LOOY4 ;
  'DESS' TSIG ;
  TSIGES = 'EVOL' 'MANU' 'EPSILYY' LOOX3 'SIGMAYY' LOOY3 ;
  'DESS' TSIGES ;
  TEPS   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'EPSILXX' LOOX2 ;
  'DESS' TEPS ;
  TEPS   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'EPSILYY' LOOX3 ;
  'DESS' TEPS ;
  TEPS   = 'EVOL' 'MANU' 'TEMPS'   LOOT  'EPSILZZ' LOOX4 ;
  'DESS' TEPS ;
'FINS' ;
'MESS' '***************************************************' ;
'FIN' ;







