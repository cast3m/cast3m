* fichier : ricjoi_3d.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*******************************************************
*  Cas test de l'implantation numérique du modele
*  RICJOI_3D LOCAL
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
* Benjamin.Richard@lmt.ens-cachan.fr
*******************************************************
* Les cas de charges sont entrés :
* - 1 : Essai cyclique mono cadrant
* - 2 : Essai cyclique bi cadrant (cisaillement)
* - 3 : Essai effet unilatteral
* - 4 : Essai frottement
* - 5 : Essai variations stiffness
* - 6 : Essai compression normale avec rouille monotone
*******************************************************
* Choix du cas de charge
ncas  = 1;
graph = mot 'N';
*
*-------------- Options de calcul ---------------------
*
OPTI DIME 3 ELEM QUA4 MODE TRID  ;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
*
*-------------- Definition de la geometrie ------------
*
A1 = 0.00 0.00 0.00 ;
B1 = 1.00 0.00 0.00 ;
C1 = 1.00 1.00 0.00 ;
D1 = 0.00 1.00 0.00 ;

O1 = 0.00 0.00 10.00 ;

H1 = A1 DROIT 1 B1 ;
H2 = B1 DROIT 1 C1 ;
H3 = C1 DROIT 1 D1 ;
H4 = D1 DROIT 1 A1 ;
*
L1 = H1 ET H2 ET H3 ET H4 ;
S1 = SURF L1 PLAN ;

IA1 = 0.00 0.00 0.00 ;
IB1 = 1.00 0.00 0.00 ;
IC1 = 1.00 1.00 0.00 ;
ID1 = 0.00 1.00 0.00 ;

IH1 = IA1 DROIT 1 IB1 ;
IH2 = IB1 DROIT 1 IC1 ;
IH3 = IC1 DROIT 1 ID1 ;
IH4 = ID1 DROIT 1 IA1 ;

IL1 = IH1 ET IH2 ET IH3 ET IH4 ;
IS1 = SURF IL1 PLAN ;

OPTION ELEM LIA4 ;
VOL = LIAISON 0.00001 S1 IS1 ;

*
*------- Définition des conditions aux limites --------
*----------- et des déplacements imposés --------------
*
CL11 = BLOQ IA1 UX ;
CL12 = BLOQ IA1 UY ;
CL13 = BLOQ IA1 UZ ;
CL1 = CL11 ET CL12 ET CL13 ;

CL21 = BLOQ IB1 UX ;
CL22 = BLOQ IB1 UY ;
CL23 = BLOQ IB1 UZ ;
CL2 = CL21 ET CL22 ET CL23 ;

CL31 = BLOQ IC1 UX ;
CL32 = BLOQ IC1 UY ;
CL33 = BLOQ IC1 UZ ;
CL3 = CL31 ET CL32 ET CL33 ;

CL41 = BLOQ ID1 UX ;
CL42 = BLOQ ID1 UY ;
CL43 = BLOQ ID1 UZ ;
CL4 = CL41 ET CL42 ET CL43 ;

SI (OU (EGA ncas 3) (EGA ncas 6));
CL5 = BLOQ S1 UZ;
SINON;
CL5 = BLOQ S1 UY;
FINSI;

CL = CL1 ET CL2 ET CL3 ET CL4 ET CL5;

*
*----- Définition du modèle ---------------------------
*
MOD1 = MODELI VOL 'MECANIQUE' ELASTIQUE 'PLASTIQUE' INTJOI JOI4 ;

*
*----- Paramètres matériaux ---------------------------
*

* Rigidite normale
knn = (36000E+6);

* Rigidite tangentielle
ksn = (knn/2.4);

* Coefficient de dilatation
alpnn = 1.0E-10;

* Fragilite
adn = 3.5E-5;

* Seuil initial en energie pour l'endommagement
y0n = 50;

* Coefficient de dissymétrie entre modes
alpan = 6;

* Coefficient d'écrouissage 1
gainn = 2.0E9;

* Coefficient d'écrouissage 2
aainn = 5.0E-7;

* Coefficient de Gurson modifié 1
q1con = 3.5;

* Coefficient de Gurson modifié 2
q2con = 0.9;

* Coefficient de Gurson modifié 3
q3con = 0.1;

* Contrainte de raidissement seuil
sycon = -1.0E6;

* Coefficient loi de contraction 1
ncoen = 2.0;

* Coefficient loi de contraction 2
kcoen = 1.0E+10;

* Degré de corrosion (en perte de section)
SI(EGA ncas 6);
tcn = 1.0E-10;
SINON;
tcn = 0;
FINSI;

* Indicateur de phase de calcul
gonfn = 0;

MA1 = MATE MOD1 KS ksn KN knn  ALPN alpnn
         AD    adn Y0 y0n ALPA alpan
         GAIN  gainn AAIN aainn
         Q1CO  q1con Q2CO q2con Q3CO q3con
         SYCO sycon NCOE ncoen KCOE kcoen
         TC tcn GONF gonfn;

*
*----- Définition des cas de charge -------------------
*

* Essai cyclique mono cadrant
SI (EGA ncas 1);
  LI1  = PROG 0. 1.      2.     3.      ;
  LI2  = PROG 0. 5.0E-3 0.0E0   10.0E-3 ;
  LIS1 = PROG 0.0 PAS 0.01 3.0;
FINSI;

* Essai cyclique bi cadrant (cisaillement)
SI (EGA ncas 2);
  LI1  = PROG 0. 1. 2. 3. 4. 5. 6. 7. 8. 9;
  LI2  = PROG 0. -3.0E-3 5.0E-3 -7.0E-3 9.0E-3 -11.0E-3 13.0E-3
             -15.0E-3 17E-3 0.0;
  LIS1 = PROG 0.0 PAS 0.01 9.0;
FINSI;

* Essai effet unilatteral
SI (EGA ncas 3);
  LI1  = PROG 0. 1. 2. 3.;
  LI2  = PROG 0. 5.0E-3 -1.0E-3 10E-3;
  LIS1 = PROG 0. PAS 0.01 3.0;
FINSI;

* Essai frottement
SI (EGA ncas 4);
  LI1  = PROG 0. pas 1. 7.;
  LI2  = PROG 0. 5.0E-3 0.0E-3 6e-3 0.0e-3 8e-3 0.0e-3 10E-3;
  LIS1 = PROG 0. PAS 0.01 7.0;
FINSI;

* Essai variations stiffness
SI (EGA ncas 5);
  LI1  = PROG 0. pas 1. 1.;
  LI2  = PROG 0. 10.0E-3 ;
  LIS1 = PROG 0. PAS 0.01 1.0;
FINSI;

* Essai compression normale avec rouille monotone
SI (EGA ncas 6);
  LI1  = PROG 0. pas 1. 1.;
  LI2  = PROG 0. -1.0E-4 ;
  LIS1 = PROG 0. PAS 0.01 1;
FINSI;

  RIG1 = DEPI CL5  1.0;
  EV1  = EVOL MANU LI1 LI2               ;
  CAR1 = CHAR 'DIMP' RIG1 EV1 ;

*
*----------- Calcul par l'operateur PASAPAS ------------
*
   TAB                       = TABLE ;
   TAB.'MOVA'                = 'DINT';
   TAB.'BLOCAGES_MECANIQUES' = CL;
   TAB.'CARACTERISTIQUES'    = MA1;
   TAB.'MODELE'              = MOD1;
   TAB.'TEMPS_CALCULES'      = LIS1;
   TAB.'CHARGEMENT'          = CAR1;
   PASAPAS TAB ;

*
*----------- Courbe effort-deplacement -----------------
*
 dep0 = TAB.'DEPLACEMENTS';
 tim0 = TAB. TEMPS;
 sig0 = TAB.'CONTRAINTES';
 var0 = TAB.'VARIABLES_INTERNES';
 ind0 = INDE dep0;
 time = PROG (tim0 . 0);

 uzp0  = PROG 0.;
 fzp0  = PROG 0.;

 ep1 = prog 0.;
 si1 = prog 0.;
 nn = (('DIME' ind0) - 1);

 j = 1;
 REPE lab1 (('DIME' ind0) - 1);
   j = j + 1;
   indj  = ind0.j;
   depj  = dep0.indj;
   sigj  = sig0.indj;
   epsj  = epsi depj mod1 ma1;
   vecj  = bsigma sigj mod1 ma1;
   time  = time et (prog tim0. &lab1);
   SI (OU (EGA ncas 3) (EGA ncas 6));
   uzp0  = uzp0 ET (PROG (EXTR depj 'UZ' A1));
   fzp0  = fzp0 ET (PROG (EXTR vecj 'FZ' IA1));
   SINON;
   uzp0  = uzp0 ET (PROG (EXTR depj 'UY' A1));
   fzp0  = fzp0 ET (PROG (EXTR vecj 'FY' IA1));
   FINSI;
 FIN lab1;
   aa = extr fzp0 ( dime fzp0);
  list aa;
  err =  abs (aa + 1.85275E+06 ) / 1.85275E+06;
  message ' erreur relative ' err;
  si (err > 1.e-3);
    erreur (5);
     finsi;
  si ( ega graph 'O');
 EV2 = EVOL MANU uzp0 (-4.0*fzp0);
 DESS EV2;
 finsi;
 FIN ;













