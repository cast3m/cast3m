OPTI 'ECHO' 0 ;
************* Cas test d'une reprise de calcul avec PASAPAS ************
*                                                                      *
* On teste la possibilite de faire une reprise de calcul avec PASAPAS. *
* Il s'agit d'un calcul mecanique simple en elasticite lineaire avec   *
* chargement thermique (dilatation d'une barre)                        *
*                                                                      *
* - On effectue un premier calcul (reference) en appelant PASAPAS une  *
*   seule fois                                                         *
* - On effectue un second calcul (de test) avec le memes donnees que   *
*   le premier mais en faisant 2 appels a PASAPAS (sur la moitie des   *
*   pas de temps, puis sur l'autre moitie).                            *
* - On post-traite le deplacement de la barre, celui-ci doit etre le   *
*   meme a l'issue des deux calculs                                    *
*                                                                      *
************************************************************************


** Options generales
OPTI 'DIME' 2 'ELEM' 'QUA4' ;
logtrac = FAUX ;


** Maillage
p1 = 0. 0. ;
p2 = 1. 0. ;
l1 = DROI 5 p1 p2 ;
s1 = l1 TRAN 1 (0. 0.2) ;
l2 = s1 COTE 4 ;


** Modele et materiau
mo = MODE s1 'MECANIQUE' ;
ma = MATE mo 'YOUN' 200.E9 'NU' 0.3 'ALPH' 1.E-4 ;


** Blocages
bl1 = BLOQ 'UX' l2 ;
bl2 = BLOQ 'UY' p1 ;


** Chargement de temperature impose
cht = MANU 'CHPO' s1 1 'T' 1. ;
ev1 = EVOL 'MANU' (PROG 0. 1.) (PROG 0. 1000.) ;
cha1 = CHAR 'T' cht ev1 ;


** Calcul de reference avec 1 seul appel a PASAPAS
t1 = TABL ;
t1 . MODELE = mo ;
t1 . CARACTERISTIQUES = ma ;
t1 . BLOCAGES_MECANIQUES = bl1 ET bl2 ;
t1 . CHARGEMENT = cha1 ;
t1 . TEMPS_CALCULES = PROG 0.1 'PAS' 0.1 1. ;
t2 = COPI t1 ;
PASAPAS t1 ;


** Calcul du meme probleme mecanique mais  avec 2 appels a PASAPAS
t2 . TEMPS_CALCULES = PROG 0.1 'PAS' 0.1 0.5 ;
PASAPAS t2 ;
t2 . TEMPS_CALCULES = PROG 0.6 'PAS' 0.1 1. ;
PASAPAS t2 ;


** Post traitement : evolution temporelle du deplacement de l'extremite
*  libre du barreau
evt1 = EVOL 'ROUG' 'TEMP' t1 'DEPLACEMENTS' 'UX' p2 ;
evt2 = EVOL 'VERT' 'TEMP' t2 'DEPLACEMENTS' 'UX' p2 ;
SI logtrac ;
  DESS (evt1 ET evt2) 'TITR' 'Deplacement du barreau vs Temps' ;
FINSI ;


** Ecart relatif entre les deux calcul
umax = MAXI (EXTR evt1 'ORDO') ;
evd = ABS ((evt1 - evt2) / umax) ;
SI logtrac ;
  DESS evd 'TITR' 'Ecart relatif vs Temps' ;
FINSI ;


** Test sur l'ecart relatif
err1 = MAXI 'ABS' (EXTR evd 'ORDO') ;
SI (err1 > 1.E-15) ;
  ERRE (CHAI 'Les deux calculs PASAPAS ne donnent pas des resultats'
             ' identiques !') ;
FINSI ;


FIN ;



