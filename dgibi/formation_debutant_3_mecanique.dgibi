* fichier : formation_debutant_3_mecanique.dgibi
*
************************************************************************
*      FORMATION DEBUTER AVEC CAST3M - CALCULS THERMO-MECANIQUES       *
*                                                                      *
* Modelisation du comportement thermo-mecanique d'une plaque perforee  *
*                                                                      *
* Ce fichier est la partie 3 sur 3 de la formation debutant a Cast3M   *
*                                                                      *
* Partie 3 : calculs mecaniques                                        *
*            restitution des objets                                    *
*            elasticite lineaire, plasticite parfaite                  *
*            chargement thermique                                      *
*            caracteristiques variables                                *
*                                                                      *
************************************************************************




* RESTITUTION DES OBJETS CREES
* ----------------------------
OPTI 'REST' 'formation_debutant_2_thermique.sauv' ;
REST ;


* OBJET LOGIQUE POUR INDIQUER SI L'ON FAIT LES TRACES OU NON
* ----------------------------------------------------------
ITRAC    = FAUX ;






************************************************************************
* CHAPITRE 5 : MECANIQUE ELASTIQUE LINEAIRE                            *
************************************************************************


* HYPOTHESE SUR LES CONTRAINTES PLANES
* ------------------------------------
OPTI 'MODE' 'PLAN' 'CONT' ;


* MODELE MECANIQUE A MATERIAU CONSTANT
* ------------------------------------
MOM1     = MODE SU 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MAM1     = MATE MOM1 'YOUN' YOUNGMAT 'NU' NUMAT 'ALPH' ALPHAMAT ;


* CALCUL DE LA MATRICE DE RAIDEUR
* -------------------------------
RI       = RIGI MOM1 MAM1 ;


* CONDITIONS AUX LIMITES: DEPLACEMENT IMPOSE
* ------------------------------------------
BLMX     = BLOQ PA   'UX' ;
BLMY     = BLOQ LBAS 'UY' ;
RITOT    = RI ET BLMX ET BLMY ;


* CONDITIONS AUX LIMITES: TRACTION IMPOSEE
* ----------------------------------------
TR       = PRES 'MASS' MOM1 LHAUT (-100.E6) ;


* CALCUL DU CHAMP DE DEPLACEMENT PAR APPEL AU SOLVEUR
* ---------------------------------------------------
U5       = RESO RITOT TR ;


* POST TRAITEMENT
* ---------------
* DEFORMATIONS
DEF      = EPSI U5 MOM1 ;
* CONTRAINTES (PAR DEUX OPERATEURS => RESULTATS EQUIVALENTS)
SIG      = ELAS DEF MOM1 MAM1 ;
SIG      = SIGM U5  MOM1 MAM1 ;
* DEFORMEES
DEF_5    = DEFO SU U5 150. 'ROUG' ;
DEF_INI  = DEFO SU U5 0. ;
SI ITRAC ;
  TRAC (DEF_INI ET DEF_5) 'TITR' '[5] Deformee elastique sous traction uniforme' ;
FINSI ;
DEF_5C   = DEFO CSU U5 150. 'ROUG' ;
DEF_INIC = DEFO CSU U5 0. ;
SI ITRAC ;
  TRAC (DEF_INIC ET DEF_5C) 'TITR' '[5] Deformee du contour sous traction uniforme' ;
FINSI ;

SI ITRAC ;
  TRAC SIG MOM1 CSU 15 'TITR' '[5] Contraintes, traction uniforme' ;
FINSI ;
DEF_5B   = DEFO SU U5 150. ;
SI ITRAC ;
  TRAC SIG MOM1 DEF_5B CSU 15 'TITR' '[5] Contraintes, traction uniforme, maillage deforme' ;
FINSI ;
* EVOLUTION DE LA CONCENTRATION DE CONTRAINTE DU TROU VERS LE BORD
SIGB     = CHAN 'CHPO' SIG MOM1 ;
LBASG    = LBAS ELEM 'COMP' PB PA ;
EVSIG    = EVOL 'CHPO' SIGB 'SMYY' LBASG ;
EVK      = EVSIG / 100.E6 ;
SI ITRAC ;
  DESS EVK 'TITR' '[5] Concentration de contrainte le long de LBAS' ;
FINSI ;

* EFFORTS DE REACTION AUX APPUIS
REAC1    = REAC U5 (BLMX ET BLMY) ;
VREAC    = VECT REAC1 'FORC' 'ROUG' ;
VFIMP    = VECT TR 'FORC' 'VERT' ;
SI ITRAC ;
  TRAC (VFIMP ET VREAC) CSU 'TITR' '[5] Efforts imposes(V) et reactions aux appuis(R)' ;
FINSI ;








************************************************************************
* CHAPITRE 6 : MECANIQUE ELASTIQUE LINEAIRE                            *
*              CHARGEMENT THERMIQUE                                    *
************************************************************************


* DEFORMATIONS THERMIQUES PURES AVEC LE DERNIER CHAMP DE TEMPERATURE
* DU CALCUL THERMIQUE AVEC CONVECTION + RAYONNEMENT
DELTA_TE = (TAB1 . 'TEMPERATURES' . (N1 - 1)) - T_INI ;
EPT      = EPTH DELTA_TE MOM1 MAM1 ;


* PSEUDO CONTRAINTES CORRESPONDANT A CES DEFORMATIONS THERMIQUES
* --------------------------------------------------------------
SIT      = ELAS EPT MOM1 MAM1 ;


* FORCES NODALES POUR CETTE DEFORMATION THERMIQUE
* -----------------------------------------------
FFT      = BSIG SIT MOM1 ;


* DEPLACEMENTS, PAR APPEL AU SOLVEUR EN SUPERPOSANT LES FORCES
* PUREMENT MECANIQUES ET LES PSEUDO FORCES THERMIQUES
* ------------------------------------------------------------
U6       = RESO RITOT (TR ET FFT) ;


* DEFORMEE THERMOMECANIQUE
* ------------------------
DEF_6    = DEFO SU  U6 150. ;
DEF_6C   = DEFO CSU U6 150. 'ORAN' ;
SI ITRAC ;
  TRAC (DEF_INIC ET DEF_5C ET DEF_6C) 'TITR' '[6] Deformees, traction seule(R), traction + temperature(O)' ;
FINSI ;


* DEFORMATIONS TOTALES
* --------------------
EP       = EPSI U6 MOM1 ;


* DEFORMATIONS ELASTIQUES
* -----------------------
EPE      = EP - EPT ;


* LES CONTRAINTES SONT CALCULEES A PARTIR DES DEFORMATIONS ELASTIQUES
* -------------------------------------------------------------------
SIGT     = ELAS EPE MOM1 MAM1 ;
SI ITRAC ;
  TRAC SIGT MOM1 DEF_6 CSU 15 'TITR' '[6] Contraintes, traction + temperature' ;
FINSI ;







************************************************************************
* CHAPITRE 7 : MECANIQUE ELASTIQUE LINEAIRE                            *
*              CHARGEMENT THERMIQUE                                    *
*              MATERIAU VARIABLE DANS L'ESPACE                         *
************************************************************************


* ON REND LE COEFFICIENT DE DILATATION LINEAIRE ALPHA
* VARIABLE EN FONCTION DE LA COORDONNEE X
* ALPHA(X) = ALPHA0 * (1 + BETA*((X-XMOYEN)/(XMAX-XMIN))**3)

* RECUPERATION DE LA COORDONNEE X ET FORMATION D'UN
* CHAMP PAR POINTS (CHPOINT) PORTANT CETTE INFORMATION
* ----------------------------------------------------
XX       = COOR SU 1 ;


* VALEURS EXTREMES ET MOYENNE DE LA COORDONNEE X DU MAILLAGE
* ----------------------------------------------------------
XMAX     = MAXI XX ;
XMIN     = MINI XX ;
XMOY     = 0.5 * (XMAX + XMIN) ;


* CALCUL DU CHAMP PAR POINTS SCALAIRE DE ALPHA VARIABLE
* -----------------------------------------------------
BETA     = 7. ;
CHP_ALPH = ALPHAMAT * (1. + (BETA * (((XX - XMOY) / (XMAX - XMIN)) ** 3))) ;


* CONVERSION DU CHPOINT -> EN MCHAML
* ----------------------------------
CHM_ALPH = CHAN 'CHAM' CHP_ALPH MOM1 ;


* MISE A JOUR DU MCHAML DU MATERIAU AVEC ALPHA VARIABLE
* -----------------------------------------------------
MAM1B    = MATE MOM1 'YOUN' YOUNGMAT 'NU' NUMAT 'ALPH' CHM_ALPH ;


* MISE A JOUR DES DEFORMATIONS THERMIQUES PURES AVEC ALPHA VARIABLE
* -----------------------------------------------------------------
EPT      = EPTH DELTA_TE MOM1 MAM1B ;


* MISE A JOUR DES PSEUDO CONTRAINTES THERMIQUES AVEC ALPHA VARIABLE
* -----------------------------------------------------------------
SIT      = ELAS EPT MOM1 MAM1B ;


* MISE A JOUR DES FORCES NODALES POUR CETTE DEFORMATION THERMIQUE
* ---------------------------------------------------------------
FFT      = BSIG SIT MOM1 ;


* DEPLACEMENTS AVEC ALPHA VARIABLE
* --------------------------------
U7       = RESO RITOT (TR ET FFT) ;


* DEFORMEE AVEC ALPHA VARIABLE
* ----------------------------
DEF_7    = DEFO SU  U7 150. ;
DEF_7C   = DEFO CSU U7 150. 'ROUG' ;
SI ITRAC ;
  TRAC (DEF_INIC ET DEF_6C ET DEF_7C) 'TITR' '[7] Deformees, theromeca(O), + alpha var.(R)' ;
FINSI ;


* DEFORMATIONS TOTALES AVEC ALPHA VARIABLE
* ----------------------------------------
EP       = EPSI U7 MOM1 ;


* DEFORMATION ELASTIQUES AVEC ALPHA VARIABLE
* ------------------------------------------
EPE      = EP - EPT ;


* CONTRAINTES AVEC ALPHA VARIABLE
* -------------------------------
SIGT     = ELAS EPE MOM1 MAM1B ;
SI ITRAC ;
  TRAC SIGT MOM1 DEF_7 CSU 15 'TITR' '[7] Contraintes, thermomeca, alpha var.' ;
FINSI ;








************************************************************************
* CHAPITRE 8 : MECANIQUE ELASTIQUE LINEAIRE ET PLASTIQUE PARFAIT       *
*              THERMIQUE TRANSITOIRE PRECEDENT (COND. + CONV. + RAYO.) *
*              MATERIAU VARIABLE DANS L'ESPACE                         *
************************************************************************


* MISE A JOUR DU MODELE MECANIQUE
* -------------------------------
MOM2     = MODE SU 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' 'PLASTIQUE' 'PARFAIT' ;


* MISE A JOUR DU MATERIAU (AJOUT DE LA LIMITE D'ELASTICITE 'SIGY')
* ----------------------------------------------------------------
MAM2     = MATE MOM2 'YOUN' YOUNGMAT 'NU' NUMAT 'ALPH' CHM_ALPH 'SIGY' SIGYMAT ;


* DEFINITION DU CHARGEMENT MECANIQUE INCREMENTAL
* ----------------------------------------------
* NOTA : EN PLASTICITE ON NE PEUT GENERALEMENT PAS APPLIQUER
*        BRUTALEMENT UN CHARGEMENT FINAL POUR LEQUEL IL Y AURAIT
*        DEPASSEMENT EXCESSIF DE LA LIMITE D'ELASTICITE DES LA
*        PREMIERE ITERATION DU CALCUL NON LINEAIRE ITERATIF

* EVOLUTION TEMPORELLE DU CHARGEMENT MECANIQUE
* --------------------------------------------
EVTM     = EVOL 'MANU' 'Temps' (PROG 0. (0.98 * TPSFIN) TPSFIN)
                       'Coef'  (PROG 0.  0.             1.) ;
CHM      = CHAR 'MECA' EVTM TR ;


* PREPARATION DE LA TABLE POUR PASAPAS
* ------------------------------------
TAB2                         = TABL ;
TAB2 . 'MODELE'              = MOT ET MOC ET MOR ET MOM2 ;
TAB2 . 'CARACTERISTIQUES'    = MAT ET MAC ET MAR ET MAM2 ;
TAB2 . 'BLOCAGES_MECANIQUES' = BLMX ET BLMY ;
TAB2 . 'BLOCAGES_THERMIQUES' = BLT ;
TAB2 . 'CHARGEMENT'          = CHT ET CHACONV ET CHARAYE ET CHM ;
TAB2 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' (0.02 * TPSFIN)  (0.98 * TPSFIN)
                                       'PAS' (0.001 * TPSFIN) TPSFIN ;
TAB2 . 'TEMPERATURES'        = TABL ;
TAB2 . 'TEMPERATURES' . 0    = T_INI ;
TAB2 . 'CELSIUS'             = VRAI ;


* APPEL A PASAPAS
* ---------------
PASAPAS TAB2 ;


* POST TRAITEMENT
* ---------------
* COMPARAISON DE LA DEFORMEE FINALE AVEC LES CALCULS PRECEDENTS
N2       = DIME (TAB2 . 'DEPLACEMENTS') ;
U8       = TAB2 . 'DEPLACEMENTS' . (N2 - 1) ;
DEF_8    = DEFO SU  U8 150. ;
DEF_8C   = DEFO CSU U8 150. 'VIOL' ;
SI ITRAC ;
  TRAC (DEF_INIC ET DEF_7C ET DEF_8C) 'TITR' '[8] Deformees, elas.(R), + plas.(V)' ;
FINSI ;

* BOUCLE SUR LES PAS DE TEMPS ET TRACE DES CONTRAINTES ET DE LA
* DEFORMATION PLASTIQUE EQUIVALENTE CUMULEE
LISO2    = PROG 0. 'PAS' 10.E6 160.E6 ;
SI ITRAC ;
  REPE B1 N2 ;
    TPS_I    = TAB2 . 'TEMPS' . (&B1 - 1) ;
    U_I      = TAB2 . 'DEPLACEMENTS' . (&B1 - 1) ;
    DEF_I    = DEFO SU U_I 150. ;
    S_I      = TAB2 . 'CONTRAINTES' . (&B1 - 1) ;
    MOT_I    = CHAI '[8] Contraintes au temps ' TPS_I ;
    TRAC S_I MOM2 DEF_I CSU LISO2 'TITR' MOT_I ;
  FIN B1 ;
  VI       = TAB2 . 'VARIABLES_INTERNES' . (N2 - 1) ;
  EQ_MAX   = MAXI (EXCO 'EPSE' VI) ;
  LISO3    = PROG 0. 'PAS' (EQ_MAX / 15.) EQ_MAX ;
  REPE B1 N2 ;
    TPS_I    = TAB2 . 'TEMPS' . (&B1 - 1) ;
    U_I      = TAB2 . 'DEPLACEMENTS' . (&B1 - 1) ;
    DEF_I    = DEFO SU U_I 150. ;
    V_I      = TAB2 . 'VARIABLES_INTERNES' . (&B1 - 1) ;
    MOT_I    = CHAI '[8] Def. plas. eq. cumulee au temps ' TPS_I ;
    TRAC V_I MOM2 DEF_I CSU LISO3 'TITR' MOT_I ;
  FIN B1 ;
FINSI ;









************************************************************************
* CHAPITRE 9 : MECANIQUE ELASTIQUE LINEAIRE ET PLASTIQUE PARFAIT       *
*              CHARGEMENT THERMIQUE                                    *
*              MATERIAU VARIABLE DANS L'ESPACE                         *
*              MATERIAU DEPENDANT DE LA TEMPERATURE                    *
************************************************************************


* VARIATION DE LA LIMITE ELASTIQUE EN FONCTION DE T
* -------------------------------------------------
EVSIGY   = EVOL 'MANU' 'T' (PROG 50. 200.) 'SIGY' (PROG 300.E6 100.E6) ;


* MISE A JOUR DU MATERIAU
* -----------------------
MAM2     = MATE MOM2 'YOUN' YOUNGMAT 'NU' NUMAT 'ALPH' CHM_ALPH 'SIGY' EVSIGY ;


* MISE A JOUR DES DONNEES D'ENTREE DANS LA TABLE DE PASAPAS
* ----------------------------------------------------------
TAB2                         = TABL ;
TAB2 . 'MODELE'              = MOT ET MOC ET MOR ET MOM2 ;
TAB2 . 'CARACTERISTIQUES'    = MAT ET MAC ET MAR ET MAM2 ;
TAB2 . 'BLOCAGES_MECANIQUES' = BLMX ET BLMY ;
TAB2 . 'BLOCAGES_THERMIQUES' = BLT ;
TAB2 . 'CHARGEMENT'          = CHT ET CHACONV ET CHARAYE ET CHM ;
TAB2 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' (0.02 * TPSFIN)  (0.98 * TPSFIN)
                                       'PAS' (0.001 * TPSFIN) TPSFIN ;
TAB2 . 'TEMPERATURES'        = TABL ;
TAB2 . 'TEMPERATURES' . 0    = T_INI ;
TAB2 . 'CELSIUS'             = VRAI ;


* APPEL A PASAPAS
* ---------------
PASAPAS TAB2 ;


* POST TRAITEMENT
* ---------------
* COMPARAISON DE LA DEFORMEE FINALE AVEC LES CALCULS PRECEDENTS
N2       = DIME (TAB2 . 'DEPLACEMENTS') ;
U9       = TAB2 . 'DEPLACEMENTS' . (N2 - 1) ;
DEF_9    = DEFO SU  U9 150. ;
DEF_9C   = DEFO CSU U9 150. 'BLEU' ;
MOT1     = CHAI '[9] Deformees, elas.(R), + plas.(V), + Sy var.(B)' ;
SI ITRAC ;
  TRAC (DEF_INIC ET DEF_7C ET DEF_8C ET DEF_9C) 'TITR' MOT1 ;
FINSI ;

* DEFORMATIONS PLASTIQUES CUMULEES
SI ITRAC ;
  VI       = TAB2 . 'VARIABLES_INTERNES' . (N2 - 1) ;
  EQ_MAX   = MAXI (EXCO 'EPSE' VI) ;
  LISO3    = PROG 0. 'PAS' (EQ_MAX / 15.) EQ_MAX ;
  REPE B1 N2 ;
    TPS_I    = TAB2 . 'TEMPS' . (&B1 - 1) ;
    U_I      = TAB2 . 'DEPLACEMENTS' . (&B1 - 1) ;
    DEF_I    = DEFO SU U_I 150. ;
    V_I      = TAB2 . 'VARIABLES_INTERNES' . (&B1 - 1) ;
    MOT_I    = CHAI '[9] Def. plas. eq. cumulee au temps ' TPS_I ;
    TRAC V_I MOM2 DEF_I CSU LISO3 'TITR' MOT_I ;
  FIN B1 ;
FINSI ;


FIN ;


