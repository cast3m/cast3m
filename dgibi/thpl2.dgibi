* fichier :  thpl2.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
* CAS TEST DU 91/06/13    PROVENANCE : TEST


*******************************************
*    Test thpl2.dgibi: jeux de données    *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;

*******************************************
*                  THPL2                  *
*                                         *
*   PLAQUE CARREE ENCASTREE SOUMIS A UN   *
*           CHAMPS DE TEMPERTURE          *
*                                         *
*   Test de la commission V.P.C.S.        *
*   Groupe Thermo-plasticite              *
*   Statique non lineaire                 *
*                                         *
*   Une plaque carree de 10 cm de cote est*
*   encastree et soumise a un champs      *
*   constant de temperature de 500 degres *
*   celsius.                              *
*                                         *
*  On se propose de calculer              *
*   - contraintes smxx et smyy,           *
*   - deformations plastiques epxx et epyy*
*                                         *
*      Les valeurs trouvees sont ensuites *
*   comparees aux valeurs de reference    *
*   obtenues analytiquement               *
*******************************************
OPTION DIME 2 ;
OPTION MODE PLAN CONT;
OPTION ELEM QUA4;

*=========================================*
*          geometrie : maillage           *
*=========================================*

* Dimensions en metres
CO = 0.01;

PA = 0. 0.;
PB = CO 0.;
PC = CO CO;
PD = 0. CO;

LAB = PA DROIT 8 PB;
LBC = PB DROIT 8 PC;
LCD = PC DROIT 8 PD;
LDA = PD DROIT 8 PA;

LIG = LAB ET LBC ET LCD ET LDA;

SUR1 = DALL LAB LBC LCD LDA PLAN;

SI(NEG GRAPH 'N');
  TITR 'THPL2 : MAILLAGE';
  TRACE 'QUAL' SUR1;
FINSI;

*=========================================*
*        conditions aux limites           *
*=========================================*
CL = BLOQUE LIG DEPL ROTA;

*=========================================*
*           MODE - materiau
*=========================================*
MO  = MODE SUR1 MECANIQUE ELASTIQUE
                PLASTIQUE CINEMATIQUE QUA4;
MAT = MATE MO YOUN 163750.0E6 NU 0.3
              ALPHA 13.875E-6 SIGY 100E6
              H 2000E6;

*=========================================*
* champs relatifs a la procedure  PASAPAS *
*=========================================*
* Liste des pseudo-temps ou l'on desire des
* resultats.
T0 = 0. ;
T1 = 1.;
LIS1 =    PROG T0  T1;

* Champs points de temperature.
TEMP = TABL;
TEMP . 0 = T0;
TEMP . 1 = T1;

TEMPER = TABL;
TEMPER . 0 = MANU CHPO SUR1 1 T 0.  ;
TEMPER . 1 = MANU CHPO SUR1 1 T 500.;
CHA2       = CHAR 'T' TEMP TEMPER;

* Chargement mecanique : chargement nul.
LFORCE = PROG 0. 0.;
FOR1   = FORCE FY 1. PA;
CHPO1  = RESU FOR1;
EVOL1  = EVOL MANU TEMPS LIS1 FORCE LFORCE;
CHA1   = CHAR 'MECA' CHPO1 EVOL1;

* Parametres pour la procedure PASAPAS
TABDEF          = TABL ;
TABDEF .'DEFTO' = VRAI;
TABDEF .'DEFIN' = VRAI;

TAB1                       = TABL  ;
TAB1.'CHARGEMENT'          =(CHA1 ET CHA2);
TAB1.'MODELE'              = MO   ;
TAB1.'CARACTERISTIQUES'    = MAT   ;
TAB1.'BLOCAGES_MECANIQUES' = CL    ;
TAB1.'TEMPS_CALCULES'      = LIS1 ;
TAB1.'TEMPS_SAUVES'        = LIS1 ;
tab1.'MES_SAUVEGARDES'     = TABDEF;

* procedure PASAPAS
PASAPAS TAB1;

*=========================================*
*       Extraction des resultats          *
*=========================================*

* Contraintes
NN = 1;
CHEL1 = TAB1.CONTRAINTES.NN;

* Interpolations aux noeuds :
CHAM2 = CHAN NOEUD MO CHEL1;
CHPO1 = CHAN CHPO  MO CHAM2;

* Extractionsn des contraintes smxx et smyy
SMXX1 = EXTR CHPO1 SMXX1 PA;
SMYY1 = EXTR CHPO1 SMYY1 PA;
SMXX1 = SMXX1 / 1.E6;
SMYY1 = SMYY1 / 1.E6;

SMXX2 = -125.9;
SMYY2 = -125.9;

* Calcul de l'erreur commise
ERGSXX = 100.*(ABS((SMXX1-SMXX2)/ SMXX2 ));
ERGSYY = 100.*(ABS((SMYY1-SMYY2)/ SMYY2 ));

* Deformations plastiques.
CHEL10 = TAB1.DEFORMATIONS_INELASTIQUES.NN;

* Interpolations aux noeuds du maillage
CHAM20 = CHAN NOEUD MO CHEL10;
CHPO10 = CHAN CHPO  MO CHAM20;

* Extractions des deformations plastiques
EPXX1 = EXTR CHPO10 EIXX PA;
EPYY1 = EXTR CHPO10 EIYY PA;

EPXX2 = -0.6399E-2;
EPYY2 = -0.6399E-2;

* Calcul de l'erreur commise
ERGEXX = 100.*(ABS((EPXX1-EPXX2)/ EPXX2 ));
ERGEYY = 100.*(ABS((EPYY1-EPYY2)/ EPYY2 ));

* Nombre d'elements et noeuds du maillage
NOSUR1 = NBNO SUR1 ;
ELSUR1 = NBEL SUR1 ;

*=========================================*
*        Affichage des resultats          *
*=========================================*
MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

MESS '   Contraintes interpolees (MPa)';
MESS '   -----------------------------';
SAUT 1 LIGN;
MESS
' Contrainte theorique smxx :' SMXX2 'MPa';
MESS
' Contrainte calculee  smxx :' SMXX1 'MPa';
MESS '      Soit un ecart de :' ERGSXX '%';
SAUT 1 LIGN;

MESS
' Contrainte theorique smyy :' SMYY2 'MPa';
MESS
' Contrainte calculee  smyy :' SMYY1 'MPa';
MESS '      Soit un ecart de :' ERGSYY '%';
SAUT 1 LIGN;

MESS 'Deformations plastiques interpolees';
MESS '-----------------------------------';
SAUT 1 LIGN;

MESS
' Deformation plastique theorique epxx :'
EPXX2;
MESS
' Deformation plastique calculee  epxx :'
EPXX1;
MESS '      Soit un ecart de :' ERGEXX '%';
SAUT 1 LIGN;

MESS
' Deformation plastique theorique epyy :'
EPYY2;
MESS
' Deformation plastique calculee  epyy :'
EPYY1;
MESS '      Soit un ecart de :' ERGEYY '%';
SAUT 2 LIGN;

MESS '   Maillage ';
MESS '   -------- ';
MESS '     Nombre de noeuds  : ' NOSUR1 ;
MESS '     Nombre d elements : ' ELSUR1 ;
SAUT 1 LIGN;

SI (NEG GRAPH 'N');
  DEP1 = TAB1. 'DEPLACEMENTS' . NN;
  DEF0 = DEFO SUR1 DEP1 0.;
  DEF1 = DEFO SUR1 DEP1 10. BLEU;
  TITR 'THPL2 : DEFORMEE';
  TRAC (DEF0 ET DEF1);

  TITR 'THPL2 : Deformation Plastique';
  TRAC CHEL10 MO;
FINSI;

*=========================================*
*      Code fonctionnement et fin         *
*=========================================*
* Maximum des erreurs relatives
ERGMAX = MAXI
        (PROG ERGSXX ERGSYY ERGEXX ERGEYY);

SI (ERGMAX <EG 5.);
     ERRE  0 ;
SINON;
     ERRE  5 ;
FINSI;
SAUT 1 LIGN;
TEMPS ;

FIN;




