* fichier :  plas8.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
*           Test Plas8.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test plas8.dgibi: Jeux de données         *
*           ---------------------------------         *
*******************************************************

*  POUR CALCUL COMPLET METTRE COMPLET À : VRAI;

complet = vrai;

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************************
*  TEST PLAS8
*
*   CONSOLE SOUS CHARGE CONCENTREE
*
*   On impose un deplacement vertical (une fleche) sur
*   l'extremite libre d'une console. On etudie
*   l'evolution de la force de reaction fonction de la
*   fleche.La force doit tendre vers une valeur
*   asymptotique donnee par l'expression suivante
*   (si on considere la loi elastoplastique parfaite):
*
*   FU = (SIGY*H**2)/(4*L)
*    ou  SIGY = contrainte limite elastique
*        H    = l'epaisseur de la poutre
*        L    = longueur de poutre
*
*******************************************************
OPTI DIME 3 ELEM TRI3 DENS 0.5 ;

*______________________________________________________
*  GEOMETRIE
*______________________________________________________
B   =  1. ;
L   = 10. ;
nel = 20;

A1 = 0. 0. 0. ;
A2 = 0. B  0. ;
B1 = L  0. 0. ;
B2 = L  B 0.  ;

A2A1 = DROI 2   A2 A1 ;
A1B1 = DROI nel A1 B1 ;
B1B2 = DROI 2   B1 B2 ;
B2A2 = DROI nel B2 A2 ;
ST   = DALL A2A1 A1B1 B1B2 B2A2 PLAN ;
OEIL = -100 -100 -100 ;

SI (NEG GRAPH 'N') ;
  TRAC OEIL 'QUAL' ST  ;
FINSI ;

*______________________________________________________
*  INITIALISATION
*______________________________________________________
XABS  = PROG ;
FONC1 = PROG ;
EP    = L/10 ;

*______________________________________________________
*   MODELE
*______________________________________________________
MOD1 = MODE ST MECANIQUE ELASTIQUE PLASTIQUE  PARFAIT DKT  'INTEGRE' 7 ;

*______________________________________________________
*  MATERIAUX
*______________________________________________________
E    = 2.1E11 ;
SY   = 100.E6;
MAT1 = MATE MOD1 YOUN E NU 0. SIGY SY  EPAI EP CALF 1. ;

*______________________________________________________
*  CONDITIONS AUX LIMITES ET RIGIDITE
*______________________________________________________
RI1   = RIGI MOD1 MAT1 ;
CL1   = BLOQ UX UY UZ RY RZ A2A1 ;
CLDEP = BLOQ UZ B1B2 ;
CL    = CL1  ET CLDEP;
RIT1  = RI1  ET CL;

*______________________________________________________
*   DEPLACEMENT IMPOSE
*______________________________________________________
 FDEP = DEPI CLDEP  0.2;

*______________________________________________________
*  PREPARATION POUR PASAPAS
*______________________________________________________
LI1 = PROG 0. 1. ;
LI2 = PROG 0. 1. ;
EV = EVOL MANU T LI1 F(T) LI2 ;
CHA1 = CHAR DIMP FDEP  EV ;
*
si complet;
  LIS1 = PROG 0. PAS 0.01 0.5;
sinon;
  LIS1 = PROG PAS 0.03333 0.1 PAS 0.02 0.16 ;
finsi;

TAB1 = TABL ;
TAB1.'CARACTERISTIQUES'    = MAT1;
TAB1.'CHARGEMENT'          = CHA1;
TAB1.'MODELE'              = MOD1;
TAB1.'TEMPS_CALCULES'      = LIS1;
TAB1.'BLOCAGES_MECANIQUES' = CL  ;
TAB1.'PROCESSEURS'         = 'MOT' 'COMPORTEMENT';

*______________________________________________________
* CALCUL
*______________________________________________________
PASAPAS TAB1;

TEMPS ;

*______________________________________________________
* POST-TRAITEMENT
*______________________________________________________
NB   = DIME LIS1           ;
XABS = INSER XABS  1 (0.0) ;
FONC1= INSER FONC1 1 (0.0) ;

REPE BOUC1 NB ;
  ITER1 = &BOUC1 ;
  TT    = EXTR LIS1 ITER1              ;
  DEP   = PECHE TAB1 'DEPLACEMENTS' TT ;
  FLECH = EXTR DEP UZ B1               ;
  FF    = REAC DEP RIT1                ;
  FFF   = RESU(REDU FF B1B2)           ;
  FORC  = EXTR FFF 'FZ' B1             ;
  XABS  = INSER XABS  ITER1 FLECH      ;
  FONC1 = INSER FONC1 ITER1 FORC       ;

  SI (ITER1 EGA (NB-1));
    LIST TAB1.'DEPLACEMENTS' ;
    DEF0 = DEFO 0. DEP ST      ;
    DEF1 = DEFO 5. DEP ST VERT ;
    SI (NEG GRAPH 'N') ;
      TITR 'DEPLACEMENT UZ';
      TRAC 'CACH' (EXCO DEP UZ)  ST ;
      TITR 'DEFORMEE';
      TRAC 'CACH' (DEF0 ET DEF1) ST ;
    FINSI;
  FINSI;
FIN BOUC1 ;

*______________________________________________________
* ON TRACE LA COURBE FORCE-FLECHE
*______________________________________________________
TITR 'FORCE PONCTUELLE FONCTION DE LA FLECHE  ';
EV1 = EVOL ROUGE MANU 'FLECHE' XABS 'FORCE' FONC1 ;

*______________________________________________________
* CALCUL ANALYTIQUE DE LA VALEUR ASYMPTOTIQUE
*______________________________________________________
si complet;
  FU= (SY * (EP**2))/(4*L) ;
  ERRMAX = 8.5 ;
sinon;
  FU = 1.679E+06;
  ERRMAX = 0.06;
finsi;

SI(NEG GRAPH 'N');
 DESS EV1 ;
FINSI ;

*______________________________________________________
*            CODE FONCTIONNEMENT
*______________________________________________________
FC  = RESU(REDU FF B1B2) ;
FCC = EXTR FC 'FZ' B1 ;

RESI= 100 * (ABS((FCC-FU)/FU)) ;

MESS '    Force theorique  :' FU  'N';
MESS '    Force calcule    :' FCC 'N';
MESS '    Soit un ecart de :' RESI '%';

SI (RESI <EG ERRMAX) ;
    ERRE  0 ;
SINO;
    ERRE  5 ;
FINSI;

TEMPS ;
FIN ;




