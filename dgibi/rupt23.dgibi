* fichier :  rupt23.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************

*******************************************************
*           Test rupt23.dgibi: Jeux de données        *
*           ---------------------------------         *
*******************************************************
* CAS TEST DU 15/12/15     PROVENANCE : TEST

*Cas test de validation pour le calcul de J sous plusieurs chargement
*avec les procedures g_theta.procedur et g_calcul.procedur
*
*- chargement en traction
*- chargement avec pression sur levres
*- chargement thermique

*Calcul en dimension 2 avec des elements QUA8 sur un maillage complet
*symetrique

opti dime 2 elem qua8 mode axis echo 1 ;

* ig1 : mettre a vrai pour activer traces
ig1      = faux ;

************************
*Données paramètriques :
************************
* a : profondeur de la fissure *
* t : epaisseur du tube *
* ri, re : rayon interne/externe *
* h : hauteur du tube *

h = 1. ;
t = 60.e-3 ;
a = t/5 ;
ri = t*5;
re = ri+t;

*COORDONNEE DE LA POINTE DE LA FISSURE
pf = (a + ri) 0. ;

*NOMBRE D'ELEMENTS AUTOUR DE LA POINTE DE LA FISSURE (1 et 2 COUT)
n_fiss = 10 ;

*TAILLE D'UN ELEMENT DE LA 1ERE ET 2EME COUTURE*
t_el = 200e-6 ; t_el2 = 400e-6 ;
*Facteur d'agrandissement de la taille du derafinement
tt_el2 = 4.*t_el2 ;

*LONGUEUR DE LA 1ERE ET 2EME COUTURE*
lc1 = n_fiss * t_el ; lc2 = t_el2 * n_fiss;

*NIVEAU DE CHARGEMENT
p0T = -400. ; p0P = 400. ; dt0 = 300.;

*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                    DEBUT DU MAILLAGE
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


**************************************************************
**********************  1ERE COUTURE  ************************
************  (Autour de la pointe de la fissure)  ***********
**************************************************************

p1cbd = pf plus (lc1 0.) ;
p1chd = pf plus (lc1 lc1) ;
pf1 = pf plus (0. lc1) ;
p1chg = pf1 moin (lc1 0.) ;
p1cbg = pf moin (lc1 0.) ;

d1ch = droi (2*n_fiss) p1chg p1chd;
d1cg = droi (n_fiss) p1cbg p1chg ;
d1cd = droi (n_fiss) p1cbd p1chd ;
d1cbg = droi (n_fiss) p1cbg pf ;
d1cbd = droi (n_fiss) pf p1cbd ;

cout1 = regl n_fiss d1ch (d1cbg et d1cbd) ;
cout1 = coul jaun cout1 ;

**************************************************************
**********************  2EME COUTURE  ************************
************  (Autour de la pointe de la fissure)  ***********
**************  Partie au-dessus de la fissure  **************
**************************************************************

p2cbd = pf plus (lc2 0.) ;
p2chd = pf plus (lc2 lc2) ;
pf2 = pf plus (0. lc2) ;
p2chg = pf2 moin (lc2 0.) ;
p2cbg = pf moin (lc2 0.) ;

d2ch = droi (2*n_fiss) p2chg p2chd;
d2cg = droi (n_fiss) p2cbg p2chg ;
d2cd = droi (n_fiss) p2cbd p2chd ;

cout2 = regl n_fiss (d1cd et d1ch et d1cg) (d2cd et d2ch et d2cg) ;
cout2 = coul vert cout2 ;

cout1et2 = cout1 et cout2;

**************************************************************
*****************  DERAFINEMENT DES COUTURES  ****************
**************  Partie au-dessus de la fissure  **************
**************************************************************

*------------------( DERAF A 4 ELEMENT )-------------------

pid1 = p2chg moin (0. tt_el2) ;
pid2 = pid1 plus (0. t_el2) ;
pid3 = pid2 plus (0. t_el2) ;
pid4 = pid3 plus (0. t_el2) ;
pid5 = pid4 plus (0. t_el2) ;
pid6 = pid2 moin (t_el2 0.) ;
pid7 = pid3 moin (t_el2 0.) ;
pid8 = pid4 moin (t_el2 0.) ;
pid9 = pid1 moin (tt_el2 0.) ;
pid10 = pid3 moin (tt_el2 0.) ;
pid11 = pid5 moin (tt_el2 0.) ;

did1 = droi 1 pid1 pid2 ;
did2 = droi 1 pid2 pid3 ;
did3 = droi 1 pid3 pid4 ;
did4 = droi 1 pid4 pid5 ;
did5 = droi 1 pid9 pid6 ;
did6 = droi 1 pid6 pid7 ;
did7 = droi 1 pid7 pid8 ;
did8 = droi 1 pid11 pid8 ;
did9 = droi 1 pid10 pid7 ;

si1 = (regl 1 did1 did5) et (regl 1 did2 did6) et
      (regl 1 did3 did7) et (regl 1 did4 (inve did8)) et
      (regl 1 did8 did9) et (regl 1 did9 did5) ;
elim si1 1.e-5 ;

*------------------( DERAF A 3 ELEMENT )-------------------

pad1 = pf moin (lc2 0.) ;
pad2 = pad1 plus (0. t_el2) ;
pad3 = pad2 plus (0. t_el2) ;
pad4 = pad3 plus (0. t_el2) ;
pad5 = pad2 moin (t_el2 0.) ;
pad6 = pad3 moin (t_el2 0.) ;
pad7 = pad1 moin (tt_el2 0.) ;
pad8 = pad4 moin (tt_el2 0.) ;

dad1 = droi 1 pad1 pad2 ;
dad2 = droi 1 pad2 pad3 ;
dad3 = droi 1 pad3 pad4 ;
dad4 = droi 1 pad7 pad5 ;
dad5 = droi 1 pad5 pad6 ;
dad6 = droi 1 pad6 pad8 ;

sa1 = (regl 1 dad1 dad4) et (regl 1 dad2 dad5) et
      (regl 1 dad3 dad6) et (regl 1 dad4 (inve dad6));
saa1 = sa1 ;
repe i0 1 ;
ssa1 = sa1 plus ( 0. (3.*&i0*t_el2)) ;
fin i0 ;
sa1 = sa1 et ssa1 ;
elim sa1 1.e-5 ;

*---------------------- PARTIE GAUCHE -----------------------
sig = sa1 et si1 ; elim sig 1.e-5 ;

*---------------------- PARTIE DROITE -----------------------
sid = sig syme droi ((coor 1 pf) 0.) ((coor 1 pf) lc2) ;
elim sid 1.e-5 ;

*---------------------- PARTIE HAUTE -----------------------

*lignes diagonales pour la symetrie
p_diagod = p2chd plus (lc1 lc1);
p_diago = p2chg moin (lc1 0.);
p_diagog = p_diago plus (0. lc1);

d_diagog = droi 1 p1chg p_diagog;
d_diagod = droi 1 p1chd p_diagod;

sihg = sig syme droi p1chg p_diagog ;
elim sihg 1.e-5 ;
sihd = sid syme droi p1chd p_diagod ;
elim sihd 1.e-5 ;

sih = sihd et sihg ; elim sih 1.e-5 ;

*---------------------- PARTIE COIN -----------------------

dg = droi 1 pid11 p2chg;
dcg = dg tran 1 (0. tt_el2);
dcd = dcg syme droi ((coor 1 pf) 0.) ((coor 1 pf) lc2) ;
sic = dcd et dcg; elim sic 1.e-5;
cout3 = sig et sid et sih et sic ; elim cout3 1.e-5 ;

cout_tot = cout1 et cout2 et cout3;
elim d1cbd cout_tot 1.e-5 ;

**************************************************************
*********************  RESTE DU MAILLAGE  ********************
**************  Partie au-dessus de la fissure  **************
**************************************************************

*Partie de gauche
*----------------
pt1 = mini (coor 1 cout_tot) 0.;
pt2 = (mini (coor 1 cout_tot)) (maxi (coor 2 cout_tot));
pt_partg = cout3 poin droit pt1 pt2 1.e-5 ;
d_partg = (cont cout_tot) elem appuye strictement pt_partg ;
p_ri = ri 0.;
pg = d_partg tran
(((coor 1 p_ri)-(mini(coor 1 cout_tot))) 0.) dini 1.6e-3 dfin 3.2e-3;

*Partie de droite
*----------------
pt3 = maxi (coor 1 cout_tot) 0.;
pt4 = (maxi (coor 1 cout_tot)) (maxi (coor 2 cout_tot));
pt_partd = cout_tot poin droit pt3 pt4 1.e-5 ;
d_partd = (cont cout_tot) elem appuye strictement pt_partd ;
p_re = re 0.;
pd = d_partd tran
(((coor 1 p_re)-(maxi(coor 1 cout_tot))) 0.) dini 1.6e-3 dfin 3.2e-3;
bas_cout = pg et pd et cout_tot ; elim bas_cout 1.e-5 ;

*Partie du haut
*--------------
p5 = (mini (coor 1 bas_cout)) (maxi (coor 2 bas_cout));
p6 = (maxi (coor 1 bas_cout)) (maxi (coor 2 bas_cout));
pt_parth = bas_cout poin droit p5 p6 1.e-5 ;
d_partd = (cont bas_cout) elem appuye strictement pt_parth ;
ph = d_partd tran (0. ((h/2.) - (maxi(coor 2 bas_cout))))
dini 1.6e-3 dfin t ;

*Structure totale
*************************************
s0 = ph et bas_cout ; elim s0 1.e-5 ;
*************************************

**************************************************************
******************     LEVRE SUPERIEUR    ********************
**************************************************************

pa = s0 poin proc (ri 0.);
pb = s0 poin proc (re 0.);

lig1 = (cont s0) elem compris p1cbd pb;

lvsup = (cont s0) elem compris pa pf ;

*Definition des bords

l_hau = cote 3 ph;
l_bas = (cont s0) elem compris pf pb ;
p_gau = s0 poin droit (ri (mini (coor 2 s0)))
(ri (maxi (coor 2 s0))) 1.e-5 ;
l_gau = (cont s0) elem appuye strictement p_gau ;

*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                    FIN DU MAILLAGE
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*                       PARTIE CALCULS
*=============================================================
**************************************************************
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

* PROPRIETE MATERIAUX A 300°C
E0 = 185e3; nu0 = 0.3; alfa0 = 13.08e-6;

mo0 = mode s0 mecanique elastique isotrope ;
ma0 = mate mo0 YOUN E0 nu nu0 alph alfa0  ;
rg0 = rigi mo0 ma0 ;

*CONDITIONS AUX LIMITES
*Blocages
cl1 = bloq uz l_bas ;
cl2 = rela ense uz l_hau ;
cl0 = cl1 et cl2;

*Traction uniaxiale (via un modele de pression)
moph = MODE l_hau 'CHARGEMENT' 'PRESSION' 'CONS' 'HAUT' ;

*Pression sur les levres (via un modele de pression)
mopl = MODE lvsup 'CHARGEMENT' 'PRESSION' 'CONS' 'LEVRES' ;

*Elevation de temperature
chr = ((s0 coor 1) - ri) / (re - ri) ;
cht0 = nomc 'T' (dt0 * chr);


***********************
* SOLUTIONS ANALYTIQUES
***********************
*Fonctions d'influence
i0 = 1.211 ;
i1 = 0.718 ;

*Contraintes imposées pour le gradient de temperature
sig0 = ((E0*alfa0*dt0)/(1-nu0)) * (ri/(3*t)) *
((2*(re**2))/(ri*(re+ri)) - 1);
sig1 = -1. * ((E0*alfa0*dt0)/(1-nu0));

* J analytiques
JT = (1-(nu0**2)) * ((i0*(-1.*p0T)*((pi*a)**(1./2.)))**2) / E0;
JP = (1-(nu0**2)) * ((i0*(-1.*p0P)*((pi*a)**(1./2.)))**2) / E0;
JTH = (1-(nu0**2)) *
((((i0*sig0)+ (i1*sig1*(a/t)))*((pi*a)**(1./2.)))   **2)/E0;


****************************************************
* CALCUL ELASTIQUE AVEC RESO - CALCUL DE J ELASTIQUE
****************************************************

* Construction des second membres
maph = pres moph 'PRES' p0T ;
f0T  = BSIG moph maph ;
mapl = pres mopl 'PRES' p0P ;
f0P  = BSIG mopl mapl ;
sgth0 = THET mo0 ma0 cht0 ;
f0TH  = BSIG mo0 sgth0 ;

* RESOLUTION ELASTIQUE DES 3 PROBLEMES
utestT utestP utestTH = RESO (rg0 ET cl0) f0T f0P f0TH;
mena ;

*PROCEDURE G_THETA
*cas 1 : traction seule
tabJel = table ;
tabJel . 'MODELE'                 = mo0 ET moph ;
tabJel . 'CARACTERISTIQUES'       = ma0 ;
tabJel . 'PRESSION'               = maph ;
tabJel . 'BLOCAGES_MECANIQUES'    = cl0 ;
tabJel . 'SOLUTION_RESO'          = utestT ;
tabJel . 'OBJECTIF'               = MOT 'J' ;
tabJel . 'LEVRE_SUPERIEURE'       = lvsup ;
tabJel . 'FRONT_FISSURE'          = pf ;
tabJel . 'COUCHE'                 = 5 ;
g_theta tabJel ;
JelT1 = tabJel.resultats ;

*cas 2 : pression sur les levres
tabJel . 'MODELE'                 = mo0 ET mopl;
tabJel . 'CARACTERISTIQUES'       = ma0 ;
tabJel . 'PRESSION'               = mapl ;
tabJel . 'SOLUTION_RESO'          = utestP ;
g_theta tabJel ;
JelP1 = tabJel.resultats ;

*cas 3 : gradient de temperature
tabJel . 'MODELE'                 = mo0 ;
tabJel . 'CARACTERISTIQUES'       = ma0 ;
tabJel . 'TEMPERATURES' = cht0 ;
tabJel . 'SOLUTION_RESO'          = utestTH ;
g_theta tabJel ;
JelTH1 = tabJel.resultats ;

*Erreurs sur J  : solution analytique VS calcul RESO + G_THETA
errT1  = ((JelT1-JT)/JT)*100.;
errP1  = ((JelP1-JP)/JP)*100.;
errTH1 = ((JelTH1-JTH)/JTH)*100.;



*******************************************************
* CALCUL ELASTIQUE AVEC PASAPAS - CALCUL DE J ELASTIQUE
*******************************************************

* Chargements de pression (obligatoires si modele de pression)
evph = EVOL 'MANU' 'TEMP' (PROG 0. 1. 2. 3.)
                   'PRES' (PROG 0. 1. 0. 0.) ;
chaph = CHAR 'PRES' maph evph ;
evpl = EVOL 'MANU' 'TEMP' (PROG 0. 1. 2. 3.)
                   'PRES' (PROG 0. 0. 1. 0.) ;
chapl = CHAR 'PRES' mapl evpl ;

* Chargement thermique
chath = CHAR 'T' cht0 (EVOL 'MANU' (PROG 0. 1. 2. 3.)
                                   (PROG 0. 0. 0. 1.)) ;

*RESOLUTION AVEC PASAPAS DES 3 PROBLEMES (UN A CHAQUE PAS DE TEMPS)
*AU PAS 1 : Traction seule
*AU PAS 2 : Pression sur les levres
*AU PAS 3 : Gradient de temperature
tabT = TABL ;
tabT . 'MODELE' = mo0 ET moph ET mopl ;
tabT . 'CARACTERISTIQUES' = ma0 ;
tabT . 'BLOCAGES_MECANIQUES' = cl0 ;
tabT . 'CHARGEMENT' = chaph ET chapl ET chath ;
tabT . 'TEMPS_CALCULES' = PROG 1. 2. 3. ;
PASAPAS tabT ;

*PROCEDURE G_THETA POUR LES 3 PROBLEMES (UN A CHAQUE PAS DE TEMPS)
tabJel = TABL ;
tabJel . 'SOLUTION_PASAPAS'       = tabT ;
tabJel . 'OBJECTIF'               = MOT 'J' ;
tabJel . 'LEVRE_SUPERIEURE'       = lvsup ;
tabJel . 'FRONT_FISSURE'          = pf ;
tabJel . 'COUCHE'                 = 5 ;
g_theta tabJel ;
JelT2  = tabJel.resultats.1 ;
JelP2  = tabJel.resultats.2 ;
JelTH2 = tabJel.resultats.3 ;
*Erreurs sur J  : solution analytique VS calcul PASAPAS + G_THETA
errT2  = ((JelT2-JT)/JT)*100.;
errP2  = ((JelP2-JP)/JP)*100.;
errTH2 = ((JelTH2-JTH)/JTH)*100.;




****************************************
* AFFICHAGE DES RESULTATS ET DES ERREURS
****************************************
SAUT 5 'LIGNE' ;
mess 'Solution Theorique     : ' JT JP JTH ;
mess ;
mess 'Solution MEF (RESO)    : ' JelT1 JelP1 JelTH1 ;
mess 'Erreur en %            : ' errT1 errP1 errTH1 ;
mess ;
mess 'Solution MEF (PASAPAS) : ' JelT2 JelP2 JelTH2 ;
mess 'Erreur en %            : ' errT2 errP2 errTH2 ;

* Test sur les erreurs
errT = MAXI 'ABS' (PROG errT1 errT2) ;
si ((abs errT) > 0.3) ;
  erre 'Erreur sur le calcul de JelT' ;
fins ;
errP = MAXI 'ABS' (PROG errP1 errP2) ;
si ((abs errP) > 0.3) ;
  erre 'Erreur sur le calcul de JelP' ;
fins ;
errTH = MAXI 'ABS' (PROG errTH1 errTH2) ;
si ((abs errTH) > 0.4) ;
  erre 'Erreur sur le calcul de JelTH' ;
fins ;

FIN ;




