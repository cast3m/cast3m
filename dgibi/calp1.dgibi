*----------------------------------------------------------------------*
* Cas-test du calcul de VMIS dans le cas des poutres.                  *
*                                                                      *
* On soumet l'extremite d'une poutre orientee suivant l'axe Ox a un    *
* deplacement suivant Oy, l'autre extremite etant encastree.           *
*                                                                      *
* On verifie le calcul de la contrainte de von Mises.                  *
*                                                                      *
* Le calcul est realise en DIME 2 ELEM SEG2.                           *
*----------------------------------------------------------------------*

opti dime 2 elem seg2 ; 

* Maillage :
O1 = 0 0  ; 
X1 = 1 0  ; 
L1 = O1 droi 10 (10 * X1) ; 
P2 = L1 poin 2 ; 

* Modele / caracteristiques :
mo1  = mode L1 mecanique pout ;
sec1 = 0.0025 ;
dy1  = 0.03 ;
irz1 = 1. ;
tor1 = 1. ; 
ma1  = mate mo1 youn 20.e9 nu 0.2 sect sec1 dy dy1 inrz irz1 ; 

* CL / Chargement :
cl1 = bloq depl rota O1 ; 
cl2 = bloq uy P2 ; 

dcl2 = depi cl2 0.01 ; 

* Resolution :
K1 = rigi mo1 ma1 ; 
u1 = reso (K1 et cl1 et cl2) dcl2 ; 

sig1 = sigm mo1 ma1 u1 ; 

* VMIS :
vmi1 = vmis mo1 ma1 sig1 ; 
*list vmi1 ;

* Calcul de VMIS selon la formule indiquee dans sa notice :
* Seq = ((EFFX / SECT)**2 + (MOMZ * DY / INRZ)**2)**0.5
Seq1 = ((exco sig1 effx scal) / sec1 ** 2)
     + ((exco sig1 momz scal) * dy1 / irz1 ** 2) ;
Seq1 = Seq1 ** 0.5 ;
*List Seq1 ;

* Erreur calcul VMIS :
ERR1 = maxi abs (vmi1 - seq1) ;
list err1 ;

xpr1 = (vale prec) ** 0.5 ;
si (err1 > xpr1) ;
  erre 5 ;
fins ;

fin ;







