OPTI 'DIME' 2 'ECHO' 0 ;

************************************************************************
*                    Test de la procedure PROJGRIL                     *
*    Projection dans 2 dimensions d'un nuage representant une grille   *
*                           de n dimensions                            *
*        utilise dans le cas de l'operateur IPOL option 'GRILL'        *
*                                                                      *
* - test avec fonction de 2, 3, 4 et meme 5 variables                  *
************************************************************************
MESS ; MESS ; MESS ;




** Tolerance pour les tests et indicateur de trace
** -----------------------------------------------
tol1 = 1.E-14 ;
itrac = FAUX ;




** TEST sur une grille de dimension 2
** ----------------------------------
MESS 'Grille de dimension 2' ;
MESS ;
l1 = PROG 1. 5. 12. ;
l2 = PROG 0. 2.5 ;
lf = PROG 4. 8. 15.   16. 23. 42. ;
nu1 = NUAG 'COMP' 'JIN' l1
           'COMP' 'SUN' l2
           'COMP' 'JACK' lf ;
mail1 chp1 = PROJGRIL nu1 ;
tp = TABL ;
tf = TABL ;
tp . 1 = 1. 0. ;
tf . 1 = 4. ;
tp . 2 = 5. 0. ;
tf . 2 = 8. ;
tp . 3 = 12. 0. ;
tf . 3 = 15. ;
tp . 4 = 1. 2.5 ;
tf . 4 = 16. ;
tp . 5 = 5. 2.5 ;
tf . 5 = 23. ;
tp . 6 = 12. 2.5 ;
tf . 6 = 42. ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp1 'JACK' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp1 mail1 'TITR' 'Test sur une grille en dimension 2' ;
FINSI ;
MESS ; MESS ; MESS ;




** TEST sur une grille de dimension 3
** ----------------------------------
MESS 'Grille de dimension 3' ;
MESS ;
l3 = PROG 0. 10. ;
lf = lf ET (PROG 8. 42. 4.   16. 15. 23.) ;
nu1 = NUAG 'COMP' 'EKO' l1
           'COMP' 'BEN' l2
           'COMP' 'BOONE' l3
           'COMP' 'KATE' lf ;
mail1 chp1 = PROJGRIL nu1 (MOTS 'BEN') (PROG 1.25) ;
tp = TABL ;
tf = TABL ;
tp . 1 = 1. 0. ;
tf . 1 = 10. ;
tp . 2 = 5. 0. ;
tf . 2 = 15.5 ;
tp . 3 = 12. 0. ;
tf . 3 = 28.5 ;
tp . 4 = 1. 10. ;
tf . 4 = 12. ;
tp . 5 = 5. 10. ;
tf . 5 = 28.5 ;
tp . 6 = 12. 10. ;
tf . 6 = 13.5 ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp1 'KATE' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp1 mail1 'TITR' 'Test sur une grille en dimension 3' ;
FINSI ;
MESS ; MESS ; MESS ;



** TEST sur une grille de dimension 4
** ----------------------------------
MESS 'Grille de dimension 4' ;
MESS ;
l4 = PROG 1977. 2004. ;
lf = lf ET (PROG 42.  4. 23.   8. 15. 16.
                  4. 23. 16.   8. 15. 42.) ;
nu1 = NUAG 'COMP' 'WALT' l1
           'COMP' 'ROSE' l2
           'COMP' 'JACOB' l3
           'COMP' 'SAYID' l4
           'COMP' 'HUGO' lf ;
mail1 chp1 = PROJGRIL nu1 (MOTS 'ROSE' 'WALT') (PROG 1.25 8.5) ;
tp = TABL ;
tf = TABL ;
tp . 1 = 0. 1977. ;
tf . 1 = 22. ;
tp . 2 = 10. 1977. ;
tf . 2 = 21. ;
tp . 3 = 0. 2004. ;
tf . 3 = 14.5 ;
tp . 4 = 10. 2004. ;
tf . 4 = 24. ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp1 'HUGO' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp1 mail1 'TITR' 'Test sur une grille en dimension 4' ;
FINSI ;
MESS ; MESS ; MESS ;




** TEST sur une grille de dimension 5
** ----------------------------------
MESS 'Grille de dimension 5' ;
MESS ;
l5 = PROG 1. 4. 6. ;
lf = lf ET (PROG  4. 23. 15.    4.  8. 15.   4.  8. 15.   42. 23. 16.
                  8. 42. 16.   42. 23. 16.  16. 23. 16.   15.  8. 15.
                 15.  4. 23.    4.  8. 15.  15. 42. 23.   16.  4.  8.
                 16.  8. 42.   42. 23. 16.   8.  4. 42.   23. 42.  4.) ;
nu1 = NUAG 'COMP' 'ALEX' l1
           'COMP' 'ANA' l2
           'COMP' 'SAWYER' l3
           'COMP' 'CHANG' l4
           'COMP' 'DESMOND' l5
           'COMP' 'JOHN' lf ;
mail1 chp1 = PROJGRIL nu1 (MOTS 'ALEX' 'CHANG' 'SAWYER' )
                          (PROG   5.    1984.     10.   ) ;
xb = (1984. - 1977.) / (2004. - 1977.) ;
xbb = 1. - xb ;
tp = TABL ;
tf = TABL ;
tp . 1 = 0. 1. ;
tf . 1 = (42. * xbb) + (23. * xb) ;
tp . 2 = 2.5 1. ;
tf . 2 = 15. ;
tp . 3 = 0. 4. ;
tf . 3 = (8. * xbb) + (23. * xb) ;
tp . 4 = 2.5 4. ;
tf . 4 = (23. * xbb) + (8. * xb) ;
tp . 5 = 0. 6. ;
tf . 5 = (42. * xbb) + (4. * xb) ;
tp . 6 = 2.5 6. ;
tf . 6 = (4. * xbb) + (42. * xb) ;
MESS ' Point  |   Valeur       |   Valeur       | Erreur' ;
MESS '        |   theorique    |   interpolee   |       ' ;
MESS '--------|----------------|----------------|-------' ;
REPE b1 (DIME tp) ;
  ft = tf . &b1 ;
  pt = mail1 POIN 'PROC' (tp . &b1) ;
  fc = EXTR chp1 'JOHN' pt ;
  err1 = ABS ((fc - ft) / ft) ;
  MESS &b1 '|' ft '|' fc '|' err1 ;
  SI (err1 > tol1) ;
    MESS ; MESS ;
    MESS 'ECHEC DU CAS TEST !' ;
    ERREUR 4 ;
  FINSI ;
FIN b1 ;
SI itrac ;
  TRAC chp1 mail1 'TITR' 'Test sur une grille en dimension 5' ;
FINSI ;
MESS ; MESS ; MESS ;




MESS ; MESS ;
MESS 'SUCCES DU CAS TEST' ;




FIN ;



