* fichier :  timp_echanp.dgibi
'OPTI' 'DIME' 2 'ELEM' 'QUA4' ;

DISCR = 'LINE' ;
KPRES = 'MSOMMET' ;

EPSI  = 1.0E-5 ;
NPAS  = 20 ;

*** cavité soumise à une différence de température fluide/paroi
*** avec une stratification thermique initiale

L1 = 1.0 ;
H1 = 2.0 ;

P1 = 0.0 0.0 ;
P2 =  L1 0.0 ;
P3 =  L1  H1 ;
P4 = 0.0  H1 ;

NX1 =  5 ;
NY1 = 10 ;

D1 = 'DROIT' P1 P2 NX1 ;
D2 = 'DROIT' P2 P3 NY1 ;
D3 = 'DROIT' P3 P4 NX1 ;
D4 = 'DROIT' P4 P1 NY1 ;

S1 = 'DALLER' D1 D2 D3 D4 ;

*********************
*** partie CALCUL ***
*********************

*** calcul de la façon standard de type TIMP1
RXT1 = 'TABLE' ;

RXT1.'vtf' = S1 ;
RXT1.'pi' = S1 'POIN' 'PROC' ((L1/2.0) (H1/2.0)) ;
RXT1.'IMPR' = 2 ;

TF0 = 100.0 ;

RXT1.'TF0' = TF0 ;
RXT1.'PT0' = 1.0E5 ;

RXT1.'MODTURB' = 'NUTURB' ;
RXT1.'NUT' = 200.0*2.0E-5 ;

RXT1.'epsi'  = EPSI  ;
RXT1.'DISCR' = DISCR ;
RXT1.'KPRE'  = KPRES ;
RXT1.'GRAPH' = FAUX  ;

RXT1.'TIMP1' = 'TABLE' ;
RXT1.'TIMP1'.'MAILLAGE' = D2 'ET' D4 ;
RXT1.'TIMP1'.'ECHAN'    = 10.0 ;
RXT1.'TIMP1'.'t'        = 'PROG'    0.0   1000.0 ;
RXT1.'TIMP1'.'TIMP'     = 'PROG'   20.0     20.0 ;

RXT1.'DT0' = 1.0 ;

*** calcul de la façon ECHANP
RXT2 = 'TABLE' ;

RXT2.'vtf' = S1 ;
RXT2.'pi' = S1 'POIN' 'PROC' ((L1/2.0) (H1/2.0)) ;
RXT2.'IMPR' = 2 ;

RXT2.'TF0' = TF0 ;
RXT2.'PT0' = 1.0E5 ;

RXT2.'MODTURB' = 'NUTURB' ;
RXT2.'NUT' = 200.0*2.0E-5 ;

RXT2.'epsi'  = EPSI  ;
RXT2.'DISCR' = DISCR ;
RXT2.'KPRE'  = KPRES ;
RXT2.'GRAPH' = FAUX  ;

RXT2.'ECHANP' = 'TABLE' ;
RXT2.'ECHANP'.'MAILLAGE' = D2 'ET' D4 ;
RXT2.'ECHANP'.'ECHAN' = 10.0 ;
RXT2.'ECHANP'.'TMUR'  = 20.0 ;

RXT2.'DT0' = 1.0 ;

EXECRXT  RXT1 0 ;
EXECRXT  RXT2 0 ;

EPS1 = 30.0 ;
*** définition d'une stratification thermique instable
ZZ1 = 'COORD' 2 RXT1.'GEO'.'vtf' ;
TF_Z1 = (-2.0 * EPS1 / H1 * ZZ1) + (TF0 + EPS1) ;
RXT1.'TIC'.'TF' = 'KCHT' RXT1.'GEO'.'$vtf' 'SCAL' 'SOMMET' TF_Z1 ;

ZZ2 = 'COORD' 2 RXT2.'GEO'.'vtf' ;
TF_Z2 = (TF0 + EPS1) - ((2.0 * EPS1 / H1) * ZZ2) ;
RXT2.'TIC'.'TF' = 'KCHT' RXT2.'GEO'.'$vtf' 'SCAL' 'SOMMET' TF_Z2 ;

*** itération en temps

EXECRXT  RXT1 NPAS ;
EXECRXT  RXT2 NPAS ;

*** tests de non-régression
*** la température fluide calculée par les deux péthodes doit être
*** strictement la même

vtf1 = RXT1.'GEO'.'vtf' ;
vtf2 = RXT2.'GEO'.'vtf' ;
'ELIM' (vtf1 'ET' vtf2) EPSI ;

TF1  = RXT1.'TIC'.'TF'  ;
TF2  = RXT2.'TIC'.'TF'  ;

DIFF1 = 'ABS' (TF2 - TF1) ;

MAX1  = 'MAXI' DIFF1 ;
ERROR = 0 ;

'SI' (MAX1 '>' 1.E-10) ;
ERROR = ERROR + 1 ;
'MESS' 'Erreur dans le calcul de la température fluide' ;
'MESS' 'en utilisant la méthode TIMP1 ou ECHANP' ;
'MESS' '|TF1 - TF2| = ' MAX1 ' > 1.E-10 ' ;
'FINSI' ;

'SI' (ERROR > 0) ;
'MESS' 'Il y a des erreurs !' ;
'ERRE' 5 ;
'FINSI' ;

'FIN' ;




