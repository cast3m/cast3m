* fichier : joi1_coulomb.dgibi
************************************************************************
* Section : Mecanique Plastique
************************************************************************
'OPTI' 'ECHO' 1 ;

'OPTI' 'ELEM' 'SEG2' 'DIME' 3 'MODE' 'TRID' ;
'OPTI' 'EPSILON' 'LINEAIRE'                 ;

GRAPH = FAUX ;

P1 = 0 0 0   ;
P2 = 0 0 0   ;

joint = P1 P2 'DROIT' 1  ;

************************************************************************
*                          Début: MODELE                               *
************************************************************************
mo = MODE joint  'MECANIQUE' 'ELASTIQUE'     'ORTHOTROPE'
                               'PLASTIQUE'     'COULOMB' 'JOI1'        ;

************************************************************************
*                           Fin: MODELE                                *
************************************************************************


************************************************************************
*                          Début: MATERIAU                             *
************************************************************************
kn  = 100. ;
ks1 =  50. ;
ks2 = ks1  ;

qn  = 40.  ;
qs1 = 20.  ;
qs2 = 30.  ;
*
ang  = 45.                      ;
cosa = 'COS' ang                ;
sina = 'SIN' ang                ;
dir1 =     cosa    sina       0 ;
dir2 = (-1*sina)   cosa       0 ;
*
phi  = 45.                      ;
kt   = kn/10.                   ;
fne  = 1E6                      ;

ma = 'MATE' mo 'DIRECTION' dir1 dir2
                                    'KN  ' kn   'KS1 ' ks1  'KS2 ' ks2
                                    'QN  ' qn   'QS1 ' qs1  'QS2 ' qs2
                                    'TYPE' 1.   'FRIC' phi
                                    'QT  ' kt   'FNE ' fne  'COHE' 0.  ;

************************************************************************
*                          Fin: MATERIAU                               *
************************************************************************

************************************************************************
*                          Début: CL & CHARGEMENT                      *
************************************************************************
cl1 = 'BLOQ' 'DEPL' 'DIRECTION' dir1   P1     ;
cl2 = 'BLOQ' 'DEPL' 'DIRECTION' dir2   P1     ;
cl3 = 'BLOQ' 'UZ' 'RX' 'RY' 'RZ'       joint  ;
cl4 = 'BLOQ' 'DEPL' 'DIRECTION' dir1   P2     ;
cl5 = 'BLOQ' 'DEPL' 'DIRECTION' dir2   P2     ;
cl  = cl1 'ET' cl2 'ET' cl3 'ET' cl4 'ET' cl5 ;

vdpi = 0.1                                                            ;
dpi1 = 'DEPI' cl4   vdpi                                              ;
dpi2 = 'DEPI' cl5   vdpi                                              ;
lt   = 'PROG' 0 1 2 3 4  5  6                                         ;
lc1  = 'PROG' 0 -1 -1 -1  -0.5  -0.01  1                              ;
ev1  = 'EVOL' 'MANU' 't' lt 'coef'  lc1                               ;
lc2  = 'PROG' 0  0  1  3     3    3    3                              ;
ev2  = 'EVOL' 'MANU' 't' lt 'coef'  lc2 ;

cha1  = 'CHAR' 'DIMP' dpi1 ev1 ;
cha2  = 'CHAR' 'DIMP' dpi2 ev2 ;
cha   = cha1 'ET' cha2         ;
************************************************************************
*                          Fin: CL & CHARGEMENT                        *
************************************************************************
lis_t = 'PROG'  1  2  2.5  3  4 5 5.0098 6 ;

tab                       = 'TABLE' ;
tab.'MODELE'              = mo      ;
tab.'CARACTERISTIQUES'    = ma      ;
tab.'BLOCAGES_MECANIQUES' = cl      ;
tab.'CHARGEMENT'          = cha     ;
tab.'TEMPS_CALCULES'      = lis_t   ;
tab.'TEMPS_SAUVES'        = lis_t   ;
tab.'FTOL'                = 1E-3    ;

PASAPAS tab ;


time=tab.'TEMPS';
ntime = 'DIME' time ;
n=ntime - 1 ;

progt    = 'PROG' ;
prN      = 'PROG' ;
prFY     = 'PROG' ;
prepse   = 'PROG' ;
pretat   = 'PROG' ;
prepou   = 'PROG' ;
prlam1   = 'PROG' ;
prpla1   = 'PROG' ;
prpla3   = 'PROG' ;
************************************************************************
*                          Début: CONTROLE                             *
************************************************************************
REPETER contro ntime                                                   ;
   i      = &contro - 1                                                ;
   t      = time.i                                                     ;
   progt  = progt 'ET' ('PROG' t)                                      ;
*-
   sig    = tab.'CONTRAINTES'.i                                        ;
   prN    = prN  'ET' ('PROG' ('EXTR' sig 'EFFX' 1 1 1))               ;
   prFY   = prFY 'ET' ('PROG' ('EXTR' sig 'EFFY' 1 1 1))               ;
*-
   vi    = tab.'VARIABLES_INTERNES'.i                                  ;
  prepse = prepse 'ET' ('PROG' ('EXTR' vi 'EPSE' 1 1 1))               ;
  pretat = pretat 'ET' ('PROG' ('EXTR' vi 'STAT' 1 1 1))               ;
  prepou = prepou 'ET' ('PROG' ('EXTR' vi 'EPOU' 1 1 1))               ;
  prlam1 = prlam1 'ET' ('PROG' ('EXTR' vi 'LAM1' 1 1 1))               ;
  prpla1 = prpla1 'ET' ('PROG' ('EXTR' vi 'PLA1' 1 1 1))               ;
  prpla3 = prpla3 'ET' ('PROG' ('EXTR' vi 'PLA3' 1 1 1))               ;
FIN contro                                                             ;
************************************************************************
*                          Début: CONTROLE                             *
************************************************************************

evN    = evol manu 't' progt 'N'    prN      ;
evFY   = evol manu 't' progt 'FY'   prFY     ;
evepse = evol manu 't' progt 'EPSE' prepse   ;
evetat = evol manu 't' progt 'STAT' pretat   ;
evepou = evol manu 't' progt 'EPOU' prepou   ;
evpla1 = evol manu 't' progt 'PLA1' prpla1   ;
evpla3 = evol manu 't' progt 'PLA3' prpla3   ;
evlam1 = evol manu 't' progt 'LAM1' prlam1   ;

mu = ('SIN' phi) / ('COS' phi) ;

*-
* 0 <eg t <eg 1 (purement elastique)
* ----------------------------------
t1      = 'EXTR' 1 lis_t              ;
N_t1    =  kn*vdpi*('IPOL' t1 lt lc1) ;
Fy_t1   = ks1*vdpi*('IPOL' t1 lt lc2) ;
etat_t1 = 2                           ;
epou_t1 = 0.                          ;
pla1_t1 = 0.                          ;
pla3_t1 = 0.                          ;
lam1_t1 = 0.                          ;
epse_t1 = 0.                          ;

* 1 <eg t <eg 2 (purement elastique)
* ----------------------------------
t2      = 'EXTR' 2 lis_t              ;
N_t2    =  kn*vdpi*('IPOL' t2 lt lc1) ;
Fy_t2   = ks1*vdpi*('IPOL' t2 lt lc2) ;
etat_t2 = 2                           ;
epou_t2 = 0.                          ;
pla1_t2 = 0.                          ;
pla3_t2 = 0.                          ;
lam1_t2 = 0.                          ;
epse_t2 = 0.                          ;

* 2 <eg t <eg 2.5 (purement elastique)
* ------------------------------------
t3      = 'EXTR' 3 lis_t              ;
N_t3    =  kn*vdpi*('IPOL' t3 lt lc1) ;
Fy_t3   = ks1*vdpi*('IPOL' t3 lt lc2) ;
etat_t3 = 2                           ;
epou_t3 = 0.                          ;
pla1_t3 = 0.                          ;
pla3_t3 = 0.                          ;
lam1_t3 = 0.                          ;
epse_t3 = 0.                          ;

* 2.5 <eg t <eg 3 (plastique)
* ---------------------------
t4      = 'EXTR' 4 lis_t                        ;
N_t4    =  kn*vdpi*('IPOL' t4 lt lc1)           ;
Fy_t4   = (-1*mu)*N_t4                          ;
etat_t4 = 2                                     ;
epou_t4 = 0.                                    ;
pla1_t4 = (vdpi*('IPOL' t4 lt lc2))-(Fy_t4/ks1) ;
pla3_t4 = 0.                                    ;
lam1_t4 = 0.                                    ;
epse_t4 = ((2./3.)**0.5)*pla1_t4                ;

* 3 <eg t <eg 4 (plastique)
* -------------------------
t5      = 'EXTR' 5 lis_t                        ;
N_t5    =  kn*vdpi*('IPOL' t5 lt lc1)           ;
Fy_t5   = (-1*mu)*N_t5                          ;
etat_t5 = 2                                     ;
epou_t5 = 0.                                    ;
pla1_t5 = (vdpi*('IPOL' t5 lt lc2))-(Fy_t5/ks1) ;
pla3_t5 = 0.                                    ;
lam1_t5 = 0.                                    ;
epse_t5 = ((2./3.)**0.5)*pla1_t5                ;

* 4 <eg t <eg 5 (plastique)
* -------------------------
t6      = 'EXTR' 6 lis_t                        ;
N_t6    =  kn*vdpi*('IPOL' t6 lt lc1)           ;
Fy_t6   = (-1*mu)*N_t6                          ;
etat_t6 = 2                                     ;
epou_t6 = 0.                                    ;
pla1_t6 = (vdpi*('IPOL' t6 lt lc2))-(Fy_t6/ks1) ;
pla3_t6 = 0.                                    ;
lam1_t6 = 0.                                    ;
epse_t6 = ((2./3.)**0.5)*pla1_t6                ;

* 5 <eg t <eg 5.0098 (plastique)
* ------------------------------
t7      = 'EXTR' 7 lis_t                        ;
N_t7    =  kn*vdpi*('IPOL' t7 lt lc1)           ;
Fy_t7   = (-1*mu)*N_t7                          ;
etat_t7 = 2                                     ;
epou_t7 = 0.                                    ;
pla1_t7 = (vdpi*('IPOL' t7 lt lc2))-(Fy_t7/ks1) ;
pla3_t7 = 0.                                    ;
lam1_t7 = 0.                                    ;
epse_t7 = ((2./3.)**0.5)*pla1_t7                ;

* 5.0098 < t <eg 6 (le joint s'ouvre)
* -----------------------------------
t8      = 'EXTR' 8 lis_t                                ;
N_t8    = 0.                                            ;
Fy_t8   = 0.                                            ;
etat_t8 = 1                                             ;
pla1_t8 = (vdpi*('IPOL' t8 lt lc2))                     ;
pla3_t8 = (vdpi*('IPOL' t8 lt lc1))                     ;
epou_t8 = pla3_t8                                       ;
lam1_t8 = pla3_t8                                       ;
deltap  = (2./3.)*(((pla1_t8-pla1_t7)**2)+(pla3_t8**2)) ;
deltap  = deltap**0.5                                   ;
epse_t8 = epse_t7 + deltap                              ;


progt  = 'PROG' 0     t1      t2      t3    t4    t5    t6    t7    t8 ;
progN  = 'PROG' 0   N_t1    N_t2    N_t3  N_t4  N_t5  N_t6  N_t7  N_t8 ;
progFy = 'PROG' 0  Fy_t1   Fy_t2   Fy_t3 Fy_t4 Fy_t5 Fy_t6 Fy_t7 Fy_t8 ;
etat   = 'PROG'
    0  etat_t1 etat_t2 etat_t3 etat_t4 etat_t5 etat_t6 etat_t7 etat_t8 ;
epou   = 'PROG'
    0  epou_t1 epou_t2 epou_t3 epou_t4 epou_t5 epou_t6 epou_t7 epou_t8 ;
pla1   = 'PROG'
    0  pla1_t1 pla1_t2 pla1_t3 pla1_t4 pla1_t5 pla1_t6 pla1_t7 pla1_t8 ;
pla3   = 'PROG'
    0  pla3_t1 pla3_t2 pla3_t3 pla3_t4 pla3_t5 pla3_t6 pla3_t7 pla3_t8 ;
lam1   = 'PROG'
    0  lam1_t1 lam1_t2 lam1_t3 lam1_t4 lam1_t5 lam1_t6 lam1_t7 lam1_t8 ;
epse   = 'PROG'
    0  epse_t1 epse_t2 epse_t3 epse_t4 epse_t5 epse_t6 epse_t7 epse_t8 ;

tN    = evol roug manu 't' progt 'N'    progN    ;
tFy   = evol roug manu 't' progt 'FY'   progFy   ;
tetat = evol roug manu 't' progt 'STAT' etat     ;
tepou = evol roug manu 't' progt 'EPOU' epou     ;
tpla1 = evol roug manu 't' progt 'PLA1' pla1     ;
tpla3 = evol roug manu 't' progt 'PLA3' pla3     ;
tlam1 = evol roug manu 't' progt 'LAM1' lam1     ;
tepse = evol roug manu 't' progt 'EPSE' epse     ;


t             = 'TABLE'           ;
t . 1         = 'MOT' 'MARQ CROI' ;
t . 2         = 'MOT' 'MARQ LOSA' ;
t.'TITRE'     = 'TABLE'           ;
t.'TITRE' . 1 = 'Calcul'          ;
t.'TITRE' . 2 = 'Theorie'         ;


'SI' GRAPH ;
'DESS' (tN  'ET' evN)    'TITR' 'Effort normal'                t 'LEGE';
'DESS' (tFy 'ET' evFY)   'TITR' 'Effort de glissement'         t 'LEGE';
'DESS' (tetat et evetat) 'TITR' 'Etat du joint'                t 'LEGE';
'DESS' (tepou et evepou) 'TITR' 'Inc. def. plas. en ouverture' t 'LEGE';
'DESS' (tpla1 et evpla1) 'TITR' 'Def. plas. de glissement'     t 'LEGE';
'DESS' (tpla3 et evpla3) 'TITR' 'Def. plas. normale'           t 'LEGE';
'DESS' (tlam1 et evlam1) 'TITR' 'Mult. plas. / compression'    t 'LEGE';
'DESS' (tepse et evepse) 'TITR' 'Def. plas. cumulee'           t 'LEGE';
'FINSI' ;
*
* ----------    CODE DE FONCTIONNEMENT       ----------
*
dif_N    = 'MAXI' ('ABS' ('EXTR'  (tN  - evN)       'ORDO')) ;
dif_Fy   = 'MAXI' ('ABS' ('EXTR'  (tFy - evFY)      'ORDO')) ;
dif_pla1 = 'MAXI' ('ABS' ('EXTR'  (tpla1 - evpla1)  'ORDO')) ;
dif_pla3 = 'MAXI' ('ABS' ('EXTR'  (tpla3 - evpla3)  'ORDO')) ;

test = 'MAXI' ('PROG' dif_N  dif_Fy  dif_pla1  dif_pla3) ;


'SI' (test '<EG' 1E-6 ) ;
  'ERRE'  0 ;
'SINO';
   'ERRE'  5 ;
'FINSI' ;

*opti donn 5;
'FIN' ;









