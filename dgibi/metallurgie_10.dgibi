* fichier :  metallurgie_10.dgibi
************************************************************************
* Section : Metallurgie
************************************************************************

*__________________________________________________________________*
*   TEST METALLURGIE_10                                            *
*                                                                  *
*   CALCUL DES PROPORTIONS DE PHASE METALLURGIQUE                  *
*                                                                  *
*                                                                  *
*   Le modele metallurgique est code en ESOPE (Cast3m)             *
*                                                                  *
*__________________________________________________________________*
*                                                                  *
*   Vitesse de refroidissement : 4 C/s                             *
*      - Transformation (type Leblond) de l'autenite               *
*        en ferrite seulement                                      *
*      - La valeur calculee de la proportion d'AUSTENITE  est      *
*        comparÃ©e avec la valeur analytique                        *
*__________________________________________________________________*
*
'OPTI' 'TRAC' PSC 'EPTR' 10 ;
IDESS = VRAI ;

OPTI DIME 2 ELEM 'TRI3';

P1 = 0. 0. ;
P2 = 1. 0. ;
P3 = 0. 1. ;

MAILT   = MANU 'TRI3' P1 P2 P3;

LISTPHA = MOTS 'AUST' 'FERR' ;
LISTREAC = MOTS 'AUST' ;
LISTPROD = MOTS 'FERR' ;
LISTTYPE = MOTS 'LEBL' ;
NOMCONS = '16MND5';

MODCP1 = MODE MAILT 'METALLURGIE' 'PHASES' LISTPHA
                                  'REACTIFS' LISTREAC
                                  'PRODUITS' LISTPROD
                                  'TYPE' LISTTYPE
                                  'CONS' NOMCONS ;

TINI = 735.  ;
TFIN = 730.  ;

PEQ1 = 'EVOL' 'MANU' 'T' (PROG 625 630 730 735)
                     'PEQ' (PROG 0 1 1 0) ;

F1  = 'EVOL' 'MANU' 'TPOI' (PROG -8.5 -6.2 -4.7 -4.6 0 1.D-6)
                    'F' (PROG 0.001 0.13 1.5 1.5 1.5 0) ;

TAU1 = 5. ;

*Declaration de la vitesse de refroidissement : en Degres/s
DT_vit = -4. ;

F1L = 'IPOL' DT_vit F1 ;

*Calcul de la duree du refroidissement :
DUREE = -1. * (TINI - TFIN) / DT_vit ;

*Declaration du Temps initial :
TEMPINI = 1.D0 ;
*Calcul du Temps final :
TEMPFIN = TEMPINI + DUREE ;
*Calcul du pas de temps en fonction de la vitesse de refroidissement :
DTPS    = -10 / DT_vit ;

LTCAL   ='PROG' TEMPINI 'PAS' DTPS TEMPFIN ;
T      = TINI + (DT_vit * (LTCAL - TEMPINI)) ;

ALPHA = 1. ;
*Initialisation de CHE1 : proportions de phases initiales :
PROP = 'PROG' ALPHA ;
CTYP = 'CHAI' 'VARIABLES_INTERNES' ;
CHE1 ='MANU' 'CHML' MODCP1 'AUST' ('EXTR' PROP 1)
                           'FERR' 0.
               'TYPE' CTYP 'NOEUD' ;

nbpas = (DIME LTCAL) - 1;
REPE CTEMP nbpas;

* On recupere le temps debut de pas et le temps fin de pas :
  temp0   = EXTR LTCAL  &CTEMP     ;
  temp1   = EXTR LTCAL (&CTEMP + 1);
  CHETPS0 = 'MANU' 'CHML' MAILT 'TEMP' temp0 ;
  CHETPS1 = 'MANU' 'CHML' MAILT 'TEMP' temp1 ;
* On calcule la temperature debut de pas et la temperature fin de pas :
  T0      = EXTR T  &CTEMP             ;
  T1      = EXTR T (&CTEMP + 1)        ;
  CHT0    = 'MANU' 'CHML' MAILT 'T' T0 ;
  CHT1    = 'MANU' 'CHML' MAILT 'T' T1 ;

* On determine les differents parametres materiaux :
  PEQ11L = 'IPOL' T0 PEQ1 ;
  PEQ12L = 'IPOL' T1 PEQ1 ;

* On definit le Materiaux debut de pas et le materiaux fin de pas :
  MATCP1 = 'MATE' MODCP1 'PEQ1' PEQ11L 'TAU1' TAU1 'F1' F1L;
  MATCP2 = 'MATE' MODCP1 'PEQ1' PEQ12L 'TAU1' TAU1 'F1' F1L;


  CHE1 = COMP MODCP1 (CHE1 'ET' CHETPS0 'ET' CHT0 'ET' MATCP1 ) ( CHETPS1 'ET' CHT1 'ET' MATCP2 ) ;

* Extraction pour tracer :
  Pi     ='EXCO' CHE1 AUST 'AUST';
  PiEvol = 'EXTR' Pi AUST 1 1 1;
  PROP = PROP 'ET' PiEvol ;

FIN CTEMP;

*Evolution calculee :
EVO   = 'EVOL' 'NOIR' 'MANU' 'TEMP' LTCAL 'PROP' PROP;

*Evolution analytique :
*PANALY = ( F1 * (TEMPFIN - LTCAL) + (TAU1 * (1 - EXP( -1 * F1 * (LTCAL - TEMPINI) / TAU1 ) )) ) / ( F1 * (TEMPFIN - LTCAL) ) ;
PANALY = EXP( -1 * F1L * (LTCAL - TEMPINI) / TAU1 ) ;
PANALY = (F1L * (TEMPFIN - LTCAL)) + (TAU1 * (1 - PANALY )) ;
PANALY = PANALY / ( F1L * (TEMPFIN - TEMPINI) ) ;
EVOANA = 'EVOL' 'VERT' 'MANU' 'TEMP' LTCAL 'AUST' PANALY;

*Erreur absolue entre analytique et calculee :
ERRABS  = 'ABS' (PANALY '-' PROP) ;
EVOERR = 'EVOL' 'ROUG' 'MANU' 'TEMP' LTCAL 'ERREUR' ERRABS ;

'SI' IDESS ;

  TL = TABL ;
  TL . 1 = 'TIRR' ;
  TL . 2 = 'MARQ ROND NOLI' ;
  TL . 'TITRE' = TABL ;
  TL . 'TITRE' . 1 = 'Evolution analytique' ;
  TL . 'TITRE' . 2 = 'Evolution calculee' ;
  DESS ( EVOANA 'ET' EVO ) 'GRIL' 'TIRC' 'GRIS'
                            'LEGE' TL
                            'TITR' 'LEBLOND - Refroidissement acier 16MND5 -10 deg/s' ;

  TL = TABL ;
  TL . 1 = 'TIRR' ;
  TL . 'TITRE' = TABL ;
  TL . 'TITRE' . 1 = 'Erreur';
  DESS EVOERR 'GRIL' 'TIRC' 'GRIS'
                          'LEGE' TL
                          'TITR' 'LEBLOND - Refroidissement acier 16MND5 -10 deg/s' ;

'FINSI' ;

*Emission d'une erreur GIBIANE si l'erreur analytique - cast3m est trop grande :
Diff1 = 'MAXI' ERRABS;
Tol1  = 1.D-2; COMM 'Valeur fixee en 2018 par Thibault LINDECKER';

MESS (CHAI '      Erreur : ' Diff1 ' | Tolerence : ' Tol1);
SI (Diff1 >EG Tol1);
 MESS 'Erreur dans le calcul de la proportion d AUSTENITE';
 ERRE 5;
FINS;

fin;


