* fichier :  trac3d.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Dynamique
* Section : Mecanique Fourier
************************************************************************
*
******************************************************************
*                                                                *
*  Mots-clés : Vibrations, calcul modal, coque, 2D Fourier,      *
*              Reconstruction 3D                                 *
*                                                                *
*                        TEST  TRAC3D                            *
*                                                                *
*                                                                *
*                            P3                                  *
*                           |                                    *
*                           |                                    *
*                           |                                    *
*                           |                                    *
*                           |                                    *
*                           |                                    *
*                           |                                    *
*             ______________|                                    *
*             P1             P2                                  *
*  Soit un cylindre dont on calcule la fréquence propre du       *
*  deuxieme mode de flexion en mode fourier 1 puis 3. On         *
*  construit alors les champs de déplacement tridimensionnels    *
*  ainsi que le maillage 3D. on superpose les deux calculs pour  *
*  obtenir un champs de deplacement 3D et une déformée.          *
*                                                                *
******************************************************************
*
OPTION ECHO 1;
GRAPH='N';
SAUT PAGE ;
OPTI NORM AUTO ;
OPTI DIME 2;
OPTI MODE FOUR NOHARM ;
OPTI ELEM SEG2;
OPTI EPSI LINEAIRE;
*
*================================================================*
*               GEOMETRY                                         *
*==============================================================*
*
P1 = 0. 0. ;
P2 = 3.5 0.0 ;
P3 = 3.5 7.0 ;
*
LI1 = P1 DROIT 3 P2 ;
STEEL = LI1 ET (P2 DROIT 8 P3) ;
*
*==============================================================*
*        MODELE - MATERIAU - RIGIDITE - MASSE                  *
*==============================================================*
MODCOQ=MODE STEEL MECANIQUE COQ2 ;
MATCOQ=MATE MODCOQ RHO 8.0E3 YOUN 2.E11 NU 0.3 ;
CARCOQ=CARA MODCOQ EPAI 0.035 ;
MATCOQ=MATCOQ ET CARCOQ;
*==============================================================*
*                BOUNDARY CONDITION                            *
*==============================================================*
CL1 = BLOQ DEPL ROTA LI1 ;
*==============================================================*
*============= MODE FOURIER 1  ================================*
*==============================================================*
OPTI MODE FOUR 1 ;
RIG1 = RIGI  MODCOQ  MATCOQ  ;
MAS1 = MASS  MODCOQ  MATCOQ  ;
*==============================================================*
*        CALCULATION OF THE FREQUENCIES                        *
*                 AND                                          *
*        EXTRACTION OF SOME RESULTS                            *
*==============================================================*
RESUL = VIBR proche (prog  150.)  (CL1 ET RIG1 )  MAS1 IMPR   ;
FREQ1 = RESUL.MODES. 1 . FREQUENCE ;
DEP1 = RESUL. MODES. 1 . DEFORMEE_MODALE ;
DEP1 = NNOR DEP1 AVEC (MOTS UR) ;
DEP3D1 = CREER_3d STEEL DEP1 360. 20 1 ;
*==============================================================*
*============= MODE FOURIER 3  ================================*
*==============================================================*
OPTI MODE FOUR 3 ;
RIG1 = RIGI  MODCOQ  MATCOQ  ;
MAS1 = MASS  MODCOQ  MATCOQ  ;
*==============================================================*
*        CALCULATION OF THE FREQUENCIES                        *
*                 AND                                          *
*        EXTRACTION OF SOME RESULTS                            *
*==============================================================*
RESUL = VIBR PROC (PROG 100.)  (CL1 ET RIG1 )  MAS1 IMPR   ;
FREQ1 = RESUL.MODES. 1 . FREQUENCE ;
DEP1 = RESUL. MODES. 1 . DEFORMEE_MODALE ;
DEP1 = NNOR DEP1 AVEC (MOTS UR) ;
DEP3D3 = CREER_3d STEEL DEP1 360. 20 3 ;

ELIM DEP3D1.MAILLAGE DEP3D3.MAILLAGE 1.E-3 ;
DEP3DT = DEP3D1.DEPLACEMENT + DEP3D3.DEPLACEMENT ;
DEF3DT= DEFO DEP3DT DEP3D1.MAILLAGE 1 ;
TITR 'MAILLAGE 3D MODE FOURIER 1 ET 3 MODE AXIAL 2' ;
SI ( EGA GRAPH  'O') ;
TRAC (0. 12. 30.) CACH  DEF3DT ;
FINSI;
POI1 = 0. 3.5 7. ;
POI2 = DEP3D1.MAILLAGE POIN PROC POI1 ;
DEPPOI2 = REDU DEP3DT POI2 ;
UXPOI2 = EXCO UX DEPPOI2 ;
POI22 = EXTR UXPOI2 MAIL ;
PP1 = POI22 POIN PROC (0 0 0) ;
UXPOI22 = EXTR UXPOI2 SCAL PP1 ;
ERR1=100*(ABS(0.14 - UXPOI22)/0.14);
SI (ERR1 < 5 );
ERRE 0;
SINON ;
ERRE 5;
FINSI;
FIN ;



