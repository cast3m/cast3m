* fichier :  j2_bcn.dgibi
************************************************************************
* Section : Mecanique Plastique
* Section : Mecanique Plastique
************************************************************************
* PERFORATED STRIP UNDER TRACTION
* TEST: J2 perfect plasticity model
*
*
*-------------  OPCIONES GENERALES  --------------------------------
*
GRAPH = 'N' ;
OPTION DIME 2 TRAC x ELEM qua8 MODE plan defo;
* ajout de option epsilon lineaire pour la precision des test!
 OPTION epsilon lineaire;
*
*-------------  CREACION DE LA GEOMETRIA  --------------------------
*
p1 =  5.  0.;
p2 = 10.  0.;
p3 = 10. 18.;
p4 =  0. 18.;
p5 =  0.  5.;
pce = 0.  0.;
aux = 0.7071067 * 5.;
p6 =  aux aux;
den = 12;
l12 = d den p1 p2;
l23 = d den p2 p3;
l36 = d den p3 p6;
l61 = cerc den p6 pce p1;
l34 = d den p3 p4;
l45 = d den p4 p5;
l56 = cerc den p5 pce p6;
l63 = d den p6 p3;
mall1 = daller l12 l23 l36 l61;
mall2 = daller l34 l45 l56 l63;
malla = mall1 et mall2;
elim 1.E-3 malla;
*
*-------------  MODELO  -------------------------------------------
*
 NUHOR  = 0.2;
 ROHOR  = 2.5e3;
 E_ELAS = 7.e7;
 E_PLAS = 0.e0;
 K_PLAS = (E_PLAS*E_ELAS)/(E_ELAS-E_PLAS);
 SININI = 2.43e5;
 SINFIN = 0.;
 VELOCI = 0.;
 mod1   = model malla mecanique elastique plastique j2;
 mat1   = mater mod1 youn E_ELAS  nu   NUHOR    rho  ROHOR
                     sig0 SININI  sigi SINFIN   kiso K_PLAS
                     velo VELOCI;
* mod1   = model malla mecanique elastique plastique parfait;
* mat1   = mater mod1 youn E_ELAS  nu   NUHOR
*                     sigy SININI  ;
*
*--------------  MATRICES DE RIGIDEZ  -----------------------------
*
 rig1 = rigi mod1 mat1;
*
*--------------  CONDICIONES DE CONTORNO  -------------------------
*
 rigcont1 = bloq uy l12;
 rigcont2 = bloq ux l45;
 rigcont  = rigcont1 et rigcont2;
 rig2     = rig1 et rigcont;
*
*--------------  MOVIMIENTO IMPUESTO  -----------------------------
*
 rigsupe = bloq uy l34;
 valor   = .1;
 movyy   = depi rigsupe valor;
 evol1   = evol manu t (prog 0. 1.) level (prog 0. 1.);
 evfut   = char dimp evol1 movyy;
*
*---------------  RESOLUCION  -------------------------------------
*
 maxiter = 15;
 t0 = table;
 t0.MODELE              = mod1;
 t0.CARACTERISTIQUES    = mat1;
 t0.BLOCAGES_MECANIQUES = rigcont et rigsupe;
 t0.CHARGEMENT          = evfut;
 t0.ACCELERATION        = maxiter;
 t0.MAXITERATION        = maxiter;
 t0.PRECISION           = 1.E-10;
 t0.CONVERGENCE_FORCEE  = faux;
 t0.K_TANGENT           = vrai;
 t0.TEMPS_CALCULES      = prog 0. pas 0.1  1.;
 t0.TEMPS_SAUVES        = t0.TEMPS_CALCULES;
 pasapas t0;
*
*---------------  POSTPROCESO  ------------------------------------
*
 imax   = 10;
 fuer   = prog 0.;
 i      = 1;
 repeter blocdefi imax;
   aux  = reac rigsupe t0.deplacements.i;
   aux  = resu aux;
   pbas = extr aux MAIL ;
   pbas = POIN 1 pbas ;
   aux  = extr aux fy pbas;
   fuer = fuer et (prog aux);
   i=i+1;
 fin blocdefi;
 law = evol manu t (prog 0. pas 0.02 0.2) f(t) fuer;
 SI (NEG GRAPH 'N') ;
   dessin law;
  @cartoon t0 malla 1.;
 FINSI ;
 err = (aux - 1.40382E+06)/1.40382E+06 ;
 list err;
 SI ((ABS err) < 0.5e-5) ;
   ERRE 0 ;
 SINON ;
   ERRE 5 ;
 FINSI ;
 FIN;








