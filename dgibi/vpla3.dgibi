* fichier :  vpla3.dgibi
************************************************************************
* Section : Mecanique Viscoplastique
************************************************************************
*           Test vpla3.dgibi: jeux de données         *
*           ---------------------------------         *
*                                                     *

*******************************************************
*           Test vpla3.dgibi: jeux de données         *
*           ---------------------------------         *
*******************************************************

*  POUR CALCUL COMPLET METTRE COMPLET À : VRAI;

COMPLET = faux;

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 1   ;
FINSI ;

SAUT PAGE;
*-----------------------------------------------------------------------
*                           TEST VPLA3
*
* SORTIE DU DOMAINE ELASTIQUE ET PHASE VISCO-PLASTIQUE
* COMPORTEMENT VISCO-PLASTIQUE - MODELE GUIONNET - CAS AXISYMETRIQUE
* CYLINDRE CREUX SOUMIS A PRESSION INTERNE CONSTANTE ET
*                         DEFORMATION AXIALE IMPOSEE CYCLIQUE VARIANT DE
*                         0.35% A -0.35%
* COMPARAISON DES BOUCLES SIGMA AXIALE FONCTION DE DEFORMATION AXIALE
* AVEC RESULTATS EXPERIMENTAUX (RAPPORT DMT/93-434)
* COMPARAISON DE LA DEFORMATION CIRCONFERENTIELLE PROGRESSIVE EN
* FONCTION DU NOMBRE DE CYCLES AVEC RESULTATS EXPERIMENTAUX
*-----------------------------------------------------------------------
OPTI DIME 2 ELEM QUA8 MODE AXISYMETRIQUE ;

* -----DEFINITION DE LA GEOMETRIE
RAY  = 8. ;
EP   = 2. ;
LONG = EP ;

P0 =  RAY      0.     ;
P1 =  RAY      LONG   ;
P2 = (RAY+EP)  LONG   ;
P3 = (RAY+EP)  0.     ;
*
S01 = P0 DROIT 1 P1   ;
S12 = P1 DROIT 1 P2   ;
S23 = P2 DROIT 1 P3   ;
S30 = P3 DROIT 1 P0   ;
POUT= DALL S01 S12 S23 S30 'PLAN' ;


SI (NEG GRAPH 'N') ;
  TRAC 'QUAL' POUT ;
FINSI ;

* -----DEFINITION DES CONDITIONS AUX LIMITES-----
* -----     ET DES DEPLACEMENTS IMPOSES     -----
CL1 = BLOQ UZ S12 ;
CL2 = BLOQ UZ S30 ;
CL  = CL1 ET CL2 ;

DELTAL=LONG * 0.35E-2;
FL1 = DEPI CL1 DELTAL ;

MO = MODE POUT 'MECANIQUE' 'ELASTIQUE' 'VISCOPLASTIQUE' 'GUIONNET' ;
MA = MATE MO   'YOUN' 192500.  'NU  ' 0.3     'N   ' 24     'K   ' 200.
               'CK  ' 0.       'R0  ' 25.     'CD  ' 0.     'M   ' 0.8
               'A   ' 24000.   'A1  ' 3.3E5   'C   ' 180.   'C1  ' 40.
               'G   ' 0.       'R   ' 0.      'NN  ' 0.005  'C2  ' 40.
               'Q   ' 1.466    'G1  ' 0.      'R1  ' 0.     'BETA' 1.1
               'C0  ' 180.     'P1M0' 0.00333 ;

* -----CHARGEMENT EN TRACTION/COMPRESSION

*      MONTEE EN CHARGE ET 1ER CYCLE
L1 = PROG 0. 1.  3. 5.   ;
L2 = PROG 0. 1. -1. 1.   ;

*        AU TOTAL 10 CYCLES
SI   COMPLET;
  REPETER STRU1 9;
    NL1 = DIME L1 ;
    X   = EXTR L1 NL1 ;
    L1  = L1 ET (PROG (X+2.) (X+4.)) ;
    L2  = L2 ET (PROG -1 1)          ;
  FIN STRU1        ;
FINSI;

EV = EVOL MANU 'T' L1 'FT' L2 ;
CHA1 = CHAR 'DIMP' FL1 EV ;

* -----CHARGEMENT EN PRESSION
MOP = 'MODE' S01 'CHARGEMENT' 'PRESSION' ;
MOTOT = MO 'ET' MOP ;
*MATOT = MA ET MAP ;
MATOT = MA ;
*
EVPRE = 'EVOL' 'MANU' 'TEMP' ('PROG' 0. 1. 100000.)
                      'PRES' ('PROG' 0. 1. 1.) ;
VALP = pres MOP 'PRES' -21.5 ;
CHA2 = 'CHAR' 'PRES' VALP EVPRE ;

* -----MISE EN CHARGE
LIS0 = PROG .2 .3 .4 PAS .05 .6 PAS .02 .8 PAS .01 1 ;
SI COMPLET;

* -----PREMIER CYCLE
  LIS1 = PROG 1.01 1.02 PAS 0.02 1.1 PAS 0.05 2.9 PAS 0.02 2.98 2.99 3.
              3.01 3.02 PAS 0.02 3.1 PAS 0.05 4.9 PAS 0.02 4.98 4.99 5.;

* -----CYCLE 2 A 10
  NLIS   = DIME LIS1    ;
  LISCTE4=PROG NLIS * 4.;
  LIS2 = LIS1 + LISCTE4 ;
  LIS3 = LIS2 + LISCTE4 ;
  LIS4 = LIS3 + LISCTE4 ;
  LIS5 = LIS4 + LISCTE4 ;
  LIS6 = LIS5 + LISCTE4 ;
  LIS7 = LIS6 + LISCTE4 ;
  LIS8 = LIS7 + LISCTE4 ;
  LIS9 = LIS8 + LISCTE4 ;
  LIS10= LIS9 + LISCTE4 ;

  LIS = LIS0 ET LIS1 ET LIS2 ET LIS3 ET LIS4 ET LIS5 ET LIS6 ET LIS7
             ET LIS8 ET LIS9 ET LIS10;
SINON;
  LIS = LIS0;
FINSI;

TAB = TABL ;
TAB.'PRECISION'           = 1E-3;
TAB.'BLOCAGES_MECANIQUES' = CL  ;
TAB.'CARACTERISTIQUES'    = MATOT ;
TAB.'CHARGEMENT'          = CHA1 'ET' CHA2 ;
TAB.'TEMPS_CALCULES'      = LIS ;
TAB.'MODELE'              = MOTOT  ;
PASAPAS TAB ;

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
* COMPARAISON DES RESULTATS AVEC RESULTATS EXPERIMENTAUX
*                RAPPORT DMT/93-434
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
J = 0;
PROGSIG = PROG;
PROGEPS = PROG;
SI COMPLET;
  LECTI = LECT 37 85 133 181 229 277 325 469 517 949 997;
SINON;
  LECTI = LECT 37;
FINSI;

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
* CONTRAINTE AXIALE EN FIN DE MONTEE EN CHARGE
*                   EN MILIEU ET FIN DES CYCLES 1 2 ET 3
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
SI COMPLET;
  NFOIS = (DIME LECTI) - 4 ;
SINON;
  NFOIS = 1;
FINSI;
REPETER STRU1 NFOIS;
  J = J + 1;
  I = EXTR LECTI J;
  SIG = TAB.CONTRAINTES.I;
  SIG11 = EXTR SIG SMZZ 1 1 9;
  PROGSIG = PROGSIG ET (PROG SIG11);
FIN STRU1;

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
* DEFORMATION CIRCONFERENTIELLE MOYENNE (EN %) POUR CYCLES 5 ET 10
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
SI COMPLET;
  REPETER STRU2 2;
    J = J + 1;
    I1 = EXTR LECTI J    ;
    I2 = EXTR LECTI (J+1);
    EPS_1   = EPSI MOTOT (TAB.DEPLACEMENTS.I1);
    EPS_2   = EPSI MOTOT (TAB.DEPLACEMENTS.I2);
    EPS22_1 = ( (EXTR EPS_1 EPTT 1 1 9) + (EXTR EPS_1 EPTT 1 1 6) +
                (EXTR EPS_1 EPTT 1 1 3) ) / 3.;
    EPS22_2 = ( (EXTR EPS_2 EPTT 1 1 9) + (EXTR EPS_2 EPTT 1 1 6) +
                (EXTR EPS_2 EPTT 1 1 3) ) / 3.;
    EPS22 = (EPS22_1 + EPS22_2) / 2. ;
    PROGEPS = PROGEPS ET (PROG EPS22);
    J = J + 1;
  FIN STRU2;

  PROGEPS = 100. * PROGEPS;
FINSI;

*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
*              RESULTATS EXPERIMENTAUX
*                RAPPORT DMT/93-434
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++
SI COMPLET;
  PROGSX = PROG 300 -300 330.3 -309.8 345.9 -312.8 351.9 ;
  PROGEX = PROG 0.22 0.2675 ;
SINON;
  PROGSX = PROG 300 ;
FINSI;

*++++++++++++++
*   COMPARAISON
*++++++++++++++
ERR_SIG = ABS ( (PROGSIG - PROGSX) / PROGSX ) ;
ERR_SIG = MAXI ERR_SIG                        ;
MESS 'Erreur sur les Contraintes : ' ERR_SIG  ;

SI COMPLET ;
  ERRADM = 0.18 ;

  ERR_EPS = ABS ( (PROGEPS - PROGEX) / PROGEX ) ;
  ERR_EPS = MAXI ERR_EPS                        ;
  MESS 'Erreur sur les Deformations : ' ERR_EPS ;
  SI (ERR_EPS <EG 0.18);
    ERRE 0;
  SINON;
    ERRE 5;
  FINSI;
SINON;

  ERRADM=0.03;
  SI (ERR_SIG <EG ERRADM);
    ERRE 0;
  SINON;
    ERRE 5;
  FINSI;
FINSI;

FIN;












