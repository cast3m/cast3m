* fichier :  gonfl2Dex.dgibi
*****************************************************************
** Jeu de données MISTRA pour le maillage 2D de l enceinte
**
** Il a été choisi de mailler tous les volumes à charge
** ensuite à l utilisateur de ne retenir que ce qui
** l intéresse pour le calcul
**
*
* Essai de gonflement de l"enceinte en AIR isotherme
*
** Auteur E. Studer
** Date : 13/03/02
*****************************************************************
COMPLET = FAUX ;
GRAPH = FAUX ;

'SI' (COMPLET) ;
     NPTS = 897 ;
'SINON' ;
     NPTS = 10 ;
'FINSI' ;

'OPTI' 'DIME' 2 ;
'OPTI' 'TRAC' 'PSC' ;
'OPTI' 'MODE' 'AXIS' ;
'OPTI' 'ELEM' 'QUA4' ;

* on modifie CALCP pour avoir un Cp qui ne depend pas de la
* temperature pour valider la montée en pression et en temperature

DEBPROC CALCP ;
'ARGU' T/'CHPOINT'
       TF/'FLOTTANT'
       TL/'LISTREEL' ;
*-------------------------------------------------*
* calcul de la capacite calorifique a pression    *
* constante pour les incondensables en fonction   *
* de la temperature  (J/kg/K)                     *
*                                                 *
* Entree T en Celsius                             *
* Sortie Cp en J/kg/K                             *
*                                                 *
* PS : le resultat est du meme type que l'entree  *
*-------------------------------------------------*
A0 = 14.346 ;
A1 = 8.16D-4 ;
A2 = 4.35D-7 ;
B0 = 5.193 ;
B1 = 0.0 ;
B2 = 0.0 ;
C0 = 0.917 ;
C1 = 1.404D-4 ;
C2 = 3.0D-8 ;
D0 = 1.0309 ;
D1 = 2.044D-4 ;
D2 = -3.3D-8 ;
E0 = 0.836  ;
E1 = 7.432D-4 ;
E2 = -2.511D-7 ;
F0 = 1.0302 ;
F1 = 2.396D-4 ;
F2 = -4.950D-8 ;

'SI' ( 'EXIST' T ) ;
    Cph2  = ( ((A2 * T) * T ) + ( T * A1 ) + A0 ) * 1.D3        ;
    Cphe  = ( ((B2 * T) * T ) + ( T * B1 ) + B0 ) * 1.D3        ;
    Cpo2  = ( ((C2 * T) * T ) + ( T * C1 ) + C0 ) * 1.D3        ;
    Cpn2  = ( ((D2 * T) * T ) + ( T * D1 ) + D0 ) * 1.D3        ;
    Cpco2 = ( ((E2 * T) * T ) + ( T * E1 ) + E0 ) * 1.D3        ;
    Cpco  = ( ((F2 * T) * T ) + ( T * F1 ) + F0 ) * 1.D3        ;
*    Cpair = (0.8*Cpn2) + (0.2*Cpo2) ;
    Cpair = 1000.0 ;
    'RESPROC' Cph2 Cphe Cpo2 Cpn2 Cpco2 Cpco Cpair ;
    'QUITTER' CALCP ;
'FINSI' ;
'SI' ( 'EXIST' TF) ;
    Cph2  = ( ((A2 * TF) * TF ) + ( TF * A1 ) + A0 ) * 1.D3        ;
    Cphe  = ( ((B2 * TF) * TF ) + ( TF * B1 ) + B0 ) * 1.D3        ;
    Cpo2  = ( ((C2 * TF) * TF ) + ( TF * C1 ) + C0 ) * 1.D3        ;
    Cpn2  = ( ((D2 * TF) * TF ) + ( TF * D1 ) + D0 ) * 1.D3        ;
    Cpco2 = ( ((E2 * TF) * TF ) + ( TF * E1 ) + E0 ) * 1.D3        ;
    Cpco  = ( ((F2 * TF) * TF ) + ( TF * F1 ) + F0 ) * 1.D3        ;
*    Cpair = (0.8*Cpn2) + (0.2*Cpo2) ;
    Cpair = (0.0*Cpn2) + 1000.0 ;
    'RESPROC' Cph2 Cphe Cpo2 Cpn2 Cpco2 Cpco Cpair ;
    'QUITTER' CALCP ;
'FINSI' ;
'SI' ( 'EXIST' TL) ;
    T00 = TL / TL ;
    A00 = A0 * T00 ;
    A10 = A1 * T00 ;
    A20 = A2 * T00 ;
    B00 = B0 * T00 ;
    B10 = B1 * T00 ;
    B20 = B2 * T00 ;
    C00 = C0 * T00 ;
    C10 = C1 * T00 ;
    C20 = C2 * T00 ;
    D00 = D0 * T00 ;
    D10 = D1 * T00 ;
    D20 = D2 * T00 ;
    E00 = E0 * T00 ;
    E10 = E1 * T00 ;
    E20 = E2 * T00 ;
    F00 = F0 * T00 ;
    F10 = F1 * T00 ;
    F20 = F2 * T00 ;

    Cph2  = ( ((A20* TL) * TL ) + ( TL * A10) + A00) * 1.D3        ;
    Cphe  = ( ((B20* TL) * TL ) + ( TL * B10) + B00) * 1.D3        ;
    Cpo2  = ( ((C20* TL) * TL ) + ( TL * C10) + C00) * 1.D3        ;
    Cpn2  = ( ((D20* TL) * TL ) + ( TL * D10) + D00) * 1.D3        ;
    Cpco2 = ( ((E20* TL) * TL ) + ( TL * E10) + E00) * 1.D3        ;
    Cpco  = ( ((F20* TL) * TL ) + ( TL * F10) + F00) * 1.D3        ;
*    Cpair = (0.8*Cpn2) + (0.2*Cpo2) ;
    Cpair = (0.0*Cpn2) + 1000.0 ;
    'RESPROC' Cph2 Cphe Cpo2 Cpn2 Cpco2 Cpco Cpair ;
    'QUITTER' CALCP ;

'FINSI' ;
'ERRE' 5 ;
FINPROC                ;

CTRV = FAUX ;

DMIN = 0.05 ;
DMIN1 = 0.02 ;
DMAX = 0.2 ;

* Dimensions sur plan
R0 = 2.125 ;
R0S2 = R0 '/' 2.0 ;

EPB = 0.025 ;
EPF = 0.015 ;
EPH = 0.119 ;
EPSI = 1.E-5 ;

HCOND1 = 2.187 ;
HCOND2 = 1.784 ;
HCOND3 = 1.784 ;

REQC = 1.9079 ;
EPC = 0.040 ;
EPCC = 0.120 ;
HCC = 0.150 ;
HEC = 0.120 ;


EPBR = 0.110 ;
HBR1 = 0.190 ;
HBR2 = 0.195 ;

RINJ = 0.1 ;
RS = 3.2529 ;

ZCOND1 = 1.285 ;
ZCOND2 = 3.592 ;
ZCOND3 = 5.4965 ;

ZHB1 = 1.938 ;
ZHB2 = 4.341 ;
ZHB3 = 6.559 ;

* Entiers pour le nombre de mailles
NINJ = 2 ;
NX1 = 6 ;
NX2 = 2 ;
NX3 = 3 ;
NX4 = 3 ;

NF5 = 2 ;
NM1 = 2 ;
NM2 = 4 ;
NZ1 = 2 ;
NZ2 = 6 ;
NZ3 = 2 ;
*NZ4 = 5 ;
*NZ5 = 2 ;
NZ4 = 12 ;
NZ5 = 4 ;

* Injection
A = 0.0 ZCOND1 ;
B = RINJ ZCOND1 ;
AB = A 'DROI' NINJ B ;
AB = AB 'COUL' 'BLAN' ;

* jusqu"a R/2
C = R0S2 ZCOND1 ;
BC = B 'DROI' C 'DINI' DMIN 'DFIN' DMAX ;
BC = BC 'COUL' 'BLAN' ;

* jusqu"au condenseurs
D = REQC ZCOND1 ;
CD = C 'DROI' D 'DINI' DMAX 'DFIN' DMIN1 ;
CD = CD 'COUL' 'BLAN' ;

* condenseurs plus isolant
E = (REQC '+' EPC) ZCOND1 ;
DE = D 'DROI' NX2 E ;
DE = DE 'COUL' 'BLEU' ;

* collecteurs des condenseurs
F = (REQC '+' EPC '+' EPCC) ZCOND1 ;
EF = E 'DROI' NX3 F ;
EF = EF 'COUL' 'JAUN' ;

* volume mort derriere les condenseurs
G = R0 ZCOND1 ;
FG = F 'DROI' NX4 G ;
FG = FG 'COUL' 'BLAN' ;

* fut de l"enceinte
H = (R0 '+' EPF) ZCOND1 ;
GH = G 'DROI' NM1 H ;
GH = GH 'COUL' 'ROUG' ;

* Brides
I = (R0 '+' EPF '+' EPBR) ZCOND1 ;
HI = H 'DROI' NM2 I ;
HI = HI 'COUL' 'VERT' ;

* Passage au maillage 2D
BASE = BC 'ET' CD 'ET' (DE 'COUL' 'BLAN') 'ET'
       (EF 'COUL' 'BLAN') 'ET' FG 'ET' GH 'ET' HI ;
BASE_1 = BASE 'PLUS' (0. (HBR1 '*' -1.0)) ;
VB_1 = 'TRAN' NZ1 BASE (0. (HBR1 '*' -1.0)) ;
'ELIM' EPSI (BASE_1 'ET' VB_1) ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1)
     'TITR' 'Volume VB_1 ' ;
'FINSI' ;

V1 = 0. ZCOND1 ;
BASE_21 = (BASE_1 'ELEM' 'BLAN') ;
BASE_22 = (BASE_1 'ELEM' 'ROUG') ;
BASE_31 = BASE_21 'PROJ' 'CYLI'  (-1.0 '*' V1) 'CERC' (0. RS)
         ( 0. 0. ) ;
BASE_32 = BASE_22 'PROJ' 'CYLI'  (-1.0 '*' V1) 'CERC' (0. RS)
         ( 0. 0. ) ;
BASE_3 = BASE_31 'ET' BASE_32 ;
'ELIM' EPSI BASE_3 ;

VB_2_1 = 'REGLER' BASE_21 BASE_31 NZ2 ;
VB_2_2 = 'REGLER' BASE_22 BASE_32 NZ2 ;
VB_2 = VB_2_1 'ET' VB_2_2 ;
'ELIM' EPSI VB_2 ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2)
     'TITR' 'Volume VB_1,VB_2 ' ;
'FINSI' ;

BASE_4 = BASE_3 'HOMO' (0. RS) (RS '+' EPB '/' RS) ;
VB_3 = 'REGLER' BASE_3 BASE_4 NM1 ;
VB_3 = VB_3 'COUL' 'ROUG' ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3)
     'TITR' 'Volume VB_1,VB_2,VB_3 ' ;
'FINSI' ;

* Maintenant on monte en Z
*
* Collecteur
HAUT = AB 'ET' BC 'ET' CD 'ET' DE 'ET' EF 'ET' FG 'ET'
       GH 'ET' HI ;
HAUT_2 = HAUT 'PLUS' (0. HCC) ;
VB_4 = 'TRAN' NZ3 HAUT (0. HCC) ;
'ELIM' EPSI (HAUT_2 'ET' VB_4) ;
H_V = VB_4 'ELEM' 'VERT' ;
VB_4 = 'DIFF' VB_4 H_V ;
SC1_1 = INTER ('CONT' (VB_4 'ELEM' 'BLAN'))
        ('CONT' (VB_4 'ELEM' 'BLEU'));
* il faut construire l"axe
CNT = 'CONT' VB_4 ;
CNT = 'DIFF' CNT HAUT ;
CNT = 'DIFF' CNT HAUT_2 ;
CNT = 'CCON' CNT ;
AXE = CNT.2 ;
'ELIM' EPSI (AXE 'ET' VB_4) ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4)
     'TITR' 'Volume VB_1,VB_2,VB_3,VB_4 ' ;
'FINSI' ;

*Bride et le collecteur devient fluide (BLAN)
HAUT_3 = HAUT_2 'PLUS' (0. HBR2) ;
VB_5 = 'TRAN' NZ1 HAUT_2 (0. HBR2) ;
'ELIM' EPSI (HAUT_3 'ET' VB_5) ;
SC1_2 = INTER ('CONT' (VB_5 'ELEM' 'BLAN'))
        ('CONT' (VB_5 'ELEM' 'BLEU'));
H1_J = VB_5 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_5 = 'DIFF' VB_5 H1_J ;
VB_5 = VB_5 'ET' H1_J ;

CNT = 'CONT' VB_5 ;
CNT = 'DIFF' CNT HAUT_2 ;
CNT = 'DIFF' CNT HAUT_3 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_5) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 )
     'TITR' 'Volume VB_1,VB_2,VB_3,VB_4,VB_5 ' ;
'FINSI' ;

* entre les deux brides on ajoute un plan pour le hublot
Z3 = ( 'MAXI' ('COOR' HAUT_3 2)) ;
HAUT_3M = HAUT_3 'PLUS' (0. (ZHB1 '-' Z3)) ;
HAUT_4 = HAUT_3 'PLUS'
  (0. (HCOND1 '-' HBR2 '-' HBR2 '-' HCC '-' HCC )) ;
VB_6_1 = 'TRAN' NZ5 HAUT_3 (0. (ZHB1 '-' Z3)) ;
VB_6_2 = 'TRAN' NZ4 HAUT_3M
 (0. (HCOND1 '-' HBR2 '-' HBR2 '-' HCC '-' HCC '-' ZHB1 '+' Z3))  ;
VB_6 = VB_6_1 'ET' VB_6_2 ;
'ELIM' EPSI (HAUT_3M 'ET' HAUT_4 'ET' VB_6) ;

SC1_3 = INTER ('CONT' (VB_6 'ELEM' 'BLAN'))
        ('CONT' (VB_6 'ELEM' 'BLEU'));
H2_V = VB_6 'ELEM' 'VERT' ;
VB_6 = 'DIFF' VB_6 H2_V ;
H1_J = VB_6 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_6 = 'DIFF' VB_6 H1_J ;
VB_6 = VB_6 'ET' H1_J ;

CNT = 'CONT' VB_6 ;
CNT = 'DIFF' CNT HAUT_3 ;
CNT = 'DIFF' CNT HAUT_4 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_6) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 )
     'TITR' 'Volume VB_1,VB_2,VB_3,VB_4,VB_5,VB_6 ' ;
'FINSI' ;

* une nouvelle bride
HAUT_5 = HAUT_4 'PLUS' (0. HBR2) ;
VB_7 = 'TRAN' NZ1 HAUT_4 (0. HBR2) ;
'ELIM' EPSI (HAUT_5 'ET' VB_7) ;
SC1_4 = INTER ('CONT' (VB_7 'ELEM' 'BLAN'))
        ('CONT' (VB_7 'ELEM' 'BLEU'));
H1_J = VB_7 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_7 = 'DIFF' VB_7 H1_J ;
VB_7 = VB_7 'ET' H1_J ;
CNT = 'CONT' VB_7 ;
CNT = 'DIFF' CNT HAUT_4 ;
CNT = 'DIFF' CNT HAUT_5 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_7) ;
AXE = AXE 'ET' AXE1 ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 )
     'TITR' 'VB_1,VB_2,VB_3,VB_4,VB_5,VB_6, VB_7 ' ;
'FINSI' ;

* un collecteur haut du condenseur bas
HAUT_6 = HAUT_5 'PLUS' (0. HCC) ;
VB_8 = 'TRAN' NZ1 HAUT_5 (0. HCC) ;
'ELIM' EPSI (HAUT_6 'ET' VB_8) ;
SC1_5 = INTER ('CONT' (VB_8 'ELEM' 'BLAN'))
        ('CONT' (VB_8 'ELEM' 'BLEU'));
H2_V = VB_8 'ELEM' 'VERT' ;
VB_8 = 'DIFF' VB_8 H2_V ;
CNT = 'CONT' VB_8 ;
CNT = 'DIFF' CNT HAUT_5 ;
CNT = 'DIFF' CNT HAUT_6 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_8) ;
AXE = AXE 'ET' AXE1 ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 )
     'TITR' 'VB1,VB2,VB3,VB4,VB5,VB6,VB7,VB8 ' ;
'FINSI' ;

* entre le condenseur 1 et le condenseur 2
HAUT_7 = HAUT_6 'PLUS' (0. HEC) ;
VB_9 = 'TRAN' NZ1 HAUT_6 (0. HEC) ;
'ELIM' EPSI (HAUT_7 'ET' VB_9) ;
H2_V = VB_9 'ELEM' 'VERT' ;
VB_9 = 'DIFF' VB_9 H2_V ;
H1_J = VB_9 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_9 = 'DIFF' VB_9 H1_J ;
VB_9 = VB_9 'ET' H1_J ;
H1_B = VB_9 'ELEM' 'BLEU' ;
H1_B = H1_B 'COUL' 'BLAN' ;
VB_9 = 'DIFF' VB_9 H1_B ;
VB_9 = VB_9 'ET' H1_B ;
CNT = 'CONT' VB_9 ;
CNT = 'DIFF' CNT HAUT_6 ;
CNT = 'DIFF' CNT HAUT_7 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_9) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9 )
     'TITR' 'VB1,VB2,VB3,VB4,VB5,VB6,VB7,VB8,VB9 ' ;
'FINSI' ;

* collecteur bas du condenseur moyen
HAUT_8 = HAUT_7 'PLUS' (0. HCC) ;
VB_10 = 'TRAN' NZ1 HAUT_7 (0. HCC) ;
'ELIM' EPSI (HAUT_8 'ET' VB_10) ;
SC2_1 = INTER ('CONT' (VB_10 'ELEM' 'BLAN'))
        ('CONT' (VB_10 'ELEM' 'BLEU'));
H2_V = VB_10 'ELEM' 'VERT' ;
VB_10 = 'DIFF' VB_10 H2_V ;
CNT = 'CONT' VB_10 ;
CNT = 'DIFF' CNT HAUT_7 ;
CNT = 'DIFF' CNT HAUT_8 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_10) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 )
 'TITR' 'VB1,VB2,VB3,VB4,VB5,VB6,VB7,VB8,VB9,VB10 ' ;
'FINSI' ;

* Bride du condenseur 2
HAUT_9 = HAUT_8 'PLUS' (0. HBR2) ;
VB_11 = 'TRAN' NZ1 HAUT_8 (0. HBR2) ;
'ELIM' EPSI (HAUT_9 'ET' VB_11) ;
SC2_2 = INTER ('CONT' (VB_11 'ELEM' 'BLAN'))
        ('CONT' (VB_11 'ELEM' 'BLEU'));
H1_J = VB_11 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_11 = 'DIFF' VB_11 H1_J ;
VB_11 = VB_11 'ET' H1_J ;
CNT = 'CONT' VB_11 ;
CNT = 'DIFF' CNT HAUT_8 ;
CNT = 'DIFF' CNT HAUT_9 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_11) ;
AXE = AXE 'ET' AXE1 ;

'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 )
 'TITR' 'VB1 A VB11 ' ;
'FINSI' ;

* entre bride et collecteur haut pour condenseur moyen
Z3 = ( 'MAXI' ('COOR' HAUT_9 2)) ;
HAUT_9M = HAUT_9 'PLUS' (0. (ZHB2 '-' Z3)) ;
HAUT_10 = HAUT_9 'PLUS'
  (0. (HCOND2 '-' HBR2 '-' HCC '-' HCC )) ;
VB_12_1 = 'TRAN' NZ5 HAUT_9 (0. (ZHB2 '-' Z3)) ;
VB_12_2 = 'TRAN' (NZ4 '-' NZ5) HAUT_9M
     (0. (HCOND2 '-' HBR2 '-' HCC '-' HCC '-' ZHB2 '+' Z3)) ;
VB_12 = VB_12_1 'ET' VB_12_2 ;
'ELIM' EPSI (HAUT_9M 'ET' HAUT_10 'ET' VB_12) ;
SC2_3 = INTER ('CONT' (VB_12 'ELEM' 'BLAN'))
        ('CONT' (VB_12 'ELEM' 'BLEU'));
H2_V = VB_12 'ELEM' 'VERT' ;
VB_12 = 'DIFF' VB_12 H2_V ;
H1_J = VB_12 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_12 = 'DIFF' VB_12 H1_J ;
VB_12 = VB_12 'ET' H1_J ;
CNT = 'CONT' VB_12 ;
CNT = 'DIFF' CNT HAUT_9 ;
CNT = 'DIFF' CNT HAUT_10 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_12) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 )
 'TITR' 'VB1 A VB12 ' ;
'FINSI' ;

* collecteur haut du condenseur moyen
HAUT_11 = HAUT_10 'PLUS' (0. HCC) ;
VB_13 = 'TRAN' NZ1 HAUT_10 (0. HCC) ;
'ELIM' EPSI (HAUT_11 'ET' VB_13) ;
SC2_4 = INTER ('CONT' (VB_13 'ELEM' 'BLAN'))
        ('CONT' (VB_13 'ELEM' 'BLEU'));
H2_V = VB_13 'ELEM' 'VERT' ;
VB_13 = 'DIFF' VB_13 H2_V ;
CNT = 'CONT' VB_13 ;
CNT = 'DIFF' CNT HAUT_10 ;
CNT = 'DIFF' CNT HAUT_11 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_13) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 )
 'TITR' 'VB1 A VB13 ' ;
'FINSI' ;

* entre le condenseur moyen et le condenseur haut
HAUT_12 = HAUT_11 'PLUS' (0. HEC) ;
VB_14 = 'TRAN' NZ1 HAUT_11 (0. HEC) ;
'ELIM' EPSI (HAUT_12 'ET' VB_14) ;
H2_V = VB_14 'ELEM' 'VERT' ;
VB_14 = 'DIFF' VB_14 H2_V ;
H1_J = VB_14 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_14 = 'DIFF' VB_14 H1_J ;
VB_14 = VB_14 'ET' H1_J ;
H1_B = VB_14 'ELEM' 'BLEU' ;
H1_B = H1_B 'COUL' 'BLAN' ;
VB_14 = 'DIFF' VB_14 H1_B ;
VB_14 = VB_14 'ET' H1_B ;
CNT = 'CONT' VB_14 ;
CNT = 'DIFF' CNT HAUT_11 ;
CNT = 'DIFF' CNT HAUT_12 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_14) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 )
 'TITR' 'VB1 A VB14 ' ;
'FINSI' ;

* collecteur bas du condenseur haut
HAUT_13 = HAUT_12 'PLUS' (0. HCC) ;
VB_15 = 'TRAN' NZ1 HAUT_12 (0. HCC) ;
'ELIM' EPSI (HAUT_13 'ET' VB_15) ;
SC3_1 = INTER ('CONT' (VB_15 'ELEM' 'BLAN'))
        ('CONT' (VB_15 'ELEM' 'BLEU'));
H2_V = VB_15 'ELEM' 'VERT' ;
VB_15 = 'DIFF' VB_15 H2_V ;
CNT = 'CONT' VB_15 ;
CNT = 'DIFF' CNT HAUT_12 ;
CNT = 'DIFF' CNT HAUT_13 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_15) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 )
 'TITR' 'VB1 A VB15 ' ;
'FINSI' ;

* entre collecteur bas et bas de la bride sup
Z3 = ( 'MAXI' ('COOR' HAUT_13 2)) ;
HAUT_13M = HAUT_13 'PLUS' (0. (ZHB3 '-' Z3)) ;
HAUT_14 = HAUT_13 'PLUS'
  (0. (HCOND3 '-' HBR2 '-' HCC '-' HCC )) ;
VB_16_1 = 'TRAN' (NZ4 '-' NZ5) HAUT_13 (0. (ZHB3 '-' Z3)) ;
VB_16_2 = 'TRAN' NZ5 HAUT_13M
   (0. (HCOND3 '-' HBR2 '-' HCC '-' HCC '-' ZHB3 '+' Z3)) ;
VB_16 = VB_16_1 'ET' VB_16_2 ;
'ELIM' EPSI (HAUT_13M 'ET' HAUT_14 'ET' VB_16) ;
SC3_2 = INTER ('CONT' (VB_16 'ELEM' 'BLAN'))
        ('CONT' (VB_16 'ELEM' 'BLEU'));
H2_V = VB_16 'ELEM' 'VERT' ;
VB_16 = 'DIFF' VB_16 H2_V ;
H1_J = VB_16 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_16 = 'DIFF' VB_16 H1_J ;
VB_16 = VB_16 'ET' H1_J ;
CNT = 'CONT' VB_16 ;
CNT = 'DIFF' CNT HAUT_13 ;
CNT = 'DIFF' CNT HAUT_14 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_16) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 )
 'TITR' 'VB1 A VB16 ' ;
'FINSI' ;

* une nouvelle bride
HAUT_15 = HAUT_14 'PLUS' (0. HBR2) ;
VB_17 = 'TRAN' NZ1 HAUT_14 (0. HBR2) ;
'ELIM' EPSI (HAUT_15 'ET' VB_17) ;
SC3_3 = INTER ('CONT' (VB_17 'ELEM' 'BLAN'))
        ('CONT' (VB_17 'ELEM' 'BLEU'));
H1_J = VB_17 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_17 = 'DIFF' VB_17 H1_J ;
VB_17 = VB_17 'ET' H1_J ;
CNT = 'CONT' VB_17 ;
CNT = 'DIFF' CNT HAUT_14 ;
CNT = 'DIFF' CNT HAUT_15 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_17) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 'ET' VB_17 )
 'TITR' 'VB1 A VB17 ' ;
'FINSI' ;

* un collecteur haut du condenseur haut
HAUT_16 = HAUT_15 'PLUS' (0. HCC) ;
VB_18 = 'TRAN' NZ1 HAUT_15 (0. HCC) ;
'ELIM' EPSI (HAUT_16 'ET' VB_18) ;
SC3_4 = INTER ('CONT' (VB_18 'ELEM' 'BLAN'))
        ('CONT' (VB_18 'ELEM' 'BLEU'));
H2_V = VB_18 'ELEM' 'VERT' ;
VB_18 = 'DIFF' VB_18 H2_V ;
CNT = 'CONT' VB_18 ;
CNT = 'DIFF' CNT HAUT_15 ;
CNT = 'DIFF' CNT HAUT_16 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_18) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 'ET' VB_17 'ET' VB_18 )
 'TITR' 'VB1 A VB18 ' ;
'FINSI' ;

* entre le condenseur haut et le plafond
HAUT_17 = HAUT_16 'PLUS' (0. 0.100) ;
VB_19 = 'TRAN' NZ1 HAUT_16 (0. 0.100) ;
'ELIM' EPSI (HAUT_17 'ET' VB_19) ;
H2_V = VB_19 'ELEM' 'VERT' ;
VB_19 = 'DIFF' VB_19 H2_V ;
H1_J = VB_19 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'BLAN' ;
VB_19 = 'DIFF' VB_19 H1_J ;
VB_19 = VB_19 'ET' H1_J ;
H1_B = VB_19 'ELEM' 'BLEU' ;
H1_B = H1_B 'COUL' 'BLAN' ;
VB_19 = 'DIFF' VB_19 H1_B ;
VB_19 = VB_19 'ET' H1_B ;
CNT = 'CONT' VB_19 ;
CNT = 'DIFF' CNT HAUT_16 ;
CNT = 'DIFF' CNT HAUT_17 ;
CNT = 'CCON' CNT ;
AXE1 = CNT.2 ;
'ELIM' EPSI (AXE1 'ET' VB_19) ;
AXE = AXE 'ET' AXE1 ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 'ET' VB_17 'ET' VB_18
  'ET' VB_19 )
 'TITR' 'VB1 A VB19' ;
'FINSI' ;

* le plafond
HAUT_18 = HAUT_17 'PLUS' (0. EPH) ;
VB_20 = 'TRAN' NM1 HAUT_17 (0. EPH) ;
'ELIM' EPSI (HAUT_18 'ET' VB_20) ;
H2_V = VB_20 'ELEM' 'VERT' ;
VB_20 = 'DIFF' VB_20 H2_V ;
H1_J = VB_20 'ELEM' 'JAUN' ;
H1_J = H1_J 'COUL' 'ROUG' ;
VB_20 = 'DIFF' VB_20 H1_J ;
VB_20 = VB_20 'ET' H1_J ;
H1_B = VB_20 'ELEM' 'BLEU' ;
H1_B = H1_B 'COUL' 'ROUG' ;
VB_20 = 'DIFF' VB_20 H1_B ;
VB_20 = VB_20 'ET' H1_B ;
H1_BL = VB_20 'ELEM' 'BLAN' ;
H1_BL = H1_BL 'COUL' 'ROUG' ;
VB_20 = 'DIFF' VB_20 H1_BL ;
VB_20 = VB_20 'ET' H1_BL ;
'SI' GRAPH ;
  'TRACE' 'CACH' (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 'ET' VB_17 'ET' VB_18
  'ET' VB_19 'ET' VB_20 )
 'TITR' 'VB1 A VB20' ;
'FINSI' ;

***************************************************************
* On effectue des controles par rapport aux plans usines
***************************************************************
MISTRA = (VB_1 'ET' VB_2 'ET' VB_3 'ET' VB_4
  'ET' VB_5 'ET' VB_6 'ET' VB_7 'ET' VB_8 'ET' VB_9
  'ET' VB_10 'ET' VB_11 'ET' VB_12 'ET' VB_13 'ET'
  VB_14 'ET' VB_15 'ET' VB_16 'ET' VB_17 'ET' VB_18
  'ET' VB_19 'ET' VB_20 )
;

'SI' CTRV ;

* volume du fluide
VFF = MISTRA 'ELEM' 'BLAN' ;
'TRACE' 'CACH' VFF 'TITR' 'Volume fluide' ;
MVFF = 'CHAN' VFF 'QUAF' ;
$VFF = 'MODE' MVFF 'NAVIER_STOKES' 'QUAF' ;
VVFF =  'DOMA' $VFF 'XXDIAGSI' ;
VFF = 'DOMA' $VFF 'MAILLAGE' ;
'MESS' '==> Volume du fluide : '
  ('SOMT' VVFF)  ;

* Elevation
ZZ = 'COOR' VFF 2 ;
'MESS' '==> Zfluide min =' ('MINI' ZZ) ;
'MESS' '==> Zfluide max =' ('MAXI' ZZ) ;

* Surfaces de condensation
SC1 = SC1_1 'ET' SC1_2 'ET' SC1_3 'ET' SC1_4 'ET'
      SC1_5 ;
SC2 = SC2_1 'ET' SC2_2 'ET' SC2_3 'ET' SC2_4 ;
SC3 = SC3_1 'ET' SC3_2 'ET' SC3_3 'ET' SC3_4 ;
'TRACE' 'CACH' (SC1 'ET' SC2 'ET' SC3)
    'TITR' 'Surface des condenseurs' ;
MS1 = 'CHAN' SC1 'QUAF' ;
$S1 = 'MODE' MS1 'NAVIER_STOKES' 'QUAF' ;
VS1 = 'DOMA' $S1 'XXDIAGSI' ;
MS2 = 'CHAN' SC2 'QUAF' ;
$S2 = 'MODE' MS2 'NAVIER_STOKES' 'QUAF' ;
VS2 = 'DOMA' $S2 'XXDIAGSI' ;
MS3 = 'CHAN' SC3 'QUAF' ;
$S3 = 'MODE' MS3 'NAVIER_STOKES' 'QUAF' ;
VS3 = 'DOMA' $S3 'XXDIAGSI' ;
'MESS' '==> Surface condenseur BAS : '
  ('SOMT' VS1) ;
'MESS' '==> Surface condenseur MOY : '
  ('SOMT' VS2) ;
'MESS' '==> Surface condenseur HAU : '
  ('SOMT' VS3) ;

* Volume des condenseurs
CS = (MISTRA 'ELEM' 'BLEU') 'ET' (MISTRA 'ELEM' 'JAUN') ;
'TRACE' 'CACH' CS 'TITR' 'Volume des condenseurs' ;
MCS = 'CHAN' CS 'QUAF' ;
$CS = 'MODE' MCS 'NAVIER_STOKES' 'QUAF' ;
VCS = 'DOMA' $CS 'XXDIAGSI' ;
'MESS' '==> Volume des condenseurs : '
  ('SOMT' VCS);

* Volume des brides
BRIDE = MISTRA 'ELEM' 'VERT' ;
'TRACE' 'CACH' BRIDE 'TITR' 'Volume des brides' ;
MBR = 'CHAN' BRIDE 'QUAF' ;
$BR = 'MODE' MBR 'NAVIER_STOKES' 'QUAF' ;
VBR = 'DOMA' $BR 'XXDIAGSI' ;
'MESS' '==> Masse des Brides : '
  (('SOMT' VBR) '*' 7800.0) ;

* Volume acier enceinte
ACI = MISTRA 'ELEM' 'ROUG' ;
'TRACE' 'CACH' ACI 'TITR' 'Volume Acier Enceinte' ;
MAC = 'CHAN' ACI 'QUAF' ;
$AC = 'MODE' MAC 'NAVIER_STOKES' 'QUAF' ;
VAC = 'DOMA' $AC 'XXDIAGSI' ;
'MESS' '==> Masse des Aciers : '
  (('SOMT' VAC) '*' 7800.0) ;
'FINSI' ;

*************************************************************
*      On s"occupe maintenant du maillage 2D Complet
*
*************************************************************
SC1 = SC1_1 'ET' SC1_2 'ET' SC1_3 'ET' SC1_4 'ET'
      SC1_5 ;
SC2 = SC2_1 'ET' SC2_2 'ET' SC2_3 'ET' SC2_4 ;
SC3 = SC3_1 'ET' SC3_2 'ET' SC3_3 'ET' SC3_4 ;
*************************************************************
**                   FIN DU MAILLAGE                       **
*************************************************************
VFF = MISTRA 'ELEM' 'BLAN' ;
PAR = (MISTRA 'ELEM' 'ROUG') 'ET' (MISTRA 'ELEM' 'VERT') ;

* Recherche de la paroi externe

CPAR = 'CONT' PAR ;
CVTF = 'CONT' VFF ;
CINT = 'INTE' CPAR CVTF ;
CEXT = 'DIFF' CPAR CINT ;
NPT = 'NBEL' CEXT ;
B1 = CEXT 'ELEM' ('LECT' 1 'PAS' 1 NM1) ;
B2 = CEXT 'ELEM' ('LECT' (NPT '-' NM1 '+' 1) 'PAS' 1 NPT) ;
CEXT = 'DIFF' CEXT B1 ;
CEXT = 'DIFF' CEXT B2 ;


* Mise en place de la procedure

RXT = 'TABLE' ;
RXT.'vtf' = VFF ;
RXT.'breche' = AB ;
RXT.'axe' = AXE ;
RXT.'diru1' = (0 1) ;
RXT.'pi' = ( VFF 'POIN' 'PROC' (0.0 2.0)) ;
RXT.'IMPR' = 2 ;

*RXT.'THERMP' = VRAI ;
*RXT.'vtp' = PAR ;
*RXT.'LAMBDA' = 17.0 ;
*RXT.'ROCP' = (7800.0 '*' 500.0) ;
*RXT.'Tp0' = 20.0 ;
*RXT.'ECHAN' = 5.0 ;

*RXT.'ECHEXT' = VRAI ;
*RXT.'parext' = CEXT ;
*RXT.'HEXT' = 4.0 ;
*RXT.'TPEXT' = 20.0 ;


*RXT.'TIMP1' = 'TABLE' ;
*RXT.'TIMP1'.'MAILLAGE' = SC1 ;
*RXT.'TIMP1'.'t' = 'PROG' 0.0 25.5 345.5 100000.0 ;
*RXT.'TIMP1'.'TIMP' = 'PROG' 135.0 135.0 120.0 120.0 ;
*RXT.'TIMP1'.'ECHAN' = 30.0 ;
*RXT.'TIMP2' = 'TABLE' ;
*RXT.'TIMP2'.'MAILLAGE' = SC2 ;
*RXT.'TIMP2'.'t' = 'PROG' 0.0 25.5 345.5 100000.0 ;
*RXT.'TIMP2'.'TIMP' = 'PROG' 135.0 135.0 120.0 120.0 ;
*RXT.'TIMP2'.'ECHAN' = 30.0 ;
*RXT.'TIMP3' = 'TABLE' ;
*RXT.'TIMP3'.'MAILLAGE' = SC3 ;
*RXT.'TIMP3'.'t' = 'PROG' 0.0 25.5 345.5 100000.0 ;
*RXT.'TIMP3'.'TIMP' = 'PROG' 135.0 135.0 120.0 120.0;
*RXT.'TIMP3'.'ECHAN' = 30.0 ;

RXT.'VAPEUR' = FAUX ;
RXT.'TF0' = 20.0 ;
RXT.'PT0' = 1.E5 ;
* RXT.'Yvap0' = 0.5 ;

*RXT.'MODTURB' = 'NUTURB' ;
*RXT.'NUT' = 5.E-3 ;
RXT.'MODTURB' = 'LMEL' ;
RXT.'LMEL' = 0.08 ;

RXT.'epsi' = 1.E-5 ;
RXT.'DISCR' = 'LINE' ;
RXT.'KPRE' = 'MSOMMET' ;
RXT.'GRAPH' = GRAPH ;

RXT.'scenario' = 'TABLE' ;
RXT.'scenario'.'t' = 'PROG' 0.0 10000.0 ;
RXT.'scenario'.'qair' = 'PROG' 0.1302 0.1302 ;
* RXT.'scenario'.'qeau' = 'PROG' 0.150 0.150 0.0 0.0 0.12 0.12 ;
RXT.'scenario'.'tinj' = 'PROG' 20.0 20.0 ;

*-- Definition des capteurs de mesures dans l"enceinte
R00 = 0. 1.575 ;
R01 = 0. 2.025 ;
R02 = 0. 2.625 ;
R03 = 0. 3.625 ;
R04 = 0. 4.625 ;
R05 = 0. 5.625 ;
R06 = 0. 6.425 ;
R07 = 0. 7.125 ;
R10 = 0.475 1.575 ;
R11 = 0.475 2.025 ;
R12 = 0.475 2.625 ;
R13 = 0.475 3.625 ;
R14 = 0.475 4.625 ;
R15 = 0.475 5.625 ;
R16 = 0.475 6.425 ;
R17 = 0.475 7.125 ;
R20 = 0.950 1.575 ;
R21 = 0.950 2.025 ;
R22 = 0.950 2.625 ;
R23 = 0.950 3.625 ;
R24 = 0.950 4.625 ;
R25 = 0.950 5.625 ;
R26 = 0.950 6.425 ;
R27 = 0.950 7.125 ;
R28 = 0.950 0.600 ;
R29 = 0.950 1.100 ;
R30 = 1.425 1.575 ;
R31 = 1.425 2.025 ;
R32 = 1.425 2.625 ;
R33 = 1.425 3.625 ;
R34 = 1.425 4.625 ;
R35 = 1.425 5.625 ;
R36 = 1.425 6.425 ;
R37 = 1.425 7.125 ;
R40 = 1.814 1.575 ;
R41 = 1.814 2.025 ;
R42 = 1.814 2.625 ;
R43 = 1.814 3.625 ;
R44 = 1.814 4.625 ;
R45 = 1.814 5.625 ;
R46 = 1.814 6.425 ;
R47 = 1.814 7.125 ;
R51 = 2.000 2.625 ;
R52 = 2.000 4.625 ;
R53 = 2.000 6.425 ;

PERSO=TABLE;
PERSO.CAPT0 = VRAI ;
PERSO.TITRE = 'Tableperso' ;
PERSO.TEMPS = 'PROG'         ;
PERSO.FREQ0 = 10 ;
PERSO.LPOINTS= R00 et R01 et R02 et R03 et R04 et R05
et R06 et R07 et R10 et R11 et R12 et R13 et R14 et
R15 et R16 et R17 et R20 et R21 et R22 et R23 et R24
et R25 et R26 et R27 et R28 et R29 et R30 et R31 et R32
et R33 et R34 et R35 et R36 et R37 et R40 et R41 et
R42 et R43 et R44 et R45 et R46 et R47 et R51 et R52
et R53 ;
PERSO.NPOINTS=45 ;
PERSO.CAPT='MOTS' C00 C01 C02 C03 C04 C05 C06 C07
C10 C11 C12 C13 C14 C15 C16 C17 C20 C21 C22 C23 C24
C25 C26 C27 C28 C29
C30 C31 C32 C33 C34 C35 C36 C37 C40 C41
C42 C43 C44 C45 C46 C47 C51 C52 C53 ;
PERSO.COMP= MOTS 'TF' ;
* Trace de champs 3D
PERSO.CH3D0  = FAUX ;
PERSO.COMP3D = MOTS 'TF' ;
PERSO.TPS3D  = PROG  ;
PERSO.FREQ3D = 4  ;

*rxt.'PERSO'   = VRAI              ;
*rxt.'PRCPERSO'= 'VISUP'           ;
*rxt.'TABPERSO'= PERSO             ;

*************************************
**          EXECUTION              **
*************************************
RXT.'DT0' = 0.05 ;
NSAV = 0 ;
NPAS = 10 ;
EXECRXT NPAS RXT ;
NSAV = NSAV '+' NPAS ;

RXT.'DT0' = 0.5 ;
NPAS = 11 ;
EXECRXT NPAS RXT ;
NSAV = NSAV '+' NPAS ;

RXT.'DT0' = 2.0 ;
NPAS = NPTS ;
EXECRXT NPAS RXT ;
NSAV = NSAV '+' NPAS ;

*'OPTI' 'SAUVE' 'gonflairiso.save'  ;
*'SAUVE' ;

Si (COMPLET) ;

NPTS = 'DIME' RXT.'TIC'.'LTPS' ;
PT = 'EXTR' RXT.'TIC'.'PT' NPTS ;
TF = 'EXTR' RXT.'TIC'.'Tfm' NPTS ;
PSOL = 3.90785E+05 ;
TSOL = 99.64 ;
erp=abs( PT - PSOL )/PSOL ;
ert=abs( TF - TSOL )/TSOL;
mess ' Erreur Pression    = ' erp
     ' Erreur Temperature = ' ert ;
Si (erp  > 0.002); erreur 5 ; Finsi ;
Si (ert  > 0.002); erreur 5 ; Finsi ;

Finsi ;


Si ('NON' COMPLET) ;

NPTS = 'DIME' RXT.'TIC'.'LTPS' ;
PT = 'EXTR' RXT.'TIC'.'PT' NPTS ;
TF = 'EXTR' RXT.'TIC'.'Tfm' NPTS ;
PSOL = 1.04200E+05 ;
TSOL =  23.432 ;
erp=abs( PT - PSOL )/PSOL ;
ert=abs( TF - TSOL )/TSOL;
mess ' Erreur Pression    = ' erp
     ' Erreur Temperature = ' ert ;
Si (erp  > 0.002); erreur 5 ; Finsi ;
Si (ert  > 0.002); erreur 5 ; Finsi ;

Finsi ;

'FIN' ;
*




