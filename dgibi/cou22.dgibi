* fichier :  cou22.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 1 ;
SAUT PAGE ;
MESS '                                                       ';
MESS '                                                       ';
MESS '        TEST DE CISAILLEMENT SUR UN JOINT              ';
MESS '                                                       ';
MESS '                                                       ';
MESS '               C_________________D                     ';
MESS '               A                 B                     ';
MESS '                                                       ';
MESS '  JOINT FORME DES NOEUDS CDAB                          ';
MESS '  CONDITIONS AUX LIMITES  :                            ';
MESS '       - AB BLOQUE EN UX ET EN UY                      ';
MESS '  CHARGEMENT              :                            ';
MESS '       - FORCE DE CISAILLEMENT SUIVANT CD              ';
MESS '                                                       ';
MESS '  LE CHARGEMENT EST TEL QUE L ON RESTE DANS LE DOMAINE ';
MESS '  ELASTIQUE                                            ';
MESS '                                                       ';
********************************************************************
*
OPTION DIME 2 ;
OPTION ELEM SEG2 MODE PLAN DEFO ;
*
COJANA = 1.5 ;
DEPANA = 0.015 ;
*
* ----------DEFINITION DE LA GEOMETRIE DU JOINT ----------
*
A = 0.00 0.00 ;
B = 1.00 0.00;
C = 0.00 0.00 ;
D = 1.00 0.00 ;
*
* ----------                  LIGNE                  ----------
*
AB =  A DROIT 1 B ;
CD =  C DROIT 1 D ;
*
* ----------         MAILLAGE                ----------
*
OPTION ELEM RAC2 ;
*
JOINT = RACC 0.001 CD AB ;
*
* ----------     DEFINITION DES CONDITIONS AUX LIMITES   ----------
*
*
*                        JOINT
*
CL1 = BLOQ AB UX ;
CL2 = BLOQ AB UY ;
CLJOINT =  CL1 ET CL2;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MODJOI = MODE JOINT MECANIQUE ELASTIQUE PLASTIQUE COULOMB JOI2 ;
MAJOI  = MATE MODJOI KS   100.0  KN   100.0   EF  150.0   ECN  0.05
                     COHE 2.0    FRIC  45.0   ;
*
* ----------            DEFINITION DU CHARGEMENT       ----------
*
CHPFO = FORCE (1.5 00.00) CD ;
LIST CHPFO;
*
* ----------   DEFINITION DE LA PROGRESSION DU CHARGEMENT ----------
*
   LI1 = PROG 0. 2.0 ; LI2 = PROG 1.0 1.0 ;
   EV = EVOL MANU X LI1 Y LI2 ;
   CAR1 = CHAR 'MECA' CHPFO EV ;
*
* ----------            RESOLUTION            ----------
*
   TAB = TABLE ;
   LIS1 = PROG 0.0 1.0 2.0 ;
   TAB.'BLOCAGES_MECANIQUES' = CLJOINT;
   TAB.'CARACTERISTIQUES' = MAJOI;
   TAB.'MODELE' = MODJOI;
   TAB.'TEMPS_CALCULES' = LIS1;
   TAB.'CHARGEMENT' = CAR1;
TMASAU=table;
tab . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
   PASAPAS TAB ;
*
* ---------- POST TRAITEMENT ----------
*
*
* -----RECUPERATION DE LA TABLE DES DEPLACEMENTS
*
dtab1=tab.temps;
*
ndime= (dime dtab1) - 1;
*
* ----- EXTRACTION DES DIFFERENTES TABLES
*
*                                    DEPLACEMENTS
*
i=0 ;
repeter bloc1 ndime ;
   i=i+1 ;
   dep1=tab.deplacements.i ;
   sig1=tab.contraintes.i ;
   var1=tab.variables_internes.i ;
   def1=tab.deformations_inelastiques.i ;
   eps1 = epsi modjoi dep1 ;
*
* ----- REDUCTION DES CHPOINTS A L'ELEMENT JOINT
*
SIGJOI = REDU SIG1 JOINT ;
DEFJOI = REDU EPS1 JOINT ;
DPLJOI = REDU DEP1 JOINT ;
DEPJOI = REDU DEF1 JOINT ;
VARJOI = REDU VAR1 JOINT ;
*
mess '              '   ;
mess ' PAS DE TEMPS ' i ;
mess ' ____________ '   ;
mess '              '   ;
mess ' CONTRAINTES  '   ;
list sigjoi ;
mess ' DEFO ELASTI  '   ;
list defjoi ;
mess ' DEFO INELASTI'   ;
list depjoi ;
mess ' VARIABLES INTERNES '   ;
list varjoi ;
mess ' DEPLACEMENTS '   ;
list dpljoi ;
*
SIGJOI1 = EXTR SIGJOI SMSN 1 1 1 ;
SIGJOI2 = EXTR SIGJOI SMSN 1 1 2 ;
DEFJOI1 = EXTR DEFJOI DRSN 1 1 1 ;
DEFJOI2 = EXTR DEFJOI DRSN 1 1 2 ;
*
RESI2 = ABS ( ( SIGJOI1 - COJANA)/COJANA ) ;
RESI3 = ABS ( ( SIGJOI2 - COJANA)/COJANA ) ;
RESI4 = ABS ( ( DEFJOI1 - DEPANA)/DEPANA ) ;
RESI5 = ABS ( ( DEFJOI2 - DEPANA)/DEPANA ) ;
*
SI (  (RESI2 <EG 1E-4 ) ET (RESI3 <EG 1E-4 ) ET
      (RESI4 <EG 1E-4 ) ET (RESI5 <EG 1E-4 ) ) ;
   ERRE  0 ;
SINON;
   ERRE  5 ;
FINSI ;
*
MESS '                                                       ' ;
MESS '   SOLUTION THEORIQUE :                                ' ;
MESS '     CONTRAINTES DANS LE JOINT' COJANA                   ;
MESS '     DEFORMATION DANS LE JOINT' DEPANA                   ;
MESS '                                                       ' ;
MESS '   SOLUTION CALCULEE  :                                ' ;
MESS '     CONTRAINTES DANS LE JOINT' SIGJOI1                  ;
MESS '     DEFORMATION DANS LE JOINT' DEFJOI1                  ;
fin bloc1 ;
FIN ;









