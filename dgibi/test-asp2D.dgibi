* fichier :  test-asp2D.dgibi
************************************************************************
* Section : Fluides Thermique
************************************************************************
*
*****************************************************************
**   Jeu de données  - maillage 2D
**   injection de gouttes à 40°C
**   dans une enceinte remplie d'air à 24°C
**   modèle à 7 équations (diphasique-1 phase dispersée)
**
**   Date : L. Blumenfeld 29/09/05
*****************************************************************
'OPTI' 'DIME' 2      ;
'OPTI' 'TRAC' 'X'    ;
'OPTI' 'MODE' 'AXIS' ;
'OPTI' 'ELEM' 'QUA4' ;

*****************************************************************
**                   MAILLAGE                                 ***
*****************************************************************
 GRAPH = VRAI ;
 GRAPH = FAUX ;
 HB = 7.294   ;
 HA = 1.294   ;
 RA = 1.9     ;
 RJ = 0.1     ;
 RM = RA/2.   ;
 HF = (HB+HA)/2. ;
*................................................
*      Nombre d'elements injection et cotés     *
*            en MACRO, N réel = n x 2           *
*...............................................*
*___________________CAS N°1_______________________
 nj  = -4    ;
 nr  = -20   ;
 nm  = nr/2  ;
 nhb = -50   ;
 nha = -10   ;
* nhe = -1   ;
 nf  = nhb/2 ;

*_______________coordonées des points_______________*
 A0 = 0. HA ;
 A1 = RA HA ;
 AM = RM HA ;
 AJ = RJ HA ;
 C0 = 0. 0. ;
 CJ = RJ 0. ;
 C1 = RA 0. ;
 CM = RM 0. ;
 B0 = 0. HB ;
 BJ = RJ HB ;
 B1 = RA HB ;
 BM = RM HB ;
* E0 = 0. HE ;
* EJ = RJ HE ;
* E1 = RA HE ;
* EM = RM HE ;
 F0 = 0. HF ;
 F1 = RA HF ;
 FJ = RJ HF ;
*______________________densité________________________*
dh0  = 1.  ;
*dhj   = 1.  ;
dhm1  = 1.  ;
dhm2  = 1.  ;
dh1   = 1.  ;
*dh1=0.2    ;
dvc   = 1.  ;
dvb   = 1.  ;
dva1  = 1.  ;
dva2  = 1.  ;
dve1  = 1.  ;
dve2  = 1. ;
df1   = 1.  ;
df2   = 1.  ;
dhj   = 1.  ;
* ______________Définition des segments_______________*

  CJC0 = 'DROIT' nj  BJ B0 'DINI' dhj   'DFIN' dh0  ;
  CJCM = 'DROIT' nr  CJ CM 'DINI' dhj  'DFIN' dhm1 ;
  CMC1 = 'DROIT' nm  CM C1 'DINI' dhm2 'DFIN' dh1  ;
  C1A1 = 'DROIT' nha C1 A1 'DINI' dvc  'DFIN' dva1 ;
  A1AM = 'DROIT' nm  A1 AM 'DINI' dh1  'DFIN' dhm2 ;
  AMAJ = 'DROIT' nr  AM AJ 'DINI' dhm1 'DFIN' dhj  ;
  AJCJ = 'DROIT' nha  AJ CJ 'DINI' dva2 'DFIN' dvc ;

  A1F1 = 'DROIT' nf  A1 F1 'DINI' dve2 'DFIN' df1  ;
  F1B1 = 'DROIT' nf  F1 B1 'DINI' df2 'DFIN' dvb   ;
  B1BM = 'DROIT' nm  B1 BM 'DINI' dh1  'DFIN' dhm2 ;
  BMBJ = 'DROIT' nr  BM BJ 'DINI' dhm1 'DFIN' dhj  ;
  BJFJ = 'DROIT' nf  BJ FJ 'DINI' dvb  'DFIN' df2  ;
  FJAJ = 'DROIT' nf  FJ AJ 'DINI' df1  'DFIN' dve2 ;

  BJB0 = 'DROIT' nj  BJ B0 'DINI' dhj   'DFIN' dh0  ;
  B0F0 = 'DROIT' nf  B0 F0 'DINI' dvb  'DFIN' df2   ;
  F0A0 = 'DROIT' nf  F0 A0 'DINI' df1  'DFIN' dve2  ;
  A0AJ = 'DROIT' nj  A0 AJ 'DINI' dh0  'DFIN' dhj   ;

  CJC1 = CJCM 'ET' CMC1 ;
  A1AJ = A1AM 'ET' AMAJ ;
  A1B1 = A1F1 'ET' F1B1 ;
  B1BJ = B1BM 'ET' BMBJ ;
  A1B1 = A1F1 'ET' F1B1 ;
  B0A0 = B0F0 'ET' F0A0 ;
  A1AJ = A1AM 'ET' AMAJ ;
  B1B0 = B1BJ 'ET' BJB0 ;
  BJAJ = BJFJ 'ET' FJAJ ;

  ZONE_1 = 'DALLER' CJC1 C1A1 A1AJ AJCJ PLAN ;
  ZONE_2 = 'DALLER' A1B1 B1BJ BJAJ ('INVERSE' A1AJ) PLAN ;
  ZONE_3 = 'DALLER' BJB0 B0A0 A0AJ ('INVERSE' BJAJ) PLAN ;

'ELIMINATION' 1e-5 ZONE_1 'ET' ZONE_2;
'ELIMINATION' 1e-5 ZONE_2 'ET' ZONE_3;
  MT =  ZONE_2 'ET' ZONE_3;
*'TRACE' MT ;
* opti donn 5;


* Lignes pour conditions aux limites et post-traitement

  AXE  = ('INVERSE' B0A0) ;
*  INJ  = A0AJ             ;
  jd = ('INVERSE' BJB0)  coul roug ;
  domf = ('INVERSE' B1B0) coul vert;
  BAS  = A1AJ ET ('INVERSE' A0AJ)   ;
*  BAS  = INJ 'ET' AJCJ 'ET' BAS2  ;
*  HAUT = ('INVERSE' B1B0) ;
  PCON = A1B1 ;
*  COTE = C1A1 ;
  PTOT = BAS 'ET' PCON   ;
  aria = PTOT et jd et domf ;
*  elim aria PTOT  1.e-5 ;
*  elim jd domf  1.e-5 ;
*  trac aria   ;

*opti donn 5;
* Mise en place de la procedure

RXT = 'TABLE' ;
RXT.'vtf' = MT ;

RXT.'axe' = AXE ;
*RXT.'diru1' = (0 1) ;
RXT.'pi' = ( MT 'POIN' 'PROC' (0.0 2.0)) ;
RXT.'IMPR' = 2 ;

RXT.'THERMP' = FAUX ;

*RXT.'TIMP1' = 'TABLE' ;
*RXT.'TIMP1'.'MAILLAGE' = PCON ;
*RXT.'TIMP1'.'t' = 'PROG' 0.0 1000 ;
*RXT.'TIMP1'.'TIMP' = 'PROG' 120.0 120.0 ;
*RXT.'TIMP1'.'ECHAN' = 25.0 ;

RXT.'VAPEUR' = VRAI ;

*RXT.'AIR' = VRAI  ;
RXT.'TF0' = 24.3   ;
RXT.'PT0' = 1.01E5 ;
*RXT.'Yvap0' = 0.4372 ;
RXT.'Yvap0' = 0.0044  ;

RXT.'MODTURB' = 'NUTURB' ;
RXT.'NUT' = 1.8E-2       ;
*RXT.'MODTURB' = 'LMEL'   ;
*RXT.'LMEL' = 0.2         ;
*RXT.'LMEL' = 0.04        ;

RXT.'epsi' = 1.E-5       ;
RXT.'DISCR' = 'LINE'     ;
RXT.'KPRE' = 'MSOMMET'   ;
*RXT.'DISCR' = 'MACRO'   ;
*RXT.'KPRE' = 'CENTREP1' ;
RXT.'GRAPH' = FAUX       ;

RXT.'scenasp' = 'TABLE' ;
RXT.'scenasp'.'t' = 'PROG' 0.0 25.5        ;
RXT.'ASPER' = VRAI ;

 ASPER  = rxt.'ASPER'                                         ;
 si ASPER                                                     ;
**************description essai************************
  rop      =        1.000e+3                                  ;
  C        =        9.0e-2                                    ;
  Vi       =        2.0                                      ;
  Dp       =        6.0e-4                                    ;
  S        =        3.14*((0.1)**2)                           ;
  C_s      =        C/S                                       ;
  alpha    =        C_s/rop/Vi                                ;
* alpha = 1.4e-04
*  list alpha                                                  ;
*  opti donn 5                                                 ;
  rxt.'aspinj' =   jd                                         ;
  rxt.'toitf'  =   domf                                       ;
  rxt.'rod'    =   rop                                        ;
  rxt.'Cpd'    =   4.180e+3                                   ;
  rxt.'scenasp'.'vzinj'  = prog  (-1.*Vi)   (-1.*Vi)          ;
  rxt.'scenasp'.'xdinj'  = prog   alpha      alpha            ;
  rxt.'scenasp'.'tdinj'  = prog   4.000e+1   4.000e+1         ;
  rxt.'scenasp'.'ddinj'  = prog   Dp         Dp               ;
 finsi                                                        ;

*************************************
**          EXECUTION              **
*************************************
* temps caracteristiques a recalculer pour estimer le temps
RXT.'DT0' = 0.1 ;
NSAV = 0 ;
NPAS = 5 ;
execrxt NPAS RXT ;
*opti donn 5;
NSAV = NSAV '+' NPAS ;



*'OPTI' 'SAUVE' 'boite-asp.save'  ;
*'SAUVE' ;


 Maxe = chan axe QUAF     ;
 Mdomf = chan domf QUAF   ;
 $vtf= rxt.'GEO'.'$vtf'   ;
 vtf = doma $vtf maillage ;
* elim (Maxe et vtf) epsi  ;
* elim (Mdomf et vtf) epsi  ;
 vtf = doma $vtf maillage ;


un=rxt.'TIC'.'UN'  ;
vn=rxt.'TIC'.'VN'  ;
rho=rxt.'TIC'.'RHO';
rvp=rxt.'TIC'.'RVP';
tf=rxt.'TIC'.'TF'  ;
rvp=rxt.'TIC'.'RVP';
td=rxt.'TIC'.'TD'  ;
xd=rxt.'TIC'.'XD'  ;
dd=rxt.'TIC'.'DD'  ;

an= psca vn vn (mots ux uy uz)(mots ux uy uz) ;
an= an **0.5 ;
mess ' Norme de VN ' (mini an) (maxi an) ;

list rxt.'TIC'.'Tdmi' ;
list rxt.'TIC'.'Qaspe';
list rxt.'TIC'.'Mrest';
list rxt.'TIC'.'LMAXU';
*



 lTdmi = prog
  40.000     40.000   34.438   29.848    26.280    23.420 ;

 lqaspe=prog
   0.0000   0.0000  1.03552E-03  1.66385E-03  2.20096E-03  2.65541E-03 ;

 lmrest=prog
   0.35316  0.35316   0.35326   0.35343   0.35365 0.35392 ;


* lmaxu=prog
* 0.0000   0.0000   9.89819E-02  0.31752 0.51396  0.72466 ;
*0.0000   0.0000   0.10029   0.33355   0.53667   0.75167 ;
* Après correction Gounand 2012/12/20
 lmaxu=prog
0.0000       0.0000      8.34311E-02  0.26067      0.41732      0.68018;


 tic=rxt.'TIC' ;
****************


 ERti=SOMM( abs (ltdmi - tic.'Tdmi') )/ 80.   ;
 ERQA=SOMM( abs (lqaspe - tic.'Qaspe' ) ) /3. ;
 ERMR=SOMM( abs (lmrest - tic.'Mrest' ) ) /6. ;
 ERum=SOMM( abs (Lmaxu  - tic.'LMAXU' ) )     ;

 Mess ' ERti=' ERti ' ERQA=' ERQA ' ERMR=' ERMR 'ERum=' ERum ;

 Si (ERti '>' 1.e-2) ; erreur 5 ; Finsi ;
 Si (ERQA '>' 1.e-2) ; erreur 5 ; Finsi ;
 Si (ERMR '>' 1.e-2) ; erreur 5 ; Finsi ;
 Si (ERum '>' 1.e-2) ; erreur 5 ; Finsi ;

 Finsi;

SI GRAPH ;
uy1  = 'EXCO' 'UY' un                    ;
evuy= evol 'CHPO' uy1 axe ;
dess evuy 'TITRE' ' UY axe ';

evvy= evol 'CHPO' (exco vn uy) axe ;
dess evvy 'TITRE' ' VY axe ';

evr= evol 'CHPO' rho axe ;
dess evr 'TITRE' ' Rho axe ';

evrv= evol 'CHPO' rvp axe ;
dess evrv 'TITRE' ' Rho vap axe ';

evtf= evol 'CHPO' tf axe ;
dess evtf 'TITRE' ' Tf axe ';
list evtf ;

evtd= evol 'CHPO' td axe ;
dess evtd 'TITRE' ' Td axe ';
list evtd  ;

evxd= evol 'CHPO' xd axe ;
dess evxd 'TITRE' ' Xd axe ';

evdd= evol 'CHPO' dd axe ;
dess evdd 'TITRE' ' DD axe ';


ixd=masq xd 'SUPERIEUR' 1.e-5 ;
vn = ixd *  vn  ;

ung= vect un 1. ux uy  jaune ;
vng= vect vn 0.1 ux uy  vert   ;
trace ung vtf (cont vtf);
trace vng vtf ;
*trace tp $vtp            ;
trace (ung et vng) vtf ;

*********
*debit sur toutes surface
 Vparoi      = vn * rxt.'rod'                         ;
 flimp       = KOPS   Xd '*' Vparoi                   ;
  nmfU = DOMA rxt.'GEO'.'$menvfU' 'NORMALEV'          ;
   vnfU =vect nmfU 0.05 'UX' 'UY' 'ROUG'              ;
   trace vnfU rxt.'GEO'.'menvfU'                      ;
 DepufU = 'DBIT' flimp rxt.'GEO'.'$menvfU'            ;
* list nmfu ;
 MESS 'debit sortie par les parois a t'    ;
 list DepufU ;
* masseU = DepufU  * rxt.'TIC'.'LTPS'    ;
* MESS 'masse sortie par les parois a t'    ;
 MESS 'temps final' ;
 list rxt.'TIC'.'LTPS' ;

  denseau = 'KOPS' Xd '*' rop                     ;
  Diagvtf    = 'DOMA' $vtf    'XXDIAGSI'          ;
  masseeau = 'SOMT' (Diagvtf '*' denseau)         ;
  MESS 'masse dans l enceinte a t'              ;
  list masseeau                                   ;
  trace denseau vtf TITR 'denseau'                ;
  ev =evol chpo denseau domf ;
  dess ev ;
  ev =evol chpo denseau axe ;
  dess ev ;
*
*opti donn 5;
  un = rxt.'TIC'.'UN'                                   ;
  uy1  = 'EXCO' 'UY' un                                 ;
  vn = rxt.'TIC'.'VN'                                   ;
  vy1  = 'EXCO' 'UY' vn                                 ;



trace rvp vtf (cont vtf) TITR ' RVP ' ;
trace tf vtf (cont vtf) TITR ' TF '   ;
trace td vtf (cont vtf) TITR ' TD '   ;
trace xd vtf (cont vtf) TITR ' XD '   ;
*opti donn 5;


'FINSI' ;

  'FIN' ;
















