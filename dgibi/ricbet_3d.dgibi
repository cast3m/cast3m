* fichier : ricbet_3d.dgibi
************************************************************************
* Section : Mecanique Endommagement
************************************************************************
*******************************************************
*  Cas test de l'implantation numerique du modele
*  RICBET LOCAL/NON LOCAL 3D
*******************************************************
* Développé par :
*     Benjamin Richard
* Contact :
*     Benjamin.Richard@lmt.ens-cachan.fr
* Institution :
*     CEA\DEN\DANS\DM2S\SEMT\EMSI
*     LMT\ENS Cachan
*******************************************************
* Les cas de charges sont entrés :
* - 1 : Traction monotone
* - 2 : Compression monotone
* - 3 : Traction cyclique
* - 4 : Compression cyclique
* - 5 : Traction/compression cyclique
* - 6 : Compression/traction
* - 7 : Traction/Compression/Traction 1
* - 8 : Traction/Compression/Traction 2
*******************************************************
graph = 'N';

* Choix du cas de charge-
ncas  = 1;

* Test du fichier compatible avec le non local
* nloc0 = 0;   Cas local
* nloc0 = 1;   Cas non local

nloc0 = 0;

* Calcul élastiques (pour référence avant fissuration)
* nelas = 1; cas non lineaire
* nelas = 0; cas lineaire

nelas  = 1;
*******************************************************

*
*-------------- Options de calcul ---------------------
*
OPTION DIME 3 ELEM CUB8;

*
*-------------- Definition de la geometrie ------------
*
P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;
P5 = 0. 0. 1. ;

L1 = P1 DROIT 1 P2 ;
L2 = P2 DROIT 1 P3 ;
L3 = P3 DROIT 1 P4 ;
L4 = P4 DROIT 1 P1 ;

LTOT = L1 ET L2 ET L3 ET L4 ;
SURF1 = SURF LTOT PLANE;
VOLTO =VOLU SURF1 1 TRANS P5;
SURF2= FACE VOLTO 2;

VOLTOT = VOLTO;

*
*------- Définition des conditions aux limites --------
*----------- et des déplacements imposés --------------
*
CL  = BLOQ SURF1 UZ;
CLL = BLOQ P1 'DEPL';
CL1 = BLOQ SURF2 Uz;
CL2 = bloq (noeud 3) UY;
cl=cl et cl2;

D1 = DEPI CL1 1;

*
*----- Définition du modèle ---------------------------
*
SI (EGA nelas 1);
  SI (EGA nloc0 1);
   MOD1=MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT RICBET
      'NON_LOCAL' 'MOYE' 'V_MOYENNE' (MOTS 'EPTI') ;
  SINON;
   MOD1=MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE ENDOMMAGEMENT RICBET;
  FINSI ;
SINON;
      MOD1=MODE VOLTOT MECANIQUE ELASTIQUE ISOTROPE;
FINSI;
EBETON  = 34100E6;
NUBETON = 0.16;
LC      = 0.020;
FTN     = 3.80E+6;

* Young modulus
youngn = EBETON;

* Poisson ratio
nun    = NUBETON;

* Brittleness in tension
aldin  = 6.0e-3;

* Hardening modulus 1
gam1n  = 5.0e9;

* Hardening modulus 2
a1n    = 8.0e-6;

* Closure stress
srefn = -3e6;

* Compression strength (initial)
fcn = 6e6;

* Asymptotic stress in compression
sigun = -4e6;

* Yield surface 1
afn = 0.7;

* Yield surface 2
bfn = 0.3;

* Plastic potential 1
agn = 0.6;

* Plastic potential 2
bgn = 0.45;

* Yield hardening 1
acn = 3.2e10;

* Yield hardening 1
bcn = 700.0;

SI (EGA nelas 1);
mat1   =MATE mod1 YOUN youngn  NU   nun
                      FT   ftn     GAM1 gam1n
                      A1   a1n     ALDI aldin
                      SREF srefn AF afn BF bfn
                      AG agn BG bgn AC acn BC bcn
                      SIGU sigun FC fcn;
SINON;
    mat1   =MATE mod1 YOUN youngn  NU   nun;
FINSI;




*
*----- Définition des cas de charge -------------------
*

SI (EGA ncas 1) ;
LI1  = PROG 0. 1. ;
LI2  = PROG 0. 1.0e-3;
LIS1 = PROG 0. PAS 0.01 1.;
FINSI;

SI (EGA ncas 2) ;
LI1  = PROG 0.  1.;
LI2  = PROG 0.  -1;
LIS1 = PROG 0. PAS 1.E-4 6.0E-3;
FINSI;

SI (EGA ncas 3) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 1.5e-4 9.5e-6 2.0E-4 3.0E-5 2.5E-4;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 4) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. -3.0e-3 -1.5e-4 -5.0E-3 -3.5E-4 -10.0E-3;
LIS1 = PROG 0. PAS 0.02 5.;
FINSI;

SI (EGA ncas 5) ;
LI1  = PROG 0. 1. 2. 3. 4. 5. 6. 7.;
LI2  = PROG 0. 1.3e-4 -3.0e-3 -1.5E-4 -5.0E-3 -3.5E-4 -8.0E-3 1.0E-3;
LIS1 = PROG 0. PAS 0.005 7;
FINSI;

SI (EGA ncas 6) ;
LI1  = PROG 0. 1. 2.;
LI2  = PROG 0. -2.0e-3 5.0e-4;
LIS1 = PROG 0. PAS 0.005 2;
FINSI;

SI (EGA ncas 7) ;
LI1  = PROG 0. pas 1. 11.;
LI2  = PROG 0.  5.0e-4 -5.0e-4 6.0e-4  -5.0e-4
                7.0e-4 -5.0e-4 8.0e-4  -5.0e-4
                9.0e-4 -5.0e-4 10.0e-4;
LIS1 = PROG 0. PAS 0.01 11;
FINSI;

SI (EGA ncas 8) ;
LI1  = PROG 0. 1. 2. 3. 4. 5.;
LI2  = PROG 0. 3.e-4 -1.e-5 5.0E-4 -5E-3 -4.0E-3;
LIS1 = PROG 0. PAS 0.02 2;
FINSI;


EV = EVOL MANU LI1 LI2 ;
CHA1 = CHAR 'DIMP' D1 EV ;

*
*----------- Calcul par l'operateur PASAPAS ------------
*
LC = 1.0e-10;
CO1 = CONNEC mod1 LC NORMAL;

TAB1 = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CL ET CLL ET CL1;
TAB1.'MODELE' = MOD1;
TAB1.'MOVA' = 'D   ';
TAB1.'CHARGEMENT' = CHA1;
TAB1.'TEMPS_CALCULES' = LIS1;

SI (EGA nloc0 1);
TAB1.CONN = CO1;
MAT1 = MAT1 ET ( MATE MOD1 'LCAR' LC) ;
FINSI;

TAB1.'CARACTERISTIQUES' = MAT1;
PASAPAS TAB1 ;

dd=dime tab1.deplacements;

*
*----------- Courbe effort-deplacement -----------------
*
si (ega graph 'O');
ev2=@global tab1 CL1 EV fz;
@excel1 ev2 'loi_beton-RL5.txt';
DESS EV2;
finsi;

ee=tab1.variables_internes.(dd-1);
aa=extr ee 'D   ' 1 1 1;
list aa;
message 'erreur relative';

err = (aa - 0.99020) / 0.99020;
list err;

si (> err 1.0E-3);
erreur(5);
finsi;

fin;


















