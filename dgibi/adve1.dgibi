* fichier : adve1.dgibi
************************************************************************
* Section : Thermique Advection
* Section : Thermique Statique
************************************************************************
* Cas-test de l'operateur ADVEction.                                   *
*                                                                      *
* Comparaison a une solution analytique.                               *
*                                                                      *
* On calcule la temperature d'un fluide qui s'ecoule dans un tuyau     *
* chauffe sur toute sa longueur.                                       *
* On verifie la temperature du fluide en sortie du tuyau.              *
*                                                                      *
* La puissance lineique est de : 500 W/m                               *
* La temperature en entree est de : 20째C                               *
* La vitesse d'ecoulement est de : 25 cm/s                             *
* La section du tuyau est de : 5.e-5 m2, soit environ 8 mm de diametre *
* La longueur du tuyau est de : 5 m                                    *
*                                                                      *
* La temperature attendue en sortie est de : 70째C                      *
*                                                                      *
* On utilise les elements lineaires et quadratiques (TUY2,TUY3)        *
*                                                                      *
*----------------------------------------------------------------------*
'OPTI' 'DIME' 3 'ELEM' 'SEG2' ;
*                                                                      *
* Commentez cette ligne pour voir les traces :                         *
'OPTI' 'TRAC' 'PSC' ;
*                                                                      *
O1       = 0 0 0 ;
X1       = 1 0 0 ;
P1       = 2.5 * X1 ;
P2       = 5.0 * X1 ;
Y1       = 0 1 0 ;
Z1       = 0 0 1 ;
*                                                                      *
L1       = O1 'DROI' 5 P1 'COUL' 'VERT' ;
L2       = P1 'DROI' 5 P2 ;
L2       = 'CHAN' 'QUAD' L2 'COUL' 'BLEU' ;
'TITR' ' Maillage du tuyau, longueur = 5 m (Vert=SEG2,Bleu=SEG3) ' ;
'TRAC' 'QUAL' (L1 'ET' L2) ;
*                                                                      *
mo1      = 'MODE' L1 thermique advection 'TUY2' ;
mo2      = 'MODE' L2 thermique advection 'TUY3' ;
mo1      = mo1 'ET' mo2 ;
*                                                                      *
L0       = L1 'ET' L2 ;
*                                                                      *
* La veleur de la conductivite ne joue pas, on prend donc "1." :       *
ma1      = 'MATE' mo1 'K' 1. 'RHO' 1.e3 'C' 4.e3
           'VITE' 0.25 'SECT' 5.e-5 ;
*                                                                      *
sq1      = 'SOUR' mo1 ma1 L0 (5.e2 / 5.e-5) ;
*                                                                      *
KA1      = 'ADVE' mo1 ma1 ;
*                                                                      *
cl1      = 'BLOQ' T O1 ;
dcl1     = 'DEPI' cl1 20. ;
*                                                                      *
cht2     = 'RESO' (ka1 'ET' cl1) (sq1 'ET' dcl1);
*                                                                      *
'TITR' ' Evolution de la temperature le long du tuyau ' ;
'DESS' ('EVOL' 'VERT' 'CHPO' cht2 L0 T) ;
*                                                                      *
* Test :
tref1    = 70. ;
tp1      = 'EXTR' cht2 T P2 ;
err1     = 'ABS' (TREF1 - tp1) / tp1 ;
*                                                                      *
'OPTI' 'ECHO' 0 ;
'MESS' ;
mot1     = 'CHAI' ' > Temperature calculee en  sortie = '
  'FORMAT' '(F3.0)' tp1 '째C pour 70째C attendu ' ;
'MESS' mot1 ;
'MESS' ' > Erreur relative = ' err1 ;
'MESS' ;
'OPTI' 'ECHO' 1 ;
*                                                                      *
'SI' (err1 > 1.e-5) ;
  'ERRE' 5 ;
'SINO' ;
  'OPTI' 'ECHO' 0 ;
  'MESS' ;
  'MESS' ' > Test reussi ' ;
  'MESS' ;
'FINS' ;
*                                                                      *
'FIN' ;


