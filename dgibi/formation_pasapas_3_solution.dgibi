************************************************************************
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Chauffage d'une plaque avec changementde phase par une source de     *
* chaleur variable, dependante de la temperature)                      *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement (convection + source variable avec la  *
*   procedure CHARTHER)                                                *
* - la resolution avec PASAPAS                                         *
* - le post traitement                                                 *
************************************************************************


** Options generales
OPTI 'DIME' 2 'ELEM' 'QUA4' 'ECHO' 0 ;
ITRAC = FAUX ;


** Parametres geometriques, materiaux, conditions aux limites
L        = 1. ;
R        = 0.25 ;
LAMBDA   = EVOL 'MANU' 'T' (PROG   0.  27. 727.)
                       'K' (PROG 401. 398. 357.) ;
CP       = 385. ;
RHO      = EVOL 'MANU' 'T' (PROG   20. 1083. 1100. 1200. 1300.)
                     'RHO' (PROG 8930. 7940. 7924. 7846. 7764.) ;
TFUS     = 1083. ;
QLATFUS  = 204.8E3 ;
HCONV    = 150. ;
VSOUR    = EVOL 'MANU' 'T'   (PROG 20. 150. 300.  500.  700.  1000.  1500.)
                   'Q' (1.E6*(PROG  5.   4.   3.7   3.5   3.3    3.1    3.)) ;
T_INI    = 20. ;
T_EXT    = 20. ;


** Maillage
OPTI 'DENS' (R/5.) ;
P0       = 0. 0. ;
P1       = L 0. ;
LBAS     = DROI P0 P1 ;
S1       = LBAS TRAN (0. L) ;
CHX      = S1 COOR 1 ;
PTX      = CHX POIN 'EGINFE' (R + 1.E-8) ;
EL1      = S1 ELEM 'APPU' 'STRI' PTX ;
CHY      = EL1 COOR 2 ;
PTY      = CHY POIN 'EGINFE' (R + 1.E-8) ;
MAIL1    = EL1 ELEM 'APPU' 'STRI' PTY ;
MAIL2    = DIFF S1 MAIL1 ;
MAILT    = MAIL1 ET MAIL2 ;
LDROITE  = S1 COTE 2 ;
LHAUT    = S1 COTE 3 ;
LEXT     = LDROITE ET LHAUT ;
SI ITRAC ;
  TRAC 'FACE' ((MAIL1 COUL 'ROUG') ET (MAIL2 COUL 'BLEU')) 'TITR' 'Maillage, zone source en rouge' ;
FINSI ;


** Modeles et materiaux
MOD_THER = MODE MAILT 'THERMIQUE' 'PHASE' ;
MAT_THER = MATE MOD_THER 'K' LAMBDA 'C' CP 'RHO' RHO 'TPHA' TFUS 'QLAT' QLATFUS ;
* sous-modele reduit au maillage de la source (pour l'operateur SOUR)
MOD1     = REDU MOD_THER MAIL1 ;

MOD_CONV = MODE LEXT 'THERMIQUE' 'CONVECTION' ;
MAT_CONV = MATE MOD_CONV 'H' HCONV ;


** Chargements thermiques
EV1      = EVOL 'MANU' (PROG 0. 1.E5) (PROG 1. 1.) ;
CHTCONV  = MANU 'CHPO' LEXT 1 'T' T_EXT ;
CHA1     = CHAR 'TECO' CHTCONV EV1 ;
*CHFL1    = SOUR MOD1 VSOUR MAIL1 ;
*CHA2     = CHAR 'Q' CHFL1 EV1 ;


** Champ de temperature initial
TEM0     = MANU 'CHPO' MAILT 1 'T' T_INI ;


**Resolution avec PASAPAS
LTPSCAL1 = PROG 0. 'PAS' 0.01E5 0.2E5 'PAS' 0.05E5 1.E5 ;
TAB1                         = TABL ;
TAB1 . 'MODELE'              = MOD_THER ET MOD_CONV ;
TAB1 . 'CARACTERISTIQUES'    = MAT_THER ET MAT_CONV ;
TAB1 . 'TEMPERATURES'        = TABL ;
TAB1 . 'TEMPERATURES' . 0    = TEM0 ;
TAB1 . 'CHARGEMENT'          = CHA1 ;
TAB1 . 'TEMPS_CALCULES'      = LTPSCAL1 ;
TAB1 . 'PROCEDURE_CHARTHER'  = VRAI ;
TAB1 . 'AMOI'                = TABL ;
TAB1 . 'AMOI' . 'MAIL'       = MAIL1 ;
TAB1 . 'AMOI' . 'MODE'       = MOD1 ;
TAB1 . 'AMOI' . 'EVOL'       = VSOUR ;


** Procedure CHARTHER
DEBP CHARTHER T1*'TABLE'  TPS1*'FLOTTANT' ;
  MA1 = T1 . 'AMOI' . 'MAIL' ;
  MO1 = T1 . 'AMOI' . 'MODE' ;
  EV1 = T1 . 'AMOI' . 'EVOL' ;
* reduction du champ de temperatures au maillage de la source
***  CHT1 = T1 . 'ESTIMATION' . 'TEMPERATURES' ;
  CHT1 = T1 . 'WTABLE' . 'THER_COURANT' ;
  CHT2 = REDU CHT1 MA1 ;
* interpolation du champ de source a partir du champ de temperature
  CHS = IPOL CHT2 EV1 ;
* creation du second membre
  CHFL1 = SOUR MO1 CHS ;
* sortie du second membre
  T2 = TABL ;
  T2 . 'ADDI_SECOND' = CHFL1 ;
FINP T2 ;


** Appel à la procedure PASAPAS
PASAPAS TAB1 ;


** Post-traitement
NBOU1 = DIME (TAB1 . 'TEMPS_CALCULES') ;
T_MAX = MAXI (TAB1 . 'TEMPERATURES' . (NBOU1-1)) ;
LIS1 = PROG T_INI 'PAS' ((T_MAX-T_INI)/20.) T_MAX ;
LTPS = PROG ;
LPHA = PROG ;
TL = TABL ;
TL . 'TITRE' = TABL ;
TL . 'TITRE' . 1 = 'Coin inf. gauche' ;
TL . 'TITRE' . 2 = 'Coin sup. droite' ;
REPE BOU1 NBOU1 ;
  I        = &BOU1 - 1 ;
  TEMPS_I  = TAB1 . 'TEMPS' . I ;
  LTPS = LTPS ET TEMPS_I ;
* temperatures au temps i
  TEMP_I   = TAB1 . 'TEMPERATURES' . I ;
  SI ITRAC ;
*    TRAC TEMP_I MAILT LIS1 'TITR' (CHAI 'Temperature au temps' TEMPS_I) ;
  FINSI ;
* proportions de phase au temps i
  PHASE_I  = TAB1 . 'PROPORTIONS_PHASE' . I ;
  LPHA = LPHA ET (INTG MOD_THER PHASE_I) ;
  SI ITRAC ;
    TRAC PHASE_I MOD_THER 'TITR' (CHAI 'Proportions de phase au temps' TEMPS_I) ;
  FINSI ;
FIN BOU1 ;
EVPHA = EVOL 'ROUG' 'MANU' 'Temps' LTPS 'Vol Phase' LPHA ;
SI ITRAC ;
  DESS EVPHA 'TITR' 'Volume de phase liquide vs Temps' ;
FINSI ;
EVTP0 = EVOL 'ROUG' 'TEMP' TAB1 'TEMPERATURES' 'T' P0 ;
P2 = MAILT 'POIN' 'PROC' (L L) ;
EVTP2 = EVOL 'VERT' 'TEMP' TAB1 'TEMPERATURES' 'T' P2 ;
SI ITRAC ;
  DESS (EVTP0 ET EVTP2) 'LEGE' TL 'TITR' 'Temperature aux coins vs Temps' ;
FINSI ;

FIN ;








