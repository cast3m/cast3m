* fichier : dyne03.dgibi
************************************************************************
* Section : Mecanique Dynamique
* Section : Mecanique Dynamique
************************************************************************
****************************************************
*
*   EXEMPLE D'UTILISATION DE L'OPERATEUR DYNE
*
*   Rupture de tuyauterie avec impact
*   Calcul sur base modale avec raideur de choc
*   Opérateur Dyne
*   D. Combescure
*   Aout 2006
*
*****************************************************
* GRAPH = VRAI;
GRAPH = FAUX;
*
opti dime 3 elem seg2;
OPTI EPSI LINEAIRE;
*
L1 = 3.;
n1 = 20;
L = 6.;
n2 = 20;
H = 2.;
nh = 20;
*
p1 = 0. 0. 0.;
p2 = L1 0. 0.;
p2b = L1 0. 0.;
p3 = L 0. 0.;
p3b = L 0. 0.;
lig1 = d n1 p1 p2;
lig2 = d n2 p2 p3;
mesh = lig1 et lig2;
*
P4 = L 0. ((-1.)*H);
*
meshV = d nh P4 P3b;
*
R = 0.30;
P0 = 3.6D6;
sigy = 219.D6;
e = (P0/sigy)*R;
I1 = 0.5*2.*pi*(R**3)*e;
S1 = 2.0*pi*R*e;
S0 = pi* (R**2);
*
R2 = 0.10;
ep2 = 0.01;
I2 = 0.5*2.*pi*(R2**3)*ep2;
S2 = 2.0*pi*R2*ep2;
*
F0 = 1.26*P0*S0;
*
E1 = 200000.D6;
E2= E1;
*
nu1 =0.2;
ro1 = 7.8D3;
*
ro2 = ro1;
nu2 = nu1;
*
*
ustat = ((L**3)/(3.*E1*I1))*F0;
mul = 4.*2.*sigy*e*(R**2);
*
mod1 = 'MODELI' mesh mecanique elastique POUT;
mat1 = mate mod1 YOUN E1 NU nu1 RHO ro1
                 INRY I1 INRZ I1 TORS (2.*I1)
                 SECT S1;
K1 =  (3.*E1*I1/((L**3)));
*
mod2 = 'MODELI' meshV mecanique elastique POUT;
mat2 = mate mod2 YOUN E2 NU nu2 RHO ro2
                 INRY I2 INRZ I2 TORS (2.*I2)
                 SECT S2;
K2 =  (3.*E2*I2/((H**3)));
*
modT = mod1 et mod2;
Matt = mat1 et mat2;
MeshT = mesh et MeshV;
*
rig1 = RIGI modT matT;
mas1 = mass modT matT;
bl1 = (BLOQ p1 RY)
   et (BLOQ p1 UY) et (BLOQ p1 RZ);
bl2b = BLOQ p2b DEPL;
bl3b = (BLOQ P4 DEPL ROTA) et (BLOQ p3b UX);
blM = (BLOQ  mesh UZ) et (BLOQ mesh RX)
    et (BLOQ mesh UX);
*
For1 = FORCE (0. (F0) 0.) p3;
*
*
* Caractéristiques liaisons
*
umax = 1.17;
Kliai = 15.6D6;
Jeuliai = 0.500;
******************************
******************************
*
* Cas 1: 2 modes avec choc mou
*
******************************
******************************
nmode = 2;
FR = VIBR PROCHE (PROG 11. ) (LECT nmode)
          (RIG1 et bl1 et blm et bl3b) MAS1;
*
Mequi3 = TABLE;
*
*
lmot2 = MOTS UX UY UZ RX RY RZ;
lmot1 = MOTS FX FY FZ MX MY MZ;
lmot3 = lmot2;
IdFYT = (manu chpo meshT 6 UX 0. UY (-1.0) UZ 0. RX 0. RY 0. RZ 0.
       NATURE DISCRET);
*
oeil = 10. 10. 2.;
Repeter bou1 nmode;
  FIFI = FR . MODES . &Bou1 . FREQUENCE      ;
  TT = FR . MODES . &Bou1 .  DEFORMEE_MODALE  ;
  DEf1 = DEFO meshT TT;
  SI GRAPH;
    titre (CHAIN 'Frequence=' Fifi);
    trac oeil def1;
  FINSI;
  Mm1 = resu ((mas1*(TT)) '*' (TT) lmot1 lmot2 lmot3);
  Mm1V =  ( ((maxi (exco Mm1 UX))) +
          ((maxi (exco Mm1 UY))) +
          ((maxi (exco Mm1 UZ))) +
          ((maxi (exco Mm1 RX))) +
          ((maxi (exco Mm1 RY))) +
          ((maxi (exco Mm1 RZ))) );
  Mequi3.&bou1 = MM1V/((extr TT P3 UY)**2);
fin bou1;
*
Cliai = 2.*((Kliai*(Mequi3.1))**0.5);
*
* Appel à DYNE
*
************************************************
* TEMPS
*************************
*
DT  = MINI (PROG (1.E-5) (0.1/(2.*pi*fifi))) ;
NTT = ENTIER (0.1/DT)               ;
*
*************************
* TABLE AMORTISSEMENT
*************************
*
TAMOR = TABLE 'AMORTISSEMENT'       ;
ValAmor = 0.;
AMO = PROG NMODE*ValAmor                ;
TAMOR . AMORTISSEMENT = AMOR FR AMO ;
*
*************************
* TABLE CHARGEMENT (CHARG1 CHARG2 CHARG3)
*************************
TCHAR = TABLE 'CHARGEMENT'          ;
*
Ampl = 1.0;
Evotime = evol manu (prog 0. 0.01 100.)
                    (Ampl*(prog 0. 1. 1.));
CHA1 = CHAR For1 Evotime;
CHAP1 = PJBA FR CHA1;
TCHAR.'BASE_A' = CHAP1;
***********************************************************
*
* LIAISONS POINT-POINT
*
***********************************************************
*
TILIA = TABLE 'LIAISON'  ;
TTLB = TABLE 'LIAISON_B' ;
TILIA.'LIAISON_B' = TTLB ;
*
TLA12 = TABLE 'LIAISON_ELEMENTAIRE' ;
TTLB .  1 = TLA12 ;
*
TLA12 . 'TYPE_LIAISON' = MOT 'POINT_POINT';
TLA12 . 'POINT_A' = P3 ;
TLA12 . 'POINT_B' = P3b ;
TLA12. 'NORMALE' = ( 0. 1. 0.);
TLA12 . 'RAIDEUR' = Kliai ;
TLA12 . 'JEU' = JeuLiai      ;
TLA12 . 'AMORTISSEMENT' = Cliai;
*************************
* TABLE SORTIE
*************************
*
TABSOR = TABLE 'SORTIE';
*
TABSOR2 = TABLE 'LIAISON_B' ;
TABSOR.'LIAISON_B' = TABSOR2  ;
*
TVARPP = TABLE 'VARIABLE'    ;
TVARPP.'FORCE_DE_CHOC_POINT_A' = VRAI ;
TVARPP.'FORCE_DE_CHOC_POINT_B' = VRAI ;
TVARPP.'UY_POINT_A' = VRAI ;
TVARPP.'UY_POINT_B' = VRAI ;
*
TVARGD = TABLE 'VARIABLE'    ;
TVARGD.'FORCE_DE_CHOC' = VRAI ;
TVARGD.'UY' = VRAI ;
*
TABSOR2. TLA12 = TVARPP;
*
************************************************
* CALCUL TEMPOREL PROPREMENT DIT
*************************
*
TRESU = DYNE DE_VOGELAERE FR TAMOR TCHAR TILIA
             NTT DT 1 TABSOR  ;
*
************************************************
* TRACE DES DEPLACEMNTS EN FONCTION DU TEMPS
*************************
*
UXP2 = EVOL VERT RECO TRESU FR 'DEPL' P2  UY  ;
UXP3 = EVOL BLEU RECO TRESU FR 'DEPL' P3  UY  ;
UXPB3 = EVOL BLEU RECO TRESU FR 'DEPL' P3B  UY  ;
UXPB3 = evol manu (extr UXPB3 absc)
     ((extr UXPB3 ordo) + (prog (dime (extr UXPB3 ordo))*jeuliai));
*
TDS = TRESU . 'TEMPS_DE_SORTIE' ;
RESCHOC = TRESU . TLA12;
FchocB = RESCHOC. FORCE_DE_CHOC_POINT_B;
FchocA = RESCHOC. FORCE_DE_CHOC_POINT_A;
*
EVFchoA = 'EVOL' 'MANU' TDS FChocA;
EVFchoB = 'EVOL' 'MANU' TDS FChocB;
*
*
Ncont = 10.;
REPETER bo3 (ENTIER ((NTT + 1)/Ncont));
  dep1 = RECO TRESU FR ((&bo3 - 1 )*DT*Ncont) 'DEPL';
  con1 = SIGMA MOD1 MAT1 dep1;
  con2 = SIGMA MOD2 MAT2 dep1;
  SI (&bo3 EGA 1);
    time = PROG ((&bo3 - 1 )*DT*Ncont);
    pfyp1 = prog (extr con1 1 1 2 EFFY);
    pmzp1 = prog (extr con1 1 1 2 MOMZ);
    deft = defo meshT dep1 1.;
  SINON;
    time = time et (PROG ((&bo3 - 1 )*DT*Ncont));
    pfyp1 = pfyp1 et (prog (extr con1 1 1 2 EFFY));
    pmzp1 = pmzp1 et (prog (extr con1 1 1 2 MOMZ));
    deft = deft et (defo meshT dep1 1.);
  FINSI;
FIN bo3;
tifyp1 = evol manu Time pfyp1;
timzp1 = evol manu Time pmzp1;
*
SI GRAPH;
  TRAC oeil DEFT ANIME;
 TITRE 'Deplacement avec 20 modes';
 dess (UXP2 et UXP3 et UXPB3) xbord 0. 0.10 ybord 0. 1.40;
 Titre 'Force de choc';
 DESS EVFchoA xbord 0. 0.10;
FINSI;
*
*
FIN;
*









