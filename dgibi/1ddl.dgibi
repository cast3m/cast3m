* fichier :1ddl.dgibi
************************************************************************
* Section : Mecanique Dynamique
************************************************************************
***************************************
*
* Etude d'un système 1 ddl
* Exemple d'utilisation de OSCI, SPO et SPON
*
* D. Combescure aout 2006
*
***************************************
*GRAPH = VRAI;
GRAPH = FAUX;
*COMPLET = VRAI;
COMPLET = FAUX;
*
opti dime 2 elem seg2;
*
fr1 = 5.;
*
dt = 0.005;
prtime = prog 0. pas dt 100.;
*
* Durée de la rampe de montée et de descente du chargement
dtramp = 0.001;
*
* Durée de l'impulsion
*
dtchar = 0.05;
prtim1 = prog 0. 0.1 (0.1 + dtramp) (0.1 + dtramp + dtchar)
                     (0.1 + dtchar + (2.*dtramp)) 100.;
prload1 = ((2.*pi*fr1)**2)*(prog 0 0. 1. 1. 0. 0.);
evload1 = evol manu prtime (ipol prtime prtim1 prload1);
prtimec = prog 0. pas dt 1.;
*
SI (EGA GRAPH VRAI);
 dess evload1 xbord 0. 1. ;
FINSI;
*
* Réponse 1ddl elastique
*
ev1 = osci evload1 amor 0.05 freq fr1 temps prtimec depl 0.;
SI (EGA GRAPH VRAI);
 dess ev1 xbord 0. 1.;
 @excel1 ev1 'ev1.excel';
FINSI;
*
*
*
* Calcul spectre oscillateur
*
SI COMPLET;
 lfreq = (prog 0.01 pas 0.1 20.);
SINON;
 lfreq  = prog 1. pas 1. 20.;
FINSI;
spev1 = spo evload1 amor (prog 0.05)
          freq lfreq
          acce coul rouge;
*
* Calcul spectre inélastique - oscilateur élastoplastique parfait
*
* Option DEMA => on trace la pseudoaccélération correspondant
*                à la limite élastique (pour le dimensionnement)
*
* Ductilité 1
spnlev1 = spon DEMA SIGN evload1 SPEL spev1 ACCE
          AMOR (prog 0.05) CINE (prog 1. 0.00) ACCE;
* Ductilité 2
spnlev2 = spon DEMA SIGN evload1 SPEL spev1 ACCE
          AMOR (prog 0.05) CINE (prog 2. 0.00) ACCE;
* Ductilité 5
spnlev5 = spon DEMA SIGN evload1 SPEL spev1 ACCE
          AMOR (prog 0.05) CINE (prog 5. 0.00) ACCE;
*
SI (EGA GRAPH VRAI);
 dess (spnlev1 et spnlev2 et spnlev5 et spev1);
FINSI;
*
ERR = maxi ((extr spnlev1 ordo) - (extr spev1 ordo)) abs;
*
SI (ERR > 1.D-3);
 ERRE 4;
FINSI;
*
FIN;
*





