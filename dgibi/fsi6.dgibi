* fichier :  fsi6.dgibi
************************************************************************
* Section : Fluides Vibration
* Section : Mecanique Interaction Fluide Structure
************************************************************************
* CAS TEST DU 91/10/04     PROVENANCE : PETI

*******************************************
*    Test fsi6.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC PSC   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************
*                                         *
*              TEST FSI6                  *
*                                         *
*  PARALLELEPIPEDIQUE FLUID VOLUME WITH   *
*              FREE SURFACE               *
*                                         *
*  Calculation of the sloshing frequency  *
*  of a parallelpipedique fluid volume    *
*  Height (h) 0.1m length (a) 2m          *
*  Breadth (b) 1.58m                      *
*                                         *
*                 P2                      *
*                 _________________       *
*                /                 /|     *
*               /                 / |     *
*              /              b  /  |     *
*             /                 /   |     *
*            /       a         /    |     *
*        P1 /_________________/    /      *
*           |                 |   /       *
*           |                 |  /        *
*           |               h | /         *
*           |_________________|/          *
*                                         *
*                                         *
*                                         *
*                                         *
*  The boundary conditions are            *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dz  | h = 0.                       *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dx  | x = 0  , a                   *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dy  | y = 0  , b                   *
*******************************************

TEMPS;

OPTI DIME 3;
OPTI ELEM CUB8;
OPTI EPSI LINEAIRE;

*=========================================*
*               geometry                  *
*=========================================*

* Dimensions en metres

* Points

P1 = 0. 0. 0.     ;
P2 = 0. 1.58114 0. ;
EYE =  5. -3. 3.  ;

S1 =P1 D 15 P2    ;

FRSUR  = S1 TRAN 15 (2. 0. 0.)      ;
WATER = FRSUR VOLU TRAN 2 (0. 0. -0.1)   ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI6 : MAILLAGE';
  TRAC QUAL  WATER CACH EYE   ;
FINSI;

*=========================================*
*      MODE - materiau - rigidite - masse *
*=========================================*

MODLIQ1=MODE WATER LIQUIDE LCU8;
MODLIQ2=MODE FRSUR LIQUIDE LSU4;

MATLIQ = MATE (MODLIQ1 ET MODLIQ2)
               RHO 1.E3 RORF 1.E3
               CSON 1435. CREF 1435.
               LCAR 1.0  G 9.81 ;

RIG1 = RIGI (MODLIQ1 ET MODLIQ2) MATLIQ  ;
MAS1 = MASS (MODLIQ1 ET MODLIQ2) MATLIQ  ;

*=========================================*
*                boundary conditions      *
*=========================================*

* No explicit boundary condition
* the boundary conditions are natural

* Theoritical value  of frequency

FRE1 =  TABL ;
FRE1.1 = 0.2476;
FRE1.2 = 0.3132;
FRE1.3 = 0.3993;
FRE1.4 = 0.4952;
LIST1 = PROG FRE1.1 FRE1.2 FRE1.3 FRE1.4 ;

*=========================================*
*     calculation of the frequencies      *
*                   and                   *
*       extraction of some results        *
*=========================================*

* Use of the operator VIBR. (option PROC)
 RESUL = VIBR PROC LIST1 RIG1  MAS1  ;

*=========================================*
*                   results               *
*=========================================*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

FRE2 = TABL;
 MOD = TABL;
 DEF = TABL;
 ERG = TABL;

I = 0;
REPETER BLOC1 (dime LIST1);
  I = I + 1;
  FRE2.I = RESUL . MODES . I . FREQUENCE;
  ERG.I = 100 *
        (ABS ((FRE1.I - FRE2.I) / FRE1.I));
  MESS ' MODE '  I     ;
  MESS ' ----------';
  MESS 'Frequence theorique :' FRE1.I 'Hz';
  MESS 'Frequence calculee  :' FRE2.I 'Hz';
  MESS '    Soit un ecart de : ' ERG.I '%';
  SAUT 1 LIGN;

* mode shape extraction and optional trace
  SI (NEG GRAPH 'N');
    MOD.I= RESUL . MODES . I . DEFORMEE_MODALE;
    DEF0 = DEFO FRSUR  MOD.I 0.;
    DEF.I= DEFO FRSUR MOD.I VERT;
    DEPZ = EXCO MOD.I 'UZ';
    TRAC CACH (DEF.I ET DEF0) EYE CACH ;
    TRAC DEPZ FRSUR
            (DEFO FRSUR MOD.I NOIR) ;
  FINSI;
FIN BLOC1;

*=========================================*
*                   code validation       *
*=========================================*

ERGMAX = MAXI
         (PROG ERG.1 ERG.2 ERG.3 ERG.4 )  ;

SI (ERGMAX <EG 5.0);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






