* fichier :  cou31.dgibi
************************************************************************
* Section : Mecanique Contact
* Section : Mecanique Plastique
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 0;
SAUT PAGE;
MESS '                                                       ';
MESS '                                                       ';
MESS '                                                       ';
MESS '  TEST AVEC 2 CUBES ET UN JOINT (3D)                   ';
MESS '                                                       ';
MESS '  LE CUBE DU HAUT EST SOUMIS A UN DEPLACEMENT IMPOSE   ';
MESS '  DE 0.1 DANS LA DIRECTION DE CISAILLEMENT X.          ';
MESS '  LE CUBE DU BAS EST BLOQUE EN X, EN Y ET EN Z.        ';
MESS '  LE JOINT PLASTIFIE EN CISAILLEMENT                   ';
MESS '  LA CONTRAINTE DE CISAILLEMENT DANS LA                ';
MESS '  DIRECTION X ATTEINT LA COHESION, ALORS QUE LA        ';
MESS '  CONTRAINTE DE CISAILLEMENT DANS LA DIRECTION Y       ';
MESS '  ET LA CONTRAINTE NORMALE VALENT 0.                   ';
MESS '  LE DEPLACEMENT DU JOINT DANS LA DIRECTION X EST DE   ';
MESS '  0.1                                                  ';
MESS '                                                       ';
MESS '                                                       ';
MESS '                                                       ';
MESS '               B _______________ C                     ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |    CUBE 1     |                      ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |_______________|                      ';
MESS '               A|    JOINT      |H                     ';
MESS '               G|_______________|D                     ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |    CUBE 2     |                      ';
MESS '                |               |                      ';
MESS '   Z            |_______________|                      ';
MESS '   |   Y       K                 F                     ';
MESS '   |  /                                                ';
MESS '   | /                                                 ';
MESS '   |/_______ X                                         ';
********************************************************************
*
OPTION DIME 3 ;
OPTION ELEM QUA4 MODE TRID ;
*
OEIL = -1.E1 -1.E6 1.E1 ;
*
* ----------DEFINITION DE LA GEOMETRIE DU CUBE SUPERIEUR----------
*
SA1 = 0.00 0.00 0.00 ;
SB1 = 1.00 0.00 0.00 ;
SC1 = 1.00 1.00 0.00 ;
SD1 = 0.00 1.00 0.00 ;
*
* ----------                  MAILLAGE                  ----------
*
SH1 =  SA1 DROIT 1 SB1 ;
SH2 =  SB1 DROIT 1 SC1 ;
SH3 =  SC1 DROIT 1 SD1 ;
SH4 =  SD1 DROIT 1 SA1 ;
*
SL1 = SH1 ET SH2 ET SH3 ET SH4 ;
SS1 = SURF SL1 PLAN ;
*
* ----------DEFINITION DE LA GEOMETRIE DU CUBE INFERIEUR----------
*
IA1 = 0.00 0.00 0.00 ;
IB1 = 1.00 0.00 0.00 ;
IC1 = 1.00 1.00 0.00 ;
ID1 = 0.00 1.00 0.00 ;
*
* ----------                  MAILLAGE                  ----------
*
IH1 =  IA1 DROIT 1 IB1 ;
IH2 =  IB1 DROIT 1 IC1 ;
IH3 =  IC1 DROIT 1 ID1 ;
IH4 =  ID1 DROIT 1 IA1 ;
*
IL1 = IH1 ET IH2 ET IH3 ET IH4 ;
IS1 = SURF IL1 PLAN ;
*
OPTION ELEM CUB8 ;
*
VOLS = SS1 VOLU 1 TRAN ( 0.00 0.00   1.00 ) ;
VOLI = IS1 VOLU 1 TRAN ( 0.00 0.00  -1.00 ) ;
FACS = FACE 2 VOLS ;
FACI = FACE 2 VOLI ;
VT1  = VOLS ET VOLI ;
*   definition pour la verif finale
P3 = NOEUD 3 ;
*
* ----------DEFINITION DE LA GEOMETRIE DU JOINT----------
*
* JOINT FORME DES FACES SS1 ET IS1
*
* ----------         MAILLAGE                ----------
*
OPTION ELEM LIA4 ;
*
JOINT = LIAISON 0.001 SS1 IS1 ;
*
*
* ----------      MAILLAGE TOTAL               ----------
*
GEOTOT = VT1 ET JOINT  ;
*
* ---------- DEFINITION DES CONDITIONS AUX LIMITES ----------
*
CL11 = BLOQ VOLI UX ;
CL12 = BLOQ VOLI UY ;
CL13 = BLOQ VOLI UZ ;
CL1  = CL11 ET CL12 ET CL13 ;
*
CL21 = BLOQ FACI UX ;
CL22 = BLOQ FACI UY ;
CL23 = BLOQ FACI UZ ;
CL2  = CL21 ET CL22 ET CL23 ;
*
CL3  = BLOQ SS1 UX  ;
DEP  = DEPI CL3 0.1 ;
*
CL   = CL1 ET CL3 ;
*
* ----------        DEFINITION DU MODELE DES CUBES       ----------
*
MODCUB = MODE VT1 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MACUB  = MATE MODCUB YOUN 100.0 NU 0.0000001  ;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MODJOI = MODE JOINT MECANIQUE ELASTIQUE PLASTIQUE COULOMB JOI4 ;
MAJOI  = MATE MODJOI KS   100.0  KN   100.0   EF  150.0   ECN  0.05
                     COHE 2.0     FRIC 45.0    ;
*
* ---------- MODELE ET MATERIAU DE TOUTE LA STRUCTURE ----------
*
MODTOT = MODCUB ET MODJOI;
MATTOT = MACUB  ET MAJOI ;
*
* ----------        DEFINITION DU CHARGEMENT         ----------
*
   LI1 = PROG 0.0  1.0  2.0 ;
   LI2 = PROG 0.0  1.0  1.0 ;
   EV = EVOL MANU X LI1 Y LI2 ;
*
   CAR1 = CHAR 'DIMP' DEP EV ;
*
* ----------            RESOLUTION            ----------
*
   TAB = TABLE ;
   LIS1 = PROG 0.0 PAS 1.0 2.0 ;
   TAB.'BLOCAGES_MECANIQUES' = CL;
   TAB.'CARACTERISTIQUES' = MATTOT;
   TAB.'CHARGEMENT' = CAR1;
   TAB.'TEMPS_CALCULES' = LIS1;
   TAB.'MODELE' = MODTOT;

   TMASAU=table;
tab . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
PASAPAS TAB ;
*
* ---------- POST TRAITEMENT ----------
*
*
* -----RECUPERATION DE LA TABLE DES DEPLACEMENTS
*
dtab1= tab.temps;
*
ndime= (dime dtab1) - 1 ;
*
* ----- EXTRACTION DES DIFFERENTES TABLES
*
*                                    DEPLACEMENTS
*
i=0 ;
repeter bloc1 ndime ;
   i=i+1 ;
   dep1=tab.deplacements.i ;
   deft=defo geotot dep1 1. ;
**   trac      deft ;
**   trac OEIL deft ;
   sig1=tab.contraintes.i ;
   var1=tab.variables_internes.i ;
   defp=tab.deformations_inelastiques.i ;
   eps1 = epsi modjoi dep1 ;
   epscu = epsi modcub dep1 ;
*
* ----- REDUCTION DES CHPOINTS A L'ELEMENT JOINT
*
*
*
*
DPTCU1 = REDU DEP1  VOLS  ;
SIGCU1 = REDU SIG1  VOLS  ;
DEFCU1 = REDU EPSCU VOLS ;
DPTCU2 = REDU DEP1  VOLI  ;
SIGCU2 = REDU SIG1  VOLI  ;
DEFCU2 = REDU EPSCU VOLI ;
DPTJOI = REDU DEP1 JOINT ;
SIGJOI = REDU SIG1 JOINT ;
DEFJOI = REDU EPS1 JOINT ;
DEPJOI = REDU DEFP JOINT ;
VARJOI = REDU VAR1 JOINT ;
*
mess '               '   ;
mess ' TEMPS  ' D ;
mess ' ____________  '   ;
mess '               '   ;
mess ' deplacements du cube 1  '   ;
list dptcu1 ;
mess '               '   ;
mess ' deplacements du cube 2  '   ;
list dptcu2 ;
mess '               '   ;
mess ' deplacements du joint   '   ;
list dptjoi ;
mess '               '   ;
mess ' contraintes du cube 1    '   ;
list sigcu1 ;
mess '               '   ;
mess ' contraintes du cube 2    '   ;
list sigcu2 ;
mess '               '   ;
mess ' contraintes du joint  '   ;
list sigjoi ;
mess '               '   ;
mess ' def totales du joint'   ;
list defjoi ;
mess '               '   ;
mess ' def inelasti. du joint '   ;
list depjoi ;
mess ' variables internes du joint'   ;
list varjoi ;
mess ' defo. elasti du cube du haut '   ;
list defcu1 ;
mess ' defo. elasti du cube du bas  '   ;
list defcu2 ;
*
*
DEP13 = EXTR DPTCU1 UX P3 ;
DIF13 = DEP13-0.1;
XDIF1 = ABS(DIF13);
    SI (XDIF1 <EG 1.E-4) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
DEP23 = EXTR DPTCU1 UZ P3 ;
DIF23 = DEP23-0.08;
ZDIF1 = ABS(DIF13);
    SI (ZDIF1 <EG 1.E-4) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
SMNJ  = EXTR SIGJOI SMN 1 1 1   ;
ASMN  = ABS(SMNJ) ;
    SI (ASMN <EG 1.E-4) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
SMZC  = EXTR SIGCU1 SMZZ 1 1 1   ;
DIFZ  = SMZC ;
ASZC  = ABS(DIFZ) ;
    SI (ASZC <EG 1.E-4) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
fin bloc1 ;
*
FIN;





































