* fichier :  cou21.dgibi
************************************************************************
* Section : Mecanique Contact
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 0 ;
SAUT PAGE ;
MESS '                                                       ';
MESS '                                                       ';
MESS '  TEST AVEC 2 CUBES ET UN JOINT  (2D)                  ';
MESS '  LE CUBE DU HAUT EST SOUMIS A UN DEPLACEMENT IMPOSE.  ';
MESS '  L EVOLUTION DU CHARGEMENT EST TELLE QUE LE JOINT     ';
MESS '  S OUVRE, PUIS SE REFERME, PUIS SE COMPRIME, ET ENFIN ';
MESS '  S OUVRE DE NOUVEAU                                   ';
MESS '  LE JOINT A AU DEPART UN JEU INITIAL                  ';
MESS '                                                       ';
MESS '                                                       ';
MESS '                                                       ';
MESS '               B _______________ C                     ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |  ^     ^    ^ |                      ';
MESS '                |__|_____|____|_|                      ';
MESS '               A|               |H                     ';
MESS '               G|_______________|D                     ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |               |                      ';
MESS '                |_______________|                      ';
MESS '               K                 F                     ';
MESS '                                                       ';
MESS '  JOINT FORME DES NOEUDS GDHA                          ';
MESS '  CONDITIONS AUX LIMITES  :                            ';
MESS '       - K F  BLOQUES EN UX ET EN UY                   ';
MESS '       - B C  BLOQUES EN UX ET EN UY                   ';
MESS '  CHARGEMENT              :                            ';
MESS '       - DEPLACEMENT IMPOSE SUR AH                     ';
MESS '                                                       ';
********************************************************************
*
OPTION DIME 2 ;
OPTION ELEM SEG2 MODE PLAN CONT ;
* ajout de option epsilon lineaire pour la precision des test!
OPTION epsilon lineaire;
*
* ----------DEFINITION DE LA GEOMETRIE DU CUBE ----------
*
A = 0.00  0.30 ;
B = 0.00  1.30;
C = 4.00  1.30 ;
H = 4.00  0.30 ;
K = 0.00 -1.00 ;
F = 4.00 -1.00 ;
*
G = 0.00 0.00 ;
D = 4.00 0.00 ;
*
* ----------                  LIGNE                  ----------
*
AB =  A DROIT 1 B ;
BC =  B DROIT 1 C ;
CH =  C DROIT 1 H ;
HA =  H DROIT 1 A ;
AH =  A DROIT 1 H ;
*
DG =  D DROIT 1 G ;
DF =  D DROIT 1 F ;
FK =  F DROIT 1 K ;
KG =  K DROIT 1 G ;
*
* ----------            MAILLAGE DU CUBE                 ----------
*
OPTION ELEM QUA4 MODE PLAN CONT ;
*
CUB1 = DALLER AB BC CH HA PLAN ;
CUB2 = DALLER KG DG DF FK PLAN ;
*  quelques definitions pour la verif finale
P4 = NOEUD 4 ;
P1 = NOEUD 1 ;
P8 = NOEUD 8 ;
P7 = NOEUD 7 ;
*
* ----------DEFINITION DE LA GEOMETRIE DU JOINT----------
*
* JOINT FORME DES NOEUDS H A D G
*
* ----------         MAILLAGE                ----------
*
OPTION ELEM RAC2 ;
*
JOINT = RACC 0.3001 HA DG ;
JOINT = RACC 0.3001 (INVE HA) ( INVE DG) ;
*
* ----------      MAILLAGE TOTAL               ----------
*
CUBE = CUB1 ET CUB2    ;
GEOTOT = CUBE ET JOINT ;
*trac geotot ;
*
* ----------     DEFINITION DES CONDITIONS AUX LIMITES   ----------
*
*
*
*                        CUBE
*
CLBX  = BLOQ B UX  ;
CLBY  = BLOQ B UY  ;
CLB   = CLBX ET CLBY ;
*
CLCX  = BLOQ C UX  ;
CLCY  = BLOQ C UY  ;
CLC   = CLCX ET CLCY ;
*
CLKY  = BLOQ K UY  ;
CLKX  = BLOQ K UX  ;
CLK   = CLKY ET CLKX ;
*
CLFY  = BLOQ F UY  ;
CLFX  = BLOQ F UX  ;
CLF   = CLFY ET CLFX ;
*
cl5  = bloq ha uy ;
dep5 = depi cl5 1.0 ;
*
CLCUBE = CLB ET CLC ET CLF ET CLK ET cl5 ;
*
* ----------       CONDITIONS AUX LIMITES TOTALES   ----------
*
CLTOT = CLCUBE ;
*
* ----------        DEFINITION DU MODELE DES CUBES       ----------
*
MODCUB = MODE CUBE 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' ;
MACUB  = MATE MODCUB YOUN 100.0 NU 0.000001  ;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MODJOI = MODE JOINT MECANIQUE ELASTIQUE PLASTIQUE COULOMB JOI2 ;
MAJOI  = MATE MODJOI KS   1000.0  KN   100.0   EF  150.0    ECN  0.05
                     COHE 2.0     FRIC 45.0    ;
*
* ---------- MODELE ET MATERIAU DE TOUTE LA STRUCTURE ----------
*
MODTOT = MODCUB ET MODJOI;
MATTOT = MACUB  ET MAJOI ;
*
* ----------        DEFINITION DU CHARGEMENT         ----------
*
   L51 = PROG 0.0  1.0  2.0   3.0   4.0 ;
   L52 = PROG 0.0  1.0  0.0  -0.5   1.0 ;
   EV5 = EVOL MANU X L51 Y L52 ;
   CAR = CHAR 'DIMP' DEP5 EV5;
*
* ----------            RESOLUTION            ----------
*
   TAB = TABLE ;
   LIS1 = PROG 0.0 PAS 0.5 4.0 ;
   DEFH = (MANU CHML MODCUB EIXX 0.0 EIYY 0.0 EIZZ 0.0 GIXY 0.0
           STRESSES TYPE 'DEFORMATIONS INELASTIQUES') ET
          (MANU CHML MODJOI FRSN 0.0 FRN 0.3
           STRESSES TYPE 'DEFORMATIONS INELASTIQUES') ;
   TAB.'DEFORMATIONS_INELASTIQUES' = TABLE;
   TAB.'DEFORMATIONS_INELASTIQUES' . 0 = DEFH;
   TAB.'BLOCAGES_MECANIQUES' = CLTOT;
   TAB.'MODELE' = MODTOT;
   TAB.'CHARGEMENT' = CAR;
   TAB.'TEMPS_CALCULES' = LIS1;
   TAB.'CARACTERISTIQUES' = MATTOT;
TMASAU=table;
tab . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
PASAPAS TAB ;
*
* ---------- POST TRAITEMENT ----------
*
*
* -----RECUPERATION DE LA TABLE DES DEPLACEMENTS
*
dtab1=tab.temps;
*
ndime=dime dtab1 ;
*
* ----- EXTRACTION DES DIFFERENTES TABLES
*
*                                    DEPLACEMENTS
*
i=0 ;
REPETER BOU1 (ndime-1) ;
   i=i+1 ;
   dep1=tab.deplacements.i ;
   sig1=tab.contraintes.i ;
   var1=tab.variables_internes.i ;
   defp=tab.deformations_inelastiques.i ;
   eps1 = epsi modjoi dep1 ;
*
* ----- REDUCTION DES CHPOINTS A L'ELEMENT JOINT
*
*
*
*
DPTCU1 = REDU DEP1 CUB1  ;
SIGCU1 = REDU SIG1 CUB1  ;
DPTCU2 = REDU DEP1 CUB2  ;
SIGCU2 = REDU SIG1 CUB2  ;
DPTJOI = REDU DEP1 JOINT ;
SIGJOI = REDU SIG1 JOINT ;
DEFJOI = REDU EPS1 JOINT ;
DEPJOI = REDU DEFP JOINT ;
VARJOI = REDU VAR1 JOINT ;
*
mess '               '   ;
mess ' PAS DE TEMPS  ' i ;
mess ' ____________  '   ;
mess '               '   ;
mess ' deplacements du cube 1  '   ;
*list dptcu1 ;
mess '               '   ;
mess ' deplacements du cube 2  '   ;
*list dptcu2 ;
mess '               '   ;
mess ' deplacements du joint   '   ;
*list dptjoi ;
mess '               '   ;
mess ' contraintes du cube 1    '   ;
*list sigcu1 ;
mess '               '   ;
mess ' contraintes du cube 2    '   ;
*list sigcu2 ;
mess '               '   ;
mess ' contraintes du joint  '   ;
*list sigjoi ;
mess '               '   ;
mess ' def totales'   ;
*list defjoi ;
mess '               '   ;
mess ' def inelasti. '   ;
*list depjoi ;
mess ' variables internes '   ;
*list varjoi ;
*
fin bou1 ;
*
DEP11 = EXTR DPTCU1 UY P1 ;
DEP14 = EXTR DPTCU1 UY P4 ;
DEP28 = EXTR DPTCU2 UY P8 ;
DEP27 = EXTR DPTCU2 UY P7 ;
*
SMY1  = EXTR SIGCU1 SMYY 1 1 1  ;
*
SMNJ  = EXTR SIGJOI SMN 1 1 1   ;
DEPJ  = EXTR DEPJOI FRN 1 1 1   ;
STAJ  = EXTR VARJOI STAT 1 1 1  ;

*
SI (EGA I 8)  ;
*
*   DEPLACEMENTS DU CUBE 1
*
 DIF11 = DEP11-1.0 ;
 DIF14 = DEP14-1.0 ;
 T11 = ABS(DIF11) ;
 T14 = ABS(DIF14) ;
    SI (( T11 <EG 1.E-4   ) ET ( T14 <EG 1.E-4   ));
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
 T28 = ABS(DEP28) ;
 T27 = ABS(DEP27) ;
    SI ( ( T28 <EG 1.E-4   ) ET ( T27  <EG 1.E-4 ));
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
*   CONTRAINTES DU CUBE 1
*
  DIS1 = SMY1+1.E2 ;
  TI1  = ABS(DIS1) ;
    SI ( TI1 <EG 1.E-4 ) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
*   CONTRAINTES DU JOINT
*
  DIJ1 = DEPJ-1.3 ;
  DIA1 = STAJ-1.0 ;
  TIM1 = ABS(SMNJ) ;
  TIJ1 = ABS(DIJ1) ;
  TIA1 = ABS(DIA1) ;
    SI ( ( TIM1 <EG 1.E-4 ) ET ( TIJ1 <EG 1.E-4 )
      ET ( TIA1 <EG 1.E-4 ) ) ;
      ERRE 0 ;
    SINON ;
      ERRE 5 ;
    FINSI ;
*
FINSI ;
fin;


















