* fichier :  fimpvf.dgibi
***********************************************************
**** APPROCHE VF "Cell-Centred Formulation" pour la    ****
**** solution des                                      ****
**** Equations d'Euler pour un gaz parfait             ****
**** OPERATEUR  FIMP                                   ****
**** Test de la gravité                                ****
****                                                   ****
**** A. BECCANTINI DEN/DM2S/SFME/LTMF   JANV  2003     ****
***********************************************************


 'OPTION'  'DIME' 2 'ELEM' QUA4 'ECHO'  0 ;

***********************
**** TEST1 ************
***********************
* 2D


 A1 = 0.0D0 0.0D0;
 A2 = 2.0D0 0.0D0;
 A3 = 3.0D0 0.0D0;
 A4 = 4.0D0 1.0D0;
 A5 = 1.0D0 1.0D0;
 A6 = 0.0D0 1.0D0;

 MAI1 = ('MANUEL' 'POI1' A1) 'ET'
        ('MANUEL' 'POI1' A2) 'ET'
        ('MANUEL' 'POI1' A3) 'ET'
        ('MANUEL' 'POI1' A4) 'ET'
        ('MANUEL' 'POI1' A5) 'ET'
        ('MANUEL' 'POI1' A6) ;

*
**** CHPOINTs definis sur la meme rapresentation du maillage
*

 RN    = 'BRUI' 'BLAN' 'UNIF' 1.24 0.5  MAI1 ;
 VNY   = ('NOMC' 'UY'
        ('BRUI' 'BLAN' 'UNIF' 2.24 7.5  MAI1)
        'NATU' 'DISCRET') ;
 VNX   = ('NOMC' 'UX'
        ('BRUI' 'BLAN' 'UNIF' 7.24 0.5  MAI1)
        'NATU' 'DISCRET') ;
 VN    = VNX 'ET' VNY ;
 GN    = RN '*' VN ;

 GY   = ('NOMC' 'UY'
        ('BRUI' 'BLAN' 'UNIF' 1.24 7.5  MAI1)
        'NATU' 'DISCRET') ;
 GX   = ('NOMC' 'UX'
        ('BRUI' 'BLAN' 'UNIF' 0.24 0.5  MAI1)
        'NATU' 'DISCRET') ;

 GRAV  = GX 'ET' GY ;

 LISTINCO = 'MOTS' 'RN' 'RUX' 'RUY' 'RET' ;

 RES  = ('NOMC' ('MOTS' 'UX' 'UY') (RN * GRAV) ('MOTS' 'RUX' 'RUY')
         'NATU' 'DISCRET') 'ET'
        ('NOMC' 'RET' ('PSCAL' GN GRAV ('MOTS' 'UX' 'UY')
         ('MOTS' 'UX' 'UY')) 'NATU' 'DISCRET') ;

 RES1 = 'FIMP' 'VF' 'GRAVMONO' 'RESI' LISTINCO RN GN GRAV ;

 ERRES  = ('MAXIMUM' (RES '-' RES1) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

* Jacobienne

 JAC = 'FIMP' 'VF' 'GRAVMONO' 'JACOCONS' LISTINCO RN GN GRAV ;
 CHPO1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RN' 1.0 ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 2 'RUX'
     ('EXTRAIRE' GRAV A4 'UX') 'RUY'
     ('EXTRAIRE' GRAV A4 'UY') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

 CHPO1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RUX' 1.0 '+'
  ('MANUEL' 'CHPO' MAI1  1 'RUX' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RET'
     ('EXTRAIRE' GRAV A4 'UX') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

 CHPO1 = ('MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RUY' 1.0) '+'
  ('MANUEL' 'CHPO' MAI1  1 'RUY' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RET'
     ('EXTRAIRE' GRAV A4 'UY') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5 ;
 'FINSI' ;

***********************
**** TEST2 ************
***********************
*
*** 3D
*

 'OPTION'  'DIME' 3 'ELEM' 'CUB8' 'ECHO'  1 ;

*
**** GRAPH
*

 GRAPH = FAUX ;
* GRAPH = VRAI ;

 A1 = 0.0D0 0.0D0 0.0 ;
 A2 = 2.0D0 0.0D0 0.0 ;
 A3 = 3.0D0 0.0D0 0.0 ;
 A4 = 4.0D0 1.0D0 0.0 ;
 A5 = 1.0D0 1.0D0 0.0 ;
 A6 = 0.0D0 1.0D0 0.0 ;

 MAI1 = ('MANUEL' 'POI1' A1) 'ET'
        ('MANUEL' 'POI1' A2) 'ET'
        ('MANUEL' 'POI1' A3) 'ET'
        ('MANUEL' 'POI1' A4) 'ET'
        ('MANUEL' 'POI1' A5) 'ET'
        ('MANUEL' 'POI1' A6) ;

*

 RN    = 'BRUI' 'BLAN' 'UNIF' 1.24 0.5  MAI1 ;
 VNY   = ('NOMC' 'UY'
        ('BRUI' 'BLAN' 'UNIF' 4.24 7.5  MAI1)
        'NATU' 'DISCRET') ;
 VNX   = ('NOMC' 'UX'
        ('BRUI' 'BLAN' 'UNIF' 0.24 0.5  MAI1)
        'NATU' 'DISCRET') ;
 VNZ   = ('NOMC' 'UZ'
        ('BRUI' 'BLAN' 'UNIF' 1.74 0.5  MAI1)
        'NATU' 'DISCRET') ;
 VN    = VNX 'ET' VNY 'ET' VNZ ;
 GN    = RN '*' VN ;

 GY   = ('NOMC' 'UY'
        ('BRUI' 'BLAN' 'UNIF' 3.24 7.5  MAI1)
        'NATU' 'DISCRET') ;
 GX   = ('NOMC' 'UX'
        ('BRUI' 'BLAN' 'UNIF' 2.24 0.5  MAI1)
        'NATU' 'DISCRET') ;
 GZ   = ('NOMC' 'UZ'
        ('BRUI' 'BLAN' 'UNIF' 0.24 0.5  MAI1)
        'NATU' 'DISCRET') ;

 GRAV  = GX 'ET' GY 'ET' GZ ;

 LISTINCO = 'MOTS' 'RN' 'RUX' 'RUY' 'RUZ' 'RET' ;

 RES  = ('NOMC' ('MOTS' 'UX' 'UY' 'UZ') (RN * GRAV)
        ('MOTS' 'RUX' 'RUY' 'RUZ')
         'NATU' 'DISCRET') 'ET'
        ('NOMC' 'RET' ('PSCAL' GN GRAV ('MOTS' 'UX' 'UY' 'UZ')
         ('MOTS' 'UX' 'UY' 'UZ')) 'NATU' 'DISCRET') ;

 RES1 = 'FIMP' 'VF' 'GRAVMONO' 'RESI' LISTINCO RN GN GRAV ;

 ERRES  = ('MAXIMUM' (RES '-' RES1) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

* Jacobienne

 JAC = 'FIMP' 'VF' 'GRAVMONO' 'JACOCONS' LISTINCO RN GN GRAV ;

 CHPO1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RN' 1.0 '+'
  ('MANUEL' 'CHPO' MAI1  1 'RN' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 3 'RUX'
     ('EXTRAIRE' GRAV A4 'UX') 'RUY'
     ('EXTRAIRE' GRAV A4 'UY') 'RUZ'
     ('EXTRAIRE' GRAV A4 'UZ')  ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

 CHPO1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RUX' 1.0 '+'
  ('MANUEL' 'CHPO' MAI1  1 'RUX' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RET'
     ('EXTRAIRE' GRAV A4 'UX') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5;
 'FINSI' ;

 CHPO1 = ('MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RUY' 1.0) '+'
  ('MANUEL' 'CHPO' MAI1  1 'RUY' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RET'
     ('EXTRAIRE' GRAV A4 'UY') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  31 ;
 'FINSI' ;

 CHPO1 = ('MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RUZ' 1.0) '+'
  ('MANUEL' 'CHPO' MAI1  1 'RUZ' 0.0) ;

 CHPRE = 'KOPS' 'MULT' JAC CHPO1 ;

 CHPRE1 = 'MANUEL' 'CHPO' ('MANUEL' 'POI1' A4) 1 'RET'
     ('EXTRAIRE' GRAV A4 'UZ') ;

 ERRES  = ('MAXIMUM' (CHPRE1 '-' CHPRE) 'ABS') ;

 'SI' (ERRES '>' 1.0D-12);
    'ERREUR'  5 ;
 'FINSI' ;

 'FIN' ;


