* fichier :  joi42.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
OPTI ECHO 0 ;
SAUT PAGE ;
*===================================================================*
*                                                                   *
*                          TEST JOI42                               *
*                                                                   *
*                ESSAI DE TRACTION SUR UN JOINT 3D                  *
*                                                                   *
*      Un joint 3D JOI4 a sa surface inferieure encastree. Sa       *
*      surface superieure est libre. Un effort de traction est      *
*      exercee sur sa surface superieure.                           *
*                                                                   *
*      Force totale exercee sur la surface superieure = 100000      *
* ===> Force repartie sur les quatre noeuds de la surface =         *
*            100000 / 4 = 25000                                     *
*                                                                   *
*      Solution analytique :                                        *
*                                                                   *
*         En ecrivant le principe des travaux virtuels, on montre   *
*      que le deplacement delta en chaque noeud est egal, dans le   *
*      cas d'un essai de traction uniaxial, a :                     *
*                           F                                       *
*            delta = ----------------                               *
*                       k * L * l                                   *
*      ou                                                           *
*            F = force totale exercee sur la structure              *
*            k = raideur                                            *
*            L = longueur de l'element joint                        *
*            l = largeur de l'element joint                         *
*                                                                   *
*      Application numerique :                                      *
*                                                                   *
*            F = 100000.0                                           *
*            k = 4.2E10                                             *
*            L = 2.0                                                *
*            l = 2.0                                                *
*                                                                   *
* ===>       Delta   =  100000 / (4.2E10 * 2.0 * 2.0)               *
*                    =  5.95238E-7                                  *
*                                                                   *
*===================================================================*
OPTION DIME 3 ;
OPTION ELEM QUA4 MODE TRID  ;
*
SOLANA = 5.95238E-7 ;
SOLNUL = 0.0 ;
*
* ---------- DEFINITION DE LA SURFACE TOP DU JOINT ----------
*
A1 = 0.00 0.00 0.00 ;
B1 = 2.00 0.00 0.00 ;
C1 = 2.00 2.00 0.00 ;
D1 = 0.00 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
O1 = 0.00 0.00 10.00 ;
H1 = A1 DROIT 1 B1 ;
H2 = B1 DROIT 1 C1 ;
H3 = C1 DROIT 1 D1 ;
H4 = D1 DROIT 1 A1 ;
*
L1 = H1 ET H2 ET H3 ET H4 ;
S1 = SURF L1 PLAN ;
*
* ---------- DEFINITION DE LA SURFACE BOT DU JOINT ----------
*
IA1 = 0.00 0.00 0.00 ;
IB1 = 2.00 0.00 0.00 ;
IC1 = 2.00 2.00 0.00 ;
ID1 = 0.00 2.00 0.00 ;
*
* ----------          MAILLAGE          ----------
*
IH1 = IA1 DROIT 1 IB1 ;
IH2 = IB1 DROIT 1 IC1 ;
IH3 = IC1 DROIT 1 ID1 ;
IH4 = ID1 DROIT 1 IA1 ;
*
IL1 = IH1 ET IH2 ET IH3 ET IH4 ;
IS1 = SURF IL1 PLAN ;
*
* ----------   CREATION DU JOINT JOI4   ----------
*
OPTION ELEM LIA4 ;
VOL = LIAISON 0.00001 S1 IS1 ;
*
* ---------- DEFINITION DES CONDITIONS LIMITES ----------
*
CL11 = BLOQ IA1 UX ;
CL12 = BLOQ IA1 UY ;
CL13 = BLOQ IA1 UZ ;
CL1 = CL11 ET CL12 ET CL13 ;
*
CL21 = BLOQ IB1 UX ;
CL22 = BLOQ IB1 UY ;
CL23 = BLOQ IB1 UZ ;
CL2 = CL21 ET CL22 ET CL23 ;
*
CL31 = BLOQ IC1 UX ;
CL32 = BLOQ IC1 UY ;
CL33 = BLOQ IC1 UZ ;
CL3 = CL31 ET CL32 ET CL33 ;
*
CL41 = BLOQ ID1 UX ;
CL42 = BLOQ ID1 UY ;
CL43 = BLOQ ID1 UZ ;
CL4 = CL41 ET CL42 ET CL43 ;
*
CL = CL1 ET CL2 ET CL3 ET CL4 ;
*
* ----------         DEFINITION DU MODELE DU JOINT      ----------
*
MOD1 = MODE VOL 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE' JOI4 ;
MA1 = MATE MOD1 KS 4.2E10 KN 4.2E10 ;
*
* ----------       MATRICE DE RIGIDITE        ----------
*
RI1 = RIGI MOD1 MA1 ;
RI2 = RI1 ET CL ;
*
* ----------        FORCE DE TRACTION         ----------
*
FO1 = FORCE ( 0. 0. 100000.0 ) S1 ;
*
* ----------            RESOLUTION            ----------
*
RE = RESO RI2 FO1 ;
MESS '                                                             ' ;
MESS ' ___________________________________________________________ ' ;
MESS '!                                                           !' ;
MESS '!                       JOINT 3D  JOI4                      !' ;
MESS '!                                                           !' ;
MESS '!   - Surface inferieure du joint encastree.                !' ;
MESS '!   - Surface superieure du joint libre    .                !' ;
MESS '!   - Force de traction exercee sur la surface superieure   !' ;
MESS '!     perpendiculairement au plan du joint dans la          !' ;
MESS '!     direction z                                           !' ;
MESS '!                                                           !' ;
MESS '!                                                           !' ;
MESS '!     Solution analytique :                                 !' ;
MESS '!                                                           !' ;
MESS '!     En ecrivant le principe des travaux virtuels, on      !' ;
MESS '!montre, sous certaines hypotheses, que le deplacement delta!' ;
MESS '!en chaque noeud, dans la direction de l essai uniaxial, est!' ;
MESS '!egal a :                                                   !' ;
MESS '!                     F                                     !' ;
MESS '!      delta = ----------------                             !' ;
MESS '!                 k * L * l                                 !' ;
MESS '! ou                                                        !' ;
MESS '!     F = force totale exercee sur la structure             !' ;
MESS '!     k = raideur                                           !' ;
MESS '!     L = longueur de l element joint                       !' ;
MESS '!     l = largeur de l element joint                        !' ;
MESS '!                                                           !' ;
MESS '! Application numerique :                                   !' ;
MESS '!                                                           !' ;
MESS '!     F = 100000.0                                          !' ;
MESS '!     k = 4.2E10                                            !' ;
MESS '!     L = 2.0                                               !' ;
MESS '!     l = 2.0                                               !' ;
MESS '!                                                           !' ;
MESS '!      delta   =  100000 / (4.2E10 * 2.0 * 2.0)             !' ;
MESS '!              =  5.95238E-7                                !' ;
MESS '!___________________________________________________________!' ;
MESS '                                                             ' ;
MESS '                                                             ' ;
MESS ' Solution Analytique :'                                        ;
MESS '                                                             ' ;
MESS '    UX =' SOLNUL  ;
MESS '    UY =' SOLNUL  ;
MESS '    UZ =' SOLANA  ;
MESS '                                                             ' ;
MESS '                                                             ' ;
MESS '                                                             ' ;
MESS ' Solution Calculee :'                                          ;
MESS '                                                             ' ;
LIST RE ;
*
* ----------        CODE DE FONCTIONNEMENT          ----------
*
DEPA1 = EXTR RE UZ A1 ;
RESI = ABS( (DEPA1-SOLANA)/SOLANA ) ;
*
SI (RESI <EG 1E-4 ) ;
   ERRE  0 ;
SINO;
   ERRE  5 ;
FINSI ;
*
* ----------        CALCUL DES DEFORMATIONS         ----------
*
EPS1 = EPSI MOD1 RE ;
LIST EPS1 ;
*
* ----------        CALCUL DES CONTRAINTES         ----------
*
SIG1 = SIGMA MOD1 MA1 RE ;
LIST SIG1 ;
*
FIN ;









