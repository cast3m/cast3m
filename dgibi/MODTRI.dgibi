**********************************************
* EXEMPLE MODTRI.dgibi
**********************************************
* Entrée :      Sans objet

**********************************************
* Sortie :      Sans objet
*
**********************************************
* Commentaire : Test de la procedure
*               @MODTRI.PROCEDUR
**********************************************
* Developpeur : Benjamin Richard
*               CEA, DEN, DANS, DM2S, SEMT, EMSI
*               benjamin.richard@cea.fr
**********************************************
GRAPH0 = FAUX;

* Parametres materiaux
* TAB1 . 1 = K1 -- pente elastique
* TAB1 . 2 = K2 -- pente endommagee
* TAB1 . 3 = K3 -- pente plastique
* TAB1 . 4 = X0 -- seuil 1
* TAB1 . 5 = X1 -- seuil 2
*
TAB1 = TABLE;
TAB1 . 1 = 2.822E6;
TAB1 . 2 = 1.4536E6;
TAB1 . 3 = 4.7975E4;
TAB1 . 4 = 0.0022;
TAB1 . 5 = 0.024;

* Initialisation des donnees
* DATA . 1 = DPLUS -- endommagement +
* DATA . 2 = DMOIN -- endommagement -
* DATA . 3 = DJ    -- deplacement
* DATA . 4 = MAXDP -- deplacement max
* DATA . 5 = MAXDM -- deplacement min
*
DATA = TABLE;
DATA . 1 = 0.0;
DATA . 2 = 0.0;
DATA . 3 = 0.0;
DATA . 4 = 0.0;
DATA . 5 = 0.0;

* Creation de la liste de chargement
LIDJ1 = PROG 0 PAS 0.0001 0.05;
LIDJ2 = PROG 0.0499 PAS -0.0001 -0.05;
LIDJ3 = PROG -0.0499 PAS 0.0001 0;

LIDJ = LIDJ1 ET LIDJ2 ET LIDJ3;

FY = PROG;
DY = PROG;

* Boucle sur les pas de deplacement
REPE BOU1 ((DIME LIDJ) - 1);

I = &BOU1;

DJ = EXTR LIDJ I;

DATA . 3 = DJ;

* Appel a la loi constitutive
**********************************************
TAB3 = @MODTRI TAB1 DATA;
**********************************************

* Mise a jour des variables internes
DATA . 1 = TAB3 . 2;
DATA . 2 = TAB3 . 3;

DATA . 4 = TAB3 . 4;
DATA . 5 = TAB3 . 5;

FO1 = TAB3 . 1;

FY = FY ET (PROG FO1);
DY = DY ET (PROG DJ);

FIN BOU1;

* Construction de l evolution et visualisation
EV1 = EVOL MANU DY FY;

SI GRAPH0;
DESS EV1;
FINSI;

ABX = EXTR EV1 'ABSC';
ORD = EXTR EV1 'ORDO';

ORD50 = EXTR ORD 50;

SI (ABS (ORD50 - 10133.0) > 1.0E0);
ERREUR 5;
FINSI;


FIN;







