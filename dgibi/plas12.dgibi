* fichier :  plas12.dgibi
************************************************************************
* Section : Thermique Mecanique
* Section : Thermique Conduction
* Section : Mecanique Plastique
************************************************************************
*           Test Plas12.dgibi: Jeux de données        *
*           ---------------------------------         *
*                                                     *
GRAPH='N';
SAUT PAGE ;
*
*******************************************************
* TEST PLAS12
*
* Un tuyauderie encastré en deux extremités
* soumis à un choc thermique.
*
* GEOMETRIE : Longueur du tuyau l         : 1000. mm
*             Rayon extérieure du tuyau r : 10.   mm
*             Epaisseur du tuyau e        : 100.  mm
* MATERIAU  : Module d'Young E            : dépendant
*                                           de la
*                                           température
*             Coeficient de Poisson Nu    : 0.3
*             dilatation thermique Alpha  : dépendant
*                                           de la
*                                           température
*             Courbe de traction          : dépendant
*                                           de la
*                                           température
* CHARGEMENT: Une tempérarure T uniforme passant
*             de 0°C à 800°C
*
*     encastrement                         encastrement
*         |-------------------------------------|
*         p1        == CHOC THERMIQUE ==        p2
*
*******************************************************
* Objectif du test:
* Verifier la procédure PASAPAS avec un matériau
* dépendant de la température.
*******************************************************
* Validation du calcul:
* On compare la contrainte de PASAPAS avec la solution
* exacte : EPSILLON = Alpha * (Delta T)
*          SIGMA = Interpolation de la valeur EPSILLON
*                  sur la courbe de traction
*******************************************************
*******************************************************
* procédure calculant le module D'Young avec une courbe
* de traction
     DEBP CALYO EV1*EVOLUTION;
       X1=EXTR EV1 ABSC;
       Y1=EXTR EV1 ORDO;
       YOU1=(EXTR Y1 2)/(EXTR X1 2);
     FINP YOU1;
*******************************************************
*
* 1) option de calcul
     opti dime 3 elem seg2;
*
* 2) données géométriques
     l=1000.; e = 10; r = 100.;
*
* 3) données de caractéristiques du matériau
**** 3.1) courbe de traction en fonction de la
****      température
     TITR 'Courbe de traction en fonction de la  température';
     progx = prog 0. 0.001785 0.00386 0.00701 0.00984 0.0132 0.0167 0.0259 0.0491 0.0994;
     progy = prog 0. 350.23 470.05 553.73 610.14 635.94 663.59 717.97 795.39 877.42;
     ev1 = evol manu eps progx sig (progy*1.0);
     ev2 = evol manu eps progx sig (progy*0.8);
     ev3 = evol manu eps progx sig (progy*0.6);
     ev4 = evol manu eps progx sig (progy*0.4);
     ev5 = evol manu eps progx sig (progy*0.2);
     tra = nuage comp T    0.  200. 400. 600. 800. comp TRAC ev1 ev2  ev3  ev4  ev5;
     SI (NEG GRAPH 'N');
       TAB1=TABL;
       TAB1.1 = 'MARQ LOSA ';
       TAB1.2 = 'MARQ TRIB ';
       TAB1.3 = 'MARQ CROI ';
       TAB1.4 = 'MARQ TRIA ';
       TAB1.5 = 'MARQ CARR ';
       TAB1.'TITRE'=TABL;
       TAB1.'TITRE'. 1 = MOT 'T=800.C';
       TAB1.'TITRE'. 2 = MOT 'T=600.C';
       TAB1.'TITRE'. 3 = MOT 'T=400.C';
       TAB1.'TITRE'. 4 = MOT 'T=200.C';
       TAB1.'TITRE'. 5 = MOT 'T=0.C';
       DESS (ev5 et ev4 et ev3 et ev2 et ev1) lege tab1;
     FINSI;
**** 3.2) Module D'Young en fonction de la température
     TITR 'Module d Young en fonction de la  température';
     y1=calyo ev1;
     y2=calyo ev2;
     y3=calyo ev3;
     y4=calyo ev4;
     y5=calyo ev5;
     evy = evol manu T   (prog 0.  200. 400. 600. 800.) YOUN (prog y1  y2   y3   y4   y5);
     SI (NEG GRAPH 'N'); DESS evy; FINSI;
**** 3.3) Alpha en fonction de la température
     TITR 'Dilatation thermique en fonction de la  température';
     eva = evol manu T  (prog 0.   200. 400. 600. 800.) ALPH ((prog 10.4 11.9 13.0 13.8 14.25)*8E-6);
     SI (NEG GRAPH 'N'); DESS eva; FINSI;
**** 3.4) la section du tuyau
     s = pi*((r*r) - ((r - e)*(r - e)));
* 4) maillage
     TITR 'Tuyauderie encastré soumis à un choc thermique';
     p1 = 0 0 0; p2 = L 0 0;
     d1= p1 d 10 p2;
     SI (NEG GRAPH 'N'); TRAC D1; FINSI;
*
* 5) Température de référence
     TREFE = MANU CHPO d1 1 T 0.;
*
* 6) modele, materiaux et caracteristiques geometriques
     modti=MODE d1 mecanique elastique plastique  isotrope tuya;
     mater=MATE modti 'YOUN' evy 'NU' 0.3 'ALPH' eva  'TRAC' tra;
     carac= 'CARA' modti 'EPAI' e 'RAYO' r 'CISA' 1. ;
     mc=mater et carac;
*
* 6) bloquage
     bloqtot=bloq depl rota (p1 et p2) ;
*
* 8) chargement thermique
     TBTEMPE = TABLE;
     TBTEMPE.0 =TREFE;
     TBTEMPE.1 =MANU CHPO d1 1 T 200.;
     TBTEMPE.2 =MANU CHPO d1 1 T 400.;
     TBTEMPE.3 =MANU CHPO d1 1 T 600.;
     TBTEMPE.4 =MANU CHPO d1 1 T 800.;
     TEMP = TABLE;
     TEMP.0 = 0.;
     TEMP.1 = 2.;
     TEMP.2 = 4.;
     TEMP.3 = 6.;
     TEMP.4 = 8.;
     EVT = EVOL MANU TEMPS (prog 0. 2.   4.   6.   8.)  T   (prog 0. 200. 400. 600. 800.);
     CH2 = CHAR T TEMP TBTEMPE;
*
* 9) chargement mecanique
     f1 = forc (0. 0. 0.) p1;
     ch1 = char meca f1 (evol manu (prog 0. 10.)  (prog 0. 0.));
     chat = ch2 et ch1;
*
* 10) calcul nonlinéaire
     TAB1 = TABLE;
     TAB1.'TEMPERATURE_REFERENCE'=TREFE;
     TAB1.'MODELE' = modti;
     TAB1.'CARACTERISTIQUES' = MC;
     TAB1.'BLOCAGES_MECANIQUES' = BLOQTOT;
     TAB1.'TEMPS_CALCULES' = (prog 0. pas 1. 8.);
     TAB1.'CHARGEMENT' = CHAT;
     PASAPAS TAB1;
*
* 11) comparaison et message
     TBEVOL=TABL;
     TBEVOL.1=(EV1 + EV2)/2.;
     TBEVOL.2=EV2;
     TBEVOL.3=(EV2 + EV3)/2.;
     TBEVOL.4=EV3;
     TBEVOL.5=(EV3 + EV4)/2.;
     TBEVOL.6=EV4;
     TBEVOL.7=(EV4 + EV5)/2.;
     TBEVOL.8=EV5;
     TBCONT=TAB1.CONTRAINTES;
     PCALCUL=PROG; PANALYT=PROG; PEPSI=PROG;
     REPETER B1 ((DIME TBCONT) - 1);
       V1 = (EXTR (TBCONT.(&B1)) EFFX 1 1 1)/S;
       PCALCUL=PCALCUL ET (PROG (ABS V1));
       T1 = IPOL (EXTR EVT ABSC) (EXTR EVT ORDO)  (TAB1.TEMPS.&B1);
       Y1 = IPOL (EXTR EVY ABSC) (EXTR EVY ORDO) T1;
       A1 = IPOL (EXTR EVA ABSC) (EXTR EVA ORDO) T1;
       PEPSI=PEPSI ET (PROG (A1*T1));
       EVC=TBEVOL.&B1;
       V2 = IPOL (EXTR EVC ABSC) (EXTR EVC ORDO) (A1*T1);
       PANALYT=PANALYT ET (PROG V2);
     FIN B1;
     ECALCUL=EVOL ROUG MANU 'Epsillon' PEPSI  'Sigma(Mpa)' PCALCUL;
     EANALYT=EVOL TURQ MANU 'Epsillon' PEPSI  'Sigma(Mpa)' PANALYT;
     TAB1 = TABL;
     TAB1.1 = 'MARQ LOSA ';
     TAB1.2 = 'MARQ TRIB ';
     TAB1.'TITRE' = TABL;
     TAB1.'TITRE'. 1 = MOT 'CASTEM2000';
     TAB1.'TITRE'. 2 = MOT 'Analytique';
     SI (NEG GRAPH 'N');
       DESS (ECALCUL et EANALYT) lege tab1;
     FINSI;
     ENEANA=EXTR 1 (INTG EANALYT);
     ENEK20=EXTR 1 (INTG ECALCUL);
     ERR1=ABS ((ENEANA - ENEK20)/ENEANA);
     mess '***********************';
     mess 'Energie de déformation cumulée';
     mess 'k2000,theorie,erreur:  ' ENEK20 ENEANA ERR1;
     mess '***********************';
     si (ERR1 > 5.5d-3);
       erre 5;
     sinon;
       erre 0;
     finsi;
     fin;

















