* fichier :  eauacti.dgibi
 SAUT PAGE ;
********************************************************************
*  Test de bon fonctionnement des operateurs LOGK COAC FION et NEUT
*               eau + temperature  80.   + pression CO2
*********************************************************************
OPTION DIME 2 ;
A= 0. 0. ;
B= 5. 0. ;
OPTION  ELEM QUA4 ;
 AB= A DROIT 1 B ;
 MT = CHANGE AB POI1  ;
  XX YY = COOR MT ;
*
*                table de données pour CHI1
*
TABDON=TABLE ;
TABDON.BDD= 'STRASBG' ;
TABDON.IDEN= LECT 1 50 60 61 101 165 ;
 TABDON.CHXMX= LECT 2144 2148 2157 2166 2168 2192 2198 2200
    2224 2231 2249 2278 2281 2372 ;
 TABCLIM=TABLE ;
 TABCLIM.TYP6= LECT 2002 ;
 TABCLIM.TYP3= LECT 2400 ;
 TABCLIM.COMP3= LECT 101 ;
 TABDON.CLIM=TABCLIM ;
 TABDON.TEMPERATURE = 'OUI' ;
*
   TB1=CHI1 TABDON
  LOGK '../divers/COMPSM'
  ENTH  '../divers/COMPSM'
  COMP  '../divers/COMPSM' ;
*
*
*                   table de données pour CHI2
*
 TBPARM= TABLE ;
 TBPAR2= TABLE ;
 TBPAR2.'SOUSTYPE'= 'DONNEES_CHIMIQUES' ;
 TBPARM.ITMAX = 95;
 TBPARM.ITERSOLI  = 10 ;
 TBPARM.NFI  = 8;
 TBPARM.EPS= 1.D-4 ;
 TBPAR2.LOGC= MANU CHPO MT 6 X001 -3.6 X050 -6.0 X060 -5.0
              X061 -3. X101 -6.5  X165 -6.5  ;
  TOT001= MANU CHPO MT 1 X001  6.D-4 ;
  TOT050= MANU CHPO MT 1 X050  -1.15D-3  ;
  TOT060= MANU CHPO MT 1 X060  0.D0 ;
  TOT061= MANU CHPO MT 1 X061  2.8D-2 ;
  TOT101= MANU CHPO MT 1 X101  0.D0 ;
  TOT165= MANU CHPO MT 1 X165  5.5D-5 ;
 TBPAR2.TOT= TOT001+ TOT050 + TOT060 + TOT061 + TOT101 + TOT165 ;
*
*     définition du champ de temperature
*
  TMPE= MANU CHPO MT 1 SCAL 80. ;
  TBPAR2.TEMPE= TMPE ;
*
*
*  KCO2= MANU CHPO MT 1 W016 20.45 ;
   KCO2= MANU CHPO MT 1 W016 2.32 ;
  TBPAR2.CLIM= KCO2 ;
 TBPARM.SORTIE= MOTS 'PREC' 'FION' 'TYP5' 'SOLU' ;
 TBPARM.IMPRIM= LECT 1   ;
*
* option impi 1 ;
 TB3= CHI2 TB1 TBPARM TBPAR2 ;
  option impi 0 ;
*
*                  Opérateur LOGK
*
  ONI= TB3.FION ;
 LLK= LOGK TB1  FORCEIONI ONI TEMPERATURE TMPE ;
*  LIST LLK ;
*
*                 Verification du résultat
*    Les valeurs LKREF sont celles figurant dans la colonne LOGK
*       dans le tableau sorti par CHIMI2 avec l'option  impi 1
*
 LKREF = MANU CHPO MT 32  W001 0.  W002 0. W003 0.  W004 0.  W005  0.
 W006 0. W007  14.799 W008 1.41924 W009  3.5127 W010 12.063
 W011 -8.982 W012 12.103 W013 6.6868 W014 10.049 W015 16.374
 W016 18.266 W017 12.564  W018  12.793 W019 8.871 W020  3.2849
 W021  33.481 W022 38.633 W023  27.960  W024  32.431 W025 41.452
 W026 29.842 W027 2.4878 W028 12.715 W029  42.9798 W030 47.0985
 W031  26.582 W032 18.491   'NATURE' DISCRET ;
 VERLK=( ABS( LLK-LKREF)) MASQ SUPERIEUR  0.01 SOMME  ;
 LIST VERLK ;
 SI ( VERLK EGA 0) ;
     ERRE 0 ;
 SINO ;
     ERRE 5 ;
 FINSI ;
*
*                   Opérateur NEUT
*
 CHA= NEUT TB1 ( TB3.SOLU ) ;
*LIST CHA ;
*
*                  Vérification du résultat
*      Les valeurs de référence pour NCAT et NANI sont les valeurs
*    de cations et anions dans le tableau imprimé par CHIMI2 avec
*    l'option impi 1
*
 NCAT= EXTR CHA  'CATI' ((EXTR  CHA MAIL) POINT 1) ;
 NANI= EXTR CHA  'ANIO' ((EXTR  CHA MAIL) POINT 1) ;
 LIST NANI ;
 SI (ABS(NANI + 0.001136 ) > 0.000001) ;
     ERRE 5 ;
 FINSI ;
 LIST NCAT ;
 SI (ABS(NCAT - 0.001131 ) > 0.000001) ;
     ERRE 5 ;
 FINSI ;
*
*                Opérateur FION
*
 FIO = FION TB1 ( TB3.SOLU ) ;
*LIST FIO ;
*
*              vérification du résultat
*
 FFF= RESU( ABS(TB3.FION - FIO)) ;
 VFF=  EXTR FFF 'SCAL' ((EXTR FFF MAIL) POINT 1) ;
 LIST VFF ;
 SI (VFF > 1.D-10) ;
      ERRE 5 ;
 FINSI ;
*
*                Opérateur COAC
*
 ACT = COAC TB1 FORCEIONI ONI TEMPERAT TMPE ;
*LIST ACT ;
 VAC=  EXTR ACT 'SCAL' ((EXTR ACT MAIL) POINT 1) ;
 LIST VAC ;
 SI (ABS(VAC + 0.02229 ) > 0.0001) ;
     ERRE 5 ;
 FINSI ;
*
*
*
fin ;






