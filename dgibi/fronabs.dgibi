* fichier :  fronabs.dgibi
************************************************************************
* Section : Mecanique Interaction Fluide Structure
************************************************************************
******************************************************************
*
*  test des frontieres absorbantes
*
*  on teste la reultante pour un champ de vitesses donne
*
*
***************************************************************

option dime 3 elem cub8;

FRA    = 0.4;
MFR    = -1 * FRA;
RAC    = (2. ** 0.5) / 2. ;
MRA    = -1 * RAC ;

P0     = 0.  0.  0. ;
P1a    = FRA 0.  0. ;
P1     = 1.  0.  0. ;
P2a    = 0.  0. MFR;
P2     = 0.  0. -1. ;
P3     = FRA 0. MFR ;
P4     = RAC 0. MRA ;


* Plus NBRAY est grand et plus la solution numérique converge vers la
* solution analytique.
NBRAY  = 6;
D1a    = P0  DROI NBRAY P1a ;
D1b    = P1a DROI NBRAY P1  ;
D2a    = P0  DROI NBRAY P2a ;
D2b    = P2a DROI NBRAY P2  ;
D2c    = P1a DROI NBRAY P3  ;
D1c    = P2a DROI NBRAY P3  ;
D3     = P3  DROI NBRAY P4  ;

C1     = CERC NBRAY P2 P0 P4 ;
C2     = CERC NBRAY P1 P0 P4 ;

SUR1   = REGL NBRAY D2a D2c  ;
SUR2   = REGL NBRAY D2c C2   ;
SUR3   = REGL NBRAY D1c C1   ;
ELIM 1.D-6 SUR1 SUR2 ;
ELIM 1.D-6 SUR1 SUR3 ;
ELIM 1.D-6 SUR2 SUR3 ;

SUR1   = SUR1 ET SUR2 ET SUR3;

NBROT  = NBRAY * 2 ;
SOL1   = SUR1 VOLU NBROT ROTA 90. ( 0.0 0.0 0.0 ) ( 0.0 0.0 1.0 ) ;
ELIM 1.e-6    SOL1 ;
SOL1   = REGE SOL1 ;

*TRAC QUAL SOL1 cach;

C1     = C1 ET C2 ;
BOR1   = C1 ROTA NBROT 90. ( 0.0 0.0 0.0 ) ( 0.0 0.0 1.0 ) ;
BOR1   = REGE BOR1   ;
ELIM 1.e-6 BOR1 SOL1 ;

MOD_S  = MODE SOL1 MECANIQUE                    ;

E      = 1. ;
POIS   = 0. ;
RO     = 1. ;
MAT_S1 = MATE MOD_S 'YOUN' E 'NU' POIS 'RHO' RO ;
AMOT1  = AMOR MOD_S BOR1 MAT_S1                 ;

E      = 2. ;
POIS   = 0. ;
RO     = 1. ;
MAT_S2 = MATE MOD_S 'YOUN' E 'NU' POIS 'RHO' RO ;
AMOT2  = AMOR MOD_S BOR1 MAT_S2 ;

X1 Y1 Z1 = COOR BOR1  ;

DEP1   = (X1 NOMC 'UX') ET (Y1 NOMC 'UY') ET (Z1 NOMC 'UZ') ;

RIG1   = RELA CORI DEPL SOL1 ;
RIG2   = BLOQ (P0 et P2) DEPL;
RIG3   = BLOQ  P1 UY ;
F1     = DEPI  RIG3 1. ;
DEP2   = RESO (RIG1 ET RIG2 et rig3 ) F1 ;

*vec1   = vect dep2 ux uy uz 1. jaun ;
*trac vec1 sol1 cach ;

FOR1   = RESU (AMOT1 * DEP1);
FOR2   = RESU (AMOT2 * DEP2);

for1   = (xtx for1) **.5 ;
for2   = (xtx for2) **.5 ;

** resultat analytiques
fan1   =  3**.5 * pi / 4 ;
fan2   =  2**.5 * pi / 4 ;

** comparaison et calcul d'erreur
err1   = (for1 -  fan1) / fan1 * 100 ;
err2   = (for2 -  fan2) / fan2 * 100 ;
errmax = maxi abs (prog err1 err2) ;

list errmax ;

MESS 'La résultante des forces visqueuses dues au déplacement radial est' FOR1;

MESS 'La résultante des forces visqueuses dues au déplacement tangentiel est' FOR2;
LIST (SOL1 ELEM 'TYPE');

* 1.2276 est l'erreur pour NBRAY = 6. On tolere un poil plus
SI(errmax <EG 1.228);
    ERRE 0;
SINO;
    ERRE 5;
FINSI;

FIN;





