* fichier : uo2s_cas1.dgibi
************************************************************************
* Section : Mecanique Endommagement
* Section : Mecanique Endommagement
* Section : Mecanique Viscoplastique
************************************************************************
*     Test uo2s_cas1.dgibi: Jeux de donnees
*     ------------------------------------
*
'OPTI' 'DIME' 3 'MODE' 'TRID'        ;
'OPTI' 'ELEM' 'CUB8'                 ;
'OPTI' 'TRAC' 'PSC'                  ;
'TEMPS' 'ZERO'                       ;
L = 'MOT' LIST ; F = 'MOT' FIN ;
*
**********************************************************
*                TEST DE VALIDATION                      *
*                ------------------                      *
*                   MODELE UO2                           *
*   (COUPLAGE DES MODELES GATT_MONERIE ET OTTOSEN)       *
*   UO2 STANDARD COMPRESSIBLE AVEC COUPLAGE STATIQUE     *
*               RESOLUTION SIMPLIFIEE                    *
*                                                        *
*  MAILLAGE:                                             *
*     CUBE                                               *
*                                                        *
*  CHARGEMENT:                                           *
*     DEPLACEMENTS IMPOSES A VITESSES CTE EN  TRACTION   *
*     SELON LES DIRECTIONS Z ET X :                      *
*     VIT DE DEF EN X = 0.8 * (VIT DE DEF EN Z)          *
*     TEMPERATURE         CTE                            *
*     DENSITE DE FISSIONS CTE                            *
*     OTTOSEN ACTIVE (FISSURATIONS PUIS RUPTURES DS LES  *
*     DEUX DIRECTIONS (D ABORD Z PUIS X)                 *
*     VISCOPLASTICITE SIMPLIFIEE (VIA CHOIX DES COEFS)   *
*     ACTIVEE                                            *
*     GONFLEMENT ACTIVE                                  *
*     PAS DE DENSIFICATION                               *
**********************************************************
*
DESSI = FAUX ;
*
* -----------------------------*
* MAILLAGE D UN CUBE DE COTE 1 *
* -----------------------------*
   HS2  = 2E-5                                                 ;
*
   OO   = 0.     0.         0.                                 ;
   A1   = HS2    0.         0.                                 ;
   A2   = HS2    0.         HS2                                ;
   A3   = 0.     0.         HS2                                ;
*
   LB   = 'DROIT' 1  OO A1                                     ;
   LD   = 'DROIT' 1  A1 A2                                     ;
   LH   = 'DROIT' 1  A2 A3                                     ;
   LG   = 'DROIT' 1  A3 OO                                     ;
*
   SUA  = 'DALL' LB  LD  LH  LG  'PLAN'                        ;
   SU2  = SUA 'PLUS' (0. HS2 0.)                               ;
   VOL1 = 'VOLU' 1 SUA SU2                                     ;
   'ELIM' (HS2/1000.) VOL1                                     ;
*
   SUB = 'POIN' ('COOR' 3 VOL1) 'INFERIEUR' (HS2/1000.)        ;
   SUH = 'POIN' ('COOR' 3 VOL1) 'SUPERIEUR' (HS2 - (HS2/1000.));
   LG  = 'POIN' ('COOR' 1 VOL1) 'INFERIEUR' (HS2/1000.)        ;
   LD  = 'POIN' ('COOR' 1 VOL1) 'SUPERIEUR' (HS2 - (HS2/1000.));
*
   VOL2 = VOL1 'TOUR' 45. OO (0 HS2 0);
   SUB2 = SUB  'TOUR' 45. OO (0 HS2 0); 'ELIM' (HS2/1000.) vol2 SUB2;
   SUH2 = SUH  'TOUR' 45. OO (0 HS2 0); 'ELIM' (HS2/1000.) vol2 SUH2;
   LG2  = LG   'TOUR' 45. OO (0 HS2 0); 'ELIM' (HS2/1000.) vol2 LG2 ;
   LD2  = LD   'TOUR' 45. OO (0 HS2 0); 'ELIM' (HS2/1000.) vol2 LD2 ;
   SUA2 = SUA  'TOUR' 45. OO (0 HS2 0); 'ELIM' (HS2/1000.) vol2 SUA2;
   VOL1 = VOL2                        ;

   VX2 = (1 0 0) 'TOUR' 45. OO (0 HS2 0);
   VZ2 = (0 0 1) 'TOUR' 45. OO (0 HS2 0);
* -----------------------------------------------------*
* Conditions aux limites + deplacement impose sur CLZH *
* -----------------------------------------------------*
CLZB = 'BLOQ' SUB2 'DIRECTION'  VZ2                 ;
CLZH = 'BLOQ' SUH2 'DIRECTION'  VZ2                 ;
CLXG = 'BLOQ' LG2  'DIRECTION'  VX2                 ;
CLXD = 'BLOQ' LD2  'DIRECTION'  VX2                 ;
CLYG = 'BLOQ' UY SUA2                               ;
CLT  = CLZB 'ET' CLZH 'ET' CLXG 'ET' CLXD 'ET' CLYG ;
* -------*
* Modele *
* -------*
MODL1= MODE VOL1 MECANIQUE ELASTIQUE VISCOPLASTIQUE UO2 ;
*
TA = @GATTPAR '../divers/fichier_gatt' ;
*
* Temperature moyenne (cte au cours du calcul)
* --------------------------------------------
  TE1 = 1420. + 273.  ;
*
* Porosite initiale
* -----------------
  PO   = 4.52E-2      ;
*
* Densite de fission (cte au cours du calcul)
* -------------------------------------------
  PHI1 = 3E20 ;
*
   RHO0 = 1. - TA.'POR0'                      ;
   TAU0 = TA.'EFIS'*270./238./10950./RHO0     ;
*
PP = 'MANU' 'CHML' MODL1 'T' TE1 'PORO' PO RIGIDITE ;
*
*
* --------------------------------------*
* 'YOUN' constant ou fonction de T et f *
* --------------------------------------*
 MATREE = MATE MODL1 'YOUN' (TA.'YOUN')          ;
 EE     = 'VARI' 'NUAG' MODL1 MATREE PP          ;
* EE     = (TA.'YOUN')                            ;
 MATREE = MATE MODL1 'YOUN' EE                   ;
* ------------------------------------*
* 'NU' constant ou fonction de T et f *
* ------------------------------------*
 MATRUU = MATE MODL1 'NU' (TA.'NU')              ;
 UU     = 'VARI' 'NUAG' MODL1 MATRUU PP          ;
* UU     = (TA.'NU')                              ;
* --------------------------------------*
* 'ALPH' constant ou fonction de T et f *
* --------------------------------------*
 MATRAA = MATE MODL1 'ALPH' (TA.'ALPH')          ;
 XALFA  = 'VARI' 'NUAG' MODL1 MATRAA PP          ;
* XALFA  = (TA.'ALPH')                            ;
 MATRAA = MATE MODL1 'ALPH' XALFA                ;
* -----------------------------------------------------------------*
* Temperature de reference et temperature de reference pour 'ALPH' *
* -----------------------------------------------------------------*
 TREF   = TE1 ;
 TALPHA = 0.  ;
* ----------------------------------------*
* 'ALPH' pour la temperature de reference *
* ----------------------------------------*
 PP       = 'MANU' 'CHML' MODL1 'T' TREF 'PORO' PO RIGIDITE ;
 ALFATREF = 'MAXI' ('VARI' 'NUAG' MODL1 MATRAA PP)          ;
*
* -----------------------------*
* Materiau partie GATT_MONERIE *
* -----------------------------*
* fluage primaire (negligeable devant le fluage d'irradiation)
  TA.'AP'   = 1                   ;
  TA.'BP'   = 0                   ;
* fluage secondaire (2 mecanismes)
  TA.'N1'   = 1.                  ;
  TA.'N2'   = 1.                  ;
* fluage d"irradiation
  TA.'N3'   = 1.                  ;
* fonction de couplage
  TA.'OMEG' = 0.                  ;
* densification/gonflement
  TA.'BUMI' = 5E3 * (TA.'BUMI')   ;
*
MATR11 = 'MATE' MODL1 'YOUN' EE 'NU' UU 'RHO' (TA.'RHO')  'ALPH' XALFA ;
*
MATR12 = 'MATE' MODL1  'R'    (TA.'R')    'DG0' (TA.'DG0') 'DG'  (TA.'DG') 'K1'   (TA.'K1')   'M1'  (TA.'M1')  'Q1'  (TA.'Q1')  'N1'   (TA.'N1')  'K2'   (TA.'K2')   'M2'  (TA.'M2')  'Q2'  (TA.'Q2')  'N2'   (TA.'N2')  'OMEG' (TA.'OMEG') 'H'   (TA.'H')   'Q'   (TA.'Q')   'BETA' (TA.'BETA') 'K'    (TA.'K')    'A'   (TA.'A')   'Q3'  (TA.'Q3')  'N3'   (TA.'N3') 'CR'   (TA.'CR')   'CR1' (TA.'CR1') 'CR2' (TA.'CR2') 'CR3'  (TA.'CR3');
*
MATR13 = 'MATE' MODL1  'KP' (TA.'KPUO')   'AP' (TA.'AP')   'BP' (TA.'BP')     'QP' (TA.'QP') ;
*
MATR14 = 'MATE' MODL1  'ADEN' (TA.'ADEN')   'KGON' (TA.'KGON')  'POR0' (TA.'POR0')   'BUMI' (TA.'BUMI')  'EFIS' (TA.'EFIS')                                ;
*
* TYPE = 0. combustible UO2            sinon combustible AFA3GLAA
* COMP = 0. combustible compressible   sinon incompressible
* DYN  = 0. couplage statique          sinon dynamique
*
* -----------------------------*
* Materiau partie OTTOSEN      *
* -----------------------------*
 MATR16 = 'TAIL' MODL1 ;
*
* Resistance au cisaillement 'GS  ' par defaut: 'YOUN'*1.8E-4
* Ouverture a rupture 'WRUP' = 0 --> 'WRUP' = 2*'GFTR'/'LTR '
* Relation bilineaire 'BILI' = 0 --> 'BILI' = 0 (ouverture)
*
 XGFTR = 3.0   ; XGFTR1=XGFTR ; XGFTR2=XGFTR ; XGFTR3=XGFTR ;
 XLTR  = 136E6 ; XLTR1 =XLTR  ; XLTR2 =XLTR  ; XLTR3 =XLTR  ;
 XBTR  = 0.2   ;
*
 MATR17 = 'MATE' MODL1  'GFTR' XGFTR    'LTR ' XLTR     'BTR ' XBTR 'WRUP' 0.       'BILI' 0. 'GFT1' XGFTR1   'GFT2' XGFTR2   'GFT3' XGFTR3 'GS1 ' 0.       'GS2 ' 0.       'GS3 ' 0. 'LTR1' XLTR1    'LTR2' XLTR2    'LTR3' XLTR3 'WRU1' 0.       'WRU2' 0.       'WRU3' 0. 'BIL1' 0.       'BIL2' 0.       'BIL3' 0. 'SIMP' 1.                                      ;
*
MATR1 = MATR11 'ET' MATR12 'ET' MATR13 'ET'  MATR14 'ET' MATR16 'ET' MATR17     ;
*
*
************************************************************************
'DEBP' XFONC  FN*'FLOTTANT' XFN1*'FLOTTANT'       DT*'FLOTTANT' SXN*'FLOTTANT'  SZN*'FLOTTANT' PAX*'FLOTTANT'  PAZ*'FLOTTANT'  EX*'FLOTTANT'   EZ*'FLOTTANT' NU_SUR_E*'FLOTTANT' C12*'FLOTTANT'   C3*'FLOTTANT'                      ;

 UNTIER = 1. / 3. ;  DEUXTIER = 2. / 3. ; UNDEMI = 1. / 2. ;

 det = (EX * EZ)  - (NU_SUR_E ** 2)  ;
 AX  = (EZ * PAX) + (NU_SUR_E * PAZ) ;
 AZ  = (EX * PAZ) + (NU_SUR_E * PAX) ;

 A   = XFN1 / (1. - XFN1)                     ;
 B   = (1. + (DEUXTIER * XFN1)) / (1. - XFN1) ;

 X   =  UNTIER   * C12 * A                    ;
 Y   = (DEUXTIER * C12 * B) + (UNDEMI * C3)   ;

 GDT = DT / det ;  GDT2 = GDT**2   ;

 GDET1a = 2. * NU_SUR_E * (Y - X)            ;
 GDET1b = (EX + EZ) * (X + (2.*Y))           ;
 GDET1  = GDT * (GDET1a - GDET1b)            ;
 GDET2  = 3. * GDT2 * det * Y * (Y + (2.*X)) ;
 GDET   = (1. - GDET1) + GDET2               ;

 FONC1a = 3. * Y * GDT * (NU_SUR_E - EX)        ;
 FONC1a = 1. - FONC1a                           ;
 FONC1b = (GDT * AX) + SXN                      ;
 FONC1c = 3. * Y * GDT * (NU_SUR_E - EZ)        ;
 FONC1c = 1. - FONC1c                           ;
 FONC1d = (GDT * AZ) + SZN                      ;
 FONC1e = (FONC1b * FONC1a) + (FONC1d * FONC1c) ;
 FONC1  = DT * C12 * XFN1 * FONC1e              ;
 FONC2  = GDET * (XFN1 - FN)                    ;
 VFONC  = FONC1 - FONC2                         ;
 TFONC  = ('ABS' VFONC) / FONC1                 ;
'FINP' VFONC TFONC FONC1e GDET ;
************************************************************************
*
************************************************************************
'DEBP' FCTNUL   FN*'FLOTTANT'                       DT*'FLOTTANT' SXN*'FLOTTANT'  SZN*'FLOTTANT' PAX*'FLOTTANT'  PAZ*'FLOTTANT'  EX*'FLOTTANT'   EZ*'FLOTTANT' NU_SUR_E*'FLOTTANT' C12*'FLOTTANT'   C3*'FLOTTANT'                      ;

 XFN1 = FN                           ;
 V1 TES1 FONCe1 GDET1  = XFONC   FN   XFN1   DT   SXN   SZN PAX  PAZ    EX   EZ    NU_SUR_E C12  C3  ;
 XFN2a = GDET1 * FN                  ;
 XFN2b = GDET1 - (DT * C12 * FONCe1) ;
 XFN2 = XFN2a / XFN2b                ;
*--
 'REPETER' STRU1 ;
*--
 V2 TES2 FONCe2 GDET2  = XFONC   FN   XFN2   DT   SXN   SZN PAX  PAZ    EX   EZ    NU_SUR_E C12  C3  ;
 PREC = 1E-4            ;
 'SI' ('<EG' TES2 PREC) ;
     FN1   = XFN2       ;
     GDET1 = GDET2      ;
    'QUIT' STRU1        ;
 'FINSI'                ;

* 'SI' ('>' &STRU1 100)                                  ;
*    'MESS' 'PAS DE SOLUTION DANS FCTNUL: &STRU1' &STRU1 ;
* 'FINSI'                                                ;

* 'SI' ( ('>' (V1 * V2) 0.) 'ET' ('EGA' &STRU1 1) )                   ;
*    'MESS' 'MAUVAISE ESTIMATION INITIALE DANS FCTNUL: V1 V2 =' V1 V2 ;
* 'FINSI'                                                             ;

 XX  = V1 / (V1 - V2)              ;
 XXF = ((XFN2 - XFN1) * XX) + XFN1 ;
 'SI' ('<EG' (V1 * V2) 0.)         ;
    XFN2 = XXF                     ;
 'SINON'                           ;
    XFN1 = XXF                     ;
 'FINSI'                           ;
*--
 'FIN' STRU1 ;
*--
'FINP' FN1 GDET1 ;
************************************************************************
*
************************************************************************
'DEBP' CONT  XFN1*'FLOTTANT'  GDET1*'FLOTTANT'  DT*'FLOTTANT'        SXN*'FLOTTANT'    SZN*'FLOTTANT' PAX*'FLOTTANT'    PAZ*'FLOTTANT'  EX*'FLOTTANT'     EZ*'FLOTTANT'  NU_SUR_E*'FLOTTANT' C12*'FLOTTANT'     C3*'FLOTTANT'                      ;

 UNTIER = 1. / 3. ;  DEUXTIER = 2. / 3. ; UNDEMI = 1. / 2. ;

 det = (EX * EZ)  - (NU_SUR_E ** 2)  ;
 AX  = (EZ * PAX) + (NU_SUR_E * PAZ) ;
 AZ  = (EX * PAZ) + (NU_SUR_E * PAX) ;

 A   = XFN1 / (1. - XFN1)                     ;
 B   = (1. + (DEUXTIER * XFN1)) / (1. - XFN1) ;

 X   =  UNTIER   * C12 * A                    ;
 Y   = (DEUXTIER * C12 * B) + (UNDEMI * C3)   ;

 GDT = DT / det ;

 SZN1a = (GDT * AZ) + SZN             ;
 SZN1b = NU_SUR_E * (Y - X)           ;
 SZN1c = EZ * (X + (2. * Y))          ;
 SZN1d = 1. - (GDT * (SZN1b - SZN1c)) ;
 SZN1e = SZN1a * SZN1d                ;
 SZN1a = (GDT * AX) + SXN             ;
 SZN1b = NU_SUR_E * (X + (2. * Y))    ;
 SZN1c = EX * (Y - X)                 ;
 SZN1d = GDT * (SZN1c - SZN1b)        ;
 SZN1  = SZN1e + (SZN1a * SZN1d)      ;
 SZN1  = SZN1 / GDET1                 ;

 SXN1a = (GDT * AX) + SXN             ;
 SXN1b = NU_SUR_E * (Y - X)           ;
 SXN1c = EX * (X + (2. * Y))          ;
 SXN1d = 1. - (GDT * (SXN1b - SXN1c)) ;
 SXN1e = SXN1a * SXN1d                ;
 SXN1a = (GDT * AZ) + SZN             ;
 SXN1b = NU_SUR_E * (X + (2. * Y))    ;
 SXN1c = EZ * (Y - X)                 ;
 SXN1d = GDT * (SXN1b - SXN1c)        ;
 SXN1  = SXN1e - (SXN1a * SXN1d)      ;
 SXN1  = SXN1 / GDET1                 ;

'FINP' SXN1 SZN1 ;
************************************************************************
*
************************************************************************
'DEBP' CONTX  XFN1*'FLOTTANT'   SXN*'FLOTTANT'   DT*'FLOTTANT' PAX*'FLOTTANT'      E*'FLOTTANT' C12*'FLOTTANT'     C3*'FLOTTANT'                ;

 UNTIER = 1. / 3. ;  DEUXTIER = 2. / 3. ; UNDEMI = 1. / 2.    ;

 A   = XFN1 / (1. - XFN1)                                     ;
 B   = (1. + (DEUXTIER * XFN1)) / (1. - XFN1)                 ;

 X   =  UNTIER   * C12 * A                                    ;
 Y   = (DEUXTIER * C12 * B) + (UNDEMI * C3)                   ;

 SXN1a = SXN + (DT * E * PAX)                                 ;
 SXN1b = DT * E * ( X+(2.*Y) )                                ;
  SXN1 = SXN1a / (1. + SXN1b)                                 ;

'FINP' SXN1                                                   ;
************************************************************************
*
************************************************************************
'DEBP' FISZ      XFN1*'FLOTTANT' SXN1*'FLOTTANT' PAX*'FLOTTANT'        PAZ*'FLOTTANT' EX*'FLOTTANT'   NU_SUR_E*'FLOTTANT'  C12*'FLOTTANT'         C3*'FLOTTANT'   ;

 UNTIER = 1. / 3. ;  DEUXTIER = 2. / 3. ; UNDEMI = 1. / 2. ;

 AZ  = (EX * PAZ) + (NU_SUR_E * PAX) ;

 A   = XFN1 / (1. - XFN1)                     ;
 B   = (1. + (DEUXTIER * XFN1)) / (1. - XFN1) ;

 X   =  UNTIER   * C12 * A                    ;
 Y   = (DEUXTIER * C12 * B) + (UNDEMI * C3)   ;

 VFZ1     = NU_SUR_E * (X + (2. * Y)) ;
 VFZ1     = VFZ1 + ((X - Y) * EX)     ;
 VFZ1     = SXN1 * VFZ1               ;
 VEPSFZN1 = (AZ - VFZ1) / EX          ;

'FINP' VEPSFZN1 ;
************************************************************************
*
CONVSIG = 1E6                      ;
CONVTO  = 24.*3600E3               ;
*
TFIN  = 1E5                        ;
TMIL  = TFIN / 2.                  ;
TEND  = 1.E4                       ;
* Le calcul est conduit jusqu a TEND seulement
*
* ------------------------------*
* Chargement en temperature cte *
* ------------------------------*
TP1      = 1E-4                                                 ;
TE0      = 1320. + 273.                                         ;
TE2      = (TP1*TFIN) + TE0                                     ;
CHTEMP   = 'MANU' 'CHPO' VOL1 1 'T' 1.                          ;
EVT      = 'EVOL' 'MANU' ('PROG' 0.  TFIN)  ('PROG' TE1 TE1)    ;
CHARTEMP = 'CHAR' 'T' CHTEMP EVT                                ;
* -------------------------------------*
* Chargement en densite de fission cte *
* -------------------------------------*
PHIP1    = 3E14                                                 ;
PHI2     = PHIP1 * TFIN                                         ;
CHFISS   = 'MANU' 'CHPO' VOL1 1 'DFIS' 1.                       ;
EVF      = 'EVOL' 'MANU' ('PROG' 0.  TFIN) ('PROG' PHI1 PHI1)   ;
CHARFISS = 'CHAR' 'DFIS' CHFISS EVF                             ;
*
*
TE  = TE1                                  ;
PHI = PHI1                                 ;
UN  = 1.                                   ;
*
* --------------------------------------------------------------------
* Coefficients pour la viscoplasticite, l"elasticite et la fissuration
* --------------------------------------------------------------------
*
* Facteurs multiplicatifs dus au dopage par le Chrome
*
WC1 = 'TANH' ( (TA.'CR' - TA.'CR2') / TA.'CR3' ) ;
WC1 = 1. + ( 0.5 * TA.'CR1' * (1. + WC1) )       ;
CV  = 180. / PI                                  ;
WC2 = 1. - ( 'COS' (CV * TA.'DG' / TA.'DG0') )   ;
WC2 = 2. * (TA.'DG0'**TA.'M2') * WC2             ;
*
CP  = -1. * (TA.'QP') / (TA.'R') / TE ;
CP  = TA.'KPUO' * ('EXP' CP)          ;
*
C1 = -1. * (TA.'Q1') / (TA.'R') / TE       ;
C1 = ( (TA.'DG')**(TA.'M1') ) * ('EXP' C1) ;
C1 = 0.5 * (TA.'K1') * C1                  ;
C1 = WC1 * C1                              ;
*
C2 = -1. * (TA.'Q2') / (TA.'R') / TE       ;
C2 = WC2 * ('EXP' C2)                      ;
C2 = 0.5 * (TA.'K2') * C2                  ;
*
C3 = -1. * (TA.'Q3') / (TA.'R') / TE       ;
C3 = (TA.'A') * PHI * ('EXP' C3)           ;
C3 = C3 + (PHI * CP)                       ;
*
TETA = 'TANH' (TE / (TA.'H'))              ;
TETA = 0.5 * (TA.'BETA') * (UN + TETA)     ;
*
GROPHI = UN + ((TA.'K') * PHI)             ;
*
C12 = ((UN - TETA) * C1) + (TETA * C2)     ;
C12 = 1.5 * GROPHI * C12                   ;
*
E        = 'MAXI' EE    ;
NU       = 'MAXI' UU    ;
NU_SUR_E = NU / E       ;
ALFA     = 'MAXI' XALFA ;
EX       = 1./E         ;
EZ       = 1./E         ;
*
LZ    = 'MAXI' ('EXCO' MATR16 'LZZ')          ;
*
COEF     = 0.8                            ;
EPSZ_PT  = 3E-7                           ; EPSX_PT  = COEF * EPSZ_PT ;
EPSTH_PT = 0.                             ;
EPSG_PT  = TA.'KGON' * TAU0 * PHI / 3.    ;
PAZ      = EPSZ_PT - (EPSTH_PT + EPSG_PT) ;
PAX      = EPSX_PT - (EPSTH_PT + EPSG_PT) ;
*
*
PRECIS = 1E-3 ;
SXN = 0.    ; SZN = 0.    ; FN = PO ; TN = 0. ;
EPSFZN = 0. ; EPSFXN = 0. ;
*
LTIME  = 'PROG' TN  ;
LSX    = 'PROG' SXN ; LSZ   = 'PROG' SZN  ;
LF     = 'PROG' FN  ;
LEPSFX = 'PROG' 0.  ; LEPSFZ = 'PROG' 0.  ;
LEPSVX = 'PROG' 0.  ; LEPSVZ = 'PROG' 0.  ;
*
* -----------
* t <= TFISS1
* -----------
DT  = 20. ;
*--
'REPE' BLO1 ;
*--
   FN1 GDET1 = FCTNUL   FN   DT   SXN        SZN   PAX   PAZ EX   EZ   NU_SUR_E   C12   C3         ;

   SXN1 SZN1 = CONT     FN1  GDET1  DT        SXN    SZN   PAX    PAZ  EX   EZ     NU_SUR_E  C12    C3               ;

   LIMZ = (1. + PRECIS) * XLTR1              ;
   'SI' ('>' SZN1 LIMZ)                      ;
      DT = DT * (XLTR1 - SZN) / (SZN1 - SZN) ;
      'ITERER' BLO1                          ;
   'FINSI'                                   ;

   TN1 = TN + DT  ;

   EPSFZN1 = 0. ; EPSFXN1 = 0. ;
*
   LTIME  = LTIME  'ET' ('PROG' TN1)   ;
   LF     = LF     'ET' ('PROG' FN1)   ;
   LSX    = LSX    'ET' ('PROG' SXN1)  ;
   LSZ    = LSZ    'ET' ('PROG' SZN1)  ;
   LEPSFX = LEPSFX 'ET' ('PROG' EPSFXN1) ;
   LEPSFZ = LEPSFZ 'ET' ('PROG' EPSFZN1) ;
*
   TN = TN1 ; SXN = SXN1 ; SZN = SZN1 ; FN = FN1 ;
   EPSFZN = EPSFZN1 ; EPSFXN = EPSFXN1 ;
*
   TESTZ = 'ABS' ((SZN1 - XLTR1) / XLTR1) ;
   'SI' ('<EG' TESTZ PRECIS)              ;
      'QUIT' BLO1                         ;
   'FINSI'                                ;
*--
'FIN' BLO1 ;
*--
* --------------------
* TFISS1 < t <= TFISS2
* --------------------
DT  = 20. ;
*
SZN  = 0. ;
SZN1 = 0. ;
*--
'REPE' BLO2 ;
*--
   FN1 GDET1 = FCTNUL   FN   DT   SXN        SZN   PAX   PAZ EX   EZ   NU_SUR_E   C12   C3         ;

   SXN1 = CONTX  FN1   SXN   DT   PAX   E   C12    C3         ;

   LIMX = (1. + PRECIS) * XLTR1              ;
   'SI' ('>' SXN1 LIMX)                      ;
      DT = DT * (XLTR1 - SXN) / (SXN1 - SXN) ;
      'ITERER' BLO2                          ;
   'FINSI'                                   ;

   TN1 = TN + DT  ;

   EPSFXN1 = 0. ;
   VEPSFZN1 = FISZ   FN1  SXN1      PAX  PAZ EX   NU_SUR_E  C12  C3       ;
   EPSFZN1 = EPSFZN + (DT * VEPSFZN1)             ;
*
   LTIME  = LTIME  'ET' ('PROG' TN1)      ;
   LF     = LF     'ET' ('PROG' FN1)      ;
   LSX    = LSX    'ET' ('PROG' SXN1)     ;
   LSZ    = LSZ    'ET' ('PROG' SZN1)     ;
   LEPSFX = LEPSFX 'ET' ('PROG' EPSFXN1)  ;
   LEPSFZ = LEPSFZ 'ET' ('PROG' EPSFZN1)  ;
*
   TN = TN1 ; SXN = SXN1 ; SZN = SZN1 ; FN = FN1 ;
   EPSFZN = EPSFZN1 ; EPSFXN = EPSFXN1 ;
*
   TESTX = 'ABS' ((SXN1 - XLTR1) / XLTR1) ;
   'SI' ('<EG' TESTX PRECIS)              ;
      'QUIT' BLO2                         ;
   'FINSI'                                ;
*--
'FIN' BLO2 ;
*--
* -------------------
* TFISS1 < TFISS2 < t
* -------------------
DT  = 20. ;
*
SZN  = 0. ;
SZN1 = 0. ;
SXN  = 0. ;
SXN1 = 0. ;
*--
'REPE' BLO3 ;
*--
   FN1 GDET1 = FCTNUL   FN   DT   SXN        SZN   PAX   PAZ EX   EZ   NU_SUR_E   C12   C3         ;

   VEPSFZN1 = PAZ                       ;
   EPSFZN1  = EPSFZN + (DT * VEPSFZN1)  ;

   VEPSFXN1 = PAX                       ;
   EPSFXN1  = EPSFXN + (DT * VEPSFXN1)  ;

   TN1 = TN + DT        ;
*
   'SI' ('>' TN1 TEND)  ;
      'QUIT' BLO3       ;
   'FINSI'              ;
*
   LTIME  = LTIME  'ET' ('PROG' TN1)      ;
   LF     = LF     'ET' ('PROG' FN1)      ;
   LSX    = LSX    'ET' ('PROG' SXN1)     ;
   LSZ    = LSZ    'ET' ('PROG' SZN1)     ;
   LEPSFX = LEPSFX 'ET' ('PROG' EPSFXN1)  ;
   LEPSFZ = LEPSFZ 'ET' ('PROG' EPSFZN1)  ;
*
   TN = TN1 ; SXN = SXN1 ; SZN = SZN1 ; FN = FN1 ;
   EPSFZN = EPSFZN1 ; EPSFXN = EPSFXN1 ;
*--
'FIN' BLO3 ;
*--
* -------------------
*
EV_SX    ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'SIGX(MPa)'  (LSX/CONVSIG);
EV_SZ    ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'SIGZ(MPa)' (LSZ/CONVSIG);
EV_F     ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'Porosite' LF          ;
EV_EPSFX ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'Def Fiss X' LEPSFX    ;
EV_EPSFZ ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'Def Fiss Z' LEPSFZ    ;
EV_WX    ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'WX(m)'     (LZ*LEPSFX);
EV_WZ    ='EVOL' 'ROUG' 'MANU' 'Temps(s)' LTIME 'WZ(m)'     (LZ*LEPSFZ);
EV_FISX  ='EVOL' 'ROUG' 'MANU' 'WX(m)'   (LZ*LEPSFX) 'SIGX(MPa)' (LSX/CONVSIG);
EV_FISZ  ='EVOL' 'ROUG' 'MANU' 'WZ(m)'   (LZ*LEPSFZ)  'SIGZ(MPa)' (LSZ/CONVSIG);
* ---------------------------------*
* Chargement en deplacement impose *
* ---------------------------------*
EPS_TFIN = EPSZ_PT * TFIN                                      ;
DEPZ     = 'DEPI' CLZH 1                                      ;
DEPX     = 'DEPI' CLXD 1                                      ;
LDEPX    = 'PROG' 0  TFIN                                     ;
LDEPY    = ('PROG' 0  EPS_TFIN) * HS2                         ;
'TITR' 'DEPLACEMENT IMPOSE'                                   ;
EVDEP    = 'EVOL' 'MANU' 'Temps (s)' LDEPX   'Dep (m)'  LDEPY ;
'SI' DESSI ; 'DESS' EVDEP ; 'FINSI' ;
CHARMECZ = 'CHAR' 'DIMP' DEPZ EVDEP                           ;
CHARMECX = 'CHAR' 'DIMP' DEPX (COEF*EVDEP)                    ;
CHARMECA = CHARMECZ 'ET' CHARMECX                             ;
*-------------------*
* Instants calcules *
*-------------------*
LIST1 = 'PROG' 0. 'PAS' 200.  TEND ;
*
* ---------------------------------------*
* Variables internes initiales 'PORO'=PO *
* ---------------------------------------*
VAR00 = 'ZERO' MODL1 'VARINTER'                                    ;
VAR01 = 'MANU' 'CHML' MODL1 'PORO' PO  'TYPE' 'VARIABLES INTERNES' 'STRESSES' ;
VAR0 = VAR00 + VAR01                                               ;
*
CHARTOT = CHARMECA 'ET' CHARTEMP 'ET' CHARFISS             ;
*
 TAB1                          = TABLE   ;
 TAB1.'VARIABLES_INTERNES'     = TABLE   ;
 TAB1.'BLOCAGES_MECANIQUES'    = CLT     ;
 TAB1.'MODELE'                 = MODL1   ;
 TAB1.'CHARGEMENT'             = CHARTOT ;
 TAB1.'VARIABLES_INTERNES' . 0 = VAR0    ;
 TAB1.'CARACTERISTIQUES'       = MATR1   ;
 TAB1.'TEMPS_CALCULES'         = LIST1   ;
 TAB1.'TEMPS_SAUVES'           = LIST1   ;
*
TMASAU=table;
tab1 . 'MES_SAUVEGARDES'=TMASAU;
TMASAU .'DEFTO'=VRAI;
TMASAU .'DEFIN'=VRAI;
 PASAPAS TAB1                            ;
*
*
* -----------------------*
* Controle des resultats *
* -----------------------*
TT    = TAB1 . 'TEMPS'                      ;
SS    = TAB1 . 'CONTRAINTES'                ;
VV    = TAB1 . 'VARIABLES_INTERNES'         ;
IN    = TAB1 . 'DEFORMATIONS_INELASTIQUES'  ;
NCONT = 'DIME' TAB1 . 'CONTRAINTES'         ;
*
LZEIT  = 'PROG' ;
LSIGX  = 'PROG' ;
LSIGZ  = 'PROG' ;
LW1    = 'PROG' ;
LW2    = 'PROG' ;
LW3    = 'PROG' ;
LPORO  = 'PROG' ;
LEFX   = 'PROG' ;
LEFZ   = 'PROG' ;
*
MI = 'MOTS' 'EIXX' 'EIYY' 'EIZZ' 'GIXY' 'GIXZ' 'GIYZ' ;
MV = 'MOTS' 'EVXX' 'EVYY' 'EVZZ' 'GVXY' 'GVXZ' 'GVYZ' ;
ME = 'MOTS' 'EPXX' 'EPYY' 'EPZZ' 'GAXY' 'GAXZ' 'GAYZ' ;
*
*
'REPE' BLOC NCONT ;
*
  IND  = &BLOC - 1         ;
  ZEIT = TT.IND            ;
*
  SIG  = SS.IND            ;
  SIG  = 'RTENS' SIG MODL1 VX2 (0 1 0)                ;
  SIGX = 'EXCO' SIG 'SMXX' ;
  SIGX_MAX = 'MAXI' SIGX   ; SIGX_MIN = 'MINI' SIGX   ;
  SIGZ = 'EXCO' SIG 'SMZZ' ;
  SIGZ_MAX = 'MAXI' SIGZ   ; SIGZ_MIN = 'MINI' SIGZ   ;
*
  VI    = VV.IND                                           ;
  EI    = IN.IND                                           ;
  EI    = 'CHANG' 'TYPE' ('EXCO' MI EI ME) 'DEFORMATIONS'  ;
*
  EPS_VP = 'CHANG' 'TYPE' ('EXCO' MV VI ME) 'DEFORMATIONS' ;
  DIF    = EI - EPS_VP                                     ;
  DIF    = 'RTENS' DIF MODL1 VX2 (0 1 0)                   ;
*
  PORO  = 'MAXI' ('EXCO' VI 'PORO')     ;
  W1    = 'MAXI' ('EXCO' VI 'W1')       ;
  W2    = 'MAXI' ('EXCO' VI 'W2')       ;
  W3    = 'MAXI' ('EXCO' VI 'W3')       ;
*
  LZEIT  = LZEIT  'ET' ('PROG' ZEIT)                        ;
  LSIGX  = LSIGX  'ET' ('PROG' SIGX_MAX)                    ;
  LSIGZ  = LSIGZ  'ET' ('PROG' SIGZ_MAX)                    ;
  LW1    = LW1    'ET' ('PROG' W1)                          ;
  LW2    = LW2    'ET' ('PROG' W2)                          ;
  LW3    = LW3    'ET' ('PROG' W3)                          ;
  LEFX   = LEFX   'ET' ('PROG' ('MAXI' ('EXCO' DIF 'EPXX')));
  LEFZ   = LEFZ   'ET' ('PROG' ('MAXI' ('EXCO' DIF 'EPZZ')));
  LPORO  = LPORO  'ET' ('PROG' PORO)                        ;
*
'FIN' BLOC ;
*
* ----------------------
* CONTROLE DES RESULTATS
* ----------------------
LSOUS   = 'PROG' 500 'PAS' 500 6000 7000 'PAS' 500 (TEND - 500) ;
UN      = 'PROG' ('DIME' LSOUS)*1           ;
*
SX_CAL  = 'IPOL' LSOUS LZEIT LSIGX; SX_ANA  = 'IPOL' LSOUS LTIME LSX   ;
SZ_CAL  = 'IPOL' LSOUS LZEIT LSIGZ; SZ_ANA  = 'IPOL' LSOUS LTIME LSZ   ;
PO_CAL  = 'IPOL' LSOUS LZEIT LPORO; PO_ANA  = 'IPOL' LSOUS LTIME LF    ;
EFX_CAL = 'IPOL' LSOUS LZEIT LEFX ; EFX_ANA = 'IPOL' LSOUS LTIME LEPSFX;
EFZ_CAL = 'IPOL' LSOUS LZEIT LEFZ ; EFZ_ANA = 'IPOL' LSOUS LTIME LEPSFZ;
*
PREX = 5E-3                                                       ;
T_SX =  (SX_ANA - SX_CAL) / (SX_ANA + UN)                         ;
T_SX = 'MAXI' ('ABS' T_SX)                                        ;
'SI' ( T_SX '<EG' PREX )                                          ;
   'ERRE' 0                                                       ;
'SINO'                                                            ;
   'MESS' 'ERREUR MAXIMALE CONTRAINTE SELON X = ' T_SX ' > ' PREX ;
   'ERRE' 5                                                       ;
'FINS'                                                            ;
*
T_SZ = (SZ_ANA - SZ_CAL) / (SZ_ANA + UN)                          ;
T_SZ = 'MAXI' ('ABS' T_SZ)                                        ;
'SI' ( T_SZ '<EG' PREX )                                          ;
   'ERRE' 0                                                       ;
'SINO'                                                            ;
   'MESS' 'ERREUR MAXIMALE CONTRAINTE SELON Z = ' T_SZ ' > ' PREX ;
   'ERRE' 5                                                       ;
'FINS'                                                            ;
*
PREX = 1E-4                                             ;
T_LF = (PO_ANA - PO_CAL) / PO_ANA                       ;
T_LF = 'MAXI' ('ABS' T_LF)                              ;
'SI' ( T_LF '<EG' PREX )                                ;
   'ERRE' 0                                             ;
'SINO'                                                  ;
   'MESS' 'ERREUR MAXIMALE POROSITE = ' T_LF ' > ' PREX ;
   'ERRE' 5                                             ;
'FINS'                                                  ;
*
WRUPT   = (2.*XGFTR1) / XLTR1                          ;
ERUPT   = WRUPT / LZ                                   ;
NONZERO = (ERUPT / 1E6) * UN                           ;
PREX  = 5E-2                                           ;
T_FX  = (EFX_ANA - EFX_CAL) / (EFX_ANA +  NONZERO)     ;
T_FX  = 'MAXI' ('ABS' T_FX)                            ;
'SI' ( T_FX '<EG' PREX )                               ;
   'ERRE' 0                                            ;
'SINO'                                                 ;
   'MESS' 'ERREUR MAXIMALE DEFORMATION DE FISSURATION  SELON X = ' T_FX ' > ' PREX                            ;
   'ERRE' 5                                            ;
'FINS'                                                 ;
*
PREX = 5E-2                                            ;
T_FZ = (EFZ_ANA - EFZ_CAL) / (EFZ_ANA +  NONZERO)      ;
T_FZ = 'MAXI' ('ABS' T_FZ)                             ;
'SI' ( T_FZ '<EG' PREX )                               ;
   'ERRE' 0                                            ;
'SINO'                                                 ;
   'MESS' 'ERREUR MAXIMALE DEFORMATION DE FISSURATION  SELON Z = ' T_FZ ' > ' PREX                            ;
   'ERRE' 5                                            ;
'FINS'                                                 ;
*
*
TAB               = 'TABLE'               ;
TAB . 'TITRE'     = 'TABLE'               ;
TAB . 'TITRE' . 1 = 'Solution Analytique' ;
TAB . 'TITRE' . 2 = 'Solution Calculee'   ;
*
*
EVSIGX = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'SIGX(MPa)' (LSIGX/CONVSIG);
EVSIGZ = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'SIGZ(MPa)' (LSIGZ/CONVSIG);
'SI' DESSI                                 ;
   'TITR' 'CONTRAINTE DANS LA DEUXIEME DIRECTION DE FISSURATION' ;
   'DESS' (EV_SX 'ET' EVSIGX) TAB 'LEGE'                         ;
   'TITR' 'CONTRAINTE DANS LA PREMIERE DIRECTION DE FISSURATION' ;
   'DESS' (EV_SZ 'ET' EVSIGZ) TAB 'LEGE'                         ;
'FINSI'                                                          ;
*
EVPORO = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'Porosite' LPORO ;
'SI' DESSI                                 ;
   'TITR' 'POROSITE'                       ;
   'DESS' (EV_F 'ET' EVPORO) TAB 'LEGE'    ;
'FINSI'                                    ;
*
EVWX  = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'WX(m)' LW2            ;
EVWZ  = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'WZ(m)' LW1            ;
'SI' DESSI                                                      ;
   'TITR' 'OUVERTURE DANS LA DEUXIEME DIRECTION DE FISSURATION' ;
   'DESS' (EV_WX 'ET' EVWX) TAB 'LEGE'                          ;
   'TITR' 'OUVERTURE DANS LA PREMIERE DIRECTION DE FISSURATION' ;
   'DESS' (EV_WZ 'ET' EVWZ) TAB 'LEGE'                          ;
'FINSI'                                                         ;
*
EVEPSFX = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'Def Fiss X' LEFX    ;
EVEPSFZ = 'EVOL' 'MANU' 'Temps (s)' LZEIT  'Def Fiss Z' LEFZ    ;
'SI' DESSI                                                      ;
   'TITR' 'DEFORMATION DE FISS. DANS LA DIRECTION DE FISS. N°2' ;
   'DESS' (EV_EPSFX 'ET' EVEPSFX) TAB 'LEGE'                    ;
   'TITR' 'DEFORMATION DE FISS. DANS LA DIRECTION DE FISS. N°1' ;
   'DESS' (EV_EPSFZ 'ET' EVEPSFZ) TAB 'LEGE'                    ;
'FINSI'                                                         ;
*
EVFISX = 'EVOL' 'MANU' 'WX(m)' LW2 'SIGX(MPa)' (LSIGX/CONVSIG)       ;
EVFISZ = 'EVOL' 'MANU' 'WZ(m)' LW1 'SIGZ(MPa)' (LSIGZ/CONVSIG)       ;
'SI' DESSI                                                           ;
   'TITR' 'RELATION CONTR./OUVERTURE DANS LA DIRECTION DE FISS. N°2' ;
   'DESS' (EV_FISX 'ET' EVFISX) TAB 'LEGE'                           ;
   'TITR' 'RELATION CONTR./OUVERTURE DANS LA DIRECTION DE FISS. N°1' ;
   'DESS' (EV_FISZ 'ET' EVFISZ) TAB 'LEGE'                           ;
'FINSI'                                                              ;
*
'FIN';






