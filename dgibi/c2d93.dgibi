* fichier :  c2d93.dgibi
************************************************************************
* Section : Magnetodynamique Magnetodynamique
************************************************************************
***********************************************************************
*
*       2D   AXISYMMETRIC  MAGNETIC FIELD COMPUTATION
*
*       Formulation  : VECTOR POTENTIAL
*
*                      NON LINEAR MATERIAL
*
***********************************************************************
 OPTION DIME 2  ELEM  TRI6  COUL VERT echo 1 ;
 GRAPH ='N' ;
* ------------------------Mesh --------------------------------------
 R1 =20. ; R2= 25. ; R3= 27. ; R4= 29. ; R5=39.;R6 = 130.;
 Z1= 2.5;  Z2=  5. ; Z3= 20. ; Z4= 130.;Z31=30.;Z32=50.;
 DI1= 1.; DI2=5.;DI3=60. ;DI4= 10.;NET2= 6;

  DENS DI1 ;
 OZ3= 0.  Z3; OZ2= 0.  Z2;OZ1= 0.  Z1;OZ4=0. Z4;
 OO= 0.  0.;R0Z0= DI1  0 ;
 R1Z0=  R1  0. ;R2Z0= R2  0. ;R3Z0 = R3  0. ;R4Z0 = R4  0.;
 R0Z2= DI1 Z2 ; R4Z2 = R4 Z2 ;
  DENS DI2 ;
 R5Z0=  R5  0. ;R5Z3= R5 Z3 ;R0Z3 =DI2 Z3 ;OZ3 = 0. Z3 ;
  DENS DI3 ;
 R6Z0=R6  0.; R6Z4=R6 Z4 ;
  DENS 10 ;
 R2Z3= R2  Z3 ; R1Z3= R1  Z3; R4Z1 = R4   Z1;
 R3Z1= R3  Z1 ;
 DENSITE DI4 ;
 R0Z4 = DI4 Z4 ; OZ4 = 0. Z4 ;
 NTRA= 1;
 FLAN1 =( D OO R0Z0 D R1Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;
*-------------------------  COIL SURFACE -------------------------
 BOBI  =( D R1Z0 R2Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
                                                      COUL BLEU ;
*-----------------------------------------------------------------
 FLAN2 =(INVE( BOBI COTE 3)) TRAN DINI DI1 DFIN DI1
                            ( OZ2 MOINS OZ1) COUL VERT ;
 FLAN3 =( D R2Z0 R3Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;
 FLAN4 =( D R3Z0 R4Z0 ) TRAN DINI DI1 DFIN DI1 ( OZ1 MOINS OO )
             TRAN DINI DI1 DFIN DI1 ( OZ2 MOINS OZ1) COUL VERT ;

*---------------------IRON ------------------------------------------
 FER = (D OZ2 R0Z2 D R4Z2 D 6 R4Z0 D R5Z0 D R5Z3 D R0Z3 D OZ3 D OZ2 )
 SURF PLANE COUL ROUG ;
*---------------------EXTERNAL AIR BOX ------------------------------
 FLAN5 = ( D   R5Z0 R5Z3  D R6Z4 D R6Z0 D R5Z0 ) SURF PLANE ;
*
 FLAN6 =( D R0Z3  R5Z3 D R6Z4  D R0Z4 D R0Z3 ) SURF PLANE
                        COUL VERT ;
 TUB6 = (D OZ3 R0Z3 D R0Z4 D OZ4 D OZ3) SURF PLANE COUL vert ;
 AIREXT = (FLAN6 ET TUB6 ET FLAN5 ) coul blan ;
 AIRIN = ((FLAN1 ET FLAN2 ET FLAN3  ET FLAN4 ) coul vert) et BOBI  ;
 TOUT = AIRIN ET AIREXT ET FER ; ELIM .2 TOUT ;
*
*---------------  BOUNDARIES -----------------------------------------
*
 ENPP = CONTOUR TOUT  ;
 CEXTR=ENPP POINTS DROITE   (130.  0.) (130. 100.)  .1 ;
 CEXTH=ENPP POINTS DROITE   ( 0. 130.) (10.  130.)  .1 ;
 AXE=ENPP POINTS DROITE   ( 0.  0. ) ( 0. 100. )  .1  ;
 AX1 = (AIRIN ET FER) POINT DROITE ( 0.  0. ) ( 0. 100. )  .1  ;
 AX2 = DIFF ( CHAN POI1 AXE) (CHAN POI1 AX1) ;
*
*----------------------------------------------------------------------
*        FORMER DESCRIPTION IN  MILLIMETRES
*---------------  SHIFT FOR METERS ------------------------------------
*
 deplacer tout homo  .001  (0. 0.) ;
*
 SI (NEG GRAPH  N ) ;
 TITRE  ' MESH 2D ' ;
 TRAC  tout  ;
 FINSI ;
*----------------------------------------------------------------------
 MU0= 4. * PI * 1.E-7 ;
*
*------------------CURENT DESCRIPTION ---------------------------------
 TABCOUR= TABLE ;
 DESCOUR TABCOUR 1 BOBI 'AMP' 800.E6 ;
 TABB = TABLE ;
*----------------- AXISYMMETRIC  PROBLEM ------------------------------
 TABB.'AXI'= VRAI  ;
  NF = FER NBEL ;
   FER1 = FER ELEM ( LECT 1 PAS 1 30 )  ;
   FER2 = FER ELEM ( LECT 31 PAS 1 NF ) ;
*------------------MATERIALS  -----------------------------------------
 KEVOL = H_B MU0    ;
 TABMAT = TABLE ;
    OBFER1 = FER1 MODE THERMIQUE ISOTROPE ;
    OBFER2 = FER2 MODE THERMIQUE ISOTROPE ;
   STN = TABLE ; STN.EV1 = KEVOL   ;
   TABMAT.OBFER1 = STN ;
   STN = TABLE ; STN.EV1 = KEVOL   ;
   TABMAT.OBFER2 = STN ;
 TABB.'TABNUSEC' = TABMAT ;
 TABB.'MUAIR' =MU0 ;
*------- LINEAR MATERIAL CAN BE TRAITED AS A SUPER ELEMENT OR NOT
*
   isuper= 1 ;
 'SI' ('EGA' isuper 1 ) ;
 TABB.'AIRSUP' = AIREXT ;
 TABB.'MAITRES' = ( FER CONTOUR) 'ELEM' 'COMP' R5Z0 OZ3 ;
 TABB.'ENCS' = CEXTR ET CEXTH ET AX2 ;
 TABB.'BLOQUE' = BLOQUER 'T' AX1 ;
 TABB.'AIR' = AIRIN ;
 'SINON' ;
 TABB.'AIR' = (AIRIN ET AIREXT) ;
 TABB.'BLOQUE' = BLOQUER 'T' (CEXTR ET CEXTH  ET AXE ) ;
 'FINSI' ;
 TABB.'COUR'= TABCOUR ;
*
* ------POTENTIAL  COMPUTATION OR FIRST STEP IF NON LINEAR PROBLEM ---
*------
 POT_VECT  TABB 'SOLIN' ;
*-----
*------------------------------------------------------------------
 SOL1 = (TABB.'POTENTIEL' ) ENLEVER LX ;
 RAY = FLAN1 COTE 1 ;
 RAY2 = D 10 (0. .0001) ( .020  .0001) ;
* -------------SOME  POST TRAITMENT   B COMPUTATION ----------------
 BB = INDUCTIO  AIRIN  SOL1  VRAI ;
 PREF = AIRIN POINT PROCHE ( 0. 0. ) ;
 BY0 = EXTR BB 'BY'  PREF  ;
 BY10 = EXTR BB 'BY' ( AIRIN POINT PROCHE ( .010 0.)) ;
 TITRE ' COMPOSANTE BY AVANT ET APRES LISSAGE ' ;
 EVB1  = EVOL ROUG  CHPO  BB    'BY'  RAY  ;
*----------    polynomial  smothing  if  wanted  --------------------
 CHLIS = PROI  POLY  TOUT RAY2   SOL1 1 AXIS ;
 BBY = (EXCO CHLIS 'BY' ) NOMC  'BY' ;
 BY01 = EXTR BBY 'BY' (RAY2 POINT INITIAL) ;
 BAT1  = 800.e6  * 5.e-3 * 5.e-3  * MU0 /10.e-3 ;

 option echo 0 ;
 MESS '**************************************************************';
 MESS '*  CIRCULAR COIL  internal radius 20 mm '                      ;
 MESS '*        total    cross section  5*5 mm '                      ;
 MESS '*         SYMMETRY BY HORIZONTAL PLANE  '                      ;
 MESS '*  AMPERE  mufer >> muo  all AMPERE*TURNS in the GAP of 10 mm ';
 MESS '*                                                             ';
 MESS '*       J * EP * HAUT = B/ MU0 * e                            ';
 MESS '*   WAITED :   By = '  bat1 '  TESLAS                           ';
 MESS ' *************************************************************';
 MESS '  NUMERICAL DERIVATION    BY AU CENTRE ' BY0   ;
 MESS '  SMOTHED SOLUTION        BY AU CENTRE ' BY01   ;
 MESS ' *****************************************';
*******************************************************************
   RAP = ABS ((BY01 - bat1 ) / bat1  );
       SI ( RAP  > .01 ) ; ERREUR 5 ; FINSI ;
*******************************************************************
 SI (NEG  GRAPH  N ) ;
 TITRE ' POTENTIAL   BEFORE AND AFTER   SMOTHING ' ;
 EVPO1 = EVOL ROUG  CHPO  SOL1   'T'   RAY  ;
 EVPO2 = EVOL VERT  CHPO  CHLIS    'A' RAY2  ;
 dess (evpo1 et evpo2) xbor 0. (R1 * 0.001 * .75);
 TITRE ' BY COMPONENT  BEFORE AND AFTER SMOTHING  ' ;
 EVB1  = EVOL ROUG  CHPO  BB    'BY'   RAY  ;
 EVB2  = EVOL VERT  CHPO  CHLIS  'BY'  RAY2  ;

 dess (evb1 et evb2) xbor 0. (R1 * 0.001 * .75) ;
  FINSI ;
*
**-----------------     NON LINEAR  COMPUTATION  -------------------
*       filling
 TABB.SOUSTYPE='THERMIQUE' ;
 TABB.CRITERE =1.E-4 ;
 TABB.NITER   =1;
 TABB.'OME' = .1 ;
*-----------  test shortened to 2 iterations ----------------------
 TABB.ITERMAX=2;
 TABB.NIVEAU =1;
*
*-------------------------------
  MAG_NLIN  TABB  ;
*-------------------------------
*
 SOL2 = enlever (TABB.'POTENTIEL' ) LX ;
 BB = INDUCTIO  AIRIN SOL2  VRAI ;
 BY02 = EXTR BB 'BY'  PREF ;
 CHLIS = PROI  POLY  TOUT RAY2   SOL2 1 AXIS ;
 BBY = (EXCO CHLIS 'BY' ) NOMC  'BY' ;
 BY03 = EXTR BBY 'BY' (RAY2 POINT INITIAL) ;
 BAT = 2.4106 ;
 MESS ' *****************************************';
 MESS '  EXPECTED SOLUTIONS   AT CENTER   BY  ' BAT      ;
 MESS '  NUMERICAL DERIVATION             BY  ' BY02     ;
 MESS '  SMOTHED SOLUTION                 BY  ' BY03     ;
 MESS ' *****************************************';
    itest = 1 ;
*--------------------GOOD WORKING MESSAGE -----------------------
    SI (EGA ITEST 1 ) ;
*------- test   shortened ---------------------------------------
   RAP = ABS ((BY03 - BAT ) / BAT);
       SI ( RAP  > .01 ) ; ERREUR 5 ; FINSI ;
*----------------------------------------------------------------
   SINON ;
*------  COMPUTATION TILL CONVERGENCE OR UP TO 100 MORE ITERATION
 TABB.ITERMAX=100;
  MAG_NLIN  TABB  ;
*----------------------------------------------------------------
 SOL2 = enlever (TABB.'POTENTIEL' ) LX ;
 BB = INDUCTIO  AIRIN SOL2  VRAI ;
 BY02 = EXTR BB 'BY'  PREF ;
 CHLIS = PROI  POLY  TOUT RAY2   SOL2 1 AXIS ;
 BBY = (EXCO CHLIS 'BY' ) NOMC  'BY' ;
 BY03 = EXTR BBY 'BY' (RAY2 POINT INITIAL) ;
 BAT = 2.3468 ;
 MESS ' *****************************************';
 MESS '  EXPECTED SOLUTION ON AXIS      BY   ' BAT      ;
 MESS '  NUMERICAL DERIVATION  BY AU CENTRE ' BY02     ;
 MESS '  SMOTHED SOLUTION      BY AU CENTRE ' BY03     ;
 MESS ' *****************************************';
*
*******************************************************************
   RAP = ABS ((BY03 - BAT ) / BAT );
       SI ( RAP  > .01 ) ; ERREUR 5 ; FINSI ;
*******************************************************************

  SI (NEG GRAPH N )  ;
 TITRE ' POTENTIAL   BEFORE AND AFTER   SMOTHING ' ;
 EVPO1 = EVOL ROUG  CHPO  SOL2   'T'   RAY  ;
 EVPO2 = EVOL VERT  CHPO  CHLIS    'A' RAY2  ;
 dess (evpo1 et evpo2) xbor 0. (R1 * 0.001 * .75);
 TITRE ' BY COMPONENT  BEFORE AND AFTER SMOTHING ' ;
 EVB1  = EVOL ROUG  CHPO  BB    'BY'  RAY  ;
 EVB2  = EVOL VERT  CHPO  CHLIS  'BY'  RAY2  ;
 dess (evb1 et evb2) xbor 0. (R1 * 0.001 * .75);
 FINSI ;
   FINSI ;
 FIN ;
              ;







