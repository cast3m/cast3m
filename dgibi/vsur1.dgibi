* fichier :  vsur1.dgibi
************************************************************************
* Section : Mecanique Elastique
************************************************************************
*           Test Vsur1.dgibi: Jeux de données         *
*           ---------------------------------         *
*                                                     *
*******************************************************
*   vsur1.dgibi : Test de l'opérateur VSUR en coq2    *
*******************************************************
opti echo 0 dime 2 elem seg2 mode axis;
*
*  Définition de la géométrie
*
p0 = 0. 0.;
p1 = 10. 10.;
pc = 0. 10.;
obj1 = cerc 10 p0 pc p1;
cl1 = bloq 'DEPL' 'ROTA' p1;
cl2 = bloq 'UZ' p0;
*
*  Création du modèle
*
mod =  modeli obj1  mecanique elastique isotrope coq2;
*
*  Comparaison des champs obtenus par VSUR
*
* che1 : champ de vecteurs surfaciques
che1 = VSUR mod;
* chn1 : champ de normaes
chn1 = VSUR mod 'NORM';
* chj1 : champ des jacobiens
chj1 = JACO mod;
cnr1 = exco 'VR' che1 'SCAL';
cnz1 = exco 'VZ' che1 'SCAL';
* champ des normes
cnn1 = (cnr1*cnr1) + (cnz1*cnz1);
cjn1 = chj1*chj1;
cze1 = cnn1 - cjn1;
*list cze1;
cpz1 = chan 'CHPO' mod cze1;
res1 = maxi cpz1 'ABS';
*list res1;
cnr1 = exco 'VR' chn1 'SCAL';
cnz1 = exco 'VZ' chn1 'SCAL';
cnn1 = (cnr1*cnr1) + (cnz1*cnz1);
*list cnn1;
cpn1 = chan 'CHPO' mod cnn1;
res2 = mini cpn1 'ABS';
res3 = maxi cpn1 'ABS';
*list res2;
*list res3;
EC1 = RES3 - RES2;
SI ((res1 < 1.e-6) ET (ABS(res2 - 1.) < 1.E-6) ET (EC1 < 1.E-6)) ;
  MESS 'OPERATEUR <VSUR> ERR  0';
  ERRE  0;
SINON;
  MESS 'OPERATEUR <VSUR> ERR  5';
  ERRE  5;
FINSI;
fin;









