* fichier :  sort_MAILLAGE.dgibi
************************************************************************
* Section : Entree-Sortie Entree-Sortie
************************************************************************

***************************************************************
*           Test sort_MAILLAGE.dgibi: Jeux de données         *
*           -----------------------------------------         *
***************************************************************

*___________________________________________________________________
*   TEST sort_MAILLAGE.dgibi                                       *
*                                                                  *
* Sortie d'un MAILLAGE avec SORT
* Relecture avec LIRE (Option non testee a-priori)                 *

* Cas-Test de Verification & Validation
*__________________________________________________________________*
*
OPTI DIME 3;
OPTI ELEM CU20;
GRAPH = MOT 'N';
*
*==================================================================*
*             geometrie : maillage                                 *
*==================================================================*
*

K = 2;

PB = 0. 2.75 0.;
PB1 = 0. 2.75 (((3.25 ** 2) - (2.75 ** 2)) ** 0.5);
PC = 3.25 0. 0.;
*
C1 = PC CERC (6 * K) (0. 0. 0.) PB1;
C2 = C1 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
PA = 0. 1. 0.;
PA1 = 0. 1. (((2. ** 2) - (1. ** 2)) ** 0.5);
PD = 2. 0. 0.;
*
C3 = Pd CERC (6 * K) (0. 0. 0.) PA1;
C4 = C3 PROJ CYLI (0. 0. 1) PLAN (0 0 0) (1 0 0) (0 1 0);
*
D1 = PA DROI (2 * K) (0. 1.583 0.) DROI (2 * K) PB;
D3 = PC DROI (2 * K) (2.417 0. 0.) DROI (2 * K) PD;
*
ELIM (D1 ET C2 ET D3 ET C4) 0.0001;
SUR1 = DALL D1 C2 D3 C4 PLAN;
*
VOL1 = SUR1 VOLU (2 * K) TRAN (0. 0. -0.6);
*
* Surfaces et arc d'ellipse pour conditions aux limites
*
FACE1 = D1 TRAN (2 * K) (0. 0. -0.6);
FACE2 = C2 TRAN (2 * K) (0. 0. -0.6);
FACE3 = D3 TRAN (2 * K) (0. 0. -0.6);
C2CL  = C2 PLUS (0. 0. -0.3);
*
ELIM (VOL1 ET FACE1 ET FACE2 ET FACE3 ET C2CL) 0.0001;
*
SI (NEG GRAPH 'N');
  TITR 'ELAS9 : MAILLAGE';
  TRAC (-1000 -1000 1000) FACE QUAL (COUL VOL1 BLAN);
FINSI;

*
*==================================================================*
*             sortie : maillage                                    *
*==================================================================*
*
OPTI SORT 'Sort_MAILLAGE.mesh';
SORT VOL1;
OPTI SORT 'BIDON';
VOL2  = VOL1;
OUBL VOL1;
NBNO2 = NBNO VOL2;
NBEL2 = NBEL VOL2;
*
*==================================================================*
*             Relecture : maillage                                 *
*==================================================================*
*
OPTI LECT 'Sort_MAILLAGE.mesh';
LIRE;
NBNO1 = NBNO VOL1;
NBEL1 = NBEL VOL1;

*
*==================================================================*
*             Verification : Comparaison des MAILLAGES             *
*==================================================================*
*
ELIM VOL1 VOL2 1.E-5;
UNIQ (VOL1 ET VOL2);

INT1 = INTE VOL1 VOL2;
NBEI = NBEL INT1;

OPTI ECHO 0;
MESS 'NOEUDS  ' NBNO2 NBNO1;
MESS 'ELEMENTS' NBEL2 NBEL1;
MESS 'INTERSEC' NBEI       ;

SI ((NEG NBNO2 NBNO1) OU (NEG NBEL2 NBEL1) OU (NEG NBEL1 NBEI));
  ERRE 5;
FINS;
OPTI ECHO 1;
* OPTI DONN 5;

FIN;





