* fichier :  fsi3.dgibi
************************************************************************
* Section : Fluides Vibration
* Section : Mecanique Interaction Fluide Structure
************************************************************************
* CAS TEST DU 91/10/04     PROVENANCE : PETI

*******************************************
*    Test fsi3.dgibi: jeux de données     *
*    ---------------------------------    *
*******************************************

* SI GRAPH = N PAS DE GRAPHIQUE AFFICHE
* SINON SI GRAPH DIFFERENT DE N TOUS
* LES GRAPHIQUES SONT AFFICHES

GRAPH = 'N' ;

SAUT PAGE;
SI (NEG GRAPH 'N') ;
  OPTI ECHO 1   ;
  OPTI TRAC X   ;
SINO ;
  OPTI ECHO 0   ;
FINSI ;

SAUT PAGE;
*******************************************
*                                         *
*               TEST FSI3                 *
*                                         *
*    CYLINDRICAL TANK CONTAINING WATER    *
*                                         *
*  Calculation of the frequencies of      *
*  cylindrical tank containing water, for *
*  m = 30.  The radius is 1.43m and       *
*  height is 1.039m.                      *
*                                         *
*                                         *
*                            P2           *
*                           +             *
*                           |             *
*                           |             *
*                fluid      |             *
*                           |             *
*                           |             *
*                           |             *
*             --------------+             *
*             P4              P3          *
*                                         *
* The boundary condition :    p3 is fixed *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dz  | h = 0. , 1.43                *
*                                         *
*      dp  |                              *
*     ---- |                 = 0.         *
*      dr  | r = R                        *
*******************************************

TEMPS;

OPTI DIME 2;
OPTI MODE FOUR 10 ;
OPTI ELEM QUA4;
OPTI EPSI LINEAIRE;

*=========================================*
*               geometry                  *
*=========================================*
* Dimensions en metres

* Points

N1 = 20       ; N2 = 30          ;
P2 = 1.43 1.039  ;
P3 = 1.43 0.0    ;
P4= 0. 0.        ;
P5 = 1.43 1.039  ;
P6 = 1.43 0.0    ;
P7= 0. 0.        ;
P8 = 0. 1.039    ;

S2 = P3 D N1 P2 ; S3 =P3 D N2 P4 ;
S4 = P6 D N1 P5 ; S5 =P7 D N2 P6 ;
S6 = P7 D N1 P8 ; S7 =P8 D N2 P5 ;

STEEL = S2 ET S3                 ;
WATER = DALL S4 S5 S6 S7  QUELC   ;
RAC1  = RACC 0.002 WATER STEEL   ;

* OPTIO FOR TRACE

SI (NEG GRAPH 'N');
  TITR ' FSI3 : MAILLAGE';
 TRAC QUAL (WATER ET (0 0));
FINSI;

*=========================================*
*      MODE - materiau - rigidite - masse *
*=========================================*
*
MODCOQ = MODE STEEL MECANIQUE COQ2  ;
MODLIQ = MODE WATER LIQUIDE   LQU4   ;
MODRAC = MODE RAC1  MECANIQUE LIQUIDE RACO;

MATCOQ = MATE MODCOQ RHO 7.8E3 YOUN 2.E11
                     NU 0.3 ;
MATLIQ = MATE (MODLIQ ET MODRAC)
         RHO 1.E3 RORF 1.E3 CSON 1435.
         CREF 1435. LCAR 1. G 0.;

CARCOQ = CARA MODCOQ EPAI 0.002;
CARRAC = CARA MODRAC LIQU WATER;
MATCOQ = MATCOQ ET CARCOQ;
MATLIQ = MATLIQ ET CARRAC;

RIG1 = RIGI  (MODCOQ ET MODLIQ ET MODRAC)
             (MATCOQ ET MATLIQ)  ;
MAS1 = MASS  (MODCOQ ET MODLIQ ET MODRAC)
             (MATCOQ ET MATLIQ)   ;

*=========================================*
*                boundary conditions      *
*=========================================*

RES1 = BLOQ DEPL ROTA P3 ;
*
*=========================================*
* theritical value  of frequency

FRE1  = 106.64 ;
LIST1 = PROG FRE1 ;

*=========================================*
*     calculation of the frequencies      *
*                   and                   *
*       extraction of some results        *
*=========================================*

* Use of the operator VIBR. (option PROC)

RESUL = VIBR PROC LIST1  (RES1 ET RIG1 )  MAS1 ;

*=========================================*
*             results                     *
*=========================================*

MESS ' RESULTATS ';
MESS ' --------- ';
SAUT 1 LIGN;

  FRE2  = RESUL . MODES . 1 . FREQUENCE;
  ERG   = 100 *
        (ABS ((FRE1   - FRE2  ) / FRE1  ));
  MESS ' MODE '  1  ;
  MESS ' ----------';
  MESS 'Frequence theorique :' FRE1   'Hz';
  MESS 'Frequence calculee  :' FRE2   'Hz';
  MESS '    Soit un ecart de : ' ERG   '%';
  SAUT 1 LIGN;

* mode shape extraction and optional trace

  SI (NEG GRAPH 'N');
     MOD  = RESUL . MODES . 1 . DEFORMEE_MODALE;
     DEF0 = DEFO STEEL  MOD   0.  ;
     DEF  = DEFO STEEL  MOD   BLEU;
     TRAC (DEF   ET DEF0);
  FINSI;

*=========================================*
*           code validation               *
*=========================================*

ERGMAX = ERG      ;

SI (ERGMAX <EG 5.);
   ERRE 0;
SINON;
   ERRE 5;
FINSI;

SAUT 1 LIGN;
TEMPS;
SAUT 1 LIGN;

FIN;






