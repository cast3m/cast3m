* fichier :  enc2d.dgibi
*
*  Enceinte 2D Axisymetrique
*  Relachement d'un melange d'hydrogene et d'helium
*
'OPTI' 'DIME' 2 'ELEM' 'CU20' ;
 'OPTI' 'MODE' 'AXIS' ;
 'OPTI' 'TRAC' 'PSC';

 COMPLET= VRAI                                          ;
 COMPLET= FAUX                                          ;
 GRAPH  = VRAI                                          ;
 GRAPH  = FAUX                                          ;

 'SI' COMPLET ;
  nbit=100    ;
  nbit=20     ;
  DT0 = 1.    ;
  n1=80 ;
  n2=160;
  n3=320;
 'SINON'      ;
  nbit=20     ;
  DT0 = 1.    ;
  n1=5 ;
  n2=10;
  n3=20;
 'FINSI'      ;

p1=0 0 ;
p11=0.4 0 ;
p2=0.1 0. ;
p3=1 0 ;
p4=1 2;
p5=0 2 ;


breche = p1  'DROI' n1 p2 ;
bas= p2 'DROI' n2 p3 ;
paroid=p3 'DROI' n3 p4 ;
plaf=p4 'DROI' (n1 + n2) p5 ;
axe = p5 'DROI' n3 p1 ;
vtp = bas 'ET' paroid 'ET' plaf ;
mt= 'DALLER' (breche 'ET' bas) paroid plaf axe ;

Si GRAPH ;
trace mt Titr (chai ' NBEL= ' (nbel mt));
Finsi ;

rxt = 'TABLE' ;
rxt.'vtf'=mt ;
rxt.'axe'   = axe ;
rxt.'breche'=breche ;
rxt.'diru1'=0. 1. ;
rxt.'epsi'= 1.e-4 ;
rxt.'pi'  = 0.5 0.5 ;
rxt.'DT0'  = DT0     ;
rxt.'DETMAT'= VRAI ;

rxt.'TKPR'  = VRAI   ;

rxt.'RENU'  = 'RIEN' ;
rxt.'DISCR'= 'LINE';
rxt.'KPRE' = 'MSOMMET';
rxt.'MODTURB'  = 'LMEL'  ;
rxt.'LMEL'  = 0.05  ;
rxt.'GRAPH'  = GRAPH ;
rxt.'HE'     = VRAI ;
rxt.'H2'     = VRAI ;
rxt.'CO'     = VRAI ;

 rxt.'TF0'    =       20.0                         ;
 rxt.'PT0'    =        1.0e5                       ;
 rxt.'Yhe0'   =        0.0                         ;
 rxt.'Yh20'   =        0.0                         ;
 rxt.'Yco0'   =        0.0                         ;

 rxt.'scenario'          = table                              ;
 rxt.'scenario'.'t'      = prog   0.0 100.0                   ;
 rxt.'scenario'.'qair'   = prog   0.0   0.0                   ;
 rxt.'scenario'.'qhe'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'qh2'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'qco'    = prog   0.0011  0.0011              ;
 rxt.'scenario'.'tinj'   = prog  20.0  20.0                   ;

EXECRXT  nbit rxt ;

un=rxt.'TIC'.'UN';
mess ' Max U ' (maxi un) ' Min U ' (mini un) ;
test = abs ((maxi un) - 1.9    );
mess ' Test =' test;
 Si (test '>' 0.1  ) ; erreur 5 ; Finsi ;

tf =rxt.'TIC'.'TF' ;
mess ' Max Tf' (maxi tf) ' Min Tf' (mini tf) ;
test = abs ((maxi tf) - 27.    );
mess ' Test =' test;
 Si (test '>' 0.5  ) ; erreur 5 ; Finsi ;



$vtf=rxt.'GEO'.'$vtf' ;
vtf=doma $vtf maillage;
rho=rxt.'TIC'.'RHO' ;
rhe=rxt.'TIC'.'RHE';
rh2=rxt.'TIC'.'RH2';
rco=rxt.'TIC'.'RCO';
rair=rxt.'TIC'.'RAIR';
un =rxt.'TIC'.'UN';

DISCR=rxt.'DISCR' ;
axe = chan DISCR axe ;
ixe=inve axe ;
evauz = evol chpo (exco un 'UY') ixe ;
evarh = evol chpo rho ixe ;
evatf = evol chpo tf  ixe ;
evahe = evol chpo rhe ixe ;
evah2 = evol chpo rh2 ixe ;


Si GRAPH ;

 dess evauz 'TITRE' 'Vitesse sur l axe';
 dess evarh 'TITRE' 'Densite melange sur l axe';
 dess evatf 'TITRE' 'Temperature sur l axe';
 dess evahe 'TITRE' 'Densite Helium sur l axe';
 dess evah2 'TITRE' 'Densite Hydrogene l axe';

ung= vect un 0.1 ux uy jaune;
trace ung vtf;
opti isov suli ;
trace tf  vtf 'TITRE' ' Tf ' ;
trace rho vtf 'TITRE' ' Rho' ;
trace rhe vtf 'TITRE' ' Rhe' ;
trace rh2 vtf 'TITRE' ' Rh2' ;
trace rco vtf 'TITRE' ' Rco' ;
trace rair vtf 'TITRE' ' Rair' ;
Finsi ;

FIN ;




